@更新チェック
IF !SP
	IF LASTUPDATECHECK == ""
		$CHECK
		UPDATECHECK
		SELECTCASE RESULT
			;最新版、失敗もしくはUPDATECHECK切ってるなら何もしない
			CASE 0, 3, 4
				LASTUPDATECHECK = 
			;アップデートある場合は日付を記録する
			CASE 1, 2
				LASTUPDATECHECK = %GETTIMES()%
		ENDSELECT
	ELSE
		;前回の確認から3日以上経過してたらまた催促する
		{
		IF GETDAYS(TOINT(SUBSTRING(GETTIMES(), 0, 4)), TOINT(SUBSTRING(GETTIMES(), 5, 2)), TOINT(SUBSTRING(GETTIMES(), 8, 2)))-
		GETDAYS(TOINT(SUBSTRING(LASTUPDATECHECK, 0, 4)), TOINT(SUBSTRING(LASTUPDATECHECK, 5, 2)), TOINT(SUBSTRING(LASTUPDATECHECK, 8, 2)))
		>= 3
		}
			GOTO CHECK
		ENDIF
	ENDIF
ENDIF

@CSVチェック
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC NAMEVAR, 10000
SIF EXISTCSV(10000)
	THROW キャラ番号10000は当バリアントでは使用できません。別の番号に置き換えてください

;名前被りを確認する
FOR LCOUNT, 0, 10000
	SIF EXISTCSV(LCOUNT)
		NAMEVAR:LCOUNT = %CSVNAME(LCOUNT)%
NEXT
FOR LCOUNT, 0, 10000
	SIF NAMEVAR:LCOUNT == ""
		CONTINUE
	FINDELEMENT NAMEVAR, NAMEVAR:LCOUNT, LCOUNT+1, 10000, 1
	SIF RESULT >= 0
		THROW No.{LCOUNT}の「%CSVNAME(LCOUNT)%」はNo.{RESULT}の「%CSVNAME(RESULT)%」と競合しています
NEXT
[SKIPSTART]
FOR LCOUNT, 0, 10000
	;レーダー用の値を計算しておく
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	CALLF 開発難易度PT計算(LCOUNT)
	CALLF 性経験PT計算(LCOUNT)
	CALL 売却額PT計算(LCOUNT)
	CALLF 屈服難易度PT計算(LCOUNT)
NEXT
[SKIPEND]

ENDING = 11
CSVCOUNT = ENUMFILES("CSV/Chara", "*")

@グローバルバージョンチェック
;2023/10/10(Ver.2.21)で導入
IF グローバルバージョン < 20231010
	;実績のリファクタリングに伴いリセット
	VARSET 実績
	グローバルバージョン = 20231010
ENDIF



@起動時オプションチェック
IF !SP
	SELECTCASE サウンド
		CASE 0
			SETBGMVOLUME 0
			SETSOUNDVOLUME 0
		CASE 1
			SETBGMVOLUME BGM音量
			SETSOUNDVOLUME SOUND音量
	ENDSELECT
ENDIF

;10秒(デフォルト)
IF ツールチップ表示時間
	TOOLTIP_SETDURATION ツールチップ表示時間
ELSE
	TOOLTIP_SETDURATION 10000
ENDIF

;ツールチップディレイ(デフォルトは無し)
SIF ツールチップディレイ
	TOOLTIP_SETDELAY ツールチップディレイ
