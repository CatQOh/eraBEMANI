@ゲーム開始
#DIMS DYNAMIC HTML用, 4
#DIM DYNAMIC LCOUNT

TARGET = -1
ASSI = -1

VARLINE = 0

LOADGLOBAL

;周回数を1に
FLAG:周回数 = 1

;コンフィグ初期設定
OPTION:地の文表示 = 1
OPTION:口上表示 = 1

;データIDを設定
データID = GETSECOND()

;経営モードフラグの初期化
CALL CLEAR_STRATEGY_FLAG

;ORIGINはPFREEモード
IF ORIGIN
	FLAG:ゲームモード = 10
	GOTO ORIGIN
ENDIF

;チュートリアルならPFREE固定
IF TUTORIAL == "FIRST"
	FLAG:ゲームモード = 9
	CALL TUTORIAL
	GOTO ORIGIN
ENDIF

;モード選択
DRAWLINE
IF SP
	PRINTL ★★モードを選択してください★★
	CALL PASELIL, @"[0] - STANDARD (100日期限 目標金額1,000,000＄ 経営使用不可 クリア後は可能)"
	CALL PASELIL, @"[1] - STRATEGY (300日期限 目標金額100,000,000＄ 経営使用可能)"
	PRINTL [7] - 新婚モード (期限なし 好きなキャラと好きなだけイチャイチャする)
	PRINTL [8] - 段位認定 (期限あり 特定の4人を攻略)
	PRINTL [9] - PREMIUM FREE (期限なし エンドレス)
	GOTO INPUTWAIT
ENDIF

PRINT 　
FONTBOLD
PRINT M
FONTREGULAR
PRINTL ODE SELECT　　　　　　　　　　　　　　　　　＼ＳＰ／
IF LANGUAGE == "ENG"
	PRINTL 　　　　　　　　　　　　　　　　　　　　　　 　　＼／DP＼
ELSE
	PRINTL 　クリックで選択 価格はすべて税込みとなります　　＼／DP＼
ENDIF

FOR LCOUNT, 0, 7
	GCREATEFROMFILE LCOUNT, "MODE.png"
	GCREATEFROMFILE LCOUNT+10, "MODEB.png"
	GSETBRUSH LCOUNT, 0xFF000000
	GSETBRUSH LCOUNT+10, 0xFF000000
NEXT
FOR LCOUNT, 0, 17
	SELECTCASE LCOUNT%10
		CASE 0
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "WE", 75, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("hotpink"))
			GDRAWTEXT LCOUNT, "DD", 125, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "ING", 170, 25
			GDRAWTEXT LCOUNT, "新婚モード", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "期限無し", 75, 105
			GDRAWTEXT LCOUNT, "所持金3億$", 75, 145
		CASE 1
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "CLA", 75, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("darkseagreen"))
			GDRAWTEXT LCOUNT, "SS", 135, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "段位認定", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "期限あり", 75, 105
			GDRAWTEXT LCOUNT, "特定の4人を攻略", 75, 145
		CASE 2
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "PRE", 75, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("green"))
			GDRAWTEXT LCOUNT, "M", 135, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "IU", 160, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("green"))
			GDRAWTEXT LCOUNT, "M", 190, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "FREE", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "期限無し", 75, 145
		CASE 3
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "School Mode", 75, 25
			GDRAWTEXT LCOUNT, "学園モード", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "目標・期限:無し", 75, 145
		CASE 4
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "STAN", 75, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("darkorange"))
			GDRAWTEXT LCOUNT, "D", 155, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "AR", 180, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("darkorange"))
			GDRAWTEXT LCOUNT, "D", 225, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "通常モード", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "目標:1,000,000$", 75, 105
			GDRAWTEXT LCOUNT, "期限:100日", 75, 145
		CASE 5
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "S", 75, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("royalblue"))
			GDRAWTEXT LCOUNT, "T", 95, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "RA", 115, 25
			GSETBRUSH LCOUNT, 0xFF000000+(COLOR_FROMNAME("royalblue"))
			GDRAWTEXT LCOUNT, "T", 160, 25
			GSETBRUSH LCOUNT, 0xFF000000
			GDRAWTEXT LCOUNT, "EGY", 180, 25
			GDRAWTEXT LCOUNT, "経営モード", 75, 65
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "目標:100,000,000$", 75, 105
			GDRAWTEXT LCOUNT, "期限:300日", 75, 145
		CASE 6
			GSETFONT LCOUNT, "Arial", 25, 1
			GDRAWTEXT LCOUNT, "Origin Mode", 75, 25
			GSETFONT LCOUNT, "Arial", 25, 0
			GDRAWTEXT LCOUNT, "目標・期限:無し", 75, 105
			GDRAWTEXT LCOUNT, "全キャラ解放済み", 75, 145
	ENDSELECT
NEXT


SPRITECREATE "WEDDING", 0
SPRITECREATE "WEDDINGB", 10

SPRITECREATE "CLASS", 1
SPRITECREATE "CLASSB", 11

SPRITECREATE "PFREE", 2
SPRITECREATE "PFREEB", 12

SPRITECREATE "SCHOOL", 3
SPRITECREATE "SCHOOLB", 13

SPRITECREATE "STANDARD", 4
SPRITECREATE "STANDARDB", 14

SPRITECREATE "STRATEGY", 5
SPRITECREATE "STRATEGYB", 15

SPRITECREATE "ORIGIN", 6
SPRITECREATE "ORIGINB", 16

HTML用 =<nobr><button value='7' pos='100'> <img src='WEDDING' srcb='WEDDINGB' height='700'></button>
HTML用:1 =<button value='8' pos='1700'> <img src='CLASS' srcb='CLASSB' height='700'></button>
HTML用:2 =<button value='9' pos='3300'> <img src='PFREE' srcb='PFREEB' height='700'></button>
HTML用:3 =<button value='2' pos='4900'> <img src='SCHOOL' srcb='SCHOOLB' height='700'></button>

HTML_PRINT STRJOIN(HTML用, "")

FOR LCOUNT, 0, 6
	PRINTL 
NEXT

VARSET HTML用

HTML用 =<nobr><button value='0' pos='900'> <img src='STANDARD' srcb='STANDARDB' height='700'></button>
HTML用:1 =<button value='1' pos='2500'> <img src='STRATEGY' srcb='STRATEGYB' height='700'></button>
HTML用:2 =<button value='10' pos='4100'> <img src='ORIGIN' srcb='ORIGINB' height='700'></button>

HTML_PRINT STRJOIN(HTML用, "")

FOR LCOUNT, 0, 6
	PRINTL 
NEXT

PRINTL CREDIT:1　　　　　　　　　　　　　　　　　┏━━━━━━┓　　　　　　　　　　　　　　　　　　CREDIT:0
PRINTL PASELI:NO ACCOUNT　　　　　　　　　　　　 ┃EFFECTOR OFF┃　　　　　　　　　　　　　　　　　　PASELI:NO ACCOUNT

;各ゲームモードごとの初期設定
$INPUTWAIT
BINPUT
FLAG:ゲームモード = RESULT

IF ゲームモード() == "学園モード"
	LOOP_BREAK = 1
	RETURN
ENDIF

$ORIGIN
;「あなた」の追加と暦の算出、所持金初期設定など
CALL CHARA_ADD, 0
MASTER = 0
CALL SETUP_GAMESTART

;10はORIGIN
IF FLAG:ゲームモード == 10
	ORIGIN = 1
	FLAG:ゲームモード = 9
ENDIF

SELECTCASE ゲームモード()
	CASE "通常モード", "経営モード"
		CALL PLAYER_CUSTOM, 0
		CALL SELECT_DIFFICULT
	CASE "新婚モード"
		CALL START_CHARA_SELECT
	CASE "段位認定"
		CALL 段位認定緑
		SELECTCASE 受験段位名
			CASE "EDIT"
				CALL SELECT_DIFFICULT
			CASE "裏七級"
				FLAG:難易度 = 3
			CASEELSE
				FLAG:難易度 = 1
		ENDSELECT
	CASE "フリーモード"
		CALL START_CHARA_SELECT
		CALL SELECT_DIFFICULT
		;チュートリアルならいろいろ飛ばす
		IF TUTORIAL == "キャラクリ終了"
			OPTION:パセリ表示 = 1
			GOTO TUTORIAL_SKIP
		ENDIF
		;originなら音ゲーマー素質は抜く
		SIF ORIGIN && NOF(MASTER) == 0
			TALENT:MASTER:音ゲーマー = 0
ENDSELECT

;オープニング表示
SELECTCASE ゲームモード()
	CASE "通常モード"
		IF DOPTION:有効 && DOPTION:チュートリアル && LANGUAGE == "JP"
			DRAWLINE
			PRINTL オープニングストーリーを表示しますか？
			PRINTL [0] - 表示する
			PRINTL [1] - 表示しない
			BINPUT
			IF RESULT == 0
				DRAWLINE
				CALL OPENING
			ENDIF
		ENDIF
	;STRATEGYモード専用処理
	CASE "経営モード"
		CALL 口上表示設定
		DRAWLINE
		TFLAG:ページ数 = 0
		IF LANGUAGE == "ENG"
			PRINTL ★★Select your manager★★
		ELSE
			PRINTL ★★パートナーを選択してください★★
		ENDIF
		CALL CHARA_LIST, 3
		CALL CHARA_ADD, RESULT
		ASSI = 1
		TARGET = -1
		;MOD適用されてるかもしれないので陥落素質を一度消す
		TALENT:ASSI:恋慕 = 0
		TALENT:ASSI:淫乱 = 0
		TALENT:ASSI:服従 = 0
		RESULT = 0
		IF OPTION:口上表示 && EXISTFUNCTION(@"PARTNER_%CSVNAMERPS(NOF(ASSI))%")
			CALLFORM PARTNER_%CSVNAMERPS(NOF(ASSI))%
		ELSE
			IF LANGUAGE == "ENG"
				PRINTFORMW %NAMEDISP(ASSI)% is your manager.
			ELSE
				PRINTFORMW %NAMEDISP(ASSI)%をパートナーに選択しました
			ENDIF
		ENDIF
		IF RESULT
			RESULT = RESULT-1
		ELSE
			IF LANGUAGE == "ENG"
				PRINTFORML Select %CALLNAMEDISP(ASSI)%'s falltype.
				PRINTL [0] - Affection
				PRINTL [1] - Lewd
				PRINTL [2] - Obey
			ELSE
				PRINTL 陥落状態を選択してください。
				PRINTL [0] - 恋慕
				PRINTL [1] - 淫乱
				PRINTL [2] - 服従
			ENDIF
			BINPUT
		ENDIF
		SELECTCASE RESULT
			CASE 0
				TALENT:ASSI:恋慕 = 1
				FLAG:累計恋慕++
				SIF LANGUAGE == "JP"
					PRINTL 恋慕に設定しました
			CASE 1
				TALENT:ASSI:淫乱 = 1
				FLAG:累計淫乱++
				SIF LANGUAGE == "JP"
					PRINTL 淫乱に設定しました
			CASE 2
				TALENT:ASSI:服従 = 1
				FLAG:累計服従++
				SIF LANGUAGE == "JP"
					PRINTL 服従に設定しました
		ENDSELECT
		CFLAG:ASSI:売却不可 = 1
		CFLAG:ASSI:売却可能 = 1
		CFLAG:ASSI:助手可能 = 1
		CFLAG:ASSI:陥落 = 1
		MARK:ASSI:屈服刻印 = 3
		;FLAG:パートナー = NOF(ASSI)
		パートナー = %NAME:ASSI%
		SIF 相性値(ASSI, MASTER) < 200
			RELATION:ASSI:(NOF(MASTER)) = 200
		SIF 相性値(MASTER, ASSI) < 200
			RELATION:MASTER:(NOF(ASSI)) = 200
		キャラ未購入 = 1
		バンめし = 1
ENDSELECT

IF ゲームモード() != "新婚モード"
	;チュートリアル表示
	IF DOPTION:有効 &&  DOPTION:チュートリアル && LANGUAGE == "JP" && !ORIGIN
		DRAWLINE
		PRINTL チュートリアルを表示しますか？（オプションからでも確認できます）
		PRINTL [0] - 表示する
		PRINTL [1] - 表示しない
		BINPUT
		IF RESULT == 0
			DRAWLINE
			IF ゲームモード() == "段位認定"
				CALL TEST_TUTORIAL
			ELSE
				CALL MANUAL_TUTORIAL
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF QMA
	通貨単位 = マジカ
ELSEIF DOPTION:有効
	OPTION:パセリ表示 = DOPTION:パセリ表示
ELSEIF ORIGIN
	通貨単位 = $
ELSE
	DRAWLINE
	IF LANGUAGE == "ENG"
		PRINTL Display the PASELI icon?(Can change options command)
		PRINTL [0] - No
		PRINTL [1] - Yes
	ELSE
		PRINTL PASELI表示をオンにしますか？(オプションからでも変更できます)
		PRINTL [0] - オンにしない
		PRINTL [1] - オンにする
	ENDIF
	BINPUT
	OPTION:パセリ表示 = RESULT
ENDIF

IF DOPTION:有効
	SIF DOPTION:着衣オプション
		FLAG:着衣設定 = 3
ELSE
	DRAWLINE
	IF LANGUAGE == "ENG"
		PRINTL Select character's clothes at start of training(Can change options command)
		PRINTL [0] - Wear
		PRINTL [1] - Naked
	ELSE
		PRINTL 奴隷の着衣設定を変更しますか？（オプションでも変更できます）
		PRINTL [0] - 服を着たままでいい
		PRINTL [1] - 面倒だから脱がせてから調教始める
	ENDIF
	BINPUT
	SIF RESULT == 1
		FLAG:着衣設定 = 3
ENDIF

$TUTORIAL_SKIP

CALL TUTORIAL

;プレイログに記録する
SELECTCASE ゲームモード()
	CASE "通常モード"
		CALL プレイログ記録, "通常モードを開始"
	CASE "経営モード"
		CALL プレイログ記録, @"「%NAME:ASSI%」パートナーで経営モードを開始"
	CASE "新婚モード"
		CALL プレイログ記録, @"「%NAME:MASTER%」と「%NAME:TARGET%」の新婚生活が始まる"
	CASE "段位認定"
		CALL プレイログ記録, @"\@ SEX(MASTER) == 1 ? 男性 # 女性\@主人公で段位認定「%受験段位名%」を開始"
	CASE "フリーモード"
		IF ORIGIN
			CALL プレイログ記録, @"「%NAME:MASTER%」主人公でORIGINモードを開始"
		ELSE
			CALL プレイログ記録, @"「%NAME:MASTER%」主人公でPFREEモードを開始"
		ENDIF
ENDSELECT

;天気の決定
CALL WEATHER

CFLAG:MASTER:憑依 = -1

IF ゲームモード() == "フリーモード" && NAME:MASTER == "BEATFREE"
	FLAG:実績解除不可 = 1
	CALL DEBUG_SAVEDATA, 2
ENDIF

CALL SYSTEM_AUTOSAVE
JUMP SHOP画面

;ここまでがメインとなる@EVENTFIRST
;以下@EVENTFIRST内で処理される関数
@SETUP_GAMESTART
#DIM DYNAMIC 日数, 2

;ORIGINでは仕様を説明する
IF ORIGIN
	PRINTL eraOrigin、及びOriginモードでは所持金の概念はありません 収入・支出の表示は全てフレーバー要素です
	PRINTW また、難易度はNORMAL(最低難易度)固定となります
ENDIF

DAY = 1
SELECTCASE ゲームモード()
	CASE "通常モード"
		MONEY = 3000
		OPTION:価格フィルタ上限 = 3000
		ITEM:医務室 = 1
		ITEM:図書室 = 1
		TARGET = -1
	CASE "経営モード"
		MONEY = TONUM("100万")
		ITEM:風俗店営業許可証 = 1
		TARGET = -1
	CASE "新婚モード"
		MONEY = TONUM("3億")
	CASE "段位認定"
		MONEY = 0
		TARGET = -1
	CASE "フリーモード"
		MONEY = 30000
		OPTION:価格フィルタ上限 = 30000
		ITEM:風俗店営業許可証 = 1
		TARGET = -1
	CASE "学園モード"
		CALL PASELIW, "入学祝い金として10000＄を貰った！"
		MONEY = 10000
ENDSELECT

SIF MONEY > 0
	CALL 現金出納帳記録, @"ゲーム開始/{MONEY}"

OPTION:カスタムフィルタ１ = 1
OPTION:カスタムフィルタ２ = 3
OPTION:色仕掛け = 1
OPTION:お誘い = 1
LASTMONEY = MONEY
購入人数 = 0
;リアルタイム計算処理 変数REALTIMEも残しておくこと
;リアルタイム(文字列式)からリアル西暦を抜き出して数値型に変換して代入する
REALTIME:年 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
REALTIME:月 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
REALTIME:日 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

SELECTCASE ゲームモード()
	CASE "学園モード"
		YEAR = REALTIME:年
		MONTH = 4
		DAYS = 1
		WHILE 休日()
			DAYS++
		WEND
	CASEELSE
		YEAR = REALTIME:年
		MONTH = REALTIME:月
		DAYS = REALTIME:日
ENDSELECT

ターン = 昼

;曜日計算
FLAG:曜日 = ZELLER(YEAR, MONTH, DAYS)

;月齢計算
CALL 月齢逆算, 30, -1

;過去19年の午前0時で最も新月に近いと思われる日と現在の日付の日数差を調べる
日数 = GETDAYS(YEAR, MONTH, DAYS) - GETDAYS(RESULT, RESULT:1, RESULT:2)

SELECTCASE 日数
	CASE IS <= 30
		月齢 = 日数*100
	CASEELSE
		日数:1 = 日数-(日数%30)
		月齢 = ((日数:1*100000)%2953059)/1000
		月齢 += (日数%30)*100
ENDSELECT

月齢 %= 3000

VARSET SELECTCOM, -1
SELECTCOM = 0
CALL FADEOUTBGM

;気温の算出
CALL TEMPERATURE

OPTION:TSBランダム = 1

TRYCALLFORM ゲーム開始_%CSVNAMERPS(NOF(MASTER))%

@START_CHARA_SELECT
#DIM DYNAMIC LRESULT

;チュートリアルならあなた固定
SIF TUTORIAL == "キャラクリ"
	GOTO CUSTOM

DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL ★★You can change Master★★
	PRINTL ★★Please select character★★
ELSE
	PRINTL ★★プレイヤーを変更できます★★
	PRINTL ★★キャラを選択してください★★
ENDIF

CALL CHARA_LIST, 2

SELECTCASE RESULT
	CASE 10000
		IF LANGUAGE == "ENG"
			PRINTW Does not change Master.
		ELSE
			PRINTW プレイヤーを変更しませんでした
		ENDIF
		$CUSTOM
		CALL PLAYER_CUSTOM, 0
	CASEELSE
		CALL CHARA_ADD, RESULT
		CALL CLEAR_CHARA, 0, 1
		MASTER = 0
		IF LANGUAGE == "ENG"
			PRINTFORMW Master is "%NAMEDISP(MASTER)%" now.
		ELSE
			PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
		ENDIF
		DRAWLINE
		IF ゲームモード() == "フリーモード"
			IF LANGUAGE == "ENG"
				PRINTFORML Do you add "You" For want training by %CALLNAMEDISP(MASTER)%?
				PRINTL [0] - Yes! Yes! Yes! I want!
				PRINTL [1] - Don't wanna
			ELSE
				PRINTFORML %CALLNAME:MASTER%に調教されたい人向けに奴隷としてあなたを追加しますか？
				PRINTL [0] - 調教されたい！
				PRINTL [1] - いらん
			ENDIF
			BINPUT
			IF RESULT == 0
				CALL CHARA_ADD, 0
				TARGET = 1
				CALL PLAYER_CUSTOM, TARGET
			ENDIF
		ENDIF
ENDSELECT

;新婚モード専用処理
IF ゲームモード() == "新婚モード"
	FLAG:難易度 = 1
	TFLAG:ページ数 = 0
	IF LANGUAGE == "ENG"
		PRINTL ★★Select you wife or husband★★
	ELSE
		PRINTL ★★嫁、もしくは婿を選択してください★★
	ENDIF
	CALL CHARA_LIST, 1
	CALL CHARA_ADD, RESULT
	TARGET = 1
	SIF NOF(TARGET) == 0
		CALL PLAYER_CUSTOM, TARGET
	RESULT = 0
	TRYCALLFORM WEDDING_%CSVNAMERPS(NOF(TARGET))%
	LRESULT = RESULT
	IF LANGUAGE == "ENG"
		PRINTW Congratulations on your wedding!
	ELSE
		PRINTFORMW %NAME:TARGET%とのご成婚おめでとうございます
	ENDIF
	;MODがあるかもしれないので消しておく
	TALENT:恋慕 = 0
	TALENT:淫乱 = 0
	TALENT:服従 = 0
	CFLAG:嫁 = 1
	CFLAG:陥落 = 1
	TALENT:恋心 = 0
	TALENT:親愛 = 1
	TALENT:MASTER:恋心 = 0
	TALENT:既婚者 = 1
	CSTR:既婚者 = %NAME:MASTER%
	TALENT:MASTER:既婚者 = 1
	CSTR:MASTER:既婚者 = %NAME:TARGET%
	MARK:屈服刻印 = 3
	MARK:MASTER:屈服刻印 = 3
	CFLAG:依存度 = 5000
	CFLAG:MASTER:依存度 = 5000
	;経営、施設、設備アイテム
	ITEM:医務室 = 1
	ITEM:図書室 = 1
	;SKIPDISPで農業用アイテムを全解禁しておく
	SKIPDISP 1
	CALL 栽培解禁
	SKIPDISP 0
	ITEM:田んぼ = 2
	ITEM:畑 = 8
	ITEM:プランター = 4
	田んぼの作物:1 = 米
	田んぼの作物:2 = 麦
	ITEM:牧場 = 1
	ITEM:畜舎 = 1
	ITEM:養蜂場 = 1
	ITEM:生け簀 = 1
	家畜:牛 = 100
	家畜:馬 = 100
	家畜:羊 = 100
	家畜:豚 = 100
	家畜:鶏 = 100
	家畜:蜂 = 100
	リソース:牧場の予算 = 1000000
	リソース:畜舎の予算 = 1000000
	リソース:養蜂場の予算 = 100000
	リソース:生け簀の予算 = 1000000
	ITEM:風俗店営業許可証 = 1
	ITEM:楽器セット一式 = 1
	ITEM:事業開発オフィス = 1
	SIF 相性値(TARGET, MASTER) < 300
		RELATION:(NOF(MASTER)) = 300
	SIF 相性値(MASTER, TARGET) < 300
		RELATION:MASTER:(NOF(TARGET)) = 300
	;WEDDING口上の返り値による
	SELECTCASE LRESULT
		CASE 0
			IF LANGUAGE == "ENG"
				PRINTFORML Did you experience with %CALLNAMEDISP(TARGET)%?
				PRINTL [0] - Oh yes
				PRINTL [1] - It is wrong. We are virgin.
			ELSE
				PRINTL ご結婚前からいろいろ経験なさりましたよね？
				PRINTL [0] - そりゃまぁ
				PRINTL [1] - 純潔を保ってきた
			ENDIF
		CASE 1
			GOTO 経験あり
		CASE 2
			GOTO 経験無し
	ENDSELECT
	BINPUT
	SELECTCASE RESULT
		CASE 0
			$経験あり
			SELECTCASE SEX(MASTER)
				CASE 1
					EXP:MASTER:射精経験 += 10
					EXP:絶頂経験 += 10
					EXP:MASTER:性交経験 += 10
					TALENT:MASTER:童貞 = 0
					EXP:性交経験 += 10
					CSTR:MASTER:童貞 = %NAME:TARGET%
					SELECTCASE SEX(TARGET)
						CASE 1
							EXP:MASTER:Ａ経験 += 10
							EXP:射精経験 += 10
							EXP:絶頂経験 += 10
							TALENT:童貞 = 0
							CSTR:童貞 = %NAME:MASTER%
						CASE 2
							EXP:膣射経験 += 10
							EXP:Ｖ経験 += 10
							TALENT:処女 = 0
							CSTR:処女 = %NAME:MASTER%
							CFLAG:処女の奪われ方 = 2
					ENDSELECT
				CASE 2
					EXP:MASTER:Ｖ経験 += 10
					TALENT:MASTER:処女 = 0
					EXP:MASTER:性交経験 += 10
					CSTR:MASTER:処女 = %NAME:TARGET%
					CFLAG:MASTER:処女の奪われ方 = 2
					SELECTCASE SEX(TARGET)
						CASE 1
							EXP:MASTER:膣射経験 += 10
							EXP:射精経験 += 10
							EXP:絶頂経験 += 10
							TALENT:童貞 = 0
							CSTR:童貞 = %NAME:MASTER%
						CASE 2
							EXP:Ｖ経験 += 10
							TALENT:処女 = 0
							CSTR:処女 = %NAME:MASTER%
							CFLAG:処女の奪われ方 = 2
					ENDSELECT
			ENDSELECT
		CASE 1
			$経験無し
			SELECTCASE SEX(MASTER)
				CASE 1
					TALENT:MASTER:童貞 = 1
					EXP:MASTER:性交経験 = 0
				CASE 2
					TALENT:MASTER:処女 = 1
					EXP:MASTER:性交経験 = 0
					EXP:MASTER:Ｖ経験 = 0
					EXP:MASTER:膣射経験 = 0
			ENDSELECT
			SELECTCASE SEX(TARGET)
				CASE 1
					TALENT:童貞 = 1
					EXP:性交経験 = 0
				CASE 2
					TALENT:処女 = 1
					EXP:性交経験 = 0
					EXP:Ｖ経験 = 0
					EXP:膣射経験 = 0
			ENDSELECT
	ENDSELECT
ENDIF

RETURN 0

@口上表示設定
DRAWLINE
IF DOPTION:有効
	OPTION:地の文表示 = DOPTION:地の文
	OPTION:口上表示 = DOPTION:口上
ELSE
	IF LANGUAGE == "ENG"
		PRINTL Want you display dialogues?(Can change options command)
		PRINTL [0] - Character Dialogues+General Dialogues
		PRINTL [1] - Character Dialogues only
		PRINTL [2] - General Dialogues only
		PRINTL [3] - No, Don't wanna
	ELSE
		PRINTL 口上を表示しますか？(オプションでも変更できます)
		PRINTL [0] - 口上+地の文を表示する
		PRINTL [1] - 口上のみ表示する
		PRINTL [2] - 地の文のみ表示する
		PRINTL [3] - 口上も地の文も表示しない
	ENDIF
	BINPUT
	SELECTCASE RESULT
		CASE 0
			OPTION:地の文表示 = 1
			OPTION:口上表示 = 1
		CASE 1
			OPTION:地の文表示 = 0
			OPTION:口上表示 = 1
		CASE 2
			OPTION:地の文表示 = 1
			OPTION:口上表示 = 0
		CASE 3
			OPTION:地の文表示 = 0
			OPTION:口上表示 = 0
	ENDSELECT
ENDIF


@PLAYER_CUSTOM, ARG = -1
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 種族名, 50
#DIM DYNAMIC 種族
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点
#DIM DYNAMIC 表示数
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日

CALL TUTORIAL

CALL LOOP_CLEARLINE

SIF ARG == -1
	ARG = MASTER
SIF CSTR:ARG:種族 == ""
	CSTR:ARG:種族 = 人間
DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL 【Basic】
	PRINTFORM 　Name:%NAMEDISP(ARG)% [1] - Change your name
	PRINTPLAIN  

	PRINT Gender:
	SELECTCASE SEX(ARG)
		CASE 1
			PRINT Male
		CASE 2
			PRINT Female
	ENDSELECT
	SIF TALENT:ARG:ふたなり
		PRINT (Futanari)
	PRINT  

	PRINT  [2] - Change your gender 
	PRINTPLAIN  
	PRINTFORM Race:%RACEDISP(ARG)% [3] - Change your race
	PRINTL 

	PRINTL 【容姿】
	PRINT 　Physical:
	SELECTCASE TALENT:ARG:体格
		CASE 1
			PRINT Petit 
		CASE 2
			PRINT Child 
		CASE 3
			PRINT Basic 
		CASE 4
			PRINT Tall 
	ENDSELECT
	PRINT [4] - Change your physical
	PRINTPLAIN  
	IF SEX(ARG) == 2
		PRINT BreastSize:
		SELECTCASE TALENT:ARG:バストサイズ
			CASE 1
				PRINT None 
			CASE 2
				PRINT Little 
			CASE 3
				PRINT Basic 
			CASE 4
				PRINT Large 
			CASE 5
				PRINT XLarge 
			CASE 6
				PRINT Huge 
		ENDSELECT
		PRINT [5] - Change your BreastSize
	ENDIF
	PRINTL

	PRINTFORMLC 　Animal Ear:\@ TALENT:ARG:動物耳 ? Have # None\@ [6] - Change
	PRINTPLAIN  
	PRINTFORMLC Tail:\@ TALENT:ARG:尻尾 ? Have # None\@ [7] - Change 
	PRINTPLAIN  
	PRINTFORMLC Wings:\@ TALENT:ARG:羽 ? Have # None\@ [8] - Change 
	PRINTPLAIN  
	PRINTL 
	PRINTFORMLC 　Farry:\@ TALENT:ARG:ファーリー ? Have # None\@ [9] - Change 
	PRINTPLAIN 
	PRINTFORMLC Life:\@ !TALENT:ARG:ゾンビ ? Have # None\@ [10] - Change
	PRINTL 
	PRINTL 【Sexual Experience】
	PRINTL 　[11] - None [12] - Basic [13] - Awesome
ELSE
	PRINTL 【基礎】
	SETCOLORBYNAME lightskyblue
	PRINTFORM 　名前:%NAME:ARG% [1] - 名前を変更する
	PRINTPLAIN  

	SETCOLORBYNAME lightcoral
	PRINT 性別:
	SELECTCASE SEX(ARG)
		CASE 1
			PRINT 男性
		CASE 2
			PRINT 女性
		CASEELSE
			PRINT 未選択
	ENDSELECT
	SIF TALENT:ARG:ふたなり
		PRINT (ふたなり)
	PRINT  

	PRINT [2] - 性別を変更する
	PRINTPLAIN  
	SETCOLORBYNAME lightskyblue
	PRINTFORM 種族:%CSTR:ARG:種族% [3] - 種族を変更する
	PRINTL 

	RESETCOLOR
	PRINTL 【容姿】
	SETCOLORBYNAME lightskyblue
	PRINT 　体格:
	SELECTCASE TALENT:ARG:体格
		CASE 1
			PRINT 幼児 
		CASE 2
			PRINT 小柄 
		CASE 3
			PRINT 普通 
		CASE 4
			PRINT 長身 
	ENDSELECT
	PRINT [4] - 体格を変更する
	PRINTPLAIN  
	SETCOLORBYNAME lightcoral
	IF SEX(ARG) == 2
		PRINT バストサイズ:
		SELECTCASE TALENT:ARG:バストサイズ
			CASE 1
				PRINT 絶壁 
			CASE 2
				PRINT 貧乳 
			CASE 3
				PRINT 並乳 
			CASE 4
				PRINT 巨乳 
			CASE 5
				PRINT 豊乳 
			CASE 6
				PRINT 爆乳 
		ENDSELECT
		PRINT [5] - バストサイズを変更する
	ENDIF
	PRINTL

	SETCOLORBYNAME lightskyblue
	PRINTFORMLC 　動物耳:\@ TALENT:ARG:動物耳 ? 有り # 無し\@ [6] - 変更
	PRINTPLAIN  
	SETCOLORBYNAME lightcoral
	PRINTFORMLC 尻尾:\@ TALENT:ARG:尻尾 ? 有り # 無し\@ [7] - 変更 
	PRINTPLAIN  
	SETCOLORBYNAME lightskyblue
	PRINTFORMLC 羽:\@ TALENT:ARG:羽 ? 有り # 無し\@ [8] - 変更 
	PRINTPLAIN  
	PRINTL 
	SETCOLORBYNAME lightskyblue
	PRINTFORMLC 　もふもふ:\@ TALENT:ARG:ファーリー ? 有り # 無し\@ [9] - 変更 
	PRINTPLAIN 
	SETCOLORBYNAME lightcoral
	PRINTFORMLC 生命活動:\@ !TALENT:ARG:ゾンビ ? 有り # 無し\@ [10] - 変更
	PRINTL 
	RESETCOLOR
	PRINTL 【性経験】
	PRINTL 　[11] - 無し [12] - 普通 [13] - すごい
ENDIF
表示数 = 0
FOR LCOUNT, 0, 100
	SELECTCASE EXPNAME:LCOUNT
		CASE "Ｖ経験", "膣射経験", "出産経験", "流産経験"
			SIF !VAGINA(ARG)
				CONTINUE
		CASE "射精経験"
			SIF !PENIS(ARG)
				CONTINUE
		CASE "レズ経験", "パイズリ経験"
			SIF SEX(ARG) != 2
				CONTINUE
		CASE "ゲイ経験"
			SIF SEX(ARG) != 1
				CONTINUE
		CASE "Ａ経験", "絶頂経験", "性交経験", "精液経験", "フェラ経験", "自慰経験"
		CASEELSE
			CONTINUE
	ENDSELECT
	SIF 表示数 == 0
		PRINT 　
	IF 表示数%2 == 0
		SETCOLORBYNAME lightskyblue
	ELSE
		SETCOLORBYNAME lightcoral
	ENDIF

	IF LANGUAGE == "ENG"
		PRINTFORMLC %EXPDISP(LCOUNT)%:{EXP:ARG:LCOUNT} [{LCOUNT+20}] - Change
	ELSE
		PRINTFORMLC %EXPDISP(LCOUNT)%:{EXP:ARG:LCOUNT} [{LCOUNT+20}] - 変更
	ENDIF
	PRINTPLAIN  
	表示数++
	IF 表示数%3 == 0 && 表示数 != 0
		PRINTL 
		表示数 = 0
	ENDIF
NEXT
RESETCOLOR
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　PLAYER CUSTOM　
IF LANGUAGE == "ENG"
	PRINTL [0] - Complete [100] - Save character data [101] - Load character data
ELSE
	PRINTL [0] - 完了 [100] - キャラデータを保存 [101] - キャラデータをロード
ENDIF

BINPUT
SELECTCASE RESULT
	CASE 0
		IF TALENT:ARG:性別 == 0
			IF LANGUAGE == "ENG"
				PRINTW Please select gender
			ELSE
				PRINTW 性別を選択してください
			ENDIF
			RESTART
		ENDIF
		SIF VAGINA(ARG) && !EXP:ARG:Ｖ経験
			TALENT:ARG:処女 = 1
		SIF PENIS(ARG) && !EXP:ARG:性交経験
			TALENT:ARG:童貞 = 1
		SELECTCASE CSTR:ARG:種族
			CASE "スライム"
				CFLAG:ARG:スライム度 = 30
				TALENT:ARG:スライム = 1
			CASE "ヴァンパイア"
				TALENT:ARG:吸血鬼 = 1
		ENDSELECT
		EXP:ARG:音ゲー経験 = 1000
		VARLINE = 0
		主人初期種族 = %CSTR:ARG:種族%
		CALL TUTORIAL, "キャラクリ終了"
		RETURN
	CASE 1
		CALL INPUT_NAME, ARG
	CASE 2
		CALL SELECT_GENDER, ARG
	CASE 3
		VARSET 種族名

		種族 = GETNUM(CSTR, "種族")

		FOR LCOUNT, 1, 10000
			SIF !EXISTCSV(LCOUNT)
				CONTINUE
			;変数「種族名」の配列に該当種族名がまだ入ってない場合は入れる
			IF CSVCSTR(LCOUNT, 種族) != ""
				SIF FINDELEMENT(種族名, ESCAPE(CSVCSTR(LCOUNT, 種族)), 0, VARSIZE("種族名"), 1) == -1
					種族名:FINDELEMENT(種族名, "", 0, VARSIZE("種族名"), 1) = %CSVCSTR(LCOUNT, 種族)%
			ENDIF
		NEXT

		;追加の特殊種族
		種族名:FINDELEMENT(種族名, "", 0, VARSIZE("種族名"), 1) = スライム

		FOR LCOUNT, 0, VARSIZE("種族名")
			SIF 種族名:LCOUNT == ""
				BREAK
			PRINTFORMLC [{LCOUNT}] %種族名:LCOUNT%
		NEXT
		CALL NEWLINE
		BINPUT
		CSTR:ARG:種族 = %種族名:RESULT%
	CASE 4
		IF LANGUAGE == "ENG"
			PRINTL [1] - Petit
			PRINTL [2] - Child
			PRINTL [3] - Basic
			PRINTL [4] - Tall
		ELSE
			PRINTL [1] - 幼児
			PRINTL [2] - 小柄
			PRINTL [3] - 普通
			PRINTL [4] - 長身
		ENDIF
		BINPUT
		TALENT:ARG:体格 = RESULT
	CASE 5
		IF LANGUAGE == "ENG"
			PRINTL [1] - None
			PRINTL [2] - Little
			PRINTL [3] - Basic
			PRINTL [4] - Large
			PRINTL [5] - XLarge
			PRINTL [6] - Huge
		ELSE
			PRINTL [1] - 絶壁にする
			PRINTL [2] - 貧乳にする
			PRINTL [3] - 並乳にする
			PRINTL [4] - 巨乳にする
			PRINTL [5] - 豊乳にする
			PRINTL [6] - 爆乳にする
		ENDIF
		BINPUT
		TALENT:ARG:バストサイズ = RESULT
	CASE 6
		INVERTBIT TALENT:ARG:動物耳, 0
	CASE 7
		INVERTBIT TALENT:ARG:尻尾, 0
	CASE 8
		INVERTBIT TALENT:ARG:羽, 0
	CASE 9
		INVERTBIT TALENT:ARG:ファーリー, 0
	CASE 10
		INVERTBIT TALENT:ARG:ゾンビ, 0
	CASE 11
		VARSET EXP:ARG:0, 0
	CASE 12
		VARSET EXP:ARG:0, 0
		IF VAGINA(ARG)
			EXP:ARG:Ｖ経験 = RAND(10, 31)
			EXP:ARG:膣射経験 = RAND(0, 6)
			EXP:ARG:絶頂経験 += RAND(10, 21)
			EXP:ARG:精液経験 = RAND(20, 51)+EXP:ARG:膣射経験
		ENDIF
		IF PENIS(ARG)
			EXP:ARG:射精経験 = RAND(50, 101)
			EXP:ARG:絶頂経験 += EXP:ARG:射精経験
		ENDIF
		IF SEX(ARG) == 2
			EXP:ARG:パイズリ経験 = RAND(3, 11)
			EXP:ARG:フェラ経験 = RAND(5, 21)
		ENDIF
		EXP:ARG:性交経験 = RAND(5, 11)
		EXP:ARG:自慰経験 = RAND(10, 31)
	CASE 13
		VARSET EXP:ARG:0, 0
		IF VAGINA(ARG)
			EXP:ARG:Ｖ経験 = RAND(500, 1001)
			EXP:ARG:膣射経験 = RAND(300, 501)
			EXP:ARG:出産経験 = RAND(1, 4)
			SIF RAND:5 == 0
				EXP:ARG:流産経験 = 1
		ENDIF
		SIF PENIS(ARG)
			EXP:ARG:射精経験 = RAND(2000, 5001)
		SELECTCASE SEX(ARG)
			CASE 1
				EXP:ARG:ゲイ経験 = RAND(500, 1001)
			CASE 2
				EXP:ARG:レズ経験 = RAND(500, 1001)
				EXP:ARG:パイズリ経験 = RAND(200, 301)
		ENDSELECT
		EXP:ARG:Ａ経験 = RAND(500, 1001)
		EXP:ARG:性交経験 = RAND(300, 501)
		EXP:ARG:自慰経験 = RAND(1000, 2001)
		EXP:ARG:フェラ経験 = RAND(500, 1001)
		EXP:ARG:絶頂経験 = RAND(500, 1001)+EXP:ARG:射精経験
		EXP:ARG:精液経験 = RAND(2000, 5001)+EXP:ARG:膣射経験
	CASE 20 TO 85
		LRESULT = RESULT-20
		IF EXPNAME:(LRESULT) != ""
			IF LANGUAGE == "ENG"
				PRINTFORML Please input %EXPDISP(LRESULT)% count.
			ELSE
				PRINTFORML %EXPDISP(LRESULT)%の数値を入力してください
			ENDIF
			INPUT
			SIF RESULT < 0
				RESULT = 0
			EXP:ARG:(LRESULT) = RESULT
		ENDIF
	CASE 100
		FOR LCOUNT, 0, 10
			IF CHKCHARADATA(@"CUSTOMPLAYER_{LCOUNT}") == 0
				PRINTFORML [{LCOUNT}] - %RESULTS%
			ELSE
				SETCOLORBYNAME gray
				IF LANGUAGE == "ENG"
					PRINTFORML [{LCOUNT}] - -----------
				ELSE
					PRINTFORML [{LCOUNT}] - 空きスロット
				ENDIF
				RESETCOLOR
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　PC SAVE　
		IF LANGUAGE == "ENG"
			PRINTL [10] - Back
		ELSE
			PRINTL [10] - 戻る
		ENDIF
		BINPUT
		SELECTCASE RESULT
			CASE 0 TO 9
				LRESULT = RESULT
				IF CHKCHARADATA(@"CUSTOMPLAYER_{RESULT}") == 0
					IF LANGUAGE == "ENG"
						PRINTL It was exist charadata. Overwrite sure?
						PRINTL [0] - Yes
						PRINTL [1] - No
					ELSE
						PRINTL 既にカスタムデータが存在します。上書きしてもよろしいですか？
						PRINTL [0] - はい
						PRINTL [1] - いいえ
					ENDIF
					BINPUT
					SIF RESULT == 1
						RESTART
				ENDIF
				年 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
				月 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
				日 = TOINT(SUBSTRING(GETTIMES(), 8, 2))
				IF LANGUAGE == "ENG"
					SAVECHARA @"CUSTOMPLAYER_{LRESULT}", @"%NAME:ARG% Created in {年}/{月}/{日}", ARG
				ELSE
					SAVECHARA @"CUSTOMPLAYER_{LRESULT}", @"%NAME:ARG% {年}/{月}/{日}に作成", ARG
				ENDIF
		ENDSELECT
	CASE 101
		FOR LCOUNT, 0, 10
			IF CHKCHARADATA(@"CUSTOMPLAYER_{LCOUNT}") == 0
				PRINTFORML [{LCOUNT}] - %RESULTS%
			ELSE
				SETCOLORBYNAME gray
				IF LANGUAGE == "ENG"
					PRINTFORML [{LCOUNT}] - -----------
				ELSE
					PRINTFORML [{LCOUNT}] - 空きスロット
				ENDIF
				RESETCOLOR
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　PC LOAD　
		IF LANGUAGE == "ENG"
			PRINTL [10] - Back
		ELSE
			PRINTL [10] - 戻る
		ENDIF
		BINPUT
		SELECTCASE RESULT
			CASE 0 TO 9
				IF CHKCHARADATA(@"CUSTOMPLAYER_{RESULT}") == 0
					DELCHARA ARG
					LOADCHARA @"CUSTOMPLAYER_{RESULT}"
				ENDIF
		ENDSELECT
ENDSELECT

RESTART

@SELECT_GENDER, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL Select your gender.
	PRINTL [0] - Male
	PRINTL [1] - Female
	SELECTCASE ゲームモード()
		CASE "通常モード", "経営モード"
		CASEELSE
			PRINTL [2] - Male(Futanari)
			PRINTL [3] - Female(Futanari)
	ENDSELECT
ELSE
	PRINTL 性別を選択してください
	PRINTL [0] - 男性
	PRINTL [1] - 女性
	SELECTCASE ゲームモード()
		CASE "通常モード", "経営モード"
		CASEELSE
			PRINTL [2] - 男性(ふたなり)
			PRINTL [3] - 女性(ふたなり)
	ENDSELECT
ENDIF
BINPUT
SELECTCASE RESULT
	CASE 0, 2
		TALENT:ARG:性別 = 1
		TALENT:ARG:バストサイズ = 0
		CSTR:ARG:上半身上着 = シャツ
		CSTR:ARG:下半身上着 = ズボン
		CSTR:ARG:全身上着 = 
		CSTR:ARG:上半身下着 =
		CSTR:ARG:下半身下着 = トランクス
		CFLAG:ARG:挿入可 = 0
	CASE 1, 3
		TALENT:ARG:性別 = 2
		TALENT:ARG:バストサイズ = 3
		CSTR:ARG:上半身上着 = 
		CSTR:ARG:下半身上着 = 
		CSTR:ARG:全身上着 = ワンピース
		CSTR:ARG:上半身下着 = ブラジャー
		CSTR:ARG:下半身下着 = パンティ
		CFLAG:ARG:挿入可 = 1
ENDSELECT
SIF GROUPMATCH(RESULT, 2, 3)
	TALENT:ARG:ふたなり = 1

@INPUT_NAME, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
DO
	IF LANGUAGE == "ENG"
		PRINT Please input your name(If nothing, Your name is ""
		PRINTBUTTON "You", "You"
		PRINTL ")
	ELSE
		PRINT 名前を入力してください(何も入力しない場合は「
		PRINTBUTTON "あなた", "あなた"
		PRINTL 」になります)
	ENDIF
	INPUTS
	SELECTCASE RESULTS
		CASE ""
			CALLNAME:ARG = あなた
			NAME:ARG = あなた
			CSTR:ARG:英語名 = You
			CSTR:ARG:英語呼び名 = You
		CASEELSE
			CALLNAME:ARG = %RESULTS%
			NAME:ARG = %RESULTS%
			CSTR:ARG:英語名 = %RESULTS%
			CSTR:ARG:英語呼び名 = %RESULTS%
	ENDSELECT
	IF LANGUAGE == "ENG"
		PRINTFORML Do you confirm "%CALLNAMEDISP(MASTER)%"?
		PRINTL [0] - Yes
		PRINTL [1] - No
	ELSE
		PRINTFORML 「%CALLNAME:ARG%」でよろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
	ENDIF
	BINPUT
LOOP LOOPRES(0)

@SELECT_DIFFICULT
;ORIGINとチュートリアルはNORMAL固定(暫定)
IF ORIGIN || TUTORIAL == "キャラクリ終了"
	FLAG:難易度 = 1
	RETURN
ENDIF

DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL ★★Please select difficulty★★
	SETCOLORBYNAME lime
	PRINTL [-1] - BEGINNER(Can not earn achievement)
	SETCOLORBYNAME deepskyblue
	PRINTL [0] - NORMAL
	SETCOLORBYNAME yellow
	PRINTL [1] - HYPER
	SETCOLORBYNAME coral
	PRINTL [2] - ANOTHER
	SETCOLORBYNAME violet
	PRINTL [3] - LEGGENDARIA
ELSE
	PRINTL ★★難易度を選択してください★★
	IF QMA
		SETCOLORBYNAME lime
		PRINTL [-1] - ビギナー級(周回不可、実績解除不可)
		SETCOLORBYNAME deepskyblue
		PRINTL [0] - フェアリー級(初心者でも安心)
		SETCOLORBYNAME yellow
		PRINTL [1] - ミノフェニ級(それなりに)
		SETCOLORBYNAME coral
		PRINTL [2] - ドラゴン級(ようこそ廃人の入口へ)
		SETCOLORBYNAME violet
		PRINTL [3] - レジェンダリア級
	ELSE
		SETCOLORBYNAME lime
		PRINTL [-1] - BEGINNER(周回不可、実績解除不可)
		SETCOLORBYNAME deepskyblue
		PRINTL [0] - NORMAL
		SETCOLORBYNAME yellow
		PRINTL [1] - HYPER
		SETCOLORBYNAME coral
		PRINTL [2] - ANOTHER
		SETCOLORBYNAME violet
		PRINTL [3] - LEGGENDARIA
	ENDIF
ENDIF
RESETCOLOR
BINPUT
IF RESULT == -1
	FLAG:難易度 = -1
ELSE
	FLAG:難易度 = RESULT+1
ENDIF
