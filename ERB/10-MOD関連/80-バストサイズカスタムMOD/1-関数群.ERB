@キャラバストサイズ表示, 対象
#FUNCTIONS
#DIM DYNAMIC 対象
#DIM DYNAMIC トップ
#DIM DYNAMIC アンダー
#DIM DYNAMIC 成長割合
#DIM DYNAMIC トップMIN
#DIM DYNAMIC アンダーMIN
#DIM DYNAMIC トップMAX
#DIM DYNAMIC アンダーMAX

トップMIN = バストサイズ詳細トップ:(NOF(対象)):(TALENT:対象:バストサイズ)
アンダーMIN = バストサイズ詳細アンダー:(NOF(対象)):(TALENT:対象:バストサイズ)
トップMAX = バストサイズ詳細トップ:(NOF(対象)):(TALENT:対象:バストサイズ+1)
アンダーMAX = バストサイズ詳細アンダー:(NOF(対象)):(TALENT:対象:バストサイズ+1)

IF TALENT:対象:バスト成長 && トップMIN && アンダーMIN && トップMAX && アンダーMAX
	成長割合 = LIMIT(CFLAG:対象:バスト成長, 0, 999)
	トップ = トップMIN
	アンダー = アンダーMIN
	トップ += (トップMAX-トップMIN)*成長割合/1000
	アンダー += (アンダーMAX-アンダーMIN)*成長割合/1000
ELSE
	トップ = トップMIN
	アンダー = アンダーMIN
ENDIF
RETURNF バストサイズ詳細表示(トップ, アンダー)


@バストサイズ詳細表示, トップ, アンダー
#FUNCTIONS
#DIM DYNAMIC トップ
#DIM DYNAMIC アンダー
#DIM DYNAMIC 差分
#DIM DYNAMIC 整数
#DIM DYNAMIC 小数
#DIMS DYNAMIC 本文
;バストサイズ詳細小数処理　0...四捨五入 1...切り捨て 2...切り上げ 3...小数表示
IF トップ
	整数 = トップ/10
	小数 = トップ%10
	;小数表示
	IF バストサイズ詳細小数処理 == 3
		本文 += @"{整数}.{小数}cm"
	ELSE
		SELECTCASE バストサイズ詳細小数処理
			;四捨五入
			CASE 0
				SIF 小数 >= 5
					整数++
			;切り上げ
			CASE 2
				SIF 小数 > 0
					整数++
		ENDSELECT
		本文 += @"{整数}cm"
	ENDIF
	IF アンダー
		差分 = トップ-アンダー
		;2.5の倍数に丸めて2.5で割る
		差分 += 12
		差分 /= 25
		SELECTCASE 差分
			CASE IS <= 4
				本文 += @"(%"A"*(LIMIT(5-差分, 1, 5))%カップ)"
			CASE 5 TO 29
				本文 += @"(%UNICODE(ENCODETOUNI("B")+(差分-5))%カップ)"
			CASE IS >= 30
				本文 += @"(Zカップ+)"
		ENDSELECT
	ENDIF
ELSE
	本文 += "未設定"
	;式中関数化したので一旦色変え保留
	;CALL PRINTCOLOR, "未設定", "gray"
ENDIF
RETURNF 本文


;;;カップ数を入れるとトップとアンダーの差をミリメートル単位で出す
;;;入力が対応していない場合、-1を返す
@トップアンダー差分, カップサイズ
#FUNCTION
#DIMS DYNAMIC カップサイズ
#DIM DYNAMIC エンコード

;A以下
SELECTCASE CONV_ABC(カップサイズ)
	CASE "AAAAA"
		RETURNF 0
	CASE "AAAA"
		RETURNF 25
	CASE "AAA"
		RETURNF 50
	CASE "AA"
		RETURNF 75
	CASE "A"
		RETURNF 100
ENDSELECT
;B以上
SELECTCASE ENCODETOUNI(CONV_ABC(カップサイズ))
	CASE ENCODETOUNI("B") TO ENCODETOUNI("Z")
		エンコード = ENCODETOUNI(CONV_ABC(カップサイズ))
		RETURNF (エンコード-ENCODETOUNI("A"))*25+100
ENDSELECT
RETURNF -1
