@セーブデータアップデート
#DIM DYNAMIC LCOUNT
#DIM UPDATE

;セーブデータIDを入れる
SIF データID == 0
	データID = GETSECOND()
;バージョンを入れる
IF バージョン == 0
	IF LASTLOAD_VERSION == -1
		バージョン = GAMEBASE_VERSION
	ELSE
		バージョン = LASTLOAD_VERSION
	ENDIF
ENDIF

CCB:(NOF(MASTER)) = 0

;2151 憑依フラグの初期設定
IF バージョン <= 2150
	FOR LCOUNT, 0, CHARANUM
		SIF CFLAG:LCOUNT:憑依 == 0
			CFLAG:LCOUNT:憑依 = -1
	NEXT
	バージョン = 2151
ENDIF
;2152 人体錬成キャラの憑依フラグ設定
IF バージョン <= 2151
	FOR LCOUNT, 0, CHARANUM
		SIF NOF(LCOUNT) > 10000 && CFLAG:LCOUNT:人工物 && CFLAG:LCOUNT:憑依 == 0
			CFLAG:LCOUNT:憑依 = -1
	NEXT
	バージョン = 2152
ENDIF
;2153 隠しキャラの憑依フラグ設定
IF バージョン <= 2152
	FOR LCOUNT, 0, CHARANUM
		SIF GROUPMATCH(CHARANAMEF(LCOUNT), "キャロル", "ことみちゃん")
			CFLAG:LCOUNT:憑依 = -1
	NEXT
	バージョン = 2153
ENDIF

FOR LCOUNT, 0, CHARANUM
	;CHILDNAMEの確認
	SIF NOF(LCOUNT) > 10000 && CHILDNAME:(NOF(LCOUNT)-10000) == ""
		CHILDNAME:(NOF(LCOUNT)-10000) = %NAME:LCOUNT%
	;以下経営に関するセーブデータのアップデート用
	SIF UPDATE
		BREAK
	;モチベーションが設定されてなければ初期値(50)にする
	SIF CFLAG:LCOUNT:モチベーション == 0
		CFLAG:LCOUNT:モチベーション = 50
NEXT

;各SAVESTRにアイテム名をセットする
FOR LCOUNT, 0, 50
	SIF SAVESTR:LCOUNT == ""
		SAVESTR:LCOUNT = %SAVESTRNAME:LCOUNT%
NEXT

UPDATE = 1
