@セーブデータアップデート
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 旧連絡先, 10000
#DIM UPDATE

WAIT

;セーブデータIDを入れる
SIF データID == 0
	データID = GETSECOND()
;バージョンを入れる
IF バージョン == 0
	IF LASTLOAD_VERSION == -1
		バージョン = GAMEBASE_VERSION
	ELSE
		バージョン = LASTLOAD_VERSION
	ENDIF
ENDIF

;アイテム改造変数にアイテム名をセットする
FOR LCOUNT, 0, VARSIZE("アイテム改造")
	SIF アイテム改造:LCOUNT == ""
		アイテム改造:LCOUNT = %ERDNAME(アイテム改造, LCOUNT)%
NEXT

;体格フラグが無いキャラは3
FOR LCOUNT, 0, CHARANUM
	SIF TALENT:LCOUNT:体格 == 0
		TALENT:LCOUNT:体格 = 3
NEXT

;学園モードで旧バージョンなら連絡先をNOに修正
IF ゲームモード() == "学園モード"
	IF バージョン < 225020240921
		ARRAYCOPY "連絡先", "旧連絡先"
		VARSET 連絡先
		FOR LCOUNT, 0, 10000
			SIF 旧連絡先:LCOUNT
				連絡先:(NO:LCOUNT) = 1
		NEXT
	ENDIF
	バージョン = 225020240921
ENDIF

UPDATE = 1
