@SHOP画面
;毎ターン呼ばれる関数はそちらに移動
CALL SHOP画面処理

CALL LOOP_CLEARLINE

;段位認定では施設経営を外す
IF ゲームモード() == "段位認定"
	CALL カラム生成, "SHOP", "メイン", "キャラクター", "キャラ・アイテム購入", "その他", "データベース", "セーブ", "ロード", "ショートカット", "オプション"
ELSE
	CALL カラム生成, "SHOP", "メイン", "キャラクター", "施設・経営", "キャラ・アイテム購入", "その他", "データベース", "セーブ", "ロード", "ショートカット", "オプション"
ENDIF

;何かあったときのためにRESTART置いておく
RESTART

@SHOP画面処理
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIM CSV読み込み

REDRAW 0

CALL GIDDEL

MAX_LINE = 45

;MaxLogを超えるとdivが崩れる不具合がまだ起きるのでERBで対応する
;SIF LINECOUNT >= 45000
;	CLEARLINE LINECOUNT

;ORIGINならCSV実装済みの全キャラ解禁する
IF CSV読み込み == 0 && ORIGIN
	LTARGET = TARGET
	FOR LCOUNT, 0, 10000
		SIF EXISTCSV(LCOUNT) && GETCHARA(LCOUNT) == -1 && NOF(MASTER) != LCOUNT
			CALL CHARA_ADD, LCOUNT
	NEXT
	TARGET = LTARGET
	CSV読み込み = 1
ENDIF

;ORIGINには金の概念は無いものの内部処理的には多いほうが都合が良いのである程度の額で固定する
SIF ORIGIN
	MONEY = 1000000

;モードに応じてBGMを流す
SELECTCASE ゲームモード()
	CASE "新婚モード"
		CALL 新婚モード_BGM
	CASE "段位認定"
		CALL 段位認定_BGM
		;口上もオフにしておく
		OPTION:口上表示 = 0
	CASEELSE
		CALL 通常モード_BGM
ENDSELECT

VARLINE = LINECOUNT

SIF TARGET == 0
	TARGET = -1

;STRATEGYモードクリアデータを周回可能にする
SIF ゲームモード() == "フリーモード" && パートナー != ""
	FLAG:周回可能 = 1

CALL セーブデータアップデート

CALL キャラステータス最適化

CALL CVAR_RESET

SIF GCS:店名 == ""
	GCS:店名 = %I18N("ゲームセンター")%

SIF BRS:店名 == ""
	BRS:店名 = %I18N("娼館")%

;GUMINDAYSキャラ5人以上陥落でユリア天使化イベント
SIF GUMINDAYS >= 5
	CALL ユリア天使化

;実績:STRATEGYモードで期限内に100億円稼ぐ
SIF ゲームモード() == "経営モード" && MONEY >= TONUM("100億")
	CALL 実績解除, "10，000，000，000"

;実績:5000兆円稼ぐ
SIF MONEY >= TONUM("5000兆")
	CALL 実績解除, "5000兆円欲しい！"

SIF 受験段位名 == "魅惑のサバト"
	ITEM:媚薬 = 99

;新婚モードで経験値100以下なら53万補充される
SIF ゲームモード() == "新婚モード" && FLAG:経験値 <= 100
	FLAG:経験値 = TONUM("53万")

;曜日計算
FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

;祝日・記念日の設定
CALL HOLIDAY

;対象か助手が使用不可の場合は外す
SIF TARGET >= 0 && CFLAG:使用不可
	TARGET = -1
SIF (ASSI >= 0 && CFLAG:ASSI:使用不可) || ASSI == 0
	ASSI = -1

VARLINE = LINECOUNT

@SHOP_メイン
#DIMS DYNAMIC 能力表示用, 4
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 店長
#DIM DYNAMIC 誕生日キャラ
#DIMS DYNAMIC LCALL
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 売却可能
#DIM DYNAMIC 調教可能
#DIM DYNAMIC 別キャラあり
#DIMS DYNAMIC 表示中
#DIM DYNAMIC ABLNAMELEN

;ゲーム内で表示されるショップ画面
;□XXXX年XX月XX日(曜日) X日目(昼夜) X周目 MODE:XXXXX
;　天気:XX 気温:XX.X℃ XXX(月齢.XXX) 
;　所持金:$XXXX 昨日の所持金:$XXXX XXXX 変動価格
;　難易度:XXXX
;　総奴隷数:X/陥落済み:X
;
;□調教中 [750]　前のキャラ [751] 次のキャラ
;　No.X XXXXXX(♂♀)[ふたなり]【陥落素質等】依存度:XX ストレス値:XXX/999
;　従順:LVXX 欲望:LVXX 技巧:LVXX
;　Ｃ感:LVXX Ｖ感:LVXX Ａ感:LVXX Ｂ感:LVXX
;　苦痛刻印:LVX [XXX]　快楽刻印:LVX [XXX]　屈服刻印:LVX [XXX]　反発刻印:LVX [XXX]
;　体力[********************************](XXXX/XXXX)[排卵誘発中]
;　気力[********************************](XXXX/XXXX)
;
;　保管済み母乳:Xml　保管済み精液:Xml　保管済み血液:Xml　保管済み粘液:Xml
;　主人:XXX
;　助手:XXX
;-------------------------------------------------------------------------------------
;□ゲームセンターの状況
;　営業中or休業中 [700]営業を開始するor休業する
;　前日の収入:($XXX) 店長:XXXX
;-------------------------------------------------------------------------------------
;□娼館の状況
;　営業中or休業中 [701]営業を開始するor休業する
;　前日の収入:($XXX)
;-------------------------------------------------------------------------------------
;□飲食店の状況
;　営業中or休業中 [702]営業を開始するor休業する
;　前日の収入:($XXX)
;-------------------------------------------------------------------------------------
;□適用中のMOD(あれば表示)
;　[XXX][XXX]
;

CALL SHOP画面処理

IF GUI
	;カレンダーは別カラムで表示する
	CALL COLUMNCREATE, 1
	CALL COLUMNRESIZE, 1, 1100, 1200
	CALL COLUMNMOVE, 1, 5000, 0
	CALL カレンダー生成
	;そうそうサイズは変わらないと思うのでサイズ直指定してみる
	CALL COLUMNIMG, 1, "カレンダー", 1095, 1162
	CALL COLUMNPRINTL, 1, ""
ENDIF

;現助手のキャラ数の確認
FOR LCOUNT, 1, CHARANUM
	;助手可能
	SIF 助手可能(LCOUNT)
		助手可能++
	;売却可能
	SIF 売却可能(LCOUNT)
		売却可能++
	;TARGETの他に調教できるキャラがいるかどうか
	SIF LCOUNT == TARGET
		CONTINUE
	SIF 調教可能(LCOUNT)
		調教可能++
NEXT

;日数表示
;CALL CHECKDRAWLINE
CALL COLUMNPRINT, 0, @"□%日付表示()%"

;ORIGINでは表示しない
IF !ORIGIN
	IF FLAG:周回数 > 1
		IF LANGUAGE == "ENG"
			CALL COLUMNPRINT, 0, @" Playthrough.{FLAG:周回数} "
		ELSE
			CALL COLUMNPRINT, 0, @" {FLAG:周回数}周目 "
		ENDIF
	ENDIF
	IF ゲームモード() == "段位認定"
		CALL GRADE, FLAG:選択段位, 1
	ELSE
		CALL COLUMNPRINT, 0, @"%ゲームモード表示()% "
	ENDIF
	CALL COLUMNPRINT, 0, ""
;ただしeraBEMANIのORIGINモードでは表示
ELSEIF ORIGIN && GAMEBASE_TITLE == "eraBEMANI+α"
	CALL COLUMNPRINT, 0, "MODE:Origin "
ENDIF
CALL COLUMNPRINTL, 0, ""

CALL COLUMNPRINT, 0, @"　%I18N("天気")%:"
IF GUI
	;夜で晴れなら月アイコン
	IF ターン == "夜" && 天気() == "晴れ"
		CALL COLUMNIMG, 0, 月齢画像(), 100, 100
	ELSE
		CALL COLUMNIMG, 0, 天気(FLAG:天気, 1), 100, 100
	ENDIF
ELSE
	CALL COLUMNPRINT, 0, @"%天気()%"
ENDIF
CALL COLUMNPRINTL, 0, @" %I18N("気温")%:%DIV10(FLAG:気温)%℃ \@ LANGUAGE == "JP" ? %月齢()%(月齢:%DIV100(月齢)%)#\@"

;段位認定のときのみ表示を変える
;ORIGINでは所持金、難易度は表示しない
IF !ORIGIN
	IF ゲームモード() != "段位認定"
		;所持金表示
		CALL COLUMNPRINT, 0, @"　%I18N("所持金")%:%TOOPTIONINT(MONEY)%＄"
		SIF 借金 > 0
			CALL COLUMNPRINT, 0, @" %I18N("借金")%:%TOOPTIONINT(借金)%＄"
		CALL COLUMNPRINT, 0, @" %I18N("昨日の所持金")%:%TOOPTIONINT(LASTMONEY)%＄"
		IF MONEY == LASTMONEY
			CALL COLUMNPRINT, 0, "→0 KEEP "
		ELSEIF MONEY > LASTMONEY
			CALL COLUMNPRINT, 0, @"↑%TOOPTIONINT(MONEY-LASTMONEY)% UP!", "coral"
		ELSEIF MONEY < LASTMONEY
			CALL COLUMNPRINT, 0, @"↓%TOOPTIONINT(LASTMONEY-MONEY)% DOWN...", "lightskyblue"
		ENDIF
		CALL COLUMNPRINTL, 0, ""

		;新婚生活中は非表示
		IF ゲームモード() != "新婚モード"
			;難易度表示
			CALL COLUMNPRINT, 0, @"　%I18N("難易度")%:%難易度()%", 難易度色()
			;奴隷人数表示
			CALL COLUMNPRINTL, 0, @"　%I18N("総キャラ数")%:{CHARANUM-1}\/%I18N("陥落済み")%:{陥落人数()}"
		ENDIF
	ELSE
		;所持金表示
		CALL COLUMNPRINT, 0, @"　%I18N("")%:%TOOPTIONINT(MONEY)%＄"
		SIF ストック > 0
			CALL COLUMNPRINT, 0, @" (%I18N("ストック")%:%TOOPTIONINT(ストック)%＄)"
		CALL COLUMNPRINTL, 0, ""

		;難易度表示
		CALL COLUMNPRINT, 0, @"　%I18N("難易度")%:%難易度()% ", 難易度色()
		;売却可能人数表示
		CALL COLUMNPRINTL, 0, @"%I18N("総キャラ数")%:{CHARANUM-1}\/%I18N("売却可能")%:{売却可能}"
		CALL COLUMNPRINT, 0, "　"
		CALL 段位ゲージ表示
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ENDIF

;奴隷の状態------------------------------------------------------------
IF TARGET > 0
	CALL COLUMNPRINTL, 0, ""
	IF ゲームモード() == "新婚モード"
		CALL COLUMNPRINTL, 0, @"□%I18N("同棲中")%"
		CALL COLUMNPRINT, 0, @"　%NAMEDISP(TARGET)%\@ TEQUIP:仮死薬 ? (仮死薬作用中) #\@\@ TEQUIP:睡眠 ? (睡眠薬作用中) #\@"
	ELSE
		IF LANGUAGE == "ENG"
			CALL COLUMNPRINT, 0, "□Training "
			FOR LCOUNT, 1, TARGET
				IF 調教可能(LCOUNT)
					CALL COLUMNPRINT, 0, "[10] Prev character "
					BREAK
				ENDIF
				SIF LCOUNT == TARGET || TARGET == 1
					CALL COLUMNPRINT, 0, @"%SPACESLENS("[10] Prev character ")%"
			NEXT
			FOR LCOUNT, TARGET+1, CHARANUM
				IF 調教可能(LCOUNT)
					CALL COLUMNPRINT, 0, "[19] Next character"
					BREAK
				ENDIF
			NEXT
			CALL COLUMNPRINTL, 0, ""
		ELSE
			CALL COLUMNPRINT, 0, @"□%I18N("調教中")% "
			別キャラあり = 0
			FOR LCOUNT, 1, TARGET
				IF 調教可能(LCOUNT) && (!OPTION:配属キャラを飛ばす || CSTR:LCOUNT:配属 == "") && !CFLAG:LCOUNT:監禁
					CALL COLUMNPRINT, 0, @"[10] %I18N("前のキャラ")% "
					別キャラあり = 1
					BREAK
				ENDIF
			NEXT
			SIF !別キャラあり
				CALL COLUMNPRINT, 0, @"%SPACESLENS(I18N("前のキャラ"))%"
			別キャラあり = 0
			FOR LCOUNT, TARGET+1, CHARANUM
				IF 調教可能(LCOUNT) && (!OPTION:配属キャラを飛ばす || CSTR:LCOUNT:配属 == "") && !CFLAG:LCOUNT:監禁
					CALL COLUMNPRINT, 0, @"[19] %I18N("次のキャラ")% "
					別キャラあり = 1
					BREAK
				ENDIF
			NEXT
			SIF !別キャラあり
				CALL COLUMNPRINT, 0, @"%SPACESLENS(I18N("次のキャラ"))%"
			CALL COLUMNPRINTL, 0, @"[15] %I18N("配属中のキャラを飛ばす")%(\@ OPTION:配属キャラを飛ばす ? %I18N("オン")% # %I18N("オフ")% \@)"
		ENDIF
		CALL COLUMNPRINT, 0, @"　No.{TARGET} %NAMEDISP(TARGET)% \@ TEQUIP:仮死薬 ? (%I18N("仮死薬作用中")%) #\@\@ TEQUIP:睡眠 ? (%I18N("睡眠薬作用中")%) #\@"
	ENDIF
	CALL COLUMNPRINT, 0, @"%性別表示(TARGET)% "
	SIF TALENT:ふたなり
		CALL COLUMNPRINT, 0, @"[%I18N("ふたなり")%]"
	SIF TALENT:アニマル
		CALL COLUMNPRINT, 0, @"[%I18N("獣")%]"
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF !TALENT:LCOUNT
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT
			CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "隷属", "崩壊", "淫核", "淫膣", "淫尻", "淫乳"
				CALL COLUMNPRINT, 0, @"【%I18N(TALENTNAME:LCOUNT, "TALENT")%】"
			CASE "妊娠"
				CALL COLUMNPRINT, 0, @"【%I18N("妊娠中")%】"
			CASE "リミットバースト"
				CALL COLUMNPRINT, 0, "【BURST】"
		ENDSELECT
	NEXT
	SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
		CALL COLUMNPRINT, 0, @"%I18N("依存度")%:{CFLAG:依存度} "
	IF GUI
		CALL COLUMNPRINT, 0, @"%I18N("ストレス値")%:%IMGTOTAG(BASEBAR("ストレス値", TARGET, 4))% "
	ELSE
		CALL COLUMNPRINT, 0, @"%I18N("ストレス値")%"
		SELECTCASE CFLAG:ストレス値
			CASE 0
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999 ", "gray"
			CASE 1 TO 99
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999 ", "lime"
			CASE 100 TO 299
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999 ", "yellow"
			CASE 300 TO 499
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999 ", "red"
			CASE IS >= 500
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999 ", "darkred"
		ENDSELECT
	ENDIF
	IF GROUPMATCH(CSTR:種族, "獣人", "獣")
		CALL COLUMNPRINT, 0, @"%I18N("発情")%:"
		IF GUI
			;26日周期で13日目がピーク
			SELECTCASE CFLAG:発情周期
				CASE 0 TO 6
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(発情度(TARGET), 100, 2, "white", "gray", @"{発情度(TARGET)}\%", "black"))% "
				CASE 7 TO 9
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(発情度(TARGET), 100, 2, "yellow", "gray", @"{発情度(TARGET)}\%", "black"))% "
				CASE 10 TO 12
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(発情度(TARGET), 100, 2, "lime", "gray", @"{発情度(TARGET)}\%", "black"))% "
				CASE 13
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(発情度(TARGET), 100, 2, "deepskyblue", "gray", "100%", "black"))% "
				CASE 14 TO 16
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(100-発情度(TARGET), 100, 2, "gray", "lime", @"{発情度(TARGET)}\%", "black"))% "
				CASE 17 TO 19
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(100-発情度(TARGET), 100, 2, "gray", "yellow", @"{発情度(TARGET)}\%", "black"))% "
				CASE 20 TO 25
					CALL COLUMNPRINT, 0, @"%IMGTOTAG(GBAR(100-発情度(TARGET), 100, 2, "gray", "white", @"{発情度(TARGET)}\%", "black"))% "
			ENDSELECT
		ELSE
			CALL COLUMNPRINT, 0, @"{発情度(TARGET)}\%"
		ENDIF
	ENDIF
	CALL COLUMNPRINTL, 0, ""
	
	ABLNAMELEN = LANGUAGE == "JP" ? 4 # 12
	IF GUI
		CALL COLUMNTOOLTIP, 0, @"　%I18N("従順", "ABL"), ABLNAMELEN%:", ABL_COMMENT("従順")
		CALL COLUMNIMG, 0, GBAR(ABL:従順, 10, 3, BARCOLOR:能力, "gray", @"{ABL:従順}", "black")
		CALL COLUMNTOOLTIP, 0, @"%I18N("欲望", "ABL"), ABLNAMELEN%:", ABL_COMMENT("欲望")
		CALL COLUMNIMG, 0, GBAR(ABL:欲望, 10, 3, BARCOLOR:能力, "gray", @"{ABL:欲望}", "black")
		CALL COLUMNTOOLTIP, 0, @"%I18N("技巧", "ABL"), ABLNAMELEN%:", ABL_COMMENT("技巧")
		CALL COLUMNIMG, 0, GBAR(ABL:技巧, 10, 3, BARCOLOR:能力, "gray", @"{ABL:技巧}", "black")
		CALL COLUMNPRINTL, 0, ""

		CALL COLUMNTOOLTIP, 0, @"　%I18N("Ｃ感"), ABLNAMELEN%:", ABL_COMMENT("Ｃ感覚")
		CALL COLUMNIMG, 0, GBAR(ABL:Ｃ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｃ感覚}", "black")
		IF VAGINA(TARGET)
			CALL COLUMNTOOLTIP, 0, @"%I18N("Ｖ感"), ABLNAMELEN%:", ABL_COMMENT("Ｖ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ｖ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｖ感覚}", "black")
		ENDIF
		CALL COLUMNTOOLTIP, 0, @"%I18N("Ａ感"), ABLNAMELEN%:", ABL_COMMENT("Ａ感覚")
		CALL COLUMNIMG, 0, GBAR(ABL:Ａ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ａ感覚}", "black")
		CALL COLUMNTOOLTIP, 0, @"%I18N("Ｂ感"), ABLNAMELEN%:", ABL_COMMENT("Ｂ感覚")
		CALL COLUMNIMG, 0, GBAR(ABL:Ｂ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｂ感覚}", "black")
		CALL COLUMNPRINTL, 0, ""
	ELSE
		CALL COLUMNTOOLTIP, 0, @"　%I18N("従順", "ABL"), ABLNAMELEN%:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順")
		CALL COLUMNTOOLTIP, 0, @"%I18N("欲望", "ABL"), ABLNAMELEN%:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望")
		CALL COLUMNTOOLTIPL, 0, @"%I18N("技巧", "ABL"), ABLNAMELEN%:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧")

		CALL COLUMNTOOLTIP, 0, @"　%I18N("Ｃ感"), ABLNAMELEN%:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚")
		SIF VAGINA(TARGET)
			CALL COLUMNTOOLTIP, 0, @"%I18N("Ｖ感"), ABLNAMELEN%:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚")
		CALL COLUMNTOOLTIP, 0, @"%I18N("Ａ感"), ABLNAMELEN%:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚")
		CALL COLUMNTOOLTIPL, 0, @"%I18N("Ｂ感"), ABLNAMELEN%:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚")
	ENDIF
	CALL COLUMNTOOLTIP, 0, @"　%I18N("苦痛刻印", "MARK"), ABLNAMELEN%:LV{MARK:苦痛刻印, 2, LEFT}", MARK_COMMENT("苦痛刻印")
	CALL COLUMNTOOLTIP, 0, @" %I18N("快楽刻印", "MARK"), ABLNAMELEN%:LV{MARK:快楽刻印, 2, LEFT}", MARK_COMMENT("快楽刻印")
	CALL COLUMNTOOLTIPL, 0, @" %I18N("屈服刻印", "MARK"), ABLNAMELEN%:LV{MARK:屈服刻印, 2, LEFT}", MARK_COMMENT("屈服刻印")

	IF GUI
		CALL COLUMNPRINT, 0, @"　%I18N("体力")%:"
		CALL COLUMNIMG, 0, BASEBAR("体力")
		CALL COLUMNPRINTL, 0, ""

		CALL COLUMNPRINT, 0, @"　%I18N("気力")%:"
		CALL COLUMNIMG, 0, BASEBAR("気力")
		CALL COLUMNPRINTL, 0, ""

		IF TALENT:魔法使い
			CALL COLUMNPRINT, 0, @"　%I18N("魔力")%:"
		CALL COLUMNIMG, 0, BASEBAR("魔力")
			CALL COLUMNPRINTL, 0, ""
		ENDIF

		IF MOD:SCAT
			CALL INITIALIZE_AMOUNT_FOR_DEFECATE, TARGET
			CALL INITIALIZE_AMOUNT_FOR_FECES, TARGET
			CALL COLUMNPRINT, 0, @"　%I18N("便意")%:"
			CALL COLUMNIMG, 0, BASEBAR("便意")
			CALL COLUMNPRINTL, 0, ""
			CALL COLUMNPRINT, 0, @"　%I18N("便量")%:"
			CALL COLUMNIMG, 0, BASEBAR("便量")
			CALL COLUMNPRINTL, 0, ""
		ENDIF
	ELSE
		CALL COLUMNPRINTL, 0, @"　%I18N("体力")%%BARBASE("体力")%"
		CALL COLUMNPRINTL, 0, @"　%I18N("気力")%%BARBASE("気力")%"
		SIF TALENT:魔法使い
			CALL COLUMNPRINTL, 0, @"　%I18N("魔力")%%BARBASE("魔力")%"

		IF MOD:SCAT
			CALL INITIALIZE_AMOUNT_FOR_DEFECATE, TARGET
			CALL INITIALIZE_AMOUNT_FOR_FECES, TARGET
			CALL COLUMNPRINTL, 0, @"　%I18N("便意")%%BARBASE("便意")%"
			CALL COLUMNPRINTL, 0, @"　%I18N("便量")%%BARBASE("便量")%"
		ENDIF
	ENDIF
ENDIF
CALL COLUMNPRINTL, 0, ""

;主人・助手の状態------------------------------------------------------------
SIF リソース:母乳
	CALL COLUMNPRINT, 0, @"　%I18N("保管済み母乳")%:{リソース:母乳}ml"
SIF リソース:精液
	CALL COLUMNPRINT, 0, @"　%I18N("保管済み精液")%:{リソース:精液}ml"
SIF リソース:血液
	CALL COLUMNPRINT, 0, @"　%I18N("保管済み血液")%:{リソース:血液}ml"
SIF リソース:粘液
	CALL COLUMNPRINT, 0, @"　%I18N("保管済み粘液")%:{リソース:粘液}ml"
SIF COLUMNLOG:0:(COLUMN:0:LINE) != ""
	CALL COLUMNPRINTL, 0, ""

CALL COLUMNPRINT, 0, @"　%I18N("主人")%:%NAMEDISP(MASTER)% %性別表示(MASTER)% "
SIF TALENT:MASTER:ふたなり
	CALL COLUMNPRINT, 0, @"[%I18N("ふたなり", "TALENT")%]"
CALL COLUMNPRINTL, 0, ""
IF TALENT:MASTER:魔法使い
	IF GUI
		CALL COLUMNPRINT, 0, @"　%I18N("魔力")%:"
		CALL COLUMNIMG, 0, BASEBAR("魔力", MASTER)
		CALL COLUMNPRINTL, 0, ""
	ELSE
		CALL COLUMNPRINTL, 0, @"　%I18N("魔力")%%BARBASE("魔力", MASTER)%"
	ENDIF
ENDIF

IF ASSI >= 0
	IF TALENT:ASSI:アニマル
		CALL COLUMNPRINT, 0, @"　%I18N("ペット")%:%NAMEDISP(ASSI)%"
	ELSE
		CALL COLUMNPRINT, 0, @"　%I18N("助手")%:%NAMEDISP(ASSI)%"
	ENDIF
	CALL COLUMNPRINT, 0, @"%性別表示(ASSI)% "
	SIF TALENT:ASSI:ふたなり
		CALL COLUMNPRINT, 0, @"[%I18N("ふたなり", "TALENT")%]"
	SIF TALENT:ASSI:アニマル
		CALL COLUMNPRINT, 0, @"[%I18N("獣")%]"
	CALL COLUMNPRINTL, 0, ""
	IF TALENT:ASSI:魔法使い
		IF GUI
			CALL COLUMNPRINT, 0, @"　%I18N("魔力")%:"
			CALL COLUMNIMG, 0, BASEBAR("魔力", ASSI)
			CALL COLUMNPRINTL, 0, ""
		ELSE
			CALL COLUMNPRINTL, 0, @"　%I18N("魔力")%%BARBASE("魔力", ASSI)%"
		ENDIF
	ENDIF
ENDIF

CALL COLUMNDRAWLINE, 0
IF GC:物件 && ゲームモード() != "通常モード"
	CALL COLUMNPRINTL, 0, @"□%GCS:店名%%I18N("の状況")%"
	IF GC:営業中 > 0
		CALL COLUMNPRINT, 0, @"　%I18N("営業中")% "
		CALL COLUMNPRINTL, 0, @"[20] - %I18N("一時休業する")%"
		店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
		SIF 店長 == -1
			店長 = 0
		CALL COLUMNPRINTL, 0, @"　%I18N("前営業日の収入")%:%TOOPTIONINT(GC:前日の収入)%＄ %I18N("店長")%:%NAMEDISP(店長)%"
	ELSE
		CALL COLUMNPRINT, 0, @"　%I18N("休業中")% "
		CALL COLUMNPRINTL, 0, @"[20] - %I18N("営業を開始する")%"
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

IF ITEM:風俗店営業許可証 && ゲームモード() != "通常モード"
	CALL COLUMNPRINTL, 0, @"□%BRS:店名%%I18N("の状況")%"
	IF BR:営業中 > 0
		CALL COLUMNPRINT, 0, @"　%I18N("営業中")% "
		CALL COLUMNPRINTL, 0, @"[21] - %I18N("一時休業する")%"
		CALL COLUMNPRINTL, 0, @"　%I18N("前営業日の収入")%:%TOOPTIONINT(BR:前日の収入)%＄"
	ELSE
		CALL COLUMNPRINT, 0, @"　%I18N("休業中")% "
		CALL COLUMNPRINTL, 0, @"[21] - %I18N("営業を開始する")%"
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

IF INNS:業種 != "" && ゲームモード() != "通常モード"
	CALL COLUMNPRINTL, 0, @"□%INNS:店名%%I18N("の状況")%"
	IF INN:営業中 > 0
		CALL COLUMNPRINT, 0, @"　%I18N("営業中")% "
		CALL COLUMNPRINTL, 0, @"[22] - %I18N("一時休業する")%"
		CALL COLUMNPRINTL, 0, @"　%I18N("前営業日の収入")%:%TOOPTIONINT(飲食店売上)%＄"
	ELSE
		CALL COLUMNPRINT, 0, @"　%I18N("休業中")% "
		CALL COLUMNPRINTL, 0, @"[22] - %I18N("営業を開始する")%"
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

CALL SHOW_MODS, 1

誕生日キャラ = 0
;グリムキャラが全員誕生日2月21日なのでUI破壊対策
IF MONTH == 2 && DAYS == 21
	CALL COLUMNPRINTL, 0, @"　%I18N("今日はグリムキャラ（QMA）の誕生日です")%"
	誕生日キャラ++
ENDIF
FOR LCOUNT, 0, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	IF CSVCFLAG(LCOUNT, GETNUM(CFLAG, "誕生日"))/100 == MONTH && CSVCFLAG(LCOUNT, 0)%100 == DAYS && !CSVCFLAG(LCOUNT, GETNUM(CFLAG, "グリム"))
		CALL COLUMNPRINTL, 0, @"　%I18N("今日は|0|の誕生日です", , CSVNAMEDISP(LCOUNT))%"
		誕生日キャラ++
	ENDIF
NEXT
IF 誕生日キャラ
	CALL COLUMNPRINTL, 0, @"　%I18N("ハッピーバースデー！")%"
	CALL COLUMNDRAWLINE, 0
ENDIF
;IF LANGUAGE == "ENG"
;	CALL COLUMNPRINTL, 0, @"□ITEM [710] \@ アイテム非表示 ? Show # Hide \@"
;ELSE
;	CALL COLUMNPRINTL, 0, @"□所持アイテム [710] \@ アイテム非表示 ? 表示する # 隠す \@"
;ENDIF
;SIF !アイテム非表示
;	CALL ITEMPRINT
;CALL COLUMNDRAWLINE, 0

SELECTCASE SEX(TARGET)
	CASE 1
		LCALL = him
	CASE 2
		LCALL = her
ENDSELECT

CALL COLUMNPRINT, 0, "　"

;TARGETがいれば表示
;九尾狐夜行では夜だけ調教可能
IF 受験段位名 == "九尾狐夜行" && ターン == "昼"
	CALL COLUMNTOOLTIP, 0, @"[-] %I18N("調教開始")% ", @"%I18N("九尾狐夜行に挑戦中は夜のみ調教可能です")%", "gray"
ELSEIF TARGET > 0
	SELECTCASE FALLTYPE(TARGET)
		CASE 1
			CALL COLUMNTOOLTIP, 0, @"[0] %I18N("愛を育む")% ", @"%I18N("|0|と幸せな時間を過ごします", , CALLNAMEDISP(TARGET))%"
		CASE 2
			CALL COLUMNTOOLTIP, 0, @"[0] %I18N("乱れ合う", , LCALL)% ", @"%I18N("|0|と互いの欲望を満たし合います", , CALLNAMEDISP(TARGET))%"
		CASE 3
			CALL COLUMNTOOLTIP, 0, @"[0] %I18N("躾をする")% ", @"%I18N("|0|に権威を示します", , CALLNAMEDISP(TARGET))%"
		CASEELSE
			CALL COLUMNTOOLTIP, 0, @"[0] %I18N("調教開始")% ", @"%I18N("|0|を調教します", , CALLNAMEDISP(TARGET))%"
	ENDSELECT
ELSE
	IF CHARANUM == 1 && ゲームモード() == "段位認定"
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("調教開始")% ", @"%I18N("調教可能なキャラが居ません")%", "gray"
	ELSEIF ゲームモード() == "新婚モード"
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("愛を育む")% ", @"%I18N("キャラクター→対象変更 から対象を選択してください")%", "gray"
	ELSEIF 調教可能
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("調教開始")% ", @"%I18N("キャラクター→奴隷変更 から調教対象を選択してください")%", "gray"
	ELSE
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("調教開始")% ", @"%I18N("キャラ・アイテム購入→奴隷購入 からキャラを購入してください")%", "gray"
	ENDIF
ENDIF
;無条件で表示
CALL COLUMNTOOLTIP, 0, @"[1] %I18N("休憩")% ", @"%I18N("|0|。体力、気力、ストレスが回復します", , @"\@ ターン == "昼" ? 夜まで休憩します # 翌日まで休憩します \@")%"

;STRATEGYモード、PFREEモードなら表示
SELECTCASE ゲームモード()
	CASE "経営モード", "フリーモード"
		;パートナーに話しかける口上が実装されていれば表示
		IF EXISTFUNCTION(@"TALK_PARTNER_%CSVNAMERPS(GETNO(パートナー))%")
			SIF OPTION:口上表示 && !CFLAG:GETCHARA(GETNO(パートナー)):口上非表示
				CALL COLUMNTOOLTIP, 0, @"[2] %I18N("経営相談")% ", @"%I18N("|0|に経営の相談をします", , CSVCALLNAMEF(GETNO(パートナー)))%"
		ENDIF
ENDSELECT

;新婚モードの一転攻勢
IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
	IF TALENT:FINDCHARA(CFLAG:嫁, 1):妊娠 || TALENT:FINDCHARA(CFLAG:嫁, 1):育児中
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("一転攻勢")% ", @"%I18N("|0|が身篭っているため、できません", ,CALLNAMEDISP(FINDCHARA(CFLAG:嫁, 1)))%", "gray"
	ELSEIF TALENT:MASTER:妊娠 || TALENT:MASTER:育児中
		CALL COLUMNTOOLTIP, 0, @"[-] %I18N("一転攻勢")% ", @"%I18N("|0|が身篭っているため、できません", ,CALLNAMEDISP(MASTER))%", "gray"
	ELSEIF !CFLAG:FINDCHARA(CFLAG:嫁, 1):キャラロスト
		CALL COLUMNTOOLTIP, 0, @"[3] %I18N("一転攻勢")% ", @"%I18N("|0|と|1|の受け攻めを変えます", ,CALLNAMEDISP(MASTER), CALLNAMEDISP(FINDCHARA(CFLAG:嫁, 1)))%"
	ENDIF
ENDIF

;主人のスキルアップ
CALL COLUMNTOOLTIP, 0, "[4] SKILL UP ", @"%I18N("|0|のスキルツリーを表示します", , CALLNAMEDISP(MASTER))%"
CALL COLUMNPRINTL, 0

;所持金が条件に達していて、ゲームモードがSTANDARDもしくはSTRATEGYなら表示
SELECTCASE ゲームモード()
	;STANDARD
	CASE "通常モード"
		IF MONEY < DIG_SEP("1,000,000")
			CALL COLUMNTOOLTIP, 0, @"　[-] %I18N("納金")% ", @"%I18N("|0|の所持金が必要です", , "1,000,000")%", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, @"　[5] %I18N("納金")% ", @"%I18N("|0|の所持金を納め、ゲームをクリアします", , "1,000,000")%"
		ENDIF
	;STRATEGY
	CASE "経営モード"
		IF MONEY < DIG_SEP("100,000,000")
			CALL COLUMNTOOLTIP, 0, @"　[-] %I18N("納金")% ", @"%I18N("|0|の所持金が必要です", , "100,000,000")%", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, @"　[5] %I18N("納金")% ", @"%I18N("|0|の所持金を納め、ゲームをクリアします", , "100,000,000")%"
		ENDIF
	;新婚
	CASE "新婚モード"
		CALL COLUMNTOOLTIP, 0, @"　[-] %I18N("納金")% ", @"%I18N("新婚モードでは納金の必要はありません")%", "gray"
	;段位認定
	CASE "段位認定"
		CALL COLUMNTOOLTIP, 0, @"　[-] %I18N("納金")% ", @"%I18N("段位認定では納金の必要はありません")%", "gray"
	;PFREE
	CASE "フリーモード"
		CALL COLUMNTOOLTIP, 0, @"　[-] %I18N("納金")% ", @"%I18N("PREMIUM FREE MODEでは納金の必要はありません")%", "gray"
ENDSELECT

;周回可能なら表示
IF FLAG:周回可能 && FLAG:難易度 != -1
	CALL COLUMNTOOLTIP, 0, @"[6] %I18N("周回")% ", @"%I18N("この周を終了し、引き継いでニューゲームを開始します")%"
ELSE
	SELECTCASE ゲームモード()
		CASE "新婚モード"
			CALL COLUMNTOOLTIP, 0, @"[-] %I18N("周回")% ", @"%I18N("新婚モードでは周回出来ません")%", "gray"
		CASE "段位認定"
			CALL COLUMNTOOLTIP, 0, @"[-] %I18N("周回")% ", @"%I18N("段位認定では周回出来ません")%", "gray"
		CASE "フリーモード"
			CALL COLUMNTOOLTIP, 0, @"[-] %I18N("周回")% ", @"%I18N("PREMIUM FREE MODEでは周回出来ません")%", "gray"
		CASEELSE
			IF FLAG:難易度 == -1
				CALL COLUMNTOOLTIP, 0, @"[-] %I18N("周回")% ", @"%I18N("難易度|0|では周回出来ません", , 難易度())%", "gray"
			ELSE
				CALL COLUMNTOOLTIP, 0, @"[-] %I18N("周回")% ", @"%I18N("ゲームをクリアしないと周回は出来ません", )%", "gray"
			ENDIF
	ENDSELECT
ENDIF
CALL COLUMNPRINTL, 0, ""
SIF TARGET > 0 && 陥落(TARGET)
	CALL COLUMNPRINTL, 0, @"　[9] %I18N("|0|の奉仕行為を抑制する(現在:|1|)", , CALLNAMEDISP(TARGET), @"\@ CFLAG:奉仕イベント抑制 ? %I18N("抑制中")% # %I18N("非抑制")% \@")%"

;PALAMのオーバーフロー警告をする
VARLINE = LINECOUNT
FOR LCOUNT, 0, 100
	SIF PALAMOVERFLOW:LCOUNT
		PRINTFORML 　前回の調教で%PALAMNAME:LCOUNT%がオーバーフローしました
	SIF LCOUNT == 99 && VARLINE != LINECOUNT
		PRINTW 　正常に続行するには、前回の調教対象にアイテム「リミッター制御」を使用してください
NEXT
VARSET PALAMOVERFLOW

@SHOP_メイン_INPUT
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 伴侶
SELECTCASE CRESULT:1
	;調教開始
	CASE 0
		CALL 調教開始
	;休憩
	CASE 1
		;休憩フラグを立てる
		FLAG:休憩フラグ = 1
		CALL プレイ称号計算, "休憩"
		CALL ターン終了
	;経営相談
	CASE 2
		LTARGET = TARGET
		TARGET = GETCHARANAME(パートナー)
		TRYCALLFORM TALK_PARTNER_%CSVNAMERPS(GETNO(パートナー))%
		TARGET = LTARGET
	;一転攻勢
	CASE 3
		伴侶 = FINDCHARA(CFLAG:嫁, 1)
		TALENT:MASTER:親愛 = 1
		TALENT:伴侶:親愛 = 0
		CFLAG:MASTER:嫁 = 1
		CFLAG:伴侶:嫁 = 0
		SWAPCHARA 伴侶, MASTER
		SIF CHARANAMEF(伴侶) == "シカトリス"
			CALL 実績解除, "ドイツ人を右に"
	;スキルツリー
	CASE 4
		CALL プレイ称号計算, "スキル"
		CALL スキルツリー
	;納金
	CASE 5
		SELECTCASE ゲームモード()
			CASE "通常モード"
				;実績:1周目のゲームを20日以内にクリアする
				SIF FLAG:周回数 == 1 && DAY <= 20
					CALL 実績解除, "SPEED DEMON"
				CALL ENDING_CHECK
			CASE "経営モード"
				;実績:1周目のゲームを20日以内にクリアする
				SIF FLAG:周回数 == 1 && DAY <= 20
					CALL 実績解除, "SPEED DEMON"
				;実績:キャラを一人も購入せずにパートナーだけでSTRATEGYモードをクリアする
				SIF キャラ未購入
					CALL 実績解除, "もう全部あいつ一人でいいんじゃないかな"
				;実績:芸能プロダクションの収入のみでSTRATEGYモードをクリアする
				SIF バンめし
					CALL 実績解除, "他人がバンドで稼いだ金でめしが食いたい"
				CALL ENDING_CHECK
		ENDSELECT
	;周回
	CASE 6
		CALL SUCCESSION
	CASE 9
		INVERTBIT CFLAG:奉仕イベント抑制, 0
		CALL EVENT_CHECK, "奉仕イベント抑制"
	;キャラ変更
	CASE 10, 19
		LTARGET = TARGET
		LRESULT = CRESULT:1
		DO
			SELECTCASE LRESULT
				CASE 10
					LTARGET--
				CASE 19
					LTARGET++
			ENDSELECT
			;調教可能かどうか確認してからTARGETにアサインする 不可能だった場合は次のキャラに飛ぶ
			IF 調教可能(LTARGET) && (!OPTION:配属キャラを飛ばす || CSTR:LTARGET:配属 == "") && !CFLAG:LTARGET:監禁
				CALL ASSIGN_TARGET, LTARGET
				BREAK
			ENDIF
		LOOP INRANGE(LTARGET, 1, CHARANUM-2)
	CASE 15
		INVERTBIT OPTION:配属キャラを飛ばす, 0
	;ゲーセン営業変更
	CASE 20
		IF GC:営業中 > 0
			;PRINTFORMW %GCS:店名%の経営を一時中断しました
			GC:営業中 = -1
		ELSE
			;PRINTFORMW %GCS:店名%の経営を開始しました
			GC:営業中 = DAY
		ENDIF
	;娼館営業変更
	CASE 21
		IF BR:営業中 > 0
			;PRINTFORMW %BRS:店名%の経営を一時中断しました
			BR:営業中 = -1
		ELSE
			;PRINTFORMW %BRS:店名%の経営を開始しました
			BR:営業中 = DAY
		ENDIF
	;飲食店営業変更
	CASE 22
		IF INN:営業中 > 0
			;PRINTFORMW %INNS:店名%の経営を一時中断しました
			INN:営業中 = -1
		ELSE
			;PRINTFORMW %INNS:店名%の経営を開始しました
			INN:営業中 = DAY
		ENDIF
	;コナミコマンド
	CASE 573
		PRINTW このコマンドはもう意味無いよ
		CALL 実績解除, "いつのバージョンだよ"
	;アイテム表示の切り替え
	;CASE 710
	;	INVERTBIT アイテム非表示, 0
ENDSELECT

;旧@LIFE_LIST
@SHOP_キャラクター
CALL 所持キャラ一覧

@SHOP_キャラクター_INPUT
CALL 所持キャラ一覧_INPUT

@SHOP_施設・経営

IF !GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
	CALL COLUMNPRINTL, 0, "□経営"
	IF ITEM:風俗店営業許可証
		CALL COLUMNPRINTL, 0, "　[ 0] - ゲームセンター経営"
		CALL COLUMNPRINTL, 0, "　[ 1] - 娼館経営"
	ELSE
		CALL COLUMNPRINTL, 0, "　[--] - ゲームセンター経営（風俗店営業許可証が必要です）", "gray"
		CALL COLUMNPRINTL, 0, "　[--] - 娼館経営（風俗店営業許可証が必要です）", "gray"
		RESETCOLOR
	ENDIF
	;PRINTPLAIN 　[3] - ゲーム開発（工事中）
	;PRINTL 
	CALL COLUMNPRINTL, 0, "　[ 2] - 芸能プロダクション運営"
	CALL COLUMNPRINTL, 0, "　[ 3] - 飲食店・ホテル経営"
	IF ITEM:事業開発オフィス
		CALL COLUMNPRINTL, 0, "　[ 4] - 出版社経営"
	ELSE
		CALL COLUMNPRINTL, 0, "　[--] - 出版社経営（アイテム[事業開発オフィス]が必要です）", "gray"
	ENDIF
	IF MOD:STS
		IF ITEM:博物館
			CALL COLUMNPRINTL, 0, "　[ 5] - 博物館経営"
		ELSE
			CALL COLUMNPRINTL, 0, "　[--] - 博物館経営（アイテム[博物館]が必要です）", "gray"
		ENDIF
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

CALL COLUMNPRINTL, 0, "□施設・設備"
IF ITEM:医務室
	CALL COLUMNPRINTL, 0, "　[10] - 医務室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 医務室（アイテム[医務室]が必要です）", "gray"
ENDIF
IF ITEM:実験室
	CALL COLUMNPRINTL, 0, "　[11] - 実験室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 実験室（アイテム[実験室]が必要です）", "gray"
ENDIF
IF ITEM:図書室
	CALL COLUMNPRINTL, 0, "　[12] - 図書室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 図書室（アイテム[図書室]が必要です）", "gray"
ENDIF
;IF ITEM:音楽室
;	CALL COLUMNPRINTL, 0, "[4] - 音楽室
;ELSE
;	CALL COLUMNPRINTL, 0, "[-] - 音楽室
;ENDIF

CALL COLUMNPRINTL, 0, "　[13] - 調理室"
CALL COLUMNPRINTL, 0, "　[14] - 農産物の管理"
CALL COLUMNPRINTL, 0, "　[15] - 畜産関連の管理"
CALL COLUMNPRINTL, 0, "　[16] - 監禁室"
SIF MOD:STS && CMATCH(CFLAG:死亡, 1) >= 0
	CALL COLUMNPRINTL, 0, "　[17] - 霊安室"

@SHOP_施設・経営_INPUT
SELECTCASE CRESULT:1
	CASE 0
		CALL プレイ称号計算, "ゲーセン経営"
		CALL STRATEGY_GC
	CASE 1
		CALL プレイ称号計算, "娼館経営"
		CALL STRATEGY_BROTHEL
	CASE 2
		CALL プレイ称号計算, "芸プロ運営"
		CALL STRATEGY_IDOL
		;返り値が1ならTURNENDする
		SIF RESULT == 1
			CALL ターン終了
	CASE 3
		CALL 飲食店経営
	CASE 4
		CALL 出版社経営
	CASE 5
		CALL プレイ称号計算, "博物館"
		CALL 博物館
		;返り値が1ならTURNENDする
		IF RESULT == 1
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	CASE 10
		CALL プレイ称号計算, "医務室"
		CALL 医務室
	CASE 11
		CALL プレイ称号計算, "実験室"
		CALL 実験室
	CASE 12
		CALL プレイ称号計算, "図書室"
		CALL 図書室
	CASE 13
		CALL プレイ称号計算, "調理室"
		CALL 調理室
		;返り値が1ならTURNENDする
		IF RESULT == 1
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	CASE 14
		CALL プレイ称号計算, "農業"
		CALL 農産物
		CALL 献立条件確認
	CASE 15
		CALL プレイ称号計算, "畜産業"
		CALL 畜産
		CALL 献立条件確認
	CASE 16
		CALL 監禁室
	CASE 17
		CALL 霊安室
ENDSELECT

@SHOP_キャラ・アイテム購入
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 売却可能

;売却可能キャラ数の確認
FOR LCOUNT, 1, CHARANUM
	SIF 売却可能(LCOUNT)
		売却可能++
NEXT

CALL COLUMNPRINTL, 0, @"□アイテム一覧 \@ !ORIGIN ? 所持金:%TOOPTIONINT(MONEY)%＄#\@"
CALL ITEMPRINT, 1
CALL COLUMNDRAWLINE, 0
CALL COLUMNPRINT, 0, "□アイテム購入 "
IF ゲームモード() == "段位認定"
	CALL COLUMNPRINTL, 0, "　[0] アイテムを購入する"
ELSE
	CALL COLUMNPRINT, 0, "[-1] 調教道具 ", @"\@ SHOP_アイテム分類 == "調教道具" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-2] 消耗品 ", @"\@ SHOP_アイテム分類 == "消耗品" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-3] 素質アイテム ", @"\@ SHOP_アイテム分類 == "素質アイテム" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-4] 薬品類 ", @"\@ SHOP_アイテム分類 == "薬品類" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-5] 施設 ", @"\@ SHOP_アイテム分類 == "施設" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-6] 本 ", @"\@ SHOP_アイテム分類 == "本" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-7] すべて ", @"\@ SHOP_アイテム分類 == "すべて" ? deepskyblue#\@"
	CALL COLUMNPRINTL, 0, ""

	CALL SHOPITEM_LIST, SHOP_アイテム分類, 0, 1
ENDIF

;段位認定、新婚モード以外で表示
;ORIGINでは全キャラ無条件解禁なので購入もできない
IF LANGUAGE == "ENG"
	;段位認定、新婚モード以外で表示
	IF !ORIGIN
		CALL COLUMNDRAWLINE, 0
		IF ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[----] - Buy Slaves ", "段位認定では奴隷を購入することができません", "gray"
		ELSEIF ゲームモード() != "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[2000] - Buy Slaves ", "調教するための奴隷を購入します"
		ENDIF
		;売却できるキャラがいれば表示
		IF !売却禁止 && ゲームモード() != "新婚モード"
			IF 売却可能
				CALL COLUMNTOOLTIP, 0, "　[2001] - Sell Slaves ", "調教で仕上がった奴隷を売却します"
			ELSE
				CALL COLUMNTOOLTIP, 0, "　[----] - Sell Slaves ", "売却可能な奴隷が居ません", "gray"
			ENDIF
		ENDIF
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ELSE
	IF !ORIGIN
		CALL COLUMNDRAWLINE, 0
		IF ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[----] - 奴隷購入 ", "段位認定では奴隷を購入することができません", "gray"
		ELSEIF ゲームモード() != "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[2000] - 奴隷購入 ", "調教するための奴隷を購入します"
		ENDIF
		;売却できるキャラがいれば表示
		IF !売却禁止 && ゲームモード() != "新婚モード"
			IF 売却可能
				CALL COLUMNTOOLTIP, 0, "　[2001] - 売却 ", "調教で仕上がった奴隷を売却します"
			ELSEIF ゲームモード() != "新婚モード"
				CALL COLUMNTOOLTIP, 0, "　[----] - 売却 ", "売却可能な奴隷が居ません", "gray"
			ENDIF
		ENDIF
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ENDIF

@SHOP_キャラ・アイテム購入_INPUT
#DIMS DYNAMIC 購入アイテム分類
SELECTCASE CRESULT:1
	CASE -1
		SHOP_アイテム分類 = 調教道具
	CASE -2
		SHOP_アイテム分類 = 消耗品
	CASE -3
		SHOP_アイテム分類 = 素質アイテム
	CASE -4
		SHOP_アイテム分類 = 薬品類
	CASE -5
		SHOP_アイテム分類 = 施設
	CASE -6
		SHOP_アイテム分類 = 本
	CASE -7
		SHOP_アイテム分類 = すべて
	CASE 2000
		CALL TUTORIAL, "奴隷購入"
		CALL CHARA_BUY
	CASE 2001
		CALL CHARA_SELL
	CASE 0 TO VARSIZE("ITEM")-1
		IF ゲームモード() == "段位認定" && CRESULT:1 == 0
			CALL GORILLA_ITEM
			RETURN
		ENDIF
		RESULT = CRESULT:1
		IF ITEMPRICE:RESULT > MONEY && !ORIGIN
			PRINTW 所持金が足りません
		ELSE
			;アイテム分類を名前で渡す
			SELECTCASE RESULT
				CASE 0 TO 199
					購入アイテム分類 = 買い切り
				CASE 200 TO 299
					購入アイテム分類 = 複数購入可
				;CASE 200 TO 249
				;	購入アイテム分類 = 素質アイテム
				CASEELSE
					購入アイテム分類 = 薬品等
			ENDSELECT
			;以下特定のアイテムの分類を変えるときに使う
			SELECTCASE ITEMNAME:RESULT
				CASE "ロジカルダッシュ"
					購入アイテム分類 = 買い切り
			ENDSELECT
			CALL ITEMBUY_DECIDE, ITEMNAME:RESULT, 購入アイテム分類
		ENDIF
ENDSELECT

@SHOP_データベース
IF LANGUAGE == "ENG"
	;ビデオ所持していれば表示
	SIF FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1) > 0
		CALL COLUMNTOOLTIPL, 0, "　[0] - VIDEO", "所持しているビデオの一覧を表示します"
	CALL COLUMNTOOLTIPL, 0, "　[4] - MANUAL", "ゲーム内マニュアルを表示します"
ELSE
	;ビデオ所持していれば表示
	SIF FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1) > 0
		CALL COLUMNTOOLTIPL, 0, "　[0] - 所持ビデオ一覧", "所持しているビデオの一覧を表示します"
	;アルバム
	CALL COLUMNTOOLTIPL, 0, "　[1] - アルバム", "撮影、保管した写真を表示します"
	;部屋ログあれば表示
	SIF 部屋ログ != ""
		CALL COLUMNTOOLTIPL, 0, "　[2] - 部屋ログ", "居住部屋で起きた出来事を確認します"
	CALL COLUMNTOOLTIPL, 0, "　[3] - プレイログ", "プレイログ及び現金出納帳を確認します"
	CALL COLUMNTOOLTIPL, 0, "　[4] - ゲームマニュアル", "ゲーム内マニュアルを表示します"
ENDIF

@SHOP_データベース_INPUT
SELECTCASE CRESULT:1
	;ビデオ一覧
	CASE 0
		CALL 所持ビデオ一覧
	;アルバム
	CASE 1
		CALL 所持写真一覧
	;部屋ログ
	CASE 2
		CALL 部屋ログ表示
	;プレイログ
	CASE 3
		CALL プレイログ閲覧
	;マニュアル
	CASE 4
		CALL GAME_MANUAL
ENDSELECT

@SHOP_その他
IF LANGUAGE == "ENG"
	;魔法
	IF 詠唱可能()
		CALL COLUMNTOOLTIPL, 0, "　[0] - Magic Chant", "魔法を使用します"
	ELSE
		CALL COLUMNTOOLTIPL, 0, "　[-] - Magic Chant", "魔法を使用するには主人、助手、もしくは調教対象が魔法使いである必要があります", "gray"
	ENDIF
	;今月号のパズル雑誌持ってれば表示
	SIF ITEM:ロジカルダッシュ == YEAR*100+MONTH
		CALL COLUMNTOOLTIPL, 0, "　[1] - Puzzle", "今月号のパズル雑誌で遊びます"
	;経営、PFREEなら表示
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[2] - Casino", "カジノに行ってギャンブルをします"
	;裏カジノ行ければ表示
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && FLAG:裏カジノ && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[3] - Darkness Casino", "高レートの裏カジノに行ってギャンブルをします"
	;無条件表示（暫定）
	CALL COLUMNTOOLTIPL, 0, "[4] - Redesign", "家の模様替えを行います"

	;変化できるキャラが居れば表示
	SIF CMATCH(TALENT:変化, 1)
		CALL COLUMNTOOLTIPL, 0, "　[5] - Transform", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます"

	;新婚モード以外で表示
	IF !GROUPMATCH(ゲームモード(), "新婚モード", "段位認定")
		;奴隷が一人でもいれば表示
		IF CHARANUM > 1
			CALL COLUMNTOOLTIPL, 0, "　[-] - Ritual", "眷属の儀、もしくは印の儀を行います"
			CALL COLUMNPRINTL, 0, "　　[10] - 眷属の儀"
			CALL COLUMNPRINTL, 0, "　　[11] - 印の儀"
			CALL COLUMNPRINTL, 0, "　　[12] - 幽体離脱・憑依の儀"
			SIF MOD:STS
				CALL COLUMNPRINTL, 0, "　　[13] - カニバリズム"
			;#;CALL COLUMNPRINTL, 0, "　　[14] - 装飾の儀"
		ELSE
			CALL COLUMNTOOLTIPL, 0, "　[-] - Ritual", "キャラ・アイテム購入→奴隷購入 からキャラを購入してください", "gray"
		ENDIF
	ENDIF
ELSE
	;魔法
	IF 詠唱可能()
		CALL COLUMNTOOLTIPL, 0, "　[0] - 魔法詠唱", "魔法を使用します"
	ELSE
		CALL COLUMNTOOLTIPL, 0, "　[-] - 魔法詠唱", "魔法を使用するには主人、助手、もしくは調教対象が魔法使いである必要があります",  "gray"
	ENDIF
	;今月号のパズル雑誌持ってれば表示
	SIF ITEM:ロジカルダッシュ == YEAR*100+MONTH
		CALL COLUMNTOOLTIPL, 0, "　[1] - パズル", "今月号のパズル雑誌で遊びます"
	;経営、PFREEなら表示
	;ORIGINでは金の概念が無いので使えない
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[2] - カジノ", "カジノに行ってギャンブルをします"

	;裏カジノ行ければ表示 同じくORIGINではry
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && FLAG:裏カジノ && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[3] - 裏カジノ", "高レートの裏カジノに行ってギャンブルをします"
	;スマホ版では使えない
	SIF !SP
		CALL COLUMNTOOLTIPL, 0, "　[4] - 模様替え", "家の模様替えを行います"
	;変化できるキャラが居れば表示
	SIF 変化可能()
		CALL COLUMNTOOLTIPL, 0, "　[5] - 変化", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます"

	;新婚モードと段位認定以外で表示
	IF !GROUPMATCH(ゲームモード(), "新婚モード", "段位認定")
		;奴隷が一人でもいれば表示
		IF CHARANUM > 1
			CALL COLUMNTOOLTIPL, 0, "　[-] - 儀式", "眷属の儀、もしくは印の儀を行います"
			CALL COLUMNPRINTL, 0, "　　[10] - 眷属の儀"
			CALL COLUMNPRINTL, 0, "　　[11] - 印の儀"
			CALL COLUMNPRINTL, 0, "　　[12] - 幽体離脱・憑依の儀"
			SIF MOD:STS
				CALL COLUMNPRINTL, 0, "　　[13] - カニバリズム"
			CALL COLUMNPRINTL, 0, "　　[14] - 装飾の儀"
		ELSE
			CALL COLUMNTOOLTIPL, 0, "　[-] - 儀式", "アイテム・キャラ購入→奴隷購入 からキャラを購入してください", "gray"
		ENDIF
	;新婚モードでは眷属の儀だけ使用可能
	ELSEIF ゲームモード() == "新婚モード" && CHARANUM > 1
		CALL COLUMNPRINTL, 0, "　　[10] - 眷属の儀"
	ENDIF
ENDIF

;デバッグモードなら表示
;#;CALL COLUMNPRINTL, 0, "　[997] - 乱キャラ"
;#;CALL COLUMNPRINTL, 0, "　[998] - デバッグ"

@SHOP_その他_INPUT

SELECTCASE CRESULT:1
	;魔法詠唱
	CASE 0
		CALL SPELLCAST
		;魔法を詠唱したならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "魔法詠唱"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	;パズル
	CASE 1
		CALL PUZZLE
	;カジノ
	CASE 2
		CALL GAMBLE, 1
		;ギャンブルしたならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "ギャンブル"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	;裏カジノ
	CASE 3
		CALL GAMBLE, 2
		;ギャンブルしたならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "ギャンブル"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	;模様替え
	CASE 4
		CALL プレイ称号計算, "マップ"
		CALL 模様替え
	;変化
	CASE 5
		CALL 変化
	;儀式
	CASE 10
		CALL FAMILIAR
		;儀を行った場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "眷属"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	CASE 11
		CALL RITUAL_OF_SIGN
		;儀を行った場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "印"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	CASE 12
		CALL SPECTRAL
	CASE 13
		CALL CANNIBALISM
		;食事をした場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "カニバリスト"
			FLAG:休憩フラグ = 1
			CALL ターン終了
		ENDIF
	CASE 14
		CALL RITUAL_OF_ACCESSORY
	;デバッグ用
	;#;CASE 997
	;#;	CALL ADD_ランダムキャラ, 2, -1
	;#;CASE 998
	;#;	CALL DEBUG
ENDSELECT


;USERSAVEの流用
@SHOP_セーブ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC データ番号
#DIM DYNAMIC データ数
CALL COLUMNPRINTL, 0, "□何番にセーブしますか？"
CALL COLUMNPRINTL, 0, "　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間"

FOR LCOUNT, SHOP_セーブページ*20, SHOP_セーブページ*20+20
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] %RESULTS%"
		CASE 1
			;表示テンプレ
			;PRINTFORML 　[ 5] 2016/01/10(曜) 00:10:15 | 2016/3/28(曜) 197日目夜 難易度:(N) ∞ 
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] ----　　　　　　　　　　|", "gray"
		CASE 2
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] 別バリアントのセーブデータです 上書きは可能です", "gray"
		CASE 3
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] 互換性の無いセーブデータです 上書きは可能です", "gray"
		CASE 4
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] ロードできないセーブデータです 上書きは可能です", "gray"
	ENDSELECT
NEXT
CALL COLUMNDRAWLINE, 0
;PRINTPLAINFORM 　SAVE.{ページ数+1}　
CALL COLUMNPRINT, 0, @"%RETURNSPACE(SHOP_セーブページ > 0, "[101] 前ページ ")%"
CALL COLUMNPRINT, 0, @"%RETURNSPACE(SHOP_セーブページ < 3, "[109] 次ページ")%"
CALL COLUMNPRINTL, 0, ""

IF SHOP_セーブ確認 >= 0
	CALL COLUMNPRINTL, 0, @"{SHOP_セーブ確認}番には既にセーブデータが存在します。上書きしますか？"
	CALL COLUMNPRINT, 0, "[0] はい "
	CALL COLUMNPRINTL, 0, "[1] いいえ"
ENDIF

@SHOP_セーブ_INPUT
#DIM DYNAMIC データ番号
#DIM DYNAMIC データ数
#DIM DYNAMIC LCOUNT

IF SHOP_セーブ確認 >= 0
	SELECTCASE CRESULT:1
		CASE 0
			データ番号 = SHOP_セーブ確認
		CASE 1
			SHOP_セーブ確認 = -1
			RETURN
	ENDSELECT
ELSE
	SELECTCASE CRESULT:1
		CASE 0 TO 79, 94 TO 99
			SELECTCASE CHKDATA(CRESULT:1)
				CASE 0, 2, 3, 4
					SHOP_セーブ確認 = CRESULT:1
					RETURN
				CASE 1
					データ番号 = CRESULT:1
			ENDSELECT
		CASE 101
			SHOP_セーブページ--
			RETURN
		CASE 109
			SHOP_セーブページ++
			RETURN
	ENDSELECT
ENDIF

CALL プレイ称号計算, "セーブ"
SAVEDATA データ番号, セーブデータ情報()

SHOP_セーブ確認 = -1

データ数 = 0
FOR LCOUNT, 0, 80
	SIF CHKDATA(LCOUNT) == 0
		データ数++
NEXT

SIF データ数 == 80
	CALL 実績解除, "メモリーカードの容量が足りません"

SAVEGLOBAL

@SHOP_ロード
CALL タイトル_セーブデータのロード

@SHOP_ロード_INPUT
CALL タイトル_セーブデータのロード_INPUT

@SHOP_オプション
LOADGLOBAL
CALL COLUMNPRINTL, 0, "□設定項目を選択してください"
CALL COLUMNPRINTL, 0, "　[0] - テキスト設定"
CALL COLUMNPRINTL, 0, "　[1] - 着衣関連設定"
CALL COLUMNPRINTL, 0, "　[2] - イベント関連の設定"
SIF TALENT:MASTER:ドラゴン使い
	CALL COLUMNPRINTL, 0, "　[3] - ドラゴンの名前変更"
SIF !SP
	CALL COLUMNPRINTL, 0, "　[4] - サウンドオプション"
CALL COLUMNPRINTL, 0, @"　[5] - 能力表示画面の顔グラ表示(現在:\@ 顔グラ表示 == 1 ? オン # オフ \@)"
CALL COLUMNPRINTL, 0, @"　[6] - GUIを有効にする(現在:\@ GUI ? オン # オフ \@)"
CALL COLUMNPRINTL, 0, @"　　　　[7] - GUIの色変更"
IF LANGUAGE == "ENG"
	CALL COLUMNPRINTL, 0, "　[8] Change Language - 言語変更"
ELSE
	CALL COLUMNPRINTL, 0, "　[8] 言語変更 - Change Language"
ENDIF

@SHOP_オプション_INPUT
#DIM DYNAMIC LRESULT

SELECTCASE CRESULT:1
	CASE 0
		CALL プレイ称号計算, "テキスト設定"
		CALL TEXT_SETTING
	CASE 1
		CALL プレイ称号計算, "着衣設定"
		CALL CLOTHES_SETTING
	CASE 2
		CALL EVENT_SETTING
	CASE 3
		DO
			PRINTL ドラゴンの名前を入力してください(何も入力しない場合、「ドラゴン」になります)
			INPUTS
			SIF RESULTS == ""
				RESULTS = ドラゴン
			PRINTFORML %RESULTS%でよろしいですか？(後からオプション画面で変更できます)
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			BINPUT
			IF RESULT == 0
				CSTR:MASTER:ドラゴン = %RESULTS%
				BREAK
			ENDIF
			CONTINUE
		LOOP 1
	CASE 4
		DRAWLINE
		VARLINE = LINECOUNT
		$SOUNDOPTION
		CLEARLINE LINECOUNT-VARLINE
		PRINTFORML □現在の設定 サウンド再生機能:\@ サウンド ? オン # オフ \@ BGM音量:{BGM音量} SE音量:{SOUND音量}
		PRINTFORML 　[0] - サウンド再生機能(現在:\@ サウンド ? オン # オフ \@)
		PRINTL 　[1] - BGMの音量を上げる
		PRINTL 　[2] - BGMの音量を下げる
		PRINTL 　[3] - SEの音量を上げる
		PRINTL 　[4] - SEの音量を下げる
		DRAWLINE
		PRINTPLAIN 　SOUND　
		PRINTL [100] - 戻る
		DO
			INPUT
			LRESULT = RESULT
			SELECTCASE RESULT
				CASE 0
					INVERTBIT サウンド, 0
					SELECTCASE サウンド
						CASE 0
							SETBGMVOLUME 0
							SETSOUNDVOLUME 0
						CASE 1
							SETSOUNDVOLUME SOUND音量
							SETBGMVOLUME BGM音量
					ENDSELECT
					GOTO SOUNDOPTION
				CASE 1
					BGM音量 += 10
					CALL SETMIN(BGM音量, 100)
				CASE 2
					BGM音量 -= 10
					CALL SETMAX(BGM音量, 0)
				CASE 3
					SOUND音量 += 10
					CALL SETMIN(SOUND音量, 100)
				CASE 4
					SOUND音量 -= 10
					CALL SETMAX(SOUND音量, 0)
				CASE 100
					BREAK
				CASEELSE
					CONTINUE
			ENDSELECT

			SETSOUNDVOLUME 0
			SETBGMVOLUME 0
			SETSOUNDVOLUME SOUND音量
			SETBGMVOLUME BGM音量
			SIF GROUPMATCH(LRESULT, 3, 4)
				CALL SE再生, トランプドロー
			GOTO SOUNDOPTION
		LOOP 1
		SAVEGLOBAL
	CASE 5
		INVERTBIT 顔グラ表示, 0
		SAVEGLOBAL
	CASE 6
		INVERTBIT GUI, 0
		SAVEGLOBAL
	CASE 7
		CALL GUI_COLOR
	CASE 8
		IF LANGUAGE == "ENG"
			LANGUAGE = JP
		ELSE
			LANGUAGE = ENG
		ENDIF
		SAVEGLOBAL
ENDSELECT

@SHOP_ショートカット
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT
;ショートカット有効にしていれば表示
;カラム機能を作ったので常時表示
CALL COLUMNPRINTL, 0, @"□ショートカット一覧 現在の対象:\@ TARGET == -1 ? 無し # %NAME:TARGET% \@ 現在の助手:\@ ASSI == -1 ? 無し # %NAME:ASSI% \@"
FOR LCOUNT, 0, VARSIZE("LNK")
	IF LNK:LCOUNT != ""
		CALL COLUMNPRINT, 0, @"　[{LCOUNT, 2}] - %LNKCOM(LNK:LCOUNT), 30, LEFT%"
		表示数++
		SIF 表示数 != 0 && 表示数%3 == 0
			CALL COLUMNPRINTL, 0, ""
	ENDIF
NEXT
SIF 表示数%3 != 0
	CALL COLUMNPRINTL, 0, ""

CALL COLUMNDRAWLINE, 0

SIF FINDELEMENT(LNK, "", 0, VARSIZE("LNK"), 1) >= 0
	CALL COLUMNPRINT, 0, "　[100] - ショートカットコマンドの登録 "
CALL COLUMNPRINTL, 0, "[101] - ショートカットキーの削除"

@SHOP_ショートカット_INPUT
SELECTCASE CRESULT:1
	CASE 0 TO VARSIZE("LNK")
		CALL ショートカットコマンド, CRESULT:1
	CASE 100
		CALL ショートカット登録
	CASE 101
		CALL ショートカット削除
ENDSELECT

[SKIPSTART]
@HOGE
;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 1, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
	;売却可能
	SIF CFLAG:LCOUNT:状態 >= 1 && !CFLAG:LCOUNT:売却不可
		売却可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;対象も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT
[SKIPEND]

@変化
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LTARGET
#DIM DYNAMIC 相性参照キャラ
#DIM DYNAMIC 変身先
#DIM DYNAMIC 不明な変身先
#DIM DYNAMIC CSV体格
DRAWLINE
PRINTL □変化させるキャラを選んでください
FOR LCOUNT, 1, CHARANUM
	SIF !TALENT:LCOUNT:変化
		CONTINUE
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	SIF CFLAG:LCOUNT:監禁
		CONTINUE
	IF 陥落(LCOUNT)
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ミゼラン・ミゼラン"
				IF !TALENT:LCOUNT:妊娠
					SELECTCASE TRANCEFLAG:LCOUNT:ミゼラン専用
						CASE IS < 0
							CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 現在：マゼラン・マゼランに変身中(休憩中です)", "gray"
						CASE 0
							PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：マゼラン・マゼランに変身中
						CASE 10000
							FOR LCOUNT2, 0, CHARANUM
								SIF !ミゼラン変身中:LCOUNT2:0
									CONTINUE
								不明な変身先 = LCOUNT2
								BREAK
							NEXT
							IF CSVNAME(不明な変身先) == "マゼラン・マゼラン"
								PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：フェルディナンド＝カジキ先輩に変身中
							ELSE
								PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：%NAME:不明な変身先%の\@ TALENT:相性参照キャラ:恋心 ? 想い人 # 結婚相手 \@に変身中
							ENDIF
						CASEELSE
							PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：%CSVNAME(TRANCEFLAG:LCOUNT:ミゼラン専用)%に変身中
					ENDSELECT
				ELSE
					CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 現在：マゼラン・マゼランに変身中(妊娠中は変身できません)", "gray"
				ENDIF
			CASEELSE
				PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：\@ TALENT:LCOUNT:アニマル ? 獣 # 人間 \@\@ TRANCEFLAG:LCOUNT:獣変化 ? に変身中 # の姿 \@
		ENDSELECT
	ELSE
		CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 未陥落", "gray"
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　変化　
PRINTL [0] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASEELSE
		;TRANCE変数を使うのは変化後に付いた素質を維持するため
		LTARGET = TARGET
		TARGET = RESULT
		;一部キャラは専用の挙動
		SELECTCASE CHARANAMEF(TARGET)
			CASE "ミゼラン・ミゼラン"
				;ミゼランは今いるキャラの知り合い（全キャラ選べると面倒）に変身する
				$MIGELLAN_TRANCE
				IF !TRANCEFLAG:ミゼラン専用
					PRINTL □誰の知り合いに変身しますか？
					FOR PAGING, 0, CHARANUM
						CALL PAGING, NO:PAGING < 10000 && PAGING != TARGET
					NEXT
					SIF RESULT == -1
						RESTART
					相性参照キャラ = RESULT
					PRINTL □変身先を選んでください
					;恋心か既婚者持ちの場合別枠（不明も可能）ただしハニーバニーと重複不可
					CALL DISABLE, "EVENT_ミゼラン変身", 相性参照キャラ
					IF !RESULT && !ハニーバニー:相性参照キャラ:0
						IF TALENT:相性参照キャラ:恋心
							IF CSVNAME(相性参照キャラ) == "マゼラン・マゼラン"
								PRINTFORML 　[10000] フェルディナンド＝カジキ先輩
							ELSEIF CSTR:相性参照キャラ:恋心 == "不明"
								PRINTFORML 　[10000] %NAME:相性参照キャラ%の想い人
							ELSEIF GETNO(CSTR:相性参照キャラ:恋心)
								PRINTFORML 　[{GETNO(CSTR:相性参照キャラ:恋心)}] %CSTR:相性参照キャラ:恋心%
							ENDIF
						ELSEIF TALENT:相性参照キャラ:既婚者
							IF CSTR:相性参照キャラ:既婚者 == "不明"
								PRINTFORML 　[10000] %NAME:相性参照キャラ%の結婚相手
							ELSEIF GETNO(CSTR:相性参照キャラ:既婚者)
								PRINTFORML 　[{GETNO(CSTR:相性参照キャラ:既婚者)}] %CSTR:相性参照キャラ:既婚者%
							ENDIF
						ENDIF
					ENDIF
					FOR LCOUNT, 1, 10000
						SIF !EXISTCSV(LCOUNT)
							CONTINUE
						;相性のあるキャラのみ変身可能（正負は問わない）
						SIF !RELATION:相性参照キャラ:LCOUNT
							CONTINUE
						;マゼランにはもうなってる（自分にもなれない）
						SIF GROUPMATCH(CSVNAME(LCOUNT), "マゼラン・マゼラン", "ミゼラン・ミゼラン")
							CONTINUE
						;ハニバ対象は上で表示済
						SIF GROUPMATCH(CSVNAME(LCOUNT), CSTR:相性参照キャラ:恋心, CSTR:相性参照キャラ:既婚者)
							CONTINUE
						PRINTFORML 　[{LCOUNT}] %CSVNAME(LCOUNT)%
					NEXT
					DRAWLINE
					PRINTFORML 　[-1] 戻る
					BINPUT
					SIF RESULT == -1
						GOTO MIGELLAN_TRANCE
					変身先 = RESULT
					PRINT ＜ミゼランは
					IF 変身先 == 10000
						IF CSVNAME(相性参照キャラ) == "マゼラン・マゼラン"
							PRINT フェルディナンド＝カジキ先輩
						ELSE
							PRINTFORM %NAME:相性参照キャラ%の\@ TALENT:相性参照キャラ:恋心 ? 想い人 # 結婚相手 \@
						ENDIF
					ELSE
						PRINTFORM %CSVNAME(変身先)%
					ENDIF
					PRINTW に変身した＞
					SIF 変身先 == 10000 || GROUPMATCH(CSVNAME(変身先), CSTR:相性参照キャラ:恋心, CSTR:相性参照キャラ:既婚者)
						CALL 実績解除, "もっとマシな使い方はないのか"
					;残り時間は不明の場合参照キャラに記録 それ以外はミゼランに記録
					TRANCEFLAG:ミゼラン専用 = 変身先
					IF 変身先 == 10000
						ミゼラン変身中:相性参照キャラ:0 = 6
					ELSE
						ミゼラン変身中:TARGET:0 = 6
					ENDIF
					;CSVのある対象に変身する場合一部素質を模倣（性別、体格、バストサイズ、羽、尻尾、動物耳、ファーリー）種族はそのままとする
					IF 変身先 != 10000
						CSV体格 = CSVTALENT(変身先, TALENTF("体格")) > 0 ? CSVTALENT(変身先, TALENTF("体格")) # 3
						CALL TRANCETALENT, TARGET, "体格", CSV体格
						CALL TRANCETALENT, TARGET, "バストサイズ", CSVTALENT(変身先, TALENTF("バストサイズ"))
						;後天的に付与される可能性も一応考慮しておく
						CALL TRANCETALENT, TARGET, "羽", CSVTALENT(変身先, TALENTF("羽"))
						CALL TRANCETALENT, TARGET, "尻尾", CSVTALENT(変身先, TALENTF("尻尾"))
						CALL TRANCETALENT, TARGET, "動物耳", CSVTALENT(変身先, TALENTF("動物耳"))
						CALL TRANCETALENT, TARGET, "ファーリー", CSVTALENT(変身先, TALENTF("ファーリー"))
						;TS処理を最後に行う
						SIF CSVTALENT(変身先, TALENTF("性別")) > 0 && TALENT:性別 != CSVTALENT(変身先, TALENTF("性別"))
							GOTO TS
					ELSEIF 1
						;CSVない場合参照キャラが同性なら性別だけ変えておく バストサイズは女体化の場合3
						IF SEX(TARGET) == SEX(相性参照キャラ)
							CALL TRANCETALENT, TARGET, "バストサイズ", SEX(TARGET) == 1 ? 3 # 0
							GOTO TS
						ENDIF
					ELSE
						$TS
						;TS処理 性転換薬使用済などで変になりそう
						TRANCE:TALENTF("性別") = TALENT:性別
						IF SEX(TARGET) == 2
							TALENT:性別 = 1
							SIF CSTR:童貞 == ""
								TALENT:童貞 = 1
							CALL TRANCETALENT, TARGET, "処女", 0
						ELSE
							TALENT:性別 = 2
							SIF CSTR:処女 == ""
								TALENT:処女 = 1
							CALL TRANCETALENT, TARGET, "童貞", 0
						ENDIF
					ENDIF
					;服装をどうするかは未定
				ELSE
					;変身解除処理だけ分離
					CALL ミゼラン変身解除, TARGET
				ENDIF
			CASEELSE
				;人間←→アニマルに変化
				IF TALENT:アニマル
					;着衣フラグのリセット
					FOR LCOUNT, GETNUM(CFLAG, "特殊着衣不可"), GETNUM(CFLAG, "足元着衣不可")+1
						CFLAG:LCOUNT = CSVCFLAG(NOF(TARGET), LCOUNT)
					NEXT
					FOR LCOUNT, GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
						CSTR:LCOUNT = %CSVCSTR(NOF(TARGET), LCOUNT)%
					NEXT
					PRINTFORMW ＜%CALLNAME:TARGET%は人間の姿に変化した＞
					CALL TRANCETALENT, TARGET, "アニマル", 0
					CALL TRANCETALENT, TARGET, "動物耳", 0
					CALL TRANCETALENT, TARGET, "尻尾", 0
					CALL TRANCETALENT, TARGET, "ファーリー", 0
					;茶倉だけ特殊で機械処理なので人間に戻す
					;スーツだと判明したので除外
					[SKIPSTART]
					IF CHARANAMEF(TARGET) == "戌"
						IF TRANCE:TALENTF("無機物")
							TALENT:無機物 = 0
							CSTR:種族 = %TRANCERACE:TARGET:0%
							NAME:TARGET = 大伴 茶倉
							CALLNAME:TARGET = 茶倉
							TRANCE:TALENTF("無機物") = 0
						ENDIF
					ELSE
					ENDIF
					[SKIPEND]
					TALENT:バストサイズ = TRANCE:TALENTF("バストサイズ")
					TRANCE:TALENTF("バストサイズ") = 0
				ELSE
					PRINTFORMW ＜%CALLNAME:TARGET%は獣の姿に変化した＞
					;着衣フラグの変更
					VARSET CFLAG, 1, GETNUM(CFLAG, "特殊着衣不可"), GETNUM(CFLAG, "足元着衣不可")+1
					VARSET CSTR, "", GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
					CALL TRANCETALENT, TARGET, "アニマル", 1
					CALL TRANCETALENT, TARGET, "ファーリー", 1
					CALL TRANCETALENT, TARGET, "動物耳", 1
					CALL TRANCETALENT, TARGET, "尻尾", 1
					;茶倉だけ特殊で機械（戌）になる
					;スーツだと判明したので除外
					[SKIPSTART]
					IF CHARANAMEF(TARGET) == "大伴 茶倉"
						IF !TALENT:無機物
							TALENT:無機物 = 1
							TRANCERACE:TARGET:0 = %CSTR:種族%
							CSTR:種族 = 機械
							NAME:TARGET = 戌
							CALLNAME:TARGET = 戌
							TRANCE:TALENTF("無機物") = 1
						ENDIF
					ELSE
					ENDIF
					[SKIPEND]
					TRANCE:TALENTF("バストサイズ") = TALENT:バストサイズ
					TALENT:バストサイズ = 0
				ENDIF
				INVERTBIT TRANCEFLAG:TARGET:獣変化, 0
		ENDSELECT
		TARGET = LTARGET
		RESTART
ENDSELECT

@ミゼラン変身時間経過処理
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ミゼラン
SIF GETCHARANAME("ミゼラン・ミゼラン") == -1
	RETURN
ミゼラン = GETCHARANAME("ミゼラン・ミゼラン")
SIF !TRANCEFLAG:ミゼラン:ミゼラン専用
	RETURN

IF TRANCEFLAG:ミゼラン:ミゼラン専用 < 0
	;クールタイムの処理
	TRANCEFLAG:ミゼラン:ミゼラン専用++
	RETURN
ELSE
	FOR LCOUNT, 0, CHARANUM
		IF ミゼラン変身中:LCOUNT:0 > 0
			ミゼラン変身中:LCOUNT:0--
			SIF !ミゼラン変身中:LCOUNT:0
				CALL ミゼラン変身解除, ミゼラン
			BREAK
		ENDIF
	NEXT
ENDIF

@ミゼラン変身解除, ミゼラン
#DIM DYNAMIC ミゼラン

PRINTFORMW ＜ミゼランはマゼランの姿に戻った＞
TRANCEFLAG:ミゼラン:ミゼラン専用 = -6
VARSET ミゼラン変身中
;クールタイム無しは強すぎなので
PRINTFORMW ミゼランは変身中の演技で疲れたようです。すぐにまた変身させるのは控えましょう
;素質を戻す処理
CALL TRANCETALENT, ミゼラン, "体格", 999
CALL TRANCETALENT, ミゼラン, "バストサイズ", 999
CALL TRANCETALENT, ミゼラン, "羽", 999
CALL TRANCETALENT, ミゼラン, "尻尾", 999
CALL TRANCETALENT, ミゼラン, "動物耳", 999
CALL TRANCETALENT, ミゼラン, "ファーリー", 999
IF TRANCE:ミゼラン:TALENTF("性別")
	IF SEX(ミゼラン) == 1
		TALENT:ミゼラン:童貞 = 0
		CALL TRANCETALENT, ミゼラン, "処女", 1
	ELSE
		TALENT:ミゼラン:処女 = 0
		CALL TRANCETALENT, ミゼラン, "童貞", 1
	ENDIF
	TALENT:ミゼラン:性別 = TRANCE:ミゼラン:TALENTF("性別")
	TRANCE:ミゼラン:TALENTF("性別") = 0
ENDIF


;;;TRANCE変数をオンオフする（要改良）
@TRANCETALENT, 対象, 素質名, 数値
#DIM DYNAMIC 対象
#DIMS DYNAMIC 素質名
#DIM DYNAMIC 数値

IF 数値 == 999
	IF TRANCE:対象:TALENTF(素質名) == -1
		TRANCE:対象:TALENTF(素質名) = 0
	ELSEIF 素質名 == "バストサイズ" || 素質名 == "体格"
		TALENT:対象:TALENTF(素質名) = TRANCE:対象:TALENTF(素質名)
	ELSEIF TRANCE:対象:TALENTF(素質名) == 1
		INVERTBIT TALENT:対象:TALENTF(素質名), 0
	ENDIF
	TRANCE:対象:TALENTF(素質名) = 0
	RETURN
ENDIF

SELECTCASE 素質名
	CASE "バストサイズ"
		SELECTCASE TRANCE:対象:TALENTF("バストサイズ")
			CASE -1
				TALENT:対象:バストサイズ = 0
				TRANCE:対象:TALENTF("バストサイズ") = 0
			CASE 0
				;男のバストサイズ無しの場合は-1にする
				TRANCE:対象:TALENTF("バストサイズ") = TALENT:対象:バストサイズ > 0 ? TALENT:対象:バストサイズ # -1
				TALENT:対象:バストサイズ = 数値
			CASEELSE
				TALENT:対象:バストサイズ = TRANCE:対象:TALENTF("バストサイズ")
				TRANCE:対象:TALENTF("バストサイズ") = 0
		ENDSELECT
	CASE "体格"
		IF TRANCE:対象:TALENTF("体格")
			TALENT:対象:体格 = TRANCE:対象:TALENTF("体格")
			TRANCE:対象:TALENTF("体格") = 0
		ELSE
			TRANCE:対象:TALENTF("体格") = TALENT:対象:体格
			TALENT:対象:体格 = 数値 > 0 ? 数値 # 3
		ENDIF
	CASEELSE
		IF TRANCE:対象:TALENTF(素質名) == -1
			TRANCE:対象:TALENTF(素質名) = 0
		ELSE
			IF TALENT:対象:TALENTF(素質名) != 数値
				TALENT:対象:TALENTF(素質名) = 数値
				INVERTBIT TRANCE:対象:TALENTF(素質名), 0
			ELSE
				 TRANCE:対象:TALENTF(素質名) = -1
			ENDIF
		ENDIF
ENDSELECT

@所持ビデオ一覧
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 総ページ数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 総本数

総本数 = FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1)

総ページ数 = 総本数/10+1
$TOP
DRAWLINE
PRINTFORML □所持ビデオ一覧 総本数:{総本数}
FOR LCOUNT, ページ数*10, ページ数*10+10
	SIF LCOUNT >= 総本数
		BREAK
	PRINTFORML 　{LCOUNT+1}.%所持ビデオ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAINFORM 　VIDEO.{ページ数+1}　
PRINT [0] - 戻る 
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
CALL PRINTSPACE, ページ数+1 < 総ページ数, "[9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		ページ数--
	CASE 9
		ページ数++
ENDSELECT

GOTO TOP

@プレイログ閲覧
#DIM DYNAMIC ページ数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 最大文字数, 4
#DIMS DYNAMIC SPLITVAR, 3
#DIMS DYNAMIC DETAILVAR, 4
#DIMS DYNAMIC 同上, 3

ページ数 = 0

DRAWLINE
PRINTL [0] - プレイログを見る
PRINTL [1] - 現金出納帳を見る
PRINTL [2] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		GOTO プレイログ
	CASE 1
		GOTO 現金出納帳
	CASE 2
		RETURN
ENDSELECT

$プレイログ
DRAWLINE
FOR LCOUNT, ページ数*30, ページ数*30+30
	SIF プレイログ:LCOUNT == ""
		BREAK
	PRINTFORML %プレイログ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAINFORM 　プレイログ,{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, プレイログ:(ページ数*30+31) != "", "[9] - 次ページ"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		ページ数--
	CASE 9
		ページ数++
ENDSELECT

GOTO プレイログ

$現金出納帳
FOR LCOUNT, ページ数*30, ページ数*30+30
	SIF 現金出納帳:LCOUNT == ""
		BREAK
	SPLIT 現金出納帳:LCOUNT, "/", SPLITVAR
	SPLIT SPLITVAR:0, "-", DETAILVAR
	SIF STRLENS(DETAILVAR:0) > 最大文字数:0
		最大文字数:0 = STRLENS(DETAILVAR:0)
	SIF STRLENS(DETAILVAR:3) > 最大文字数:1
		最大文字数:1 = STRLENS(DETAILVAR:3)
	;邪悪すぎる式
	SIF STRLENS(TODIGSEP(ABS(TOINT(SPLITVAR:1)))) > 最大文字数:2
		最大文字数:2 = STRLENS(TODIGSEP(ABS(TOINT(SPLITVAR:1))))
	SIF STRLENS(TODIGSEP(TOINT(SPLITVAR:2))) > 最大文字数:3
		最大文字数:3 = STRLENS(TODIGSEP(TOINT(SPLITVAR:2)))
NEXT

DRAWLINE
VARSET 同上
PRINTFORML │%"年", 最大文字数:0, LEFT%│月│日│%"概要", 最大文字数:1, LEFT%│%"入金", 最大文字数:2, LEFT%│%"出金", 最大文字数:2, LEFT%│%"残高", 最大文字数:3, LEFT%│
;時系列順に上から下に並べる
FOR LCOUNT, ページ数*30+29, ページ数*30-1, -1
	SIF 現金出納帳:LCOUNT == ""
		CONTINUE
	SPLIT 現金出納帳:LCOUNT, "/", SPLITVAR
	SPLIT SPLITVAR:0, "-", DETAILVAR
	;あまりにも長いので数行に分割する
	PRINTFORM │\@ 同上:0 != DETAILVAR:0 ? %DETAILVAR:0, 最大文字数:0% # %"", 最大文字数:0% \@
	PRINTFORM │\@ 同上:1 != DETAILVAR:1 ? %DETAILVAR:1, 2% # %"  "% \@
	PRINTFORM │\@ 同上:2 != DETAILVAR:2 ? %DETAILVAR:2, 2% # %"  "% \@
	PRINTFORM │%DETAILVAR:3, 最大文字数:1, LEFT%
	PRINTFORM │\@ SIGN(TOINT(SPLITVAR:1)) > 0 ? %TODIGSEP(TOINT(SPLITVAR:1)), 最大文字数:2% # %"", 最大文字数:2% \@│\@ SIGN(TOINT(SPLITVAR:1)) < 0 ? %TODIGSEP(ABS(TOINT(SPLITVAR:1))), 最大文字数:2% # %"", 最大文字数:2% \@
	PRINTFORM │%TODIGSEP(TOINT(SPLITVAR:2)), 最大文字数:3%
	PRINTL │
	同上:0 = %DETAILVAR:0%
	同上:1 = %DETAILVAR:1%
	同上:2 = %DETAILVAR:2%
NEXT
DRAWLINE
PRINTPLAINFORM 　現金出納帳,{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, 現金出納帳:(ページ数*30+31) != "", "[9] - 次ページ"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		ページ数--
	CASE 9
		ページ数++
ENDSELECT

GOTO 現金出納帳

@通常モード_BGM
;季節で変わる
SELECTCASE MONTH
	CASE 3 TO 5
		CALL BGM再生, 通常_春
	CASE 6 TO 8
		CALL BGM再生, 通常_夏
	CASE 9 TO 11
		CALL BGM再生, 通常_秋
	CASE 12, 1, 2
		CALL BGM再生, 通常_冬
ENDSELECT

@新婚モード_BGM
;季節で変わる
SELECTCASE MONTH
	CASE 3 TO 5
		CALL BGM再生, 新婚_春
	CASE 6 TO 8
		CALL BGM再生, 新婚_夏
	CASE 9 TO 11
		CALL BGM再生, 新婚_秋
	CASE 12, 1, 2
		CALL BGM再生, 新婚_冬
ENDSELECT
