@SPECTRAL
#DIM DYNAMIC LRESULT
#DIM DYNAMIC LTARGET

CALL CHECKDRAWLINE

PRINTL □幽体離脱・憑依ができます
PRINTL 　[1] - 幽体離脱させる
PRINTL 　[2] - 憑依させる
PRINTL 　[3] - 使われていない依代を見る
DRAWLINE
PRINTPLAIN 　幽体離脱・憑依　
PRINTL [0] - 戻る

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		CALL 幽体離脱
		RESTART
	CASE 2
		CALL 憑依
		RESTART
	CASE 3
ENDSELECT

PRINTL □依代一覧を表示します
CALL 依代一覧
LRESULT = RESULT

SELECTCASE LRESULT
	CASE 0
		RESTART
	CASEELSE
		LTARGET = TARGET
		TARGET = LRESULT
		DRAWLINE
		PRINTFORM □ %I18N("基本情報")% 
		IF CFLAG:タグ色
			IF ISSLIME(TARGET)
				PRINTFORM %I18N("体色")%:
			ELSE
				PRINTFORM %I18N("タグ色")%:
			ENDIF
			SETCOLOR CFLAG:タグ色
			PRINT ■
			RESETCOLOR
		ENDIF
		PRINTL 
		PRINTFORM 　%I18N("名前")%:%NAMEDISP(TARGET)% %I18N("性別")%:%性別表示(TARGET)% 
		IF NOF(TARGET) >= 10000 || CSTR:種族 == CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
			PRINTFORM %I18N("種族")%:%I18N(CSTR:種族, , "RACE")% 
		ELSEIF CSTR:種族 != CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
			PRINTFORM %I18N("種族")%:%I18N("元|0|の|1|", I18N(CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族")), , "RACE"), I18N(CSTR:種族, , "RACE"))% 
		ENDIF
		PRINTL 
		PRINTFORML □ %I18N("珠")%
		CALL SHOW_INFO_JUEL, TARGET
		CALL SHOW_INFO, TARGET
		WAIT
		TARGET = LTARGET
		RESTART
ENDSELECT

@幽体離脱, 憑依霊 = -1, 機械化
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示
#DIM DYNAMIC 対象
#DIM DYNAMIC 幽体
#DIM DYNAMIC 憑依霊
#DIM DYNAMIC 機械化

;機械化の幽体離脱は処理まですっ飛ばす
IF 機械化
	対象 = 機械化
	GOTO 人格分離
ENDIF

IF 憑依霊 >= 0
	PRINTFORML □%CALLNAME:憑依霊%を誰に憑依させますか？
ELSE
	PRINTL □誰を幽体離脱させますか？
ENDIF

FOR PAGING, 1, CHARANUM
	IF 憑依霊 >= 0
		表示 = 1
	ELSE
		表示 = 0
	ENDIF
	;霊感持ちか憑依中ならできる
	SIF TALENT:PAGING:霊感
		表示 = 1
	SIF CFLAG:PAGING:憑依 >= 0
		表示 = 1
	;先天霊体なら可
	SIF NOF(PAGING) < 10000 && CSVTALENT(NOF(PAGING), GETNUM(TALENT, "霊体"))
		表示 = 1
	;既に霊体なら無理
	SIF TALENT:PAGING:霊体
		表示 = 0
	;機械無機物は無理
	SIF TALENT:PAGING:無機物
		表示 = 0
	SIF CSTR:PAGING:種族 == "機械"
		表示 = 0
	;スライムも無理
	SIF ISSLIME(PAGING)
		表示 = 0
	;憑依霊本人には憑依できない
	SIF 憑依霊 == PAGING
		表示 = 0
	;魔法使用中は無理
	SIF ISENCHANT(PAGING)
		表示 = 0
	;DISABLEチェック
	CALLFORM DISABLE, "EVENT_幽体離脱", PAGING
	SIF RESULT
		表示 = 0
	CALL PAGING, 表示
NEXT

SIF RESULT == -1
	RETURN 0

対象 = RESULT

IF 憑依霊 > 0 && CFLAG:対象:憑依 == NOF(憑依霊)
	PRINTFORML %CSVNAMEF(NOF(対象))%の精神を追い出して%CALLNAME:憑依霊%の精神を身体に戻します。よろしいですか？
ELSEIF 憑依霊 >= 0
	PRINTFORML %NAME:対象%の精神を追い出して%CALLNAME:憑依霊%を憑依させます。よろしいですか？
ELSE
	PRINTFORML %NAME:対象%を幽体離脱させます。よろしいですか？
ENDIF
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT

SIF RESULT == 1
	RESTART

$人格分離

CALL 配属解除, 対象
SIF 憑依霊 >= 0
	CALL 配属解除, 憑依霊
ADDCOPYCHARA 対象

幽体 = CHARANUM-1

;素質やABL等を精神と身体で分離する
FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	IF BODYSTUFF("ABL", ABLNAME:LCOUNT)
		ABL:幽体:LCOUNT = 0
	ELSE
		ABL:対象:LCOUNT = 0
	ENDIF
NEXT
;CFLAGは全て身体
VARSET CFLAG:幽体:0
;誕生日だけ引き継ぐ
CFLAG:幽体:誕生日 = CFLAG:対象:誕生日
;憑依フラグを-1に
CFLAG:幽体:憑依 = -1

;CSTRは引き継ぐが種族は幽霊になる
CSTR:幽体:種族 = 幽霊

FOR LCOUNT, 0, VARSIZE("EXP")
	SIF EXPNAME:LCOUNT == ""
		CONTINUE
	IF BODYSTUFF("EXP", EXPNAME:LCOUNT)
		EXP:幽体:LCOUNT = 0
	ELSE
		EXP:対象:LCOUNT = 0
	ENDIF
NEXT

;MARKは全て幽体
VARSET MARK:対象:0

FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	IF BODYSTUFF("JUEL", PALAMNAME:LCOUNT)
		JUEL:幽体:LCOUNT = 0
	ELSE
		JUEL:対象:LCOUNT = 0
	ENDIF
NEXT

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE BODYSTUFF("TALENT", TALENTNAME:LCOUNT)
		CASE 1
			TALENT:幽体:LCOUNT = 0
		CASE 0
			TALENT:対象:LCOUNT = 0
	ENDSELECT
	;2が返ってくる素質は何もしない
NEXT
;身体の相性を初期化
VARSET RELATION:対象:0, 100
;幽体の妊娠関連フラグをリセット
CALL CLEAR_FLAG, 幽体
;幽体に売却可・助手可フラグを立てておく
SIF 売却可能条件(幽体)
	CFLAG:幽体:売却可能 = 1
SIF 助手可能条件(幽体)
	CFLAG:幽体:助手可能 = 1

;憑依フラグがあればNOと名前は元に戻る
IF CFLAG:対象:憑依 >= 0
	NAME:幽体 = %CSVNAMEF(NOF(対象))%
	CALLNAME:幽体 = %CSVCALLNAMEF(NOF(対象))%
	NAME:対象 = %CSVNAMEF(CFLAG:対象:憑依)%
	CALLNAME:対象 = %CSVCALLNAMEF(CFLAG:対象:憑依)%
	NO:対象 = CFLAG:対象:憑依
	CFLAG:対象:憑依 = -1
	TALENT:幽体:性別 = 憑依情報:対象:性別
	TALENT:幽体:バストサイズ = 憑依情報:対象:バストサイズ
	CFLAG:幽体:誕生日 = 憑依情報:対象:誕生日
	TALENT:幽体:体格 = 憑依情報:対象:体格
ENDIF
VARSET 憑依情報:対象:0
VARSET 憑依情報:幽体:0

IF 憑依霊 >= 0
	PRINTFORML %CALLNAME:対象%の身体と精神を分離しました
	TALENT:幽体:霊体 = 1
	;FOR LCOUNT, 1, 1000
	;	IF 依代:LCOUNT == 0
	;		SAVECHARA @"依代_{LCOUNT}_{データID}", @"%NAME:対象%の身体", 対象
	;		依代:LCOUNT = 1
			CFLAG:対象:依代 = 1
			CSTR:対象:ロスト要因 = 依代
			CSTR:対象:ロスト詳細 = 霊体の依代として保管された
			CALL CLEAR_CHARA, 対象
			RETURN 対象
	;	ENDIF
	;NEXT
ELSE
	;身体は依代として保管
	;FOR LCOUNT, 1, 1000
	;	IF 依代:LCOUNT == 0
	;		SAVECHARA @"依代_{LCOUNT}_{データID}", @"%NAME:対象%の身体", 対象
	;		依代:LCOUNT = 1
			CFLAG:対象:依代 = 1
			CSTR:対象:ロスト要因 = 依代
			CSTR:対象:ロスト詳細 = 霊体の依代として保管された
			CALL CLEAR_CHARA, 対象
	;		BREAK
	;	ENDIF
	;NEXT

	TARGET = CHARANUM-1

	IF 機械化 == 0
		PRINTFORMW %CALLNAME:TARGET%の身体と精神を分離して、身体は依代として保管しました
		TALENT:霊体 = 1
	ENDIF
	RETURN
ENDIF

@憑依
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示
#DIM DYNAMIC 対象
#DIM DYNAMIC 憑依霊
#DIMS DYNAMIC 憑依霊名

PRINTL □誰を憑依させますか？

FOR PAGING, 1, CHARANUM
	表示 = 0
	SIF TALENT:PAGING:霊体
		表示 = 1
	CALL PAGING, 表示
NEXT

SIF RESULT == -1
	RETURN

憑依霊 = RESULT
憑依霊名 = %NAME:RESULT%

PRINTL [0] - 他のキャラに憑依させる
PRINTL [1] - 依代に憑依させる

BINPUT

SELECTCASE RESULT
	CASE 0
		CALL 幽体離脱, 憑依霊
	CASE 1
		CALL 依代一覧
ENDSELECT

SIF RESULT == 0
	RETURN

;憑依霊名が違ってる場合は幽体離脱のCLEAR_CHARAで一人分ずれてるので修正する
;SIF 憑依霊名 != NAME:憑依霊
;	憑依霊--

;LOADCHARA @"依代_{RESULT}_{データID}"
;依代:RESULT = 0

対象 = RESULT

;幽体離脱で分離したものを合わせる
FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	;体に残ってる感覚系と中毒系は身体のまま
	SIF !BODYSTUFF("ABL", ABLNAME:LCOUNT)
		ABL:対象:LCOUNT = ABL:憑依霊:LCOUNT
NEXT

FOR LCOUNT, 0, VARSIZE("EXP")
	SIF EXPNAME:LCOUNT == ""
		CONTINUE
	SIF !BODYSTUFF("EXP", EXPNAME:LCOUNT)
		EXP:対象:LCOUNT = EXP:憑依霊:LCOUNT
NEXT

FOR LCOUNT, 0, VARSIZE("MARK")
	MARK:対象:LCOUNT = MARK:憑依霊:LCOUNT
NEXT

FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SIF !BODYSTUFF("JUEL", PALAMNAME:LCOUNT)
		JUEL:対象:LCOUNT = JUEL:憑依霊:LCOUNT
NEXT

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LCOUNT == ""
		CONTINUE
	SIF !BODYSTUFF("TALENT", TALENTNAME:LCOUNT)
		TALENT:対象:LCOUNT = TALENT:憑依霊:LCOUNT
NEXT

FOR LCOUNT, 0, 20000
	RELATION:対象:LCOUNT = RELATION:憑依霊:LCOUNT
NEXT

;憑依霊の方の性別、バストサイズ、誕生日を記録しておく
憑依情報:対象:性別 = TALENT:憑依霊:性別
憑依情報:対象:バストサイズ = TALENT:憑依霊:バストサイズ
憑依情報:対象:誕生日 = CFLAG:憑依霊:誕生日
憑依情報:対象:体格 = TALENT:憑依霊:体格

;憑依に身体のNOを移して憑依霊のNOに変える
CFLAG:対象:憑依 = NOF(対象)
NO:対象 = NOF(憑依霊)

CFLAG:対象:キャラロスト = 0
CFLAG:対象:依代 = 0

TALENT:対象:霊体 = 0

IF CFLAG:対象:憑依 == NOF(対象)
	PRINTFORMW %CALLNAME:対象%の身体に精神を戻しました
	VARSET 憑依情報:対象:0
	CFLAG:対象:憑依 = -1
ELSE
	PRINTFORMW %CALLNAME:対象%の身体に%CALLNAME:憑依霊%を憑依させました
	NAME:対象 = %NAME:憑依霊%が憑依した%NAME:対象%
	CALLNAME:対象 = %CALLNAME:憑依霊%が憑依した%CALLNAME:対象%
ENDIF
CALL CLEAR_CHARA, 憑依霊, 1

TARGET = 対象

RETURN


@依代一覧
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 処理数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC LRESULT

CALL CHECKDRAWLINE

処理数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	IF 処理数 >= ページ数*20+20
		IF CFLAG:LCOUNT:依代
			次ページあり = 1
			BREAK
		ENDIF
	ENDIF
	IF CFLAG:LCOUNT:依代
		SIF ページ数*20 <= 処理数
			PRINTFORML 　[{LCOUNT, 2}] - %NAME:LCOUNT%
		処理数++
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　依代一覧　
CALL PRINTSPACE, ページ数, "[-1] - 前ページ "
PRINT [0] - 戻る
CALL PRINTSPACE, 次ページあり, "[-9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASEELSE
		RETURN RESULT
ENDSELECT

RESTART

;;;肉体由来のものか否かは式中関数で判定する
@BODYSTUFF, 分類, 判定対象
#FUNCTION
#DIMS DYNAMIC 分類
#DIMS DYNAMIC 判定対象

SELECTCASE 分類
	;体に残るのは感覚系と中毒系
	CASE "ABL"
		{
		SIF GROUPMATCH(判定対象, "Ｃ感覚", "Ｖ感覚", "Ａ感覚", "Ｂ感覚", "メス度",
			 "自慰中毒", "精液中毒", "レズ中毒", "ゲイ中毒", "性交中毒", "噴乳中毒")
		}
			RETURNF 1
	;身体で覚える経験を分離する
	CASE "EXP"
		{
		SIF GROUPMATCH(判定対象, "Ｖ経験", "Ａ経験", "絶頂経験", "射精経験", "性交経験", "膣射経験", "精液経験", "噴乳経験", "放尿経験",
			 "射精管理経験", "Ｖ拡張経験", "Ａ拡張経験", "出産経験", "流産経験", "尿道経験", "臍拡張経験", "尿管拡張経験")
		}
			RETURNF 1
	;快系JUELを分離する
	CASE "JUEL"
		SIF GROUPMATCH(判定対象, "快Ｃ", "快Ｖ", "快Ａ", "快Ｂ")
			RETURNF 1
	;身体系素質を分離する
	CASE "TALENT"
		{
		SIF GROUPMATCH(判定対象, "性別", "バストサイズ", "体格", "処女", "再生処女", "童貞", "傷物", "改造済み",
			 "痛みに弱い", "痛みに強い", "濡れやすい", "濡れにくい", "勃ちやすい", "勃ちにくい",
			 "猫舌", "薬毒耐性", "おもらし癖", "淫核", "淫膣", "淫尻", "淫乳", "狐", "妖狐", 
			 "Ｃ鈍感", "Ｃ敏感", "Ｖ鈍感", "Ｖ敏感", "Ａ鈍感", "Ａ敏感", "Ｂ鈍感", "Ｂ敏感",
			 "ファーリー", "尻尾", "動物耳", "羽", "無機物", "小人体型", "暑さに弱い", "寒さに弱い", "アニマル",
			 "足使用不可", "性転換済み", "ゾンビ", "母乳体質", "ふたなり", "絶倫", "去勢済み", "回復早い", "回復遅い",
			 "日光浴", "月光浴", "人気", "妊娠", "育児中", "孔雀の瞳", "ドラゴン使い", "妊娠確定", "変化", "犬", "猫",
			 "リミットバースト")
		}
			RETURNF 1
		;分離時に触らないもの
		SIF GROUPMATCH(判定対象, "性別", "バストサイズ", "体格")
			RETURNF 2
ENDSELECT
RETURNF 0
