@CANNIBALISM
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 捕食者 = -1
#DIM DYNAMIC 対象 = -1
#DIM DYNAMIC 可否

DRAWLINE

PRINTL □対象を捕食することで経験値や珠を得られます
PRINTL 　捕食者は[狂気][ゾンビ][スライムor溶ける快]を持つ者だけです
PRINTL 　主人が捕食した場合は、被捕食者の能力に応じて経験値を得られることがあります(最高5)
PRINTL 　それ以外のキャラが捕食した場合は、10%の珠が捕食者に受け継がれます
PRINTL 　スライム同士の融合はより効率良く各能力が受け継がれます
PRINTL 
PRINTFORML 　[1] - 捕食者を選ぶ(\@ 捕食者 >= 0 ? %NAME:捕食者% # 無し \@\@ ISSLIME(捕食者) ? :スライム #\@)
PRINTFORML 　[2] - 対象を選ぶ(\@ 対象 >= 0 ? %NAME:対象% # 無し \@\@ ISSLIME(対象) ? :スライム #\@)
DRAWLINE
PRINTPLAIN 　CANNIBALISM　
PRINT [0] - 戻る 
IF 捕食者 >= 0 && 対象 >= 0
	PRINT [3] - これで決定 
	IF !ISSLIME(捕食者)
		CALL PRINTSPACE, EXP:捕食者:Ｖ拡張経験 >= 500, "[4] - 膣から食べる "
		CALL PRINTSPACE, EXP:捕食者:Ａ拡張経験 >= 500, "[5] - アナルから食べる "
		CALL PRINTSPACE, EXP:捕食者:尿道経験 >= 500, "[6] - 尿道から食べる "
	ENDIF
ENDIF
PRINTL 

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		FOR PAGING, 0, CHARANUM
			可否 = 0
			SIF TALENT:PAGING:狂気
				可否 = 1
			SIF TALENT:PAGING:ゾンビ
				可否 = 1
			SIF ISSLIME(PAGING)
				可否 = 1
			SIF TALENT:PAGING:無機物
				可否 = 0
			SIF TALENT:PAGING:霊体
				可否 = 0
			CALL PAGING, 可否, @"%ISTALENTF(PAGING, "狂気")%%ISTALENTF(PAGING, "ゾンビ")%%ISTALENTF(PAGING, "スライム")%%ISTALENTF(PAGING, "溶ける快")%"
		NEXT
		捕食者 = RESULT
		RESTART
	CASE 2
		FOR PAGING, 0, CHARANUM
			可否 = 1
			SIF TALENT:PAGING:無機物 || CSTR:PAGING:種族 == "機械" || TALENT:PAGING:霊体
				可否 = 0
			SIF PAGING == MASTER
				可否 = 0
			CALL PAGING, 可否, @"%ISTALENTF(PAGING, "スライム")%%ISTALENTF(PAGING, "溶ける快")%"
		NEXT
		対象 = RESULT
		RESTART
	CASE 3, 4, 5, 6
		捕食手段 = 
		SELECTCASE RESULT
			CASE 4
				SIF EXP:捕食者:Ｖ拡張経験 < 500
					RESTART
				捕食手段 = 膣
			CASE 5
				SIF EXP:捕食者:Ａ拡張経験 < 500
					RESTART
				捕食手段 = アナル
			CASE 6
				SIF EXP:捕食者:尿道経験 < 500
					RESTART
				捕食手段 = 尿道
		ENDSELECT
		IF ISSLIME(捕食者) && ISSLIME(対象)
			PRINTFORML %CALLNAME:対象%を%CALLNAME:捕食者%に融合させます。よろしいですか？
		ELSE
			PRINTFORML %CALLNAME:対象%を%CALLNAME:捕食者%に\@ 捕食手段 != "" ? %捕食手段%から #\@食べさせます。よろしいですか？
		ENDIF
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		SIF RESULT
			RESTART
		IF ISSLIME(捕食者) && ISSLIME(対象)
			CALL 捕食_スライム, 捕食者, 対象
		ELSE
			CALL 捕食, 捕食者, 対象
		ENDIF
		RETURN 1
ENDSELECT

@捕食, 捕食者, 対象, お菓子化
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 捕食者
#DIM DYNAMIC 対象
#DIM DYNAMIC 取得経験値
#DIM DYNAMIC 能力数
#DIM DYNAMIC お菓子化

DRAWLINE
VARLINE = LINECOUNT

IF お菓子化
	TFLAG:捕食相手 = 捕食者
	CALL KOJO_MESSAGE_EVENT, "お菓子被捕食", 対象
	TFLAG:捕食相手 = 対象
	CALL KOJO_MESSAGE_EVENT, "お菓子捕食", 捕食者
ELSE
	TFLAG:捕食相手 = 捕食者
	CALL KOJO_MESSAGE_EVENT, "被捕食", 対象
	TFLAG:捕食相手 = 対象
	CALL KOJO_MESSAGE_EVENT, "捕食", 捕食者
ENDIF

CALL プレイログ記録, @"%NAME:捕食者%が%NAME:対象%を捕食"
CSTR:対象:ロスト要因 = 捕食
SELECTCASE 捕食手段
	CASE "膣"
		CSTR:対象:ロスト詳細 = %NAME:捕食者%に膣から捕食された
	CASE "アナル"
		CSTR:対象:ロスト詳細 = %NAME:捕食者%にアナルから捕食された
	CASE "尿道"
		CSTR:対象:ロスト詳細 = %NAME:捕食者%に尿道から捕食された
	CASEELSE
		CSTR:対象:ロスト詳細 = %NAME:捕食者%に捕食された
ENDSELECT

;捕食者が主人の場合は経験値取得処理
IF 捕食者 == MASTER
	;全ABLの平均/2の経験値が得られる 最高5
	FOR LCOUNT, 0, VARSIZE("ABL")
		SIF ABLNAME:LCOUNT == ""
			CONTINUE
		取得経験値 += ABL:対象:LCOUNT
		能力数++
	NEXT
	DEBUGPRINTFORML {取得経験値} {能力数}
	取得経験値 /= 能力数*2
	SIF 取得経験値
		PRINTFORMW {MIN(取得経験値, 5)}の経験値を取得しました
	FLAG:経験値 = MIN(取得経験値, 5)
	CALL CLEAR_CHARA, 対象
	RETURN
ENDIF

;その他はJUELの引き継ぎ処理(10%)
FOR LCOUNT, 0, VARSIZE("JUEL")
	SIF JUEL:対象:LCOUNT < 10
		CONTINUE
	CALL JUELUP, PALAMNAME:LCOUNT, JUEL:対象:LCOUNT/10, 捕食者
NEXT
;魔力が強い相手を食べると魔法技能+1
IF ABL:対象:魔法技能 > ABL:捕食者:魔法技能
	PRINTFORML 魔力の強い相手を捕食したことで魔法技能+1
	ABL:捕食者:魔法技能++
ENDIF
;魔力は全回復させる
BASE:捕食者:魔力 = MAXBASE:捕食者:魔力
;スライム度はそのまま引き継ぐ
SIF CFLAG:対象:スライム度
	PRINTFORML %CALLNAME:対象%のスライム感染物が%CALLNAME:捕食者%の体内に入り込んだ……
CFLAG:捕食者:スライム度 += CFLAG:対象:スライム度

SIF VARLINE != LINECOUNT && !お菓子化
	WAIT
VARLINE = 0

CALL CLEAR_CHARA, 対象

RETURN

@捕食_スライム, 捕食者, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 捕食者
#DIM DYNAMIC 対象
#DIM DYNAMIC 取得経験値
#DIM DYNAMIC 能力数

DRAWLINE
VARLINE = LINECOUNT

TFLAG:捕食相手 = 捕食者
CALL KOJO_MESSAGE_EVENT, "被捕食", 対象
TFLAG:捕食相手 = 対象
CALL KOJO_MESSAGE_EVENT, "捕食", 捕食者

CALL プレイログ記録, @"%NAME:捕食者%が%NAME:対象%を取り込む"
CSTR:対象:ロスト要因 = 捕食
CSTR:対象:ロスト詳細 = %NAME:捕食者%に取り込まれた

;捕食者が主人の場合は経験値取得処理
IF 捕食者 == MASTER
	;全ABLの平均の経験値が得られる 最高10
	FOR LCOUNT, 0, VARSIZE("ABL")
		SIF ABLNAME:LCOUNT == ""
			CONTINUE
		取得経験値 += ABL:対象:LCOUNT
		能力数++
	NEXT
	取得経験値 /= 能力数
	SIF 取得経験値
		PRINTFORMW {MIN(取得経験値, 10)}の経験値を取得しました
	FLAG:経験値 = MIN(取得経験値, 10)
	CALL CLEAR_CHARA, 対象
	RETURN
ENDIF

;その他はJUELの引き継ぎ処理(50%)
FOR LCOUNT, 0, VARSIZE("JUEL")
	SIF JUEL:対象:LCOUNT < 2
		CONTINUE
	CALL JUELUP, PALAMNAME:LCOUNT, JUEL:対象:LCOUNT/2, 捕食者
NEXT
;対象のABLが1以上ならそのABLが1上がる
FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABL:対象:LCOUNT <= 0
		CONTINUE
	PRINTFORML %ABLNAME:LCOUNT%+1(%NAME:捕食者%)
	ABL:捕食者:LCOUNT++
NEXT

;魔力は全回復させる
BASE:捕食者:魔力 = MAXBASE:捕食者:魔力
;スライム度はそのまま引き継ぐ
SIF CFLAG:対象:スライム度
	PRINTFORML %CALLNAME:対象%のスライム感染物が%CALLNAME:捕食者%の体内に入り込んだ……
CFLAG:捕食者:スライム度 += CFLAG:対象:スライム度

SIF VARLINE != LINECOUNT
	WAIT
VARLINE = 0

CALL CLEAR_CHARA, 対象

RETURN
