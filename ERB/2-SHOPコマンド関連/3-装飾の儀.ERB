@RITUAL_OF_ACCESSORY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET = -1
#DIM DYNAMIC APOINT = -1
#DIMS DYNAMIC EQUIPD_ACCESSORY
#DIM ACC

DRAWLINE

PRINTL □装飾の儀
PRINTL □効果
PRINTL 　この儀式ではピアスなどの装飾を奴隷やMASTERに施すことができる
PRINTL 　MASTERに施す場合はもちろん、奴隷に施す場合でも陥落している必要はない。が、その場合は嫌がられるだろう
SETCOLORBYNAME gray
PRINT 　付ける場所や付けるものによっては調教の効果が増すこともあるようだ
RESETCOLOR
PRINTL （未実装です）
PRINTL

PRINTFORML 　[1] - 対象を選ぶ \@ LTARGET != -1 ? （選択中：%CHARANAMEF(LTARGET)%） #  \@
PRINTFORML 　[2] - 部位を選ぶ \@ APOINT != -1 ? （指定している部位：%ERDNAME(ACCESSORY,APOINT)%） #  \@
PRINTFORML 　[3] - アクセサリーを選ぶ \@ EQUIPD_ACCESSORY != "" ? （つけるアクセサリー：%EQUIPD_ACCESSORY%） #  \@
PRINTL 　[7] - 装飾をつける

DRAWLINE
PRINTPLAIN 　装飾の儀　
PRINT [0] - 戻る 
SIF LTARGET != -1
	PRINT [100] - 装飾の確認
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		DRAWLINE
		FOR PAGING, 0, CHARANUM
			CALL PAGING, 1, "", "TARGET", "やめる"
		NEXT

		SIF RESULT >= 0
			LTARGET = RESULT
	CASE 2
		DRAWLINE
		PRINTFORML □どの部位に装飾を施しますか？
		PRINTFORML [10] - 右乳首\@ LTARGET >=0 && ACCESSORY:LTARGET:右乳首 != "" ? (%ACCESSORY:LTARGET:右乳首%がついています) #  \@
		PRINTFORML [20] - 左乳首\@ LTARGET >=0 && ACCESSORY:LTARGET:左乳首 != "" ? (%ACCESSORY:LTARGET:左乳首%がついています) #  \@
		DRAWLINE
		PRINTPLAINFORM 　ACCESSORY　
		PRINTL [0] - 戻る

		BINPUT
		IF !RESULT
			RESTART
		ELSE
			APOINT = RESULT
		ENDIF
	CASE 3
		DRAWLINE
		PRINTFORML □どんなアクセサリーをつけますか？
		PRINTFORML [1] - リングピアス
		PRINTFORML [2] - バーベルピアス
		DRAWLINE
		PRINTPLAINFORM 　ACCESSORY　
		PRINTL [0] - 戻る

		BINPUT
		SELECTCASE RESULT
			CASE 0
				RESTART
			CASE 1
				EQUIPD_ACCESSORY = リングピアス
			CASE 2
				EQUIPD_ACCESSORY = バーベルピアス
		ENDSELECT
	CASE 7
		IF LTARGET == -1
			PRINTW アクセサリーをつける対象を選択してください
		ELSEIF APOINT == -1
			PRINTW アクセサリーをつける部位を選択してください
		ELSEIF EQUIPD_ACCESSORY == ""
			PRINTW つけるアクセサリーを選択してください
		ELSEIF ACCESSORY:LTARGET:APOINT == EQUIPD_ACCESSORY
			PRINTW その部位には既に同じアクセサリーがついているようです
		ELSE
			PRINTL □アクセサリーをつけますか？
			PRINTL [0] - つける
			PRINTL [1] - やっぱやめる

			BINPUT
			IF !RESULT
				ACCESSORY:LTARGET:APOINT = %EQUIPD_ACCESSORY%
				PRINTFORMW %CHARANAMEF(LTARGET)%の%ERDNAME(ACCESSORY,APOINT)%に%EQUIPD_ACCESSORY%を付け終わった
			ENDIF
		ENDIF
	CASE 100
		DRAWLINE
		PRINTFORML □%CHARANAMEF(LTARGET)%の装飾
		FOR LCOUNT, 0, 100
			IF ACCESSORY:LTARGET:LCOUNT != ""
				PRINTFORML 　%ERDNAME(ACCESSORY,LCOUNT)%：%ACCESSORY:LTARGET:LCOUNT%
				ACC++
			ENDIF
		NEXT
		IF !ACC
			PRINT 　なにもついていません
		ENDIF
		WAIT
ENDSELECT

RESTART

