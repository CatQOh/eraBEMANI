@RITUAL_OF_ACCESSORY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LTARGET = -1
#DIM DYNAMIC APOINT = -1
#DIMS DYNAMIC EQUIPD_ACCESSORY
#DIM ACC
#DIMS DYNAMIC アクセサリー名
#DIMS DYNAMIC 着用可能部位リスト
#DIMS DYNAMIC SPLITVAR, 10

;装飾の種類、装着可能な部位（/で区切る）の順
{
#DIMS DYNAMIC アクセサリーリスト = 
"空欄", "空欄", 
"リングピアス", "右乳首/左乳首/陰核/陰茎", 
"バーベルピアス", "右乳首/左乳首/陰核/陰茎", 
"ラブレットスタッド", "舌",
"カーブドバーベル", "臍",
}

DRAWLINE

PRINTL □装飾の儀
PRINTL □効果
PRINTL 　この儀式ではピアスなどの装飾を奴隷やMASTERに施すことができる
PRINTL 　MASTERに施す場合はもちろん、奴隷に施す場合でも陥落している必要はない。が、その場合は嫌がられるだろう
SETCOLORBYNAME gray
PRINT 　付ける場所や付けるものによっては調教の効果が増すこともあるようだ
RESETCOLOR
PRINTL （未実装です）
PRINTL

IF !LTARGET
	PRINTFORML 　[1] - 対象を選ぶ（選択中：%CALLNAME:MASTER%）
ELSE
	PRINTFORML 　[1] - 対象を選ぶ\@ LTARGET != -1 ? （選択中：%CHARANAMEF(LTARGET)%） #  \@
ENDIF

IF LTARGET == -1
	SETCOLORBYNAME gray
	PRINTPLAIN 　[2] - 部位を選ぶ（対象を指定してください）
	RESETCOLOR
ELSE
	PRINTFORM 　[2] - 部位を選ぶ\@ APOINT != -1 ? （指定している部位：%ERDNAME(ACCESSORY,APOINT)%） #  \@
ENDIF
PRINTL 

IF APOINT == -1
	SETCOLORBYNAME gray
	PRINTPLAIN 　[3] - アクセサリーを選ぶ（部位を指定してください）
	RESETCOLOR
ELSE
	PRINTFORM 　[3] - アクセサリーを選ぶ\@ EQUIPD_ACCESSORY != "" ? （つけるアクセサリー：%EQUIPD_ACCESSORY%） #  \@
ENDIF
PRINTL 

IF EQUIPD_ACCESSORY == ""
	SETCOLORBYNAME gray
	PRINTPLAIN 　[7] - 装飾をつける
	RESETCOLOR
ELSE
	PRINTFORM 　[7] - 装飾をつける
ENDIF
PRINTL 


DRAWLINE
PRINTPLAIN 　装飾の儀　
PRINT [0] - 戻る 
SIF LTARGET != -1
	PRINT [100] - 装飾の確認
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		DRAWLINE
		FOR PAGING, 0, CHARANUM
			CALL PAGING, 1, "", "TARGET", "やめる"
		NEXT

		SIF RESULT >= 0
			LTARGET = RESULT
	CASE 2
		DRAWLINE
		PRINTFORML □どの部位に装飾を施しますか？

		;装着可能部位はACCESSORY.ERDに定義を追加する事で増やす
		FOR LCOUNT, 0, VARSIZE("ACCESSORY")
			IF ERDNAME(ACCESSORY,LCOUNT) != "" && 装飾可能(LTARGET,ERDNAME(ACCESSORY,LCOUNT))
				{
				PRINTFORML [{LCOUNT}] - %ERDNAME(ACCESSORY,LCOUNT)%
				\@ LTARGET >=0 && ACCESSORY:LTARGET:(ERDNAME(ACCESSORY,LCOUNT)) != "" ? 
				(%ACCESSORY:LTARGET:(ERDNAME(ACCESSORY,LCOUNT))%がついています) #  \@
				}
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAINFORM 　ACCESSORY　
		PRINTL [0] - 戻る

		BINPUT
		IF !RESULT
			RESTART
		ELSE
			APOINT = RESULT
			;アクセサリー指定済の場合そこにつけられないアクセサリーなら外す（動作未確認）
			IF EQUIPD_ACCESSORY != ""
				FOR LCOUNT, 1, VARSIZE("アクセサリーリスト")/2
					アクセサリー名 = %アクセサリーリスト:(LCOUNT*2)%
					IF EQUIPD_ACCESSORY == アクセサリー名
						着用可能部位リスト = %アクセサリーリスト:(LCOUNT*2+1)%
						BREAK
					ENDIF
				NEXT
				SPLIT 着用可能部位リスト, "/", SPLITVAR
				FOR LCOUNT, 0, RESULT
					SIF SPLITVAR:LCOUNT == ERDNAME(ACCESSORY,APOINT)
						GOTO OK
				NEXT
				EQUIPD_ACCESSORY = 
				$OK
			ENDIF
		ENDIF
	CASE 3
		DRAWLINE
		PRINTFORML □どんなアクセサリーをつけますか？

		;アクセサリーの種類は上で定義
		FOR LCOUNT, 1, VARSIZE("アクセサリーリスト")/2
			アクセサリー名 = %アクセサリーリスト:(LCOUNT*2)%
			着用可能部位リスト = %アクセサリーリスト:(LCOUNT*2+1)%
			SPLIT 着用可能部位リスト, "/", SPLITVAR
			FOR LCOUNT2, 0, RESULT
				SIF SPLITVAR:LCOUNT2 == ERDNAME(ACCESSORY,APOINT)
					PRINTFORML [{LCOUNT}] - %アクセサリー名%
			NEXT
		NEXT
		DRAWLINE
		PRINTPLAINFORM 　ACCESSORY　
		PRINTL [0] - 戻る

		BINPUT
		SELECTCASE RESULT
			CASE 0
				RESTART
			CASEELSE
				EQUIPD_ACCESSORY = %アクセサリーリスト:(RESULT*2)%
		ENDSELECT
	CASE 7
		IF ACCESSORY:LTARGET:APOINT == EQUIPD_ACCESSORY
			PRINTW その部位には既に同じアクセサリーがついているようです
		ELSE
			PRINTL □アクセサリーをつけますか？
			SIF ACCESSORY:LTARGET:APOINT != ""
				PRINTFORML 　（この部位には既に%ACCESSORY:LTARGET:APOINT%がついています）
			PRINTL [0] - つける
			PRINTL [1] - やっぱやめる

			BINPUT
			IF !RESULT
				ACCESSORY:LTARGET:APOINT = %EQUIPD_ACCESSORY%
				PRINTFORMW %CHARANAMEF(LTARGET)%の%ERDNAME(ACCESSORY,APOINT)%に%EQUIPD_ACCESSORY%を付け終わった
			ENDIF
		ENDIF
	CASE 100
		DRAWLINE
		PRINTFORML □%CHARANAMEF(LTARGET)%の装飾
		FOR LCOUNT, 0, VARSIZE("ACCESSORY")
			SIF ERDNAME(ACCESSORY,LCOUNT) != "" && 装飾可能(LTARGET,ERDNAME(ACCESSORY,LCOUNT))
				PRINTFORML 　%ERDNAME(ACCESSORY,LCOUNT)%：\@ACCESSORY:LTARGET:LCOUNT != "" ? %ACCESSORY:LTARGET:LCOUNT% # なし \@
		NEXT
		WAIT
ENDSELECT

RESTART

;;;(対象者, 対象部位) 対象者の対象部位に装飾可能かどうか（装飾部位があるかどうか）
@装飾可能, 対象者, 対象部位
#FUNCTION
#DIM 対象者
#DIMS 対象部位
SIF 対象者 < 0
	RETURNF 0
SIF 対象部位 == ""
	RETURNF 0

SELECTCASE 対象部位
	CASE "陰茎"
		RETURNF PENIS(対象者)
	CASE "陰核"
		RETURNF SEX(対象者) == 2 && TALENT:対象者:ふたなり == 0
	CASEELSE
		RETURNF 1
ENDSELECT

;;;(対象者) 対象者の現在装着不可能な装飾をはずず
@装飾継続チェック, 対象者
#DIM 対象者
#DIM LCOUNT

SIF 対象者 < 0
	RETURN 0

FOR LCOUNT, 0, VARSIZE("ACCESSORY")
	SIF !装飾可能(対象者,ERDNAME(ACCESSORY,LCOUNT))
		ACCESSORY:対象者:LCOUNT = ""
NEXT
RETURN 0


;;;(提供者, 受容者, 対象部位)  提供者の対象部位にある装飾を受容者に移植する
@装飾移植, 提供者, 受容者, 対象部位
#DIM LCOUNT
#DIM 提供者
#DIM 受容者
#DIMS 対象部位
SIF 対象部位 == ""
	RETURN 0
FOR LCOUNT, 0, VARSIZE("ACCESSORY")
	SELECTCASE ERDNAME(ACCESSORY,LCOUNT)
		CASE ""
			CONTINUE
		CASE 対象部位, "右" + 対象部位, "左" + 対象部位
			SIF ACCESSORY:提供者:LCOUNT == "" || ACCESSORY:提供者:LCOUNT == "空欄" 
				RETURN 0
			ACCESSORY:受容者:LCOUNT = ACCESSORY:提供者:LCOUNT
			ACCESSORY:提供者:LCOUNT = 0
			RETURN 1
	ENDSELECT
NEXT
RETURN 0
;;;(対象者, 対象部位 = "") 対象者の対象部位にある装飾の個数。"乳首"は左右の合計が、空欄だと全部位の合計が返される。
@装飾数, 対象者, 対象部位 = ""
#FUNCTION
#DIM LCOUNT
#DIM 対象者
#DIMS 対象部位
#DIM 個数
個数 = 0

SIF 対象者 < 0
	RETURNF 0

FOR LCOUNT, 0, VARSIZE("ACCESSORY")
	SELECTCASE ERDNAME(ACCESSORY,LCOUNT)
		CASE ""
			CONTINUE
		CASE 対象部位
			SIF (ACCESSORY:対象者:LCOUNT) != "" &&  (ACCESSORY:対象者:LCOUNT != "空欄")
				個数++
		CASE "右" + 対象部位
			SIF (ACCESSORY:対象者:LCOUNT) != "" &&  (ACCESSORY:対象者:LCOUNT != "空欄")
				個数++
		CASE "左" + 対象部位
			SIF (ACCESSORY:対象者:LCOUNT) != "" &&  (ACCESSORY:対象者:LCOUNT != "空欄")
				個数++
		CASEELSE
			SIF (対象部位 == "") && (ACCESSORY:対象者:LCOUNT != "") &&  (ACCESSORY:対象者:LCOUNT != "空欄")
				個数++
	ENDSELECT
NEXT
RETURNF 個数


@装飾効果加算量, パラメータ, 装着者, 部位
#FUNCTION
#DIM パラメータ
#DIM 装着者
#DIMS 部位
#DIM 加算量

#DIM CONST 加算パーセント = 2


SIF 部位 == ""
	RETURNF 0

SIF 装飾数(装着者, 部位) == 0
	RETURNF 0



IF SIGN(パラメータ) == -SIGN(パラメータ * ABS(加算パーセント))
	;掛けてから割るとオーバーフローする場合は割ってから掛ける
	加算量 = パラメータ / 100
	加算量 *= 加算パーセント
ELSE
	加算量 = パラメータ * 加算パーセント
	加算量 /= 100
ENDIF

RETURNF 加算量
