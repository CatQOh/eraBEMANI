;段位認定における1日ごとのゲージの変動を管理
@段位ゲージ処理
#DIM DYNAMIC ストレス値合計
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LMONEY
#DIM DYNAMIC LGAUGE
#DIM DYNAMIC LSTOCK
#DIM DYNAMIC 攻略人数
#DIM DYNAMIC クリアフラグ

;日付変更時に所持金を段位ゲージへ
IF ターン == "夜" && MONEY >= 4000
	;値を格納
	LMONEY = MONEY
	LGAUGE = FLAG:段位ゲージ
	LSTOCK = ストック
	PRINT 所持金を段位ゲージに換算しました。
	FLAG:段位ゲージ += MONEY/4000
	CALL 現金出納帳記録, @"段位ゲージ換金/{MONEY/4000*4000}"
	MONEY = MONEY%4000
	;このあと1減らすためゲージの最大は51(102%)
	IF FLAG:段位ゲージ > 51
		PRINT (※回復量がカンストしています)
		ストック += 4000*(FLAG:段位ゲージ-51)
		ストック -= 3999-MONEY
		FLAG:段位ゲージ = 51
		MONEY = 3999
		PRINTFORM ストック {LSTOCK} → {ストック}
	ENDIF
	PRINTL 
	PRINTFORML 所持金 {LMONEY} → {MONEY}
	PRINTFORML 段位ゲージ {LGAUGE*2} → {FLAG:段位ゲージ*2}
ENDIF

;ここからクリア判定の処理
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:売却可能 || CFLAG:LCOUNT:助手可能
		攻略人数++
	;裏七級の処理(奴隷を崩壊させると即終了)
	SIF 受験段位名 == "裏七級" && TALENT:LCOUNT:崩壊
		FLAG:段位ゲージ = 0
NEXT

クリアフラグ = 0
SELECTCASE 受験段位名
	CASE "いちかのアダルトチャンネル"
		SIF ストック >= 100000
			クリアフラグ = 1
	CASEELSE
		SIF 攻略人数 >= (CHARANUM-1)
			クリアフラグ = 1
ENDSELECT
IF クリアフラグ
	CALL FADEOUTBGM
	CALL TEST_CLEAR
ENDIF

;上のクリア判定をスルーした場合、日付変更時にゲージを減らす処理が入る
IF ターン == "夜"
	SELECTCASE 受験段位名
		CASE "少女達の芳香は艶やかに、猛き賓客を癒し惑わす。"
			;オリジナルコース15では「混乱少女♥そふらんちゃん!!」にちなんでゲージの減りが不規則
			;次の日のDAYについて、[約数の個数]%だけゲージが減る(「FLAG:段位ゲージ」では半分の値をとっていることに注意)
			;平方数の約数は真ん中の数を二重に数える(例:次が36日目…1,36と2,18と3,12と4,9と6,6で10%減少)
				FLAG:段位ゲージ -= SIGMA(DAY+1, 0)/2
				SIF ISSQUARE(DAY+1)
					FLAG:段位ゲージ -= 1
		CASE "HELL CHARGE NOTES"
			FOR LCOUNT, 0, CHARANUM
				ストレス値合計 += CFLAG:LCOUNT:ストレス値
			NEXT
			ストレス値合計 /= 2
			SIF ストレス値合計 < 1
				ストレス値合計 = 1
			FLAG:段位ゲージ -= ストレス値合計
		CASE "BisCoのEX-HARD☆大作戦"
			FLAG:段位ゲージ -= 5
		CASE "何かを間違えた房中術"
			FLAG:段位ゲージ -= 10
		CASEELSE
			FLAG:段位ゲージ--
	ENDSELECT
	IF FLAG:段位ゲージ <= 0
		CALL FADEOUTBGM
		CALL TEST_FAILED
	ENDIF
ENDIF

;段位ゲージに応じてBGMが変わる
@段位認定_BGM
SELECTCASE FLAG:段位ゲージ
	CASE 0 TO 11
		CALL BGM再生, 段位_4
	CASE 12 TO 25
		CALL BGM再生, 段位_3
	CASE 26 TO 38
		CALL BGM再生, 段位_2
	CASE 39 TO 50
		CALL BGM再生, 段位_1
ENDSELECT

;段位ゲージの表示
@段位ゲージ表示
CALL COLUMNPRINT, 0, @"段位ゲージ {FLAG:段位ゲージ*2}\%["
REPEAT FLAG:段位ゲージ
	IF GROUPMATCH(受験段位名, "BisCoのEX-HARD☆大作戦", "何かを間違えた房中術")
		CALL COLUMNPRINT, 0, "|", "orange"
	ELSE
		CALL COLUMNPRINT, 0, "|", "red"
	ENDIF
REND
REPEAT 50-FLAG:段位ゲージ
	CALL COLUMNPRINT, 0, "|", "gray"
REND
CALL COLUMNPRINT, 0, "]"

@GORILLA_ITEM
#DIM DYNAMIC LCOUNT
#DIMS SPLITVAR, 3
#DIM DYNAMIC 複数個あり
#DIM DYNAMIC 購入アイテム
{
#DIMS アイテムリスト = 
"ローター/2",
"オナホール/4",
"バイブ/4",
"アナルバイブ/6",
"アイマスク/4",
"縄/6",
"ビデオカメラ/10",
"ビデオテープ/6/3",
"ローション/2/5",
"媚薬/2",
}

DRAWLINE
CALL 段位ゲージ表示
PRINTL 
DRAWLINE
CALL ITEMPRINT
DRAWLINE
PRINTL □段位認定試験では通貨の代わりにゲージを消耗します。
PRINTL 　購入したいアイテムを選んでください。
PRINTL

FOR LCOUNT, 0, VARSIZE("アイテムリスト")
	SPLIT アイテムリスト:LCOUNT, "/", SPLITVAR
	複数個あり = RESULT == 3 ? 1 # 0
	SELECTCASE SPLITVAR:0
		CASE "共通"
			$共通
			PRINTFORML 　[{LCOUNT}] %SPLITVAR:0%\@ 複数個あり ? ×%SPLITVAR:2% # \@（ゲージ%SPLITVAR:1%\%）
		CASE "ビデオカメラ"
			SIF ITEM:ビデオカメラ
				CONTINUE
			PRINTFORML 　[{LCOUNT}] ビデオカメラ テープ×3付属（ゲージ%SPLITVAR:1%\%）
		CASE "ビデオテープ"
			SIF !ITEM:ビデオカメラ
				CONTINUE
			GOTO 共通
		CASE "ローション", "媚薬"
			GOTO 共通
		CASEELSE
			SIF ITEM:GETNUM(ITEM, SPLITVAR:0)
				CONTINUE
			GOTO 共通
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [10] 戻る

BINPUT
SIF RESULT == 10
	RETURN
購入アイテム = RESULT
SPLIT アイテムリスト:購入アイテム, "/", SPLITVAR
複数個あり = RESULT == 3 ? 1 # 0

IF FLAG:段位ゲージ < TOINT(SPLITVAR:1)/2
	PRINTW ゲージが足りません
	RESTART
ELSEIF SPLITVAR:0 == "ローション" && ITEM:ローション >= 95
	PRINTW ローションはこれ以上持てません
	RESTART
ELSEIF SPLITVAR:0 == "媚薬" && ITEM:媚薬 >= 99
	PRINTW 媚薬はこれ以上持てません
	RESTART
ENDIF

PRINTFORMW ＜%SPLITVAR:0%\@ 複数個あり ? ×%SPLITVAR:2% #\@を購入しました＞
ITEM:GETNUM(ITEM, SPLITVAR:0) += 複数個あり ? TOINT(SPLITVAR:2) # 1
FLAG:段位ゲージ -= TOINT(SPLITVAR:1)/2
SIF SPLITVAR:0 == "ビデオカメラ"
	ITEM:ビデオテープ += 3

RESTART

;数値に応じた段位を色付き文字で表示する関数 色変更するから式中関数化は無理
@GRADE, 段位値, モード表示
#DIMS DYNAMIC COLOR
#DIM DYNAMIC 段位値
#DIM DYNAMIC モード表示

SELECTCASE ERDNAME(段位名, 段位値)
	CASE "七級", "六級", "五級", "四級", "三級", "二級", "一級"
		COLOR = lime
	CASE "初段", "二段", "三段", "四段", "五段", "六段", "七段", "八段"
		COLOR = blue
	CASE "九段", "十段"
		COLOR = red
	CASE "中伝"
		COLOR = silver
	CASE "皆伝", "裏七級"
		COLOR = gold
	CASE "overjoy"
		COLOR = white
ENDSELECT

SIF モード表示
	CALL COLUMNPRINT, 0, "MODE:段位認定 "

;EDIT以外のオリジナルコースとoverjoyだけ特殊表示
IF !段位認定対象(段位値) && ERDNAME(段位名, 段位値) != "EDIT"
	CALL COLUMNPRINT, 0, "ORIGINAL", COLOR
ELSEIF ERDNAME(段位名, 段位値) == "overjoy"
	CALL COLUMNPRINT, 0, "(^^)", COLOR
ELSE
	CALL COLUMNPRINT, 0, ERDNAME(段位名, 段位値), COLOR
ENDIF
RESETCOLOR

;ヤンデレ
SIF ERDNAME(段位名, 段位値) == "ヤンデレの音ゲーキャラに死ぬほど愛されて眠れないコース"
	SETCOLORBYNAME red
