;段位認定における1日ごとのゲージの変動を管理
@段位ゲージ処理
#DIM DYNAMIC ストレス値合計
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LMONEY
#DIM DYNAMIC LGAUGE
#DIM DYNAMIC LSTOCK
#DIM DYNAMIC 攻略人数

SIF ターン == "昼"
	GOTO クリア判定

IF MONEY >= 4000
	;値を格納
	LMONEY = MONEY
	LGAUGE = FLAG:段位ゲージ*2
	LSTOCK = ストック
	PRINT 所持金を段位ゲージに換算しました。
	FLAG:段位ゲージ += MONEY/4000
	MONEY = MONEY%4000
	;このあと1減らすためゲージの最大は51(102%)
	IF FLAG:段位ゲージ > 51
		PRINT (※回復量がカンストしています)
		ストック += 4000*(FLAG:段位ゲージ-51)
		ストック -= 3999-MONEY
		FLAG:段位ゲージ = 51
		MONEY = 3999
		PRINTFORM ストック {LSTOCK} → {ストック}
	ENDIF
	PRINTL 
	PRINTFORML 所持金 {LMONEY} → {MONEY}
	PRINTFORML 段位ゲージ {LGAUGE} → {FLAG:段位ゲージ*2}
ENDIF

$クリア判定
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:売却可能 || CFLAG:LCOUNT:助手可能
		攻略人数++
	;裏七級の処理(奴隷を崩壊させると即終了)
	SIF 受験段位名 == "裏七級" && TALENT:LCOUNT:崩壊
		FLAG:段位ゲージ = 0
NEXT

;クリア判定
;まずは特殊勝利から
IF 受験段位名 == "いちかのアダルトチャンネル"
	IF ストック >= 100000
		CALL FADEOUTBGM
		CALL TEST_CLEAR
	ENDIF
;通常のクリア条件
ELSE
	IF 攻略人数 >= (CHARANUM-1)
		CALL FADEOUTBGM
		CALL TEST_CLEAR
	ENDIF
ENDIF
SIF ターン == "昼"
	RETURN

;上のクリア判定をスルーした場合、ゲージを減らす処理が入る
IF 受験段位名 == "少女達の芳香は艶やかに、猛き賓客を癒し惑わす。"
;オリジナルコース15では「混乱少女♥そふらんちゃん!!」にちなんでゲージの減りが不規則
;次の日のDAYについて、[約数の個数]%だけゲージが減る(「FLAG:段位ゲージ」では半分の値をとっていることに注意)
;平方数の約数は真ん中の数を二重に数える(例:次が36日目…1,36と2,18と3,12と4,9と6,6で10%減少)
	FLAG:段位ゲージ -= SIGMA(DAY+1, 0)/2
	SIF ISSQUARE(DAY+1)
		FLAG:段位ゲージ -= 1
ELSEIF 受験段位名 == "HELL CHARGE NOTES"
	FOR LCOUNT, 0, CHARANUM
		ストレス値合計 += CFLAG:LCOUNT:ストレス値
	NEXT
	ストレス値合計 /= 2
	SIF ストレス値合計 < 1
		ストレス値合計 = 1
	FLAG:段位ゲージ -= ストレス値合計
ELSEIF 受験段位名 == "BisCoのEX-HARD☆大作戦"
	FLAG:段位ゲージ -= 5
ELSEIF 受験段位名 == "何かを間違えた房中術"
	FLAG:段位ゲージ -= 10
ELSE
	FLAG:段位ゲージ -= 1
ENDIF
IF FLAG:段位ゲージ <= 0
	CALL FADEOUTBGM
	CALL TEST_FAILED
ENDIF

;段位ゲージに応じてBGMが変わる
@段位認定_BGM
SELECTCASE FLAG:段位ゲージ
	CASE 0 TO 11
		CALL BGM再生, 段位_4
	CASE 12 TO 25
		CALL BGM再生, 段位_3
	CASE 26 TO 38
		CALL BGM再生, 段位_2
	CASE 39 TO 50
		CALL BGM再生, 段位_1
ENDSELECT

;段位ゲージの表示
@段位ゲージ表示
#DIM DYNAMIC EXH
#DIMS DYNAMIC COLOR
SELECTCASE 受験段位名
	CASE "BisCoのEX-HARD☆大作戦", "何かを間違えた房中術"
		EXH = 1
ENDSELECT
CALL COLUMNPRINT, 0, @"段位ゲージ {FLAG:段位ゲージ*2}\%["
REPEAT FLAG:段位ゲージ
	IF EXH
		COLOR = orange
		;SETCOLOR 220, 255, 0
	ELSE
		COLOR = red
		;SETCOLOR 255, 0, 0
	ENDIF
	CALL COLUMNPRINT, 0, "|", COLOR
REND
REPEAT 50-FLAG:段位ゲージ
	COLOR = gray
	;SETCOLOR 128, 128, 128
	CALL COLUMNPRINT, 0, "|", COLOR
REND
CALL COLUMNPRINT, 0, "]"

@GORILLA_ITEM
DRAWLINE
CALL 段位ゲージ表示
PRINTL 
DRAWLINE
CALL ITEMPRINT
DRAWLINE
PRINTL □段位認定試験では通貨の代わりにゲージを消耗します。
PRINTL 　購入したいアイテムを選んでください。
PRINTL
SIF !ITEM:ローター
	PRINTL 　[0] ローター（ゲージ2%）
SIF !ITEM:オナホール
	PRINTL 　[1] オナホール（ゲージ4%）
SIF !ITEM:バイブ
	PRINTL 　[2] バイブ（ゲージ4%）
SIF !ITEM:アナルバイブ
	PRINTL 　[3] アナルバイブ（ゲージ6%）
SIF !ITEM:アイマスク
	PRINTL 　[4] アイマスク（ゲージ4%）
SIF !ITEM:縄
	PRINTL 　[5] 縄（ゲージ6%）
SIF !ITEM:ビデオカメラ
	PRINTL 　[6] ビデオカメラ テープ×3付属（ゲージ10%）
SIF ITEM:ビデオカメラ
	PRINTL 　[7] ビデオテープ×3（ゲージ6%）
PRINTL 　[8] ローション×5（ゲージ2%）
PRINTL 　[9] 媚薬（ゲージ2%）
DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [10] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		IF FLAG:段位ゲージ > 1
			PRINTW ＜ローターを購入しました＞
			ITEM:ローター = 1
			FLAG:段位ゲージ -= 1
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 1
		IF FLAG:段位ゲージ > 2
			PRINTW ＜オナホールを購入しました＞
			ITEM:オナホール = 1
			FLAG:段位ゲージ -= 2
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 2
		IF FLAG:段位ゲージ > 2
			PRINTW ＜バイブを購入しました＞
			ITEM:バイブ = 1
			FLAG:段位ゲージ -= 2
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 3
		IF FLAG:段位ゲージ > 3
			PRINTW ＜アナルバイブを購入しました＞
			ITEM:アナルバイブ = 1
			FLAG:段位ゲージ -= 3
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 4
		IF FLAG:段位ゲージ > 2
			PRINTW ＜アイマスクを購入しました＞
			ITEM:アイマスク = 1
			FLAG:段位ゲージ -= 2
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 5
		IF FLAG:段位ゲージ > 3
			PRINTW ＜縄を購入しました＞
			ITEM:縄 = 1
			FLAG:段位ゲージ -= 3
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 6
		IF FLAG:段位ゲージ > 5
			PRINTW ＜ビデオカメラを購入しました＞
			ITEM:ビデオカメラ = 1
			ITEM:ビデオテープ += 3
			FLAG:段位ゲージ -= 5
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 7
		IF FLAG:段位ゲージ > 3
			PRINTW ＜ビデオテープ×3を購入しました＞
			ITEM:ビデオテープ += 3
			FLAG:段位ゲージ -= 3
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 8
		IF ITEM:ローション >= 95
			PRINTW ローションはこれ以上持てません
		ELSEIF FLAG:段位ゲージ > 1
			PRINTW ＜ローション×5を購入しました＞
			ITEM:ローション += 5
			FLAG:段位ゲージ -= 1
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 9
		IF ITEM:媚薬 >= 99
			PRINTW 媚薬はこれ以上持てません
		ELSEIF FLAG:段位ゲージ > 1
			PRINTW ＜媚薬を購入しました＞
			ITEM:媚薬 += 1
			FLAG:段位ゲージ -= 1
		ELSE
			PRINTW ゲージが足りません
		ENDIF
	CASE 10
		RETURN 0
ENDSELECT

RESTART

;数値に応じた段位を色付き文字で表示する関数 色変更するから式中関数化は無理
@GRADE, ARG, モード表示
#DIMS DYNAMIC COLOR
#DIM DYNAMIC モード表示

SELECTCASE ARG
	;緑段位
	CASE 1 TO 7
		COLOR = lime
		;SETCOLOR 140, 240, 140
	;青段位
	CASE 11 TO 18
		COLOR = blue
		;SETCOLOR 170, 220, 230
	;赤段位
	CASE 19, 20
		COLOR = red
		;SETCOLOR 250, 150, 150
	;中伝
	CASE 30
		COLOR = silver
		;SETCOLOR 240, 240, 240
	;皆伝、裏七級
	CASE 573, 50
		COLOR = gold
		;SETCOLOR 250, 180, 0
	;overjoy
	CASE 999
		COLOR = white
		;SETCOLOR 255, 255, 255
ENDSELECT

SIF モード表示
	CALL COLUMNPRINT, 0, "MODE:段位認定 "

SELECTCASE ARG
	;緑段位
	CASE 1, 50
		CALL COLUMNPRINT, 0, "七級", COLOR
	CASE 2
		CALL COLUMNPRINT, 0, "六級", COLOR
	CASE 3
		CALL COLUMNPRINT, 0, "五級", COLOR
	CASE 4
		CALL COLUMNPRINT, 0, "四級", COLOR
	CASE 5
		CALL COLUMNPRINT, 0, "三級", COLOR
	CASE 6
		CALL COLUMNPRINT, 0, "二級", COLOR
	CASE 7
		CALL COLUMNPRINT, 0, "一級", COLOR
	;青段位
	CASE 11
		CALL COLUMNPRINT, 0, "初段", COLOR
	CASE 12
		CALL COLUMNPRINT, 0, "二段", COLOR
	CASE 13
		CALL COLUMNPRINT, 0, "三段", COLOR
	CASE 14
		CALL COLUMNPRINT, 0, "四段", COLOR
	CASE 15
		CALL COLUMNPRINT, 0, "五段", COLOR
	CASE 16
		CALL COLUMNPRINT, 0, "六段", COLOR
	CASE 17
		CALL COLUMNPRINT, 0, "七段", COLOR
	CASE 18
		CALL COLUMNPRINT, 0, "八段", COLOR
	;赤段位
	CASE 19
		CALL COLUMNPRINT, 0, "九段", COLOR
	CASE 20
		CALL COLUMNPRINT, 0, "十段", COLOR
	;中伝・皆伝
	CASE 30
		CALL COLUMNPRINT, 0, "中伝", COLOR
	CASE 573
		CALL COLUMNPRINT, 0, "皆伝", COLOR
	;overjoy
	CASE 999
		CALL COLUMNPRINT, 0, "(^^)", COLOR
	;EDIT,無名
	CASE 99
		CALL COLUMNPRINT, 0, "EDIT", COLOR
	CASE 0
		CALL COLUMNPRINT, 0, "無名", COLOR
	;オリジナルコース
	CASE 51 TO 90
		CALL COLUMNPRINT, 0, "ORIGINAL", COLOR
ENDSELECT

RESETCOLOR

;ヤンデレ
SIF ARG == 80
	SETCOLORBYNAME red
