@友人を昼食に誘う
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRAND
#DIM DYNAMIC 候補人数
#DIM DYNAMIC 候補, 1000
#DIM DYNAMIC 好感度ボーダー

IF CSTR:PLAYER:在校 == CSTR:PLAYER:現在地
	好感度ボーダー = 100
ELSE
	好感度ボーダー = 500
ENDIF

PRINTL □誰を誘いますか？

FOR LCOUNT, 1, CHARANUM
	;同じ学校の中から
	SIF CSTR:LCOUNT:在校 != CSTR:PLAYER:現在地 && CSTR:LCOUNT:在任 != CSTR:PLAYER:現在地
		CONTINUE
	;好感度の高いキャラを選出
	IF 好感度合計(LCOUNT) >= 好感度ボーダー
		IF CSTR:LCOUNT:現在地 != CSTR:LCOUNT:在校 && CSTR:LCOUNT:現在地 != CSTR:LCOUNT:在任
			SETCOLORBYNAME gray
			PRINT 　[---] - 
			CALL SNAME(LCOUNT)
			SETCOLORBYNAME gray
			PRINTL (学校をサボっている)
			RESETCOLOR
		ELSE
			PRINTFORM 　[{LCOUNT, 3}] - 
			CALL SNAME(LCOUNT, 1)
			PRINTL 
		ENDIF
		候補人数++
	ENDIF
NEXT

IF 候補人数 == 0
	PRINTL 
	PRINTL 　----------友人が居ません----------
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAIN 　友人を昼食に誘う　
PRINTL [0] - やめる

BINPUT
SIF RESULT == 0
	RETURN 0

TARGET = RESULT

SIF 授業中
	授業参加フラグ = 1

IF CSTR:在任 != ""
	PRINTFORML %CALLNAME:TARGET%先生と一緒に昼食を食べた
	CALL SCHOOL_KOJO, TARGET, "一緒に昼食"
	CALL 好感度上昇, TARGET, "舎弟", 10
	CALL 好感度上昇, TARGET, "友情", 10
ELSE
	PRINTFORML %CALLNAME:TARGET%と一緒に昼食を食べた
	CALL SCHOOL_KOJO, TARGET, "一緒に昼食"
	CALL 好感度上昇, TARGET, "友情", 20
ENDIF
WAIT

;会話後に授業に参加する
IF 授業参加フラグ
	PRINTL
	PRINTFORML 昼食後、%NAME:授業中%先生の授業を受けた
	CALL 好感度上昇, 授業中, "舎弟", 2
	WAIT
ENDIF

RETURN 1
