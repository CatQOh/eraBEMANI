@何もせずサボる
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 訪問者
#DIM DYNAMIC LRESULT

PRINTFORMW %CALLNAME:PLAYER%は今日は学校をサボることにした……
DRAWLINE

CSTR:PLAYER:現在地 = 自宅

;同じ学校で連絡先を知ってるキャラから連絡が来ることがある
FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:在校 == CSTR:PLAYER:在校 && 連絡先入手済み(LCOUNT)
		;確率は 1/同じ学校の在校生の人数
		IF RAND:CMATCH(CSTR:在校, CSTR:PLAYER:在校) == 0
			IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:LCOUNT)%_コマンド_サボり中に連絡")
				CALL SCHOOL_KOJO, LCOUNT, "コマンド_サボり中に連絡"
				RETURN RESULT
			ENDIF

			CALL SCHOOL_KOJO, LCOUNT, "サボり中に連絡"
			PRINTL [0] - 大丈夫だと伝える
			PRINTL [1] - 具合が悪いから看病してほしいと言う
			PRINTL [2] - 正直にサボりだと答える
			BINPUT
			LRESULT = RESULT
			SELECTCASE LRESULT
				CASE 0
					CALL SCHOOL_KOJO, LCOUNT, "大丈夫と伝える"
					CALL 好感度上昇, LCOUNT, "友情", 10
				CASE 1
					;好感度が高いと聞き入れてもらえる
					IF 好感度合計(LCOUNT) >= 500
						CALL SCHOOL_KOJO, LCOUNT, "仮病を装う_成功"
						訪問者 = LCOUNT
					ELSE
						CALL SCHOOL_KOJO, LCOUNT, "仮病を装う_失敗"
					ENDIF
				CASE 2
					;サボり魔持ちにはサボりが伝染する
					IF TALENT:LCOUNT:サボり魔
						CALL SCHOOL_KOJO, LCOUNT, "サボりと伝える"
						訪問者 = LCOUNT
					;好感度が高いと許される
					ELSEIF 好感度合計(LCOUNT) >= 500
						CALL SCHOOL_KOJO, LCOUNT, "サボりと伝える"
					ELSE
						CALL SCHOOL_KOJO, LCOUNT, "サボりと伝える"
						CALL 好感度上昇, LCOUNT, "友情", -10
					ENDIF
			ENDSELECT
			WAIT
			BREAK
		ENDIF
	ENDIF
NEXT

;上記の処理で訪問者が居る場合
選択肢 = LRESULT
IF 訪問者
	SELECTCASE LRESULT
		;看病を要請
		CASE 1
			CSTR:訪問者:現在地 = %CALLNAME:PLAYER%の家
			CSTR:訪問者:目的 = 看病
			同行者 = 訪問者
			CALL SCHOOL_KOJO, 訪問者, "自宅に来訪"
		;サボり
		CASE 2
			CSTR:訪問者:現在地 = %CALLNAME:PLAYER%の家
			CSTR:訪問者:目的 = 遊び
			同行者 = 訪問者
			CALL SCHOOL_KOJO, 訪問者, "自宅に来訪"
	ENDSELECT
ENDIF

RETURN 1

@SCHOOL_MESSAGE_サボり中に連絡
PRINTFORML 学校を休んだ%CALLNAME:PLAYER%を心配してか、%CALLNAME:TARGET%が電話をかけてきた

@SCHOOL_MESSAGE_大丈夫と伝える
;素質で反応が変わる
IF TALENT:強気 || TALENT:ツンデレ
	PRINTFORMW %CALLNAME:PLAYER%が大丈夫と答えると、%CALLNAME:TARGET%は心配して損したといった様子で電話を切ってしまった……
ELSEIF TALENT:弱気 || TALENT:寂しがり屋
	PRINTFORMW %CALLNAME:PLAYER%は大丈夫と答えたものの、%CALLNAME:TARGET%は未だ不安げな様子のまま通話を終えた
ELSEIF TALENT:冷静 || TALENT:無関心
	PRINTFORMW %CALLNAME:PLAYER%が大丈夫と答えると、それを確認した%CALLNAME:TARGET%は早々に通話を切った
ELSE
	PRINTFORMW %CALLNAME:PLAYER%が大丈夫と答えると、%CALLNAME:TARGET%は安心した様子で通話を終えた
ENDIF


@SCHOOL_MESSAGE_仮病を装う_失敗
;素質で反応が変わる
IF TALENT:強気 || TALENT:ツンデレ
	PRINTFORML %CALLNAME:PLAYER%は仮病を装うが、%CALLNAME:TARGET%には無情にも拒否されてしまった……
ELSEIF TALENT:弱気 || TALENT:寂しがり屋
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うが、%CALLNAME:TARGET%はそっちに向かえないと言って謝られてしまった……
ELSEIF TALENT:冷静 || TALENT:無関心
	PRINTFORML %CALLNAME:PLAYER%は仮病を装うが、%CALLNAME:TARGET%には簡単に見抜かれ通話を切られてしまった……
ELSE
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うが、%CALLNAME:TARGET%には授業があるから無理と断られてしまった……
ENDIF

@SCHOOL_MESSAGE_仮病を装う_成功
;素質で反応が変わる
IF TALENT:強気 || TALENT:ツンデレ
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うと、%CALLNAME:TARGET%は呆れた様子で、後でそちらに向かうと答えた
ELSEIF TALENT:弱気 || TALENT:寂しがり屋
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うと、%CALLNAME:TARGET%は今にも駆け出しそうな慌てぶりで、看病に向かうと答えた
ELSEIF TALENT:冷静 || TALENT:無関心
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うと、%CALLNAME:TARGET%はその様子を疑いつつも、後でそちらに向かうと答えた
ELSE
	PRINTFORML %CALLNAME:PLAYER%が仮病を装うと、%CALLNAME:TARGET%は心配した様子で、後でそちらに向かうと答えた
ENDIF

@SCHOOL_MESSAGE_サボりと伝える
;サボり魔持ちにはサボりが伝染する
IF TALENT:サボり魔
	PRINTFORML %CALLNAME:PLAYER%が正直にサボりだと答えると、%CALLNAME:TARGET%も笑いながら自分もサボると言ってきた
;好感度が高いと許される
ELSEIF 好感度合計(TARGET) >= 500
	PRINTFORML %CALLNAME:PLAYER%が正直にサボりだと答えると、%CALLNAME:TARGET%は呆れた様子で通話を終えた
ELSE
	PRINTFORML %CALLNAME:PLAYER%が正直にサボりだと答えると、%CALLNAME:TARGET%にサボったことを咎められてしまった……
ENDIF

@SCHOOL_MESSAGE_自宅に来訪
SELECTCASE 選択肢
	;看病を要請
	CASE 1
		PRINTFORMW その後、授業を終えた%CALLNAME:TARGET%が家まで看病に来てくれた
	;サボり
	CASE 2
		PRINTFORMW その後、授業をサボった%CALLNAME:TARGET%が%CALLNAME:PLAYER%の家にやってきた
ENDSELECT
