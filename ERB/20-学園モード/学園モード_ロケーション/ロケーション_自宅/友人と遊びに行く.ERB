@友人と遊びに行く
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIM DYNAMIC LRESULT
#DIMS DYNAMIC 名称抜き取り用, 2
#DIMS DYNAMIC 目的地

PRINTL □誰を誘いますか？
;連絡先を持っているキャラを羅列
FOR LCOUNT, 1, CHARANUM
	SIF !連絡先入手済み(LCOUNT)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 3}] - 
	CALL SNAME(LCOUNT)
	PRINTL 
NEXT

IF MAXARRAY(連絡先) == 0
	PRINTL 
	PRINTL 　----------連絡先を知っている相手が居ません----------
	PRINTL 
ENDIF

DRAWLINE
PRINTL 　[0] - やっぱりやめる

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASEELSE
		対象 = RESULT
		CALL SCHOOL_KOJO, 対象, "待ち合わせ"
		PRINTL どこで待ち合わせますか？
		CALL ロケーション一覧
		DRAWLINE
		PRINTPLAIN 　待ち合わせ　
		PRINTL [10000] - 用事を思い出した
		BINPUT
		LRESULT = RESULT
		SELECTCASE LRESULT
			CASE 10000
				RETURN 0
			CASEELSE
				SPLIT ロケーション:LRESULT, "/", 名称抜き取り用
				;分割されてたらスラッシュより後の部分だけを使用する
				IF RESULT >= 2
					目的地 = %名称抜き取り用:1%
				ELSE
					目的地 = %ロケーション:LRESULT%
				ENDIF

				PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:対象%と%目的地%で待ち合わせし、そこに向かった
				CSTR:PLAYER:現在地 = %目的地%
				DRAWLINE
				CALL SCHOOL_KOJO, 対象, "合流"
				CALL 好感度上昇, 対象, "友情", 5
				CALL 好感度上昇, 対象, "ときめき", 5
				同行者 = 対象
				WAIT
		ENDSELECT
ENDSELECT

RETURN 0

@SCHOOL_MESSAGE_待ち合わせ
PRINTFORML %CALLNAME:TARGET%と連絡を取り、待ち合わせをすることにした

@SCHOOL_MESSAGE_合流
PRINTFORMW %CSTR:PLAYER:現在地%で%CALLNAME:TARGET%と合流しました
