@家に友人を呼ぶ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIM DYNAMIC 必要値

PRINTL □誰を呼びますか？
;連絡先を持っているキャラを羅列
FOR LCOUNT, 1, CHARANUM
	SIF !連絡先入手済み(LCOUNT)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 3}] - 
	CALL SNAME(LCOUNT)
	PRINTL 
NEXT

IF MAXARRAY(連絡先) == 0
	PRINTL 
	PRINTL 　----------連絡先を知っている相手が居ません----------
	PRINTL 
ENDIF

DRAWLINE
PRINTL 　[0] - やっぱりやめる

BINPUT

SIF RESULT == 0
	RETURN 0

対象 = RESULT
時間経過なし = 0

IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:対象)%_コマンド_家に友人を呼ぶ")
	CALL SCHOOL_KOJO, 対象, "コマンド_家に友人を呼ぶ"
	RETURN !時間経過なし
ENDIF

PRINTFORML %CALLNAME:対象%をどういった名目で呼びますか？
PRINTL [1] - うちで遊ぼうと提案する
PRINTL [2] - 有り余る劣情を伝える
PRINTL [0] - やっぱりやめる

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		IF CSTR:対象:在校 != "" && CSTR:対象:現在地 == CSTR:対象:在校
			IF TALENT:対象:サボり魔
				CALL SCHOOL_KOJO, 対象, "呼び出し_成功"
				CSTR:対象:現在地 = %CALLNAME:PLAYER%の家
				CSTR:対象:目的 = 遊び
				同行者 = 対象
			ELSE
				CALL SCHOOL_KOJO, 対象, "呼び出し_失敗"
				RETURN 0
			ENDIF
		ELSEIF CSTR:対象:在任 != "" && CSTR:対象:現在地 == CSTR:対象:在任
			IF GROUPMATCH(ターン, "昼")
				CALL SCHOOL_KOJO, 対象, "呼び出し_失敗"
			ELSE
				CALL SCHOOL_KOJO, 対象, "呼び出し_失敗"
			ENDIF
			RETURN 0
		ELSE
			IF ターン == "夜"
				;異性の場合は友情値は2倍必要
				必要値 = 300
				SIF SEX(PLAYER) != SEX(対象)
					必要値 *= 2
				IF TALENT:対象:恋慕 || 好感度:対象:友情 >= 必要値 || 好感度:対象:ときめき >= 1000 || 好感度:対象:セフレ >= 1000
					CSTR:対象:現在地 = %CALLNAME:PLAYER%の家
					CSTR:対象:目的 = 遊び
					CALL SCHOOL_KOJO, 対象, "呼び出し_成功"
					同行者 = 対象
					JUMP 宿泊イベント
				ELSE
					CALL SCHOOL_KOJO, 対象, "呼び出し_失敗"
					RETURN 0
				ENDIF
			ELSE
				CSTR:対象:現在地 = %CALLNAME:PLAYER%の家
				CSTR:対象:目的 = 遊び
				CALL SCHOOL_KOJO, 対象, "呼び出し_成功"
				同行者 = 対象
			ENDIF
		ENDIF
	CASE 2
		;地の文を別関数に移した問題で変なIFの使い方になっている
		IF CSTR:対象:在校 != "" && CSTR:対象:現在地 == CSTR:対象:在校
			IF 好感度:対象:既成事実
				IF TALENT:対象:恋慕
					IF TALENT:対象:サボり魔 || CFLAG:対象:倫理観 >= 50
					ELSE
						CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
						RETURN 0
					ENDIF
				ELSE
					IF TALENT:対象:サボり魔 || CFLAG:対象:倫理観 >= 50
					ELSE
						CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
						RETURN 0
					ENDIF
				ENDIF
			ELSEIF CFLAG:対象:倫理観 >= 80
			ELSEIF 好感度:対象:セフレ >= 1000
				IF TALENT:対象:サボり魔
				ELSE
					CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
					RETURN 0
				ENDIF
			ELSE
				IF CFLAG:対象:倫理観 >= 30
					CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
				ELSE
					CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
					CALL 好感度上昇, 対象, "友情", -5
					CALL 好感度上昇, 対象, "ときめき", -5
				ENDIF
				RETURN 0
			ENDIF
			CALL SCHOOL_KOJO, 対象, "劣情を伝える_成功"
			CSTR:対象:現在地 = %CALLNAME:PLAYER%の家
			CSTR:対象:目的 = セックス
			同行者 = 対象
		ELSEIF CSTR:対象:在任 != "" && CSTR:対象:現在地 == CSTR:対象:在任
			IF GROUPMATCH(ターン, "昼")
				IF 好感度:対象:既成事実
				ELSE
					CALL 好感度上昇, 対象, "友情", -5
					CALL 好感度上昇, 対象, "ときめき", -5
				ENDIF
			ELSE
				IF 好感度:対象:既成事実
				ELSE
					CALL 好感度上昇, 対象, "友情", -5
					CALL 好感度上昇, 対象, "ときめき", -5
				ENDIF
			ENDIF
			CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
			RETURN 0
		ELSE
			IF 好感度:対象:既成事実
			ELSEIF CFLAG:対象:倫理観 >= 80
			ELSEIF 好感度:対象:セフレ >= 1000
			ELSE
				IF CFLAG:対象:倫理観 >= 30
					CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
				ELSE
					CALL SCHOOL_KOJO, 対象, "劣情を伝える_失敗"
					CALL 好感度上昇, 対象, "友情", -5
					CALL 好感度上昇, 対象, "ときめき", -5
				ENDIF
				RETURN 0
			ENDIF
			CALL SCHOOL_KOJO, 対象, "劣情を伝える_成功"
			CSTR:対象:現在地 = %CALLNAME:PLAYER%の家
			CSTR:対象:目的 = セックス
			同行者 = 対象
			SIF ターン == "夜"
				JUMP 宿泊イベント
			RETURN !時間経過なし
		ENDIF
ENDSELECT
RETURN !時間経過なし

@SCHOOL_MESSAGE_呼び出し_失敗
PRINTFORMW %CALLNAME:TARGET%に、%CALLNAME:PLAYER%の家で遊ぼうと連絡した
IF CSTR:在校 != "" && CSTR:現在地 == CSTR:在校
	PRINTFORMW しかし今は学校に居るから行けないと断られてしまった……
ELSEIF CSTR:在任 != "" && CSTR:現在地 == CSTR:在任
	IF GROUPMATCH(ターン, "昼")
		PRINTFORMW しかし%CALLNAME:TARGET%は学校をサボって遊ぼうとしている%CALLNAME:PLAYER%を咎めるだけで終わった……
	ELSE
		PRINTFORMW しかしまだ仕事が終わっていないと断られてしまった……
	ENDIF
ELSE
	SIF ターン == "夜"
		PRINTFORMW しかし今日はもう遅いからやめておこうと%CALLNAME:TARGET%に諭されてしまった……
ENDIF

@SCHOOL_MESSAGE_呼び出し_成功
PRINTFORMW %CALLNAME:TARGET%に、%CALLNAME:PLAYER%の家で遊ぼうと連絡した
IF CSTR:在校 != "" && CSTR:現在地 == CSTR:在校
	SIF TALENT:サボり魔
		PRINTFORMW %CALLNAME:TARGET%は学校に居るようだが、サボってその提案に乗るといった旨の返事をしてきた
ELSEIF CSTR:在任 != "" && CSTR:現在地 == CSTR:在任
	IF GROUPMATCH(ターン, "昼")
		PRINTFORMW しかし%CALLNAME:TARGET%は学校をサボって遊ぼうとしている%CALLNAME:PLAYER%を咎めるだけで終わった……
	ELSE
		PRINTFORMW しかしまだ仕事が終わっていないと断られてしまった……
	ENDIF
ELSE
	IF ターン == "夜"
		PRINTFORMW 夜は遅いが、%CALLNAME:TARGET%は%CALLNAME:PLAYER%の家に泊まることを提案し、しばらくして%CALLNAME:PLAYER%の家にやってきた
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は肯定的な返事をすると、しばらく時間をおいて%CALLNAME:PLAYER%の家にやってきた
	ENDIF
ENDIF

@SCHOOL_MESSAGE_劣情を伝える_失敗
PRINTFORMW %CALLNAME:TARGET%に、有り余る劣情を伝えてみた
IF CSTR:在校 != "" && CSTR:現在地 == CSTR:在校
	IF 好感度:既成事実
		IF TALENT:恋慕
			PRINTFORMW %CALLNAME:TARGET%も%CALLNAME:PLAYER%に対する惚気を伝えてくるが、今は学校に居るから行けないと断られてしまった……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%はさすがに学業を疎かにはできないとその提案を断った……
		ENDIF
	ELSEIF 好感度:セフレ >= 1000
		IF TALENT:サボり魔
			PRINTFORMW %CALLNAME:TARGET%は学校に居るようだが、日頃のアプローチが功を成したか興味がある旨の返事をしてきた
			PRINTFORMW しばらくすると、%CALLNAME:TARGET%が%CALLNAME:PLAYER%の家にやってきた
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は学校に居るため断られてしまったが、放課後や休日に誘ってほしいとオブラートに包んだ返事をしてきた
			RETURN 0
		ENDIF
	ELSE
		IF CFLAG:倫理観 >= 30
			PRINTFORMW %CALLNAME:TARGET%は学校に居るからと断り、ついでにまだそんな関係ではないと釘を刺してきた
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は若干引き気味に、今は学校に居るから行けないと断ってきた……
		ENDIF
	ENDIF
ELSEIF CSTR:在任 != "" && CSTR:現在地 == CSTR:在任
	IF GROUPMATCH(ターン, "昼")
		IF 好感度:既成事実
			PRINTFORMW しかし%CALLNAME:TARGET%は性行為だけでなく授業にも専念するようにと%CALLNAME:PLAYER%を咎めるだけで終わった……
		ELSE
			PRINTFORMW しかし%CALLNAME:TARGET%は%CALLNAME:PLAYER%のアプローチは無視して、学校をサボることを咎めるだけで終わった……
		ENDIF
	ELSE
		IF 好感度:既成事実
			PRINTFORMW しかしまだ仕事が終わっていないと断られてしまった……
		ELSE
			PRINTFORMW しかし%CALLNAME:TARGET%は%CALLNAME:PLAYER%のアプローチは無視して、まだ仕事が終わっていないと断った……
		ENDIF
	ENDIF
ELSE
	IF CFLAG:倫理観 >= 30
		PRINTFORMW しかし%CALLNAME:TARGET%は平然と断り、ついでにまだそんな関係ではないと釘を刺してきた
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は若干引き気味にその提案を断ってきた……
	ENDIF
ENDIF

@SCHOOL_MESSAGE_劣情を伝える_成功
PRINTFORMW %CALLNAME:TARGET%に、有り余る劣情を伝えてみた
IF CSTR:在校 != "" && CSTR:現在地 == CSTR:在校
	IF 好感度:既成事実
		IF TALENT:恋慕
			SIF TALENT:サボり魔 || CFLAG:倫理観 >= 50
				PRINTFORMW %CALLNAME:TARGET%は学校に居るようだが、自身の抑えきれない欲望と共に学校を抜け出す旨を伝えてきた
		ELSE
			IF TALENT:サボり魔 || CFLAG:倫理観 >= 50
				PRINTFORMW %CALLNAME:TARGET%は学校に居るようだが、学校をサボってそちらに向かうと二つ返事で了承した
				PRINTFORMW しばらくすると、%CALLNAME:TARGET%が%CALLNAME:PLAYER%の家にやってきた
			ENDIF
		ENDIF
	ELSEIF CFLAG:倫理観 >= 80
		PRINTFORMW %CALLNAME:PLAYER%の遠回しなアプローチを察したのか、%CALLNAME:TARGET%は興味があると承諾した
		PRINTFORMW しばらくすると、%CALLNAME:TARGET%が%CALLNAME:PLAYER%の家にやってきた
	ELSEIF 好感度:セフレ >= 1000
		IF TALENT:サボり魔
			PRINTFORMW %CALLNAME:TARGET%は学校に居るようだが、日頃のアプローチが功を成したか興味がある旨の返事をしてきた
			PRINTFORMW しばらくすると、%CALLNAME:TARGET%が%CALLNAME:PLAYER%の家にやってきた
		ENDIF
	ENDIF
ELSE
	IF 好感度:既成事実
		IF TALENT:恋慕
			PRINTFORMW %CALLNAME:TARGET%はその提案に同じく情熱的な返事をすると、しばらく時間をおいて%CALLNAME:PLAYER%の家にやってきた
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は肯定的な返事をすると、しばらく時間をおいて%CALLNAME:PLAYER%の家にやってきた
		ENDIF
	ELSEIF CFLAG:倫理観 >= 80
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の遠回しなアプローチに乗り気で返事をすると、しばらく時間をおいて%CALLNAME:PLAYER%の家にやってきた
	ELSEIF 好感度:セフレ >= 1000
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の欲望を受け取ると自身の興味も満たしたいと返事をし、しばらく時間をおいて%CALLNAME:PLAYER%の家にやってきた
	ENDIF
ENDIF
