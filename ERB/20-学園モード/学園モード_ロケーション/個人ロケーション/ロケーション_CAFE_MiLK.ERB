@ロケーション条件_CAFE_MiLK
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"ラズ",
"シフォン",
"ソルト"
}

ロケーション説明 = ラズ、シフォン、ソルトが経営しているカフェ。彼女らが通学中は休業
ロケーション名記録用 = CAFE MiLK

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@ロケーション可否_CAFE_MiLK
;時間帯などで使えない場合は理由を書く
SIF !GROUPMATCH("", CSTR:GETCHARANAME("ラズ"):現在地, CSTR:GETCHARANAME("シフォン"):現在地, CSTR:GETCHARANAME("ソルト"):現在地)
	ロケーション可否 = スタッフが通学中

@バイト条件_CAFE_MiLK
;バイトの時間帯などを書く
バイト要項 = 夕方～夜のシフトで募集中
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "夕", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "CAFE MiLK") >= 0
	RETURN GETCHARANAME("ラズ")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_CAFE_MiLK, キャラ取得
#DIM DYNAMIC 対象
#DIM DYNAMIC キャラ取得
{
#DIMS DYNAMIC 出現キャラ =
"ラズ",
"シフォン",
"ソルト",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF !キャラ取得
	CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_CAFE_MiLK
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;3人組
		CASE "ラズ", "シフォン", "ソルト"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;しゃまみるく
		CASE "しゃま", "みるく"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～4人で行動している
				;無所属、教師陣のグループは休日のみ現れる
				IF 休日()
					CALL グループキャラ選出, LCOUNT, "相性"
				;あとは放課後等
				ELSEIF CSTR:LCOUNT:在校 != ""
					CALL クラスメイト選出, LCOUNT, "相性"
				ELSE
					CONTINUE
				ENDIF
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				IF 学園_カフェ系キャラ(CHARANAMEF(LCOUNT))
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 20
				ELSEIF TALENT:LCOUNT:食欲皆伝
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 10
				ENDIF
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@CAFE_MiLK_専用コマンド
IF 同行者
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とお茶する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  2] - %CALLNAME:同行者%とお茶する(1000＄)"
	ENDIF
ELSE
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーを注文する(500＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - コーヒーを注文する(500＄)"
	ENDIF
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーと軽食を注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  1] - コーヒーと軽食を注文する(1000＄)"
	ENDIF
ENDIF

@CAFE_MiLK_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
CALL 職場出現キャラ_CAFE_MiLK
対象 = RESULT
SELECTCASE CRESULT:1
	CASE 0
		CALL SCHOOL_KOJO, 対象, "CAFE_MiLKで注文"
		CALL 好感度上昇, 対象, "友情", 15
		MONEY -= 500
	CASE 1
		CALL SCHOOL_KOJO, 対象, "CAFE_MiLKで注文"
		CALL 好感度上昇, 対象, "友情", 30
		MONEY -= 1000
	CASE 2
		CALL SCHOOL_KOJO, 同行者, "CAFE_MiLKでお茶する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 1000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_CAFE_MiLKで注文
SELECTCASE 選択肢
	CASE 0
		PRINTFORML コーヒーを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
	CASE 1
		PRINTFORML コーヒーと軽食を注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
ENDSELECT

@SCHOOL_MESSAGE_CAFE_MiLKでお茶する
PRINTFORML CAFE MiLKで%CALLNAME:TARGET%と楽しいティータイムの時間を過ごした
