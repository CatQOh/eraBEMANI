@ロケーション条件_Cafe_VOLTE
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"埴仁 虎子",
"班田 みみこ"
}

ロケーション名記録用 = Cafe VOLTE

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

;ただしボルテ学園在校生なら無条件解放
SIF GROUPMATCH(CSTR:PLAYER:在校, "ボルテ学園初等部", "ボルテ学園中等部", "ボルテ学園高等部")
	RETURN -1

RETURN 0

@ロケーション可否_Cafe_VOLTE
;時間帯などで使えない場合は理由を書く
SIF ターン == "夜"
	ロケーション可否 = 営業終了

@バイト条件_Cafe_VOLTE
;バイトの時間帯などを書く
バイト要項 = 朝～夕方のシフトで募集中
;バイト可能かどうかの条件式
SIF !GROUPMATCH(ターン, "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "テンプレート") >= 0
	RETURN GETCHARANAME("埴仁 虎子")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_Cafe_VOLTE
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"埴仁 虎子",
"班田 みみこ"
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_Cafe_VOLTE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;店主
		CASE "埴仁 虎子"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;バイト
		CASE "班田 みみこ"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;ボルテ学園関係者（原作準拠）やカフェ好きそうな方々
		{
		CASE "英利", "大犬のお姉さん", "マモニス", "夜々祇", "クリープ", "ドゥイさん", "Dr.Φ", "エクレメス", "マタン・カトルセ",
			 "タルト", "タフィ", "ショルキー", "ムラサキ", "ハニー", "モモコさん", "サトウさん", "ウーノ", "フォクシー", "浅見 文彦",
			 "アルフォンス・ミシェル", "アルビレオ", "愛子", "ヴィルヘルム", "いずみさん", "アン", "エミーリオ", "一条司令", "チク・タク",
			 "シェリー", "ハーピア", "スミス氏", "パステルくん", "おしゃれずきん", "ソルト"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～4人で行動している
				;無所属、教師陣のグループは休日のみ現れる
				IF 休日()
					CALL グループキャラ選出, LCOUNT, "相性"
				;あとは放課後等
				ELSEIF CSTR:LCOUNT:在校 != ""
					CALL クラスメイト選出, LCOUNT, "相性"
				ELSE
					CONTINUE
				ENDIF
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				SIF TALENT:LCOUNT:食欲皆伝 && 出現率:LCOUNT >= 0
					出現率:LCOUNT = 10
				SIF GROUPMATCH("SOUND VOLTEX", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３) && 出現率:LCOUNT >= 0
					出現率:LCOUNT = 10
			ENDIF

	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
RESTART

@Cafe_VOLTE_専用コマンド
IF 同行者
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, @" [---] - %CALLNAME:同行者%とお茶する", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @" [  2] - %CALLNAME:同行者%とお茶する"
	ENDIF
ELSE
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーを注文する(500＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - コーヒーを注文する(500＄)"
	ENDIF
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーと軽食を注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  1] - コーヒーと軽食を注文する(1000＄)"
	ENDIF
ENDIF

@Cafe_VOLTE_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
IF CSTR:GETCHARANAME("班田 みみこ"):現在地 != "" && RAND:2
	対象 = GETCHARANAME("班田 みみこ")
ELSE
	対象 = GETCHARANAME("埴仁 虎子")
ENDIF
SELECTCASE CRESULT:1
	CASE 0
		CALL SCHOOL_KOJO, 対象, "Cafe_VOLTEで注文"
		CALL 好感度上昇, 対象, "友情", 15
		MONEY -= 500
	CASE 1
		CALL SCHOOL_KOJO, 対象, "Cafe_VOLTEで注文"
		CALL 好感度上昇, 対象, "友情", 30
		MONEY -= 1000
	CASE 2
		CALL SCHOOL_KOJO, 同行者, "Cafe_VOLTEでお茶する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 1000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_Cafe_VOLTEで注文
SELECTCASE 選択肢
	CASE 0
		PRINTFORML コーヒーを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
	CASE 1
		PRINTFORML コーヒーと軽食を注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
	CASE 2
		PRINTFORML Cafe VOLTEで%CALLNAME:TARGET%と楽しいティータイムの時間を過ごした
ENDSELECT
