@ロケーション条件_Roots26
#DIM DYNAMIC LCOUNT
{
#DIMS DYNAMIC 条件用キャラ = 
"水城 セリカ",
"北見 エリカ",
"雛月 聖奈",
"雛月 理々奈"
}
ロケーション説明 = Roots26のキャラ達が着ている衣装の原点にもなった洋服店

;セリカ、エリカ、セム、リリスのいずれかの好感度が100以上
FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_Roots26
バイト要項 = 昼～夕方の時間帯で募集中。イケメン店主と可愛い妹が丁寧に指導します
SIF GROUPMATCH(ターン, "昼", "夕")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "Roots26") >= 0
	RETURN GETCHARANAME("雛月 聖奈")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_Roots26
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"雛月 聖奈",
"雛月 理々奈",
"水城 セリカ",
"北見 エリカ",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_Roots26
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;セムさんは必ず居る
		CASE "雛月 聖奈"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;セリカ、エリカ、リリスは50%の確率で居る
		CASE "水城 セリカ", "北見 エリカ", "雛月 理々奈"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;Roots26世界に居るが登場作品フラグが設定されてないキャラ
		CASE "クロノス", "カイロス"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 25
		;低確率で他のRoots26キャラが居る 25%
		CASEELSE
			IF GROUPMATCH("Roots26", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 25
			ENDIF
	ENDSELECT
	;卑弥呼は居てはいけないので除外
	SIF CHARANAMEF(LCOUNT) == "卑弥呼"
		出現率:LCOUNT = 0
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@Roots26_専用コマンド
CALL COLUMNPRINTL, 0, "　[  0] - ウィンドウショッピングする"
IF MONEY < 3000
	CALL COLUMNPRINTL, 0, "　[---] - 一着何か買ってみる(3000＄)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  1] - 一着何か買ってみる(3000＄)"
ENDIF

@Roots26_専用コマンド_INPUT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
SELECTCASE CRESULT:1
	CASE 0
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "Roots26でウィンドウショッピング"
			CALL 好感度上昇, 同行者, "友情", 10
		ELSE
			CALL 出現キャラ_Roots26
			CALL 出現キャラランダム選出
			対象 = RESULT
			CALL SCHOOL_KOJO, 対象, "Roots26でウィンドウショッピング"
			CALL 好感度上昇, 対象, "友情", 10
		ENDIF
	CASE 1
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "Roots26で買い物"
			CALL 好感度上昇, 同行者, "友情", 20
		ELSE
			DO
				CALL 職場出現キャラ_Roots26
				IF RESULT
					対象 = RESULT
					BREAK
				ENDIF
			LOOP 1
			CALL SCHOOL_KOJO, 対象, "Roots26で買い物"
			CALL 好感度上昇, 対象, "友情", 30
		ENDIF
		MONEY -= 3000
		SITEM:Roots26の服++
ENDSELECT
WAIT

RETURN 1

@SCHOOL_MESSAGE_Roots26でウィンドウショッピング
IF 同行者
	PRINTFORML %CALLNAME:TARGET%と店内を回りながらお互いに似合う服を探し合った
ELSE
	PRINTFORML 服を見て回ってると%CALLNAME:TARGET%が話しかけてきた
	PRINTFORML %CALLNAME:TARGET%と服飾について語り合った
ENDIF

@SCHOOL_MESSAGE_Roots26で買い物
IF 同行者
	PRINTFORML %CALLNAME:TARGET%と似合う服を吟味して買い物を楽しんだ
ELSE
	PRINTFORML 服の会計に%CALLNAME:TARGET%が対応した
	PRINTFORML 会計をそっちのけで%CALLNAME:TARGET%と服飾について語り合った
ENDIF
