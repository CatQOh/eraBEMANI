@ロケーション条件_あけさか
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"明坂 芹菜",
"箱部 なる"
}
ロケーション説明 = レトロな喫茶店。名物メニューは芹菜特製カレー
ロケーション名備考 = カレー喫茶

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@ロケーション可否_あけさか
;時間帯などで使えない場合は理由を書く
SIF ターン == "朝"
	ロケーション可否 = 営業準備中

@バイト条件_あけさか
;バイトの時間帯などを書く
バイト要項 = 昼～夜のシフトで募集中
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "昼", "夕", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "あけさか") >= 0
	RETURN GETCHARANAME("明坂 芹菜")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_あけさか, キャラ取得
#DIM DYNAMIC 対象
#DIM DYNAMIC キャラ取得
{
#DIMS DYNAMIC 出現キャラ =
"明坂 芹菜",
"箱部 なる"
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF !キャラ取得
	CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_あけさか
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;芹菜となる、カレー好きなキャラと皿洗いのバイト
		CASE "明坂 芹菜", "箱部 なる", "おいわちゃん", "スマイル", "カーリー", "リオ"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;休日や放課後に集団で来ているグループとかがある
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				IF TALENT:LCOUNT:食欲皆伝
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 10
				ENDIF
				;イロドリミドリのキャラたち
				IF GROUPMATCH("イロドリミドリ", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 10
				ENDIF
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART


@あけさか_専用コマンド
IF 同行者
	IF MONEY < 2000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とカレーを食べる(2000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%とカレーを食べる(2000＄)"
	ENDIF
ELSE
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - カレーを注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - カレーを注文する(500＄)"
	ENDIF
ENDIF

@あけさか_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
CALL 職場出現キャラ_あけさか
対象 = RESULT
SELECTCASE CRESULT:1
	CASE 0
		IF 対象
			CALL SCHOOL_KOJO, 対象, "あけさかで注文"
			CALL 好感度上昇, 対象, "友情", 15
		ELSE
			PRINTL あけさかで美味しいカレーを食べた
		ENDIF
		MONEY -= 1000
	CASE 1
		CALL SCHOOL_KOJO, 同行者, "あけさかで食事"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 2000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_あけさかで注文
PRINTFORML カレーを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した

@SCHOOL_MESSAGE_あけさかで食事
PRINTFORML あけさかで%CALLNAME:TARGET%と一緒に美味しいカレーを食べながら楽しい時間を過ごした
