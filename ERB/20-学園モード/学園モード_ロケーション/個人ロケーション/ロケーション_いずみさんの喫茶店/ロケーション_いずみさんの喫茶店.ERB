@ロケーション条件_いずみさんの喫茶店
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"いずみさん"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@ロケーション可否_いずみさんの喫茶店
;時間帯などで使えない場合は理由を書く
SIF ターン == "夜"
	ロケーション可否 = 本日営業終了

@バイト条件_いずみさんの喫茶店
;バイトの時間帯などを書く
バイト要項 = 朝～夕方のシフトで募集中
;バイト可能かどうかの条件式
SIF !GROUPMATCH(ターン, "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "いずみさんの喫茶店") >= 0
	RETURN GETCHARANAME("いずみさん")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_いずみさんの喫茶店
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"いずみさん",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_いずみさんの喫茶店
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;店主
		CASE "いずみさん"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;シャノワールに似ている
		{
		CASE "英利", "大犬のお姉さん", "マモニス", "夜々祇", "クリープ", "ドゥイさん", "Dr.φ", "エクレメス", "マタン・カトルセ",
			 "タルト", "タフィ", "ショルキー", "ムラサキ", "ハニー", "モモコさん", "サトウさん", "ウーノ", "フォクシー", "浅見 文彦",
			 "アルフォンス・ミシェル", "アルビレオ", "愛子", "ヴィルヘルム", "アン", "エミーリオ", "一条司令", "チク・タク",
			 "シェリー", "ハーピア", "スミス氏", "パステルくん", "おしゃれずきん", "ソルト", "ラズ", "シフォン", "しゃま", "みるく",
			 "タルタ", "トリフォリ", "おいわちゃん", "埴仁 虎子", "チョコプラちゃん", "シャロン", "リエル", "グリム・シャロン", "ココ", "黒須 紘"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				IF TALENT:LCOUNT:食欲皆伝
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 10
				ENDIF
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@いずみさんの喫茶店_専用コマンド
IF 同行者
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とお茶する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  2] - %CALLNAME:同行者%とお茶する(1000＄)"
	ENDIF
ELSE
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーを注文する(500＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - コーヒーを注文する(500＄)"
	ENDIF
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーと軽食を注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  1] - コーヒーと軽食を注文する(1000＄)"
	ENDIF
ENDIF

@いずみさんの喫茶店_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
対象 = GETCHARANAME("いずみさん")
SELECTCASE CRESULT:1
	CASE 0
		CALL SCHOOL_KOJO, 対象, "いずみさんの喫茶店で注文"
		CALL 好感度上昇, 対象, "友情", 15
		MONEY -= 500
	CASE 1
		CALL SCHOOL_KOJO, 対象, "いずみさんの喫茶店で注文"
		CALL 好感度上昇, 対象, "友情", 30
		MONEY -= 1000
	CASE 2
		CALL SCHOOL_KOJO, 同行者, "いずみさんの喫茶店でお茶する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 1000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_いずみさんの喫茶店で注文
SELECTCASE 選択肢
	CASE 0
		PRINTFORML コーヒーを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
	CASE 1
		PRINTFORML コーヒーと軽食を注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
ENDSELECT

@SCHOOL_MESSAGE_いずみさんの喫茶店でお茶する
PRINTFORML いずみさんの喫茶店で%CALLNAME:TARGET%と楽しいティータイムの時間を過ごした
