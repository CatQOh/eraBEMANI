@ロケーション条件_いずみ洋裁店
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"和泉 一舞"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_いずみ洋裁店
バイト要項 = 昼～夕方の時間帯で募集中
SIF GROUPMATCH(ターン, "昼", "夕")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "いずみ洋裁店") >= 0
	RETURN -1

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_いずみ洋裁店
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"和泉 一舞",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_いずみ洋裁店
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;イブ確定
		CASE "和泉 一舞"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;他ひなビタキャラがいる
		CASE "ひなビタキャラ"
			$HINABITA
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;服飾が好きなキャラが居る
		{
		CASE "水城 セリカ", "北見 エリカ", "雛月 聖奈", "神崎 紗矢", "タルト", "タフィ", "ベリゼベス", "トリフォリ", 
			 "ジュディ", "ムラサキ", "リゼット", "モモコさん", "ラッテ", "ミニッツ", "いずみさん", "ロザリー",
			 "クアトロ", "ニット", "ノネット", "シェリー", "鈴花", "＊うさぬこ＊", "ユビ子", "おしゃれずきん",
			 "ローズ", "ミカ子", "柏木 咲姫", "舞園 星斗", "ミサキ", "ノーティガール", "天乃 探女", "アムドゥシアン",
			 "アゲハ", "広瀬川 ミヤコ", "タイマー", "阿式 澄桃"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		CASEELSE
			SIF GROUPMATCH("ひなビタ♪", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO HINABITA
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@いずみ洋裁店_専用コマンド
CALL COLUMNPRINTL, 0, "[  0] - ウィンドウショッピングする"
IF MONEY < 3000
	CALL COLUMNPRINTL, 0, "　[---] - 一着何か買ってみる(3000＄)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  1] - 一着何か買ってみる(3000＄)"
ENDIF

@いずみ洋裁店_専用コマンド_INPUT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
SELECTCASE CRESULT:1
	CASE 0
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "いずみ洋裁店でウィンドウショッピング"
			CALL 好感度上昇, 同行者, "友情", 10
		ELSE
			CALL 出現キャラ_いずみ洋裁店
			CALL 出現キャラランダム選出
			対象 = RESULT
			CALL SCHOOL_KOJO, 対象, "いずみ洋裁店でウィンドウショッピング"
			CALL 好感度上昇, 対象, "友情", 10
		ENDIF
	CASE 1
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "いずみ洋裁店で買い物"
			CALL 好感度上昇, 同行者, "友情", 20
		ELSE
			IF CSTR:GETCHARANAME("和泉 一舞"):現在地 != ""
				;対象が居ないのでここで地の文書く
				PRINTFORML いずみ洋裁店で服を購入した
			ELSE
				DO
					CALL 職場出現キャラ_いずみ洋裁店
					IF RESULT
						対象 = RESULT
						BREAK
					ENDIF
				LOOP 1
				CALL SCHOOL_KOJO, 対象, "いずみ洋裁店で買い物"
				CALL 好感度上昇, 対象, "友情", 30
			ENDIF
		ENDIF
		MONEY -= 3000
		SITEM:いずみ洋裁店の服++
ENDSELECT
WAIT

RETURN 1

;Roots26のコピペじゃん
@SCHOOL_MESSAGE_いずみ洋裁店でウィンドウショッピング
IF 同行者
	PRINTFORML %CALLNAME:TARGET%と店内を回りながらお互いに似合う服を探し合った
ELSE
	PRINTFORML 服を見て回ってると%CALLNAME:TARGET%が話しかけてきた
	PRINTFORML %CALLNAME:TARGET%と服飾について語り合った
ENDIF

@SCHOOL_MESSAGE_いずみ洋裁店で買い物
IF 同行者
	PRINTFORML %CALLNAME:TARGET%と似合う服を吟味して買い物を楽しんだ
ELSE
	PRINTFORML 服の会計に%CALLNAME:TARGET%が対応した
	PRINTFORML 会計をそっちのけで%CALLNAME:TARGET%と服飾について語り合った
ENDIF
