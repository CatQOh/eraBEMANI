@ロケーション条件_おしゃま牛乳
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"しゃま",
"みるく",
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@ロケーション可否_おしゃま牛乳
;時間帯などで使えない場合は理由を書く
SIF !GROUPMATCH("", CSTR:GETCHARANAME("しゃま"):現在地, CSTR:GETCHARANAME("みるく"):現在地)
	ロケーション可否 = しゃまとみるくは通学中

@バイト条件_おしゃま牛乳
;バイトの時間帯などを書く
バイト要項 = 夕方～夜のシフトで募集中
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "夕", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "おしゃま牛乳") >= 0
	RETURN GETCHARANAME("しゃま")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_おしゃま牛乳
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"しゃま",
"みるく",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_おしゃま牛乳
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;しゃまみるく
		CASE "しゃま", "みるく"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;3人組
		CASE "ラズ", "シフォン", "ソルト"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;牛乳やナイスバディを求めてるキャラなど
		{
		CASE "赤のリピカ", "黃のラピカ", "クリープ", "埴仁 虎子", "チョコプラちゃん", "ミルク", "ホワイトメリー",
			 "ウェルダン", "ホイップ", "育江", "ルキア", "アロエ", "グリム・ルキア", "グリム・アロエ", 
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		CASEELSE
			SIF TALENT:LCOUNT:料理人 && 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20

	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@おしゃま牛乳_専用コマンド
IF 同行者
	IF MONEY < 400
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%と牛乳を飲む(400＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%と牛乳を飲む(400＄)"
	ENDIF
ELSE
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, "　[---] - 牛乳を注文する(200＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - 牛乳を注文する(200＄)"
	ENDIF
ENDIF

@おしゃま牛乳_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
対象 = 0
CALL 職場出現キャラ_おしゃま牛乳
対象 = RESULT
SELECTCASE CRESULT:1
	CASE 0
		CALL SCHOOL_KOJO, 対象, "おしゃま牛乳で注文"
		CALL 好感度上昇, 対象, "友情", 5
		MONEY -= 500
	CASE 1
		CALL SCHOOL_KOJO, 同行者, "おしゃま牛乳で乾杯"
		CALL 好感度上昇, 同行者, "友情", 10
		MONEY -= 1000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_おしゃま牛乳で注文
PRINTFORML 美味しい牛乳を飲みながら%CALLNAME:TARGET%と談笑した
PRINTL 牛乳の成分については……企業秘密だとはぐらかされてしまった

@SCHOOL_MESSAGE_おしゃま牛乳で乾杯
PRINTFORML おしゃま牛乳で%CALLNAME:TARGET%と乾杯した。体が成長しそうな味だ
