@ロケーション条件_サウダージ
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"山形 滋",
"山形 まり花"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_サウダージ
バイト要項 = 昼～夕方の時間帯で募集中
SIF GROUPMATCH(ターン, "昼", "夕")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "サウダージ") >= 0
	RETURN GETCHARANAME("山形 滋")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_サウダージ
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"山形 滋",
"山形 まり花"
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_サウダージ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;まり花確定
		CASE "山形 まり花"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;他ひなビタキャラがいる
		CASE "ひなビタキャラ"
			$HINABITA
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;レコード好きそうなキャラ
		{
		CASE "サイレン", "雛月 聖奈", "Aegis", "ジャックティーロ", "ハーティー", "ジュリア",
			 "ザ・キング", "MZD", "ユーリ", "ムラサキ", "ジズ", "アルビレオ", "ヴィルヘルム",
			 "スモーク", "パステルくん", "アフロ", "弦", "サトウサン", "片桐 奈々菜", "フランシス", "クプロ",
			 "アイス", "レオン(pop'n)", "スザク", "宍戸 美鈴", "一条司令", "オリバー", "シャトー・ロワーレ"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		CASEELSE
			SIF GROUPMATCH("ひなビタ♪", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO HINABITA
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@サウダージ_専用コマンド
CALL COLUMNPRINTL, 0, "　[  0] - レコードを試聴する"
IF MONEY < 1000
	CALL COLUMNPRINTL, 0, "　[---] - レコードを買う(1000＄必要)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  1] - レコードを買う(1000＄必要)"
ENDIF

@サウダージ_専用コマンド_INPUT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象

SELECTCASE CRESULT:1
	CASE 0
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "サウダージで試聴"
			CALL 好感度上昇, 同行者, "友情", 10
		ELSE
			CALL 出現キャラ_サウダージ
			CALL 出現キャラランダム選出
			対象 = RESULT
			CALL SCHOOL_KOJO, 対象, "サウダージで試聴"
			CALL 好感度上昇, 対象, "友情", 10
		ENDIF
	CASE 1
		IF 同行者
			CALL SCHOOL_KOJO, 同行者, "サウダージで買い物"
			CALL 好感度上昇, 同行者, "友情", 20
		ELSE
			DO
				CALL 職場出現キャラ_サウダージ
				IF RESULT
					対象 = RESULT
					BREAK
				ENDIF
			LOOP 1
			CALL SCHOOL_KOJO, 対象, "サウダージで買い物"
			CALL 好感度上昇, 対象, "友情", 30
		ENDIF
		MONEY -= 1000
		SITEM:サウダージのレコード++
ENDSELECT
WAIT

RETURN 1

@SCHOOL_MESSAGE_サウダージで試聴
IF 同行者
	PRINTFORML 店内を回りながら%CALLNAME:TARGET%とレコードの音に浸った
ELSE
	PRINTFORML レコードを視聴していると%CALLNAME:TARGET%が話しかけてきた
	PRINTFORML %CALLNAME:TARGET%と音楽の趣味や音楽史について語り合った
ENDIF

@SCHOOL_MESSAGE_サウダージで買い物
IF 同行者
	PRINTFORML %CALLNAME:TARGET%とレコードを聴き比べながら理想の1枚を探し出して購入した
ELSE
	PRINTFORML レコードの会計に%CALLNAME:TARGET%が対応した
	PRINTFORML %CALLNAME:TARGET%はそのレコードの曲や年代についての小話を披露してくれた
ENDIF
