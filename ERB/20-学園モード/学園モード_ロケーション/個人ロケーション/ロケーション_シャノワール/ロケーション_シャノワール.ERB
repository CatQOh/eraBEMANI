@ロケーション条件_シャノワール
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"春日 咲子"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_シャノワール
バイト要項 = 昼、夜の時間帯で募集中
SIF GROUPMATCH(ターン, "昼", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "シャノワール") >= 0
	RETURN -1

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_シャノワール
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"春日 咲子",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_シャノワール
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;咲子確定
		CASE "春日 咲子"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;喫茶店を必要としている人たち（漠然すぎる概要）
		{
		CASE "英利", "大犬のお姉さん", "マモニス", "夜々祇", "クリープ", "ドゥイさん", "Dr.φ", "エクレメス", "マタン・カトルセ",
			 "タルト", "タフィ", "ショルキー", "ムラサキ", "ハニー", "モモコさん", "サトウさん", "ウーノ", "フォクシー", "浅見 文彦",
			 "アルフォンス・ミシェル", "アルビレオ", "愛子", "ヴィルヘルム", "いずみさん", "アン", "エミーリオ", "一条司令", "チク・タク",
			 "シェリー", "ハーピア", "スミス氏", "パステルくん", "おしゃれずきん", "ソルト", "ラズ", "シフォン", "しゃま", "みるく",
			 "タルタ", "トリフォリ", "おいわちゃん", "埴仁 虎子", "チョコプラちゃん", "赤のリピカ", "黃のラピカ", "シャロン", "リエル", "グリム・シャロン", "ココ", "黒須 紘"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;他ひなビタキャラがいる
		CASE "ひなビタキャラ"
			$HINABITA
			出現率:LCOUNT = 50
		CASEELSE
			SIF GROUPMATCH("ひなビタ♪", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO HINABITA
			;休日や放課後に集団で来ているグループとかがある
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～4人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ENDIF
			IF TALENT:LCOUNT:食欲皆伝
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 20
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@シャノワール_専用コマンド
IF 同行者
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とお茶する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  2] - %CALLNAME:同行者%とお茶する(1000＄)"
	ENDIF
ELSE
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーを注文する(500＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - コーヒーを注文する(500＄)"
	ENDIF
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - コーヒーと軽食を注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  1] - コーヒーと軽食を注文する(1000＄)"
	ENDIF
ENDIF

@シャノワール_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
対象 = 0
SIF CSTR:GETCHARANAME("春日 咲子"):現在地 != ""
	対象 = GETCHARANAME("春日 咲子")
SELECTCASE CRESULT:1
	CASE 0
		IF 対象
			CALL SCHOOL_KOJO, 対象, "シャノワールで注文"
			CALL 好感度上昇, 対象, "友情", 15
		ELSE
			PRINTL シャノワールで美味しいコーヒーを頂いた
		ENDIF
		MONEY -= 500
	CASE 1
		IF 対象
			CALL SCHOOL_KOJO, 対象, "シャノワールで注文"
			CALL 好感度上昇, 対象, "友情", 30
		ELSE
			PRINTL シャノワールで美味しいコーヒーと軽食を頂いた
		ENDIF
		MONEY -= 1000
	CASE 2
		CALL SCHOOL_KOJO, 同行者, "シャノワールでお茶する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 1000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_シャノワールで注文
SELECTCASE 選択肢
	CASE 0
		PRINTFORML コーヒーを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
	CASE 1
		PRINTFORML コーヒーと軽食を注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した
ENDSELECT

@SCHOOL_MESSAGE_シャノワールでお茶する
PRINTFORML シャノワールで%CALLNAME:TARGET%と楽しいティータイムの時間を過ごした
