@ロケーション条件_ヒューの整備工場
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"ヒュー"
}

ロケーション説明 = ヒューが営む整備工場。自動車以外も様々なものを整備している様子

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_ヒューの整備工場
;バイトの時間帯などを書く
バイト要項 = エンジニア募集中
;バイト可能かどうかの条件式
バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "ヒューの整備工場") >= 0
	RETURN GETCHARANAME("ヒュー")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_ヒューの整備工場
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"ヒュー",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "工作経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_ヒューの整備工場
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		CASE "ヒュー"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;乗り物に乗っている人たち キャノンボールサーキットに近い
		{
		CASE "大伴 茶倉", "ニクス", "英利", "筑波 美夏", "英田 優里", "菅生 彩香音", "茂木 英美瑠",
			 "鈴鹿 亞希", "ゼロワン：バンディング", "トヨタマヒメ", "獅子若", "ボニー", "獅子若", "カゲトラ",
			 "レル DX2300系", "電人イェーガー", "レーヴァスレーター", "おとこマン", "テッカ"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
