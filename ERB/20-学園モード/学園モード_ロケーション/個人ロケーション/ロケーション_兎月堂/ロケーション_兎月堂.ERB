@ロケーション条件_兎月堂
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"芽兎 めう"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_兎月堂
バイト要項 = 朝～夕方の時間帯で募集中
SIF GROUPMATCH(ターン, "朝", "昼", "夕")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "兎月堂") >= 0
	RETURN -1

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_兎月堂
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"芽兎 めう",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_兎月堂
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;めう確定
		CASE "芽兎 めう"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;はんこ屋に行くようなキャラはいない……と思うけど変な名字持ってる人を入れておく
		{
		CASE "苑柄 冬花", "鳳凰 鈴火", "野増菜 しずく", "犬見川 ユウ", "九鬼衣 シエ", "うらしまたろ子", "嬬武器 雷刀", "嬬武器 烈風刀",
			 "野増菜 かなで", "虹霓・シエル・奈奈", "赤志 魂", "傍丹 桜子", "栗花落 夜風", "伊賀崎ノ 楠子", "天稲荷 コテツ", "八咫烏 鋼太郎",
			 "鬼蝮 ユリア", "鬼蝮 アイリ", "京終 始果", "不律 灯色", "九部 リア", "蒼刻院 弓弦",
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 30
		;他ひなビタキャラがいる
		CASE "ひなビタキャラ"
			$HINABITA
			出現率:LCOUNT = 50
		CASEELSE
			SIF GROUPMATCH("ひなビタ♪", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO HINABITA
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@兎月堂_専用コマンド
IF MONEY < 1000
	CALL COLUMNPRINTL, 0, "　[---] - はんこを買う(500＄)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  0] - はんこを買う(500＄)"
ENDIF

@兎月堂_専用コマンド_INPUT
#DIM DYNAMIC 対象

IF CSTR:GETCHARANAME("芽兎 めう"):現在地 != ""
	;対象が居ないのでここで地の文書く
	PRINTFORML 兎月堂ではんこを購入した
ELSE
	対象 = GETCHARANAME("芽兎 めう")
	CALL SCHOOL_KOJO, 対象, "兎月堂で買い物"
	CALL 好感度上昇, 対象, "友情", 15
ENDIF

MONEY -= 500
SITEM:はんこ++
RETURN 1

@SCHOOL_MESSAGE_兎月堂で買い物
PRINTFORML 兎月堂ではんこを購入し、会計をしてくれた%CALLNAME:TARGET%とはんこについて語り合った
