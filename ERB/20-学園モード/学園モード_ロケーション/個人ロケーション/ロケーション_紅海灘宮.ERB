@ロケーション条件_紅海灘宮
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"チャオ"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@ロケーション可否_紅海灘宮
;時間帯などで使えない場合は理由を書く
SIF GROUPMATCH(ターン, "朝", "夕")
	ロケーション可否 = 営業準備中

@バイト条件_紅海灘宮
;バイトの時間帯などを書く
バイト要項 = 昼、夜のシフトで募集中
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "昼", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
SIF FINDELEMENTF(解禁済みロケーション, "紅海灘宮") >= 0
	RETURN GETCHARANAME("チャオ")

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_紅海灘宮
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"チャオ",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_紅海灘宮
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		CASE "チャオ"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;高級料理店なのでセレブが来る
		{
		CASE "菱宮 津軽", "ジルチ", "梅桐 緋浮美", "菱宮 春一", "大犬のお姉さん", "マモニス", "夜兎姫", "ドゥイさん", "エクレメス", "マタン・カトルセ",
			 "ザ・キング", "ショルキー", "アイス", "ハニー", "白鳥 姫子", "ハラ＝ヘッタ", "こうもりおとこ", "アフロ", "グリム・ヤンヤン", "サーミリアス"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 30
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@紅海灘宮_専用コマンド
IF 同行者
	IF MONEY < 10000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%と食事する(10000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%と食事する(10000＄)"
	ENDIF
ELSE
	IF MONEY < 5000
		CALL COLUMNPRINTL, 0, "　[---] - 中華料理を注文する(5000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - 中華料理を注文する(5000＄)"
	ENDIF
ENDIF

@紅海灘宮_専用コマンド_INPUT
#DIM DYNAMIC 対象

選択肢 = CRESULT:1
対象 = 0
SIF CSTR:GETCHARANAME("チャオ"):現在地 != ""
	対象 = GETCHARANAME("チャオ")
SELECTCASE CRESULT:1
	CASE 0
		IF 対象
			CALL SCHOOL_KOJO, 対象, "紅海灘宮で注文"
			CALL 好感度上昇, 対象, "友情", 25
		ELSE
			PRINTL 紅海灘宮で高級中華料理を頂いた
		ENDIF
		MONEY -= 800
	CASE 1
		CALL SCHOOL_KOJO, 同行者, "紅海灘宮で食事する"
		CALL 好感度上昇, 同行者, "友情", 50
		MONEY -= 1600
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_紅海灘宮で注文
PRINTFORML 料理を注文して、持ってきてくれた%CALLNAME:TARGET%と談笑した

@SCHOOL_MESSAGE_紅海灘宮で食事する
PRINTFORML 紅海灘宮で%CALLNAME:TARGET%と豪華な食事を楽しんだ
