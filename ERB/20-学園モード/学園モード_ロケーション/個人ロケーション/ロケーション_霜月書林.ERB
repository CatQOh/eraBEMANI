@ロケーション条件_霜月書林
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"霜月 凛"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_霜月書林
バイト要項 = 朝～夕の時間帯で募集中
SIF GROUPMATCH(ターン, "朝", "昼", "夕")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "霜月書林") >= 0
	RETURN -1

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_霜月書林
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"霜月 凛",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_霜月書林
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;凛確定
		CASE "霜月 凛"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;本が好きそうな人達がいる（ルーテージ図書館と同じメンツ）
		{
		CASE "雛月 理々奈", "梅桐 彩葉", "ファウラ", "ラジェ", "浅見 文彦", "ロキ",
			 "アルフォンス・ミシェル", "花緒", "獅子若", "アルビレオ", "愛子", "リアン", 
			 "ロザリー", "寿々姫", "ルート＠超ドー研", "氷海", "パステルくん", "アイラ", "しおり", "メリリィ"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;他ひなビタキャラがいる
		CASE "ひなビタキャラ"
			$HINABITA
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		CASEELSE
			SIF GROUPMATCH("ひなビタ♪", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO HINABITA
			IF TALENT:LCOUNT:読書家
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 20
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@霜月書林_専用コマンド
IF MONEY < 1000
	CALL COLUMNPRINTL, 0, "　[---] - 本を買う(1000＄)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  0] - 本を買う(1000＄)"
ENDIF

@霜月書林_専用コマンド_INPUT
#DIM DYNAMIC 対象

IF CSTR:GETCHARANAME("霜月 凛"):現在地 != ""
	;対象が居ないのでここで地の文書く
	PRINTFORML 霜月書林で本を購入した
ELSE
	DO
		CALL 職場出現キャラ_霜月書林
		IF RESULT
			対象 = RESULT
			BREAK
		ENDIF
	LOOP 1
	CALL SCHOOL_KOJO, 対象, "霜月書林で買い物"
	CALL 好感度上昇, 対象, "友情", 15
ENDIF

MONEY -= 1000
SITEM:書籍++
RETURN 1

@SCHOOL_MESSAGE_霜月書林で買い物
PRINTFORML 霜月書林で本を購入し、会計をしてくれた%CALLNAME:TARGET%と本について話した
