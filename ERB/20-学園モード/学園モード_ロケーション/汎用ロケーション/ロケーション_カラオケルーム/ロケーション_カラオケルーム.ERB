@ロケーション条件_カラオケルーム
RETURN -1

@バイト条件_カラオケルーム
;バイトの時間帯などを書く
バイト要項 = 終日募集中
;バイト可能かどうかの条件式
バイト可能 = 1
RETURN -1

@出現キャラ_カラオケルーム
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	IF RANDPER(1) && グループ数 < 10
		;相性の良いキャラと2～5人で行動している
		;無所属、教師陣のグループは休日のみ現れる
		IF 休日()
			CALL グループキャラ選出, LCOUNT, "相性"
		;あとは放課後等
		ELSEIF CSTR:LCOUNT:在校 != ""
			CALL クラスメイト選出, LCOUNT, "相性"
		ELSE
			CONTINUE
		ENDIF
		;2人以上揃わなかったら無かったことにする
		SIF RESULT:0 == 0
			CONTINUE
		;2～5人のグループ
		集団行動:グループ数:0 = LCOUNT
		集団行動:グループ数:1 = RESULT:0
		IF RANDPER(50)
			集団行動:グループ数:2 = RESULT:1
			IF RANDPER(50)
				集団行動:グループ数:3 = RESULT:2
				SIF RANDPER(50)
					集団行動:グループ数:4 = RESULT:3
			ENDIF
		ENDIF
		グループ数++
	ENDIF
	;ヒトカラするやつは居ない
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@カラオケルーム_専用コマンド
IF MONEY < 500
	CALL COLUMNPRINTL, 0, "　[---] - 1時間ほど歌っていく(500＄必要)", "gray"
	SIF 同行者
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%に奢らせる"
ELSE
	IF 同行者
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%とカラオケを楽しむ(500＄必要)"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - 1時間ほど歌っていく(500＄必要)"
	ENDIF
ENDIF

@カラオケルーム_専用コマンド_INPUT

IF MONEY < 500
	IF CRESULT:1 == 1
		CALL SCHOOL_KOJO, 同行者, "カラオケで奢らせる"
		;教師か恋人なら許される
		IF CSTR:同行者:在任 != "" || TALENT:同行者:恋慕
		ELSE
			CALL 好感度上昇, 同行者, -5
		ENDIF
	ENDIF
ELSE
	IF CRESULT:1 == 1
		IF ABL:PLAYER:歌唱技能 >= 4
			IF ABL:PLAYER:歌唱技能 > ABL:同行者:歌唱技能
				CALL SCHOOL_KOJO, 同行者, "一緒にカラオケ"
				CALL 好感度上昇, 同行者, "尊敬", 20
				CALL 好感度上昇, 同行者, "友情", 10
			ELSE
				CALL SCHOOL_KOJO, 同行者, "一緒にカラオケ"
				CALL 好感度上昇, 同行者, "尊敬", 10
				CALL 好感度上昇, 同行者, "友情", 10
			ENDIF
		ELSE
			CALL SCHOOL_KOJO, 同行者, "一緒にカラオケ"
			CALL 好感度上昇, 同行者, "友情", 10
		ENDIF
		CALL EXPUP, "歌唱経験", 3, PLAYER
		CALL EXPUP, "歌唱経験", 3, 同行者
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は一人でカラオケを楽しんだ
		CALL EXPUP, "歌唱経験", 3, PLAYER
	ENDIF
	MONEY -= 500
	IF 同行者
		IF TALENT:同行者:恋慕
			PRINTFORML カラオケルームに2人きり、どことなく良い雰囲気だ
			PRINTL [0] - 何もしない
			PRINTL [1] - スキンシップを試みる
			PRINTL [2] - キスしてみる
		ELSEIF 好感度:同行者:既成事実
			PRINTFORML %NAME:同行者%が先程から何かを期待するようにこちらを見ている
			PRINTL [0] - 気づかないフリをする
		ELSE
			RETURN 1
		ENDIF
		IF 好感度:同行者:既成事実
			PRINTL [3] - ねだってみる
			PRINTL [4] - 積極的に触ってみる
		ENDIF
		BINPUT
		選択肢 = RESULT
		SELECTCASE RESULT
			CASE 1
				CALL SCHOOL_KOJO, 同行者, "カラオケルームでロマンス"
				CALL 好感度上昇, 同行者, "ときめき", 10
				CALL 好感度上昇, 同行者, "友情", 10
			CASE 2
				CALL SCHOOL_KOJO, 同行者, "カラオケルームでロマンス"
				CALL 好感度上昇, 同行者, "ときめき", 20
			CASE 3
				CALL SCHOOL_KOJO, 同行者, "カラオケルームでロマンス"
				CALL 好感度上昇, 同行者, "ときめき", 10
				CALL 好感度上昇, 同行者, "セフレ", 20
				CALL EXPUP, "奉仕快楽経験", 2, 同行者
				IF PENIS(PLAYER)
					CALL EXPUP, "射精経験", 1, PLAYER
					CALL EXPUP, "精液経験", 1, 同行者
				ELSE
					CALL EXPUP, "絶頂経験", 1, PLAYER
				ENDIF
				IF SEX(PLAYER) == 1 && SEX(同行者) == 1
					CALL EXPUP, "ゲイ経験", 2, PLAYER
					CALL EXPUP, "ゲイ経験", 2, 同行者
				ENDIF
				IF SEX(PLAYER) == 2 && SEX(同行者) == 2
					CALL EXPUP, "レズ経験", 2, PLAYER
					CALL EXPUP, "レズ経験", 2, 同行者
				ENDIF
				CFLAG:同行者:倫理観++
			CASE 4
				CALL SCHOOL_KOJO, 同行者, "カラオケルームでロマンス"
				CALL 好感度上昇, 同行者, "ときめき", 10
				CALL 好感度上昇, 同行者, "セフレ", 20
				IF PENIS(同行者)
					CALL EXPUP, "射精経験", 1, 同行者
					CALL EXPUP, "精液経験", 1, PLAYER
				ELSE
					CALL EXPUP, "絶頂経験", 1, 同行者
				ENDIF
				IF SEX(PLAYER) == 1 && SEX(同行者) == 1
					CALL EXPUP, "ゲイ経験", 2, PLAYER
					CALL EXPUP, "ゲイ経験", 2, 同行者
				ENDIF
				IF SEX(PLAYER) == 2 && SEX(同行者) == 2
					CALL EXPUP, "レズ経験", 2, PLAYER
					CALL EXPUP, "レズ経験", 2, 同行者
				ENDIF
				CFLAG:同行者:倫理観++
		ENDSELECT
	ENDIF
ENDIF

RETURN 1

@SCHOOL_MESSAGE_カラオケで奢らせる
IF CSTR:同行者:在任 != "" || TALENT:同行者:恋慕
	PRINTFORML %CALLNAME:同行者%は金欠の%CALLNAME:PLAYER%を笑いながらも快く奢ってくれた
ELSE
	PRINTFORML %CALLNAME:同行者%はたかられるのを不満そうにしつつも奢ってくれた
ENDIF

@SCHOOL_MESSAGE_一緒にカラオケ
PRINTFORML %CALLNAME:同行者%とカラオケを楽しむことにした
IF ABL:PLAYER:歌唱技能 >= 4
	IF ABL:PLAYER:歌唱技能 > ABL:同行者:歌唱技能
		PRINTFORML %CALLNAME:PLAYER%の歌声に%CALLNAME:同行者%は聞き入っている
	ELSE
		PRINTFORML %CALLNAME:PLAYER%の歌唱に感心する%CALLNAME:同行者%とデュエットを組み、存分にカラオケを楽しんだ
	ENDIF
ENDIF

@SCHOOL_MESSAGE_カラオケルームでロマンス
SELECTCASE 選択肢
	CASE 0
		IF TALENT:恋慕
			PRINTFORML 良い雰囲気になりながらも%CALLNAME:PLAYER%は%CALLNAME:TARGET%に何もせずカラオケルームを後にした
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の視線に気づかないフリをして何もせずカラオケルームを後にした
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:TARGET%にスキンシップを試みると、恋人らしい反応を見せてくれた
	CASE 2
		PRINTFORML じっと%CALLNAME:TARGET%を見つめると、%CALLNAME:TARGET%も察してキスを受け入れてくれた
	CASE 3
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に欲情したことを伝え、部屋の入り口から見えづらい位置で
		IF CFLAG:PLAYER:挿入可
			PRINTFORM %OVER_GENITAL(PLAYER)%をたくし上げ、
		ELSE
			PRINTFORM %OVER_GENITAL(PLAYER)%を緩め、
		ENDIF
		IF PENIS(PLAYER)
			PRINTFORML 勃起した男性器を%CALLNAME:TARGET%に見せた
			PRINTFORML %CALLNAME:TARGET%は本番とまでは行かずとも口や手を使ってそれを慰め、数分の後に%CALLNAME:PLAYER%は射精に達した
		ELSE
			PRINTFORML 濡れる女性器を%CALLNAME:TARGET%に見せた
			PRINTFORML %CALLNAME:TARGET%は口や手で%CALLNAME:PLAYER%を慰め、%CALLNAME:PLAYER%は防音壁の中で声を上げながら絶頂に達した
		ENDIF
	CASE 4
		PRINTFORML %CALLNAME:TARGET%との距離を縮め体に触ってみると%CALLNAME:TARGET%は抵抗せず、\@ PENIS(PLAYER) ? 勃起したペニス # 濡れる陰部 \@への愛撫を許す
		PRINTFORM %CALLNAME:PLAYER%も我慢できずに
		IF PENIS(TARGET)
			PRINTFORML それを口に咥え、%CALLNAME:TARGET%の中に溜まった欲望を吸い出した
		ELSE
			PRINTFORML 女性器に舌を這わせ、%CALLNAME:TARGET%が達するまでの間しばし口淫に耽った
		ENDIF
ENDSELECT
