@ロケーション条件_ギタドランド
;汎用ロケーション 条件無し
RETURN -1

@バイト条件_ギタドランド
バイト要項 = 土曜日、日曜日の終日募集中
SIF GROUPMATCH(DOW(), "土", "日") && GROUPMATCH(ターン, "朝", "昼", "夕", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENT(解禁済みロケーション, "ギタドランド", 0, VARSIZE("解禁済みロケーション"), 1) >= 0
	RETURN -1

RETURN 0

@出現キャラ_ギタドランド
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;ギタドラキャラが中確率で居る
		CASE "ギタドラキャラ"
			$GITADORA
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;ボーナスキャラ 10%
		CASE "ミミ", "ニャミ", "HOT.D", "アンナ", "ハーレクイン", "テント・カント"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 10
		;日曜もしくはホリデー期間なら集団で来ているグループとかがある
		CASEELSE
			IF (DOW(FLAG:曜日) == "日" || ISHOLIDAY_SCHOOL()) && CSTR:LCOUNT:在校 != "" && RANDPER(3) && グループ数 < 10
				;相性の良いキャラと3人で行動している
				CALL クラスメイト選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				集団行動:グループ数:2 = RESULT:1
				グループ数++
			ENDIF
			SIF GROUPMATCH("GUITARFREAKS&drummania", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO GITADORA
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@ギタドランド_専用コマンド
IF 同行者
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とアトラクションを楽しむ(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  0] - %CALLNAME:同行者%とアトラクションを楽しむ(1000＄)"
	ENDIF
	IF MONEY < 500
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%と軽食を食べる(500＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%と軽食を食べる(500＄)"
	ENDIF
ENDIF

@ギタドランド_専用コマンド_INPUT
#DIM DYNAMIC 対象

SELECTCASE CRESULT:1
	CASE 0
		CALL SCHOOL_KOJO, 同行者, "ギタドランドでアトラクションを楽しむ"
		CALL 好感度上昇, 同行者, "友情", 20
		CALL 好感度上昇, 同行者, "ときめき", 5
	CASE 1
		CALL SCHOOL_KOJO, 同行者, "ギタドランドで軽食を食べる"
		CALL 好感度上昇, 同行者, "友情", 15
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_ギタドランドでアトラクションを楽しむ
PRINTFORML %CALLNAME:同行者%と様々なアトラクションを体験した。楽しい！

@SCHOOL_MESSAGE_ギタドランドで軽食を食べる
PRINTFORML %CALLNAME:同行者%と軽食を食べた。美味しい！
