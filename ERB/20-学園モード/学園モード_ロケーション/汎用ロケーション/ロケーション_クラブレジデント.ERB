@ロケーション条件_クラブレジデント
RETURN -1

@ロケーション可否_クラブレジデント
;時間帯などで使えない場合は理由を書く
SIF !GROUPMATCH(ターン, "夕", "夜")
	ロケーション可否 = 営業準備中

@バイト条件_クラブレジデント
;バイトの時間帯などを書く
バイト要項 = 夕方、夜のシフトで募集。アイス、流しそうめん等のまかない有り
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "夕", "夜")
	バイト可能 = 1

RETURN -1

;バイト時に好感度が上がるキャラ
@職場出現キャラ_クラブレジデント
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"神崎 士朗",
"梅桐 天土",
"レオン(pop'n)",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "演奏経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_クラブレジデント
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;ディスコっぽいキャラ
		{
		CASE "神崎 士朗", "梅桐 天土", "サイレン", "405nm", "ハーティー", "ジュリア", "リフレナ", "ぼんきち",
			 "ミミ", "ニャミ", "MZD","ジュディ", "マリィ", "ザ・キング", "モモコさん", "うさおくん", "ロミ夫",
			 "ニッキー", "キャロル", "ミク", "ライラ", "レオン(pop'n)", "トア", "スザク", "結", "千代", "アゲハ", "ケリー", 
			 "ツクバ", "アキバ", "DJつよし"
		}
			$出現
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 10
		CASEELSE
			SIF GROUPMATCH("Dance Dance Revolution", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				GOTO 出現
			;休日または夜なら集団で来ているグループとかがある
			IF (休日() || ターン == "夜") && CSTR:LCOUNT:在校 != "" && RANDPER(3) && グループ数 < 10
				;相性の良いキャラと3人で行動している
				CALL クラスメイト選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				集団行動:グループ数:2 = RESULT:1
				グループ数++
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
