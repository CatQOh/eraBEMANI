@ロケーション条件_ゲームセンター
;汎用ロケーション 条件無し
RETURN -1

@バイト条件_ゲームセンター
バイト要項 = 昼～夜の時間帯で募集中
SIF GROUPMATCH(ターン, "昼", "夕", "夜")
	バイト可能 = 1
;ロケーション解禁済みで可能
SIF FINDELEMENTF(解禁済みロケーション, "ゲームセンター") >= 0
	RETURN GETCHARANAME("識")

RETURN 0

@ロケーション可否_ゲームセンター
SIF ターン == "朝"
	ロケーション可否 = 営業準備中

@出現キャラ_ゲームセンター
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	始点 = 1
	終点 = CHARANUM
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;主に音ゲーマーなどが居る
	SELECTCASE CHARANAMEF(LCOUNT)
		;識と士朗が100%居る
		CASE "識", "神崎 士朗"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;数が多いので各30%
		{
		CASE "ユーズ", "ナイア", "水城 セリカ", "ニクス", "孔雀", "北見 エリカ", 
			 "ジルチ", "光伊 計名", "雛月 理々奈", "神崎 慧靂", "菱宮 津軽", "刃雪 達磨", 
			 "梅桐 彩葉", "シア", "鉄火", "トラン", "黄シグ", "ハーティ", "夜兎姫", "おいわちゃん", 
			 "ミミ", "ニャミ", "MZD", "パステルくん", "揚羽", "エミ", "ユニ", "フリオ", "リノン", 
			 "オトベア", "BisCo", "Mei", "ToRa", "伊澄 いずみ", "三田 希少", "グリム・ハルト", "芽兎 めう", 
			 "いちか", "クプロ", "ゴリラ", "エリザベス", "リリィ", "九部 リア", "青士朗", "ラプラシア", "ぼんきち", 
			 "新井 桃子", "黒原 きずな", "スカイフェザー", "くらんて", "荒場 流子", "舞園 星斗", "ライオンくん", "のの", "みや",
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 30
		;休日や放課後に集団で来ているグループとかがある
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～4人で行動している
				;無所属、教師陣のグループは休日のみ現れる
				IF 休日()
					CALL グループキャラ選出, LCOUNT, "相性"
				;あとは放課後等
				ELSEIF CSTR:LCOUNT:在校 != ""
					CALL クラスメイト選出, LCOUNT, "相性"
				ELSE
					CONTINUE
				ENDIF
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				SIF TALENT:LCOUNT:音ゲーマー && 出現率:LCOUNT >= 0
					出現率:LCOUNT = 10
			ENDIF
	ENDSELECT
NEXT

@ゲームセンター_専用コマンド
IF MONEY < 100
	CALL COLUMNPRINTL, 0, "　[---] - 音ゲーを遊ぶ(100＄必要)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[  0] - 音ゲーを遊ぶ(100＄必要)"
ENDIF
IF 同行者
	IF MONEY < 200
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%と一緒に音ゲーを遊ぶ(200＄必要)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%と一緒に音ゲーを遊ぶ(200＄必要)"
	ENDIF
ENDIF

@ゲームセンター_専用コマンド_INPUT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 滞在キャラ, 1000
#DIM DYNAMIC 対象

CALL 出現キャラ_ゲームセンター

FOR LCOUNT, 1, CHARANUM
	SIF 出現率:LCOUNT && CSTR:LCOUNT:現在地 == ""
		ARRAYSHIFT 滞在キャラ, 1, LCOUNT
NEXT

SELECTCASE CRESULT:1
	CASE 0
		;誰かが話しかけてくる
		IF 滞在キャラ:0 != 0
			対象 = 滞在キャラ:(RAND:FINDELEMENT(滞在キャラ, 0))
			IF EXP:PLAYER:音ゲー経験 < EXP:対象:音ゲー経験
				CALL SCHOOL_KOJO, 対象, "音ゲー中に会話"
				CALL 好感度上昇, 対象, "友情", 10
				CALL 好感度上昇, 対象, "舎弟", 10
			ELSEIF EXP:PLAYER:音ゲー経験 >= 100
				CALL SCHOOL_KOJO, 対象, "音ゲー中に会話"
				CALL 好感度上昇, 対象, "友情", 10
				CALL 好感度上昇, 対象, "尊敬", 10
			ELSE
				CALL SCHOOL_KOJO, 対象, "音ゲー中に会話"
				CALL 好感度上昇, 対象, "友情", 10
			ENDIF
			CALL EXPUP, "音ゲー経験", 1, 対象
		ENDIF
		CALL EXPUP, "音ゲー経験", 1, PLAYER
		MONEY -= 100
		WAIT
	CASE 1
		IF EXP:PLAYER:音ゲー経験 <= EXP:同行者:音ゲー経験
			IF RANDPER(80)
				選択肢 = 1
				CALL SCHOOL_KOJO, 同行者, "一緒に音ゲー"
				CALL 好感度上昇, 同行者, "友情", 10
				CALL 好感度上昇, 同行者, "舎弟", 10
			ELSE
				選択肢 = 2
				CALL SCHOOL_KOJO, 同行者, "一緒に音ゲー"
				CALL 好感度上昇, 同行者, "友情", 10
				CALL 好感度上昇, 同行者, "尊敬", 10
			ENDIF
		ELSEIF EXP:PLAYER:音ゲー経験 >= 100
			CALL SCHOOL_KOJO, 同行者, "一緒に音ゲー"
			CALL 好感度上昇, 同行者, "友情", 10
			CALL 好感度上昇, 同行者, "尊敬", 10
		ELSE
			CALL SCHOOL_KOJO, 同行者, "一緒に音ゲー"
			CALL 好感度上昇, 同行者, "友情", 10
		ENDIF
		CALL EXPUP, "音ゲー経験", 1, PLAYER
		CALL EXPUP, "音ゲー経験", 1, 同行者
		MONEY -= 200
		WAIT
ENDSELECT

RETURN 1

@SCHOOL_MESSAGE_音ゲー中に会話
PRINTL 音ゲーを遊ぶことにした
PRINTFORML 音ゲーを遊んでいると、同じく音ゲーマーであろう%CALLNAME:TARGET%が話しかけてきた
IF EXP:PLAYER:音ゲー経験 < EXP:音ゲー経験
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%と音ゲーの話で盛り上がりながら、上達のコツを教えてくれた
ELSEIF EXP:PLAYER:音ゲー経験 >= 100
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の音ゲーの腕前を褒め称え、音ゲーの話で盛り上がった
ELSE
	PRINTFORML %CALLNAME:TARGET%と音ゲー談義をして盛り上がった
ENDIF

@SCHOOL_MESSAGE_一緒に音ゲー
PRINTFORML %CALLNAME:TARGET%と一緒に音ゲーを遊ぶことにした
IF EXP:PLAYER:音ゲー経験 <= EXP:音ゲー経験
	SELECTCASE 選択肢
		CASE 1
			PRINTFORML スコアでは負けてしまったが、充実した時間を過ごすことができた
		CASE 2
			PRINTFORML かろうじてスコアで勝利し、充実な時間を過ごすことができた
	ENDSELECT
ELSEIF EXP:PLAYER:音ゲー経験 >= 100
	PRINTFORML %CALLNAME:PLAYER%の鮮やかなプレイに、%CALLNAME:TARGET%は感嘆している様子だ
ENDIF
