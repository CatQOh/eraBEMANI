@ロケーション条件_ジョニーズ
ロケーション説明 = 北見エリカ、クロノス、カイロスがバイトで務めるメイドカフェ
RETURN -1

@ロケーション可否_ジョニーズ
;時間帯などで使えない場合は理由を書く
SIF ターン == "朝"
	ロケーション可否 = 営業準備中

@バイト条件_ジョニーズ
;バイトの時間帯などを書く
バイト要項 = 昼～夜のシフトで募集中。制服貸与
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "昼", "夕", "夜")
	バイト可能 = 1

RETURN -1

;バイト時に好感度が上がるキャラ
@職場出現キャラ_ジョニーズ, キャラ取得
#DIM DYNAMIC 対象
#DIM DYNAMIC キャラ取得
{
#DIMS DYNAMIC 出現キャラ =
"北見 エリカ",
"クロノス",
"カイロス",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF !キャラ取得
	CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_ジョニーズ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;バイト組 50%
		CASE "北見 エリカ", "クロノス", "カイロス", "おいわちゃん"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;お客さんたち メイド喫茶なのでメイドが来る（？？？）
		{
		CASE "ベリゼベス", "ニーソ姫", "恋刃", "ラッテ", "リエル", "グリム・リエル", "鬼蝮 ユリア", "葉和 とれび",
			 "ラズ", "シフォン", "ソルト", "春日 咲子", "芽兎 めう", "エスメラルダ"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;休日や放課後に集団で来ているグループとかがある
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				IF TALENT:LCOUNT:食欲皆伝
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 20
				ENDIF
				;Roots26キャラたち
				IF GROUPMATCH("Roots26", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 25
				ENDIF
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@ジョニーズ_専用コマンド
;メイド喫茶は割高
IF 同行者
	IF MONEY < 2000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%とお茶する(2000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  2] - %CALLNAME:同行者%とお茶する(1000＄)"
	ENDIF
ELSE
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - 飲み物を注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - 飲み物を注文する(1000＄)"
	ENDIF
	IF MONEY < 2000
		CALL COLUMNPRINTL, 0, "　[---] - 飲み物とオムライスを注文する(2000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  1] - 飲み物とオムライスを注文する(2000＄)"
	ENDIF
ENDIF

@ジョニーズ_専用コマンド_INPUT
#DIM DYNAMIC 対象

対象 = 0
選択肢 = CRESULT:1
IF キャラ現在地判定("北見 エリカ", "クロノス", "カイロス")
	DO
		CALL 職場出現キャラ_ジョニーズ, 1
		対象 = RESULT
	LOOP !対象
ENDIF

SELECTCASE CRESULT:1
	CASE 0
		IF 対象
			CALL SCHOOL_KOJO, 対象, "ジョニーズで注文"
			CALL 好感度上昇, 対象, "友情", 15
		ELSE
			PRINTL ジョニーズでメイドさんが魔法をかけてくれた飲み物を頂いた。いつもより美味しい気がする
		ENDIF
		MONEY -= 1000
	CASE 1
		IF 対象
			CALL SCHOOL_KOJO, 対象, "ジョニーズで注文"
			CALL 好感度上昇, 対象, "友情", 30
		ELSE
			PRINTL ジョニーズでメイドさんが魔法をかけてくれた飲み物とオムライスを頂いた。いつもより美味しい気がする
		ENDIF
		MONEY -= 2000
	CASE 2
		CALL SCHOOL_KOJO, 同行者, "ジョニーズでお茶する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 2000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_ジョニーズで注文
SELECTCASE 選択肢
	CASE 0
		PRINTFORML ジョニーズで飲み物を注文すると、持ってきてくれた%CALLNAME:TARGET%がおまじないをかけてくれた。いつもより美味しい気がする
	CASE 1
		PRINTFORML ジョニーズで飲み物とオムライスを注文すると、持ってきてくれた%CALLNAME:TARGET%がおまじないをかけてくれた。いつもより美味しい気がする
ENDSELECT

@SCHOOL_MESSAGE_ジョニーズでお茶する
PRINTFORML ジョニーズで%CALLNAME:TARGET%と楽しいティータイムの時間を過ごした
