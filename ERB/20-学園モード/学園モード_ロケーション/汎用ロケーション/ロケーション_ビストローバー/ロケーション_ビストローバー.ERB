@ロケーション条件_ビストローバー
RETURN -1

@ロケーション可否_ビストローバー
;時間帯などで使えない場合は理由を書く
SIF DOW(FLAG:曜日) == "火"
	ロケーション可否 = 毎週火曜日定休
SIF ターン == "朝"
	ロケーション可否 = 営業準備中

@バイト条件_ビストローバー
;バイトの時間帯などを書く
バイト要項 = 定休日（火）除く昼、夜のシフト。メイド服が似合う可愛い子ちゃん募集。野郎でも可
;バイト可能かどうかの条件式
IF GROUPMATCH(ターン, "昼", "夜")
	SIF DOW(FLAG:曜日) != "火"
		バイト可能 = 1
ENDIF
;返り値はその店の責任者で、面接官扱いになる。居なければ1でOK
RETURN GETCHARANAME("ジルチ")

;バイト時に好感度が上がるキャラ
@職場出現キャラ_ビストローバー, キャラ取得
#DIM DYNAMIC 対象
#DIM DYNAMIC キャラ取得
{
#DIMS DYNAMIC 出現キャラ =
"ジルチ",
"ナイア",
"チャオ",
"光伊 計名",
"識"
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF !キャラ取得
	CALL EXPUP, "料理経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_ビストローバー
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	IF RANDPER(1) && グループ数 < 10
		;相性の良いキャラと2～4人で行動している
		CALL クラスメイト選出, LCOUNT, "相性"
		;2人以上揃わなかったら無かったことにする
		SIF RESULT:0 == 0
			CONTINUE
		集団行動:グループ数:0 = LCOUNT
		集団行動:グループ数:1 = RESULT:0
		IF RANDPER(50)
			集団行動:グループ数:2 = RESULT:1
			SIF RANDPER(50)
				集団行動:グループ数:3 = RESULT:2
		ENDIF
		グループ数++
	ELSE
		SELECTCASE CHARANAMEF(LCOUNT)
			;店長
			CASE "ジルチ"
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 100
			;バイトやヘルプ
			CASE "菱宮 津軽", "シア", "ナイア", "識", "光伊 計名", "チャオ"
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 50
			;Roots26キャラ
			CASE "Roots26"
				$Roots26
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 25
			CASEELSE
				SIF GROUPMATCH("Roots26", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
					GOTO Roots26
				IF TALENT:LCOUNT:食欲皆伝
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 20
				ENDIF
				;地元で愛されるお店なので誰でも来る
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 1
		ENDSELECT
	ENDIF
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART


@ビストローバー_専用コマンド
IF 同行者
	IF MONEY < 2000
		CALL COLUMNPRINTL, 0, @"　[---] - %CALLNAME:同行者%と食事する(2000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, @"　[  1] - %CALLNAME:同行者%と食事する(2000＄)"
	ENDIF
ELSE
	IF MONEY < 1000
		CALL COLUMNPRINTL, 0, "　[---] - コースメニューを注文する(1000＄)", "gray"
	ELSE
		CALL COLUMNPRINTL, 0, "　[  0] - コースメニューを注文する(1000＄)"
	ENDIF
ENDIF

@ビストローバー_専用コマンド_INPUT
#DIM DYNAMIC 対象

対象 = 0
IF キャラ現在地判定("ジルチ", "チャオ", "ナイア", "識", "光伊 計名")
	DO
		CALL 職場出現キャラ_ビストローバー, 1
		対象 = RESULT
	LOOP !対象
ENDIF
SELECTCASE CRESULT:1
	CASE 0
		IF 対象
			CALL SCHOOL_KOJO, 対象, "ビストローバーで注文"
			CALL 好感度上昇, 対象, "友情", 20
		ELSE
			PRINTL ビストローバーでコースメニューを注文した。この価格とは思えないほどの味と量だ！
		ENDIF
		MONEY -= 1000
	CASE 1
		CALL SCHOOL_KOJO, 同行者, "ビストローバーで食事する"
		CALL 好感度上昇, 同行者, "友情", 30
		MONEY -= 2000
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_ビストローバーで注文
PRINTFORML コースメニューを注文して、持ってきてくれた%CALLNAME:TARGET%と談笑しつつ美味しい料理に舌鼓を打った

@SCHOOL_MESSAGE_ビストローバーで食事する
PRINTFORML ビストローバーで%CALLNAME:TARGET%と美味しい食事を楽しんだ
