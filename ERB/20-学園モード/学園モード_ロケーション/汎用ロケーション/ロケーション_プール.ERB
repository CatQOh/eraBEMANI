@ロケーション条件_プール
;汎用ロケーション
;主に夏季期間中の朝、昼のみ
ロケーション説明 = 7～9月の朝、昼に開放されている市民プール
SELECTCASE MONTH
	CASE 7 TO 9
		SELECTCASE ターン
			CASE "朝", "昼"
				RETURN -1
		ENDSELECT
ENDSELECT

RETURN 0

@ロケーション可否_プール
SIF !INRANGE(MONTH, 7, 9)
	ロケーション可否 = 夏季のみオープン
SIF !GROUPMATCH(ターン, "朝", "昼")
	ロケーション可否 = 朝と昼のみ営業

@出現キャラ_プール
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;水が好きそうなキャラたちがいる
		{
		CASE "レーヴァスレーター", "パール", "Aegis", "マーメイドガール", "ジュリア", 
			 "テトラ", "ベンベン", "クアトロ", "ティア", "グリム・ミュー", "キララ", "オンディーヌ・シフィカ・チハ",
			 "シェリー・F・アレスターズ", "ロクミ", "丹子", "犬公 ミミ美"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;集団で来る人が多い
		CASEELSE
			IF CSTR:LCOUNT:在校 != "" && RANDPER(5) && グループ数 < 10
				;相性の良いキャラと3人以上で行動している
				CALL クラスメイト選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				集団行動:グループ数:2 = RESULT:1
				SIF RANDPER(30)
					集団行動:グループ数:3 = RESULT:2
				グループ数++
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
