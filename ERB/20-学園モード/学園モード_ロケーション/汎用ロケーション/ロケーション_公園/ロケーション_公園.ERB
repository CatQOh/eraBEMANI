@ロケーション条件_公園
;汎用ロケーション 条件無し
RETURN -1

@出現キャラ_公園
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	始点 = 1
	終点 = CHARANUM
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;子供や変な人がよくいる
	SELECTCASE CHARANAMEF(LCOUNT)
		{
		CASE "神崎 士朗", "北見 エリカ", "ルルスス", "スタンパ", "赤シグ", "黄シグ", "青シグ", 
			 "ひなた", "アイ", "まゆ", "大犬のお姉さん", "蒼", "雛", "桃", "ニア", "ノア", 
			 "カヲル", "アシタ", "タマヨ", "ナズナ", "スズナ", "おんなのこ", "歩", "ハヤト", 
			 "育江", "サン", "白鳥 姫子", "アリシア", "風雅", "烈", "フローラ", "ポチコ", 
			 "ミニッツ", "茜", "アミュレット", "あい", "サニー", "セイナ", "ローズ", "リリカ", "ココ", 
			 "ラスク", "グリム・ラスク", "電人イェーガー", "ライオンくん", "一鬼 アカネ", "らいむ", "イングリット・オーリック・コーネル",
			 "ミア", "西日暮里さん", "ゴリラ", "ノネット"
		}
			;さすがに夜は子どもたちは帰る
			SIF ターン == "夜" && TALENT:LCOUNT:小柄体型
				CONTINUE
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 30
		;集団で公園に来るような人はあまりいない
		CASEELSE
			IF CSTR:LCOUNT:在校 != "" && RAND:300 == 0 && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				CALL クラスメイト選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～3人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				SIF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
				グループ数++
			ENDIF
	ENDSELECT
NEXT
