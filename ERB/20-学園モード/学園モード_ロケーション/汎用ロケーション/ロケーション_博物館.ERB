@ロケーション条件_博物館
RETURN -1

@ロケーション可否_博物館
;時間帯などで使えない場合は理由を書く
SIF ターン == "夜"
	ロケーション可否 = 本日閉館

@バイト条件_博物館
;バイトの時間帯などを書く
バイト要項 = 展示物の掃除スタッフ募集
;バイト可能かどうかの条件式
SIF ターン != "夜"
	バイト可能 = 1
RETURN -1

@出現キャラ_博物館
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	IF RANDPER(2) && グループ数 < 10
		;相性の良いキャラと2～3人で行動している
		CALL グループキャラ選出, LCOUNT, "相性"
		;2人以上揃わなかったら無かったことにする
		SIF RESULT:0 == 0
			CONTINUE
		;2～4人のグループ
		集団行動:グループ数:0 = LCOUNT
		集団行動:グループ数:1 = RESULT:0
		IF RANDPER(30)
			集団行動:グループ数:2 = RESULT:1
			SIF RANDPER(30)
				集団行動:グループ数:3 = RESULT:2
		ENDIF
		グループ数++
	ELSE
		IF TALENT:LCOUNT:ネクロマンサー || TALENT:LCOUNT:倒錯的
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		ENDIF
	ENDIF
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

@博物館_専用コマンド
IF MONEY < 500
	CALL COLUMNPRINTL, 0, "　[0] - 入場券を買う(500＄必要)", "gray"
ELSE
	CALL COLUMNPRINTL, 0, "　[0] - 入場券を買う(500＄)"
ENDIF

@博物館_専用コマンド_INPUT
IF 同行者
	CALL SCHOOL_KOJO, 同行者, "博物館鑑賞"
	CALL 好感度上昇, 同行者, "友情", 10
ELSE
	PRINTL 博物館を見て回った。どの展示物もまるで生きているかのようにリアルだ！
ENDIF
MONEY -= 500
WAIT
RETURN 1

@SCHOOL_MESSAGE_博物館鑑賞
PRINTFORML %CALLNAME:TARGET%と一緒に博物館を見て回り、展示物について会話を交わした
