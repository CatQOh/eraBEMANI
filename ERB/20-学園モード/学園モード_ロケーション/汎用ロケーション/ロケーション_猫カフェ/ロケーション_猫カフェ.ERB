@ロケーション条件_猫カフェ
RETURN -1

@ロケーション可否_猫カフェ
;時間帯などで使えない場合は理由を書く
IF ターン == "朝"
	ロケーション可否 = 営業準備中
ELSEIF ターン == "夜"
	ロケーション可否 = 猫たちはおねむ
ENDIF

@バイト条件_猫カフェ
;バイトの時間帯などを書く
バイト要項 = 昼、夕方のシフトで募集中
;バイト可能かどうかの条件式
SIF GROUPMATCH(ターン, "昼", "夕")
	バイト可能 = 1

RETURN -1

@出現キャラ_猫カフェ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;猫は下で出現率設定してるので猫以外で来そうなキャラ
		{
		CASE "北見 エリカ", "神崎 士朗", "ニクス", "雛月 理々奈", "シア", "ＴＡＭＡ", "サトウさん", "風雅", "大牙",
			 "BisCo", "山形 まり花", "虹霓・シエル・奈奈", "マチコ", "歩", "ハート","空羽 亜乃亜", "ひなた", "まゆ"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;休日や放課後に集団で来ているグループとかがある
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ELSE
				;猫が居る
				IF TALENT:LCOUNT:猫
					SIF 出現率:LCOUNT >= 0
						出現率:LCOUNT = 20
				ENDIF
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
