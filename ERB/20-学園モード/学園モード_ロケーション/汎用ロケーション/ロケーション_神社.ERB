@ロケーション条件_神社
ロケーション説明 = 威厳のある神社。社務所や手水舎もある
RETURN -1

@バイト条件_神社
;バイトの時間帯などを書く
バイト要項 = 土日の朝～夕方のシフト募集。女性のみ
;バイト可能かどうかの条件式
SIF !GROUPMATCH(ターン, "夜") && 休日() && SEX(PLAYER) == 2
	バイト可能 = 1
;ロケーション解禁済みで可能
;返り値はその店の責任者で、面接官扱いになる。居なければ-1でOK
IF FINDELEMENTF(解禁済みロケーション, "テンプレート") >= 0
	SELECTCASE RAND:2
		CASE 0
			RETURN GETCHARANAME("神楽")
		CASE 1
			RETURN GETCHARANAME("みここ")
	ENDSELECT
ENDIF

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_神社
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"神楽",
"みここ",
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_神社
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		;巫女と関係者
		CASE "みここ", "神楽", "紅刃", "恋刃"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;神社に居そうなキャラ
		{
		CASE "梅桐 天土", "卑弥呼", "天乃 探女", "椿", "カガミ", "クニオシ", "テンコ", "てまり", "玉藻 銀丸",
			 "ぐわんぜさま", "小坊主", "月士 華風魔", "メリー・ラ・テラス"
		}
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;休日は参拝客が来る
		CASEELSE
			IF RAND:200 == 0 && グループ数 < 10 && 休日()
				;相性の良いキャラと2～3人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～3人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				SIF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
				グループ数++
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
