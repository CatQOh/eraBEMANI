@バイトに行く
#DIM DYNAMIC 対象

勤務地 =
CSTR:PLAYER:現在地 = %バイト先%
TRYCALLFORM バイト条件_%RPS(バイト先)%
SIF 勤務地 != ""
	CSTR:PLAYER:現在地 = %勤務地%

PRINTFORMW %バイト先%のバイトに行くことにした
DRAWLINE
PRINTW ………………
PRINTW …………
PRINTW ……
CALL PASELIW, @"一通りの仕事を終えた。2000＄の給料を受け取った！(所持金:{MONEY}＄→{MONEY+2000}＄)"

RESULT = 0
TRYCALLFORM 職場出現キャラ_%RPS(バイト先)%
IF RESULT
	対象 = RESULT
	CALL SCHOOL_KOJO, 対象, "バイト終了"
	CALL 好感度上昇, 対象, "友情", 10
ENDIF

MONEY += 2000
PRINTL さて、これからどうしよう？
PRINTL [0] - 家に帰る
PRINTL [1] - 職場を散策する
BINPUT
SELECTCASE RESULT
	CASE 0
		CSTR:PLAYER:現在地 = 自宅
		RETURN 1
	CASE 1
		バイト帰り = 1
		CALL ロケーション選択, 1
		バイト帰り = 0
		CSTR:PLAYER:現在地 = 自宅
		RETURN RESULT
ENDSELECT

@SCHOOL_MESSAGE_バイト終了
PRINTFORMW %CALLNAME:TARGET%はバイトを終えた%CALLNAME:PLAYER%を労ってくれた
