@バイトを探す
#DIMS DYNAMIC SPLITVAR, 2
#DIM DYNAMIC 総数
#DIMS DYNAMIC 応募先
#DIM DYNAMIC 面接官
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 応募先関数

DRAWLINE

ENUMFUNCBEGINSWITH "バイト条件_"
総数 = RESULT
IF !総数
	PRINTL 
	PRINTL -----求人がありません-----
	PRINTW 
	RETURN 0
ELSE
	FOR LCOUNT, 0, 総数
		SPLIT RESULTS:LCOUNT, "_", SPLITVAR
		CALLFORM %RESULTS:LCOUNT%
		SIF RESULT && バイト先 != SPLITVAR:1
			PRINTFORML [{LCOUNT, 2}] - %SPLITVAR:1% %バイト要項%
	NEXT
ENDIF
DRAWLINE
PRINTPLAIN 　求人一覧　
PRINTL [-1] 戻る

BINPUT

SIF RESULT == -1
	RETURN 0

応募先関数 = %RESULTS:RESULT%

CALLFORM %応募先関数%
面接官 = RESULT

SPLIT 応募先関数, "_", SPLITVAR
応募先 = %SPLITVAR:1%

DRAWLINE
PRINTFORMW %応募先%のバイトの面接に行った
IF 面接官 >= 0
	PRINTFORMW 面接を行った%CALLNAME:面接官%は%CALLNAME:PLAYER%を採用してくれた！
	CALL SCHOOL_KOJO, 面接官, "面接"
ELSE
	PRINTFORMW 面接の結果、採用が決定した！
ENDIF

バイト先 = %SPLITVAR:1%

RETURN 1
