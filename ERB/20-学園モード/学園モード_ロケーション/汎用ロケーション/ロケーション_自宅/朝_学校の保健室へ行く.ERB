@朝_学校の保健室に行く
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 保健医
#DIM DYNAMIC 保健室滞在者
#DIM DYNAMIC 候補, 50
#DIM DYNAMIC 候補人数
#DIM DYNAMIC 睡眠中

CSTR:PLAYER:現在地 = %CSTR:PLAYER:在校%

FOR LCOUNT, 1, CHARANUM
	;保健医がいれば設定
	IF CSTR:LCOUNT:在任 == CSTR:PLAYER:在校 && CSTR:LCOUNT:担当科目 == "保健"
		保健医 = LCOUNT
		BREAK
	ENDIF
	IF CSTR:LCOUNT:在任 == CSTR:PLAYER:在校 || CSTR:LCOUNT:在校 == CSTR:PLAYER:在校
		候補:候補人数 = LCOUNT
		候補人数++
	ENDIF
NEXT

;保健医がいなければランダムで誰かが居る
SIF !保健医
	保健室滞在者 = 候補:(RAND:候補人数)

IF 保健医
	PRINTFORML %CALLNAME:PLAYER%が保健室に行くと、保健医の%CALLNAME:保健医%先生が対応してくれた
	TARGET = 保健医
	PRINTL [0] - 具合が悪いので保健室で休む
	PRINTL [1] - 授業に戻る
ELSE
	IF CSTR:保健室滞在者:在任 != ""
		PRINTFORML %CALLNAME:PLAYER%が保健室に行くと、%CSTR:保健室滞在者:担当科目%の%CALLNAME:保健室滞在者%先生が対応してくれた
		TARGET = 保健室滞在者
		PRINTL [0] - 具合が悪いので保健室で休む
		PRINTL [1] - 授業に戻る
	ELSE
		;20%で睡眠中
		SIF RANDPER(20)
			睡眠中 = 1
		PRINTFORM %CALLNAME:PLAYER%が保健室に行くと、
		IF 睡眠中
			PRINTFORML %CSTR:保健室滞在者%がベッドで眠っていた
		ELSE
			PRINTFORML %CSTR:保健室滞在者%も保健室に訪れていた
		ENDIF
		TARGET = 保健室滞在者
		PRINTL [0] - 具合が悪いので保健室で休む
		PRINTL [1] - 授業に戻る
	ENDIF
ENDIF

CALL INPUTF, 0, 1

SELECTCASE RESULT
	CASE 0
		IF CSTR:在任 != ""
			PRINTFORML □どこが悪いのかと%CALLNAME:TARGET%が聞いてきた
			PRINTL 　[0] - だるいので安静にしたい
			PRINTL 　[1] - 実はサボりだと言う
			PRINTL 　[2] - 股間の具合が悪い
			PRINTL 　[3] - 治ったので授業に戻る
			CALL INPUTF, 0, 1, 2, 3
			SELECTCASE RESULT
				CASE 0
					PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%をベッドに寝かせ、静かにカーテンを閉めた
					CALL 好感度上昇, TARGET, "舎弟", 10
				CASE 1
					PRINTFORMW %CALLNAME:TARGET%は呆れた様子で%CALLNAME:PLAYER%を叱り、授業に戻るよう諭した
					CALL 好感度上昇, TARGET, "舎弟", 10
				CASE 2
					IF 好感度:既成事実 >= 10
						PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、廊下に誰も居ないことを確認し、鍵を掛けてベッドへと向かった
					ELSEIF CFLAG:倫理観 >= 30
						PRINTFORMW %CALLNAME:TARGET%は心配するフリをして%CALLNAME:PLAYER%の体に触れると、治療が必要と言ってベッドに連れ込んだ
					ELSEIF 好感度:セフレ >= 1000
						PRINTFORMW %CALLNAME:TARGET%は少し困惑したものの、手早く済ませることを条件にベッドに向かった
					ELSE
						PRINTFORMW %CALLNAME:TARGET%は気まずそうに言葉を濁しながら、やんわりと%CALLNAME:PLAYER%を保健室から追い出した
						CALL 好感度上昇, TARGET, "ときめき", -10
						CALL 好感度上昇, TARGET, "舎弟", -10
						WAIT
						RETURN 1
					ENDIF
					CALL TRAIN_SCHOOL
					CALL CHECKDRAWLINE
					PRINTFORMW %CALLNAME:TARGET%は衣服を直し、淫らな残り香が漂う保健室に芳香剤を振り撒いて%CALLNAME:PLAYER%を見送った
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 2
					CFLAG:倫理観 += 2
				CASE 3
					PRINTFORMW %CALLNAME:TARGET%は何も疑わずに%CALLNAME:PLAYER%を見送った
					JUMP SHOP_SCHOOL
			ENDSELECT
		ELSE
			IF 睡眠中
				PRINTFORML □%CALLNAME:TARGET%はベッドで眠っている
				PRINTL 　[0] - 適当に休む
				PRINTFORML 　[1] - %CALLNAME:TARGET%にいたずらする
				PRINTL 　[2] - 治ったので授業に戻る
				BINPUT
				SELECTCASE RESULT
					CASE 0
						PRINTFORMW %CALLNAME:PLAYER%は保健室で休んだ
					CASE 1
						PRINTFORMW %CALLNAME:TARGET%は無防備に眠っている。起こさない程度にいたずらをすることにした
						TEQUIP:睡眠 = 15
						DRAWLINE
						CALL TRAIN_SCHOOL
						CALL CHECKDRAWLINE
						PRINTFORMW 満足した%CALLNAME:PLAYER%は、ベッドの布団を元に戻し、%CALLNAME:TARGET%が気づかないうちに保健室を後にした
					CASE 2
						PRINTFORMW %CALLNAME:TARGET%を起こさないように保健室を後にした
						JUMP SHOP_SCHOOL
				ENDSELECT
			ELSE
				PRINTFORML □%CALLNAME:TARGET%は%CALLNAME:PLAYER%を一瞥すると、軽く声を掛けてきた
				PRINTL 　[0] - 軽い挨拶で返す
				PRINTL 　[1] - 盛り上がりそうな話を振る
				PRINTL 　[2] - ベッドのシェアを提案する
				PRINTL 　[3] - 治ったので授業に戻る
				BINPUT
				SELECTCASE RESULT
					CASE 0
						PRINTFORMW %CALLNAME:TARGET%も挨拶に応え、一言二言程度の軽い会話を交わした
						CALL 好感度上昇, TARGET, "友情", 10
					CASE 1
						IF 好感度:友情 >= 200 || TALENT:サボり魔
							PRINTFORMW %CALLNAME:TARGET%は具合が悪そうな素振りも見せずに%CALLNAME:PLAYER%との会話を楽しんだ
							CALL 好感度上昇, TARGET, "友情", 10
							CALL 好感度上昇, TARGET, "ときめき", 10
						ELSE
							PRINTFORMW %CALLNAME:TARGET%は本当に具合が悪いのか、%CALLNAME:PLAYER%の話を不快そうな顔で遮った……
							CALL 好感度上昇, TARGET, "ときめき", -10
						ENDIF
					CASE 2
						IF 好感度:既成事実 >= 10
							PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、廊下に誰も居ないことを確認し、鍵を掛けてベッドへと向かった
						ELSEIF CFLAG:倫理観 >= 30
							PRINTFORMW %CALLNAME:TARGET%は少し考え込むと、制服のボタンを外しながら提案に乗った
						ELSEIF 好感度:セフレ >= 1000
							PRINTFORMW %CALLNAME:TARGET%は少し戸惑ったが、好奇心のほうが勝ったのか早めに済ませることを強調して提案に乗った
						ELSE
							PRINTFORMW %CALLNAME:TARGET%は引き気味に言葉を濁し、逃げるように保健室から出ていってしまった……
							CALL 好感度上昇, TARGET, "ときめき", -10
							WAIT
							RETURN 1
						ENDIF
						DRAWLINE
						CALL TRAIN_SCHOOL
						CALL CHECKDRAWLINE
						PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、汗と体液で汚れた体をアルコールティッシュで拭き、保健室を後にした
						CALL 好感度上昇, TARGET, "友情", 30
						CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
						CALL 好感度上昇, TARGET, "セフレ", 50
						CALL 好感度上昇, TARGET, "既成事実", 1
						SIF !TALENT:恋慕
							CFLAG:倫理観++
						CFLAG:倫理観++
					CASE 3
						PRINTFORMW 怪訝そうな顔をする%CALLNAME:TARGET%に会釈して保健室を後にした……
						JUMP SHOP_SCHOOL
				ENDSELECT
			ENDIF
		ENDIF
	CASE 1
		JUMP SHOP_SCHOOL
ENDSELECT
WAIT
RETURN 1
