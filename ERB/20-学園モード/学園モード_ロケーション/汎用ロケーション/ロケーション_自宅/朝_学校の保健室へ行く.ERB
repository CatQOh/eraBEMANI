@朝_学校の保健室に行く
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 保健医, 10
#DIM DYNAMIC 保健室滞在者
#DIM DYNAMIC 候補, 50
#DIM DYNAMIC 優先, 50
#DIM DYNAMIC 候補人数, 3
#DIM DYNAMIC 睡眠中

CSTR:PLAYER:現在地 = %CSTR:PLAYER:在校%

;3分の1の確率で運命の人
IF !RAND:3 && 運命の人 != "" && (CSTR:GETCHARANAME(運命の人):在任 == CSTR:PLAYER:在校 || CSTR:GETCHARANAME(運命の人):在校 == CSTR:PLAYER:在校)
	保健室滞在者 = GETCHARANAME(運命の人)
ELSE
	FOR LCOUNT, 1, CHARANUM
		;保健医とそれ以外を別々にカウント
		IF CSTR:LCOUNT:在任 == CSTR:PLAYER:在校 && CSTR:LCOUNT:担当科目 == "保健"
			保健医:(候補人数:2) = LCOUNT
			候補人数:2++
		ELSEIF CSTR:LCOUNT:在任 == CSTR:PLAYER:在校 || CSTR:LCOUNT:在校 == CSTR:PLAYER:在校
			IF 好感度合計(LCOUNT)
				優先:(候補人数:1) = LCOUNT
				候補人数:1++
			ENDIF
			候補:候補人数 = LCOUNT
			候補人数++
		ENDIF
	NEXT
	;運命の人ではない場合場合3分の2の確率で保健医の中からランダム すり抜けた場合保健医以外からランダム
	IF 候補人数:2 && RAND:3 
		保健室滞在者 = 保健医:(RAND:(候補人数:2))
	ELSE
		IF 候補人数:1
			保健室滞在者 = 優先:(RAND:(候補人数:1))
		ELSE
			保健室滞在者 = 候補:(RAND:候補人数)
		ENDIF
	ENDIF
ENDIF

IF CSTR:保健室滞在者:担当科目 == "保健"
	PRINTFORML □%CALLNAME:PLAYER%が保健室に行くと、保健医の%CALLNAME:保健医%先生が対応してくれた
	TARGET = 保健室滞在者
	CALL SCHOOL_KOJO, TARGET, "保健室"
	PRINTL 　[0] - 具合が悪いので保健室で休む
	PRINTL 　[1] - 授業に戻る
ELSE
	IF CSTR:保健室滞在者:在任 != ""
		PRINTFORML □%CALLNAME:PLAYER%が保健室に行くと、%CSTR:保健室滞在者:担当科目%の%CALLNAME:保健室滞在者%先生が対応してくれた
		TARGET = 保健室滞在者
		CALL SCHOOL_KOJO, TARGET, "保健室"
		PRINTL 　[0] - 具合が悪いので保健室で休む
		PRINTL 　[1] - 授業に戻る
	ELSE
		;20%で睡眠中
		SIF RANDPER(20)
			睡眠中 = 1
		PRINTFORM □%CALLNAME:PLAYER%が保健室に行くと、
		IF 睡眠中
			PRINTFORML %CALLNAME:保健室滞在者%がベッドで眠っていた
		ELSE
			PRINTFORML %CALLNAME:保健室滞在者%も保健室に訪れていた
		ENDIF
		TARGET = 保健室滞在者
		SIF !睡眠中
			CALL SCHOOL_KOJO, TARGET, "保健室"
		PRINTL 　[0] - 具合が悪いので保健室で休む
		PRINTL 　[1] - 授業に戻る
	ENDIF
ENDIF

BINPUT

SELECTCASE RESULT
	CASE 0
		IF CSTR:在任 != ""
			IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_保健室")
				CALL SCHOOL_KOJO, TARGET, "コマンド_保健室"
				RETURN RESULT
			ENDIF
			CALL SCHOOL_KOJO, TARGET, "保健室で休む"
			PRINTL 　[0] - だるいので安静にしたい
			PRINTL 　[1] - 実はサボりだと言う
			PRINTL 　[2] - 股間の具合が悪い
			PRINTL 　[3] - 治ったので授業に戻る
			BINPUT
			SELECTCASE RESULT
				CASE 0
					CALL SCHOOL_KOJO, TARGET, "保健室_安静"
					CALL 好感度上昇, TARGET, "舎弟", 10
				CASE 1
					CALL SCHOOL_KOJO, TARGET, "保健室_サボり"
					CALL 好感度上昇, TARGET, "舎弟", 10
				CASE 2
					IF 好感度:既成事実 >= 10
					ELSEIF CFLAG:倫理観 >= 30
					ELSEIF 好感度:セフレ >= 1000
					ELSE
						CALL SCHOOL_KOJO, TARGET, "保健室_誘惑_失敗"
						CALL 好感度上昇, TARGET, "ときめき", -10
						CALL 好感度上昇, TARGET, "舎弟", -10
						WAIT
						RETURN 1
					ENDIF
					DRAWLINE
					CALL SCHOOL_KOJO, TARGET, "保健室_誘惑_成功"
					CALL TRAIN_SCHOOL, "保健室"
					CALL CHECKDRAWLINE
					CALL SCHOOL_KOJO, TARGET, "保健室_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 2
					CFLAG:倫理観 += 2
				CASE 3
					CALL SCHOOL_KOJO, TARGET, "保健室で休まない"
					RETURN 0
			ENDSELECT
		ELSE
			IF 睡眠中
				PRINTFORML □%CALLNAME:TARGET%はベッドで眠っている
				PRINTL 　[0] - 適当に休む
				PRINTFORML 　[1] - %CALLNAME:TARGET%にいたずらする
				PRINTL 　[2] - 治ったので授業に戻る
				BINPUT
				;ここだけ対象が喋らないのでここで地の文処理
				SELECTCASE RESULT
					CASE 0
						PRINTFORMW %CALLNAME:PLAYER%は保健室で休んだ
					CASE 1
						PRINTFORMW %CALLNAME:TARGET%は無防備に眠っている。起こさない程度にいたずらをすることにした
						TEQUIP:睡眠 = 15
						DRAWLINE
						CALL TRAIN_SCHOOL, "保健室"
						CALL CHECKDRAWLINE
						PRINTFORMW 満足した%CALLNAME:PLAYER%は、ベッドの布団を元に戻し、%CALLNAME:TARGET%が気づかないうちに保健室を後にした
					CASE 2
						PRINTFORMW %CALLNAME:TARGET%を起こさないように保健室を後にした
						RETURN 0
				ENDSELECT
			ELSE
				IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_保健室")
					CALL SCHOOL_KOJO, TARGET, "コマンド_保健室"
					RETURN RESULT
				ENDIF
				CALL SCHOOL_KOJO, TARGET, "保健室"
				PRINTFORML □%CALLNAME:TARGET%は%CALLNAME:PLAYER%を一瞥すると、軽く声を掛けてきた
				PRINTL 　[0] - 軽い挨拶で返す
				PRINTL 　[1] - 盛り上がりそうな話を振る
				PRINTL 　[2] - ベッドのシェアを提案する
				PRINTL 　[3] - 治ったので授業に戻る
				BINPUT
				SELECTCASE RESULT
					CASE 0
						CALL SCHOOL_KOJO, TARGET, "保健室_挨拶"
						CALL 好感度上昇, TARGET, "友情", 10
					CASE 1
						IF 好感度:友情 >= 200 || TALENT:サボり魔
							CALL SCHOOL_KOJO, TARGET, "保健室_会話_成功"
							CALL 好感度上昇, TARGET, "友情", 10
							CALL 好感度上昇, TARGET, "ときめき", 10
						ELSE
							CALL SCHOOL_KOJO, TARGET, "保健室_会話_失敗"
							CALL 好感度上昇, TARGET, "ときめき", -10
						ENDIF
					CASE 2
						IF 好感度:既成事実 >= 10
						ELSEIF CFLAG:倫理観 >= 30
						ELSEIF 好感度:セフレ >= 1000
						ELSE
							CALL SCHOOL_KOJO, TARGET, "保健室_ベッドシェア_失敗"
							CALL 好感度上昇, TARGET, "ときめき", -10
							WAIT
							RETURN 1
						ENDIF
						CALL SCHOOL_KOJO, TARGET, "保健室_ベッドシェア_成功"
						DRAWLINE
						CALL TRAIN_SCHOOL, "保健室"
						CALL CHECKDRAWLINE
						CALL SCHOOL_KOJO, TARGET, "保健室_行為後"
						CALL 好感度上昇, TARGET, "友情", 30
						CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
						CALL 好感度上昇, TARGET, "セフレ", 50
						CALL 好感度上昇, TARGET, "既成事実", 1
						SIF !TALENT:恋慕
							CFLAG:倫理観++
						CFLAG:倫理観++
					CASE 3
						CALL SCHOOL_KOJO, TARGET, "保健室で休まない"
						RETURN 0
				ENDSELECT
			ENDIF
		ENDIF
	CASE 1
		IF 睡眠中
			PRINTFORMW %CALLNAME:TARGET%を起こさないように保健室を後にした
		ELSE
			CALL SCHOOL_KOJO, TARGET, "保健室で休まない"
		ENDIF
		RETURN 0
ENDSELECT
WAIT
RETURN 1

@SCHOOL_MESSAGE_保健室で休む
PRINTFORML □どこが悪いのかと%CALLNAME:TARGET%が聞いてきた

@SCHOOL_MESSAGE_保健室_安静
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%をベッドに寝かせ、静かにカーテンを閉めた

@SCHOOL_MESSAGE_保健室_サボり
PRINTFORMW %CALLNAME:TARGET%は呆れた様子で%CALLNAME:PLAYER%を叱り、授業に戻るよう諭した

@SCHOOL_MESSAGE_保健室_誘惑_失敗
PRINTFORMW %CALLNAME:TARGET%は気まずそうに言葉を濁しながら、やんわりと%CALLNAME:PLAYER%を保健室から追い出した

@SCHOOL_MESSAGE_保健室_誘惑_成功
IF 好感度:既成事実 >= 10
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、廊下に誰も居ないことを確認し、鍵を掛けてベッドへと向かった
ELSEIF CFLAG:倫理観 >= 30
	PRINTFORMW %CALLNAME:TARGET%は心配するフリをして%CALLNAME:PLAYER%の体に触れると、治療が必要と言ってベッドに連れ込んだ
ELSEIF 好感度:セフレ >= 1000
	PRINTFORMW %CALLNAME:TARGET%は少し困惑したものの、手早く済ませることを条件にベッドに向かった
ENDIF

@SCHOOL_MESSAGE_保健室_挨拶
PRINTFORMW %CALLNAME:TARGET%も挨拶に応え、一言二言程度の軽い会話を交わした

@SCHOOL_MESSAGE_保健室_会話_失敗
PRINTFORMW %CALLNAME:TARGET%は本当に具合が悪いのか、%CALLNAME:PLAYER%の話を不快そうな顔で遮った……

@SCHOOL_MESSAGE_保健室_会話_成功
PRINTFORMW %CALLNAME:TARGET%は具合が悪そうな素振りも見せずに%CALLNAME:PLAYER%との会話を楽しんだ

@SCHOOL_MESSAGE_保健室_ベッドシェア_失敗
PRINTFORMW %CALLNAME:TARGET%は引き気味に言葉を濁し、逃げるように保健室から出ていってしまった……

@SCHOOL_MESSAGE_保健室_ベッドシェア_成功
IF 好感度:既成事実 >= 10
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、廊下に誰も居ないことを確認し、鍵を掛けてベッドへと向かった
ELSEIF CFLAG:倫理観 >= 30
	PRINTFORMW %CALLNAME:TARGET%は少し考え込むと、制服のボタンを外しながら提案に乗った
ELSEIF 好感度:セフレ >= 1000
	PRINTFORMW %CALLNAME:TARGET%は少し戸惑ったが、好奇心のほうが勝ったのか早めに済ませることを強調して提案に乗った
ENDIF

@SCHOOL_MESSAGE_保健室で休まない
IF CSTR:在任 != ""
	PRINTFORMW %CALLNAME:TARGET%は何も疑わずに%CALLNAME:PLAYER%を見送った
ELSE
	PRINTFORMW 怪訝そうな顔をする%CALLNAME:TARGET%に会釈して保健室を後にした……
ENDIF

@SCHOOL_MESSAGE_

@SCHOOL_MESSAGE_

@SCHOOL_MESSAGE_
