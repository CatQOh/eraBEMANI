@朝_学校へ行く
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 候補人数
#DIM DYNAMIC 合流者

;好感度の高いキャラが合流することがある（他の学校でも）
;複数人居る場合はランダム ただし教師陣は例外
FOR LCOUNT, 1, CHARANUM
	SIF 好感度合計(LCOUNT) < 100
		CONTINUE
	SIF CSTR:LCOUNT:在任 != ""
		CONTINUE
	候補人数++
	IF !合流者
		合流者 = LCOUNT
	ELSE
		SIF RAND:候補人数 == 0
			合流者 = LCOUNT
	ENDIF
NEXT

IF !合流者
	PRINTFORMW 何事もなく%CSTR:PLAYER:在校%に到着した
	RETURN 0
ENDIF

IF CSTR:合流者:在校 == CSTR:PLAYER:在校
	PRINTFORML 同じ学校の%CALLNAME:合流者%が登校中に話しかけてきた
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:合流者%は一緒に学校まで登校した
	CALL 好感度上昇, 合流者, "友情", 20
ELSE
	IF CSTR:合流者:在校 != ""
		PRINTFORML %CSTR:合流者:在校%の%CALLNAME:合流者%が登校中に話しかけてきた
		PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:合流者%は途中までの通学路を共にした
		CALL 好感度上昇, 合流者, "友情", 10
	ELSE
		PRINTFORML 偶然出会った%CALLNAME:合流者%が登校中に話しかけてきた
		PRINTFORMW %CALLNAME:合流者%は途中までの通学路を共にし、学校の手前で%CALLNAME:PLAYER%と別れた
		CALL 好感度上昇, 合流者, "友情", 10
	ENDIF
ENDIF

WAIT

CSTR:PLAYER:現在地 = %CSTR:PLAYER:在校%

;登校はターン消費しない
RETURN 0
