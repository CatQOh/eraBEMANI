@色気を出す

SIF !同行者
	RETURN 0

TARGET = 同行者

IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_色気を出す")
	CALL SCHOOL_KOJO, TARGET, "コマンド_色気を出す"
	RETURN RESULT
ENDIF

PRINTFORMW 火照った体を冷ますように衣服をはだけさせ、%CALLNAME:TARGET%を誘ってみた
IF CFLAG:倫理観 >= 80
	PRINTFORMW 意図を察した%CALLNAME:TARGET%も乗り気で衣服を崩し始め、%CALLNAME:PLAYER%との行為に臨んだ
	CALL SCHOOL_KOJO, TARGET, "色気を出す_成功"
	CALL 好感度上昇, TARGET, "セフレ", 20
ELSEIF 好感度:既成事実
	IF TALENT:恋慕
		PRINTFORMW %CALLNAME:TARGET%は突然色っぽくなった%CALLNAME:PLAYER%に、看病に来たことなど忘れて欲情し始めた
		CALL SCHOOL_KOJO, TARGET, "色気を出す_成功"
		CALL 好感度上昇, TARGET, "ときめき", 30
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の肌の露出にかつての行為を思い出し、我慢できずに体に触れてきた
		CALL SCHOOL_KOJO, TARGET, "色気を出す_成功"
		CALL 好感度上昇, TARGET, "ときめき", 10
	ENDIF
ELSEIF TALENT:恋慕
	IF TALENT:恥じらい
		PRINTFORMW %CALLNAME:PLAYER%の大胆な誘いに恥じらいの色を隠せない%CALLNAME:TARGET%だったが、恋人との思いを遂げるチャンスに屈したのか消極的に体に触れてきた
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は恋人である%CALLNAME:PLAYER%のふしだらな姿に息を呑み、さらに別の場所をさらけ出すと、とうとう我慢できなくなったようだ
	ENDIF
	CALL SCHOOL_KOJO, TARGET, "色気を出す_成功"
	CALL 好感度上昇, TARGET, "ときめき", 30
ELSEIF 好感度:セフレ >= 1000
	IF TALENT:恥じらい
		PRINTFORMW はだけたせいで普段見えないところを見せられた%CALLNAME:TARGET%は顔を赤くして目をそらすが、%CALLNAME:PLAYER%のさらなる誘惑に我慢できなくなったようだ
	ELSE
		PRINTFORMW 普段から性的な話をすることが多かった%CALLNAME:TARGET%は、とうとう一線を越えようとする%CALLNAME:PLAYER%に一瞬戸惑うものの、興味と好奇心に屈したようだ
	ENDIF
	CALL SCHOOL_KOJO, TARGET, "色気を出す_成功"
	CALL 好感度上昇, TARGET, "ときめき", 10
ELSE
	IF TALENT:幼稚; || TALENT:治療
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%が元気そうになったのを見て安心したのか、誘惑の意図すら届かずにそのまま帰ってしまった……
		CALL SCHOOL_KOJO, TARGET, "色気を出す_失敗"
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は一瞬困惑したが、あくまでも理性的に振る舞うと歯切れの悪そうにお見舞いの言葉を口にして帰ってしまった……
		CALL SCHOOL_KOJO, TARGET, "色気を出す_失敗"
		CALL 好感度上昇, TARGET, "ときめき", 10
	ENDIF
	CSTR:現在地 =
	CSTR:目的 =
	TARGET = -1
	同行者 = 0
	RETURN 1
ENDIF

DRAWLINE
CALL TRAIN_SCHOOL, "自宅"
CALL CHECKDRAWLINE
IF TALENT:恋慕 && !好感度:既成事実
	PRINTFORMW やっと想いを遂げられた%CALLNAME:TARGET%は嬉しそうに%CALLNAME:PLAYER%にキスをして、しばらくベッドの中でじゃれ合ったのちに%CALLNAME:PLAYER%の家を後にした
	ときめき増加量 += 3
ELSE
	PRINTFORMW 行為に満足した%CALLNAME:TARGET%は\@ TALENT:恋慕 ? 名残惜しそうにしながらも # 軽くシャワーを浴びて \@%CALLNAME:PLAYER%の家を後にした
ENDIF
CSTR:現在地 =
CSTR:目的 =
CALL 好感度上昇, TARGET, "友情", 30
CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
CALL 好感度上昇, TARGET, "セフレ", 50
CALL 好感度上昇, TARGET, "既成事実", 1
SIF !TALENT:恋慕
	CFLAG:倫理観++
TARGET = -1
同行者 = 0
RETURN 1
