@ロケーション条件_Deuilの事務所
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"ユーリ",
"アッシュ",
"スマイル"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@バイト条件_Deuilの事務所
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"ユーリ",
"アッシュ",
"スマイル"
}

バイト要項 = 朝～夕方の時間帯で募集中
SIF GROUPMATCH(ターン, "朝", "昼", "夕")
	バイト可能 = 1
;メンバーのいずれかの好感度500以上でバイト可能
FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 500
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

;バイト時に好感度が上がるキャラ
@職場出現キャラ_Deuilの事務所
#DIM DYNAMIC 対象
{
#DIMS DYNAMIC 出現キャラ =
"ユーリ",
"アッシュ", 
"スマイル"
}

対象 = GETCHARANAME(出現キャラ:(RAND:VARSIZE("出現キャラ")))

CALL EXPUP, "演奏経験", 2, PLAYER

SIF 対象 > 0 && CSTR:対象:現在地 == ""
	RETURN 対象

RETURN 0

@出現キャラ_Deuilの事務所
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	;出現率:LCOUNT = {出現率(100分率)} で指定できる
	SELECTCASE CHARANAMEF(LCOUNT)
		CASE "ユーリ", "アッシュ", "スマイル"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
