@ロケーション条件_量子の海
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"モード",
"リリーゼ",
"比多木 小瑠璃"
}
ロケーション説明 = 相対論的量子学かと思ったら本当に海でした。ここに漁師はいない
;夏季限定
SIF !INRANGE(MONTH, 7, 9)
	RETURN 0

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SIF 好感度合計(GETCHARANAME(条件用キャラ:LCOUNT)) >= 100
			RETURN GETCHARANAME(条件用キャラ:LCOUNT)
	ENDIF
NEXT

RETURN 0

@出現キャラ_量子の海
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;ご存知の彼女たちと海上保安官
		CASE "モード", "リリーゼ", "比多木 小瑠璃", "シェリー・F・アレスターズ"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;海なら海賊も居るって分かるはずなんだけどなぁ・・・
		CASE "アイリーン", "ライア", "ニョア"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 50
		;海の家の経営者と人魚と海に接点あるキャラ
		CASE "神崎 慧靂", "サタヌ", "マーメイドガール", "パール", "テトラ", "ティア", "キララ", "フロウフロウ", "ロクミ", "丹子"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 20
		;天気が良い休日はグループで来てる人がいる
		CASEELSE
			IF 休日() && CSTR:LCOUNT:在校 != "" && RANDPER(3) && グループ数 < 10; && 天気() == "晴れ" 
				;相性の良いキャラと3～5人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;3人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				集団行動:グループ数:2 = RESULT:1
				IF RANDPER(50)
					集団行動:グループ数:3 = RESULT:2
					SIF RANDPER(50)
						集団行動:グループ数:4 = RESULT:3
				ENDIF
				グループ数++
			ENDIF
		
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART
