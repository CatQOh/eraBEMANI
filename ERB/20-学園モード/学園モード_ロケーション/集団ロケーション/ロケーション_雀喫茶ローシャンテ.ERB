@ロケーション条件_雀喫茶ローシャンテ
#DIM DYNAMIC LCOUNT
ロケーション説明 = 麻雀ファイト会場内に設立された麻雀が打てる喫茶店、ファイトガール達の活動拠点。

;MFGキャラ
FOR LCOUNT, 1, CHARANUM
	IF GROUPMATCH("麻雀ファイトガール", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
		SIF 好感度合計(LCOUNT) >= 100
			RETURN GETCHARANAME(NAME:LCOUNT)
	ENDIF
NEXT

;MFGキャラと同じ在校生であればローシャンテ無条件解放
SIF GROUPMATCH(CSTR:PLAYER:在校, "ネリマ壱役高等学校", "アキバ弐役女学院", "帝国立ナガタチョウ役満高等学校", "カワサキ参役塾高等学校")
	RETURN -1

RETURN 0

@ロケーション可否_雀喫茶ローシャンテ
;時間帯などで使えない場合は理由を書く
IF ターン == "朝"
	ロケーション可否 = 営業準備中
ENDIF

@出現キャラ_雀喫茶ローシャンテ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;NOではなく登録番号で指定する
IF データベース参照中
	始点 = データベース参照中
	終点 = データベース参照中+1
ELSE
	IF 始点
		終点 = 始点
		始点 = 1
	ELSE
		始点 = RAND(1, CHARANUM)
		終点 = CHARANUM
	ENDIF
ENDIF

FOR LCOUNT, 始点, 終点
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != "" && !データベース参照中
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;センちはスタッフなので必ずいる
		CASE "タテナオリ・セン"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 100
		;特技：麻雀
		CASE "逢坂 茜"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 52
		;他モードでギャンブラーとして特別扱いしているキャラ
		CASE "キャロル", "ことみちゃん", "BroGamerの少年"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 52
		;麻雀好きそうなキャラがいる
		CASE "ヤンヤン", "グリム・ヤンヤン"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 39
		;はい♥ 四暗刻単騎
		CASE "のの"
			SIF 出現率:LCOUNT >= 0
				出現率:LCOUNT = 13
		CASEELSE
			;他のファイトガールがいる
			IF GROUPMATCH("麻雀ファイトガール", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 39
			;遊び人も居る
			ELSEIF TALENT:LCOUNT:遊び人
				SIF 出現率:LCOUNT >= 0
					出現率:LCOUNT = 26
			ENDIF
	ENDSELECT
NEXT

SIF 始点 == 1
	RETURN
SIF !データベース参照中
	RESTART

;@雀喫茶ローシャンテ_専用コマンド
;CALL COLUMNPRINTL, 0, "　[  0] - 八牌麻雀"
