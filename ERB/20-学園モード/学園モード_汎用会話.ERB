@汎用会話可否判定, 対象, コマンド名
#DIM DYNAMIC 対象
#DIMS DYNAMIC コマンド名
RESULTS = 
TRYCCALLFORM SCHOOL_%CSVNAMERPS(NO:対象)%_コマンド不可_%コマンド名%
	SIF RESULT == -1
		RETURN -1
	IF RESULT
		SIF RESULTS != ""
			RESULTS = (%RESULTS%)
		RETURN 1
	ENDIF
CATCH
ENDCATCH
RETURN 0

@学園モード_汎用会話, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 対象
#DIMS DYNAMIC 専用コマンドリスト, 10
#DIM DYNAMIC 総数
TARGET = 対象

IF OPTION:口上表示 && !CFLAG:対象:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話開始")
	CALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話開始
ELSE
	PRINTFORML □%CALLNAME:TARGET%と会話します
ENDIF

;コマンド表示
CALL 汎用会話可否判定, TARGET, "何気ない会話をする"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 何気ない会話をする%RESULTS%", "gray"
ELSEIF RESULT == 0
	PRINTL 　[0] 何気ない会話をする
ENDIF

CALL 汎用会話可否判定, TARGET, "少し踏み込んだ会話をする"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 少し踏み込んだ会話をする%RESULTS%", "gray"
ELSEIF RESULT == 0
	PRINTL 　[1] 少し踏み込んだ会話をする
ENDIF

CALL 汎用会話可否判定, TARGET, "下ネタを振る"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 下ネタを振る%RESULTS%", "gray"
ELSEIF RESULT == 0
	PRINTL 　[2] 下ネタを振る
ENDIF

CALL 汎用会話可否判定, TARGET, "性的に誘う"
IF RESULT == 1
	IF 授業中
		IF TARGET == 授業中
			CALL PRINTCOLORL, @"　[-] 授業を放棄させて性的に誘う%RESULTS%", "gray"
		ELSE
			CALL PRINTCOLORL, @"　[-] 授業をサボって性的に誘う%RESULTS%", "gray"
		ENDIF
	ELSE
		CALL PRINTCOLORL, @"　[-] 性的に誘う%RESULTS%", "gray"
	ENDIF
ELSEIF RESULT == 0
	IF 同行者 && 同行者 != TARGET
		CALL PRINTCOLORL, @"　[-] 性的に誘う(%NAME:同行者%が同行中)", "gray"
	ELSE
		IF 授業中
			IF TARGET == 授業中
				PRINTL 　[3] 授業を放棄させて性的に誘う
			ELSE
				PRINTL 　[3] 授業をサボって性的に誘う
			ENDIF
		ELSE
			PRINTL 　[3] 性的に誘う
		ENDIF
	ENDIF
ENDIF

CALL 汎用会話可否判定, TARGET, "思い切って告白する"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 思い切って告白する%RESULTS%", "gray"
ELSEIF RESULT == 0
	IF TALENT:恋慕
		CALL PRINTCOLORL, @"　[-] 思い切って告白する(%NAME:TARGET%は既に%CALLNAME:PLAYER%の恋人)", "gray"
	ELSE
		PRINTL 　[4] 思い切って告白する
	ENDIF
ENDIF

CALL 汎用会話可否判定, TARGET, "連絡先を聞いてみる"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 連絡先を聞いてみる%RESULTS%", "gray"
ELSEIF RESULT == 0
	IF 連絡先:TARGET
		CALL PRINTCOLORL, @"　[-] 連絡先を聞いてみる(%CALLNAME:TARGET%の連絡先は既に知っている)", "gray"
	ELSE
		PRINTL 　[5] 連絡先を聞いてみる
	ENDIF
ENDIF

CALL 汎用会話可否判定, TARGET, "同行を提案する"
IF RESULT == 1
	CALL PRINTCOLORL, @"　[-] 同行を提案する%RESULTS%", "gray"
ELSEIF RESULT == 0
	IF 同行者 != TARGET && !校内()
		IF 同行者
			CALL PRINTCOLORL, @"　[-] 同行を提案する(既に%NAME:同行者%と行動中)", "gray"
		ELSE
			PRINTL 　[6] 同行を提案する
		ENDIF
	ENDIF
ENDIF

IF CSTR:PLAYER:現在地 == "雀喫茶ローシャンテ"
	CALL 汎用会話可否判定, TARGET, "八牌麻雀"
	IF RESULT == 1
		CALL PRINTCOLORL, @"　[-] 一局やらないか%RESULTS%", "gray"
	ELSEIF RESULT == 0
		PRINTL 　[20] 一局やらないか(八牌麻雀を開始)
	ENDIF
ENDIF

;口上専用コマンド
IF OPTION:口上表示 && !CFLAG:TARGET:口上非表示
	ENUMFUNCBEGINSWITH @"SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド_"
	総数 = RESULT
	IF 総数
		FOR LCOUNT, 0, 総数
			専用コマンドリスト:LCOUNT = %SUBSTRINGU(RESULTS:LCOUNT, STRLENSU(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド_"), -1)%
		NEXT
		FOR LCOUNT, 0, 総数
			RESULTS = 
			TRYCCALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド不可_%専用コマンドリスト:LCOUNT%
				SIF RESULT == -1
					CONTINUE
				IF RESULT
					SIF RESULTS != ""
						RESULTS = (%RESULTS%)
					CALL PRINTCOLORL, @"　[---] %専用コマンドリスト:LCOUNT%%RESULTS%", "gray"
					CONTINUE
				ENDIF
			CATCH
			ENDCATCH
			PRINTFORML 　[{LCOUNT+100}] %専用コマンドリスト:LCOUNT%
		NEXT
	ENDIF
ENDIF

DRAWLINE
PRINTPLAIN 　会話　
PRINTL [10] なんでもない

BINPUT
LRESULT = RESULT

;口上のコマンド上書き関数を優先
SELECTCASE LRESULT
	CASE 0
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_何気ない会話をする")
			CALL SCHOOL_KOJO, TARGET, "コマンド_何気ない会話をする"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_何気ない会話をする
	CASE 1
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_少し踏み込んだ会話をする")
			CALL SCHOOL_KOJO, TARGET, "コマンド_少し踏み込んだ会話をする"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_少し踏み込んだ会話をする
	CASE 2
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_下ネタを振る")
			CALL SCHOOL_KOJO, TARGET, "コマンド_下ネタを振る"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_下ネタを振る
	CASE 3
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_性的に誘う")
			CALL SCHOOL_KOJO, TARGET, "コマンド_性的に誘う"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_性的に誘う
	CASE 4
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_思い切って告白する")
			CALL SCHOOL_KOJO, TARGET, "コマンド_思い切って告白する"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_思い切って告白する
	CASE 5
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_連絡先を聞いてみる")
			CALL SCHOOL_KOJO, TARGET, "コマンド_連絡先を聞いてみる"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_連絡先を聞いてみる
	CASE 6
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_同行を提案する")
			CALL SCHOOL_KOJO, TARGET, "コマンド_同行を提案する"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_同行を提案する
		SIF !RESULT
			RESTART
	CASE 10
		SIF OPTION:口上表示 && !CFLAG:対象:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話キャンセル")
			CALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話キャンセル
		RETURN 0
	CASE 20
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_八牌麻雀")
			CALL SCHOOL_KOJO, TARGET, "コマンド_八牌麻雀"
			RETURN RESULT
		ENDIF
		CALL SCHOOL_八牌麻雀
	CASE 100 TO 110
		;専用コマンド用枠として確保
		CALLFORM SCHOOL_KOJO, TARGET, @"専用コマンド_%専用コマンドリスト:(RESULT-100)%"
ENDSELECT

;会話後に授業に参加する
IF 授業参加フラグ
	PRINTL
	PRINTL その後、教室へと戻り授業を受けた。
	CALL 好感度上昇, 授業中, "舎弟", 2
	WAIT
ENDIF

RETURN 1


;ここからコマンド実行時の処理
@SCHOOL_何気ない会話をする
CALL SCHOOL_KOJO, TARGET, "何気ない会話"
CALL 好感度上昇, TARGET, "友情", 10
WAIT

@SCHOOL_少し踏み込んだ会話をする
IF 好感度合計(TARGET) < 200
	CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_失敗"
ELSE
	CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_成功"
	CALL 好感度上昇, TARGET, "友情", 10
	CALL 好感度上昇, TARGET, "ときめき", 10
	WAIT
ENDIF

@SCHOOL_下ネタを振る
IF 好感度:セフレ >= 200
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "友情", 10
	CALL 好感度上昇, TARGET, "セフレ", 20
ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || CFLAG:倫理観 >= 30
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "セフレ", 20
ELSEIF 好感度合計(TARGET) >= 200
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "セフレ", 10
ELSEIF TALENT:幼稚
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
ELSE
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
	CALL 好感度上昇, TARGET, "ときめき", -10
ENDIF
WAIT


@SCHOOL_性的に誘う
#DIM DYNAMIC 倫理観ボーダー

;現状は校内プレイだけボーダーあり
IF 校内()
	IF CSTR:在任 != ""
		IF TARGET == 授業中
			倫理観ボーダー = 90
		ELSE
			倫理観ボーダー = 70
		ENDIF
	ELSE
		倫理観ボーダー = 60
	ENDIF
	倫理観ボーダー -= MIN(好感度:既成事実, 10)
	倫理観ボーダー -= MIN(好感度:セフレ/100, 10)
	SIF TALENT:恋慕
		倫理観ボーダー -= 10
ELSE
	倫理観ボーダー = 0
ENDIF

IF 好感度:既成事実 >= 10 || 好感度:セフレ >= 1000 || TALENT:恋慕
	IF CFLAG:倫理観 < 倫理観ボーダー
		CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
		RETURN
	ENDIF
	CALL SCHOOL_KOJO, TARGET, "誘惑_成功"
ELSE
	CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
	RETURN
ENDIF
WAIT
;失敗した場合はRETURNで終了しているので、ここからは成功した場合の分岐
IF CSTR:PLAYER:現在地 == "自宅"
	CALL EXECTRAIN_SCHOOL, "自宅イベント"
	CSTR:現在地 =
	CSTR:目的 =
	TARGET = -1
	同行者 = 0
ELSE
	IF 授業中
		;授業をサボる
		IF TARGET == 授業中
			;授業を放棄させた
			CALL 好感度上昇, 授業中, "セフレ", 50
			CFLAG:倫理観 += 5
			CFLAG:MASTER:倫理観 += 5
		ELSE
			CALL 好感度上昇, 授業中, "舎弟", -2
			IF CSTR:在任 != ""
				CFLAG:倫理観 += 2
				CFLAG:MASTER:倫理観 += 2
			ELSE
				CFLAG:倫理観++
				CFLAG:MASTER:倫理観++
			ENDIF
		ENDIF
		PRINTL
		授業参加フラグ = 0
	ENDIF
	IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_連れ込む")
		CALL SCHOOL_KOJO, TARGET, "コマンド_連れ込む"
		RETURN RESULT
	ENDIF
	CALL SCHOOL_連れ込み先
ENDIF


@SCHOOL_思い切って告白する
;割りと大事な場面なので素質で分岐させたりしたいかも
IF 好感度:セフレ && 好感度:セフレ >= 好感度:ときめき
	IF 好感度:既成事実
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "セフレ", 10
		WAIT
	ELSE
		IF 好感度:セフレ >= 1000
			CALL SCHOOL_KOJO, TARGET, "告白_失敗"
			CALL 好感度上昇, TARGET, "セフレ", 5
			WAIT
		ELSE
			CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		ENDIF
	ENDIF
ELSEIF 好感度:ときめき >= 1000
	CALL SCHOOL_KOJO, TARGET, "告白_成功"
	CALL 恋仲, TARGET
	WAIT
[SKIPSTART]
ELSEIF 好感度:ときめき >= 500 && 連絡先:TARGET
	CALL SCHOOL_KOJO, TARGET, "告白_保留"
	告白保留:TARGET = RAND(4, 8)
	CALL 好感度上昇, TARGET, "ときめき", 10
	WAIT
[SKIPEND]
ELSE
	IF 好感度:友情 >= 300
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "友情", 10
		CALL 好感度上昇, TARGET, "ときめき", 5
	ELSE
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "友情", 5
		CALL 好感度上昇, TARGET, "ときめき", -5
	ENDIF
	WAIT
ENDIF

@SCHOOL_連絡先を聞いてみる
IF 好感度合計(TARGET) >= 500
	CALL SCHOOL_KOJO, TARGET, "連絡先入手_成功"
	CALL 連絡先入手, TARGET
	CALL 好感度上昇, TARGET, "友情", 10
	WAIT
ELSE
	CALL SCHOOL_KOJO, TARGET, "連絡先入手_失敗"
ENDIF

@SCHOOL_同行を提案する
#DIM DYNAMIC 必要値
IF ターン == "夜"
	;異性の場合は友情値は5倍必要
	必要値 = 600
	SIF SEX(PLAYER) != SEX(TARGET)
		必要値 *= 5
	;ときめき1000で必要値0に
	必要値 *= MAX(1000-好感度:ときめき, 0)
	必要値 /= 1000
	;セフレ1000で必要値0に
	必要値 *= MAX(1000-好感度:セフレ, 0)
	必要値 /= 1000
	;TALENT:恋慕で必要値0に
	SIF TALENT:恋慕
		必要値 = 0
	IF MAX(好感度:友情, 0) >= 必要値
		CALL SCHOOL_KOJO, TARGET, "同行_成功"
		同行者 = TARGET
		CSTR:同行者:目的 = 遊び
		CSTR:同行者:現在地 = %CALLNAME:PLAYER%の家
		JUMP 宿泊イベント
	ELSE
		CALL SCHOOL_KOJO, TARGET, "同行_失敗"
		RETURN 0
	ENDIF
ELSEIF TALENT:恋慕 || 好感度合計(TARGET) >= 200
	同行者 = TARGET
	CALL SCHOOL_KOJO, TARGET, "同行_成功"
	CALL 好感度上昇, TARGET, "友情", 5
	CALL 好感度上昇, TARGET, "ときめき", 5
ELSE
	CALL SCHOOL_KOJO, TARGET, "同行_失敗"
ENDIF

@SCHOOL_八牌麻雀
CALL SCHOOL_KOJO, TARGET, "八牌麻雀_開始"
CALL MAHJONG8
IF PAI8SCORE > PAI8SCORE:1
	CALL SCHOOL_KOJO, TARGET, "八牌麻雀_勝利"
	CALL 好感度上昇, TARGET, "友情", 20
ELSE
	CALL SCHOOL_KOJO, TARGET, "八牌麻雀_敗北"
	CALL 好感度上昇, TARGET, "友情", 5
ENDIF


;連れ込み先の選択は更に分離
@SCHOOL_連れ込み先
PRINTFORML □%CALLNAME:TARGET%をどこに連れ込みますか？
IF !校内()
	IF CSTR:在任 != ""
		PRINTFORML 　[0] - ホテル(%CALLNAME:TARGET%が宿泊料負担)
	ELSEIF TALENT:恋慕
		IF MONEY < 2500
			SETCOLORBYNAME gray
			CALL PASELIL, "　[-] - ホテル(割り勘で休憩料金2500＄必要)"
			RESETCOLOR
		ELSE
			CALL PASELIL, "　[0] - ホテル(割り勘で休憩料金2500＄必要)"
		ENDIF
	ELSE
		IF MONEY < 5000
			SETCOLORBYNAME gray
			CALL PASELIL, "　[-] - ホテル(休憩料金5000＄必要)"
			RESETCOLOR
		ELSE
			CALL PASELIL, "　[0] - ホテル(休憩料金5000＄必要)"
		ENDIF
	ENDIF
ENDIF
PRINTFORML 　[1] - \@ 校内() ? 人気の無いトイレ # 共用トイレ \@
SIF ターン != "夜" && !校内()
	PRINTL 　[2] - 自宅
IF 校内()
	PRINTL 　[3] - 保健室
	PRINTL 　[4] - 体育倉庫
	SIF CSTR:担当科目 == "校長"
		PRINTL 　[5] - 校長室
	SIF CSTR:担当科目 == "理事長"
		PRINTL 　[6] - 理事長室
ENDIF
BINPUT

SELECTCASE RESULT
	CASE 0
		IF CSTR:在任 != ""
			ITEM:コンドーム += 6
		ELSEIF TALENT:恋慕
			MONEY -= 2500
		ELSE
			MONEY -= 5000
		ENDIF
		CALL EXECTRAIN_SCHOOL, "ホテル"
	CASE 1
		CALL EXECTRAIN_SCHOOL, "トイレ"
	CASE 2
		;ここだけ直でTRAINには飛ばない
		CSTR:現在地 = %CALLNAME:PLAYER%の家
		CSTR:PLAYER:現在地 = 自宅
		CSTR:目的 = セックス
		同行者 = TARGET
		CALL SCHOOL_KOJO, TARGET, "一緒に帰宅"
	CASE 3
		CALL EXECTRAIN_SCHOOL, "保健室"
	CASE 4
		CALL EXECTRAIN_SCHOOL, "体育倉庫"
	CASE 5
		CALL EXECTRAIN_SCHOOL, "校長室"
	CASE 6
		CALL EXECTRAIN_SCHOOL, "理事長室"
ENDSELECT

TARGET = -1


;地の文関数
@SCHOOL_MESSAGE_何気ない会話
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と会話した

@SCHOOL_MESSAGE_踏み込んだ会話_失敗
PRINTFORMW %CALLNAME:PLAYER%の踏み込んだ会話に、%CALLNAME:TARGET%は少し迷惑そうにしている……

@SCHOOL_MESSAGE_踏み込んだ会話_成功
PRINTFORML %CALLNAME:PLAYER%の踏み込んだ会話で、%CALLNAME:TARGET%のことをより詳しく知ることができた

@SCHOOL_MESSAGE_品の無い会話_失敗
IF TALENT:幼稚
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の話をよく理解できていないようだ……
ELSE
	PRINTFORML %CALLNAME:PLAYER%の下品な話に、%CALLNAME:TARGET%は若干引いている様子だ……
ENDIF

@SCHOOL_MESSAGE_品の無い会話_成功
IF 好感度:セフレ >= 200
	PRINTFORML %CALLNAME:PLAYER%の品の無い会話にも、%CALLNAME:TARGET%は喜んで付き合った
ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || CFLAG:倫理観 >= 30
	PRINTFORML %CALLNAME:PLAYER%の下品な会話に%CALLNAME:TARGET%もノリの良い返しをしてきた
ELSE
	PRINTFORML %CALLNAME:PLAYER%の下品な話を、%CALLNAME:TARGET%は宥めながらも聞いてくれた
ENDIF

@SCHOOL_MESSAGE_誘惑_失敗
IF 好感度:既成事実 >= 10
	SIF 校内()
		PRINTFORML 何度も体を重ねている%CALLNAME:TARGET%だが、さすがに学校ではできないと断られてしまった……
ELSEIF 好感度:セフレ >= 1000
	SIF 校内()
		PRINTFORML %CALLNAME:PLAYER%の提案に%CALLNAME:TARGET%も興味があるようだが、さすがに学校ではできないと断られてしまった……
ELSEIF TALENT:恋慕
	SIF 校内()
		PRINTFORML %CALLNAME:TARGET%は恋仲の%CALLNAME:PLAYER%でもさすがに学校ではできないと断られてしまった……
ELSE
	PRINTFORML %CALLNAME:TARGET%は困惑した様子で%CALLNAME:PLAYER%との会話を打ち切った……
ENDIF

@SCHOOL_MESSAGE_誘惑_成功
IF 好感度:既成事実 >= 10
	PRINTFORML 既に何度も体を重ねている%CALLNAME:TARGET%は、\@ 校内() ? 学校に居ることもお構いなしに #\@%CALLNAME:PLAYER%の誘いにも二つ返事で乗ってきた
ELSEIF 好感度:セフレ >= 1000
	IF 校内()
		PRINTFORML %CALLNAME:PLAYER%の提案に、学校に居ることもお構いなしに%CALLNAME:TARGET%は乗ってきた
	ELSE
		PRINTFORML %CALLNAME:PLAYER%の提案に、興味津々な%CALLNAME:TARGET%は快く応じた
	ENDIF
ELSEIF TALENT:恋慕
	IF 校内()
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、学校内ですることに少し困惑した様子だが、結局は了承してくれた
	ELSE
		PRINTFORML %CALLNAME:TARGET%は恋人の%CALLNAME:PLAYER%の提案を聞くと、落ち着いた声で了承した
	ENDIF
ENDIF

@SCHOOL_MESSAGE_一緒に帰宅
PRINTFORMW %CALLNAME:TARGET%を家に招き入れることにした

@SCHOOL_MESSAGE_自宅_行為後
IF TALENT:恋慕 && !好感度:既成事実
	PRINTFORMW やっと想いを遂げられた%CALLNAME:TARGET%は嬉しそうに%CALLNAME:PLAYER%にキスをして、しばらくベッドの中でじゃれ合ったのちに%CALLNAME:PLAYER%の家を後にした
ELSE
	PRINTFORMW 行為に満足した%CALLNAME:TARGET%は\@ TALENT:恋慕 ? 名残惜しそうにしながらも # 軽くシャワーを浴びて \@%CALLNAME:PLAYER%の家を後にした
ENDIF

@SCHOOL_MESSAGE_ホテル_行為前
PRINTFORMW %CALLNAME:TARGET%とホテルへ向かうことにした

@SCHOOL_MESSAGE_ホテル_行為後
PRINTFORMW 行為を終えた%CALLNAME:TARGET%たちは、時間差でホテルを後にして別れた

@SCHOOL_MESSAGE_トイレ_行為前
PRINTFORMW %CALLNAME:TARGET%を\@ 校内() ? 校舎でも人の少ない # 手頃な広さの \@トイレにこっそりと連れ込んだ

@SCHOOL_MESSAGE_トイレ_行為後
PRINTFORMW 行為を終えた%CALLNAME:TARGET%は、乱れた服を直して周りを確認しながら別れた

@SCHOOL_MESSAGE_保健室_行為前
PRINTFORMW %CALLNAME:TARGET%と保健室前まで来ると、中に誰も居ないことを確認してベッドに入った

@SCHOOL_MESSAGE_保健室_行為後
IF CSTR:在任 != ""
	PRINTFORMW %CALLNAME:TARGET%は衣服を直し、淫らな残り香が漂う保健室に芳香剤を振り撒いて%CALLNAME:PLAYER%を見送った
ELSE
	PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、汗と体液で汚れた体をアルコールティッシュで拭き、保健室を後にした
ENDIF

@SCHOOL_MESSAGE_体育館倉庫_行為前
PRINTFORMW %CALLNAME:TARGET%を体育倉庫に連れ込み、重い扉をしっかりと閉めた

@SCHOOL_MESSAGE_体育館倉庫_行為後
PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、淫臭が充満する倉庫の窓を開け、体育倉庫を後にした

@SCHOOL_MESSAGE_校長室_行為前
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を受け入れ、校長室の扉に鍵をかけた

@SCHOOL_MESSAGE_校長室_行為後
PRINTFORMW %CALLNAME:TARGET%は衣服の乱れを整え、%CALLNAME:PLAYER%を校長室から見送った

@SCHOOL_MESSAGE_理事長室_行為前
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を受け入れ、理事長室の扉に鍵をかけた

@SCHOOL_MESSAGE_理事長室_行為後
PRINTFORMW %CALLNAME:TARGET%は衣服の乱れを整え、%CALLNAME:PLAYER%を理事長室から見送った

@SCHOOL_MESSAGE_告白_保留
PRINTFORML 突然の告白に%CALLNAME:TARGET%は困惑し、今は決められないから少し時間が欲しいと言ってきた
PRINTFORMW 数日以内に%CALLNAME:TARGET%から連絡が来るだろう

@SCHOOL_MESSAGE_告白_失敗
PRINTFORMW %CALLNAME:PLAYER%は思い切って%CALLNAME:TARGET%に想いを伝えてみた
;割りと大事な場面なので素質で分岐させたりしたいかも
IF 好感度:セフレ && 好感度:セフレ >= 好感度:ときめき
	IF 好感度:既成事実
		PRINTFORMW しかし%CALLNAME:TARGET%は、%CALLNAME:PLAYER%とは体だけの関係で居たいと言って断られてしまった……
	ELSE
		IF 好感度:セフレ >= 1000
			PRINTFORMW %CALLNAME:TARGET%はしばし考え込むとその告白をやんわりと断ったが、性的なことには興味があるとオブラートに包んで言ってきた……
		ELSE
			PRINTFORMW しかし%CALLNAME:TARGET%はいまいち腑に落ちない様子で、かつ遺恨を残さないようにはっきりと断った……
		ENDIF
	ENDIF
ELSE
	IF 好感度:友情 >= 300
		PRINTFORMW しかし%CALLNAME:TARGET%は告白をはっきりと断り、友達のままで居たいと言った……
	ELSE
		PRINTFORMW しかしお互いの関係が浅すぎるせいか、%CALLNAME:TARGET%は明らかに気まずそうな様子で断った……
	ENDIF
ENDIF

@SCHOOL_MESSAGE_告白_成功
PRINTFORMW %CALLNAME:PLAYER%は思い切って%CALLNAME:TARGET%に想いを伝えてみた
;割りと大事な場面なので素質で分岐させたりしたいかも
PRINTFORMW %CALLNAME:TARGET%は少し驚いた様子を見せたが、自分も%CALLNAME:PLAYER%のことが気になっていたと肯定的な返事をした

@SCHOOL_MESSAGE_連絡先入手_失敗
PRINTFORMW %CALLNAME:TARGET%はまだそこまで親密な関係だと思っていないのか、教えてもらえずにはぐらかされてしまった……

@SCHOOL_MESSAGE_連絡先入手_成功
PRINTFORMW %CALLNAME:TARGET%に連絡先を尋ねると、快く教えてくれた

@SCHOOL_MESSAGE_同行_失敗
IF ターン == "夜"
	PRINTFORMW %CALLNAME:TARGET%と行動を共にしようと提案したが、今日はもう遅いからと断られてしまった
ELSE
	PRINTFORMW %CALLNAME:TARGET%は遠慮がちに断った。まだそこまで親密だと思っていないようだ……
ENDIF

@SCHOOL_MESSAGE_同行_成功
IF ターン == "夜"
	PRINTFORMW 夜は遅いが、%CALLNAME:TARGET%は%CALLNAME:PLAYER%の家に泊まることを提案し、一緒に%CALLNAME:PLAYER%の家に向かった
ELSE
	PRINTFORMW %CALLNAME:TARGET%は快諾して%CALLNAME:PLAYER%と行動を共にすることにした
ENDIF
@SCHOOL_MESSAGE_八牌麻雀_開始
PRINTFORMW %CALLNAME:TARGET%は意気込んで%CALLNAME:PLAYER%との勝負を受けた

@SCHOOL_MESSAGE_八牌麻雀_勝利
;気力切れたのであとで素質分岐する
PRINTFORMW なんとか%CALLNAME:TARGET%に勝つことができた。%CALLNAME:TARGET%も%CALLNAME:PLAYER%との対局を楽しんだようだ

@SCHOOL_MESSAGE_八牌麻雀_敗北
PRINTFORMW %CALLNAME:TARGET%との対局に負けてしまった。%CALLNAME:TARGET%は得意げに勝ち誇っている
