@汎用会話可否判定, 対象, コマンド名
#DIM DYNAMIC 対象
#DIMS DYNAMIC コマンド名
RESULTS = 
TRYCCALLFORM SCHOOL_%CSVNAMERPS(NO:対象)%_コマンド不可_%コマンド名%
	SIF RESULT == -1
		RETURN -1
	IF RESULT
		SIF RESULTS != ""
			RESULTS = (%RESULTS%)
		RETURN 1
	ENDIF
CATCH
ENDCATCH
RETURN 0

@学園モード_汎用会話, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 対象
#DIMS DYNAMIC 専用コマンドリスト, 10
#DIM DYNAMIC 総数
#DIMS DYNAMIC コマンドリスト = "何気ない会話をする", "少し踏み込んだ会話をする", "下ネタを振る", "性的に誘う", "思い切って告白する", "連絡先を聞いてみる", "同行を提案する", "八牌麻雀"
#DIMS DYNAMIC コマンド名表示
#DIMS DYNAMIC 実行不可理由
#DIMS DYNAMIC 本体側実行不可理由
#DIM DYNAMIC 実行不可フラグ
TARGET = 対象

IF OPTION:口上表示 && !CFLAG:対象:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話開始")
	CALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話開始
ELSE
	PRINTFORML □%CALLNAME:TARGET%と会話します
ENDIF

;コマンド表示
FOR LCOUNT, 0, VARSIZE("コマンドリスト")
	;口上でのコマンド編集
	CALL 汎用会話可否判定, TARGET, コマンドリスト:LCOUNT
	実行不可理由 = %RESULTS%
	実行不可フラグ = RESULT
	TRYCCALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド名変更_%コマンドリスト:LCOUNT%
		コマンド名表示 = %RESULTS%
	CATCH
		コマンド名表示 = %コマンドリスト:LCOUNT%
	ENDCATCH
	
	;ここから本体側のコマンド編集（実行不可判定は本体優先、理由表示はあれば口上側優先）
	;コマンドを表示しない場合は実行不可フラグを-1にすること
	SELECTCASE コマンドリスト:LCOUNT
		CASE "性的に誘う"
			IF バイト帰り && ターン == "夜"
				本体側実行不可理由 = 今日はもう夜遅いです
				実行不可フラグ = 1
			ELSEIF 同行者 && 同行者 != TARGET
				本体側実行不可理由 = %NAME:同行者%が同行中
				実行不可フラグ = 1
			ELSEIF 授業中
				;授業がある場合接頭語（ここも編集できるようにするかは検討）
				IF TARGET == 授業中
					コマンド名表示 = 授業を放棄させて%コマンド名表示%
				ELSE
					コマンド名表示 = 授業をサボって%コマンド名表示%
				ENDIF
			ENDIF
		CASE "思い切って告白する"
			IF TALENT:恋慕
				本体側実行不可理由 = %NAME:TARGET%は既に%CALLNAME:PLAYER%の恋人
				実行不可フラグ = 1
			ENDIF
		CASE "連絡先を聞いてみる"
			IF 連絡先入手済み(TARGET)
				本体側実行不可理由 = %CALLNAME:TARGET%の連絡先は既に知っている
				実行不可フラグ = 1
			ENDIF
		CASE "同行を提案する"
			IF 校内() || 同行者 == TARGET
				実行不可フラグ = -1
			ELSEIF 同行者
				本体側実行不可理由 = 既に%NAME:同行者%と行動中
				実行不可フラグ = 1
			ENDIF
		CASE "八牌麻雀"
			IF CSTR:PLAYER:現在地 != "雀喫茶ローシャンテ"
				実行不可フラグ = -1
			ELSE
				;八牌麻雀だけコマンド名が特殊（口上を優先）
				SIF コマンド名表示 == "八牌麻雀"
					コマンド名表示 = 一局やらないか
				SIF !実行不可フラグ
					コマンド名表示 += "(八牌麻雀を開始)"
			ENDIF
	ENDSELECT
	SIF 実行不可理由 == ""
		実行不可理由 = (%本体側実行不可理由%)
	
	IF 実行不可フラグ >= 1
		CALL PRINTCOLORL, @"　[-] %コマンド名表示%%実行不可理由%", "gray"
	ELSEIF !実行不可フラグ
		PRINTFORML 　[{LCOUNT}] %コマンド名表示%
	ENDIF
NEXT

;口上専用コマンドの表示
IF OPTION:口上表示 && !CFLAG:TARGET:口上非表示
	ENUMFUNCBEGINSWITH @"SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド_"
	総数 = RESULT
	IF 総数
		FOR LCOUNT, 0, 総数
			専用コマンドリスト:LCOUNT = %SUBSTRINGU(RESULTS:LCOUNT, STRLENSU(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド_"), -1)%
		NEXT
		FOR LCOUNT, 0, 総数
			RESULTS = 
			TRYCCALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_専用コマンド不可_%専用コマンドリスト:LCOUNT%
				SIF RESULT == -1
					CONTINUE
				IF RESULT
					SIF RESULTS != ""
						RESULTS = (%RESULTS%)
					CALL PRINTCOLORL, @"　[---] %専用コマンドリスト:LCOUNT%%RESULTS%", "gray"
					CONTINUE
				ENDIF
			CATCH
			ENDCATCH
			PRINTFORML 　[{LCOUNT+100}] %専用コマンドリスト:LCOUNT%
		NEXT
	ENDIF
ENDIF

DRAWLINE
PRINTPLAIN 　会話　
PRINTL [99] なんでもない

時間経過なし = 0
BINPUT
LRESULT = RESULT

;口上のコマンド上書き関数を優先
SELECTCASE LRESULT
	CASE 0 TO VARSIZE("コマンドリスト")-1
		IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_%コマンドリスト:LRESULT%")
			CALL SCHOOL_KOJO, TARGET, @"コマンド_%コマンドリスト:LRESULT%"
		ELSE
			CALLFORM SCHOOL_%コマンドリスト:LRESULT%
			SIF コマンドリスト:LRESULT == "同行を提案する" && !RESULT
				RESTART
		ENDIF
	CASE 99
		SIF OPTION:口上表示 && !CFLAG:対象:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話キャンセル")
			CALLFORM SCHOOL_%CSVNAMERPS(NO:TARGET)%_汎用会話キャンセル
		RETURN 0
	CASE 100 TO 110
		;専用コマンド用枠として確保
		CALLFORM SCHOOL_KOJO, TARGET, @"専用コマンド_%専用コマンドリスト:(RESULT-100)%"
ENDSELECT

;会話後に授業に参加する
IF 授業参加フラグ
	PRINTL
	PRINTL その後、教室へと戻り授業を受けた。
	CALL 好感度上昇, 授業中, "舎弟", 2
	WAIT
ENDIF

RETURN !時間経過なし


;ここからコマンド実行時の処理
@SCHOOL_何気ない会話をする
CALL SCHOOL_KOJO, TARGET, "何気ない会話"
CALL 好感度上昇, TARGET, "友情", 10
WAIT

@SCHOOL_少し踏み込んだ会話をする
IF 好感度合計(TARGET) < 200
	CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_失敗"
ELSE
	CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_成功"
	CALL 好感度上昇, TARGET, "友情", 10
	CALL 好感度上昇, TARGET, "ときめき", 10
	WAIT
ENDIF

@SCHOOL_下ネタを振る
IF 好感度:セフレ >= 200
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "友情", 10
	CALL 好感度上昇, TARGET, "セフレ", 20
ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || CFLAG:インモラル >= 30
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "セフレ", 20
ELSEIF 好感度合計(TARGET) >= 200
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
	CALL 好感度上昇, TARGET, "セフレ", 10
ELSEIF TALENT:幼稚
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
ELSE
	CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
	CALL 好感度上昇, TARGET, "ときめき", -10
ENDIF
WAIT


@SCHOOL_性的に誘う
#DIM DYNAMIC インモラル閾値

;現状は校内プレイだけ閾値あり
IF 校内()
	IF CSTR:在任 != ""
		IF TARGET == 授業中
			インモラル閾値 = 90
		ELSE
			インモラル閾値 = 70
		ENDIF
	ELSE
		インモラル閾値 = 60
	ENDIF
	インモラル閾値 -= MIN(好感度:既成事実, 10)
	インモラル閾値 -= MIN(好感度:セフレ/100, 10)
	SIF TALENT:恋慕
		インモラル閾値 -= 10
ELSE
	インモラル閾値 = 0
ENDIF

IF 好感度:既成事実 >= 10 || セフレ判定(TARGET) || TALENT:恋慕
	IF CFLAG:インモラル < インモラル閾値
		CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
		RETURN
	ENDIF
	CALL SCHOOL_KOJO, TARGET, "誘惑_成功"
ELSE
	CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
	RETURN
ENDIF
WAIT
;失敗した場合はRETURNで終了しているので、ここからは成功した場合の分岐
IF CSTR:PLAYER:現在地 == "自宅"
	CALL EXECTRAIN_SCHOOL, "自宅イベント"
	CSTR:現在地 =
	CSTR:目的 =
	TARGET = -1
	同行者 = 0
ELSE
	IF バイト帰り
		;バイト帰りで「性的に誘う」コマンドが使えるのは朝から夕まで(日付を跨がない)
		CALL TURNEND_学園
	ENDIF
	IF 授業中
		;授業をサボる
		IF TARGET == 授業中
			;授業を放棄させた
			CALL 好感度上昇, 授業中, "セフレ", 50
			CFLAG:インモラル += 5
			CFLAG:MASTER:インモラル += 5
		ELSE
			CALL 好感度上昇, 授業中, "舎弟", -2
			IF CSTR:在任 != ""
				CFLAG:インモラル += 2
				CFLAG:MASTER:インモラル += 2
			ELSE
				CFLAG:インモラル++
				CFLAG:MASTER:インモラル++
			ENDIF
		ENDIF
		PRINTL
		授業参加フラグ = 0
	ENDIF
	IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_連れ込む")
		CALL SCHOOL_KOJO, TARGET, "コマンド_連れ込む"
		RETURN RESULT
	ENDIF
	CALL SCHOOL_連れ込み先
ENDIF


@SCHOOL_思い切って告白する
;割りと大事な場面なので素質で分岐させたりしたいかも
IF 告白判定(TARGET)
	CALL SCHOOL_KOJO, TARGET, "告白_成功"
	CALL 恋仲, TARGET
	WAIT
ELSEIF 好感度:セフレ && 好感度:セフレ >= 好感度:ときめき
	IF 好感度:既成事実
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "セフレ", 10
		WAIT
	ELSE
		IF セフレ判定(TARGET)
			CALL SCHOOL_KOJO, TARGET, "告白_失敗"
			CALL 好感度上昇, TARGET, "セフレ", 5
			WAIT
		ELSE
			CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		ENDIF
	ENDIF
[SKIPSTART]
ELSEIF 好感度:ときめき >= 500 && 連絡先:TARGET
	CALL SCHOOL_KOJO, TARGET, "告白_保留"
	告白保留:TARGET = RAND(4, 8)
	CALL 好感度上昇, TARGET, "ときめき", 10
	WAIT
[SKIPEND]
ELSE
	IF 好感度:友情 >= 300
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "友情", 10
		CALL 好感度上昇, TARGET, "ときめき", 5
	ELSE
		CALL SCHOOL_KOJO, TARGET, "告白_失敗"
		CALL 好感度上昇, TARGET, "友情", 5
		CALL 好感度上昇, TARGET, "ときめき", -5
	ENDIF
	WAIT
ENDIF

;;;告白してOKがもらえるなら1を返す
@告白判定(対象)
#FUNCTION
#DIM DYNAMIC 対象

IF 好感度:対象:ときめき >= 1000 && 好感度:対象:ときめき > 好感度:対象:セフレ
	RETURNF 1
ENDIF
RETURNF 0

;;;セフレとして扱われうるなら1を返す
;;;自宅での宿泊イベントなどに使用
@セフレ判定(対象)
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC セフレ閾値

セフレ閾値 = 1000
SIF TALENT:対象:貞操観念
	セフレ閾値 += 300
SIF TALENT:対象:貞操無頓着
	セフレ閾値 -= 300
SIF TALENT:対象:好奇心
	セフレ閾値 -= 100
SIF TALENT:対象:保守的
	セフレ閾値 += 200

セフレ閾値 -= 5*CFLAG:対象:インモラル

SIF TALENT:対象:幼稚
	TIMES セフレ閾値, 0.90
SIF TALENT:対象:狂気
	TIMES セフレ閾値, 0.77
SIF TALENT:対象:即落ち
	TIMES セフレ閾値, 0.50

IF 好感度:対象:セフレ >= セフレ閾値
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SCHOOL_連絡先を聞いてみる
IF 好感度合計(TARGET) >= 500
	CALL SCHOOL_KOJO, TARGET, "連絡先入手_成功"
	CALL 連絡先入手, TARGET
	CALL 好感度上昇, TARGET, "友情", 10
	WAIT
ELSE
	CALL SCHOOL_KOJO, TARGET, "連絡先入手_失敗"
ENDIF

@SCHOOL_同行を提案する
#DIM DYNAMIC 必要値
IF ターン == "夜"
	;異性の場合は友情値は5倍必要
	必要値 = 600
	SIF SEX(PLAYER) != SEX(TARGET)
		必要値 *= 5
	;ときめき1000で必要値0に
	必要値 *= MAX(1000-好感度:ときめき, 0)
	必要値 /= 1000
	;セフレ1000で必要値0に
	必要値 *= MAX(1000-好感度:セフレ, 0)
	必要値 /= 1000
	;TALENT:恋慕で必要値0に
	SIF TALENT:恋慕
		必要値 = 0
	IF MAX(好感度:友情, 0) >= 必要値
		CALL SCHOOL_KOJO, TARGET, "同行_成功"
		同行者 = TARGET
		CSTR:同行者:目的 = 遊び
		CSTR:同行者:現在地 = %CALLNAME:PLAYER%の家
		JUMP 宿泊イベント
	ELSE
		CALL SCHOOL_KOJO, TARGET, "同行_失敗"
		RETURN 0
	ENDIF
ELSEIF TALENT:恋慕 || 好感度合計(TARGET) >= 200
	同行者 = TARGET
	CALL SCHOOL_KOJO, TARGET, "同行_成功"
	CALL 好感度上昇, TARGET, "友情", 5
	CALL 好感度上昇, TARGET, "ときめき", 5
ELSE
	CALL SCHOOL_KOJO, TARGET, "同行_失敗"
ENDIF

@SCHOOL_八牌麻雀
CALL SCHOOL_KOJO, TARGET, "八牌麻雀_開始"
CALL MAHJONG8
IF PAI8SCORE > PAI8SCORE:1
	CALL SCHOOL_KOJO, TARGET, "八牌麻雀_勝利"
	CALL 好感度上昇, TARGET, "友情", 20
ELSE
	CALL SCHOOL_KOJO, TARGET, "八牌麻雀_敗北"
	CALL 好感度上昇, TARGET, "友情", 5
ENDIF


;連れ込み先の選択は更に分離
@SCHOOL_連れ込み先
#DIM DYNAMIC ホテル宿泊

PRINTFORML □%CALLNAME:TARGET%をどこに連れ込みますか？
IF !校内()
	IF ターン == "夜"
		IF CSTR:在任 != ""
			PRINTFORML 　[0] - ホテル(%CALLNAME:TARGET%が宿泊料金負担)
		ELSEIF TALENT:恋慕
			IF MONEY < 3500
				SETCOLORBYNAME gray
				CALL PASELIL, "　[-] - ホテル(割り勘で宿泊料金3500＄必要)"
				RESETCOLOR
			ELSE
				CALL PASELIL, "　[0] - ホテル(割り勘で宿泊料金3500＄必要)"
			ENDIF
		ELSE
			IF MONEY < 7000
				SETCOLORBYNAME gray
				CALL PASELIL, "　[-] - ホテル(宿泊料金7000＄必要)"
				RESETCOLOR
			ELSE
				CALL PASELIL, "　[0] - ホテル(宿泊料金7000＄必要)"
			ENDIF
		ENDIF
	ELSE
		IF CSTR:在任 != ""
			PRINTFORML 　[0] - ホテル(%CALLNAME:TARGET%が休憩料金負担)
		ELSEIF TALENT:恋慕
			IF MONEY < 2500
				SETCOLORBYNAME gray
				CALL PASELIL, "　[-] - ホテル(割り勘で休憩料金2500＄必要)"
				RESETCOLOR
			ELSE
				CALL PASELIL, "　[0] - ホテル(割り勘で休憩料金2500＄必要)"
			ENDIF
		ELSE
			IF MONEY < 5000
				SETCOLORBYNAME gray
				CALL PASELIL, "　[-] - ホテル(休憩料金5000＄必要)"
				RESETCOLOR
			ELSE
				CALL PASELIL, "　[0] - ホテル(休憩料金5000＄必要)"
			ENDIF
		ENDIF
	ENDIF
ENDIF
PRINTFORML 　[1] - \@ 校内() ? 人気の無いトイレ # 共用トイレ \@
SIF ターン != "夜" && !校内()
	PRINTL 　[2] - 自宅
IF 校内()
	PRINTL 　[3] - 保健室
	PRINTL 　[4] - 体育倉庫
	SIF CSTR:担当科目 == "校長"
		PRINTL 　[5] - 校長室
	SIF CSTR:担当科目 == "理事長"
		PRINTL 　[6] - 理事長室
ENDIF
BINPUT

SELECTCASE RESULT
	CASE 0
		IF ターン == "夜"
			ホテル宿泊 = 1
			CSTR:現在地 = ラブホテル
			CSTR:PLAYER:現在地 = ラブホテル
			同行者 = TARGET
			IF CSTR:在任 != ""
			ELSEIF TALENT:恋慕
				MONEY -= 3500
			ELSE
				MONEY -= 7000
			ENDIF
		ELSE
			IF CSTR:在任 != ""
			ELSEIF TALENT:恋慕
				MONEY -= 2500
			ELSE
				MONEY -= 5000
			ENDIF
		ENDIF
		CALL EXECTRAIN_SCHOOL, "ホテル"
	CASE 1
		CALL EXECTRAIN_SCHOOL, "トイレ"
	CASE 2
		;ここだけ直でTRAINには飛ばない
		CSTR:現在地 = %CALLNAME:PLAYER%の家
		CSTR:PLAYER:現在地 = 自宅
		CSTR:目的 = セックス
		同行者 = TARGET
		CALL SCHOOL_KOJO, TARGET, "一緒に帰宅"
	CASE 3
		CALL EXECTRAIN_SCHOOL, "保健室"
	CASE 4
		CALL EXECTRAIN_SCHOOL, "体育倉庫"
	CASE 5
		CALL EXECTRAIN_SCHOOL, "校長室"
	CASE 6
		CALL EXECTRAIN_SCHOOL, "理事長室"
ENDSELECT

SIF !ホテル宿泊
	TARGET = -1


;地の文関数
@SCHOOL_MESSAGE_何気ない会話
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と会話した

@SCHOOL_MESSAGE_踏み込んだ会話_失敗
PRINTFORMW %CALLNAME:PLAYER%の踏み込んだ会話に、%CALLNAME:TARGET%は少し迷惑そうにしている……

@SCHOOL_MESSAGE_踏み込んだ会話_成功
PRINTFORML %CALLNAME:PLAYER%の踏み込んだ会話で、%CALLNAME:TARGET%のことをより詳しく知ることができた

@SCHOOL_MESSAGE_品の無い会話_失敗
IF TALENT:幼稚
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の話をよく理解できていないようだ……
ELSE
	PRINTFORML %CALLNAME:PLAYER%の下品な話に、%CALLNAME:TARGET%は若干引いている様子だ……
ENDIF

@SCHOOL_MESSAGE_品の無い会話_成功
IF 好感度:セフレ >= 200
	PRINTFORML %CALLNAME:PLAYER%の品の無い会話にも、%CALLNAME:TARGET%は喜んで付き合った
ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || CFLAG:インモラル >= 30
	PRINTFORML %CALLNAME:PLAYER%の下品な会話に%CALLNAME:TARGET%もノリの良い返しをしてきた
ELSE
	PRINTFORML %CALLNAME:PLAYER%の下品な話を、%CALLNAME:TARGET%は宥めながらも聞いてくれた
ENDIF

@SCHOOL_MESSAGE_誘惑_失敗
IF 好感度:既成事実 >= 10
	SIF 校内()
		PRINTFORML 何度も体を重ねている%CALLNAME:TARGET%だが、さすがに学校ではできないと断られてしまった……
ELSEIF 好感度:セフレ >= 1000
	SIF 校内()
		PRINTFORML %CALLNAME:PLAYER%の提案に%CALLNAME:TARGET%も興味があるようだが、さすがに学校ではできないと断られてしまった……
ELSEIF TALENT:恋慕
	SIF 校内()
		PRINTFORML %CALLNAME:TARGET%は恋仲の%CALLNAME:PLAYER%でもさすがに学校ではできないと断られてしまった……
ELSE
	PRINTFORML %CALLNAME:TARGET%は困惑した様子で%CALLNAME:PLAYER%との会話を打ち切った……
ENDIF

@SCHOOL_MESSAGE_誘惑_成功
IF 好感度:既成事実 >= 10
	PRINTFORML 既に何度も体を重ねている%CALLNAME:TARGET%は、\@ 校内() ? 学校に居ることもお構いなしに #\@%CALLNAME:PLAYER%の誘いにも二つ返事で乗ってきた
ELSEIF 好感度:セフレ >= 1000
	IF 校内()
		PRINTFORML %CALLNAME:PLAYER%の提案に、学校に居ることもお構いなしに%CALLNAME:TARGET%は乗ってきた
	ELSE
		PRINTFORML %CALLNAME:PLAYER%の提案に、興味津々な%CALLNAME:TARGET%は快く応じた
	ENDIF
ELSEIF TALENT:恋慕
	IF 校内()
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、学校内ですることに少し困惑した様子だが、結局は了承してくれた
	ELSE
		PRINTFORML %CALLNAME:TARGET%は恋人の%CALLNAME:PLAYER%の提案を聞くと、落ち着いた声で了承した
	ENDIF
ENDIF

@SCHOOL_MESSAGE_一緒に帰宅
PRINTFORMW %CALLNAME:TARGET%を家に招き入れることにした

@SCHOOL_MESSAGE_自宅_行為後
IF TALENT:恋慕 && !好感度:既成事実
	PRINTFORMW やっと想いを遂げられた%CALLNAME:TARGET%は嬉しそうに%CALLNAME:PLAYER%にキスをして、しばらくベッドの中でじゃれ合ったのちに%CALLNAME:PLAYER%の家を後にした
ELSE
	PRINTFORMW 行為に満足した%CALLNAME:TARGET%は\@ TALENT:恋慕 ? 名残惜しそうにしながらも # 軽くシャワーを浴びて \@%CALLNAME:PLAYER%の家を後にした
ENDIF

@SCHOOL_MESSAGE_ホテル_行為前
PRINTFORMW %CALLNAME:TARGET%とホテルへ向かうことにした

@SCHOOL_MESSAGE_ホテル_行為後
IF ターン == "夜"
	PRINTFORMW 行為を終えた%CALLNAME:TARGET%たちは、そのまま眠りについた
ELSE
	PRINTFORMW 行為を終えた%CALLNAME:TARGET%たちは、時間差でホテルを後にして別れた
ENDIF

@SCHOOL_MESSAGE_ホテル朝プレイ_行為前
PRINTFORMW %CALLNAME:TARGET%たちはチェックアウトの時間まで楽しむことにした

@SCHOOL_MESSAGE_ホテル朝プレイ_行為後
PRINTFORMW 行為を終えた%CALLNAME:TARGET%たちは、時間差でホテルを後にして別れた

@SCHOOL_MESSAGE_トイレ_行為前
PRINTFORMW %CALLNAME:TARGET%を\@ 校内() ? 校舎でも人の少ない # 手頃な広さの \@トイレにこっそりと連れ込んだ

@SCHOOL_MESSAGE_トイレ_行為後
PRINTFORMW 行為を終えた%CALLNAME:TARGET%は、乱れた服を直して周りを確認しながら別れた

@SCHOOL_MESSAGE_保健室_行為前
PRINTFORMW %CALLNAME:TARGET%と保健室前まで来ると、中に誰も居ないことを確認してベッドに入った

@SCHOOL_MESSAGE_保健室_行為後
IF CSTR:在任 != ""
	PRINTFORMW %CALLNAME:TARGET%は衣服を直し、淫らな残り香が漂う保健室に芳香剤を振り撒いて%CALLNAME:PLAYER%を見送った
ELSE
	PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、汗と体液で汚れた体をアルコールティッシュで拭き、保健室を後にした
ENDIF

@SCHOOL_MESSAGE_体育館倉庫_行為前
PRINTFORMW %CALLNAME:TARGET%を体育倉庫に連れ込み、重い扉をしっかりと閉めた

@SCHOOL_MESSAGE_体育館倉庫_行為後
PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、淫臭が充満する倉庫の窓を開け、体育倉庫を後にした

@SCHOOL_MESSAGE_校長室_行為前
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を受け入れ、校長室の扉に鍵をかけた

@SCHOOL_MESSAGE_校長室_行為後
PRINTFORMW %CALLNAME:TARGET%は衣服の乱れを整え、%CALLNAME:PLAYER%を校長室から見送った

@SCHOOL_MESSAGE_理事長室_行為前
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を受け入れ、理事長室の扉に鍵をかけた

@SCHOOL_MESSAGE_理事長室_行為後
PRINTFORMW %CALLNAME:TARGET%は衣服の乱れを整え、%CALLNAME:PLAYER%を理事長室から見送った

@SCHOOL_MESSAGE_告白_保留
PRINTFORML 突然の告白に%CALLNAME:TARGET%は困惑し、今は決められないから少し時間が欲しいと言ってきた
PRINTFORMW 数日以内に%CALLNAME:TARGET%から連絡が来るだろう

@SCHOOL_MESSAGE_告白_失敗
PRINTFORMW %CALLNAME:PLAYER%は思い切って%CALLNAME:TARGET%に想いを伝えてみた
;割りと大事な場面なので素質で分岐させたりしたいかも
IF 好感度:セフレ && 好感度:セフレ >= 好感度:ときめき
	IF 好感度:既成事実
		PRINTFORMW しかし%CALLNAME:TARGET%は、%CALLNAME:PLAYER%とは体だけの関係で居たいと言って断られてしまった……
	ELSE
		IF 好感度:セフレ >= 1000
			PRINTFORMW %CALLNAME:TARGET%はしばし考え込むとその告白をやんわりと断ったが、性的なことには興味があるとオブラートに包んで言ってきた……
		ELSE
			PRINTFORMW しかし%CALLNAME:TARGET%はいまいち腑に落ちない様子で、かつ遺恨を残さないようにはっきりと断った……
		ENDIF
	ENDIF
ELSE
	IF 好感度:友情 >= 300
		PRINTFORMW しかし%CALLNAME:TARGET%は告白をはっきりと断り、友達のままで居たいと言った……
	ELSE
		PRINTFORMW しかしお互いの関係が浅すぎるせいか、%CALLNAME:TARGET%は明らかに気まずそうな様子で断った……
	ENDIF
ENDIF

@SCHOOL_MESSAGE_告白_成功
PRINTFORMW %CALLNAME:PLAYER%は思い切って%CALLNAME:TARGET%に想いを伝えてみた
;割りと大事な場面なので素質で分岐させたりしたいかも
PRINTFORMW %CALLNAME:TARGET%は少し驚いた様子を見せたが、自分も%CALLNAME:PLAYER%のことが気になっていたと肯定的な返事をした

@SCHOOL_MESSAGE_連絡先入手_失敗
PRINTFORMW %CALLNAME:TARGET%はまだそこまで親密な関係だと思っていないのか、教えてもらえずにはぐらかされてしまった……

@SCHOOL_MESSAGE_連絡先入手_成功
PRINTFORMW %CALLNAME:TARGET%に連絡先を尋ねると、快く教えてくれた

@SCHOOL_MESSAGE_同行_失敗
IF ターン == "夜"
	PRINTFORMW %CALLNAME:TARGET%と行動を共にしようと提案したが、今日はもう遅いからと断られてしまった
ELSE
	PRINTFORMW %CALLNAME:TARGET%は遠慮がちに断った。まだそこまで親密だと思っていないようだ……
ENDIF

@SCHOOL_MESSAGE_同行_成功
IF ターン == "夜"
	PRINTFORMW 夜は遅いが、%CALLNAME:TARGET%は%CALLNAME:PLAYER%の家に泊まることを提案し、一緒に%CALLNAME:PLAYER%の家に向かった
ELSE
	PRINTFORMW %CALLNAME:TARGET%は快諾して%CALLNAME:PLAYER%と行動を共にすることにした
ENDIF
@SCHOOL_MESSAGE_八牌麻雀_開始
PRINTFORMW %CALLNAME:TARGET%は意気込んで%CALLNAME:PLAYER%との勝負を受けた

@SCHOOL_MESSAGE_八牌麻雀_勝利
;気力切れたのであとで素質分岐する
PRINTFORMW なんとか%CALLNAME:TARGET%に勝つことができた。%CALLNAME:TARGET%も%CALLNAME:PLAYER%との対局を楽しんだようだ

@SCHOOL_MESSAGE_八牌麻雀_敗北
PRINTFORMW %CALLNAME:TARGET%との対局に負けてしまった。%CALLNAME:TARGET%は得意げに勝ち誇っている
