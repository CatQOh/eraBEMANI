@学園モード_汎用会話, 対象
#DIM DYNAMIC 対象
TARGET = 対象

IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_汎用会話")
	CALL SCHOOL_KOJO, TARGET, "コマンド_汎用会話"
	RETURN RESULT
ENDIF

PRINTFORML □%CALLNAME:TARGET%と会話します
PRINTL 　[0] 何気ない会話をする
PRINTL 　[1] 少し踏み込んだ会話をする
PRINTL 　[2] 下ネタを振る
IF 同行者 && 同行者 != TARGET
	CALL PRINTCOLORL, @"　[-] 性的に誘う(%NAME:同行者%が同行中)"
ELSE
	PRINTL 　[3] 性的に誘う
ENDIF
IF TALENT:恋慕
	CALL PRINTCOLORL, @"　[-] 思い切って告白する(%NAME:TARGET%は既に%CALLNAME:PLAYER%の恋人)"
ELSE
	PRINTL 　[4] 思い切って告白する
ENDIF
IF 連絡先:TARGET
	CALL PRINTCOLORL, @"　[-] 連絡先を聞いてみる(%CALLNAME:TARGET%の連絡先は既に知っている)", "gray"
ELSE
	PRINTL 　[5] 連絡先を聞いてみる
ENDIF
IF 同行者 != TARGET && !校内()
	IF 同行者
		CALL PRINTCOLORL, @"[-] 同行を提案する(既に%NAME:同行者%と行動中)", "gray"
	ELSE
		PRINTL 　[6] 同行を提案する
	ENDIF
ENDIF

DRAWLINE
PRINTPLAIN 　会話　
PRINTL [10] なんでもない

BINPUT

SELECTCASE RESULT
	CASE 0
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と会話した
		CALL SCHOOL_KOJO, TARGET, "何気ない会話"
		CALL 好感度上昇, TARGET, "友情", 10
		WAIT
		RETURN 1
	CASE 1
		IF 好感度合計(TARGET) < 200
			PRINTFORMW %CALLNAME:PLAYER%の踏み込んだ会話に、%CALLNAME:TARGET%は少し迷惑そうにしている……
			CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_失敗"
		ELSE
			PRINTFORML %CALLNAME:PLAYER%の踏み込んだ会話で、%CALLNAME:TARGET%のことをより詳しく知ることができた
			CALL SCHOOL_KOJO, TARGET, "踏み込んだ会話_成功"
			CALL 好感度上昇, TARGET, "友情", 10
			CALL 好感度上昇, TARGET, "ときめき", 10
			WAIT
		ENDIF
	CASE 2
		IF 好感度:セフレ >= 200
			PRINTFORML %CALLNAME:PLAYER%の品の無い会話にも、%CALLNAME:TARGET%は喜んで付き合った
			CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
			CALL 好感度上昇, TARGET, "友情", 10
			CALL 好感度上昇, TARGET, "セフレ", 20
		ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || CFLAG:倫理観 >= 30
			PRINTFORML %CALLNAME:PLAYER%の下品な会話に%CALLNAME:TARGET%もノリの良い返しをしてきた
			CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
			CALL 好感度上昇, TARGET, "セフレ", 20
		ELSEIF 好感度合計(TARGET) >= 200
			PRINTFORML %CALLNAME:PLAYER%の下品な話を、%CALLNAME:TARGET%は宥めながらも聞いてくれた
			CALL SCHOOL_KOJO, TARGET, "品の無い会話_成功"
			CALL 好感度上昇, TARGET, "セフレ", 10
		ELSEIF TALENT:幼稚
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の話をよく理解できていないようだ……
			CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
		ELSE
			PRINTFORML %CALLNAME:PLAYER%の下品な話に、%CALLNAME:TARGET%は若干引いている様子だ……
			CALL SCHOOL_KOJO, TARGET, "品の無い会話_失敗"
			CALL 好感度上昇, TARGET, "ときめき", -10
		ENDIF
		WAIT
	CASE 3
		IF 好感度:既成事実 >= 10
			IF 校内() && CFLAG:倫理観 < 50
				PRINTFORML 何度も体を重ねている%CALLNAME:TARGET%だが、さすがに学校ではできないと断られてしまった……
				CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
				RETURN 1
			ELSE
				PRINTFORML 既に何度も体を重ねている%CALLNAME:TARGET%は、\@ 校内() ? 学校に居ることもお構いなしに #\@%CALLNAME:PLAYER%の誘いにも二つ返事で乗ってきた
			ENDIF
			CALL SCHOOL_KOJO, TARGET, "誘惑_成功"
		ELSEIF 好感度:セフレ >= 1000
			IF 校内()
				IF CFLAG:倫理観 < 50
					PRINTFORML %CALLNAME:PLAYER%の提案に%CALLNAME:TARGET%も興味があるようだが、さすがに学校ではできないと断られてしまった……
					CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
					RETURN 1
				ELSE
					PRINTFORML %CALLNAME:PLAYER%の提案に、学校に居ることもお構いなしに%CALLNAME:TARGET%は乗ってきた
				ENDIF
			ELSE
				PRINTFORML %CALLNAME:PLAYER%の提案に、興味津々な%CALLNAME:TARGET%は快く応じた
			ENDIF
			CALL SCHOOL_KOJO, TARGET, "誘惑_成功"
		ELSEIF TALENT:恋慕
			IF 校内()
				IF CFLAG:倫理観 < 50
					PRINTFORML 恋仲の%CALLNAME:TARGET%でもさすがに学校ではできないと断られてしまった……
					CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
					RETURN 1
				ELSE
					PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を聞くと、学校内ですることに少し困惑した様子だが、結局は了承してくれた
				ENDIF
			ELSE
				PRINTFORML %CALLNAME:TARGET%は恋人の%CALLNAME:PLAYER%の提案を聞くと、落ち着いた声で了承した
			ENDIF
			CALL SCHOOL_KOJO, TARGET, "誘惑_成功"
		ELSE
			PRINTFORML %CALLNAME:TARGET%は困惑した様子で%CALLNAME:PLAYER%との会話を打ち切った……
			CALL SCHOOL_KOJO, TARGET, "誘惑_失敗"
			RETURN 1
		ENDIF
		WAIT
		IF CSTR:PLAYER:現在地 == "自宅"
			DRAWLINE
			CALL SCHOOL_KOJO, TARGET, "自宅_行為前"
			CALL TRAIN_SCHOOL, "自宅"
			CALL CHECKDRAWLINE
			PRINTFORMW 行為に満足した%CALLNAME:TARGET%は\@ TALENT:恋慕 ? 名残惜しそうにしながらも # 軽くシャワーを浴びて \@%CALLNAME:PLAYER%の家を後にした
			CALL SCHOOL_KOJO, TARGET, "自宅_行為後"
			CSTR:現在地 =
			CSTR:目的 =
			CALL 好感度上昇, TARGET, "友情", 30
			CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
			CALL 好感度上昇, TARGET, "セフレ", 50
			CALL 好感度上昇, TARGET, "既成事実", 1
			SIF !TALENT:恋慕
				CFLAG:倫理観++
			TARGET = -1
			同行者 = 0
			RETURN 1
		ELSE
			IF EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:TARGET)%_コマンド_性的に誘う")
				CALL SCHOOL_KOJO, TARGET, "コマンド_性的に誘う"
				RETURN RESULT
			ENDIF
			PRINTFORML □%CALLNAME:TARGET%をどこに連れ込みますか？
			IF !校内()
				IF CSTR:在任 != ""
					PRINTFORML 　[0] - ホテル(%CALLNAME:TARGET%が宿泊料負担)
				ELSEIF TALENT:恋慕
					IF MONEY < 10000
						SETCOLORBYNAME gray
						CALL PASELIL, 　"[-] - ホテル(宿泊料10000＄必要)"
						RESETCOLOR
					ELSE
						CALL PASELIL, 　"[0] - ホテル(宿泊料10000＄必要)"
					ENDIF
				ELSE
					IF MONEY < 5000
						SETCOLORBYNAME gray
						CALL PASELIL, 　"[-] - ホテル(割り勘で宿泊料5000＄必要)"
						RESETCOLOR
					ELSE
						CALL PASELIL, 　"[0] - ホテル(割り勘で宿泊料5000＄必要)"
					ENDIF
				ENDIF
			ENDIF
			PRINTFORML [1] - \@ 校内() ? 人気の無いトイレ # 共用トイレ \@
			SIF ターン != "夜" && !校内()
				PRINTL [2] - 自宅
			IF 校内()
				PRINTL [3] - 保健室
				PRINTL [4] - 体育倉庫
				SIF CSTR:担当科目 == "校長"
					PRINTL [5] - 校長室
			ENDIF
			BINPUT
			SELECTCASE RESULT
				CASE 0
					IF CSTR:在任 != ""
					ELSEIF TALENT:恋慕
						MONEY -= 10000
					ELSE
						MONEY -= 5000
					ENDIF
					PRINTFORMW %CALLNAME:TARGET%とホテルへ向かうことにした
					DRAWLINE
					CALL SCHOOL_KOJO, TARGET, "ホテル_行為前"
					CALL TRAIN_SCHOOL, "ホテル"
					CALL CHECKDRAWLINE
					PRINTFORMW 行為を終えた%CALLNAME:TARGET%たちは、時間差でホテルを後にして別れた
					CALL SCHOOL_KOJO, TARGET, "ホテル_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観++
					TARGET = -1
					RETURN 1
				CASE 1
					PRINTFORMW %CALLNAME:TARGET%を\@ 校内() ? 校舎でも人の少ない # 手頃な広さの \@トイレにこっそりと連れ込んだ
					DRAWLINE
					トイレ使用中 = 1
					CALL SCHOOL_KOJO, TARGET, "トイレ_行為前"
					CALL TRAIN_SCHOOL, "トイレ"
					CALL CHECKDRAWLINE
					PRINTFORMW 行為を終えた%CALLNAME:TARGET%は、乱れた服を直して周りを確認しながら別れた
					CALL SCHOOL_KOJO, TARGET, "トイレ_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					;トイレはホテルほどときめかない
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 2
					CFLAG:倫理観 += 2
					トイレ使用中 = 0
					TARGET = -1
					RETURN 1
				CASE 2
					PRINTFORMW %CALLNAME:TARGET%を家に招き入れることにした
					CSTR:現在地 = %CALLNAME:PLAYER%の家
					CSTR:PLAYER:現在地 = 自宅
					CSTR:目的 = セックス
					同行者 = TARGET
					CALL SCHOOL_KOJO, TARGET, "一緒に帰宅"
					TARGET = -1
					RETURN 1
				CASE 3
					PRINTFORMW %CALLNAME:TARGET%と保健室前まで来ると、中に誰も居ないことを確認してベッドに入った
					DRAWLINE
					CALL SCHOOL_KOJO, TARGET, "保健室_行為前"
					CALL TRAIN_SCHOOL, "保健室"
					CALL CHECKDRAWLINE
					PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、汗と体液で汚れた体をアルコールティッシュで拭き、保健室を後にした
					CALL SCHOOL_KOJO, TARGET, "保健室_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 2
					CFLAG:倫理観 += 2
					TARGET = -1
					RETURN 1
				CASE 4
					PRINTFORMW %CALLNAME:TARGET%を体育倉庫に連れ込み、重い扉をしっかりと閉めた
					DRAWLINE
					CALL SCHOOL_KOJO, TARGET, "体育倉庫_行為前"
					CALL TRAIN_SCHOOL, "体育倉庫"
					CALL CHECKDRAWLINE
					PRINTFORMW %CALLNAME:TARGET%は呼吸を整え、淫臭が充満する倉庫の窓を開け、体育倉庫を後にした
					CALL SCHOOL_KOJO, TARGET, "体育倉庫_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 2
					CFLAG:倫理観 += 2
					TARGET = -1
					RETURN 1
				CASE 5
					PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の提案を受け入れ、校長室の扉に鍵をかけた
					DRAWLINE
					CALL SCHOOL_KOJO, TARGET, "校長室_行為前"
					CALL TRAIN_SCHOOL, "校長室"
					CALL CHECKDRAWLINE
					PRINTFORMW %CALLNAME:TARGET%は衣服の乱れを整え、%CALLNAME:PLAYER%を校長室から見送った
					CALL SCHOOL_KOJO, TARGET, "校長室_行為後"
					CALL 好感度上昇, TARGET, "友情", 30
					CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
					CALL 好感度上昇, TARGET, "セフレ", 50
					CALL 好感度上昇, TARGET, "既成事実", 1
					SIF !TALENT:恋慕
						CFLAG:倫理観 += 3
					CFLAG:倫理観 += 3
					TARGET = -1
					RETURN 1
			ENDSELECT
		ENDIF
	CASE 4
		PRINTFORMW %CALLNAME:PLAYER%は思い切って%CALLNAME:TARGET%に想いを伝えてみた
		;割りと大事な場面なので素質で分岐させたりしたいかも
		IF 好感度:セフレ && 好感度:セフレ >= 好感度:ときめき
			IF 好感度:既成事実
				PRINTFORMW しかし%CALLNAME:TARGET%は、%CALLNAME:PLAYER%とは体だけの関係で居たいと言って断られてしまった……
				CALL SCHOOL_KOJO, TARGET, "告白_失敗"
				CALL 好感度上昇, TARGET, "セフレ", 10
				WAIT
			ELSE
				IF 好感度:セフレ >= 1000
					PRINTFORMW %CALLNAME:TARGET%はしばし考え込むとその告白をやんわりと断ったが、性的なことには興味があるとオブラートに包んで言ってきた……
					CALL SCHOOL_KOJO, TARGET, "告白_失敗"
					CALL 好感度上昇, TARGET, "セフレ", 5
					WAIT
				ELSE
					PRINTFORMW しかし%CALLNAME:TARGET%はいまいち腑に落ちない様子で、かつ遺恨を残さないようにはっきりと断った……
					CALL SCHOOL_KOJO, TARGET, "告白_失敗"
				ENDIF
			ENDIF
		ELSEIF 好感度:ときめき >= 1000
			PRINTFORMW %CALLNAME:TARGET%は少し驚いた様子を見せたが、自分も%CALLNAME:PLAYER%のことが気になっていたと肯定的な返事をした
			CALL SCHOOL_KOJO, TARGET, "告白_成功"
			CALL 恋仲, TARGET
			WAIT
		[SKIPSTART]
		ELSEIF 好感度:ときめき >= 500 && 連絡先:TARGET
			PRINTFORML 突然の告白に%CALLNAME:TARGET%は困惑し、今は決められないから少し時間が欲しいと言ってきた
			PRINTFORMW 数日以内に%CALLNAME:TARGET%から連絡が来るだろう
			告白保留:TARGET = RAND(4, 8)
			CALL SCHOOL_KOJO, TARGET, "告白_保留"
			CALL 好感度上昇, TARGET, "ときめき", 10
			WAIT
		[SKIPEND]
		ELSE
			IF 好感度:友情 >= 300
				PRINTFORMW しかし%CALLNAME:TARGET%は告白をはっきりと断り、友達のままで居たいと言った……
				CALL SCHOOL_KOJO, TARGET, "告白_失敗"
				CALL 好感度上昇, TARGET, "友情", 10
				CALL 好感度上昇, TARGET, "ときめき", 5
			ELSE
				PRINTFORMW しかしお互いの関係が浅すぎるせいか、%CALLNAME:TARGET%は明らかに気まずそうな様子で断った……
				CALL SCHOOL_KOJO, TARGET, "告白_失敗"
				CALL 好感度上昇, TARGET, "友情", 5
				CALL 好感度上昇, TARGET, "ときめき", -5
			ENDIF
		ENDIF
	CASE 5
		IF 好感度合計(TARGET) >= 500
			PRINTFORMW %CALLNAME:TARGET%に連絡先を尋ねると、快く教えてくれた
			CALL SCHOOL_KOJO, TARGET, "連絡先入手_成功"
			CALL 連絡先入手, TARGET
			CALL 好感度上昇, TARGET, "友情", 10
			WAIT
		ELSE
			PRINTFORMW %CALLNAME:TARGET%はまだそこまで親密な関係だと思っていないのか、教えてもらえずにはぐらかされてしまった……
			CALL SCHOOL_KOJO, TARGET, "連絡先入手_失敗"
		ENDIF
	CASE 6
		IF ターン == "夜"
			PRINTFORMW %CALLNAME:TARGET%と行動を共にしようと提案したが、今日はもう遅いからと断られてしまった
			CALL SCHOOL_KOJO, TARGET, "同行_失敗"
			RESTART
		ELSEIF 好感度合計(TARGET) >= 200
			PRINTFORMW %CALLNAME:TARGET%は快諾して%CALLNAME:PLAYER%と行動を共にすることにした
			同行者 = TARGET
			CALL SCHOOL_KOJO, TARGET, "同行_成功"
			CALL 好感度上昇, TARGET, "友情", 5
			CALL 好感度上昇, TARGET, "ときめき", 5
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は遠慮がちに断った。まだそこまで親密だと思っていないようだ……
			CALL SCHOOL_KOJO, TARGET, "同行_失敗"
		ENDIF
	CASE 10
		RETURN 0
ENDSELECT

RETURN 1
