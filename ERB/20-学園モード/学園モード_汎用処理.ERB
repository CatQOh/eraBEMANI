@学校一覧
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, VARSIZE("学校名")
	PRINTFORMLC 　[{LCOUNT}] - %学校名:LCOUNT%
NEXT

CALL NEWLINE

@TURNEND_学園
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 滞在者

;ターンを進める
SELECTCASE ターン
	CASE "朝"
		PRINTW 昼になった……
		ターン = 昼
		;登校中のキャラを参照
		FOR LCOUNT, 1, CHARANUM
			;サボり魔は10%でバックレてる さすがに教師はバックレない
			SIF CSTR:LCOUNT:現在地 == CSTR:LCOUNT:在校 && TALENT:LCOUNT:サボり魔 && RAND:10 == 0
				CSTR:LCOUNT:現在地 = 
		NEXT
	CASE "昼"
		PRINTL 夕方になった……
		ターン = 夕
		;生徒は放課後タイム
		FOR LCOUNT, 1, CHARANUM
			SIF CSTR:LCOUNT:現在地 == CSTR:LCOUNT:在校
				CSTR:LCOUNT:現在地 = 
			;教師陣もぼちぼち帰る
			SIF CSTR:LCOUNT:現在地 == CSTR:LCOUNT:在任 && RAND:2
				CSTR:LCOUNT:現在地 = 
		NEXT
		;PLAYERも学校に入れば帰宅
		IF CSTR:PLAYER:現在地 == CSTR:PLAYER:在校
			PRINTL 授業の日程を終えて下校した
			CSTR:PLAYER:現在地 = 自宅
		ENDIF
		WAIT
	CASE "夕"
		PRINTW 夜になった……
		ターン = 夜
		;教師陣も確定で帰る
		;生徒は放課後タイム
		FOR LCOUNT, 1, CHARANUM
			SIF CSTR:LCOUNT:現在地 == CSTR:LCOUNT:在任
				CSTR:LCOUNT:現在地 = 
		NEXT
	CASE "夜"
		IF 同行者
			PRINTFORMW %CALLNAME:同行者%は、今日はもう遅いからまた明日と%CALLNAME:PLAYER%と別れた
			CSTR:同行者:現在地 =
			CSTR:同行者:目的 =
			同行者 = 0
		ENDIF
		;滞在者 = FINDCHARA(CSTR:現在地, @"%CALLNAME:PLAYER%の家", 1)
		;IF 滞在者 > 0
		;	PRINTFORMW %CALLNAME:滞在者%は、今日はもう遅いからと%CALLNAME:PLAYER%の家を後にした
		;	CSTR:滞在者:現在地 = 
		;	CSTR:同行者:目的 = 
		;ENDIF
		PRINTW 一日が終わった……
		ターン = 朝
		DAY++
		CALL NEXTDAY
		CSTR:PLAYER:現在地 = 自宅
		IF !休日()
			;朝は在校生と教師は学校に居る
			FOR LCOUNT, 1, CHARANUM
				;ただしサボり魔は10%でサボってる 教師も例外ではない
				IF CSTR:LCOUNT:在校 != "" && !(TALENT:LCOUNT:サボり魔 && RAND:10 == 0)
					CSTR:LCOUNT:現在地 = %CSTR:LCOUNT:在校%
					;小学校のみ私服登校可
					IF 小中高(CSTR:LCOUNT:在校) != "小"
						SELECTCASE SEX(LCOUNT)
							CASE 1
								CSTR:LCOUNT:上半身上着 = 制服
								CSTR:LCOUNT:下半身上着 = ズボン
								CSTR:LCOUNT:全身上着 =

								CFLAG:LCOUNT:挿入可 = 0
								CFLAG:LCOUNT:着衣併用 = 0
								CFLAG:LCOUNT:上着パイズリ = 5
							CASE 2
								CSTR:LCOUNT:上半身上着 = 制服
								CSTR:LCOUNT:下半身上着 = スカート
								CSTR:LCOUNT:全身上着 =

								CFLAG:LCOUNT:挿入可 = 1
								CFLAG:LCOUNT:着衣併用 = 0
								CFLAG:LCOUNT:上着パイズリ = 5
						ENDSELECT
					ENDIF
				ENDIF
				SIF CSTR:LCOUNT:在任 != "" && !(TALENT:LCOUNT:サボり魔 && RAND:10 == 0)
					CSTR:LCOUNT:現在地 = %CSTR:LCOUNT:在任%
			NEXT
		ELSE
			;休日なら各々私服になる
			FOR LCOUNT, 1, CHARANUM
				FOR LCOUNT2, 100, 107
					CSTR:LCOUNT:LCOUNT2 = %CSVCSTR(NOF(LCOUNT), LCOUNT2)%
				NEXT
				CFLAG:LCOUNT:挿入可 = CSVCFLAG(NOF(LCOUNT), GETNUM(CFLAG, "挿入可"))
				CFLAG:LCOUNT:着衣併用 = CSVCFLAG(NOF(LCOUNT), GETNUM(CFLAG, "着衣併用"))
				CFLAG:LCOUNT:上着パイズリ = CSVCFLAG(NOF(LCOUNT), GETNUM(CFLAG, "上着パイズリ"))
				CFLAG:LCOUNT:下着パイズリ = CSVCFLAG(NOF(LCOUNT), GETNUM(CFLAG, "下着パイズリ"))
			NEXT
		ENDIF
ENDSELECT

FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:倫理観 > 100
		CFLAG:LCOUNT:倫理観 = 100
NEXT

CALL SYSTEM_AUTOSAVE

VARLINE = LINECOUNT

LOOP_BREAK = 1

;JUMP SHOP_SCHOOL

@相性増加, ARG, ARG:1, 上昇値
#DIM DYNAMIC 上昇値

;引数の2人の間の相性を上昇させる関数

[SKIPSTART]
RELATION:ARG:(ARG:1) += 上昇値
RELATION:(ARG:1):ARG += 上昇値
[SKIPEND]

@クラスメイト選出, 対象, 選出タイプ
#DIM DYNAMIC 対象
#DIMS DYNAMIC 選出タイプ
;無所属が一番多くなるので余裕を持つ
#DIM DYNAMIC クラスメイト, 300
#DIM DYNAMIC 候補, 5
#DIM DYNAMIC 人数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2

;第一引数のキャラと同じ学校の生徒を、第二引数で指定した条件で選出する
;RESULT:0～4で最大5人返す 必要なぶんだけRESULTを参照すると良い

SIF データベース参照中
	RETURN

;既にどこかのグループに入ってる場合は無効
SIF 出現率:対象 == -1
	RETURN
;同行中も無効
IF 同行者 == 対象
	出現率:対象 = -1
	RETURN
ENDIF

;グループ候補は出現率を-1に設定する
出現率:対象 = -1

人数 = 0

FOR LCOUNT, 1, CHARANUM
	SIF CSTR:対象:在校 == CSTR:LCOUNT:在校
		クラスメイト:(人数++) = LCOUNT
NEXT

;クラスメイト変数の順番をシャッフルする
FOR LCOUNT, 0, 人数
	SWAP クラスメイト:LCOUNT, クラスメイト:(RAND(LCOUNT, 人数))
NEXT


SELECTCASE 選出タイプ
	;相性の良いクラスメイトを優先
	;同値のキャラが複数いればランダムで選出（上で既にシャッフルしてある）
	CASE "相性"
		FOR LCOUNT, 0, 人数
			;対象は除外
			SIF クラスメイト:LCOUNT == 対象
				CONTINUE
			;既にグループ候補の場合は入れない
			SIF 出現率:(クラスメイト:LCOUNT) == -1
				CONTINUE
			;現在地が確定してる場合も無理
			SIF CSTR:(クラスメイト:LCOUNT):現在地 != ""
				CONTINUE
			FOR LCOUNT2, 0, 5
				{
				IF 候補:LCOUNT2 == 0 ||
				相性値(クラスメイト:LCOUNT, 対象) > 相性値(候補:LCOUNT2, 対象) ||
				相性値(対象, クラスメイト:LCOUNT) > 相性値(対象, 候補:LCOUNT2)
				}
					ARRAYSHIFT 候補, 1, クラスメイト:LCOUNT, LCOUNT2
					;グループ候補は出現率を-1に設定する
					出現率:(クラスメイト:LCOUNT) = -1
					BREAK
				ENDIF
			NEXT
		NEXT
	;ランダム選出
	CASEELSE
		FOR LCOUNT, 0, 人数
			;対象は除外
			SIF クラスメイト:LCOUNT == 対象
				CONTINUE
			;既にグループ候補の場合は入れない
			SIF 出現率:(クラスメイト:LCOUNT) == -1
				CONTINUE
			;現在地が確定してる場合も無理
			SIF CSTR:(クラスメイト:LCOUNT):現在地 != ""
				CONTINUE
			ARRAYSHIFT 候補, 1, クラスメイト:LCOUNT
		NEXT
		ARRAYCOPY "クラスメイト", "候補"
ENDSELECT

RETURN 候補:0, 候補:1, 候補:2, 候補:3, 候補:4

@グループキャラ選出, 対象, 選出タイプ
#DIM DYNAMIC 対象
#DIMS DYNAMIC 選出タイプ
#DIM DYNAMIC 候補, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2

;上のとほぼ同じだが、こちらは学校ごとではなく全体でグループを作る
;ただし学校が同じでもない、相性が良いわけでもない、何も接点の無いキャラはグループにならない
;RESULT:0～4で最大5人返す 必要なぶんだけRESULTを参照すると良い

SIF データベース参照中
	RETURN

;既にどこかのグループに入ってる場合は無効
SIF 出現率:対象 == -1
	RETURN
;同行中も無効
IF 同行者 == 対象
	出現率:対象 = -1
	RETURN
ENDIF

;グループ候補は出現率を-1に設定する
出現率:対象 = -1

;番号が変わるので対象の値をNOにしておく
対象 = NO:対象

;キャラの順番をシャッフルする
FOR LCOUNT, 1, CHARANUM
	SWAPCHARA LCOUNT, RAND(LCOUNT, CHARANUM)
NEXT

SELECTCASE 選出タイプ
	;相性の良いキャラを優先
	;同値のキャラが複数いればランダムで選出
	CASE "相性"
		FOR LCOUNT, 1, CHARANUM
			;対象自身は除外
			SIF LCOUNT == GETCHARA(対象)
				CONTINUE
			;既にグループ候補の場合は入れない
			SIF 出現率:LCOUNT == -1
				CONTINUE
			;現在地が確定してる場合も無理
			SIF CSTR:LCOUNT:現在地 != ""
				CONTINUE
			;相性、学校、血縁のいずれかも接点が無いキャラは除外
			SIF CSTR:GETCHARA(対象):在校 != "" && CSTR:GETCHARA(対象):在校 == CSTR:LCOUNT:在校
				GOTO CLEAR
			SIF CSTR:GETCHARA(対象):在任 != "" && CSTR:GETCHARA(対象):在任 == CSTR:LCOUNT:在任
				GOTO CLEAR
			SIF CFLAG:血縁コード && CFLAG:GETCHARA(対象):血縁コード == CFLAG:LCOUNT:血縁コード
				GOTO CLEAR
			SIF 相性値(GETCHARA(対象), LCOUNT) > 100
				GOTO CLEAR
			SIF 相性値(LCOUNT, GETCHARA(対象)) > 100
				GOTO CLEAR
			CONTINUE

			$CLEAR
			FOR LCOUNT2, 0, 5
				{
				IF 候補:LCOUNT2 == 0 ||
				相性値(LCOUNT, GETCHARA(対象)) > 相性値(GETCHARA(候補:LCOUNT2), GETCHARA(対象)) ||
				相性値(GETCHARA(対象), LCOUNT) > 相性値(GETCHARA(対象), GETCHARA(候補:LCOUNT2))
				}
					;こっちは後でキャラ番号戻るのでNOで入れる
					ARRAYSHIFT 候補, 1, NO:LCOUNT, LCOUNT2
					BREAK
				ENDIF
			NEXT
		NEXT
	;接点のあるキャラをランダム選出
	CASEELSE
		FOR LCOUNT, 1, CHARANUM
			;対象自身は除外
			SIF LCOUNT == GETCHARA(対象)
				CONTINUE
			;既にグループ候補の場合は入れない
			SIF 出現率:LCOUNT == -1
				CONTINUE
			;現在地が確定してる場合も無理
			SIF CSTR:LCOUNT:現在地 != ""
				CONTINUE
			;相性、学校、血縁のいずれかも接点が無いキャラは除外
			SIF CSTR:GETCHARA(対象):在校 == CSTR:LCOUNT:在校
				GOTO CLEARELSE
			SIF CSTR:GETCHARA(対象):在任 == CSTR:LCOUNT:在任
				GOTO CLEARELSE
			SIF CFLAG:GETCHARA(対象):血縁コード && CFLAG:GETCHARA(対象):血縁コード == CFLAG:LCOUNT:血縁コード
				GOTO CLEARELSE
			SIF 相性値(GETCHARA(対象), LCOUNT) > 100
				GOTO CLEARELSE
			SIF 相性値(LCOUNT, GETCHARA(対象)) > 100
				GOTO CLEARELSE
			CONTINUE

			$CLEARELSE
			;こっちは後でキャラ番号戻るのでNOで入れる
			ARRAYSHIFT 候補, 1, NO:LCOUNT
		NEXT
ENDSELECT

;最後にNOでソートしなおす
SORTCHARA NO

;グループ候補は出現率を-1に設定する
FOR LCOUNT, 0, 5
	SIF 候補:LCOUNT
		出現率:GETCHARA(候補:LCOUNT) = -1
NEXT

RETURN GETCHARA(候補:0), GETCHARA(候補:1), GETCHARA(候補:2), GETCHARA(候補:3), GETCHARA(候補:4)

@学園データベース
#DIM DYNAMIC LCOUNT

CALL 学校一覧_データベース
RETURN 0

PRINTL [1] - 学校一覧
;PRINTL [2] - 
DRAWLINE
PRINTPLAIN 　DATABASE　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0 
		RETURN 0
	CASE 1
		CALL 学校一覧_データベース
		RESTART
ENDSELECT

@学校一覧_データベース
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 名称抜き取り用, 2
#DIM DYNAMIC 選択校
#DIM DYNAMIC 選択キャラ
#DIM DYNAMIC 表示数

FOR LCOUNT, 0, VARSIZE("学校名")
	SIF 学校名:LCOUNT == ""
		CONTINUE
	PRINTFORMLC 　[{LCOUNT, 2}] - %学校名:LCOUNT%
NEXT
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　DATABASE　
PRINTL [-1] - 戻る

BINPUT
選択校 = RESULT
SIF RESULT == -1
	RETURN

$生徒選択
DRAWLINE
PRINTFORML □%学校名:選択校%の在校生
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:在校 != 学校名:選択校
		CONTINUE
	PRINTFORM 　[{LCOUNT}] 
	CALL SNAME(LCOUNT)
	PRINTL 
NEXT
PRINTFORML □%学校名:選択校%の教師
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:在任 != 学校名:選択校
		CONTINUE
	PRINTFORM 　担当科目:%CSTR:LCOUNT:担当科目% [{LCOUNT}] 
	CALL SNAME(LCOUNT)
	PRINTL  
NEXT
DRAWLINE
PRINTPLAINFORM 　%学校名:選択校%　
PRINTL [-1] - 戻る

BINPUT
SIF RESULT == -1
	RESTART

選択キャラ = RESULT
DRAWLINE
PRINTFORML □%NAME:選択キャラ%の情報
PRINTFORML 　倫理観崩壊度:{CFLAG:選択キャラ:倫理観}
IF ラブスコープ使用
	PRINTL □好感度
	PRINTFORMLC 　友情:{好感度:選択キャラ:友情}
	PRINTFORMLC ときめき:{好感度:選択キャラ:ときめき}
	PRINTFORMLC 尊敬:{好感度:選択キャラ:尊敬}
	PRINTL 
	PRINTFORMLC 　\@ SEX(選択キャラ) == 2 ? 母性 # 舎弟 \@:{好感度:選択キャラ:舎弟}
	PRINTFORMLC セフレ:{好感度:選択キャラ:セフレ}
	PRINTFORMLC 既成事実:{好感度:選択キャラ:既成事実}
	PRINTL 
ENDIF
PRINTL □各ロケーションでの出現率
;どこのロケーションに出現するのかを表示
データベース参照中 = 選択キャラ
表示数 = 0
FOR LCOUNT, 0, VARSIZE("ロケーション")
	VARSET 出現率
	SPLIT ロケーション:LCOUNT, "/", 名称抜き取り用
	;分割されてたらスラッシュより後の部分だけを使用する
	IF RESULT >= 2
		RESULT = 0
		TRYCALLFORM 出現キャラ_%名称抜き取り用:1%
	ELSE
		RESULT = 0
		TRYCALLFORM 出現キャラ_%ロケーション:LCOUNT%
	ENDIF
	;出現率も表示
	SIF !出現率:選択キャラ
		CONTINUE
	PRINTFORMLC 　%ロケーション:LCOUNT%({出現率:選択キャラ}\%)
	表示数++
	SIF 表示数 != 0 && 表示数%5 == 0
		PRINTL 
NEXT
データベース参照中 = 0
CALL NEWLINE
DRAWLINE
PRINTPLAIN 　DATABASE　
PRINTW クリック・Enterで戻る

GOTO 生徒選択

@好感度合計, 対象
#FUNCTION
#DIM DYNAMIC 対象

RETURNF SUMARRAY(好感度:対象:0)

;;;学園モードの名前表示処理
@SNAME, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 好感度値
#DIMS DYNAMIC 表示好感度

IF ラブスコープ使用
	PRINTFORM %NAME:対象%
	SELECTCASE ラブスコープ使用
		CASE 1 TO 6
			表示好感度 = %ERDNAME(好感度, ラブスコープ使用-1)%
			好感度値 = 好感度:対象:(ラブスコープ使用-1)
		CASE 7
			表示好感度 = 好感度合計
			好感度値 = 好感度合計(対象)
		CASE 8
			表示好感度 = 倫理観崩壊度
			好感度値 = CFLAG:対象:倫理観
	ENDSELECT
	SELECTCASE 表示好感度
		CASE "倫理観崩壊度"
			SELECTCASE 好感度値
				CASE 0
					SETCOLORBYNAME gray
				CASE 1 TO 29
					SETCOLORBYNAME lime
				CASE 30 TO 49
					SETCOLORBYNAME yellow
				CASE IS >= 50
					SETCOLORBYNAME coral
			ENDSELECT
		CASEELSE
			SELECTCASE 好感度値
				CASE 0
					SETCOLORBYNAME gray
				CASE 1 TO 99
				CASE 100 TO 299
					SETCOLORBYNAME lime
				CASE 300 TO 499
					SETCOLORBYNAME deepskyblue
				CASE 500 TO 999
					SETCOLORBYNAME violet
				CASE IS >= 1000
					SETCOLORBYNAME coral
			ENDSELECT
	ENDSELECT
	PRINTFORM (%表示好感度%:{好感度値})
	RESETCOLOR
ELSE
	PRINTFORM %NAME:対象%
ENDIF

;;;好感度上昇とメッセージ表示を行う
@好感度上昇, 対象, 好感度種類, 上昇値
#DIM DYNAMIC 対象
#DIMS DYNAMIC 好感度種類
#DIM DYNAMIC 上昇値
#DIM DYNAMIC LCOUNT
;なんか全体的に数値が重く感じたので2倍にしてみる
#DIM DYNAMIC 倍率 = 200

;上昇値は既成事実以外は5刻みで渡されるはず
;なので倍率は20刻みになるのがちょうどいい

;口上側での上昇値補正の加算
SIF 好感度種類 != "既成事実"
	上昇値 += 好感度補正:好感度種類

;素質等での補正を入れる
SELECTCASE 好感度種類
	CASE "友情"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				{
				CASE "反抗的", "プライド高い", "弱気", "ツンデレ", "無関心", "保守的", "抑圧",
					 "恥じらい", "弱味"
				}
					倍率 -= 20
				{
				CASE "素直", "プライド低い", "ハイテンション", "寂しがり屋", "幼稚", "楽観的", "目立ちたがり",
					 "解放", "好奇心", "世話好き"
				}
					倍率 += 20
				CASE "即落ち"
					倍率 += 40
				CASE "男好き"
					SIF SEX(PLAYER) == 1
						倍率 += 40
				CASE "女好き"
					SIF SEX(PLAYER) == 2
						倍率 += 40
				CASE "男嫌い"
					SIF SEX(PLAYER) == 1
						倍率 -= 40
				CASE "女嫌い"
					SIF SEX(PLAYER) == 2
						倍率 -= 40
				CASE "同族嫌悪"
					SIF CSTR:対象:種族 == CSTR:PLAYER:種族
						倍率 -= 40
				CASE "子供好き"
					SIF TALENT:PLAYER:小柄体型
						倍率 += 20
				CASE "恋心"
					SIF TALENT:PLAYER:恋心 == NO:PLAYER
						倍率 += 20
				CASE "既婚者"
					SIF TALENT:PLAYER:既婚者 == NO:PLAYER
						倍率 += 40
			ENDSELECT
		NEXT
	CASE "ときめき"
		;性別同じだとときめきづらい
		IF SEX(対象) == SEX(PLAYER)
			IF SEX(PLAYER) == 1 && !TALENT:対象:男好き
				倍率 -= 20
			ELSEIF SEX(PLAYER) == 2 && !TALENT:対象:女好き
				倍率 -= 20
			ENDIF
		ENDIF
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				{
				CASE "生意気", "臆病", "冷静", "大人しい", "幼稚", "自制心", "無関心", "感情乏しい",
						"悲観的", "抑圧"
				}
					倍率 -= 20
				{
				CASE "素直", "衝動的", "寂しがり屋", "ツンデレ", "解放", "恥薄い"
				}
					倍率 += 20
				CASE "即落ち", "ヤンデレ"
					倍率 += 40
				CASE "男好き"
					SIF SEX(PLAYER) == 1
						倍率 += 20
				CASE "女好き"
					SIF SEX(PLAYER) == 2
						倍率 += 20
				CASE "男嫌い"
					SIF SEX(PLAYER) == 1
						倍率 -= 40
				CASE "女嫌い"
					SIF SEX(PLAYER) == 2
						倍率 -= 40
				CASE "同族嫌悪"
					SIF CSTR:対象:種族 == CSTR:PLAYER:種族
						倍率 -= 40
				CASE "子供好き"
					SIF TALENT:PLAYER:小柄体型
						倍率 += 20
				CASE "恋心"
					IF TALENT:PLAYER:恋心 == NO:PLAYER
						倍率 += 20
					ELSE
						倍率 -= 40
					ENDIF
				CASE "既婚者"
					IF TALENT:PLAYER:既婚者 == NO:PLAYER
						倍率 += 40
					ELSE
						倍率 -= 60
					ENDIF
			ENDSELECT
		NEXT
	CASE "尊敬"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "生意気", "反抗的", "プライド高い", "強気", "ツンデレ"
					倍率 -= 20
				CASE "臆病", "冷静", "プライド低い", "大人しい", "弱気", "寂しがり屋", "ヤンデレ", "幼稚", "献身的"
					倍率 += 20
				CASE "即落ち"
					倍率 += 40
				CASE "同族嫌悪"
					SIF CSTR:対象:種族 == CSTR:PLAYER:種族
						倍率 -= 40
				CASE "子供好き"
					SIF TALENT:PLAYER:小柄体型
						倍率 -= 20
				CASE "恋心"
					SIF TALENT:PLAYER:恋心 == NO:PLAYER
						倍率 += 20
				CASE "既婚者"
					SIF TALENT:PLAYER:既婚者 == NO:PLAYER
						倍率 += 40
			ENDSELECT
		NEXT
	CASE "舎弟"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "臆病", "プライド低い", "大人しい", "弱気", "悲観的", "抑圧", ""
					倍率 -= 20
				{
				CASE "生意気", "反抗的", "冷静", "プライド高い", "強気", "ツンデレ", "自制心", "保守的", "目立ちたがり", "気丈",
					 "統率", "世話好き"
				}
					倍率 += 20
				CASE "即落ち"
					倍率 += 40
				CASE "同族嫌悪"
					SIF CSTR:対象:種族 == CSTR:PLAYER:種族
						倍率 -= 40
				CASE "子供好き"
					SIF TALENT:PLAYER:小柄体型
						倍率 += 20
				CASE "恋心"
					SIF TALENT:PLAYER:恋心 == NO:PLAYER
						倍率 += 20
				CASE "既婚者"
					SIF TALENT:PLAYER:既婚者 == NO:PLAYER
						倍率 += 40
			ENDSELECT
		NEXT
	CASE "セフレ"
		;性別同じだと上がりづらい
		IF SEX(対象) == SEX(PLAYER)
			IF SEX(PLAYER) == 1 && !TALENT:対象:男好き
				倍率 -= 40
			ELSEIF SEX(PLAYER) == 2 && !TALENT:対象:女好き
				倍率 -= 40
			ENDIF
		ENDIF
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				{
				CASE "臆病", "大人しい", "幼稚", "自制心", "無関心", "一線越えない", "貞操観念", "恥じらい", "快感の否定",
						"潔癖症", 
				}
					倍率 -= 20
				{
				CASE "衝動的", "プライド低い", "ハイテンション", "弱気", "ツンデレ", "好奇心", "楽観的", "貞操無頓着",
						"解放", "恥薄い", "弱味", "快感に素直", "中毒しやすい", "濡れやすい", "勃ちやすい", "自慰しやすい"
				}
					倍率 += 20
				CASE "即落ち", "ヤンデレ"
					倍率 += 40
				CASE "男好き"
					SIF SEX(PLAYER) == 1
						倍率 += 20
				CASE "女好き"
					SIF SEX(PLAYER) == 2
						倍率 += 20
				CASE "男嫌い"
					SIF SEX(PLAYER) == 1
						倍率 -= 40
				CASE "女嫌い"
					SIF SEX(PLAYER) == 2
						倍率 -= 40
				CASE "同族嫌悪"
					SIF CSTR:対象:種族 == CSTR:PLAYER:種族
						倍率 -= 40
				CASE "子供好き"
					SIF TALENT:PLAYER:小柄体型
						倍率 += 20
				CASE "恋心"
					IF TALENT:PLAYER:恋心 == NO:PLAYER
						倍率 += 20
					ELSE
						倍率 -= 60
					ENDIF
				CASE "既婚者"
					IF TALENT:PLAYER:既婚者 == NO:PLAYER
						倍率 += 40
					ELSE
						倍率 -= 80
					ENDIF
			ENDSELECT
		NEXT
ENDSELECT

IF TALENT:PLAYER:謎の魅力
	倍率 += 40
ELSEIF TALENT:PLAYER:魅力 || TALENT:PLAYER:魅惑
	倍率 += 20
ENDIF

倍率 = MAX(倍率, 20)

SIF SIGN(上昇値) == 1
	上昇値 = 上昇値*倍率/100

好感度:対象:好感度種類 += 上昇値

IF SEX(対象) == 2 && 好感度種類 == "舎弟"
	PRINTFORML 好感度上昇:%NAME:対象%(母性\@ SIGN(上昇値) == 1 ? + #\@{上昇値})
ELSE
	PRINTFORML 好感度上昇:%NAME:対象%(%好感度種類%\@ SIGN(上昇値) == 1 ? + #\@{上昇値})
ENDIF

;;;連絡先入手とメッセージ表示
@連絡先入手, 対象
#DIM DYNAMIC 対象

連絡先:対象 = 1

PRINTFORML 連絡先入手:%NAME:対象%

;;;恋人処理とメッセージ表示
@恋仲, 対象
#DIM DYNAMIC 対象

TALENT:対象:恋慕 = 1

PRINTFORML %NAME:対象%が恋人になった！

@TRAIN_SCHOOL, 場所
#DIMS DYNAMIC 場所

CALL SCHOOL_COMFILTER, 場所

BASE:体力 = MAXBASE:体力
BASE:気力 = MAXBASE:気力

CALL EVENTUSERTRAIN
DO
	CALL SHOW_STATUS
	CALL SHOW_USERCOM
	;INPUT
	;CALL USERCOM
LOOP !TFLAG:調教終了

;習得の珠加算
SELECTCASE GETPALAMLV(PALAM:習得, 10)
	CASE 0
	CASE 1
		IF PALAM:習得 < PALAMLV:1*3
			JUEL:習得 += 1
		ELSE
			JUEL:習得 += 2
		ENDIF
	CASE 2
		IF PALAM:習得 < PALAMLV:2*3
			JUEL:習得 += 10
		ELSE
			JUEL:習得 += 20
		ENDIF
	CASE 3
		IF PALAM:習得 < PALAMLV:3*2
			JUEL:習得 += 100
		ELSE
			JUEL:習得 += 200
		ENDIF
	CASE 5
		JUEL:習得 += 1000
	CASE 6
		JUEL:習得 += 2000
	CASE 7
		JUEL:習得 += 3000
	CASE 8
		JUEL:習得 += 5000
	CASE 9
		JUEL:習得 += 8000
	CASEELSE
		JUEL:習得 += PALAM:習得/20
ENDSELECT

;とりあえず上級売春と同じ処理で自動上昇するようにしてみる
ABL:Ｃ感覚 = MAX(ABL:TARGET:Ｃ感覚, LIMIT(EXP:絶頂経験/50, 0, 10))
ABL:Ｂ感覚 = MAX(ABL:TARGET:Ｂ感覚, LIMIT(EXP:絶頂経験/50, 0, 10))
ABL:Ｖ感覚 = MAX(ABL:TARGET:Ｖ感覚, LIMIT(EXP:Ｖ経験/100, 0, 10))
ABL:Ａ感覚 = MAX(ABL:TARGET:Ａ感覚, LIMIT(EXP:Ａ経験/100, 0, 10))
ABL:精液中毒 = MAX(ABL:TARGET:精液中毒, LIMIT(EXP:精液経験/300, 0, 10))
ABL:ゲイ中毒 = MAX(ABL:TARGET:ゲイ中毒, LIMIT(EXP:ゲイ経験/300, 0, 10))
ABL:レズ中毒 = MAX(ABL:TARGET:レズ中毒, LIMIT(EXP:レズ経験/300, 0, 10))
ABL:自慰中毒 = MAX(ABL:TARGET:自慰中毒, LIMIT(EXP:自慰経験/300, 0, 10))
ABL:奉仕精神 = MAX(ABL:TARGET:奉仕精神, LIMIT(EXP:奉仕快楽経験/200, 0, 10))
ABL:性交中毒 = MAX(ABL:TARGET:性交中毒, LIMIT(EXP:性交経験/300, 0, 10))
ABL:マゾっ気 = MAX(ABL:TARGET:マゾっ気, LIMIT(EXP:苦痛快楽経験/300, 0, 10))
IF JUEL:習得 >= PALAMLV:(ABL:技巧)
	JUEL:習得 -= PALAMLV:(ABL:技巧)
	ABL:技巧++
ENDIF

;イった回数に応じてときめき増加
ときめき増加量 = SUMARRAY(EX:TARGET:0)

;立つ鳥跡を濁さず
CALL アイテム修理
CALL CVAR_RESET

@SCHOOL_COMFILTER, 場所
#DIMS DYNAMIC 場所
#DIM DYNAMIC LCOUNT

USINGFILTERG = 0

TRYCALLFORM SCHOOL_COMFILTER_%CSVNAMERPS(NO:TARGET)%, 場所

VARSET COMFILTER, 1

FOR LCOUNT, 0, VARSIZE("TRAINNAME")
	SELECTCASE TRAINNAME:LCOUNT
		;ロケーション関係なく実行可能
		{
		CASE "愛撫", "クンニ", "指挿入れ", "フェラする", "下着扱きする", "手淫する",
			 "パイズリする", "パイズリフェラする", "射精寸止め","お掃除フェラする", "アナル舐め", "アナル愛撫",
			 "胸揉み", "乳首責め", "胸愛撫", "乳首舐め", "自慰", "胸自慰", "秘貝開帳", "自慰見せつけ", "秘貝開帳見せつけ",
			 "キスする", "キスしてもらう", "情欲を煽る", "スキンシップ", "母乳飲み", "尻尾を撫でる",
			 "ふたなり化", "バストサイズ変更", "コンドーム", "ローション", "媚薬", "ローション注入", "後背位", "後背位アナル",
			 "後背位させる", "後背位アナルさせる", "手淫", "フェラチオ", "イラマチオ", "下着扱き", "素股", "パイズリ", "パイズリフェラ",
			 "縦パイズリ", "クンニさせる", "アナル愛撫させる", "アナルを舐めさせる", "兜合わせ", "貝あわせ", "シックスナイン", "69パイズリ",
			 "Gスポット刺激", "授乳手コキ", "前立腺刺激", "オナフェラ", "騎乗乳房自慰", "挿入Gスポ責め", "挿入子宮口責め", "正常位キス", "騎乗位キス",
			 "正常位アナルキス", "騎乗位アナルキス", "後背位手淫", "騎乗位手淫", "後背位アナル手淫", "騎乗位アナル手淫", "正常位キスさせる",
			 "逆レイプキス"
		}
			COMFILTER:LCOUNT = 0
	ENDSELECT

	;ベッドの有無
	IF GROUPMATCH(場所, "自宅", "ホテル", "保健室")
		SELECTCASE TRAINNAME:LCOUNT
			{
			CASE "正常位", "騎乗位", "下着ずらし挿入", "正常位アナル", "騎乗位アナル", "下着ずらし挿入アナル",
				 "正常位させる", "逆レイプ", "正常位アナルさせる", "顔面騎乗する"
			}
				COMFILTER:LCOUNT = 0
		ENDSELECT
	ENDIF
	;自宅とホテルは道具、風呂場使用可
	IF GROUPMATCH(場所, "自宅", "ホテル")
		SELECTCASE TRAINNAME:LCOUNT
			{
			CASE "ローター", "電気按摩", "バイブ", "ペニスバンド", "アナルバイブ", "乳首ローター",
				 "お風呂場プレイ", "シャワー", "腸洗浄"
			}
				COMFILTER:LCOUNT = 0
		ENDSELECT
	ENDIF
	;自宅限定
	IF GROUPMATCH(場所, "自宅")
		SELECTCASE TRAINNAME:LCOUNT
			CASE "お酒を飲ませる", "野外プレイ"
				COMFILTER:LCOUNT = 0
		ENDSELECT
	ENDIF
	;自宅限定
	IF GROUPMATCH(場所, "トイレ")
		SELECTCASE TRAINNAME:LCOUNT
			CASE "放尿"
				COMFILTER:LCOUNT = 0
		ENDSELECT
	ENDIF

	SELECTCASE 場所
		CASE "自宅", "ホテル", "トイレ", "保健室"
		CASEELSE
			;#;THROW 「%場所%」のコマンドフィルターが設定されていない
	ENDSELECT
NEXT

@学園モード用コマンドテンプレ生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC フィルター記録, 1000, 5

CALL SCHOOL_COMFILTER, "自宅"
FOR LCOUNT, 0, 1000
	フィルター記録:LCOUNT:0 = COMFILTER:LCOUNT
NEXT

CALL SCHOOL_COMFILTER, "ホテル"
FOR LCOUNT, 0, 1000
	フィルター記録:LCOUNT:1 = COMFILTER:LCOUNT
NEXT

CALL SCHOOL_COMFILTER, "トイレ"
FOR LCOUNT, 0, 1000
	フィルター記録:LCOUNT:2 = COMFILTER:LCOUNT
NEXT

CALL SCHOOL_COMFILTER, "保健室"
FOR LCOUNT, 0, 1000
	フィルター記録:LCOUNT:3 = COMFILTER:LCOUNT
NEXT

CLEARLINE LINECOUNT

FOR LCOUNT, 0, 1000
	SIF フィルター記録:LCOUNT:0 && フィルター記録:LCOUNT:1 && フィルター記録:LCOUNT:2 && フィルター記録:LCOUNT:3
		CONTINUE
	PRINTL ;-------------------------------------------------
	PRINTFORML ;%TRAINNAME:LCOUNT%
	PRINTFORML ;\@ !フィルター記録:LCOUNT:0 ? 自宅, #\@\@ !フィルター記録:LCOUNT:1 ? ホテル, #\@\@ !フィルター記録:LCOUNT:2 ? 保健室, #\@\@ !フィルター記録:LCOUNT:3 ? トイレ, #\@で実行可能
	PRINTL ;-------------------------------------------------
	PRINTFORML @SCHOOL_COM_%TRAINNAME:LCOUNT%_KX1
	PRINTL 
NEXT

OUTPUTLOG "SCHOOL_COM_KX1.ERB"

;;;主人公に名前があるか
@名前あり()
#FUNCTION
SIF CALLNAME:PLAYER == "あなた"
	RETURNF 0
RETURNF 1

@グループ_倫理観計算, デバッグ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LCOUNT3
#DIM DYNAMIC 倫理観最大値
#DIM DYNAMIC 対象
#DIM DYNAMIC 倫理観変動値
#DIM DYNAMIC デバッグ

IF デバッグ
	対象 = デバッグ
	GOTO デバッグ
ENDIF

FOR LCOUNT, 0, 10
	倫理観最大値 = 0
	FOR LCOUNT2, 0, 5
		対象 = 集団行動:LCOUNT:LCOUNT2
		SIF 対象 < 1
			CONTINUE
		倫理観最大値 = MAX(倫理観最大値, CFLAG:対象:倫理観)
	NEXT
	FOR LCOUNT2, 0, 5
		対象 = 集団行動:LCOUNT:LCOUNT2
		SIF CFLAG:対象:倫理観 >= 倫理観最大値
			CONTINUE
		$デバッグ
		倫理観変動値 = 倫理観最大値/10
		FOR LCOUNT3, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT3
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT3
				{
				CASE "衝動的", "寂しがり屋", "幼稚", "好奇心", "貞操無頓着", "解放", "恥薄い", "快感に素直",
					 "即落ち", "濡れやすい", "勃ちやすい", "自慰しやすい", "倒錯的", "男好き", "女好き", "両刀",
				}
					倫理観変動値++
				{
				CASE "冷静", "自制心", "無関心", "保守的", "一線越えない", "貞操観念", "恥じらい",
					 "快感の否定", "濡れにくい", "勃ちにくい", "男嫌い", "女嫌い", "同族嫌悪", 
				}
					倫理観変動値--
			ENDSELECT
		NEXT
		SIF デバッグ
			RETURN 倫理観変動値
		SIF 倫理観変動値 <= 0
			CONTINUE
		CFLAG:対象:倫理観 = MIN(CFLAG:対象:倫理観+倫理観変動値, 倫理観最大値)
	NEXT
NEXT
