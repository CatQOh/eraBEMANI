@学園モード開始
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 学園乱数固定用, 100
#DIM DYNAMIC 入学先 = -1
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 未実装キャラ
#DIM DYNAMIC 総数
#DIMS DYNAMIC SPLITVAR, 2

通貨単位 = 円

ADDCHARA 0
MASTER = 0
SIF ゲームモード() == "学園モード"
	CALL PLAYER_CUSTOM

$クラス分け
PRINTL クラス分け処理中...
AWAIT
SKIPDISP 1
FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	SIF NOF(MASTER) == LCOUNT
		CONTINUE
	CALL CHARA_ADD, LCOUNT
NEXT
SKIPDISP 0

;まず全キャラの順番をシャッフルする
FOR LCOUNT, 1, CHARANUM-1
	SWAPCHARA LCOUNT, RAND(LCOUNT+1, CHARANUM)
NEXT

;学校区分
;BEMANI学園 小,中,高
;BEMANI大学
;ボルテ学園 小,中,高
;華練学園中等科
;天神学園(高校)
;プリンセスリパブリック音楽学院 中等部,高等部
;マジックアカデミー 
;さくら野高校
;舞ヶ原高校

;マジックアカデミー以外で固定の教師陣

CSTR:GETCHARANAME("プロフェッサー識苑"):在任 = ボルテ学園高等部
CSTR:GETCHARANAME("プロフェッサー識苑"):担当科目 = 技術

CSTR:GETCHARANAME("シャトー・ロワーレ"):在任 = ボルテ学園高等部
CSTR:GETCHARANAME("シャトー・ロワーレ"):担当科目 = 音楽

FOR LCOUNT, 1, CHARANUM
	SELECTCASE NAME:LCOUNT
		;学校固定の人たち
		;華練学園の3人
		CASE "菱宮 津軽", "刃雪 達磨", "シア"
			CSTR:LCOUNT:在校 = 華練学園中等科
		;ボルテ学園高等部
		{
		CASE "EΛΠIΣ‡煌‡", "レイシス", "嬬武器 雷刀", "嬬武器 烈風刀", "紅刃", "昴希", "椿", "福龍", "赤志 魂", "青雨 冷音", "MAD†HOLIC",
			 "グレイス", "班田 みみこ", "不律 灯色", "京終 始果"
		}
			CSTR:LCOUNT:在校 = ボルテ学園高等部
		;中等部
		{
		CASE "虹霓・シエル・奈奈", "野増菜 かなで", "樹野 あんず", "ＴＡＭＡちゃん", "粒焼 りむる", "恋刃", "傍丹 桜子", "BisCo", "Mei", "ToRa",
			 "冥道 ユウキ", "静かなる嵐のイノテンちゃん", "天月 エクサ", "オンディーヌ・シフィカ・チハ"
		}
			CSTR:LCOUNT:在校 = ボルテ学園中等部
		;初等部
		{
		CASE "野増菜 しずく", "ヴィクトリヤ", "蒼", "雛", "桃", "リボン", "ニア", "ノア", "カヲル", "アシタ", "氷雪ちゃん", "雪翔くん",
			 "ドゥ・サン・コ・ピリカ"
		}
			CSTR:LCOUNT:在校 = ボルテ学園初等部
		;イロドリミドリ
		{
		CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる", "月鈴 那知", "月鈴 白奈", "五十嵐 撫子", "萩原 七々瀬", 
			 "葛城 華", "小野 美苗", "芒崎 奏", "藤堂 陽南袴"
		}
			CSTR:LCOUNT:在校 = 舞ヶ原高校
		;ときドル高等部
		{
		CASE "結城 秋葉", "月島 美奈都", "田中 フランチェスカ", "川口 夏海", "青山 つばさ", "真田 小幸村", "片桐 奈々菜", "日比野 記子",
			 "草壁 野々香", "三田 希少", "朝霧 春子", "桔梗 小夜曲"
		}
			CSTR:LCOUNT:在校 = プリンセスリパブリック音楽学院高等部
		;ときドル中等部
		CASE "立川 美翠", "立川 朱音", "伊澄 いずみ", "日毬 みさき"
			CSTR:LCOUNT:在校 = プリンセスリパブリック音楽学院中等部
		;マジックアカデミーは中高一貫
		{
		CASE "ルキア", "シャロン", "クララ", "アロエ", "マラリヤ", "ユリ", "ヤンヤン", "リエル", "アイコ", "メディア", "ミュー", "マヤ",
			 "レオン", "セリオス", "カイル", "ラスク", "サンダース", "タイガ", "ユウ", "ハルト", "リック"
		}
			 CSTR:LCOUNT:在校 = マジックアカデミー
		;ひなビタキャラは天神学園
		CASE "山形 まり花", "和泉 一舞", "春日 咲子", "霜月 凛"
			CSTR:LCOUNT:在校 = 天神学園
		;めうのみ中・高がランダム
		CASE "芽兎 めう"
			IF RAND:2 == 0
				CSTR:LCOUNT:在校 = 天神学園
			ELSE
				CSTR:LCOUNT:在校 = 華練学園中等科
			ENDIF
		;ここなつ
		CASE "東雲 夏陽", "東雲 心菜"
			CSTR:LCOUNT:在校 = さくら野高校
		;奏坂高校
		{
		CASE "星咲 あかり", "藤沢 柚子", "三角 葵", "高瀬 梨緒", "結城 莉玖", "藍原 椿", "桜井 春菜", 
			 "早乙女 彩華", "井之原 小星", "柏木 咲姫", "九條 楓", "逢坂 茜", "珠洲島 有栖"
		}
			CSTR:LCOUNT:在校 = 奏坂高校
		;奏坂学園（中学）
		CASE "柏木 美亜", "日向 千夏", "東雲 つむぎ"
			CSTR:LCOUNT:在校 = 奏坂学園
		;さすがに一宮トリオはBEMANI学園でしょう
		CASE "いちか", "のの", "みや"
			CSTR:LCOUNT:在校 = BEMANI学園

		;固定ではないがグループ単位で同じ学校の人たち
		;高校生たち
		CASE "雛月 理々奈", "梅桐 彩葉", "神崎 慧靂", "鉄火"
			SIF 学園乱数固定用:0 == ""
				学園乱数固定用:0 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:0%
		;この3人は同じ
		CASE "ひなた", "アイ", "まゆ"
			SIF 学園乱数固定用:1 == ""
				学園乱数固定用:1 = %学園ランダム("小")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:1%
		;モドリリ
		CASE "モード", "リリーゼ"
			SIF 学園乱数固定用:2 == ""
				学園乱数固定用:2 = %学園ランダム("小")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:2%
		;トランプの子
		CASE "ラジェ", "ガレト", "ヴァロ", "タルタ"
			SIF 学園乱数固定用:3 == ""
				学園乱数固定用:3 = %学園ランダム("小")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:3%
		;ルピレピほうちゃんは同じ
		CASE "青のルピカ", "青のレピカ", "鳳凰 鈴火"
			SIF 学園乱数固定用:4 == ""
				学園乱数固定用:4 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:4%
		;ラピス組は全員同じ ただしときドル学園以外
		{
		CASE "ミミ", "ニャミ", "タイマー", "スミレ", "ハヤト", "翔", "ルート＠超ドー研", "風雅", "烈", "氷海", "鈴花", "ニア(pop'n)", "タクト",
			 "美結", "翠里", "トア", "大牙", "塵"
		}
			IF 学園乱数固定用:5 == ""
				DO
					学園乱数固定用:5 = %学園ランダム("高")%
					SIF 学園乱数固定用:5 != "プリンセスリパブリック音楽学院高等部"
						BREAK
				LOOP 1
			ENDIF
			CSTR:LCOUNT:在校 = %学園乱数固定用:5%
		;ポップンの繋がりがあると思われる女子高生組
		CASE "リゼット", "歩", "テンコ", "うた"
			SIF 学園乱数固定用:6 == ""
				学園乱数固定用:6 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:6%
		;にじっこ
		CASE "虹野 ひかり", "夢野 みらい", "星野 そら"
			SIF 学園乱数固定用:7 == ""
				学園乱数固定用:7 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:7%
		;からふるぱすてる
		CASE "心音", "陽夏", "涼"
			SIF 学園乱数固定用:8 == ""
				学園乱数固定用:8 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:8%
		;ヨルド一行
		CASE "ヨルド", "ミームリウス", "アサギス・ニニギニル", "シャラ"
			SIF 学園乱数固定用:9 == ""
				学園乱数固定用:9 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:9%
		;ナズナとスズナ
		CASE "ナズナ", "スズナ"
			SIF 学園乱数固定用:10 == ""
				学園乱数固定用:10 = %学園ランダム("小")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:10%
		;SUPER MOMO TAROSANS
		CASE "キビ", "ワン"
			SIF 学園乱数固定用:11 == ""
				学園乱数固定用:11 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:11%
		;BBB
		CASE "栗花落 夜風", "吉廻 千代"
			SIF 学園乱数固定用:12 == ""
				学園乱数固定用:12 = %学園ランダム("高")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:12%
		CASE "黒川 亜理紗", "百武 もなか"
			SIF 学園乱数固定用:13 == ""
				学園乱数固定用:13 = %学園ランダム("小")%
			CSTR:LCOUNT:在校 = %学園乱数固定用:13%
		;以下大学＆ランダム入学
		{
		CASE "水城 セリカ", "北見 エリカ", "神崎 紗矢", "理子", "ハーティ", "夜兎姫", "夜々祇", "トヨタマヒメ", "スタンパ", "アヤ", "ヒュー", "愛子", "レナ", "彗星ローラ",
			 "エリカ", "あやめ", "ノネット", "グリム", "エミ", "響香", "しゃま", "星見 日向", "ファロ", "マルファータ", "ベルフォート", "ローズマリー", "レイジ", 
			 "ハーティー", "ノーティガール", "結", "千代", "ミサキ", "マチコ", "伊賀崎ノ 楠子", "黒原 きずな"
		}
			CSTR:LCOUNT:在校 = BEMANI大学
		;小中高ランダム
		;小○校
		{
		CASE "ルルスス", "赤シグ", "黄シグ", "青シグ", "赤のリピカ", "スノーフェアリー", "アムドゥシアン", "リプンテ", "クリープ", "テトラ",
			 "ロキ", "おんなのこ", "チップ", "ロザリー", "サン", "白鳥 姫子", "たぬちよ", "アリシア", "フローラ", "ホイップ",
			 "てまり", "ゼルハルト", "アミュレット", "ユビ子", "ルエリシア", "フリオ", "ローズ(GITADORA)", "ソルト", "みるく", "シルビア", "ケリマロ", 
			 "オオカミ少年", "ミア", "らいむ", "知雲 ひばり"
		}
			CSTR:LCOUNT:在校 = %学園ランダム("小")%
		;中学
		{
		CASE "ヒメル", "タルト", "タフィ", "ノクス・カトルセ", "マタン・カトルセ", "おはな", "ルシェ", "キュベルト", "黄のラピカ", "Aegis",
			 "スモモ", "クロノス", "牡丹", "橙之 針子", "ジャックハンス", "ジャックティーロ", "ギガデリック：オズワルド", "アーミィ", "エグゼ",
			 "Ⅸ籠", "おいわちゃん", "うらしまたろ子", "ミニッツ", "ポエット", "ロビン", "つらら", "セシル", "ジャック", "ベンベン", "ホワイトメリー",
			 "鹿ノ子", "リアン", "ター子", "ライラ", "しおん", "寿々姫", "マルクト", "ピュアクルリップ", "雷蔵", "＊うさぬこ＊", "ポチコ", "ちぇるみん",
			 "スペース＠マコ", "リデル", "なぎさ", "リノン", "リノン(黒)", "かぐや", "トルテ", "あい", "サニー", "セイナ", "ローズ", "リリカ", "ココ",
			 "アバリータ", "エンビー", "アシディア", "プリッド", "グリコ", "ラクリア", "ウエステ", "ちい", "ナルル", "ティア", "ライア", "フィクル",
			 "アイディ", "ルシ子", "イラ", "キャンディッド", "エス", "あいの影", "乙姫", "ラズ", "シフォン", "トラン", "寿々姫", "タマヨ", "シャルロット", 
			 "ニコラシカ", "アイオーネ", "サリュイス", "チャオ", "はまゆきちゃん", "サユリ", "アンナ", "みっちゃん", "クララ(pop'n)", "スカイフェザー", "観音寺 にこる",
			 "イングリッド・オーリック・コーネル", "ソラマチ・メメ", "本宮 梨々花", "リリィ"
		}
			CSTR:LCOUNT:在校 = %学園ランダム("中")%
		;高校
		{
		CASE "キラー", "リヒト", "オロロージョ", "アンネース", "おきつ", "おゆき", "駄天使ちゃん", "ヴァルキリー", "サキュバスちゃん",
			 "ニンフちゃん", "ニーソ姫", "エレクトロ：チューンド", "パール", "マーメイドガール", "比多木 小瑠璃", "カイロス", "ことみちゃん",
			 "怪盗いいんちょ", "ミラベル", "あいみ", "ものか", "エクス", "ねむ", "ゼロワン：バンディング", "鎖", "刺斬", "グラビティ：ガルフエッジ",
			 "ホリック：ランナー", "火々矢", "兎々呂鬼", "風々奇", "犬見川 ユウ", "九鬼衣 シエ", "ジュリア", "カーリー", "エス・エイト・タロウ",
			 "おコン", "リサ", "かごめ", "睦月", "シャラ(pop'n)", "ウーノ", "ツースト", "若さん", "フォース", "Bis子", "アリス(pop'n)",
			 "ラッテ", "ロッテ", "ナカジ", "花緒", "みここ", "蒼井 硝子", "エッダ", "カケルくん", "アン", "ンビリ", "ちなつ", "紅", "ニット", "まりん",
			 "みほ", "ブラック", "オーリー", "アリサ", "えりりん", "メリッサ", "神楽", "ちせ", "ユニ", "アリス・イップ", "璃音", "イリル", "フローレス",
			 "ミカ子", "ヨミ", "アメ", "キャスメラ", "405nm", "あいむ", "オフィーリア", "フェリーラ", "ヨシオ", "イナリ", "ニコ", "アリス・ルナン",
			"アイラ", "シリーちゃん", "阿式 眞白", "キララ", "ルル", "カジカ", "ニッキー", "レオン(pop'n)", "荒場 流子", "舞園 星斗", "愛野 ヒカリ", "美嶋 はるな", 
			"鬼蝮 ユリア", "葉和 とれび", "八咫烏 鋼太郎", "津久井 シズノ", "マゼラン・マゼラン", "渋沢 ノノ", "暁 凶香", "常世のサツキ", "高須 らいむ",
			"幸野 うさぎ", "新田 ちえ", "リタ・カールステット", "エリザベス"
		}
			CSTR:LCOUNT:在校 = %学園ランダム("高")%
		;グリムキャラはマジックアカデミー以外のどこか
		{
		CASE "グリム・ルキア", "グリム・シャロン", "グリム・クララ", "グリム・マラリヤ", "グリム・ユリ", "グリム・ヤンヤン", "グリム・リエル",
			 "グリム・アイコ", "グリム・メディア", "グリム・マヤ", "グリム・レオン", "グリム・セリオス", "グリム・カイル", "グリム・サンダース",
			 "グリム・タイガ", "グリム・ハルト", "グリム・リック"
		}
			CSTR:LCOUNT:在校 = %学園ランダム("高")%
		CASE "グリム・アロエ", "グリム・ミュー", "グリム・ラスク", "グリム・ユウ"
			CSTR:LCOUNT:在校 = %学園ランダム("小")%
		
		;ここから教師陣の選任
		;国語
		CASE "A", "ユーリ", "カゲトラ", "浅見 文彦", "桔梗", "ムラサキ", "玉藻 銀丸", "イマ"
			CSTR:LCOUNT:在任 = %選任ランダム("国語")%
			CSTR:LCOUNT:担当科目 = 国語
		;数学
		CASE "マモニス", "ガユカ", "Σ", "カネノブ", "スミス氏", "コンセント", "ガス", "サトウサン", "まどか", "サーミリアス"
			CSTR:LCOUNT:在任 = %選任ランダム("数学")%
			CSTR:LCOUNT:担当科目 = 数学
		;英語
		CASE "サイレン", "六", "HOT.D", "アルフォンス・ミシェル", "エイプリル", "アイリーン", "アフロ", "モモコさん"
			CSTR:LCOUNT:在任 = %選任ランダム("英語")%
			CSTR:LCOUNT:担当科目 = 英語
		;科学
		CASE "ファウラ", "パラド", "エレノア", "マサムネ", "アル・ハイア", "ガッキー博士", "正論", "ゼーグ・ルルード", "スマイル", "Dr.φ", "メリム"
			CSTR:LCOUNT:在任 = %選任ランダム("科学")%
			CSTR:LCOUNT:担当科目 = 科学
		;社会
		CASE "サタヌ", "ボルテージ：マインツ", "卑弥呼", "獅子若", "エミーリオ", "ニョア", "リリアンキル", "クニオシ", "N・ナーガ"
			CSTR:LCOUNT:在任 = %選任ランダム("社会")%
			CSTR:LCOUNT:担当科目 = 社会
		;音楽
		CASE "菱宮 春一", "ショルキー", "ファティマ", "パンデス", "スモーク", "獅子次", "山吹", "ハーピア", "ハーティ", "熊井さん", "ザ・キング", "イワン"
			CSTR:LCOUNT:在任 = %選任ランダム("音楽")%
			CSTR:LCOUNT:担当科目 = 音楽
		;体育
		CASE "シャムシール", "オルトリンデ", "ハジメ", "ネロ", "ヴォルフガング", "金剛寺 亮", "アドフーク", "ミカレイ・バーン", "ライアン", "クアトロ"
			CSTR:LCOUNT:在任 = %選任ランダム("体育")%
			CSTR:LCOUNT:担当科目 = 体育
		;美術
		CASE "孔雀", "雛月 聖奈", "英利", "シェリー", "チャコル", "パステルくん"
			CSTR:LCOUNT:在任 = %選任ランダム("美術")%
			CSTR:LCOUNT:担当科目 = 美術
		;保健
		CASE "アーシェム", "ミルク", "ヴァイス", "揚羽", "ゼロ", "エチダナ", "ハニー", "キャロル", "ミク", "サンパウロ容子"
			CSTR:LCOUNT:在任 = %選任ランダム("保健")%
			CSTR:LCOUNT:担当科目 = 保健
		;家庭科
		CASE "ベリゼベス", "トリフォリ", "アッシュ", "ジズ", "アルビレオ", "茜", "弦", "育江", "デボラ"
			CSTR:LCOUNT:在任 = %選任ランダム("家庭科")%
			CSTR:LCOUNT:担当科目 = 家庭科
		;校長
		CASE "梅桐 天土", "神崎 弦士", "キュベリア", "白のロピカ", "MZD", "ギジリ", "エクレメス", "一条司令", "ヴィルヘルム"
			CSTR:LCOUNT:在任 = %選任ランダム("校長")%
			CSTR:LCOUNT:担当科目 = 校長
		;マジックアカデミーは固定
		CASE "サツキ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 科学
		CASE "マロン"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 美術
		CASE "リディア"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 家庭科
		CASE "エリーザ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 社会
		CASE "アメリア"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 国語
		CASE "ミランダ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 保健
		CASE "ガルーダ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 体育
		CASE "ウィーズ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 数学
		CASE "フランシス"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 音楽
		CASE "ロマノフ"
			CSTR:LCOUNT:在任 = マジックアカデミー
			CSTR:LCOUNT:担当科目 = 校長
	ENDSELECT
NEXT

;キャラ番号順にソート
SORTCHARA

CALL FADEOUTBGM

FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:担当科目 == "数学" && 小中高(CSTR:LCOUNT:在任) == "小"
		CSTR:LCOUNT:担当科目 = 算数
	ELSEIF CSTR:LCOUNT:担当科目 == "科学" && 小中高(CSTR:LCOUNT:在任) == "小"
		CSTR:LCOUNT:担当科目 = 理科
	ENDIF
NEXT

$学校一覧表示
DRAWLINE
FOR LCOUNT, 0, VARSIZE("学校名")
	SIF 学校名:LCOUNT == ""
		CONTINUE
	SIF 小中高(学校名:LCOUNT) == "大"
		CONTINUE
	SIF 入学先 == LCOUNT && ゲームモード() == "学園モード"
		SETCOLORBYNAME deepskyblue
	PRINTFORM 　[{LCOUNT}] %学校名:LCOUNT%(生徒:{CMATCH(CSTR:在校, 学校名:LCOUNT)}人 教師:{CMATCH(CSTR:在任, 学校名:LCOUNT)}人) 
	SIF 入学先 == LCOUNT && ゲームモード() == "学園モード"
		PRINTFORM %NAME:MASTER%の入学先
	PRINTL 
	RESETCOLOR
NEXT
PRINTL
PRINTL 　[99] 学園モード未実装のキャラ一覧
DRAWLINE
PRINTPLAIN 　学校一覧　
PRINTL [100] - 決定 [101] - やり直し

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("学校名")-1
			DRAWLINE
			PRINTFORML □%学校名:RESULT%の在校生
			FOR LCOUNT, 0, CHARANUM
				SIF CSTR:LCOUNT:在校 != 学校名:RESULT
					CONTINUE
				PRINTFORML 　No.{NOF(LCOUNT)} %NAME:LCOUNT%
			NEXT
			PRINTFORML □%学校名:RESULT%の教師
			FOR LCOUNT, 0, CHARANUM
				SIF CSTR:LCOUNT:在任 != 学校名:RESULT
					CONTINUE
				PRINTFORML 　担当科目:%CSTR:LCOUNT:担当科目% No.{NOF(LCOUNT)} %NAME:LCOUNT%
			NEXT
			DRAWLINE
			PRINTPLAINFORM 　%学校名:RESULT%　
			PRINTFORML [0] - 戻る  \@ ゲームモード() == "学園モード" ? [1] - ここに入学する #\@

			LRESULT = RESULT

			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						BREAK
					CASE 1
						IF 小中高(学校名:LRESULT) == "小" && !TALENT:PLAYER:小柄体型
							PRINTL 小学校に入学するには素質「小柄体型」が必要です
							PRINTL [0] - 仕方ないな
							PRINTL [1] - 小柄になってやんよ
							BINPUT
							SIF !RESULT
								BREAK
							TALENT:PLAYER:小柄体型 = 1
						ENDIF
						入学先 = LRESULT
						BREAK
				ENDSELECT
			LOOP 1
			GOTO 学校一覧表示
		CASE 99
			DRAWLINE
			PRINTL □学園モード未実装キャラ
			未実装キャラ = 0
			FOR LCOUNT, 0, CHARANUM
				IF CSTR:LCOUNT:在校 == "" && CSTR:LCOUNT:在任 == ""
					PRINTFORML 　No.{NOF(LCOUNT)} %NAME:LCOUNT%
					未実装キャラ++
				ENDIF
			NEXT
			PRINTL 
			PRINTFORML 　計{未実装キャラ}人
			WAIT
			GOTO 学校一覧表示
		CASE 100
			SIF ゲームモード() == "SLGモード"
				BREAK
			IF 入学先 == -1
				PRINTW 入学先を選んでください
				GOTO 学校一覧表示
			ENDIF
			CSTR:MASTER:在校 = %学校名:入学先%
			BREAK
		CASE 101
			FOR LCOUNT, CHARANUM-1, 0, -1
				DELCHARA LCOUNT
			NEXT
			VARSET 学園乱数固定用
			入学先 = -1
			GOTO クラス分け
	ENDSELECT
LOOP 1

CALL SETUP_GAMESTART

ターン = 朝
CSTR:PLAYER:現在地 = 自宅
MONEY = 0

ENUMFUNCBEGINSWITH "ロケーション条件_"
総数 = RESULT
FOR LCOUNT, 0, 総数
	SPLIT RESULTS:LCOUNT, "_", SPLITVAR
	CALLFORM %RESULTS:LCOUNT%
	SIF RESULT
		解禁済みロケーション:FINDELEMENTF(解禁済みロケーション, "") = %SPLITVAR:1%
NEXT

CALL SYSTEM_AUTOSAVE

CALL SHOP_SCHOOL

@学園ランダム, ARGS
#FUNCTIONS
#DIMS DYNAMIC 候補
#DIM DYNAMIC 人数記録用, 10

SELECTCASE ARGS
	CASE "小"
		STRDATA 候補
			DATA BEMANI学園初等部
			DATA ボルテ学園初等部
		ENDDATA
		人数記録用:0 = CMATCH(CSTR:在校, "BEMANI学園初等部")
		人数記録用:1 = CMATCH(CSTR:在校, "ボルテ学園初等部")
		;最低人数の学校と3人以上差がついてたら再抽選する
		SIF CMATCH(CSTR:在校, 候補)-MINARRAY(人数記録用, 0, 2)
			RESTART
		RETURNF 候補
	CASE "中"
		STRDATA 候補
			DATA BEMANI学園中等部
			DATA ボルテ学園中等部
			DATA 華練学園中等科
			DATA マジックアカデミー
			DATA プリンセスリパブリック音楽学院中等部
			DATA 奏坂学園
		ENDDATA
		人数記録用:0 = CMATCH(CSTR:在校, "BEMANI学園中等部")
		人数記録用:1 = CMATCH(CSTR:在校, "ボルテ学園中等部")
		人数記録用:2 = CMATCH(CSTR:在校, "華練学園中等科")
		人数記録用:3 = CMATCH(CSTR:在校, "マジックアカデミー")
		人数記録用:4 = CMATCH(CSTR:在校, "プリンセスリパブリック音楽学院中等部")
		人数記録用:5 = CMATCH(CSTR:在校, "奏坂学園")
		;最低人数の学校と3人以上差がついてたら再抽選する
		SIF CMATCH(CSTR:在校, 候補)-MINARRAY(人数記録用, 0, 6)
			RESTART
		RETURNF 候補
	CASE "高"
		STRDATA 候補
			DATA BEMANI学園高等部
			DATA ボルテ学園高等部
			DATA 天神学園
			DATA プリンセスリパブリック音楽学院高等部
			DATA マジックアカデミー
			DATA さくら野高校
			DATA 舞ヶ原高校
			DATA 奏坂高校
		ENDDATA
		人数記録用:0 = CMATCH(CSTR:在校, "BEMANI学園高等部")
		人数記録用:1 = CMATCH(CSTR:在校, "ボルテ学園高等部")
		人数記録用:2 = CMATCH(CSTR:在校, "天神学園")
		人数記録用:3 = CMATCH(CSTR:在校, "プリンセスリパブリック音楽学院高等部")
		人数記録用:4 = CMATCH(CSTR:在校, "マジックアカデミー")
		人数記録用:5 = CMATCH(CSTR:在校, "さくら野高校")
		人数記録用:6 = CMATCH(CSTR:在校, "舞ヶ原高校")
		人数記録用:7 = CMATCH(CSTR:在校, "奏坂高校")
		;最低人数の学校と3人以上差がついてたら再抽選する
		SIF CMATCH(CSTR:在校, 候補)-MINARRAY(人数記録用, 0, 8)
			RESTART
		RETURNF 候補
ENDSELECT

@選任ランダム, ARGS
#FUNCTIONS
#DIMS DYNAMIC 赴任先
#DIM DYNAMIC LCOUNT

DO
	赴任先 = %学校名:(RAND:VARSIZE("学校名"))%
	SIF 赴任先 == ""
		CONTINUE
	;マジックアカデミーは固定で付いてるので赴任しない
	SIF 赴任先 == "マジックアカデミー"
		CONTINUE
	;大学には赴任しない
	SIF 小中高(赴任先) == "大"
		CONTINUE
	;赴任先に担当科目の教師が居なければ赴任
	FOR LCOUNT, 0, CHARANUM
		SIF 赴任先 == CSTR:LCOUNT:在任 && CSTR:LCOUNT:担当科目 == ARGS
			RESTART
	NEXT
	BREAK
LOOP 1

RETURNF 赴任先

@小中高, ARGS
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIM 小
#DIM 中
#DIM 高

IF !小
	FOR LCOUNT, 0, VARSIZE("学校名")
		IF 学校名:LCOUNT == ""
			IF !小
				小 = LCOUNT
			ELSEIF !中
				中 = LCOUNT
			ELSE
				高 = LCOUNT
				BREAK
			ENDIF
		ENDIF
	NEXT
ENDIF

IF INRANGE(FINDELEMENT(学校名, ARGS, 0, VARSIZE("学校名"), 1), 0, 小)
	RETURNF "小"
ELSEIF INRANGE(FINDELEMENT(学校名, ARGS, 0, VARSIZE("学校名"), 1), 小, 中)
	RETURNF "中"
ELSEIF INRANGE(FINDELEMENT(学校名, ARGS, 0, VARSIZE("学校名"), 1), 中, 高)
	RETURNF "高"
ELSE
	RETURNF "大"
ENDIF
