@STATUS, ロストキャラ表示
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 相性表示用
#DIM DYNAMIC 表示数
#DIM DYNAMIC 前のキャラ = -1
#DIM DYNAMIC 次のキャラ = -1
#DIM DYNAMIC ロストキャラ表示
#DIM DYNAMIC 相性補正後
#DIM DYNAMIC 種族嫌悪例外処理
#DIM マニュアル表示
DRAWLINE

CALL 基本情報

IF TARGET != MASTER
	DRAWLINE
	PRINTFORML □ %I18N("珠")% 
	CALL SHOW_INFO_JUEL, TARGET
ENDIF
CALL SHOW_INFO, TARGET

PRINTPLAINFORM □ %I18N("相性")% 
PRINTFORML [100] \@ OPTION:相性非表示 ? 開く # 閉じる \@
IF !OPTION:相性非表示
	SIF TALENT:服従
		PRINTFORML 　%I18N("全キャラ")%×120\% 
	SIF TALENT:隷属
		PRINTFORML 　%I18N("全キャラ")%×150\% 

	SIF TALENT:犬嫌い
		PRINTFORML 　%I18N("素質:|0|との相性", , I18N("犬"))%-30\%
	SIF TALENT:猫嫌い
		PRINTFORML 　%I18N("素質:|0|との相性", , I18N("猫"))%-30\%
	SIF CSTR:種族嫌悪 != ""
		PRINTFORML 　種族:%CSTR:種族嫌悪%との相性-30\%

	表示数 = 0
	FOR LCOUNT, 0, 20000
		;CSVがあるか、子供の場合に参照
		IF EXISTCSV(LCOUNT) || LCOUNT > 10000
			;子供・クローンの人数を突っ切ったら終了
			SIF LCOUNT > 10000 && CHILDNAME:(LCOUNT-10000) == ""
				BREAK
			;子供・クローンが何らかの理由で消えてたら相性も消す
			[SKIPSTART]
			IF LCOUNT > 10000 && !GETCHARA(LCOUNT)
				RELATION:LCOUNT = 100
				CONTINUE
			ENDIF
			[SKIPEND]
			相性表示用 = 
			相性補正後 = 0
			IF RELATION:LCOUNT || TALENT:犬嫌い || TALENT:猫嫌い || CSTR:種族嫌悪 != ""
				IF TALENT:隷属
					相性補正後 = 150
				ELSEIF TALENT:服従
					相性補正後 = 120
				ELSE
					相性補正後 = RELATION:LCOUNT == 0 ? 100 # RELATION:LCOUNT
				ENDIF
				IF TALENT:犬嫌い
					IF GETCHARA(LCOUNT) >= 0 && TALENT:GETCHARA(LCOUNT):犬 && !種族嫌悪例外処理(CHARANAMEF(TARGET), CHARANAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
					ELSEIF EXISTCSV(LCOUNT) && CSVTALENT(LCOUNT, GETNUM(TALENT, "犬")) && !種族嫌悪例外処理(CHARANAMEF(TARGET), CSVNAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
					ENDIF
				ENDIF
				IF TALENT:猫嫌い
					IF GETCHARA(LCOUNT) >= 0 && TALENT:GETCHARA(LCOUNT):猫 && !種族嫌悪例外処理(CHARANAMEF(TARGET), CHARANAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
					ELSEIF EXISTCSV(LCOUNT) && CSVTALENT(LCOUNT, GETNUM(TALENT, "猫")) && !種族嫌悪例外処理(CHARANAMEF(TARGET), CSVNAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
					ENDIF
				ENDIF
				IF CSTR:種族嫌悪 != ""
					IF GETCHARA(LCOUNT) >= 0 && ISHATERACE(TARGET, GETCHARA(LCOUNT)) && !種族嫌悪例外処理(CHARANAMEF(TARGET), CHARANAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
						SIF ORIGIN && CSTR:GETCHARA(LCOUNT):種族 == "人間" && 相性補正後 == 70
							CONTINUE
					ELSEIF EXISTCSV(LCOUNT) && 特定種族嫌悪(TARGET, CSVCSTR(LCOUNT, GETNUM(CSTR, "種族"))) && !種族嫌悪例外処理(CHARANAMEF(TARGET), CSVNAMEF(LCOUNT))
						相性補正後 = MAX(相性補正後-30, 1)
						;人間嫌い70%を全部表示するとえらいこっちゃなので除外
						SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "種族")) == "人間" && 相性補正後 == 70
							CONTINUE
					ENDIF
				ENDIF
				SIF (相性補正後 == 100) || (TALENT:服従 && 相性補正後 == 120) || (TALENT:隷属 && 相性補正後 == 150)
					CONTINUE
				IF 相性補正後 > 100
					SETCOLORBYNAME coral
				ELSE
					SETCOLORBYNAME lightskyblue
				ENDIF
				IF NOF(MASTER) == LCOUNT
					相性表示用 = 　%NAMEDISP(MASTER)%×{相性補正後}\%
				ELSE
					相性表示用 = 　%CSVNAMEDISP(LCOUNT)%×{相性補正後}\%
				ENDIF
			ENDIF
			IF 相性表示用 != ""
				PRINTFORMLC %相性表示用%
				表示数++
			ENDIF
			RESETCOLOR
			IF 表示数 == 4
				CALL NEWLINE
				表示数 = 0
			ENDIF
		ENDIF
	NEXT
ENDIF
CALL NEWLINE

DRAWLINE
IF ORIGIN || マニュアル表示
	PRINTFORML □%I18N("キャラマニュアル")%
	FOR LCOUNT, 0, 100
		SIF CSTR:LCOUNT == ""
			BREAK
		CALL MANUALPRINT, @"　%CSTR:LCOUNT%"
		CALL NEWLINE
	NEXT
	PRINTFORML 　登場作品：「%CSTR:登場作品%」\@ CSTR:登場作品２ != "" ? 「%CSTR:登場作品２%」 #\@\@ CSTR:登場作品３ != "" ? 「%CSTR:登場作品３%」 #\@
ENDIF
IF CFLAG:キャラロスト
	CALL CHECKDRAWLINE
	PRINTFORML □%I18N("キャラロストの原因")%
	CALL PASELIL, @"　%CSTR:ロスト詳細%"
ENDIF


CALL CHECKDRAWLINE
PRINTPLAIN 　BASIC　

FOR LCOUNT, TARGET+1, CHARANUM
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, TARGET-1, -1, -1
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT

PRINTFORM [0] %I18N("戻る")% 
SIF MOD:バスト関連ステータス表示 || TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
	PRINTFORM [4] %I18N("性経験履歴")% 
PRINTFORM [5] %I18N("その他情報")% 
CALL PRINTSPACE, 前のキャラ >= 0, @"[6] %I18N("前のキャラ")% "
CALL PRINTSPACE, 次のキャラ >= 0, @"[7] %I18N("次のキャラ")% "
;PRINTPLAINFORM %SPACESLENS("　BASIC　")%

{
IF !ロストキャラ表示 && (調教可能(TARGET) || 助手可能(TARGET))
 && (!SHOP_配属キャラ選択不可 || (SHOP_配属キャラ選択不可 && CSTR:配属 == ""))
 && !CHECK_CHILD_CARE(TARGET) && !CFLAG:使用不可 && !CFLAG:監禁
}
	CALL PRINTSPACE, SHOW_TARGET != TARGET && 調教可能(TARGET), @"[8] %I18N("対象に指定")% "
	CALL PRINTSPACE, NOSAMES(TARGET, MASTER, ASSI) && 助手可能(TARGET), @"[9] %I18N("助手に指定")% "
	PRINTFORM [10] %I18N("アイテム使用")%
ENDIF
PRINTL 
PRINTPLAINFORM %SPACESLENS("　BASIC　")%

IF !ロストキャラ表示
	IF ISSLIME(TARGET)
		PRINTFORM [11] %I18N("体色変更"), 10, LEFT% 
	ELSE
		PRINTFORM [11] %I18N("タグ色変更"), 10, LEFT% 
	ENDIF
ENDIF

PRINTFORM [12] %I18N("キャラマニュアル表示/非表示")% 

SIF MOD:バスト関連ステータス表示
	PRINTFORM [97] %I18N("バストサイズをメモする")% 
PRINTFORM [98] %I18N("メモを書く")%
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 4
		JUMP STATUS_SEX_EXP
	CASE 5
		;以下フラグリセットのためにJUMPで処理
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 6
		TARGET = 前のキャラ
		JUMP STATUS, ロストキャラ表示
	CASE 7
		TARGET = 次のキャラ
		JUMP STATUS, ロストキャラ表示
	CASE 8
		CALL ASSIGN_TARGET, TARGET
		RETURN 1
	CASE 9
		CALL ASSIGN_ASSI, TARGET
		RETURN 0
	CASE 10
		CALL アイテム使用_逆引き, TARGET
		JUMP STATUS, ロストキャラ表示
	CASE 11
		IF ISSLIME(TARGET)
			PRINTFORML □%CALLNAME:TARGET%の体色を設定してください
		ELSE
			PRINTFORML □%CALLNAME:TARGET%のタグ色を設定してください
		ENDIF
		VARLINE = LINECOUNT
		CALL RGBBAR, CFLAG:タグ色/0x10000, CFLAG:タグ色%0x10000/0x100, CFLAG:タグ色%0x100, , TARGET
		SIF RESULT
			CFLAG:タグ色 = RESULT
		JUMP STATUS, ロストキャラ表示
	CASE 12
		INVERTBIT マニュアル表示, 0
		JUMP STATUS, ロストキャラ表示
	;50 称号の固定
	CASE 50
		IF 固定称号 == ""
			CALL 称号表示
			固定称号 = %HTML_TOPLAINTEXT(HTML_POPPRINTINGSTR())%
			PRINTFORMW 称号を「%固定称号%」で固定しました
		ELSE
			固定称号 = 
			PRINTW 称号の固定を解除しました
		ENDIF
		JUMP STATUS, ロストキャラ表示
	;97 バストサイズ詳細
	CASE 97
		CALL CHARA_BUST_SETTING, TARGET
		JUMP STATUS, ロストキャラ表示
	;98 メモ
	CASE 98
		PRINTL メモの内容を入力してください
		INPUTS
		メモ:TARGET:0 = %RESULTS%
		JUMP STATUS, ロストキャラ表示
	;99に服装変更のボタンがある
	CASE 99
		CALL CHARA_CLOTHES_SETTING
		JUMP STATUS, ロストキャラ表示
	CASE 100
		INVERTBIT OPTION:相性非表示, 0
		JUMP STATUS, ロストキャラ表示
ENDSELECT

@基本情報
#DIMS DYNAMIC 表示用変数

PRINTFORM □ %I18N("基本情報")% 
IF CFLAG:タグ色
	IF ISSLIME(TARGET)
		PRINTFORM %I18N("体色")%:
	ELSE
		PRINTFORM %I18N("タグ色")%:
	ENDIF
	SETCOLOR CFLAG:タグ色
	PRINT ■
	RESETCOLOR
ENDIF
PRINTL 
PRINTFORM 　%I18N("名前")%:%NAMEDISP(TARGET)% %I18N("性別")%:%性別表示(TARGET)% 

PRINTFORM %I18N("種族")%:
IF NOF(TARGET) == MASTER || NOF(TARGET) >= 10000 || CSTR:種族 == CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
	IF TARGET == MASTER && CSTR:種族 != 主人初期種族 && 主人初期種族 != ""
		PRINTFORM %I18N("元|0|の|1|", , 主人初期種族, CSTR:種族)% 
	ELSE
		PRINTFORM %I18N(CSTR:種族, "RACE")% 
	ENDIF
ELSEIF CSTR:種族 != CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
	PRINTFORM %I18N("元|0|の|1|", , CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族")), CSTR:種族)% 
ENDIF

IF TARGET != MASTER
	PRINTFORM %I18N("ストレス値")%:
	IF GUI
		HTML_PRINT IMGTOTAG(BASEBAR("ストレス値", TARGET, 4)), 1
	ELSE
		CALL ストレス表示, TARGET
	ENDIF
	SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
		PRINTFORM  %I18N("依存度")%:{CFLAG:依存度} 
ELSEIF TARGET == MASTER && ゲームモード() != "新婚モード"
	PRINTFORM (%I18N("堕した人数")%:{FLAG:累計恋慕+FLAG:累計淫乱+FLAG:累計服従}人) 
	PRINTFORM %I18N("難易度")%:%難易度()% 
ENDIF
PRINTL
PRINT 　
SIF MOD:バスト関連ステータス表示 || TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
	PRINTFORM %I18N("経験人数")%:{経験人数(TARGET)} 
IF MOD:バスト関連ステータス表示
	PRINTFORM %I18N("パイズリ経験人数")%:{パイズリ経験人数(TARGET)} 
	IF INRANGE(NOF(TARGET), 0, 9999) && バストサイズ詳細トップ:(NOF(TARGET)):(TALENT:TARGET:バストサイズ)
		PRINTFORM %I18N("バスト")%:
		CALL キャラバストサイズ表示(TARGET)
		PRINT  
	ENDIF
ENDIF
IF TARGET == MASTER
	PRINTFORM %I18N("称号")%:
	IF 固定称号 != ""
		PRINTFORM %固定称号%
	ELSE
		CALL 称号表示
	ENDIF
	PRINTPLAIN  
	PRINTFORM [50]\@ 固定称号 == "" ? 称号を固定する # 称号の固定を解除する \@
ELSE
	PRINTFORM %メモ:TARGET:0%
ENDIF

IF 顔グラ表示
	GDISPOSE 0
	;まずFACEフォルダを探しに行く
	IF !GCREATEFROMFILE(0, @"FACE\\%NAME:TARGET%_FACE.png")
		;見つからなかったらresources直下を探す
		GCREATEFROMFILE 0, @"%NAME:TARGET%_FACE.png"
	ENDIF
	SPRITECREATE @"%NAME:TARGET%_FACE", 0
	IF SPRITECREATED(@"%NAME:TARGET%_FACE")
		表示用変数 = 　<img src='%NAME:TARGET%_FACE' srcb='' height='500' width='500'>
		HTML_PRINT 表示用変数
	ELSE
		表示用変数 = 　<img src='NO_IMAGE' srcb='' height='500' width='500'>
		HTML_PRINT 表示用変数
	ENDIF
	REPEAT 4
		PRINTL 
	REND
ELSE
	PRINTL 
ENDIF


@STATUS_ETC, ロストキャラ表示
#DIM PAGE
#DIM DYNAMIC ドラゴンに処女, 2
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 家族人数
#DIM DYNAMIC 兄数
#DIM DYNAMIC 弟数
#DIM DYNAMIC 姉数
#DIM DYNAMIC 妹数
#DIM DYNAMIC 息子娘数
#DIM DYNAMIC 孫数
#DIM DYNAMIC 父親
#DIM DYNAMIC 母親
#DIM DYNAMIC 尊属
#DIM DYNAMIC 家族番号簡易版
#DIM DYNAMIC 兄弟姉妹番号簡易版
#DIM DYNAMIC 家族構成, 30
#DIM DYNAMIC 続柄記録用
#DIMS DYNAMIC 家族名, 30
#DIMS DYNAMIC 立場
#DIM DYNAMIC 兄弟姉妹性別, 10
#DIM DYNAMIC 兄弟姉妹数
#DIMS DYNAMIC 主人名保存用
#DIM DYNAMIC 次のキャラ = -1
#DIM DYNAMIC 前のキャラ = -1
#DIM DYNAMIC ロストキャラ表示
#DIM 曲表示

;処女が主人のドラゴンとなっている場合は、現在のドラゴンの名前を入れて後で戻す
IF CSTR:処女 == "主人のドラゴン"
	CSTR:処女 = %CSTR:MASTER:ドラゴン%
	ドラゴンに処女:0 = 1
ENDIF
IF CSTR:性転換処女 == "主人のドラゴン"
	CSTR:性転換処女 = %CSTR:MASTER:ドラゴン%
	ドラゴンに処女:1 = 1
ENDIF

DRAWLINE
CALL 基本情報

PRINTL □担当楽曲・出演曲
IF 曲表示
	CALL 担当曲表示, NOF(TARGET)
	PRINTL 　[100] 非表示にする
ELSE
	PRINTL 　[100] 表示する
ENDIF

PRINTL □ その他情報
IF パートナー != "" && パートナー == CHARANAMEF(TARGET)
	PRINTFORML 　%ゲーム開始日()%に%CALLNAME:MASTER%のパートナーになった
ELSEIF ゲームモード() == "新婚モード"
	IF NOF(TARGET) >= 10000
		PRINTFORML 　%CSVNAMEF(CFLAG:血縁コード/100000)%と%CSVNAMEF(CFLAG:血縁コード%100000)%の\@ CFLAG:近親交配 ? 近親交配で # 間に \@産まれた\@ SEX(TARGET) == 1 ? 息子 # 娘 \@だ
	ELSEIF FINDCHARA(CFLAG:嫁, 1) >= 0
		IF TARGET == MASTER
			PRINTFORML 　%ゲーム開始日()%に%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%と結婚した
		ELSE
			PRINTFORML 　%ゲーム開始日()%に%CALLNAME:MASTER%と結婚した
		ENDIF
	ENDIF
ELSE
	IF TARGET == MASTER
		PRINTFORML 　%ゲーム開始日()%にこの世界へと迷いこむ
		PRINTFORML 　現在は{CHARANUM-1}人の奴隷を扱う調教師
		SIF FLAG:周回数 >= 2
			PRINTFORML 　{FLAG:周回数}周目のゲームであり、この世界を積極的に楽しんでいる
		SIF FLAG:累計恋慕
			PRINTFORML 　累計{FLAG:累計恋慕}人の奴隷の心を奪った
		SIF FLAG:累計淫乱
			PRINTFORML 　累計{FLAG:累計淫乱}人の奴隷を肉欲に溺れさせた
		SIF FLAG:累計服従
			PRINTFORML 　累計{FLAG:累計服従}人の奴隷が%CALLNAME:MASTER%に服従した
	ELSE
		IF NOF(TARGET) == 0
			PRINTFORML 　%CALLNAME:MASTER%が調教師を始めたと聞いて、自ら奴隷になりたいと志願してきた変人である
		ELSEIF NOF(TARGET) >= 10000 && !CFLAG:人工物
			PRINTFORML 　%CSVNAMEF(CFLAG:血縁コード/100000)%と%CSVNAMEF(CFLAG:血縁コード%100000)%の\@ CFLAG:近親交配 ? 近親交配で # 間に \@産まれた\@ SEX(TARGET) == 1 ? 息子 # 娘 \@だ
		ELSEIF CHARANAMEF(TARGET) == "にんじん"
			PRINTFORML 　{BUYY:0}年{BUYM:0}月{BUYD:0}日に加入
		ELSEIF !CFLAG:人工物
			PRINTFORML 　{BUYY:0}年{BUYM:0}月{BUYD:0}日に購入
		ENDIF
	ENDIF
ENDIF
IF CFLAG:嫁
	PRINTFORML 　現在は%CALLNAME:MASTER%の嫁である
ELSEIF CFLAG:淫魔
	PRINTFORML 　現在は%CALLNAME:MASTER%の使い魔である
ELSEIF CFLAG:傀儡
	PRINTFORML 　現在は%CALLNAME:MASTER%の支配下に置ける傀儡である
ENDIF

;以下血統の表示
;フラグと性別が合ってない場合は合わせる
IF CFLAG:兄弟 && SEX(TARGET) == 2
	CFLAG:姉妹 = CFLAG:兄弟
	CFLAG:兄弟 = 0
ENDIF
IF CFLAG:姉妹 && SEX(TARGET) == 1
	CFLAG:兄弟 = CFLAG:姉妹
	CFLAG:姉妹 = 0
ENDIF

IF CFLAG:血縁コード && NOF(TARGET) < 10000
	FOR LCOUNT, 0, 10000
		IF EXISTCSV(LCOUNT) && CSVCFLAG(LCOUNT, GETNUM(CFLAG, "血縁コード")) == CFLAG:血縁コード
			続柄記録用 = CSVCFLAG(LCOUNT, GETNUM(CFLAG, "続柄"))
			IF LCOUNT == NOF(TARGET) && 続柄記録用 < 1000
				兄弟姉妹性別:続柄記録用 = SEX(TARGET)
				CONTINUE
			ENDIF
			SELECTCASE 続柄記録用
				;祖父母の場合
				CASE IS >= 2000
					;家族構成:20=祖父 21=祖母
					家族構成:(20+続柄記録用%10) = LCOUNT
				;父母の場合
				CASE IS >= 1000
					;家族構成:10=父 11=母
					家族構成:(10+続柄記録用%10) = LCOUNT
				;兄弟姉妹の場合
				CASEELSE
					家族構成:続柄記録用 = LCOUNT
					兄弟姉妹数++
					SELECTCASE CSVTALENT(LCOUNT, GETNUM(TALENT, "性別"))
						CASE 1
							兄弟姉妹性別:続柄記録用 = 1
						CASE 2
							兄弟姉妹性別:続柄記録用 = 2
					ENDSELECT
			ENDSELECT
			家族人数++
		ENDIF
	NEXT
	
	;子供の場合は家族構成に表示中のキャラも入れる
	IF CFLAG:続柄 < 1000
		家族構成:(CFLAG:続柄) = NOF(TARGET)
		兄弟姉妹数++
	ENDIF
	
	SELECTCASE 家族人数
		CASE 1
			FOR LCOUNT, 0, VARSIZE("家族構成")
				IF 家族構成:LCOUNT != 0
					SIF 家族構成:LCOUNT == NOF(TARGET)
						CONTINUE
					家族番号簡易版 = 家族構成:LCOUNT
					;祖父母の場合
					IF CFLAG:続柄 >= 2000
						立場 = \@ SEX(TARGET) == 1 ? 祖父 # 祖母 \@
					ELSEIF CFLAG:続柄 >= 1000
						立場 = \@ SEX(TARGET) == 1 ? 父親 # 母親 \@
					ELSE
						SELECTCASE LCOUNT
							;祖父母が居る場合
							CASE IS >= 20
								立場 = 孫
							;父母が居る場合
							CASE IS >= 10
								立場 = \@ SEX(TARGET) == 1 ? 息子 # 娘 \@
							CASEELSE
								;兄、姉が居る場合
								IF LCOUNT < CFLAG:続柄
									立場 = \@ SEX(TARGET) == 1 ? 弟 # 妹 \@
								;兄、姉が居る場合
								ELSE
									立場 = \@ SEX(TARGET) == 1 ? 兄 # 姉 \@
								ENDIF
						ENDSELECT
					ENDIF
					BREAK
				ENDIF
			NEXT
			PRINTFORML 　%CSVNAME(家族番号簡易版)%の%立場%にあたる
		CASE IS >= 2
			;家族の中で最高齢の人、もしくは兄弟姉妹の一番下を探す
			FOR LCOUNT, 29, -1, -1
				IF 家族構成:LCOUNT != 0
					尊属 = LCOUNT
					BREAK
				ENDIF
			NEXT
			;3人以上の兄弟姉妹なら兄弟姉妹の表記を考える
			IF 兄弟姉妹数 >= 3
				;今のところ3兄弟、3姉妹までなのでそれにだけ対応
				;ちなみにCONV_NOは英数字→漢数字変換関数
				立場 = %CONV_NO(兄弟姉妹数)%
				SELECTCASE 兄弟姉妹性別:1
					;一番上がお兄ちゃん
					CASE 1
						立場 += "兄"
						;妹が入れば「兄妹」いなければ「兄弟」
						IF MATCH(兄弟姉妹性別, 2)
							立場 += "妹"
						ELSE
							立場 += "弟"
						ENDIF
					;一番上がお姉ちゃん
					CASE 2
						立場 += "姉"
						;妹が入れば「姉妹」いなければ「姉弟」
						IF MATCH(兄弟姉妹性別, 2)
							立場 += "妹"
						ELSE
							立場 += "弟"
						ENDIF
				ENDSELECT
				立場 += @"の{CFLAG:続柄}番目であり、"
				SELECTCASE CFLAG:続柄
					;一番上
					CASE 1
						FOR LCOUNT, 2, 兄弟姉妹数+1
							SIF LCOUNT > 2
								立場 +=  "、"
							立場 += @"%CSVNAME(家族構成:LCOUNT)%"
						NEXT
						立場 += @"の\@ SEX(TARGET) == 1 ? 兄 # 姉 \@"
					;中間
					CASE IS < 兄弟姉妹数
						FOR LCOUNT, 1, CFLAG:続柄
							SIF LCOUNT > 1
								立場 += "、"
							立場 += @"%CSVNAME(家族構成:LCOUNT)%"
						NEXT
						立場 += @"の\@ SEX(TARGET) == 1 ? 弟 # 妹 \@、"
						FOR LCOUNT, CFLAG:続柄+1, 兄弟姉妹数+1
							SIF LCOUNT > CFLAG:続柄+1
								立場 += "、"
							立場 += @"%CSVNAME(家族構成:LCOUNT)%"
						NEXT
						立場 += @"の\@ SEX(TARGET) == 1 ? 兄 # 姉 \@"
					;一番下
					CASE IS == 尊属
						FOR LCOUNT, 1, 兄弟姉妹数
							SIF LCOUNT > 1
								立場 += "、"
							立場 += @"%CSVNAME(家族構成:LCOUNT)%"
						NEXT
						立場 += @"の\@ SEX(TARGET) == 1 ? 弟 # 妹 \@"
				ENDSELECT
			ELSEIF 兄弟姉妹数 >= 2
				IF CFLAG:続柄 == 1
					立場 = %CSVNAME(家族構成:2)%の\@ SEX(TARGET) == 1 ? 兄 # 姉 \@
				ELSE
					立場 = %CSVNAME(家族構成:1)%の\@ SEX(TARGET) == 1 ? 弟 # 妹 \@
				ENDIF
			ENDIF
			;子供の場合
			IF CFLAG:続柄 < 1000
				SELECTCASE 尊属
					;兄弟姉妹しか居ない場合
					CASE IS < 10
						PRINTFORML 　%立場%にあたる
					;父母が居る場合
					CASE IS < 20
						PRINT 　
						SIF 兄弟姉妹数 >= 2
							PRINTFORM %立場%で、
						SIF 家族構成:10
							PRINTFORM %CSVNAME(家族構成:10)%
						SIF 家族構成:10 && 家族構成:11
							PRINT と
						SIF 家族構成:11
							PRINTFORM %CSVNAME(家族構成:11)%
						SIF 父親 && 母親
							PRINT 夫妻
						PRINTFORML の\@ SEX(TARGET) == 1 ? 息子 # 娘 \@にあたる
					;祖父母が居る場合
					CASE IS < 30
						PRINT 　
						SIF 兄弟姉妹数 >= 2
							PRINTFORM %立場%で
						;さすがに親も居れば情報量を減らす
						IF 家族構成:10 || 家族構成:11
							SIF 家族構成:10
								PRINTFORM %CSVNAME(家族構成:10)%
							SIF 家族構成:10 && 家族構成:11
								PRINT と
							SIF 家族構成:11
								PRINTFORM %CSVNAME(家族構成:11)%
							SIF 父親 && 母親
								PRINT 夫妻
							PRINTFORM の\@ SEX(TARGET) == 1 ? 息子 # 娘 \@及び、
						ENDIF
						SIF 家族構成:20
							PRINTFORM %CSVNAME(家族構成:20)%
						SIF 家族構成:20 && 家族構成:21
							PRINT と
						SIF 家族構成:21
							PRINTFORM %CSVNAME(家族構成:21)%
						PRINTL の孫にあたる
				ENDSELECT
			;親の場合
			ELSEIF CFLAG:続柄 < 2000
				;息子、娘を表示
				PRINT 　
				FOR LCOUNT, 1, 10
					SIF 家族構成:LCOUNT == 0
						BREAK
					SIF LCOUNT > 1
						PRINT 、
					PRINTFORM %CSVNAME(家族構成:LCOUNT)%
				NEXT
				PRINTFORML の\@ SEX(TARGET) == 1 ? 父親 # 母親 \@にあたる
			;祖父母の場合
			ELSEIF CFLAG:続柄 < 3000
				;孫を表示
				PRINT 　
				FOR LCOUNT, 1, 10
					SIF 家族構成:LCOUNT == 0
						BREAK
					SIF LCOUNT > 1
						PRINT 、
					PRINTFORM %CSVNAME(家族構成:LCOUNT)%
				NEXT
				PRINTFORML の\@ SEX(TARGET) == 1 ? 祖父 # 祖母 \@にあたる
			ENDIF
	ENDSELECT
ENDIF

SIF CFLAG:クローン番号
	PRINTFORML 　%CSVNAMEF(CFLAG:複製)%の{CFLAG:クローン番号}体目のクローンだ

;体型と痛み耐性
IF TALENT:スライム
	SIF CFLAG:タグ色
		SETCOLOR CFLAG:タグ色
	PRINT 　不定形なスライムの体をしている。
	IF (NOF(TARGET) < 10000 && CSTR:種族 == CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))) || CFLAG:複製 || CFLAG:人工物
		IF TALENT:MASTER:溶ける快
			SELECTCASE CFLAG:スライム度
				CASE 1 TO 200
					PRINT 　まだまだスライムとしては未熟とも言える
				CASE 200 TO 500
					PRINT 　それなりに育ってきたと言えるか
				CASE 500 TO 1000
					PRINT 　進化の兆しが見えつつある
				CASE IS > 1000
					PRINT 　スライムとしてより上位の存在となる日も近いだろう
			ENDSELECT
		ENDIF
	ELSE
		IF TALENT:MASTER:溶ける快
			SELECTCASE CFLAG:スライム度
				CASE 1 TO 100
					PRINT 　まだ慣れておらず、動きが少しぎこちなく見える
				CASE 100 TO 300
					PRINT 　スライムの体に少し慣れ始めてきたようだ
				CASE 300 TO 500
					PRINT 　今の体にも慣れつつあるようだ
				CASE 500 TO 700
					PRINT 　だいぶ慣れてきたのか体の違和感も薄れてきたようだ
				CASE 700 TO 900
					PRINT 　もうすっかりこの体にも馴染んできた
				CASE 900 TO 1050
					PRINT 　今の体の方が自然であるとすら思えてきた
				CASE 1050 TO 1200
					PRINT 　休眠する時は無意識に形を崩すくらいに順応している
				CASE 1200 TO 1300
					PRINT 　…体だけではなく、理性もとろけてきてるようにすら見える
				CASE 1300 TO 1400
					PRINT 　…まるで何かを望むかのように、無意識に体を蠢かせている
				CASE IS > 1400
					PRINT 　…元に、戻りたく、ない…ずっと、スライムで、いたい…
			ENDSELECT
		ENDIF
	ENDIF
	SIF CFLAG:スライム度 && TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
		PRINTFORM （深度:{CFLAG:スライム度}）
	PRINTL 
	RESETCOLOR
ELSEIF TALENT:溶ける快
	SIF CFLAG:タグ色
		SETCOLOR CFLAG:タグ色
	IF (NOF(TARGET) < 10000 && CSTR:種族 == CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))) || CFLAG:複製 || CFLAG:人工物
		PRINT 　不定形なスライムの体の中に、その本体と言うべき核を漂わせている。　スライムとしてはすっかり上位の存在であろう。
	ELSE
		PRINT 　不定形なスライムの体の中に、その本体と言うべき核を漂わせている。　もう本来の種族に戻る事は二度と無いだろう…
	ENDIF
	SIF CFLAG:スライム度 && TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
		PRINTFORM （深度:{CFLAG:スライム度}）
	PRINTL 
	RESETCOLOR
ELSE
	IF TARGET != MASTER
		PRINT 　
		IF TALENT:小人体型
			PRINTFORM 人間よりとても小さな体をしており、
		ELSE
			SELECTCASE TALENT:体格
				CASE 1
					PRINT いわゆる子供のような未成熟な体であり、
				CASE 2
					PRINT まだ成熟しきっていない体をしており、
				CASE 4
					PRINT 平均よりも長身な体格をしており、
			ENDSELECT
		ENDIF
		IF TALENT:痛みに弱い
			IF TALENT:小人体型 || TALENT:体格 <= 2
				PRINTL 当然与えられる痛みに耐えることは難しいと思われる
			ELSE
				SIF TALENT:体格 == 4
					PRINT しかし
				PRINTL 苦痛を与えられることが苦手なようだ
			ENDIF
		ELSEIF TALENT:痛みに強い
			IF TALENT:小人体型 || TALENT:体格 <= 2
				PRINTL その小さな体に似合わず人一倍の忍耐力があるようだ
			ELSE
				PRINTL 丈夫な体ゆえか多少の無茶も問題ないとのことだ
			ENDIF
		ELSE
			IF TALENT:小人体型 || TALENT:体格 <= 2
				PRINTFORML しかしいたって健康体で、それなり\@ゲームモード() == "新婚モード" ? に激しい行為でも # の調教なら \@耐えられるだろう
			ELSE
				PRINTFORML \@ TALENT:体格 == 4 ? その # 人並みの \@体が\@ ゲームモード() == "新婚モード" ? 激しい行為 # 調教 \@に耐えられるかどうか、心の問題だ
			ENDIF
		ENDIF
	ENDIF

	;バストサイズ
	IF SEX(TARGET) == 2
		SELECTCASE TALENT:バストサイズ
			CASE IS >= 6
				PRINTFORML 　信じられないほど大きな乳房は実に官能的だ
			CASE 4, 5
				IF TALENT:体格 <= 2
					PRINTFORML 　その幼い体には不似合いなほどに大きな乳房をしている
				ELSE
					PRINTFORML 　その豊満なスタイルは見ていて飽きないものだ
				ENDIF
			CASE 3
				IF TALENT:体格 <= 2
					PRINTFORML 　その小さな体には少し不釣り合いな大きめの乳房を持っている
				ELSE
					PRINTFORML 　体に釣り合ったバストサイズは成るべくして成ったものだろう
				ENDIF
			CASE 2
				PRINTFORML 　僅かな胸の膨らみが華奢な美を表している
			CASE 1
				PRINTFORML 　あまり女性らしいスタイルではないが、それが欠点とは限らない
		ENDSELECT
	ELSEIF SEX(TARGET) == 1
		SIF TALENT:バストサイズ
			PRINTFORML 　乳首の感度が良くなったあまりに胸が少し膨らんでしまった経緯がある
	ENDIF

	IF TALENT:性転換済
		IF TALENT:体格 <= 2
			PRINTFORML 　幼い見た目とは裏腹に、その体はかつて\@ SEX(TARGET) == 1 ? 少女から少年 # 少年から少女 \@に変貌したという特殊な過去を持つ
		ELSE
			PRINTFORML 　かつては\@ SEX(TARGET) == 1 ? 女性 # 男性 \@だったが、異性の体へと変貌してしまった過去を持つ
		ENDIF
	ENDIF

	SIF TALENT:去勢済み
		PRINTFORML 　本来男性器があった場所には切断痕が残っている
	IF CFLAG:男性器移植済み
		IF CFLAG:男性器提供者 == 0
			PRINTFORM 　%NAME:MASTER%
		ELSE
			PRINTFORM 　%CSVNAMEF(CFLAG:男性器提供者)%
		ENDIF
		PRINTL の男性器が陰部に移植されている
	ENDIF
	IF CFLAG:スライム度 && (TALENT:MASTER:孔雀の瞳 || TALENT:MASTER:溶ける快 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳))
		PRINTFORM 　スライムに侵食されつつある
		IF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
			PRINTFORM 　（深度:{CFLAG:スライム度}）
		ENDIF
		PRINTL 
	ENDIF
ENDIF

IF CFLAG:生殖力強化 == 1
	PRINTL 　薬物投与によって生殖力が引き上げられている
ELSEIF CFLAG:生殖力強化 == 2
	PRINTFORML 　%CSVCALLNAMEF(CFLAG:生殖力担当魔術師)%の魔術によって一時的に生殖力が引き上げられている
ENDIF
IF CFLAG:精管結紮 == 2 || CFLAG:卵管結紮 == 2
	PRINTFORML 　%CSVCALLNAMEF(CFLAG:不妊担当魔術師)%の魔術によって一時的に生殖機能を失っている
ELSE
	SIF !生殖機能♂(TARGET) && PENIS(TARGET)
		PRINTL 　男性としての生殖機能は無い
	SIF !生殖機能♀(TARGET) && VAGINA(TARGET)
		PRINTL 　女性としての生殖機能は無い
ENDIF

IF TALENT:霊体
	PRINTL 　常人には触れることができない霊的な存在である
ELSE
	CALL 初めての性経験
ENDIF

SIF CFLAG:憑依 >= 0
	PRINTFORML 　%CSVNAMEF(NOF(TARGET))%に憑依されている

IF TALENT:恋慕
	PRINTFORML 　%CALLNAME:MASTER%と生活を共にすることに幸せを感じている節が\@ !CFLAG:キャラロスト ? ある # あった \@
ELSEIF TALENT:淫乱
	PRINTFORML 　積極的に性行為をせがむようになり、時に自らの指で快感に溺れて\@ !CFLAG:キャラロスト ? いる # いた \@
ELSEIF TALENT:服従
	PRINTFORML 　%CALLNAME:MASTER%に与えられるもの、%CALLNAME:MASTER%に抱かれることが至上の悦びだと思い込んで\@ !CFLAG:キャラロスト ? いる # いた \@
ELSEIF TALENT:親愛
	PRINTFORML 　身も心も%CALLNAME:MASTER%に心酔しきっており、一生を%CALLNAME:MASTER%の伴侶として全うすると決めて\@ !CFLAG:キャラロスト ? いる # いた \@
ELSEIF TALENT:淫魔
	PRINTFORML 　まるで性の快感を司る淫魔であり、暇さえあれば犯し犯されるような毎日を送って\@ !CFLAG:キャラロスト ? いる # いた \@
ELSEIF TALENT:隷属
	PRINTFORML 　%CALLNAME:MASTER%の言葉こそ絶対であり、%CALLNAME:MASTER%の所有物\@ !CFLAG:キャラロスト ? である # だった \@
ELSEIF TALENT:崩壊
	PRINTFORML 　時たまに不可解な行動を取っては、理解し難い場所が誰かの体液で汚れていることが\@ !CFLAG:キャラロスト ? ある # あった \@
ENDIF

IF CFLAG:人工物
	IF CSTR:種族 == "機械"
		PRINTFORML 　{BUYY}年{BUYM}月{BUYD}日に製造された
	ELSE
		PRINTFORML 　{BUYY}年{BUYM}月{BUYD}日に人体錬成で作られた
	ENDIF
ELSEIF CFLAG:誕生日 >= 101
	PRINTFORML 　誕生日は{CFLAG:誕生日/100}月{CFLAG:誕生日%100}日のようだ
ENDIF

SIF CSTR:眷属主 != ""
	PRINTFORML 　%CSTR:眷属主%の手により%CSTR:種族%の眷属に迎えられた

SELECTCASE CFLAG:出産トラウマ
	CASE 1 TO 9
		PRINTL 　出産に関して多少の嫌悪感が見られる
	CASE 10 TO 29
		PRINTL 　妊娠や出産に対してあまり良い思い出が無いようだ
	CASE 30 TO 49
		PRINTL 　かつての経験から自らが身籠ることを避けたがっている
	CASE IS >= 50
		PRINTL 　子供を産むことに強いトラウマを抱えている
ENDSELECT

IF TALENT:ドラゴン使い && (陥落(TARGET) || TARGET == MASTER)
	PRINT 　龍を使役している。
	IF CSTR:ドラゴン == "ドラゴン"
		PRINTL 名前は特に決まっていないようだ
	ELSE
		PRINTFORML 名前は%CSTR:ドラゴン%というようだ
	ENDIF
ENDIF

SELECTCASE CSTR:背中の印
	CASE "奴隷の焼印"
		PRINTL 　背中には奴隷の焼印が押されている
	CASE "淫売の焼印"
		PRINTL 　背中には淫売の焼印が押されている
ENDSELECT

SIF CSTR:適用MOD != ""
	PRINTFORML 　%CSTR:適用MOD%が適用されたキャラ

IF TARGET != MASTER
	;新婚モードなら一時的にCALLNAME:MASTERは「主人」になる
	IF ゲームモード() == "新婚モード"
		主人名保存用 = %CALLNAME:MASTER%
		CALLNAME:MASTER = 主人
	ENDIF
	SELECTCASE PAGE
		CASE 0
			PRINTLC □ 日常経験
			PRINTPLAINFORM %SPACESLENS(" [1]← ")%
			PRINTL [2]→
			SIF CFLAG:売却寸止め
				PRINTFORML 　売却されそうになったことがある({CFLAG:売却寸止め})
			SIF CFLAG:被吸血経験
				PRINTFORML 　血を吸われたことがある({CFLAG:被吸血経験})
			SIF CFLAG:精肉寸止め
				PRINTFORML 　精肉されそうになったことがある({CFLAG:精肉寸止め})
			SIF CFLAG:朝フェラ経験
				PRINTFORML 　朝から%CALLNAME:MASTER%をフェラで射精させて起こしたことがある({CFLAG:朝フェラ経験})
			SIF CFLAG:朝パイズリ経験
				PRINTFORML 　朝から%CALLNAME:MASTER%に胸で奉仕したことがある({CFLAG:朝パイズリ経験})
			SIF CFLAG:朝騎乗位経験
				PRINTFORML 　朝から%CALLNAME:MASTER%を睡姦して精液を搾り取ったことがある({CFLAG:朝騎乗位経験})
			SIF CFLAG:調教後3P経験
				PRINTFORML 　調教後に三人で乱れたことがある({CFLAG:調教後3P経験})
			SIF CFLAG:調教後奉仕経験
				PRINTFORML 　調教を終えてから後戯を愉しんだことがある({CFLAG:調教後奉仕経験})
			SIF CFLAG:調教後セックス経験
				PRINTFORML 　調教を終えた後も満足できずに愛し合ったことがある({CFLAG:調教後セックス経験})
			SIF CFLAG:調教後強精神薬反動経験
				PRINTFORML 　調教後に薬の反動で強い絶頂に襲われたことがある({CFLAG:調教後強精神薬反動経験})
			SIF CFLAG:調教後強精神薬反動経験強
				PRINTFORML 　調教後に薬の反動で絶頂が止まらなくなったことがある({CFLAG:調教後強精神薬反動経験強})
			SIF CFLAG:調教後搾乳経験
				PRINTFORML 　調教後に母乳を搾り出して%CALLNAME:MASTER%にご馳走したことがある({CFLAG:調教後搾乳経験})
			SIF CFLAG:調教後自慰経験
				PRINTFORML 　調教に満足できず、調教後も自慰に浸ったことがある({CFLAG:調教後自慰経験})
			SIF CFLAG:調教後添い寝経験
				PRINTFORML 　%CALLNAME:MASTER%と二人同じベッドで一夜を共に過ごしたことがある({CFLAG:調教後添い寝経験})
			SIF CFLAG:調教後夜這い経験
				PRINTFORML 　寝る前に%CALLNAME:MASTER%の子種で子宮を潤してから共に床についたことがある({CFLAG:調教後夜這い経験})
			SIF CFLAG:ありえないものをプレゼントされた経験
				PRINTFORML 　IIDX筐体をプレゼントされたことがある({CFLAG:ありえないものをプレゼントされた経験})
			SIF CFLAG:主人に調合知識を伝授した経験
				PRINTFORML 　主人に薬調合の知識を伝授したことがある({CFLAG:主人に調合知識を伝授した経験})
			IF CFLAG:眷属化した人数
				IF TALENT:ネクロマンサー
					SELECTCASE CSTR:種族
						CASE "人間", "改造人間", "人間(？)", "宇宙人", "不明"
							PRINTFORML 　今までに{CFLAG:眷属化した人数}人を屍鬼の眷属に迎え入れた
						CASEELSE
							PRINTFORML 　今までに{CFLAG:眷属化した人数}人を%CSTR:種族%や屍鬼の眷属に迎え入れた
					ENDSELECT
				ELSE
					PRINTFORML 　今までに{CFLAG:眷属化した人数}人を%CSTR:種族%の眷属に迎え入れた
				ENDIF
			ENDIF
			SIF CFLAG:助手の処女喪失を見せつけられた経験
				PRINTFORML 　目の前で助手の処女喪失を見せつけられたことがある({CFLAG:助手の処女喪失を見せつけられた経験})
			SIF CFLAG:電極で責められた経験
				PRINTFORML 　電極で責められたことがある({CFLAG:電極で責められた経験})
			SIF CFLAG:尿道に挿入された経験
				PRINTFORML 　尿道に男根級のモノを挿入されたことがある({CFLAG:尿道に挿入された経験})
		CASE 1
			PRINTLC □ 奉仕経験(回数) 
			PRINTPLAIN 
			PRINTL [1]← [2]→
			IF CFLAG:主人からの膣射経験
				IF 陥落(TARGET)
					PRINTFORML 　%CALLNAME:MASTER%の精液を膣で受け入れたことがある({CFLAG:主人からの膣射経験})
				ELSE
					PRINTFORML 　%CALLNAME:MASTER%に膣内射精されたことがある({CFLAG:主人からの膣射経験})
				ENDIF
			ENDIF
			SIF CFLAG:助手からの膣射経験
				PRINTFORML 　助手に膣内射精されたことがある({CFLAG:助手からの膣射経験})
			IF CFLAG:主人からの腸射経験
				IF 陥落(TARGET)
					PRINTFORML 　アナルに%CALLNAME:MASTER%の精液を注がれたことがある({CFLAG:主人からの腸射経験})
				ELSE
					PRINTFORML 　%CALLNAME:MASTER%に腸内射精されたことがある({CFLAG:主人からの腸射経験})
				ENDIF
			ENDIF
			SIF CFLAG:助手からの腸射経験
				PRINTFORML 　助手に腸内射精されたことがある({CFLAG:助手からの腸射経験})
			SIF CFLAG:手で射精させた経験
				PRINTFORML 　手で%CALLNAME:MASTER%を射精させたことがある({CFLAG:手で射精させた経験})
			SIF CFLAG:口で射精させた経験
				PRINTFORML 　口で%CALLNAME:MASTER%を射精させたことがある({CFLAG:口で射精させた経験})
			SIF CFLAG:お掃除フェラ経験
				PRINTFORML 　%CALLNAME:MASTER%のペニスの汚れを綺麗に舐め取ったことがある({CFLAG:お掃除フェラ経験})
			SIF CFLAG:胸で射精させた経験
				PRINTFORML 　胸の奉仕で%CALLNAME:MASTER%を射精させたことがある({CFLAG:胸で射精させた経験})
			SIF CFLAG:キスで射精させた経験
				PRINTFORML 　キスだけで%CALLNAME:MASTER%を射精させたことがある({CFLAG:キスで射精させた経験})
			IF CFLAG:騎乗位で射精させた経験
				IF 陥落(TARGET)
					PRINTFORML 　騎乗位で%CALLNAME:MASTER%から精液を搾り取ったことがある({CFLAG:騎乗位で射精させた経験})
				ELSE
					PRINTFORML 　%CALLNAME:MASTER%相手に騎乗位で膣内射精されてしまったことがある({CFLAG:騎乗位で射精させた経験})
				ENDIF
			ENDIF
			IF CFLAG:アナル騎乗位で射精させた経験
				IF 陥落(TARGET)
					PRINTFORML 　自らのアナルで%CALLNAME:MASTER%を射精に導いたことがある({CFLAG:アナル騎乗位で射精させた経験})
				ELSE
					PRINTFORML 　アナルでの騎乗位で%CALLNAME:MASTER%を射精させたことがある({CFLAG:アナル騎乗位で射精させた経験})
				ENDIF
			ENDIF
			SIF CFLAG:足で射精させた経験
				PRINTFORML 　%CALLNAME:MASTER%のペニスを足で扱いて射精させたことがある({CFLAG:足で射精させた経験})
			SIF CFLAG:性交奉仕で射精させた経験
				PRINTFORML 　%CALLNAME:MASTER%を犯しながら%CALLNAME:MASTER%を射精させたことがある({CFLAG:性交奉仕で射精させた経験})
			SIF CFLAG:逆アナルで射精させた経験
				PRINTFORML 　%CALLNAME:MASTER%のアナルを犯しながら%CALLNAME:MASTER%を射精させたことがある({CFLAG:逆アナルで射精させた経験})
			SIF CFLAG:膀胱に射精された経験
				PRINTFORML 　膀胱内に直接射精されたことがある({CFLAG:膀胱に射精された経験})
			SIF CFLAG:臍に中出しされた経験
				PRINTFORML 　臍に挿入されて中出しされたことがある({CFLAG:臍に中出しされた経験})
			SIF CFLAG:乳管に中出しされた経験
				PRINTFORML 　乳管に挿入されて中出しされたことがある({CFLAG:乳管に中出しされた経験})
		CASE 2
			PRINTLC □ 絶頂経験
			PRINTPLAIN 
			PRINTL [1]← [2]→
			SIF CFLAG:絶頂放尿経験
				PRINTFORML 　絶頂と同時に尿を漏らしてしまったことがある({CFLAG:絶頂放尿経験})
			SIF CFLAG:野外放尿経験
				PRINTFORML 　調教中に野外で放尿したことがある({CFLAG:野外放尿経験})
			SIF CFLAG:風呂場放尿経験
				PRINTFORML 　調教中にお風呂場で放尿したことがある({CFLAG:風呂場放尿経験})
			SIF CFLAG:性交放尿経験
				PRINTFORML 　セックスの最中に感極まって尿を漏らしてしまったことがある({CFLAG:性交放尿経験})
			SIF CFLAG:アナル性交放尿経験
				PRINTFORML 　アナルセックスの最中に尿を漏らしてしまったことがある({CFLAG:アナル性交放尿経験})
			SIF CFLAG:クリキャップ絶頂経験
				PRINTFORML 　クリキャップの振動で絶頂したことがある({CFLAG:クリキャップ絶頂経験})
			SIF CFLAG:ニプルキャップ絶頂経験
				PRINTFORML 　ニプルキャップで乳首を責められて絶頂したことがある({CFLAG:ニプルキャップ絶頂経験})
			SIF CFLAG:バイブ絶頂経験
				PRINTFORML 　バイブで膣内を犯されて絶頂したことがある({CFLAG:バイブ絶頂経験})
			SIF CFLAG:アナルバイブ絶頂経験
				PRINTFORML 　アナルバイブで腸内を掻き回されて絶頂したことがある({CFLAG:アナルバイブ絶頂経験})
			SIF CFLAG:アナルビーズ絶頂経験
				PRINTFORML 　尻穴でアナルビーズを飲み込んで絶頂したことがある({CFLAG:アナルビーズ絶頂経験})
			SIF CFLAG:主人への膣射経験
				PRINTFORML 　%CALLNAME:MASTER%の膣内に射精したことがある({CFLAG:主人への膣射経験})
			SIF CFLAG:助手への膣射経験
				PRINTFORML 　助手の膣内に射精したことがある({CFLAG:助手への膣射経験})
			SIF CFLAG:主人への腸射経験
				PRINTFORML 　%CALLNAME:MASTER%のアナルに射精したことがある({CFLAG:主人への腸射経験})
			SIF CFLAG:助手への腸射経験
				PRINTFORML 　助手のアナルに射精したことがある({CFLAG:主人への腸射経験})
			SIF CFLAG:口で射精した経験
				PRINTFORML 　%CALLNAME:MASTER%の口で射精したことがある({CFLAG:口で射精した経験})
			SIF CFLAG:膣を責められて射精した経験
				PRINTFORML 　膣内への刺激で射精したことがある({CFLAG:膣を責められて射精した経験})
			SIF CFLAG:アナルを責められて射精した経験
				PRINTFORML 　アナルを弄られて射精したことがある({CFLAG:アナルを責められて射精した経験})
			SIF CFLAG:犯されて射精した経験
				PRINTFORML 　%CALLNAME:MASTER%に抱かれながら射精したことがある({CFLAG:犯されて射精した経験})
			SIF CFLAG:アナルを犯されて射精した経験
				PRINTFORML 　%CALLNAME:MASTER%のペニスをアナルで受け入れながら射精したことがある({CFLAG:アナルを犯されて射精した経験})
			SIF CFLAG:オナホで射精した経験
				PRINTFORML 　調教中にオナホールの中で射精したことがある({CFLAG:オナホで射精した経験})
			SIF CFLAG:道具で前立腺射精経験
				PRINTFORML 　道具で前立腺を弄られて射精してしまったことがある({CFLAG:道具で前立腺射精経験})
			SIF CFLAG:避妊具射精経験
				PRINTFORML 　調教中に避妊具の中に射精したことがある({CFLAG:避妊具射精経験})
			SIF CFLAG:搾精経験
				PRINTFORML 　搾精目的で精液を搾り取られたことがある({CFLAG:搾精経験})
			SIF CFLAG:野外で射精した経験
				PRINTFORML 　調教中に野外で射精したことがある({CFLAG:野外で射精した経験})
			SIF CFLAG:兜合わせで一緒に射精した経験
				PRINTFORML 　%CALLNAME:MASTER%との兜合わせで、二人一緒に射精したことがある({CFLAG:兜合わせで一緒に射精した経験})
			SIF CFLAG:キスで射精した経験
				PRINTFORML 　%CALLNAME:MASTER%とのキスだけで射精したことがある({CFLAG:キスで射精した経験})
			SIF CFLAG:搾乳された経験
				PRINTFORML 　調教中に母乳を搾り取られたことがある({CFLAG:搾乳された経験})
			SIF CFLAG:浣腸器で脱糞した経験
				PRINTFORML 　調教中に浣腸器を使用されて排泄物を漏らしてしまったことがある({CFLAG:浣腸器で脱糞した経験})
		CASE 3
			PRINTLC □ 異種姦経験
			PRINTPLAIN 
			PRINTL [1]← [2]→
			SIF CFLAG:触手Ｃ絶頂経験
				PRINTFORML 　触手にクリトリスを責められて絶頂したことがある({CFLAG:触手Ｃ絶頂経験})
			SIF CFLAG:触手Ｖ絶頂経験
				PRINTFORML 　触手に膣を犯されて絶頂したことがある({CFLAG:触手Ｖ絶頂経験})
			SIF CFLAG:触手Ａ絶頂経験
				PRINTFORML 　触手にアナルを犯されて絶頂したことがある({CFLAG:触手Ａ絶頂経験})
			SIF CFLAG:触手Ｂ絶頂経験
				PRINTFORML 　触手に乳首を弄られて絶頂したことがある({CFLAG:触手Ｂ絶頂経験})
			SIF CFLAG:触手射精経験
				PRINTFORML 　触手に精液を搾り取られたことがある({CFLAG:触手射精経験})
			SIF CFLAG:異種姦経験
				PRINTFORML 　動物やドラゴンに犯されたことがある({CFLAG:異種姦経験})
			SIF CFLAG:動物に舐められて射精した経験
				PRINTFORML 　動物に舐められて射精したことがある({CFLAG:動物に舐められて射精した経験})
			SIF CFLAG:動物に舐められてイった経験
				PRINTFORML 　動物に舐められて絶頂に達したことがある({CFLAG:動物に舐められてイった経験})
			SIF CFLAG:獣姦膣内射精経験
				PRINTFORML 　獣姦で膣内に中出しされたことがある({CFLAG:獣姦膣内射精経験})
			SIF CFLAG:獣姦腸内射精経験
				PRINTFORML 　獣姦でアナルに中出しされたことがある({CFLAG:獣姦腸内射精経験})
			SIF CFLAG:ドラゴンを口で射精させた経験
				PRINTFORML 　ドラゴンを口で射精させたことがある({CFLAG:ドラゴンを口で射精させた経験})
			SIF CFLAG:ドラゴン膣内射精経験
				PRINTFORML 　ドラゴンに膣内射精されたことがある({CFLAG:ドラゴン膣内射精経験})
			SIF CFLAG:ドラゴン腸内射精経験
				PRINTFORML 　ドラゴンに腸内射精されたことがある({CFLAG:ドラゴン腸内射精経験})
			SIF CFLAG:スライム姦された経験
				PRINTFORML 　スライムに犯されたことがある({CFLAG:スライム姦された経験})
		CASE 4
			PRINTLC □ 撮影経験
			PRINTPLAIN 
			PRINTL [1]←
			SIF CFLAG:絶頂を撮影された経験
				PRINTFORML 　撮影されながらイッたことがある({CFLAG:絶頂を撮影された経験})
			SIF CFLAG:放尿を撮影された経験
				PRINTFORML 　放尿しているところを撮影されたことがある({CFLAG:放尿を撮影された経験})
			SIF CFLAG:射精を撮影された経験
				PRINTFORML 　射精の瞬間を録画されたことがある({CFLAG:射精を撮影された経験})
			SIF CFLAG:噴乳を撮影された経験
				PRINTFORML 　ビデオカメラで撮影中に母乳を垂れ流したことがある({CFLAG:噴乳を撮影された経験})
			SIF CFLAG:自慰を撮影された経験
				PRINTFORML 　自慰行為の一部始終をビデオカメラに収められたことがある({CFLAG:自慰を撮影された経験})
			SIF CFLAG:脱糞を撮影された経験
				PRINTFORML 　撮影されながら浣腸液で糞便を撒き散らしたことがある({CFLAG:脱糞を撮影された経験})
	ENDSELECT
	SIF ゲームモード() == "新婚モード"
		CALLNAME:MASTER = %主人名保存用%
ENDIF

SIF ドラゴンに処女:0
	CSTR:処女 = 主人のドラゴン
SIF ドラゴンに処女:1
	CSTR:性転換処女 = 主人のドラゴン

VARSET ドラゴンに処女, 0

FOR LCOUNT, TARGET+1, CHARANUM
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, TARGET-1, -1, -1
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT

DRAWLINE
PRINTPLAIN 　etc... 
PRINT [0] 戻る 
SIF MOD:バスト関連ステータス表示 || TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
	PRINT [4] 性経験履歴 
PRINT [5] 基本情報　 
CALL PRINTSPACE, 前のキャラ >= 0, "[6] 前のキャラ "
CALL PRINTSPACE, 次のキャラ >= 0, "[7] 次のキャラ "
{
IF !ロストキャラ表示 && (調教可能(TARGET) || 助手可能(TARGET))
 && (!SHOP_配属キャラ選択不可 || (SHOP_配属キャラ選択不可 && CSTR:配属 == ""))
 && !CHECK_CHILD_CARE(TARGET) && !CFLAG:使用不可 && !CFLAG:監禁
}
	CALL PRINTSPACE, SHOW_TARGET != TARGET && 調教可能(TARGET), "[8] 対象に指定 "
	CALL PRINTSPACE, NOSAMES(TARGET, MASTER, ASSI) && 助手可能(TARGET), "[9] 助手に指定 "
	PRINT [10] アイテム使用 
ENDIF
PRINTL 
PRINTPLAINFORM %SPACESLENS("　etcccc ")%
IF !ロストキャラ表示
	IF ISSLIME(TARGET)
		PRINT [11] 体色変更
	ELSE
		PRINT [11] タグ色変更
	ENDIF
ENDIF
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	;以下フラグリセットのためにJUMPで処理
	CASE 1
		PAGE--
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 2
		PAGE++
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 4
		JUMP STATUS_SEX_EXP, 0, ロストキャラ表示
	CASE 5
		JUMP STATUS, ロストキャラ表示
	CASE 6
		TARGET = 前のキャラ
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 7
		TARGET = 次のキャラ
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 8
		CALL ASSIGN_TARGET, TARGET
		RETURN 1
	CASE 9
		CALL ASSIGN_ASSI, TARGET
		RETURN 0
	CASE 10
		CALL アイテム使用_逆引き, TARGET
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 11
		IF ISSLIME(TARGET)
			PRINTFORML □%CALLNAME:TARGET%の体色を設定してください
		ELSE
			PRINTFORML □%CALLNAME:TARGET%のタグ色を設定してください
		ENDIF
		VARLINE = LINECOUNT
		CALL RGBBAR, CFLAG:タグ色/0x10000, CFLAG:タグ色%0x10000/0x100, CFLAG:タグ色%0x100
		SIF RESULT
			CFLAG:タグ色 = RESULT
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 100
		INVERTBIT 曲表示, 0
		JUMP STATUS_ETC, ロストキャラ表示
ENDSELECT

@STATUS_SEX_EXP, 設定, ロストキャラ表示
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ID
#DIM DYNAMIC NOREV
#DIM DYNAMIC LIST_ = 10
#DIM DYNAMIC LIST_NO, 1011
#DIM DYNAMIC SEX_TOTAL
#DIM DYNAMIC NAKADASHI_TOTAL
#DIM DYNAMIC 設定
#DIM DYNAMIC 次のキャラ = -1
#DIM DYNAMIC 前のキャラ = -1
#DIM DYNAMIC ロストキャラ表示
DRAWLINE

LIST_ = 10

SIF 設定 != 1
	CALL 基本情報

PRINTL □ 性経験履歴
[IF_DEBUG]
	SEX_TOTAL = 0
	NAKADASHI_TOTAL = 0
	LIST_NO:LIST_ = LCOUNT
	PRINTFORML  [{LIST_}] 新しく加える
	LIST_++
[ENDIF]
[IF_NDEBUG]
	IF 設定
		SEX_TOTAL = 0
		NAKADASHI_TOTAL = 0
		LIST_NO:LIST_ = LCOUNT
		PRINTFORML  [{LIST_}] 新しく加える
		LIST_++
	ENDIF
[ENDIF]
FOR LCOUNT, 0, 20000
	IF 経験回数:TARGET:LCOUNT
		PRINTFORM 　%CSVNAMEF(LCOUNT)%:{経験回数:TARGET:LCOUNT}回
		[IF_DEBUG]
			SEX_TOTAL += 経験回数:TARGET:LCOUNT
			NAKADASHI_TOTAL += 中出され回数:TARGET:LCOUNT
			LIST_NO:LIST_ = LCOUNT
			PRINTPLAIN  ―――― 
			PRINTFORM [{LIST_}] 変更
			LIST_++
		[ENDIF]
		[IF_NDEBUG]
			IF 設定
				SEX_TOTAL += 経験回数:TARGET:LCOUNT
				NAKADASHI_TOTAL += 中出され回数:TARGET:LCOUNT
				LIST_NO:LIST_ = LCOUNT
				PRINTPLAIN  ―――― 
				PRINTFORM [{LIST_}] 変更
				LIST_++
			ENDIF
		[ENDIF]
		PRINTL
		ID = NOF(TARGET)
		IF 中出し回数:TARGET:LCOUNT || アナル中出し回数:TARGET:LCOUNT || 孕ませ回数:TARGET:LCOUNT
			PRINTFORM 　　%CSVCALLNAMEF(ID)%は%CSVCALLNAMEF(LCOUNT)%の
			SIF 中出し回数:TARGET:LCOUNT
				PRINTFORM 膣内に{中出し回数:TARGET:LCOUNT}回
			SIF アナル中出し回数:TARGET:LCOUNT
				PRINTFORM アナルに{アナル中出し回数:TARGET:LCOUNT}回
			PRINT 射精してい
			SIF 孕ませ回数:TARGET:LCOUNT
				PRINTFORM て、{孕ませ回数:TARGET:LCOUNT}回孕ませてい
			PRINTL る。
		ENDIF
		IF 中出され回数:TARGET:LCOUNT || アナル中出され回数:TARGET:LCOUNT || 孕み回数:TARGET:LCOUNT
			PRINTFORM 　　%CSVCALLNAMEF(LCOUNT)%は%CSVCALLNAMEF(ID)%の
			SIF 中出され回数:TARGET:LCOUNT
				PRINTFORM 膣内に{中出され回数:TARGET:LCOUNT}回
			SIF アナル中出され回数:TARGET:LCOUNT
				PRINTFORM アナルに{アナル中出され回数:TARGET:LCOUNT}回
			PRINT 射精してい
			SIF 孕み回数:TARGET:LCOUNT
				PRINTFORM て、{孕み回数:TARGET:LCOUNT}回孕ませてい
			PRINTL る。
		ENDIF
	ENDIF
NEXT
IF モブ経験人数:TARGET:0
	PRINTFORM 　不明:{モブ経験回数:TARGET:0}回/{モブ経験人数:TARGET:0}人
	[IF_DEBUG]
		SEX_TOTAL += モブ経験回数:TARGET:0
		NAKADASHI_TOTAL += モブ中出され回数:TARGET:0
		LIST_NO:LIST_ = -1
		PRINTPLAIN  ―――― 
		PRINTFORM [{LIST_}] 変更
	[ENDIF]
	[IF_NDEBUG]
		IF 設定
			SEX_TOTAL += モブ経験回数:TARGET:0
			NAKADASHI_TOTAL += モブ中出され回数:TARGET:0
			LIST_NO:LIST_ = -1
			PRINTPLAIN  ―――― 
			PRINTFORM [{LIST_}] 変更
		ENDIF
	[ENDIF]
	PRINTL
	ID = NOF(TARGET)
	IF モブ中出し回数:TARGET:0 || モブアナル中出し回数:TARGET:0 || モブ孕ませ回数:TARGET:0
		PRINTFORM 　　%CSVCALLNAMEF(ID)%は
		SIF モブ中出し回数:TARGET:0
			PRINTFORM 膣内に{モブ中出し回数:TARGET:0}回
		SIF モブアナル中出し回数:TARGET:0
			PRINTFORM アナルに{モブアナル中出し回数:TARGET:0}回
		PRINT 射精してい
		SIF モブ孕ませ回数:TARGET:0
			PRINTFORM て、{モブ孕ませ回数:TARGET:0}回孕ませてい
		PRINTL る。
	ENDIF
	IF モブ中出され回数:TARGET:0 || モブアナル中出され回数:TARGET:0 || モブ孕み回数:TARGET:0
		PRINTFORM 　　%CSVCALLNAMEF(ID)%は
		SIF モブ中出され回数:TARGET:0
			PRINTFORM 膣内に{モブ中出され回数:TARGET:0}回
		SIF モブアナル中出され回数:TARGET:0
			PRINTFORM アナルに{モブアナル中出され回数:TARGET:0}回
		PRINT 射精され
		SIF モブ孕み回数:TARGET:0
			PRINTFORM 、{モブ孕み回数:TARGET:0}回孕まされ
		PRINTL ている。
	ENDIF
ELSE
	[IF_DEBUG]
		LIST_NO:LIST_ = -1
		PRINTFORML  [{LIST_}] モブ経験人数を加える
	[ENDIF]
	[IF_NDEBUG]
		IF 設定
			LIST_NO:LIST_ = -1
			PRINTFORML  [{LIST_}] モブ経験人数を加える
		ENDIF
	[ENDIF]
ENDIF
[IF_DEBUG]
	PRINTFORML  [{LIST_+1}] 性交経験をこれに合わせる(現在:{EXP:性交経験}回)
	PRINTFORML  [{LIST_+2}] 膣射経験をこれに合わせる(現在:{EXP:膣射経験}回)
	SIF !TALENT:童貞 && PENIS(TARGET)
		PRINTFORML  [{LIST_+3}] 童貞を捧げた相手………………\@ CSTR:童貞 != "" ? %CSTR:童貞% # 不明 \@
	IF !TALENT:処女 && VAGINA(TARGET) 
		PRINTFORM  [{LIST_+4}] 処女を捧げた相手………………
		IF CFLAG:処女の奪われ方 == 4
			PRINTFORML 処女懐胎
		ELSE
			PRINTFORML \@ CSTR:処女 != "" ? %CSTR:処女% # 不明 \@
		ENDIF
	ENDIF
[ENDIF]
[IF_NDEBUG]
	IF 設定
		PRINTFORML  [{LIST_+1}] 性交経験をこれに合わせる(現在:{EXP:性交経験}回)
		PRINTFORML  [{LIST_+2}] 膣射経験をこれに合わせる(現在:{EXP:膣射経験}回)
		SIF !TALENT:童貞 && PENIS(TARGET)
			PRINTFORML  [{LIST_+3}] 童貞を捧げた相手………………\@ CSTR:童貞 != "" ? %CSTR:童貞% # 不明 \@
		IF !TALENT:処女 && VAGINA(TARGET)
			PRINTFORM  [{LIST_+4}] 処女を捧げた相手………………
			IF CFLAG:処女の奪われ方 == 4
				PRINTFORML 処女懐胎
			ELSE
				PRINTFORML \@ CSTR:処女 != "" ? %CSTR:処女% # 不明 \@
			ENDIF
		ENDIF
	ELSE
		SIF !TALENT:童貞 && PENIS(TARGET)
			PRINTFORML 　童貞を捧げた相手………………\@ CSTR:童貞 != "" ? %CSTR:童貞% # 不明 \@
		IF !TALENT:処女 && VAGINA(TARGET)
			PRINTFORM 　処女を捧げた相手………………
			IF CFLAG:処女の奪われ方 == 4
				PRINTFORML 処女懐胎
			ELSE
				PRINTFORML \@ CSTR:処女 != "" ? %CSTR:処女% # 不明 \@
			ENDIF
		ENDIF
	ENDIF
[ENDIF]

IF MOD:バスト関連ステータス表示
	ID = NOF(TARGET)
	PRINTL □パイズリ経験履歴
	FOR LCOUNT, 0, 20000
		IF パイズリ回数:TARGET:LCOUNT
			PRINTFORML 　%CSVNAMEF(LCOUNT)%:{パイズリ回数:TARGET:LCOUNT}回
			SIF 挟射回数:TARGET:LCOUNT
				PRINTFORML 　　%CSVCALLNAMEF(LCOUNT)%は%CSVCALLNAMEF(ID)%のパイズリで{挟射回数:TARGET:LCOUNT}回射精している。
		ENDIF
	NEXT
	IF モブパイズリ人数:TARGET:0
		PRINTFORML 　不明:{モブパイズリ回数:TARGET:0}回/{モブパイズリ人数:TARGET:0}人
		SIF モブ挟射回数:TARGET:0
			PRINTFORML 　　%CSVCALLNAMEF(ID)%はパイズリで{モブ挟射回数:TARGET:0}回射精させている。
	ENDIF
ENDIF

FOR LCOUNT, TARGET+1, CHARANUM
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			次のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, TARGET-1, -1, -1
	IF ロストキャラ表示
		IF CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ELSE
		IF !CFLAG:LCOUNT:キャラロスト
			前のキャラ = LCOUNT
			BREAK
		ENDIF
	ENDIF
NEXT

DRAWLINE
PRINTPLAIN 　 EXP 　
PRINT [0] 戻る 
IF 設定 != 1
	PRINT [4] 基本情報 
	PRINT [5] その他情報 
	CALL PRINTSPACE, 前のキャラ >= 0, "[6] 前のキャラ "
	CALL PRINTSPACE, 次のキャラ >= 0, "[7] 次のキャラ "
ENDIF

IF !CSVEDITOR
	IF 調教可能(TARGET) || 助手可能(TARGET)
		CALL PRINTSPACE, SHOW_TARGET != TARGET && 調教可能(TARGET), "[8] 対象に指定 "
		CALL PRINTSPACE, NOSAMES(TARGET, MASTER, ASSI) && 助手可能(TARGET), "[9] 助手に指定 "
	ENDIF
ENDIF
PRINTL 

PRINTPLAINFORM %SPACESLENS("　 EXP 　")%
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	;以下フラグリセットのためにJUMPで処理
	CASE 4
		JUMP STATUS, ロストキャラ表示
	CASE 5
		JUMP STATUS_ETC, ロストキャラ表示
	CASE 6
		TARGET = 前のキャラ
		JUMP STATUS_SEX_EXP, ロストキャラ表示
	CASE 7
		TARGET = 次のキャラ
		JUMP STATUS_SEX_EXP, ロストキャラ表示
	CASE 8, 9
		CALL ASSIGN_TARGET, TARGET
		RETURN 1
	CASE 9
		CALL ASSIGN_ASSI, TARGET
		RETURN 0
	CASEELSE
	[IF_DEBUG]
		IF INRANGE(RESULT, 10, LIST_)
		ELSEIF GROUPMATCH(RESULT, LIST_+3, LIST_+4)
		ELSEIF RESULT == 1+LIST_
			EXP:性交経験 = SEX_TOTAL
			RESTART
		ELSEIF RESULT == 2+LIST_
			EXP:膣射経験 = NAKADASHI_TOTAL
			RESTART
		ENDIF
	[ENDIF]
	[IF_NDEBUG]
		IF 設定
			IF INRANGE(RESULT, 10, LIST_)
			ELSEIF GROUPMATCH(RESULT, LIST_+3, LIST_+4)
			ELSEIF RESULT == 1+LIST_
				EXP:性交経験 = SEX_TOTAL
				RESTART
			ELSEIF RESULT == 2+LIST_
				EXP:膣射経験 = NAKADASHI_TOTAL
				RESTART
			ENDIF
		ENDIF
	[ENDIF]
ENDSELECT

;BREAKでデバッグメニュー
RESULT:1 = RESULT
DRAWLINE
IF 設定 != 1
	PRINTL □ 基本情報
	PRINTFORM 　名前:%NAME:TARGET%　
	IF NOF(TARGET) >= 10000 || CSTR:種族 == CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
		PRINTFORM 種族:%CSTR:種族%　
	ELSEIF CSTR:種族 != CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))
		PRINTFORM 種族:元%CSVCSTR(NOF(TARGET), GETNUM(CSTR, "種族"))%の%CSTR:種族%　
	ENDIF
	IF TARGET != MASTER
		PRINT ストレス値:
		CALL ストレス表示, TARGET
		PRINTFORM 　依存度:{CFLAG:依存度}
	ELSEIF TARGET == MASTER
		PRINTFORM (堕した人数:{FLAG:累計恋慕+FLAG:累計淫乱+FLAG:累計服従}人)　
		PRINTFORM 難易度:%難易度()%
	ENDIF
	PRINTL 
ENDIF
IF RESULT:1 == LIST_
	$SEX_EXP_DEBUG1
	PRINTL
	PRINTFORML □経験人数:{経験人数(TARGET)}
	PRINTFORML 　[0] モブ経験人数:{モブ経験人数:TARGET:0}人
	PRINTFORML 　[1] モブ経験回数:{モブ経験回数:TARGET:0}回
	PRINTFORML 　[2] モブ中出し回数……………{モブ中出し回数:TARGET:0}回
	PRINTFORML 　[3] モブアナル中出し回数……{モブアナル中出し回数:TARGET:0}回
	PRINTFORML 　[4] モブ孕ませ回数……………{モブ孕ませ回数:TARGET:0}回
	PRINTFORML 　[5] モブ中出され回数…………{モブ中出され回数:TARGET:0}回
	PRINTFORML 　[6] モブアナル中出され回数…{モブアナル中出され回数:TARGET:0}回
	PRINTFORML 　[7] モブ孕み回数………………{モブ孕み回数:TARGET:0}回
	PRINTL 　[8] 戻る
	BINPUT
	SIF RESULT == 8
		RESTART
	RESULT:1 = RESULT
	PRINTL 回数を入力してください。
	INPUT
	SELECTCASE RESULT:1
		CASE 0
			モブ経験人数:TARGET:0 = RESULT
		CASE 1
			モブ経験回数:TARGET:0 = RESULT
		CASE 2
			モブ中出し回数:TARGET:0 = RESULT
		CASE 3
			モブアナル中出し回数:TARGET:0 = RESULT
		CASE 4
			モブ孕ませ回数:TARGET:0 = RESULT
		CASE 5
			モブ中出され回数:TARGET:0 = RESULT
		CASE 6
			モブアナル中出され回数:TARGET:0 = RESULT
		CASE 7
			モブ孕み回数:TARGET:0 = RESULT
	ENDSELECT
	GOTO SEX_EXP_DEBUG1
ELSE
	IF  GROUPMATCH(RESULT:1, 10, LIST_+3, LIST_+4)
		PRINTL 相手の番号を入力してください
		PRINT [10000] 一覧表示 
		INPUT
		IF RESULT == 10000
			PRINTL □ 一覧表示
			CALL CHARA_LIST, "全キャラ表示"
		ELSEIF !EXISTCSV(RESULT)
			PRINTFORMW {RESULT}にはキャラが登録されていません
			RESTART
		ENDIF
		SIF RESULT == 10000
			RESTART
		ID = RESULT
	ELSE
		ID = LIST_NO:(RESULT:1)
	ENDIF
	NOREV = -1
	FOR LCOUNT, 0, CHARANUM
		IF ID == NOF(LCOUNT)
			NOREV = LCOUNT
			BREAK
		ENDIF
	NEXT
	IF RESULT:1 == LIST_+3
		CSTR:童貞 = %CSVNAME(ID)%
		RESTART
	ELSEIF RESULT:1 == LIST_+4
		CSTR:処女 = %CSVNAME(ID)%
		RESTART
	ENDIF
	$SEX_EXP_DEBUG2
	PRINTFORML □経験人数:{経験人数(TARGET)}
	PRINTFORML  [1] %CSVNAMEF(ID)%との経験回数:{経験回数:TARGET:ID}回
	PRINTFORML  [2] 中出し回数……………{中出し回数:TARGET:ID}回
	PRINTFORML  [3] アナル中出し回数……{アナル中出し回数:TARGET:ID}回
	PRINTFORML  [4] 孕ませ回数……………{孕ませ回数:TARGET:ID}回
	PRINTFORML  [5] 中出され回数…………{中出され回数:TARGET:ID}回
	PRINTFORML  [6] アナル中出され回数…{アナル中出され回数:TARGET:ID}回
	PRINTFORML  [7] 孕み回数………………{孕み回数:TARGET:ID}回
	PRINTL [8] 戻る
	BINPUT
	SIF RESULT == 8
		RESTART
	RESULT:1 = RESULT
	PRINTL 回数を入力してください。
	INPUT
	SELECTCASE RESULT:1
		CASE 1
			経験回数:TARGET:ID = RESULT
			SIF NOREV > -1
				経験回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 2
			中出し回数:TARGET:ID = RESULT
			SIF NOREV > -1
				中出され回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 3
			アナル中出し回数:TARGET:ID = RESULT
			SIF NOREV > -1
				アナル中出され回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 4
			孕ませ回数:TARGET:ID = RESULT
			SIF NOREV > -1
				孕み回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 5
			中出され回数:TARGET:ID = RESULT
			SIF NOREV > -1
				中出し回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 6
			アナル中出され回数:TARGET:ID = RESULT
			SIF NOREV > -1
				アナル中出し回数:NOREV:(NOF(TARGET)) = RESULT
		CASE 7
			孕み回数:TARGET:ID = RESULT
			SIF NOREV > -1
				孕ませ回数:NOREV:(NOF(TARGET)) = RESULT
	ENDSELECT
	GOTO SEX_EXP_DEBUG2
ENDIF

;=============================================================================
;バージンなど
@初めての性経験
IF TALENT:性転換済
	SELECTCASE SEX(TARGET)
		CASE 1
			IF CSTR:処女 == "NOTLOSTVIRGIN"
				PRINTFORML 　結局、誰にも処女を捧げることなく男性になった
			ELSEIF CSTR:処女 != ""
				IF TARGET == MASTER
					PRINTFORML 　過去に女性だった時は%CSTR:処女%に処女を捧げた
				ELSE
					PRINTFORM 　過去に女性だった時は%CSTR:処女%に処女を捧げた。
					CALL 処女の奪われ方, CFLAG:処女の奪われ方
					PRINTL 
				ENDIF
			ENDIF
			SIF CSTR:童貞 != "NOTLOSTVIRGIN" && CSTR:童貞 != ""
				PRINTFORML 　当時、両性具有でもあった%CALLNAME:TARGET%が初めて男性器で繋がった相手は%CSTR:童貞%だ
		CASE 2
			IF CSTR:童貞 == "NOTLOSTVIRGIN"
				PRINTFORML 　童貞を捨てられないまま女性になってしまった
			ELSEIF CSTR:童貞 != ""
				PRINTFORML 　かつて男性だった時の初めての相手は%CSTR:童貞%だ
			ENDIF
			IF CSTR:処女 != "NOTLOSTVIRGIN" && CSTR:処女 != ""
				IF TARGET == MASTER
					PRINTFORML 　当時、両性具有でもあった%CALLNAME:TARGET%が処女を捧げた相手は%CSTR:処女%
				ELSE
					PRINTFORM 　当時、両性具有でもあった%CALLNAME:TARGET%が処女を捧げた相手は%CSTR:処女%。
					CALL 処女の奪われ方, CFLAG:処女の奪われ方
					PRINTL 
				ENDIF
			ENDIF
		CASEELSE
	ENDSELECT
	SELECTCASE SEX(TARGET)
		CASE 1
			IF TALENT:童貞
				PRINTFORML 　性転換してからは\@ !CFLAG:キャラロスト ? まだ #  \@セックスをしたことがない
			ELSE
				PRINTFORML 　男性として初めて性交に及んだ相手は%CSTR:性転換童貞%
			ENDIF
			IF TALENT:処女
				PRINTFORML 　性転換後も両性具有という形で女性器を得たが、そちらは\@ !CFLAG:キャラロスト ? まだ使っていない # 使われることはなかった \@
			ELSEIF CFLAG:性転換処女の奪われ方 == 4
				PRINT 性転換後は
				CALL 処女の奪われ方, CFLAG:性転換処女の奪われ方
			ELSEIF CSTR:性転換処女 != ""
				IF TARGET == MASTER
					PRINTFORML 　性転換後も両性具有という形で女性器を取り戻し、そちらの初めての相手は%CSTR:性転換処女%
				ELSE
					PRINTFORM 　性転換後も両性具有という形で女性器を取り戻し、そちらの初めての相手は%CSTR:性転換処女%。
					CALL 処女の奪われ方, CFLAG:性転換処女の奪われ方
					PRINTL 
				ENDIF
			ENDIF
		CASE 2
			IF TALENT:処女
				PRINTFORML 　性転換後の身体は純潔を保っており、処女のようだ
			ELSEIF CFLAG:性転換処女の奪われ方 == 4
				PRINT 性転換後は
				CALL 処女の奪われ方, CFLAG:性転換処女の奪われ方
			ELSE
				IF TARGET == MASTER
					PRINTFORML 　性転換後は%CSTR:性転換処女%に処女を捧げた。
				ELSE
					PRINTFORM 　性転換後は%CSTR:性転換処女%に処女を捧げた。
					CALL 処女の奪われ方, CFLAG:性転換処女の奪われ方
					PRINTL 
				ENDIF
			ENDIF
			IF TALENT:童貞
				PRINTFORML 　性転換したにも関わらず両性具有という形でペニスがあるが、そちらはまだ未経験のようだ
			ELSEIF CSTR:性転換童貞 != ""
				PRINTFORML 　性転換後も両性具有という形でペニスを使っており、性転換後の初めての相手は%CSTR:性転換童貞%
			ENDIF
	ENDSELECT
ELSE
	SELECTCASE SEX(TARGET)
		CASE 1
			IF TALENT:童貞
				IF EXP:性交経験
					PRINTFORML 　被挿入側としての経験こそあるものの、挿入する側として性交した経験は\@ !CFLAG:キャラロスト ? 未だない # なかった \@ようだ
				ELSE
					PRINTFORML 　未だ性交の経験が無いようだ
				ENDIF
			ELSEIF !TALENT:童貞 && CSTR:童貞 == ""
				PRINTFORML 　女を知らないわけではないようだが、その初めての相手が誰なのかは不明である
			ELSE
				PRINTFORML 　初めての性交の相手は%CSTR:童貞%
			ENDIF
			IF TALENT:処女
				PRINTFORML 　男であるにも関わらず兼ね添えた女性器は、未だに男を知らないままだ
			ELSEIF CFLAG:処女の奪われ方 == 4
				CALL 処女の奪われ方, CFLAG:処女の奪われ方
			ELSEIF !TALENT:処女 && CSTR:処女 != "" && !TALENT:ふたなり
				PRINTFORML 　かつて両性具有となった時に%CSTR:処女%に処女を捧げたが、今はその女性器は無い
			ELSEIF !TALENT:処女 && TALENT:ふたなり
				PRINTFORM 　\@ TALENT:去勢済み ? 失った男性器の代わりに女性器を有しており # 両性具有であり \@その女性器での初めての相手は%CSTR:処女%
				IF TARGET != MASTER
					PRINT 。
					CALL 処女の奪われ方, CFLAG:処女の奪われ方
				ENDIF
				PRINTL 
			ENDIF
		CASE 2
			IF TALENT:処女
				IF EXP:性交経験 && CSTR:童貞 == ""
					PRINTFORML 　肛門性交の経験こそあるものの、女性器の方は\@ !CFLAG:キャラロスト ? 未だ純潔を保っている # 純潔を保っていた \@ようだ
				ELSE
					PRINTFORML 　未だ男を知らない生娘のようだ
				ENDIF
			ELSEIF CFLAG:処女の奪われ方 == 4
				PRINT 　
				CALL 処女の奪われ方, CFLAG:処女の奪われ方
				PRINTL 
			ELSEIF !TALENT:処女 && CSTR:処女 == ""
				PRINTFORML 　非処女だが初めての相手が誰なのかは不明
			ELSE
				IF TARGET == MASTER
					PRINTFORML 　%CSTR:処女%に処女を捧げた
				ELSE
					PRINTFORM 　%CSTR:処女%に処女を捧げた。
					CALL 処女の奪われ方, CFLAG:処女の奪われ方
					PRINTL 
				ENDIF
			ENDIF
			IF TALENT:童貞
				PRINTFORML 　両性具有であり、雄の貞操は純潔を保って\@ !CFLAG:キャラロスト ? いる # いた \@ようだ
			ELSEIF !TALENT:童貞 && CSTR:童貞 != "" && !TALENT:ふたなり
				PRINTFORML 　かつて男性器で%CSTR:童貞%と繋がったことがあるが、現在は成りを潜めている
			ELSEIF !TALENT:童貞 && TALENT:ふたなり
				PRINTFORML 　両性具有であり、%CSTR:童貞%を相手に種牡としても開花したようだ
			ENDIF
	ENDSELECT
ENDIF

@処女の奪われ方, フラグ
#DIM DYNAMIC フラグ
SELECTCASE フラグ
	CASE 1
		IF TALENT:貞操無頓着
			PRINTFORM ほとんど陵辱のようなものだったが、本人はあまり気にして\@ !CFLAG:キャラロスト ? いない # いなかった \@ようだ
		ELSE
			IF 陥落(TARGET)
				PRINTFORM その方法は強引なものだったが、\@ !CFLAG:キャラロスト ? 今はもう許している # やがて許すことにした \@ようだ
			ELSE
				PRINTFORM 強引に初めてを奪われたことを\@ !CFLAG:キャラロスト ? 未だに根に持っている # 最後まで根に持っていた \@ようだ
			ENDIF
		ENDIF
	CASE 2
		IF TALENT:恋慕 || TALENT:親愛
			IF CSTR:処女 == NAME:MASTER
				PRINTFORM 最愛の相手に処女を捧げられたことを幸せに思って\@ !CFLAG:キャラロスト ? いる # いた \@
			ELSE
				PRINTFORM 本当は%CALLNAME:MASTER%に処女を奪ってほしかったらしい
			ENDIF
		ELSEIF TALENT:淫乱 || TALENT:淫魔
			PRINTFORM 欲望の赴くままに処女を散らした故に、その時のことをよく覚えて\@ !CFLAG:キャラロスト ? いない # いなかった \@らしい
		ELSEIF TALENT:服従 || TALENT:隷属
			PRINTFORM %CALLNAME:MASTER%の命令通りに処女を捨てられたことを誇りに思って\@ !CFLAG:キャラロスト ? いる # いた \@
		ELSE
			PRINTFORM 同意の上での破瓜だったが、その面影は\@ !CFLAG:キャラロスト ? 既にない # 失われた \@
		ENDIF
	CASE 3
		IF 陥落(TARGET)
			PRINTFORM 調教を\@ !CFLAG:キャラロスト ? 受け入れた今でも # 受け入れてからも \@自分の処女を奪った当人が気になって\@ !CFLAG:キャラロスト ? いる # いた \@らしい
		ELSE
			PRINTFORM 不可解な状況で犯されるよりは、親しい人物が初めての相手で良かったと思って\@ !CFLAG:キャラロスト ? いる # いた \@
		ENDIF
	CASE 4
		PRINTFORM 処女懐妊したため、出産と同時に処女を喪失した
	CASE 5
		PRINTFORM 屠殺の際に辱めを受けて処女を失い、その生涯を終えた
	CASE 6
		PRINTFORM 死姦されて処女を失った
ENDSELECT

;=============================================================================
;体力、気力、魔力表示
@SHOW_INFO_HEALTH, 対象 = -1
#DIM DYNAMIC 対象

SIF 対象 == -1
	対象 = TARGET
;スライムでタグ色あればそれで表示
SIF ISSLIME(対象) && CFLAG:対象:タグ色
	SETCOLOR CFLAG:対象:タグ色

IF GUI
	PRINTFORM %I18N("体力"),10%:
	HTML_PRINT IMGTOTAG(BASEBAR("体力", 対象)), 1
	PRINTL 
	PRINTFORM %I18N("気力"),10%:
	HTML_PRINT IMGTOTAG(BASEBAR("気力", 対象)), 1
	PRINTL 
	IF TALENT:対象:魔法使い
		PRINTFORM %I18N("魔力"),10%:
		HTML_PRINT IMGTOTAG(BASEBAR("魔力", 対象)), 1
		PRINTL 
	ENDIF
ELSE
	PRINTFORML %I18N("体力"),10%%BARBASE("体力", 対象)%
	PRINTFORML %I18N("気力"),10%%BARBASE("気力", 対象)%
	SIF TALENT:対象:魔法使い
		PRINTFORML %I18N("魔力"),10%%BARBASE("魔力", 対象)%
ENDIF
RESETCOLOR

;=============================================================================
;パラメータ表示
@SHOW_INFO_PALAM
#DIMS DYNAMIC 記号
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC パラメータ
#DIMS DYNAMIC 表示用, 5
#DIMS DYNAMIC COLOR
#DIMS DYNAMIC BGCOLOR

FOR LCOUNT, 0, VARSIZE("PALAM")
	;パラメータ = 表示されるパラメータの数値。kMGT等と一緒に表示される
	;表示用 = いろいろな文字を組み合わせて最終的に表示される1文
	SIF PALAMNAME:LCOUNT == ""
		BREAK

	;ヴァギナ無いなら快Ｖは灰色表示
	IF PALAMNAME:LCOUNT == "快Ｖ"
		IF !VAGINA(TARGET)
			SETCOLORBYNAME gray
		ELSE
			SETCOLORBYNAME silver
		ENDIF
	ENDIF
	;ペニス無いなら勃起は灰色表示
	IF PALAMNAME:LCOUNT == "勃起"
		IF !PENIS(TARGET)
			SETCOLORBYNAME gray
		ELSE
			SETCOLORBYNAME silver
		ENDIF
	ENDIF

	;文頭に全角スペースを置く
	SIF LCOUNT%4 == 0
		表示用 = "　"
	;パラメータの名前・パラメータレベル
	表示用 = %I18N(PALAMNAME:LCOUNT, "PALAM"), LANGDATA:LANGUAGE:PALAMLEN, LEFT%Lv
	IF !VAGINA(TARGET) && PALAMNAME:LCOUNT == "快Ｖ"
		表示用 += "-"
	ELSE
		表示用 += @"{GETPALAMLV(PALAM:LCOUNT, 9)}"
	ENDIF

	;ゲージ処理 パラメータレベルが9以上なら余計な処理は飛ばす
	IF GETPALAMLV(PALAM:LCOUNT, 9) >= 9
		SIF !GUI
			表示用 += "[**********] "
	ELSEIF PALAMNAME:LCOUNT == "潤滑" && PALAM:LCOUNT >= 20000
		SIF !GUI
			表示用 += "[**********] "
	ELSEIF PALAMNAME:LCOUNT == "勃起" && PALAM:LCOUNT >= PALAMLV:3
		SIF !GUI
			表示用 += "[**********] "
	ELSE
		SIF !GUI
			表示用 += @"%BARSTR(PALAM:LCOUNT, PALAMLV:(GETPALAMLV(PALAM:LCOUNT, 9)+1), 10)% "
	ENDIF

	;これだけ特殊
	IF PALAMNAME:LCOUNT == "潤滑" && PALAM:LCOUNT >= 20000
		表示用 += @" %I18N("濡"), 18, LEFT%"
		GOTO SET
	ENDIF
	;これも特殊
	IF PALAMNAME:LCOUNT == "勃起" && PALAM:LCOUNT >= PALAMLV:3
		表示用 += @" %I18N("勃"), 18, LEFT%"
		GOTO SET
	ENDIF

	SIF GUI
		表示用 += " "

	; 1,000,000,000,000,000,000
	;  E   P   T   G   M   k
	;ｴｸｻ ﾍﾟﾀ  ﾃﾗｷﾞｶﾞ ﾒｶﾞ  ｷﾛ
	;表示領域は半角6文字分なので……
	; 1桁- 6桁:そのまま
	; 7桁- 8桁:k表記
	; 9桁-11桁:M表記
	;12桁-14桁:G表記
	;15桁-17桁:T表記
	;18桁-19桁:P表記
	;2^63≒9223P (9.223E)
	;9,12,15,18桁を直すと残りが3桁になって寂しいので小数点表記を追加
	記号 = 
	STRLENS TOSTR(PALAM:LCOUNT)
	SELECTCASE RESULT
		CASE IS >= 18
			パラメータ = PALAM:LCOUNT / POWER(10,15)
			IF RESULT == 18
				記号 = .%CHARATU(TOSTR(PALAM:LCOUNT),3)%P
			ELSE
				記号 = P
			ENDIF
		CASE IS >= 15
			パラメータ = PALAM:LCOUNT / POWER(10,12)
			IF RESULT == 15
				記号 = .%CHARATU(TOSTR(PALAM:LCOUNT),3)%T
			ELSE
				記号 = T
			ENDIF
		CASE IS >= 12
			パラメータ = PALAM:LCOUNT / POWER(10,9)
			IF RESULT == 12
				記号 = .%CHARATU(TOSTR(PALAM:LCOUNT),3)%G
			ELSE
				記号 = G
			ENDIF
		CASE IS >= 9
			パラメータ = PALAM:LCOUNT / POWER(10,6)
			IF RESULT == 9
				記号 = .%CHARATU(TOSTR(PALAM:LCOUNT),3)%M
			ELSE
				記号 = M
			ENDIF
		CASE IS >= 7
			パラメータ = PALAM:LCOUNT / POWER(10,3)
			記号 = k
		CASEELSE
			パラメータ = PALAM:LCOUNT
	ENDSELECT

	IF !VAGINA(TARGET) && PALAMNAME:LCOUNT == "快Ｖ"
		表示用 += "------"
	ELSE
		IF PALAM:LCOUNT <= 999999
			表示用 += @"{パラメータ, 6, LEFT}"
		ELSE
			表示用 += @"%TOSTR(パラメータ)+記号, 6, LEFT%"
		ENDIF
	ENDIF
	SIF GUI
		表示用 += " "

	;カンストしてたら特殊表示
	IF PALAM:LCOUNT == __INT_MAX__
		表示用 += " CounterStop"
	ELSE
		表示用 += " Next/"

		;ヴァギナ無ければ快Ｖは表示しない
		IF !VAGINA(TARGET) && PALAMNAME:LCOUNT == "快Ｖ"
			表示用 += "------"
		;ペニス無ければ勃起は表示しない
		ELSEIF !PENIS(TARGET) && PALAMNAME:LCOUNT == "勃起"
			表示用 += "------"
		ELSE
			IF GETPALAMLV(PALAM:LCOUNT, 9) >= 9
				表示用 += "MAXLV!"
			ELSE
				表示用 += @"{PALAMLV:(GETPALAMLV(PALAM:LCOUNT, 9)+1)-PALAM:LCOUNT, 6, LEFT}"
			ENDIF
		ENDIF
	ENDIF
	$SET
	IF GUI
		SELECTCASE GETPALAMLV(PALAM:LCOUNT, 9)
			CASE 0
				COLOR = white
				BGCOLOR = gray
			CASE 1
				COLOR = 0xC0FFFF
				BGCOLOR = white
			CASE 2
				COLOR = 0x70FFFF
				BGCOLOR = 0xC0FFFF
			CASE 3
				COLOR = 0x00F0FF
				BGCOLOR = 0x70FFFF
			CASE 4
				COLOR = 0x00D0FF
				BGCOLOR = 0x00F0FF
			CASE 5
				COLOR = 0x00A0FF
				BGCOLOR = 0x00D0FF
			CASE 6
				COLOR = 0x0070FF
				BGCOLOR = 0x00A0FF
			CASE 7
				COLOR = 0x0020FF
				BGCOLOR = 0x0070FF
			CASE 8, 9
				COLOR = darkorange
				BGCOLOR = 0x0020FF
		ENDSELECT
		表示用 = 　%IMGTOTAG(GBAR(PALAM:LCOUNT, PALAMLV:(GETPALAMLV(PALAM:LCOUNT, 10)+1), 14, COLOR, BGCOLOR, 表示用, "black"))%
	ENDIF
	表示用:(LCOUNT%4+1) = %SET_TOOLTIP(表示用, PALAM_COMMENT(PALAMNAME:LCOUNT))%
	RESETCOLOR

	;LCOUNT仕様の都合で3
	IF LCOUNT%4 == 3
		HTML_PRINT STRJOIN(表示用, "", 1)
		VARSET 表示用
	ENDIF
NEXT
SIF LCOUNT%4 != 3
	HTML_PRINT STRJOIN(表示用, "", 1)

CALL NEWLINE

;=============================================================================
;能力表示
@SHOW_INFO_ABL, 対象
#DIMS DYNAMIC 表示用レベル
#DIM DYNAMIC LCOUNT
#DIM CONST 表示上限 = 7
#DIMS DYNAMIC 表示用, 7
#DIM DYNAMIC 表示数
#DIM DYNAMIC 対象

FOR LCOUNT, 0, VARSIZE("ABL")
	IF LCOUNT == VARSIZE("ABL")-1
		HTML_PRINT STRJOIN(表示用, "")
		BREAK
	ELSEIF ABLNAME:LCOUNT == ""
		CONTINUE
	ENDIF

	;改行するのか？ 続けて出力するのか？
	IF 表示数%表示上限 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用, "")
		VARSET 表示用
	ENDIF

	SELECTCASE SEX(対象)
		CASE 1
			;男ならレズっ気、レズ中毒は灰色表示
			SELECTCASE ABLNAME:LCOUNT
				CASE "レズっ気", "レズ中毒"
					SIF GUI
						CONTINUE
					SETCOLORBYNAME gray
					表示用レベル = -----
				CASE "Ｖ感覚"
					;ヴァギナ無ければＶ感覚も灰色
					IF VAGINA(対象)
						SETCOLORBYNAME silver
						表示用レベル = {ABL:対象:LCOUNT, 5, LEFT}
					ELSE
						SIF GUI
							CONTINUE
						SETCOLORBYNAME gray
						表示用レベル = -----
					ENDIF
				CASEELSE
					SETCOLORBYNAME silver
					表示用レベル = {ABL:対象:LCOUNT, 5, LEFT}
			ENDSELECT
		CASE 2
			;女ならホモっ気、ゲイ中毒、メス度は灰色表示
			SELECTCASE ABLNAME:LCOUNT
				CASE "ホモっ気", "ゲイ中毒", "メス度"
					SIF GUI
						CONTINUE
					SETCOLORBYNAME gray
					表示用レベル = -----
				CASEELSE
					SETCOLORBYNAME silver
					表示用レベル = {ABL:対象:LCOUNT, 5, LEFT}
			ENDSELECT
	ENDSELECT

	IF GUI
		表示用:(表示数%表示上限) = %SET_TOOLTIP(@"　%I18N(ABLNAME:LCOUNT, "ABL"), (LANGDATA:LANGUAGE:ABLLEN)%:", ABL_COMMENT(ABLNAME:LCOUNT, 対象))%
		表示用:(表示数%表示上限) += IMGTOTAG(GBAR(ABL:対象:LCOUNT, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:LCOUNT}", "black"))
	ELSE
		表示用:(表示数%表示上限) = %SET_TOOLTIP(@"　%I18N(ABLNAME:LCOUNT, "ABL"), (LANGDATA:LANGUAGE:ABLLEN)%:LV%表示用レベル%", ABL_COMMENT(ABLNAME:LCOUNT, 対象))%
	ENDIF
	RESETCOLOR
	表示数++
NEXT
CALL NEWLINE

;=============================================================================
;素質表示
@SHOW_INFO_TALENT, 対象
#DIMS DYNAMIC 素質名
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 表示用, 6
#DIM DYNAMIC 表示数
#DIM DYNAMIC 対象

FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別"
			CONTINUE
		CASE "恋心", "既婚者"
			SIF 陥落(対象)
				CONTINUE
		CASE "おもらし癖"
			SIF !MOD:SCAT2
				CONTINUE
	ENDSELECT
	IF LCOUNT == VARSIZE("TALENT")-1
		HTML_PRINT STRJOIN(表示用, "")
		BREAK
	ELSEIF TALENT:対象:LCOUNT == 0
		CONTINUE
	ENDIF

	IF TALENTNAME:LCOUNT == "バストサイズ"
		IF LANGUAGE == "ENG"
			素質名 = [%バストサイズ(TALENT:対象:バストサイズ)%Breast]
		ELSE
			素質名 = [%バストサイズ(TALENT:対象:バストサイズ)%]
		ENDIF
	ELSEIF TALENTNAME:LCOUNT == "体格"
		IF TALENT:対象:小人体型
			CONTINUE
		ELSE
			IF LANGUAGE == "ENG"
				素質名 = [%体格(TALENT:対象:体格)%]
			ELSE
				素質名 = [%体格(TALENT:対象:体格)%体型]
			ENDIF
		ENDIF
	ELSE
		素質名 = [%I18N(TALENTNAME:LCOUNT, "TALENT")%]
	ENDIF

	SIF 表示数%6 == 0
		表示用 = 　

	表示用:(表示数%6) += @"%SET_TOOLTIP(素質名, TALENT_COMMENT(TALENTNAME:LCOUNT, 対象))%"

	表示数++

	;改行するのか？ 続けて出力するのか？
	IF 表示数%6 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用, "")
		VARSET 表示用
	ENDIF
NEXT
CALL NEWLINE

;=============================================================================
;刻印表示
@SHOW_INFO_MARK, 対象
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 表示用, 4
#DIM DYNAMIC 対象

FOR LCOUNT, 0, 4
	IF GUI
		表示用:LCOUNT = 　%SET_TOOLTIP(@"%I18N(MARKNAME:LCOUNT, "MARK")%:", MARK_COMMENT(MARKNAME:LCOUNT, 対象))%
		表示用:LCOUNT += IMGTOTAG(GBAR(MARK:対象:LCOUNT, 3, 3, BARCOLOR:能力, "gray", @"{MARK:対象:LCOUNT}", "black"))
	ELSE
		表示用:LCOUNT = 　%SET_TOOLTIP(@"%I18N(MARKNAME:LCOUNT, "MARK")%:LV{MARK:対象:LCOUNT} %BARSTR(MARK:対象:LCOUNT, 3, 3)%", MARK_COMMENT(MARKNAME:LCOUNT, 対象))%
	ENDIF
	SIF LCOUNT == 3
		HTML_PRINT STRJOIN(表示用, "")
NEXT
CALL NEWLINE

;=============================================================================
;経験表示
@SHOW_INFO_EXP, 対象, ツールチップ不使用
#DIMS DYNAMIC 表示用経験
#DIMS DYNAMIC 経験名
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 一行表示数
#DIMS DYNAMIC 表示用, 6
#DIM DYNAMIC 表示数
#DIM DYNAMIC 対象
#DIM DYNAMIC ツールチップ不使用
#DIM DYNAMIC ツールチップオプション保存用

IF ツールチップ不使用
	ツールチップオプション保存用 = ツールチップ非表示
	ツールチップ非表示 = 1
ENDIF

;英語と日本語で文字数が違いすぎるので一行の表示数を変える
IF LANGUAGE == "ENG"
	一行表示数 = 5
ELSE
	一行表示数 = 6
ENDIF

FOR LCOUNT, 0, VARSIZE("EXP")
	IF LCOUNT == VARSIZE("EXP")-1
		HTML_PRINT STRJOIN(表示用, "")
		BREAK
	ELSEIF EXPNAME:LCOUNT == ""
		CONTINUE
	ENDIF

	SIF EXPNAME:LCOUNT == "排便経験" && !MOD:SCAT
		CONTINUE
	SIF EXPNAME:LCOUNT == "放尿経験" && !MOD:SCAT2
		CONTINUE

	;改行するのか？ 続けて出力するのか？
	IF 表示数%一行表示数 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用, "")
		VARSET 表示用
	ENDIF

	表示用経験 = %TOSTR(EXP:対象:LCOUNT), 12%
	;試したけど予想以上に見た目がウザかった
	;表示用経験 = %REPLACE(表示用経験, " ", "_")%

	SELECTCASE EXPNAME:LCOUNT
		CASE "レズ経験"
			;男ならレズ経験は灰色表示
			IF SEX(対象) == 1
				SETCOLORBYNAME gray
				SIF !EXP:LCOUNT
					表示用経験 = ------------
			ENDIF
		;女ならゲイ経験は灰色表示
		CASE "ゲイ経験"
			IF SEX(対象) == 2
				SETCOLORBYNAME gray
				SIF !EXP:LCOUNT
					表示用経験 = ------------
			ENDIF
		;膣が無ければＶ経験、膣射経験、Ｖ拡張経験。出産経験、流産経験も灰色
		CASE "Ｖ経験", "膣射経験", "Ｖ拡張経験", "出産経験", "流産経験"
			IF !VAGINA(対象)
				SETCOLORBYNAME gray
				SIF !EXP:LCOUNT
					表示用経験 = ------------
			ENDIF
		;ペニス無ければ射精経験も灰色
		CASE "射精経験", "射精管理経験"
			IF !PENIS(対象)
				SETCOLORBYNAME gray
				SIF !EXP:LCOUNT
					表示用経験 = ------------
			ENDIF
	ENDSELECT

	経験名 = %短縮経験名(I18N(EXPNAME:LCOUNT, "EXP"))%
	経験名 = %経験名, LANGDATA:LANGUAGE:EXPLEN, LEFT%

	表示用:(表示数%一行表示数) = %SET_TOOLTIP(@"　%経験名, 8%:%表示用経験%", EXP_COMMENT(EXPNAME:LCOUNT, 対象))%
	RESETCOLOR
	表示数++
NEXT
CALL NEWLINE

SIF ツールチップ不使用
	ツールチップ非表示 = ツールチップオプション保存用

;=============================================================================
;珠表示
@SHOW_INFO_JUEL, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 一行表示数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 対象

;英語と日本語で文字数が違いすぎるので一行の表示数を変える
一行表示数 = LANGUAGE == "JP" ? 6 # 4

FOR LCOUNT, 0, VARSIZE("PALAM")
	SELECTCASE PALAMNAME:LCOUNT
		CASE "", "潤滑", "勃起", "反感", "不快", "抑鬱"
			CONTINUE
	ENDSELECT
	;改行するのか？ 続けて出力するのか？
	SIF 表示数%一行表示数 == 0 && 表示数 != 0
		PRINTL 
	
	PRINTFORM 　%I18N("|0|の珠", , I18N(PALAMNAME:LCOUNT, "PALAM")), LANGDATA:LANGUAGE:JUELLEN, LEFT%:{JUEL:対象:LCOUNT, 12}
	表示数++
NEXT
;PRINT 　否定の珠:
;PRINTFORM {JUEL:対象:否定, 10}
CALL NEWLINE

;=============================================================================
;服装表示
@SHOW_INFO_CLOTHES, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC チェック箇所
#DIM DYNAMIC 対象
#DIM DYNAMIC 衣服文字数

FOR LCOUNT, GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
	衣服文字数 = MAX(STRLENS(CSTR:対象:LCOUNT), 衣服文字数)
NEXT
衣服文字数 += 2
FOR LCOUNT, GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
	IF CFLAG:対象:(CSTRNAME:LCOUNT+"着衣不可")
		PRINTFORM 　%CSTRNAME:LCOUNT, 10, LEFT%:着衣不可
	ELSEIF CSTR:対象:LCOUNT != ""
		PRINTFORM 　%CSTRNAME:LCOUNT, 10, LEFT%:%CSTR:対象:LCOUNT, 衣服文字数, LEFT%
		SIF 耐水性(対象, CSTRNAME:LCOUNT)
			PRINT [耐水]
		IF MOD:着衣パイズリ && GROUPMATCH(CSTRNAME:LCOUNT, "上半身上着", "全身上着", "上半身下着")
			SELECTCASE CSTRNAME:LCOUNT
				CASE "全身上着"
					チェック箇所 = CFLAG:対象:全身上着パイズリ
				CASE "上半身上着"
					チェック箇所 = CFLAG:対象:上着パイズリ
				CASE "上半身下着"
					チェック箇所 = CFLAG:対象:下着パイズリ
			ENDSELECT
			SIF パイズリフラグ説明(チェック箇所) != ""
				PRINTFORM [%パイズリフラグ説明(チェック箇所, TARGET)%]
		ENDIF
	ENDIF
	CALL NEWLINE
NEXT
SIF CFLAG:対象:貞操帯
	PRINTFORML 　貞操帯　　:{CFLAG:対象:貞操帯}日目

@SHOW_INFO, 対象, 調教中
#DIM DYNAMIC 調教中
#DIM DYNAMIC 対象

DRAWLINE

PRINTFORML □ %I18N("経験")% 
CALL SHOW_INFO_EXP, 対象
DRAWLINE
PRINTFORML □ %I18N("能力")% 
CALL SHOW_INFO_ABL, 対象
DRAWLINE
IF 対象 != MASTER
	PRINTFORML □ %I18N("刻印")% 
	CALL SHOW_INFO_MARK, 対象
	DRAWLINE
ENDIF
PRINTFORML □ %I18N("素質")% 
CALL SHOW_INFO_TALENT, 対象
IF !CFLAG:対象:キャラロスト || CFLAG:対象:死亡 || CFLAG:対象:依代
	DRAWLINE
	PRINTFORM □ %I18N("服装") % 
	CALLFORM DISABLE, "服装変更", 対象
	IF !調教中 && !RESULT
		PRINTBUTTON "[99] - " + I18N("服装を変更する"), 99
	ENDIF
	PRINTL
	CALL SHOW_INFO_CLOTHES, 対象
ENDIF

DRAWLINE

;;;NOで渡す
@担当曲表示, 対象
#DIM DYNAMIC 対象
#DIMS DYNAMIC 曲一覧, 1000
;行頭に全角スペースを入れるため
#DIM DYNAMIC 表示文字数 = 2
#DIM CONST 表示可能文字数 = STRLENS(GETLINESTR("-"))
#DIM DYNAMIC LCOUNT

IF !EXISTCSV(対象) || CSVCSTR(対象, GETNUM(CSTR, "担当曲")) == ""
	PRINTL 　[無し]
	RETURN
ENDIF

SPLIT CSVCSTR(対象, GETNUM(CSTR, "担当曲")), "|", 曲一覧


PRINT 　
FOR LCOUNT, 0, 1000
	;行頭に改行相当のスペースが入ってるので一文字消す
	SIF CHARATU(曲一覧:LCOUNT, 0) == " "
		曲一覧:LCOUNT = %SUBSTRING(曲一覧:LCOUNT, 1)%
	SIF 曲一覧:LCOUNT == ""
		BREAK
	;括弧2個分で+4
	表示文字数 += STRLENS(曲一覧:LCOUNT)+4
	IF 表示文字数 >= 表示可能文字数
		PRINTL 
		PRINT 　
		;括弧+全角スペース=6
		表示文字数 = STRLENS(曲一覧:LCOUNT)+6
	ENDIF
	PRINTFORM 『%曲一覧:LCOUNT%』
NEXT
CALL NEWLINE
RETURN

	
