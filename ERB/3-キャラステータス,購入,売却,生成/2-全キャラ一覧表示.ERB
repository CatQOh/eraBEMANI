@CHARA_BUY
#DIM LRESULT

CALL TUTORIAL

VARLINE = LINECOUNT

CALL CHARA_LIST

SIF RESULT == 10000
	RETURN 0

LRESULT = RESULT

CALL CHARA_MANUAL, LRESULT
SIF RESULT
	RESTART
CALL CHARA_BUY_CHECK, LRESULT

IF RESULT == 0
	CALL TUTORIAL, "調教開始"
	RETURN 0
ELSE
	RESTART
ENDIF

;2017/07/11に、表示～選択までの全てを関数内で行うように
@CHARA_LIST, ARG, ARG:1
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 単位文字数
#DIM DYNAMIC 価格上限
#DIM DYNAMIC 選択可能, 10000
#DIM 表示上限 = 30
;#DIMS 登場作品フィルタ
#DIMS キャラデザフィルタ
#DIMS 種族フィルタ
#DIM DYNAMIC 表示数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 飛ばした数
#DIMS DYNAMIC 文字列表示用
#DIM DYNAMIC ランセレ, 10000
#DIM DYNAMIC LRESULT

;選択可能キャラのフラグを立てる
FOR LCOUNT, 0, 10000
	SIF LCOUNT == 0 && ARG != 1
		CONTINUE
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;隠しキャラは選べない
	SIF CSVTALENT(LCOUNT, GETNUM(TALENT, "隠しキャラ")) && ARG != 1
		CONTINUE
	;アニマルは主人にできない
	SIF ARG == 2 && CSVTALENT(LCOUNT, GETNUM(TALENT, "アニマル"))
		CONTINUE
	;以上を抜けると選択可能
	;すなわちキーボードの直接入力によるフィルタの無視は仕様とする
	選択可能:LCOUNT = 1
NEXT

$リスト表示

;ARG
;0 = 奴隷購入用キャラリスト
;1 = その他とりあえず全キャラ羅列するためのキャラリスト
;2 = PFREEで主人を選択する際のキャラリスト
;3 = STRATEGYモードでパートナーを選択する際のキャラリスト
;4 = 段位認定エディットで表示する際のキャラリスト

;ARG:1がTRUEならCASEELSEを通す

CALL LOOP_CLEARLINE
DRAWLINE

IF ARG == 0
	PRINT 　
	PRINTFORM %日付表示()%
	IF LANGUAGE == "ENG"
		CALL PASELI, @"Money:{MONEY}＄"
		PRINTFORM  Difficulty:%難易度()% 
		PRINTFORM Master:%NAMEDISP(MASTER)% 
		SIF TARGET >= 0
			PRINTFORM Target:%NAMEDISP(TARGET)% 
		SIF ASSI >= 0
			PRINTFORM Assistant:%NAMEDISP(ASSI)%
		PRINTL 
	ELSE
		CALL PASELI, @"所持金:{MONEY}＄"
		PRINTFORM  難易度:%難易度()% 
		PRINTFORM 主人:%NAME:MASTER% 
		SIF TARGET >= 0
			PRINTFORM 奴隷:%NAME:TARGET% 
		SIF ASSI >= 0
			PRINTFORM 助手:%NAME:ASSI%
		PRINTL 
	ENDIF
	DRAWLINE 
ENDIF

SIF GROUPMATCH(ARG, 0, 2, 3)
	CALL SHOW_MODS

IF OPTION:価格フィルタ所持金依存
	価格上限 = MONEY
ELSE
	価格上限 = OPTION:価格フィルタ上限
ENDIF
飛ばした数 = 0
次ページあり = 0
表示数 = 0
FOR LCOUNT, 0, 10000
	;選択可能じゃないキャラは非表示
	SIF !選択可能:LCOUNT
		CONTINUE
	;フィルタによる除外
	IF GROUPMATCH(ARG, 0, 4)
		SIF LCOUNT == NOF(MASTER)
			CONTINUE
		SIF CCB:LCOUNT
			CONTINUE
		SIF OPTION:男女フィルタ != CSVTALENT(LCOUNT , GETNUM(TALENT, "性別")) && OPTION:男女フィルタ
			CONTINUE
		;一致するキャラだけ表示
		IF OPTION:フィルタスイッチ１ == 1
			SELECTCASE TALENTNAME:(OPTION:カスタムフィルタ１)
				CASE "恋心"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "恋心")) == ""
						CONTINUE
				CASE "既婚者"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "既婚者")) == ""
						CONTINUE
				CASEELSE
					SIF CSVTALENT(LCOUNT, OPTION:カスタムフィルタ１) == 0
						CONTINUE
			ENDSELECT
		ENDIF
		;一致するキャラを除外
		IF OPTION:フィルタスイッチ１ == 2
			SELECTCASE TALENTNAME:(OPTION:カスタムフィルタ１)
				CASE "恋心"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "恋心")) != ""
						CONTINUE
				CASE "既婚者"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "既婚者")) != ""
						CONTINUE
				CASEELSE
					SIF CSVTALENT(LCOUNT, OPTION:カスタムフィルタ１) == 1
						CONTINUE
			ENDSELECT
		ENDIF
		;一致するキャラだけ表示
		IF OPTION:フィルタスイッチ２ == 1
			SELECTCASE TALENTNAME:(OPTION:カスタムフィルタ２)
				CASE "恋心"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "恋心")) == ""
						CONTINUE
				CASE "既婚者"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "既婚者")) == ""
						CONTINUE
				CASEELSE
					SIF CSVTALENT(LCOUNT, OPTION:カスタムフィルタ２) == 0
						CONTINUE
			ENDSELECT
		ENDIF
		;一致するキャラを除外
		IF OPTION:フィルタスイッチ２ == 2
			SELECTCASE TALENTNAME:(OPTION:カスタムフィルタ２)
				CASE "恋心"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "恋心")) != ""
						CONTINUE
				CASE "既婚者"
					SIF CSVCSTR(LCOUNT, GETNUM(CSTR, "既婚者")) != ""
						CONTINUE
				CASEELSE
					SIF CSVTALENT(LCOUNT, OPTION:カスタムフィルタ２) == 1
						CONTINUE
			ENDSELECT
		ENDIF
		SIF OPTION:バストサイズフィルタ != CSVTALENT(LCOUNT, GETNUM(TALENT, "バストサイズ")) && OPTION:バストサイズフィルタ
			CONTINUE
		SIF OPTION:体格フィルタ != CSVTALENT(LCOUNT, GETNUM(TALENT, "体格")) && OPTION:体格フィルタ
			CONTINUE
		SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品２")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品３"))) == 0
			CONTINUE
		SIF キャラデザフィルタ != "" && GROUPMATCH(キャラデザフィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ")), CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ２"))) == 0
			CONTINUE
		SIF 種族フィルタ != "" && 種族フィルタ != CSVCSTR(LCOUNT, GETNUM(CSTR, "種族"))
			CONTINUE
		IF OPTION:口上キャラフィルタ
			TRYCCALLFORM EXIST_KOJO_%CSVNAMERPS(LCOUNT)%
			CATCH
				CONTINUE
			ENDCATCH
		ENDIF

		SELECTCASE CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))
			CASE OPTION:価格フィルタ下限 TO 価格上限
				SIF OPTION:価格フィルタ == 2
					CONTINUE
			CASEELSE
				SIF OPTION:価格フィルタ == 1
					CONTINUE
		ENDSELECT

		SIF OPTION:開発難易度フィルタ && !INRANGE(LIMIT(開発難易度PT計算(LCOUNT)+50, 1, 100), OPTION:開発難易度フィルタ%1000, OPTION:開発難易度フィルタ/1000)
			CONTINUE
		SIF OPTION:屈服難易度フィルタ && !INRANGE(LIMIT(屈服難易度PT計算(LCOUNT)+50, 1, 100), OPTION:屈服難易度フィルタ%1000, OPTION:屈服難易度フィルタ/1000)
			CONTINUE
		CALL 売却額PT計算, LCOUNT
		SIF OPTION:売却額フィルタ && !INRANGE(LIMIT(RESULT, 1, 100), OPTION:売却額フィルタ%1000, OPTION:売却額フィルタ/1000)
			CONTINUE
		SIF OPTION:性経験フィルタ && !INRANGE(LIMIT(開発難易度PT計算(LCOUNT), 1, 100), OPTION:性経験フィルタ%1000, OPTION:性経験フィルタ/1000)
			CONTINUE

		SIF LCOUNT == NOF(MASTER)
			CONTINUE
	ENDIF

	;表示上限(デフォルト30人)表示したら次ページフラグ立ててBREAK
	IF 表示数 >= 表示上限
		次ページあり = 1
		BREAK
	ENDIF

	;ページ数に該当するカウントまでスキップ
	飛ばした数++
	SIF 飛ばした数 <= (TFLAG:ページ数*表示上限)
		CONTINUE

	;3人表示したら改行
	SIF 表示数%3 == 0 && 表示数 != 0
		PRINTL 

	IF ARG != 0
		IF LANGUAGE == "ENG"
			文字列表示用 = [{LCOUNT, 4}] %CSVENGNAME(LCOUNT)%
		ELSE
			文字列表示用 = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)%
		ENDIF
	ELSE
		IF LANGUAGE == "ENG"
			文字列表示用 = [{LCOUNT, 4}] %CSVENGNAME(LCOUNT)% 
		ELSE
			文字列表示用 = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)% 
		ENDIF
		SELECTCASE 数値表示設定
			CASE "3桁区切り"
				文字列表示用 += @"(%TODIGSEP(CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格")))%＄)"
			CASE "漢字桁表示"
				文字列表示用 += @"(%TO漢字(CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格")))%＄)"
			CASEELSE
				文字列表示用 += @"({CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))}＄)"
		ENDSELECT
	ENDIF
	
	;口上表示設定、かつ口上の存在するキャラは青文字表示
	IF OPTION:口上表示
		IF ARG == 0
			SIF EXISTFUNCTION(@"EXIST_KOJO_%CSVNAMERPS(LCOUNT)%")
				SETCOLOR 100, 150, 200
		ELSEIF ARG == 2
			SIF EXISTFUNCTION(@"EXIST_MASTER_KOJO_%CSVNAMERPS(LCOUNT)%")
				SETCOLOR 100, 150, 200
		ELSEIF ARG == 3
			SIF EXISTFUNCTION(@"EXIST_STRATEGY_KOJO_%CSVNAMERPS(LCOUNT)%")
				SETCOLOR 100, 150, 200
		ENDIF
	ENDIF
	IF ARG == 1
		PRINTFORM %文字列表示用 , 38 , LEFT%
	ELSE
		CALL PASELI, 文字列表示用, 1
		;3人目は空白埋めしない
		IF 表示数%3 != 2
			;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
			IF OPTION:パセリ表示
				単位文字数 = 2
			ELSEIF 通貨単位 != ""
				単位文字数 = STRLENS(通貨単位)
			ELSE
				単位文字数 = 1
			ENDIF
			;＄の分2文字と通貨単位分
			PRINTFORM %SPACES(44-(STRLENS(文字列表示用)-2)-単位文字数)%
		ENDIF
	ENDIF
	RESETCOLOR
	
	表示数++
NEXT

PRINTL 

DRAWLINE
IF GROUPMATCH(ARG, 0, 4)
	IF LANGUAGE == "ENG"
		PRINTPLAIN 　CHARA  FILTER　
		PRINT [20000] Filter Setting
		PRINTPLAIN 　
		PRINTFORM [20001] Gender Filter：
		SELECTCASE OPTION:男女フィルタ
			CASE 0
				PRINT None
			CASE 1
				PRINT Show Male
			CASE 2
				PRINT Show Female
		ENDSELECT
		PRINTPLAIN 　
		PRINTFORML [20002] Dialogue Filter：\@ OPTION:口上キャラフィルタ ? ON # OFF \@
		PRINTPLAIN 　CUSTOM FILTER　
		PRINT [20003] Talent Filter1：
		IF OPTION:カスタムフィルタ１ >= 0 && OPTION:フィルタスイッチ１
			PRINTFORM %TALENTDISP(OPTION:カスタムフィルタ１)%
			SELECTCASE OPTION:フィルタスイッチ１
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTPLAIN 　
		PRINT [20005] Talent Filter2：
		IF OPTION:カスタムフィルタ２ >= 0 && OPTION:フィルタスイッチ２
			PRINTFORM %TALENTDISP(OPTION:カスタムフィルタ２)%
			SELECTCASE OPTION:フィルタスイッチ２
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTL 

		PRINTPLAIN 　             　
		PRINT [20007] Price Filter：
		IF OPTION:価格フィルタ != 0
			IF OPTION:価格フィルタ所持金依存
				CALL PASELI, @"{OPTION:価格フィルタ下限}＄ ～ Money"
			ELSE
				CALL PASELI, @"{OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄"
			ENDIF
			SELECTCASE OPTION:価格フィルタ
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTPLAIN 　
		PRINT [20008] BreastSize Filter：
		SELECTCASE OPTION:バストサイズフィルタ
			CASE 0
				PRINT None
			CASE 1
				PRINT Show none breast
			CASE 2
				PRINT Show little breast
			CASE 3
				PRINT Show basic breast
			CASE 4
				PRINT Show large breast
			CASE 5
				PRINT Show extra large breast
			CASE 6
				PRINT Show huge breast
		ENDSELECT

		PRINTPLAIN 　
		SIF !OPTION:体格フィルタ
			SETCOLORBYNAME gray
		PRINT [20009] Height Filter：
		SELECTCASE OPTION:体格フィルタ
			CASE 0
				PRINTL None
			CASE 1
				PRINTL Show petit
			CASE 2
				PRINTL Show child
			CASE 3
				PRINTL Show basic
			CASE 4
				PRINTL Show tall
		ENDSELECT
		RESETCOLOR

		PRINTPLAIN 　EXTRA  FILTER　
		PRINT [20010] Original Category：
		IF 登場作品フィルタ != ""
			PRINTFORM %登場作品フィルタ% 
		ELSE
			PRINT None 
		ENDIF
		PRINT [20011] Designer Category：
		IF キャラデザフィルタ != ""
			PRINTFORM %キャラデザフィルタ% 
		ELSE
			PRINT None 
		ENDIF
		PRINT [20012] Race Category：
		IF 種族フィルタ != ""
			PRINTFORML %種族フィルタ%
		ELSE
			PRINTL None
		ENDIF
		DRAWLINE
	ELSE
		PRINTPLAIN 　CHARA  FILTER　
		PRINT [20000] フィルタ設定
		PRINTPLAIN 　
		SIF !OPTION:男女フィルタ
			SETCOLORBYNAME gray
		PRINTFORM [20001] 男女フィルタ：
		SELECTCASE OPTION:男女フィルタ
			CASE 0
				PRINT 切
			CASE 1
				PRINT 男性を表示
			CASE 2
				PRINT 女性を表示
		ENDSELECT
		RESETCOLOR
		PRINTPLAIN 　
		CALL PRINTCOLORL, @"[20002] 口上キャラフィルタ：\@ OPTION:口上キャラフィルタ ? オン # オフ \@", @"\@ !OPTION:口上キャラフィルタ ? gray #\@"
		PRINTPLAIN 　CUSTOM FILTER　
		SIF !OPTION:フィルタスイッチ１
			SETCOLORBYNAME gray
		PRINT [20003] カスタムフィルタ1：
		IF OPTION:カスタムフィルタ１ >= 0 && OPTION:フィルタスイッチ１
			PRINTFORM %TALENTNAME:(OPTION:カスタムフィルタ１)%
			SELECTCASE OPTION:フィルタスイッチ１
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN 　
		SIF !OPTION:フィルタスイッチ２
			SETCOLORBYNAME gray
		PRINT [20005] カスタムフィルタ2：
		IF OPTION:カスタムフィルタ２ >= 0 && OPTION:フィルタスイッチ２
			PRINTFORM %TALENTNAME:(OPTION:カスタムフィルタ２)%
			SELECTCASE OPTION:フィルタスイッチ２
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTL 

		PRINTPLAIN 　PRICE  FILTER　
		SIF !OPTION:価格フィルタ
			SETCOLORBYNAME gray
		PRINT [20007] 価格フィルタ：
		IF OPTION:価格フィルタ != 0
			IF OPTION:価格フィルタ所持金依存
				CALL PASELI, @"{OPTION:価格フィルタ下限}＄ ～ 所持金"
			ELSE
				CALL PASELI, @"{OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄"
			ENDIF
			SELECTCASE OPTION:価格フィルタ
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN 　
		SIF !OPTION:バストサイズフィルタ
			SETCOLORBYNAME gray
		PRINT [20008] バストサイズフィルタ：
		SELECTCASE OPTION:バストサイズフィルタ
			CASE 0
				PRINT 切
			CASE 1
				PRINT 絶壁キャラを表示
			CASE 2
				PRINT 貧乳キャラを表示
			CASE 3
				PRINT 並乳キャラを表示
			CASE 4
				PRINT 巨乳キャラを表示
			CASE 5
				PRINT 豊乳キャラを表示
			CASE 6
				PRINT 爆乳キャラを表示
		ENDSELECT
		RESETCOLOR

		PRINTPLAIN 　
		SIF !OPTION:体格フィルタ
			SETCOLORBYNAME gray
		PRINT [20009] 体格フィルタ：
		SELECTCASE OPTION:体格フィルタ
			CASE 0
				PRINTL 切
			CASE 1
				PRINTL 幼児キャラを表示
			CASE 2
				PRINTL 小柄キャラを表示
			CASE 3
				PRINTL 普通キャラを表示
			CASE 4
				PRINTL 長身キャラを表示
		ENDSELECT
		RESETCOLOR

		PRINTPLAIN 　EXTRA  FILTER　
		SIF 登場作品フィルタ == ""
			SETCOLORBYNAME gray
		PRINT [20010] 原作別カテゴリ：
		IF 登場作品フィルタ != ""
			PRINTFORM %登場作品フィルタ% 
		ELSE
			PRINT 切 
		ENDIF
		RESETCOLOR
		SIF キャラデザフィルタ == ""
			SETCOLORBYNAME gray
		PRINT [20011] キャラデザイナー別カテゴリ：
		IF キャラデザフィルタ != ""
			PRINTFORM %キャラデザフィルタ% 
		ELSE
			PRINT 切 
		ENDIF
		RESETCOLOR
		SIF 種族フィルタ == ""
			SETCOLORBYNAME gray
		PRINT [20012] 種族別カテゴリ：
		IF 種族フィルタ != ""
			PRINTFORML %種族フィルタ%
		ELSE
			PRINTL 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN　　RADOR　FILTER　
		SIF !OPTION:開発難易度フィルタ
			SETCOLORBYNAME gray
		PRINT [20013] 開発難易度フィルタ：
		IF OPTION:開発難易度フィルタ
			PRINTFORM {OPTION:開発難易度フィルタ%1000}～{OPTION:開発難易度フィルタ/1000}
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN 　
		SIF !OPTION:屈服難易度フィルタ
			SETCOLORBYNAME gray
		PRINT [20014] 屈服難易度フィルタ：
		IF OPTION:屈服難易度フィルタ
			PRINTFORM {OPTION:屈服難易度フィルタ%1000}～{OPTION:屈服難易度フィルタ/1000}
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN 　
		SIF !OPTION:売却額フィルタ
			SETCOLORBYNAME gray
		PRINT [20015] 売却額フィルタ：
		IF OPTION:売却額フィルタ
			PRINTFORM {OPTION:売却額フィルタ%1000}～{OPTION:売却額フィルタ/1000}
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTPLAIN 　
		SIF !OPTION:性経験フィルタ
			SETCOLORBYNAME gray
		PRINT [20016] 性経験フィルタ：
		IF OPTION:性経験フィルタ
			PRINTFORM {OPTION:性経験フィルタ%1000}～{OPTION:性経験フィルタ/1000}
		ELSE
			PRINT 切
		ENDIF
		RESETCOLOR
		PRINTL 
		PRINTPLAINFORM %SPACESLENS("　CUSTOM FILTER　")%
		PRINT [20020] 表示キャラを増やす 
		PRINTL [20021] 表示キャラを減らす
		DRAWLINE
	ENDIF
ENDIF

PRINTPLAINFORM 　PAGE.{TFLAG:ページ数+1, 2, LEFT}　
IF LANGUAGE == "ENG"
	CALL PRINTSPACE, TFLAG:ページ数, "[10001] Prev Page "
ELSE
	CALL PRINTSPACE, TFLAG:ページ数, "[10001] 前のページ "
ENDIF
IF LANGUAGE == "ENG"
	SELECTCASE ARG
		CASE 0
			PRINT [10000] Don't unlock 
		CASE 2
			PRINT [10000] Don't change 
		CASE 4
			PRINT [10000] Set from the beggining 
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 0
			PRINT [10000] 購入しない 
		CASE 2
			PRINT [10000] 変更しない 
		CASE 4
			PRINT [10000] 最初から設定する 
		CASEELSE
			SIF CSVEDITOR
				PRINT [10000] 戻る 
	ENDSELECT
ENDIF
IF LANGUAGE == "ENG"
	CALL PRINTSPACE, 次ページあり, "[10009] Next Page "
ELSE
	CALL PRINTSPACE, 次ページあり, "[10009] 次のページ "
ENDIF
SIF ARG == 0
	PRINT [10010] ランダムセレクト
PRINTL 

INPUT
SELECTCASE RESULT
	CASE 10000
		SELECTCASE ARG
			CASE 0, 2, 4
				VARLINE = 0
				RETURN 10000
			CASEELSE
				IF CSVEDITOR
					VARLINE = 0
					RETURN 10000
				ENDIF
		ENDSELECT
	CASE 10001
		SIF TFLAG:ページ数
			TFLAG:ページ数--
	CASE 10009
		SIF 次ページあり
			TFLAG:ページ数++
	CASE 10010
		;RAND:10000でやると各機種先頭キャラに偏るので一度別変数に入れてから決める
		VARSET ランセレ
		FOR LCOUNT, 0, 10000
			SIF EXISTCSV(LCOUNT) && 選択可能:LCOUNT && !CCB:LCOUNT && GETCHARA(LCOUNT) < 0
				ランセレ:FINDELEMENT(ランセレ, 0) = LCOUNT
		NEXT
		SIF ランセレ:0 == 0
			CONTINUE
		RETURN ランセレ:(RAND:FINDELEMENT(ランセレ, 0))
	CASE 20000
		SELECTCASE ARG
			CASE 0, 4
				CALL CHARA_FILTER
			CASEELSE
				CONTINUE
		ENDSELECT
	CASE 20001, 20003, 20005, 20007, 20008, 20009
		SELECTCASE ARG
			CASE 0, 4
				CALL CHARA_FILTER2, (RESULT-20000)
				TFLAG:ページ数 = 0
			CASEELSE
				CONTINUE
		ENDSELECT
	CASE 20002
		INVERTBIT OPTION:口上キャラフィルタ, 0
	CASE 20010
		CALL 登場作品フィルタ設定
		TFLAG:ページ数 = 0
	CASE 20011
		CALL キャラデザフィルタ設定, キャラデザフィルタ
		TFLAG:ページ数 = 0
	CASE 20012
		CALL 種族フィルタ設定, 種族フィルタ
		TFLAG:ページ数 = 0
	CASE 20013
		CALL レーダーフィルタ, "開発難易度"
	CASE 20014
		CALL レーダーフィルタ, "屈服難易度"
	CASE 20015
		CALL レーダーフィルタ, "売却額"
	CASE 20016
		CALL レーダーフィルタ, "性経験"
	CASE 20020
		表示上限 += 30
	CASE 20021
		SIF 表示上限 > 30
			表示上限 -= 30
	CASEELSE
		IF EXISTCSV(RESULT) && 選択可能:RESULT
			VARLINE = 0
			DRAWLINE
			RETURN RESULT
		ELSEIF ARG:1
			VARLINE = 0
			DRAWLINE
			RETURN RESULT
		ENDIF
		CONTINUE
ENDSELECT

GOTO リスト表示

@CHARA_FILTER
TFLAG:ページ数 = 0
DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL You can set filter the characters.
ELSE
	PRINTL キャラ購入画面にフィルタをかけることができます
ENDIF
PRINTL 
IF LANGUAGE == "ENG"
	SELECTCASE OPTION:男女フィルタ
		CASE 0
			PRINTL [1] Show male characters only
		CASE 1
			PRINTL [1] Show female characters only
		CASE 2
			PRINTL [1] Show all characters
	ENDSELECT
ELSE
	SELECTCASE OPTION:男女フィルタ
		CASE 0
			PRINTL [1] 男性キャラだけ表示する
		CASE 1
			PRINTL [1] 女性キャラだけ表示する
		CASE 2
			PRINTL [1] 全キャラ表示する
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [2] Set talent filter 1(Now:
	IF OPTION:カスタムフィルタ１ >= 0
		PRINTFORML 【%TALENTDISP(OPTION:カスタムフィルタ１)%】)
	ELSE
		PRINTL None)
	ENDIF
ELSE
	PRINT [2] カスタムフィルタ1を設定する（現在
	IF OPTION:カスタムフィルタ１ >= 0
		PRINTFORML 【%TALENTDISP(OPTION:カスタムフィルタ１)%】のフィルタ）
	ELSE
		PRINTL フィルタは設定されていません）
	ENDIF
ENDIF

IF LANGUAGE == "ENG"
	PRINT [3] Switch talent filter 1(Now
	SELECTCASE OPTION:フィルタスイッチ１
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show characters】)
		CASE 2
			PRINTL 【Hide characters】)
	ENDSELECT
ELSE
	PRINT [3] カスタムフィルタ1の切り替え（現在
	SELECTCASE OPTION:フィルタスイッチ１
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [4] Set talent filter 2(Now:
	IF OPTION:カスタムフィルタ２ >= 0
		PRINTFORML 【%TALENTDISP(OPTION:カスタムフィルタ２)%】)
	ELSE
		PRINTL None)
	ENDIF
ELSE
	PRINT [4] カスタムフィルタ2を設定する（現在
	IF OPTION:カスタムフィルタ２ >= 0
		PRINTFORML 【%TALENTDISP(OPTION:カスタムフィルタ２)%】のフィルタ）
	ELSE
		PRINTL フィルタは設定されていません）
	ENDIF
ENDIF

IF LANGUAGE == "ENG"
	PRINT [5] Switch talent filter 2(Now
	SELECTCASE OPTION:フィルタスイッチ２
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show characters】)
		CASE 2
			PRINTL 【Hide characters】)
	ENDSELECT
ELSE
	PRINT [5] カスタムフィルタ2の切り替え（現在
	SELECTCASE OPTION:フィルタスイッチ２
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [6] Set price filter(Now
	SELECTCASE OPTION:価格フィルタ
		CASE 0
			PRINTL :None)
		CASEELSE
			IF OPTION:価格フィルタ所持金依存
				CALL PASELIL, @"【{OPTION:価格フィルタ下限}＄ ～ Money】)"
			ELSE
				CALL PASELIL, @"【{OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄】)"
			ENDIF
	ENDSELECT
ELSE
	PRINT [6] 価格フィルタを設定する（現在
	SELECTCASE OPTION:価格フィルタ
		CASE 0
			PRINTL オフ）
		CASEELSE
			IF OPTION:価格フィルタ所持金依存
				CALL PASELIL, @"【{OPTION:価格フィルタ下限}＄ ～ 所持金】のフィルタ）"
			ELSE
				CALL PASELIL, @"【{OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄】のフィルタ）"
			ENDIF
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [7] Switch price filter(Now
	SELECTCASE OPTION:価格フィルタ
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show characters】）
		CASE 2
			PRINTL 【Hide characters】）
	ENDSELECT
ELSE
	PRINT [7] 価格フィルタの切り替え（現在
	SELECTCASE OPTION:価格フィルタ
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [8] BriestSize filter(Now:
	SELECTCASE OPTION:バストサイズフィルタ
		CASE 0
			PRINTL None)
		CASE 1
			PRINTL Show none breast characters)
		CASE 2
			PRINTL Show little breast characters)
		CASE 3
			PRINTL Show basic breast characters)
		CASE 4
			PRINTL Show large breast characters)
		CASE 5
			PRINTL Show extra large breast characters)
		CASE 6
			PRINTL Show huge breast characters)
	ENDSELECT
ELSE
	PRINT [8] バストサイズフィルタの切り替え（現在
	SELECTCASE OPTION:バストサイズフィルタ
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 絶壁キャラを表示）
		CASE 2
			PRINTL 貧乳キャラを表示）
		CASE 3
			PRINTL 並乳キャラを表示）
		CASE 4
			PRINTL 巨乳キャラを表示）
		CASE 5
			PRINTL 豊乳キャラを表示）
		CASE 6
			PRINTL 爆乳キャラを表示）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [9] Height filter(Now:
	SELECTCASE OPTION:体格フィルタ
		CASE 0
			PRINTL None)
		CASE 1
			PRINTL Show petit characters)
		CASE 2
			PRINTL Show child characters)
		CASE 3
			PRINTL Show basic characters)
		CASE 4
			PRINTL Show tall characters)
	ENDSELECT
ELSE
	PRINT [9] 体格フィルタの切り替え（現在
	SELECTCASE OPTION:体格フィルタ
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 幼児キャラを表示）
		CASE 2
			PRINTL 小柄キャラを表示）
		CASE 3
			PRINTL 普通キャラを表示）
		CASE 4
			PRINTL 長身キャラを表示）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINTL [0] Back
ELSE
	PRINTL [0] 戻る
ENDIF
BINPUT
SIF !RESULT
	RETURN 0

CALL CHARA_FILTER2, RESULT
TFLAG:ページ数 = 0
RESTART

@CHARA_FILTER2, ARG
TFLAG:ページ数 = 0
SELECTCASE ARG
	CASE 1
		SELECTCASE OPTION:男女フィルタ
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show male characters only.
				ELSE
					PRINTW 男性キャラのみ表示します
				ENDIF
				OPTION:男女フィルタ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Show female characters only.
				ELSE
					PRINTW 女性キャラのみ表示します
				ENDIF
				OPTION:男女フィルタ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Show all characters.
				ELSE
					PRINTW 全キャラ表示します
				ENDIF
				OPTION:男女フィルタ = 0
		ENDSELECT
	CASE 2
		DO
			CALL TALENT_PRINT
			DRAWLINE
			IF LANGUAGE == "ENG"
				PRINT [1000] Back 
				PRINTL [1001] Clear filter
			ELSE
				PRINT [1000] 戻る 
				PRINTL [1001] フィルタをリセット
			ENDIF
			BINPUT
			SELECTCASE RESULT
				CASE 1000
					RETURN
				CASE 1001
					IF LANGUAGE == "ENG"
						PRINTW Filter was been cleared.
					ELSE
						PRINTW フィルタをリセットしました
					ENDIF
					OPTION:フィルタスイッチ１ = 0
				CASEELSE
					OPTION:カスタムフィルタ１ = RESULT
					SIF OPTION:フィルタスイッチ１ == 0
						OPTION:フィルタスイッチ１ = 1
					IF LANGUAGE == "ENG"
						PRINTFORMW Now filtering "%TALENTDISP(OPTION:カスタムフィルタ１)%"
					ELSE
						PRINTFORMW 素質「%TALENTDISP(OPTION:カスタムフィルタ１)%」にフィルタをかけました。
					ENDIF
			ENDSELECT
			RETURN
		LOOP 1
	CASE 3
		SELECTCASE OPTION:フィルタスイッチ１
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				OPTION:フィルタスイッチ１ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				OPTION:フィルタスイッチ１ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned Talent filter 1 off.
				ELSE
					PRINTW カスタムフィルタ1をオフにしました
				ENDIF
				OPTION:フィルタスイッチ１ = 0
		ENDSELECT
	CASE 4
		DO
			CALL TALENT_PRINT
			DRAWLINE
			IF LANGUAGE == "ENG"
				PRINT [1000] Back 
				PRINTL [1001] Clear filter
			ELSE
				PRINT [1000] 戻る 
				PRINTL [1001] フィルタをリセット
			ENDIF
			BINPUT
			SELECTCASE RESULT
				CASE 1000
					RETURN
				CASE 1001
					IF LANGUAGE == "ENG"
						PRINTW Filter was been cleared.
					ELSE
						PRINTW フィルタをリセットしました
					ENDIF
					OPTION:フィルタスイッチ２ = 0
				CASEELSE
					OPTION:カスタムフィルタ２ = RESULT
					SIF OPTION:フィルタスイッチ２ == 0
						OPTION:フィルタスイッチ２ = 1
					IF LANGUAGE == "ENG"
						PRINTFORMW Now filtering "%TALENTDISP(OPTION:カスタムフィルタ２)%"
					ELSE
						PRINTFORMW 素質「%TALENTDISP(OPTION:カスタムフィルタ２)%」にフィルタをかけました。
					ENDIF
			ENDSELECT
			RETURN
		LOOP 1
	CASE 5
		SELECTCASE OPTION:フィルタスイッチ２
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				OPTION:フィルタスイッチ２ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				OPTION:フィルタスイッチ２ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned Talent filter 2 off.
				ELSE
					PRINTW カスタムフィルタ2をオフにしました
				ENDIF
				OPTION:フィルタスイッチ２ = 0
		ENDSELECT
	CASE 6
		DO
			IF LANGUAGE == "ENG"
				PRINTL You can filter for price.
				CALL PASELIL, @"[101] Set price the min(Now:{OPTION:価格フィルタ下限}＄)"
				IF OPTION:価格フィルタ所持金依存
					PRINTL [102] Set price your money
				ELSE
					CALL PASELIL, @"[102] Set price the max(Now:{OPTION:価格フィルタ上限}＄)"
				ENDIF
				SIF !OPTION:価格フィルタ所持金依存
					PRINTL [103] Set price the max(Your money)
				IF OPTION:価格フィルタ所持金依存
					CALL PASELIL, @"Now:{OPTION:価格フィルタ下限}＄ ～ {MONEY}"
				ELSE
					CALL PASELIL, @"Now:{OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄"
				ENDIF
				DRAWLINE
				PRINTL [100] Back
			ELSE
				PRINTL 価格でフィルタをかけることができます
				CALL PASELIL, @"[101] 下限を設定する(現在{OPTION:価格フィルタ下限}＄)"
				IF OPTION:価格フィルタ所持金依存
					PRINTL [102] 上限を設定する(現在所持金まで)
				ELSE
					CALL PASELIL, @"[102] 上限を設定する(現在{OPTION:価格フィルタ上限}＄)"
				ENDIF
				SIF !OPTION:価格フィルタ所持金依存
					PRINTL [103] 上限を所持金にする
				IF OPTION:価格フィルタ所持金依存
					CALL PASELIL, @"現在の設定 {OPTION:価格フィルタ下限}＄ ～ 所持金"
				ELSE
					CALL PASELIL, @"現在の設定 {OPTION:価格フィルタ下限}＄ ～ {OPTION:価格フィルタ上限}＄"
				ENDIF
				DRAWLINE
				PRINTL [100] 戻る
			ENDIF
			BINPUT
			SELECTCASE RESULT
				CASE 100
					RETURN
				CASE 101
					IF LANGUAGE == "ENG"
						PRINTL Please enter min price.
					ELSE
						PRINTL 数値を入力してください
					ENDIF
					DO
						INPUT
						IF RESULT > OPTION:価格フィルタ上限 && !OPTION:価格フィルタ所持金依存
							IF LANGUAGE == "ENG"
								PRINTL Can't set expensive than max price.
							ELSE
								PRINTL 上限より高い値を設定することはできません
							ENDIF
						ELSE
							OPTION:価格フィルタ下限 = RESULT
							BREAK
						ENDIF
					LOOP 1
				CASE 102
					OPTION:価格フィルタ所持金依存 = 0
					IF LANGUAGE == "ENG"
						PRINTL Please enter max price.
					ELSE
						PRINTL 数値を入力してください
					ENDIF
					DO
						INPUT
						IF RESULT < OPTION:価格フィルタ下限
							IF LANGUAGE == "ENG"
								PRINTL Can't set lower than min price.
							ELSE
								PRINTL 下限より低い値を設定することはできません
							ENDIF
						ELSE
							OPTION:価格フィルタ上限 = RESULT
							BREAK
						ENDIF
					LOOP 1
				CASE 103
					OPTION:価格フィルタ所持金依存 = 1
			ENDSELECT
		LOOP 1
	CASE 7
		SELECTCASE OPTION:価格フィルタ
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show characters range have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				OPTION:価格フィルタ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide characters range have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				OPTION:価格フィルタ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned price filter OFF.
				ELSE
					PRINTW 価格フィルタをオフにしました
				ENDIF
				OPTION:価格フィルタ = 0
		ENDSELECT
	CASE 8
		IF LANGUAGE == "ENG"
			PRINTL What size do you want?
			PRINTL [1] None breast
			PRINTL [2] Little breast
			PRINTL [3] Basic breast
			PRINTL [4] Large breast
			PRINTL [5] XLarge breast
			PRINTL [6] Huge breast
			PRINTL [0] Filter OFF
		ELSE
			PRINTL 表示したいキャラの胸のサイズを選んでください
			PRINTL [1] 絶壁
			PRINTL [2] 貧乳
			PRINTL [3] 並乳
			PRINTL [4] 巨乳
			PRINTL [5] 豊乳
			PRINTL [6] 爆乳
			PRINTL [0] フィルタオフ
		ENDIF
		BINPUT
		OPTION:バストサイズフィルタ = RESULT
	CASE 9
		IF LANGUAGE == "ENG"
			PRINTL What size do you want?
			PRINTL [1] Petit
			PRINTL [2] Child
			PRINTL [3] Basic
			PRINTL [4] Tall
			PRINTL [0] Filter OFF
		ELSE
			PRINTL 表示したいキャラの体格を選んでください
			PRINTL [1] 幼児体型
			PRINTL [2] 子供体型
			PRINTL [3] 普通体型
			PRINTL [4] 長身体型
			PRINTL [0] フィルタオフ
		ENDIF
		BINPUT
		OPTION:体格フィルタ = RESULT
ENDSELECT

RETURN 0

@TALENT_PRINT
#DIM DYNAMIC LCOUNT
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別", "バストサイズ", "隠しキャラ", "体格", ""
			CONTINUE
	ENDSELECT
	PRINTFORMLC [{LCOUNT}] %TALENTDISP(LCOUNT)%
NEXT
PRINTL 

@登場作品フィルタ設定
;#DIMS REF 登場作品フィルタ
#DIMS DYNAMIC 原作, 300
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 登場作品
#DIM DYNAMIC 登場作品２
#DIM DYNAMIC 登場作品３
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;動的変数だけど一応初期化しておく
VARSET 原作

;GETNUMの記述を省略
登場作品 = GETNUM(CSTR, "登場作品")
登場作品２ = GETNUM(CSTR, "登場作品２")
登場作品３ = GETNUM(CSTR, "登場作品３")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「原作」の配列に該当作品がまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, 登場作品) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品)), 0, 99, 1) == -1
			原作:FINDELEMENT(原作, "", 0, 99, 1) = %CSVCSTR(LCOUNT, 登場作品)%
	ENDIF
	;登場作品２は原作:100～199の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品２) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品２)), 100, 199, 1) == -1
			原作:FINDELEMENT(原作, "", 100, 199, 1) = %CSVCSTR(LCOUNT, 登場作品２)%
	ENDIF
	;登場作品３は原作:200～299の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品３) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品３)), 200, 299, 1) == -1
			原作:FINDELEMENT(原作, "", 200, 299, 1) = %CSVCSTR(LCOUNT, 登場作品３)%
	ENDIF
NEXT

DO
	;配列の使っていない範囲を検索してARRAYREMOVEで前詰めする
	始点 = FINDELEMENTF(原作, "")
	FOR LCOUNT, 始点, VARSIZE("原作")
		IF 原作:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		;配列の終端にたどり着いたら前詰め処理終了
		SIF LCOUNT == VARSIZE("原作")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 原作, 始点, 終点-始点
LOOP 1
$BREAK
FOR LCOUNT, FINDELEMENTF(原作, "")-1, -1, -1
	IF FINDELEMENT(原作, ESCAPE(原作:LCOUNT), 0, LCOUNT, 1) != -1
		DEBUGPRINTFORML %原作:LCOUNT%は{FINDELEMENTF(原作, ESCAPE(原作:LCOUNT))}にある
		ARRAYREMOVE 原作, LCOUNT, 1
	ENDIF
NEXT

PRINTL □表示したいキャラの登場作品を選んでください
FOR LCOUNT, 0, VARSIZE("原作")
	SIF 原作:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %原作:LCOUNT%
NEXT
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　原作別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

BINPUT
SELECTCASE RESULT
	CASE 0 TO VARSIZE("原作")
		;参照型変数でフィルタを返す
		登場作品フィルタ = %原作:RESULT%
	CASE -2
		登場作品フィルタ = 
ENDSELECT

@キャラデザフィルタ設定, キャラデザフィルタ
#DIMS REF キャラデザフィルタ
#DIMS DYNAMIC 作画, 1000
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC キャラデザ
#DIM DYNAMIC キャラデザ２
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;動的変数だけど一応初期化しておく
VARSET 作画

;GETNUMの記述を省略
キャラデザ = GETNUM(CSTR, "キャラデザ")
キャラデザ２ = GETNUM(CSTR, "キャラデザ２")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「作画」の配列に該当デザイナーがまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, キャラデザ) != ""
		SIF FINDELEMENTF(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ))) == -1
			作画:FINDELEMENT(作画, "", 0, 499, 1) = %CSVCSTR(LCOUNT, キャラデザ)%
	ENDIF
	;キャラデザ２は作画:500～999の範囲に入れる
	IF CSVCSTR(LCOUNT, キャラデザ２) != ""
		SIF FINDELEMENTF(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ２))) == -1
			作画:FINDELEMENT(作画, "", 500, 999, 1) = %CSVCSTR(LCOUNT, キャラデザ２)%
	ENDIF
NEXT

DO
	;配列の使っていない範囲を検索してARRAYREMOVEで前詰めする
	始点 = FINDELEMENTF(作画, "")
	FOR LCOUNT, 始点, VARSIZE("作画")
		IF 作画:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		;配列の終端にたどり着いたら前詰め処理終了
		SIF LCOUNT == VARSIZE("作画")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 作画, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラのデザイナーを選んでください
FOR LCOUNT, 0, VARSIZE("作画")
	SIF 作画:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %作画:LCOUNT%
NEXT
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　キャラデザイナー別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

BINPUT
SELECTCASE RESULT
	CASE 0 TO VARSIZE("作画")
		;参照型変数でフィルタを返す
		キャラデザフィルタ = %作画:RESULT%
	CASE -2
		キャラデザフィルタ = 
ENDSELECT

@種族フィルタ設定, 種族フィルタ
#DIMS REF 種族フィルタ
#DIMS DYNAMIC 種族名, 50
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 種族
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

VARSET 種族名

種族 = GETNUM(CSTR, "種族")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「種族名」の配列に該当種族名がまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, 種族) != ""
		SIF FINDELEMENTF(種族名, ESCAPE(CSVCSTR(LCOUNT, 種族))) == -1
			種族名:FINDELEMENT(種族名, "", 0, VARSIZE("種族名"), 1) = %CSVCSTR(LCOUNT, 種族)%
	ENDIF
NEXT

PRINTL □表示したいキャラの種族を選んでください
FOR LCOUNT, 0, VARSIZE("種族名")
	SIF 種族名:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %種族名:LCOUNT%
NEXT
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　種族別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

BINPUT
SELECTCASE RESULT
	CASE 0 TO VARSIZE("種族名")
		;参照型変数でフィルタを返す
		種族フィルタ = %種族名:RESULT%
	CASE -2
		種族フィルタ = 
ENDSELECT

@レーダーフィルタ, フィルタ対象
#DIMS DYNAMIC フィルタ対象
#DIM DYNAMIC 現在値

現在値 = GETVAR(@"OPTION:%フィルタ対象%フィルタ")

DRAWLINE
PRINTFORML %フィルタ対象%の下限(0)～上限(100)を指定します(現在:\@ !現在値 ? フィルタオフ # {現在値%1000}～{現在値/1000}\@)
PRINTL [1] 上限を設定する
PRINTL [2] 下限を設定する
PRINTL [3] オフにする
PRINTL [0] 戻る
BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		PRINTL 上限を入力してください
		INPUT
		SETVAR @"OPTION:%フィルタ対象%フィルタ", 現在値%1000+LIMIT(RESULT, 0, 100)*1000
	CASE 2
		PRINTL 下限を入力してください
		INPUT
		SETVAR @"OPTION:%フィルタ対象%フィルタ", 現在値/1000*1000+LIMIT(RESULT, 0, 100)
	CASE 3
		SETVAR @"OPTION:%フィルタ対象%フィルタ", 0
ENDSELECT

RESTART
