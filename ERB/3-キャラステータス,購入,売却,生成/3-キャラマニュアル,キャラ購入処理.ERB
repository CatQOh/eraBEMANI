@CHARA_MANUAL, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 開発難易度PT
#DIM DYNAMIC 性経験PT
#DIM DYNAMIC 売却額PT
#DIM DYNAMIC 屈服難易度PT

IF GETCHARA(ARG) > 0
	IF LANGUAGE == "ENG"
		PRINTFORMW %CSVENGNAME(ARG)% was been unlocked.
	ELSE
		PRINTFORMW %CSVNAMEF(ARG)%は既に購入されています
	ENDIF
	RETURN 1
ELSEIF CCB:ARG
	IF LANGUAGE == "ENG"
		PRINTFORMW %CSVENGNAME(ARG)% is can't unlock.
	ELSE
		PRINTFORMW %CSVNAMEF(ARG)%は購入できません
	ENDIF
	RETURN 1
ELSEIF ARG == NOF(MASTER)
	RETURN 1
ENDIF

IF LANGUAGE == "ENG"
	PRINTFORML 【%CSVENGNAME(ARG)%（%CSVNAMEF(ARG)%）】
ELSE
	FOR LCOUNT, 0, 100
		SIF CSVCSTR(ARG, LCOUNT) == ""
			BREAK
		CALL MANUALPRINT, CSVCSTR(ARG, LCOUNT)
		CALL NEWLINE
	NEXT
ENDIF
PRINTFORML 登場作品：「%CSVCSTR(ARG, GETNUM(CSTR, "登場作品"))%」\@ CSVCSTR(ARG, GETNUM(CSTR, "登場作品２")) != "" ? 「%CSVCSTR(ARG, GETNUM(CSTR, "登場作品２"))%」 #\@\@ CSVCSTR(ARG, GETNUM(CSTR, "登場作品３")) != "" ? 「%CSVCSTR(ARG, GETNUM(CSTR, "登場作品３"))%」 #\@

開発難易度PT = LIMIT(開発難易度PT計算(ARG)+50, 1, 100)
性経験PT = LIMIT(性経験PT計算(ARG), 1, 100)
CALL 売却額PT計算, ARG
売却額PT = LIMIT(RESULT, 1, 100)
屈服難易度PT = LIMIT(屈服難易度PT計算(ARG)+50, 1, 100)

HTML_PRINT IMGTOTAG(CREATE_RADOR(開発難易度PT, 性経験PT, 売却額PT, 屈服難易度PT), 1000, 1000)
FOR LCOUNT, 0, 9
	PRINTL
NEXT

IF CSVTALENT(ARG, GETNUM(TALENT, "アニマル"))
	IF LANGUAGE == "ENG"
		PRINTL ※This character is Pet chara. If you unlocked with little money, you can't clear games.
	ELSE
		PRINTL ※このキャラはペット候補なので売却できません。所持金が少ない状態で購入するとゲームの攻略が不可能になる場合があります
	ENDIF
ENDIF

IF MOD:チャレンジ
	IF LANGUAGE == "ENG"
		PRINTL ※You trying CHALLENGE mode. There has difference from this talents.
	ELSE
		PRINTL ※MOD:チャレンジを導入中です。表示されているマニュアル・素質とは齟齬がある場合があります
	ENDIF
ENDIF

;キャラマニュアル内でタグ付けされていれば特殊表示する
;これでCSV内でマニュアルも完結できる
;マニュアル用と題してるが普通の文章表示でもタグ付け機能が使える その際はLINEISEMPTYでの改行を忘れずに
@MANUALPRINT, マニュアル:0
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 検索位置
#DIMS DYNAMIC マニュアル, 2
#DIM DYNAMIC 始点
#DIM DYNAMIC 文字数
#DIMS DYNAMIC 字体, 3
#DIMS DYNAMIC 本文

;タグを探す
始点 = STRFINDU(マニュアル, "(", 検索位置)
文字数 = STRFINDU(マニュアル, ")", 検索位置)-始点
;タグ付けされてれば処理実行
IF 始点 >= 0
	;タグ付けまでの文章を通常表示
	PRINTFORM %SUBSTRINGU(マニュアル, 0, 始点)%
	;処理、内容、本文でSPLITする
	SPLIT SUBSTRINGU(マニュアル, 始点+1, 文字数-1), ":", 字体
	本文 = %字体:2%
	;字体:0 = 特殊処理の種類 字体:1 = 特殊処理の内容 本文 = 本文
	;必要に応じて追加するとよい
	SELECTCASE 字体:0
		;フォント変更系
		CASE "FONT"
			SELECTCASE 字体:1
				;太字
				CASE "BOLD"
					FONTBOLD
					CALL MANUALPRINT, 本文
					FONTREGULAR
				;打ち消し線
				CASE "STRIKE"
					FONTSTYLE 4
					CALL MANUALPRINT, 本文
					FONTSTYLE 0
			ENDSELECT
		;色指定 現時点では色名のみで数値指定はできない
		;指定した色名が無効であればデフォルト色表示
		CASE "COLOR"
			IF COLOR_FROMNAME(字体:1) == -1
				CALL MANUALPRINT, 本文
			ELSE
				SETCOLOR COLOR_FROMNAME(字体:1)
				CALL MANUALPRINT, 本文
				RESETCOLOR
			ENDIF
		;その他
		CASE "OTHER"
			SELECTCASE 字体:1
				;反転タグ 実質シャルロット専用 ALIGHNMENT命令の仕様上、タグ重複使用は不可能
				CASE "REVERSE"
					マニュアル:1 =
					FOR LCOUNT, STRLENSU(本文), -1, -1
						SELECTCASE CHARATU(本文, LCOUNT)
							CASE "【"
								マニュアル:1 += "】"
							CASE "】"
								マニュアル:1 += "【"
							CASE "「"
								マニュアル:1 += "」"
							CASE "」"
								マニュアル:1 += "「"
							CASE "["
								マニュアル:1 += "]"
							CASE "]"
								マニュアル:1 += "["
							CASE "（"
								マニュアル:1 += "）"
							CASE "）"
								マニュアル:1 += "（"
							CASEELSE
								マニュアル:1 += CHARATU(本文, LCOUNT)
						ENDSELECT
					NEXT
					ALIGNMENT RIGHT
					PRINTFORML %マニュアル:1%
					ALIGNMENT LEFT
				;WAIT このタグを使用すると改行されるので、文末までタグに含めないと表示がおかしくなる
				CASE "WAIT"
					CALL MANUALPRINT, 本文
					WAIT
			ENDSELECT
	ENDSELECT
	;次の始動位置をタグ終了後の次の文字に設定
	検索位置 = STRFINDU(マニュアル, 本文)+STRLENSU(本文)
	IF 検索位置 < STRLENSU(マニュアル)
		;残りの文章が閉じカッコならループ終了
		IF 検索位置+1 == STRLENSU(マニュアル) && CHARATU(マニュアル, 検索位置) == ")"
			RETURN
		;残りの文章が閉じカッコ以外にもあれば再帰処理
		ELSE
			;閉じカッコの分+1
			CALL MANUALPRINT, SUBSTRINGU(マニュアル, 検索位置+1)
			RETURN
		ENDIF
	ENDIF
;タグが無ければ通常表示
ELSE
	PRINTFORM %マニュアル%
	RETURN
ENDIF

@CHARA_BUY_CHECK, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 素質表示済み

IF !EXISTCSV(ARG)
	PRINTL 
	IF LANGUAGE == "ENG"
		PRINTW Does not exist CSV.
	ELSE
		PRINTW CSVが未実装です（仕様）
	ENDIF
	RETURN 1
ELSEIF MONEY < CSVCFLAG(ARG, 50)
	PRINTL 
	IF LANGUAGE == "ENG"
		PRINTW Your money not enough.
	ELSE
		PRINTW ＜金額が足りません＞
	ENDIF
	RETURN 1
ELSE
	SIF LANGUAGE == "ENG"
		GOTO SHOW_TALENTS
	DO
		PRINTL 
		IF LANGUAGE == "ENG"
			PRINTL Are you unlock this character?
			PRINTL [0]Yes
			PRINTL [1]No
		ELSE
			PRINTL 購入しますか？
			PRINTL [0]はい
			PRINTL [1]いいえ
		ENDIF
		SIF !素質表示済み
			PRINTL [2]素質を表示する
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CALL CHARA_ADD, ARG
				CALL PAYMENT, -CSVCFLAG(ARG, GETNUM(CFLAG, "価格")), @"キャラクター「%CSVNAMEF(ARG)%」購入"
				CALL プレイログ記録, @"%CSVNAMEF(ARG)%を購入"
				IF LANGUAGE == "ENG"
					PRINTFORMW ＜You unlocked %CSVENGNAME(ARG)%＞
				ELSE
					PRINTFORMW ＜%CSVNAMEF(ARG)%を解禁しました＞
				ENDIF
				TRYCALLFORM %CSVNAMERPS(ARG)%_CHARABUY
				TRYCALLFORM MASTER_%CSVNAMERPS(NOF(MASTER))%_CHARABUY, ARG
				SIF CFLAG:誕生日 == (MONTH*100)+DAYS
					CALL 実績解除, "アンハッピーバースデー"
				SIF CHARANAMEF(TARGET) == "つらら" && MONTH == 12 && (DAYS == 24 || DAYS == 25)
					CALL 実績解除, "クリスマス中止のお知らせ"
				RETURN 0
			CASE 1
				RETURN 1
			CASE 2
				$SHOW_TALENTS
				素質表示済み = 1
				PRINTL 
				IF LANGUAGE == "ENG"
					SELECTCASE CSVTALENT(ARG, GETNUM(TALENT, "性別"))
						CASE 0
							PRINT Sex:Unknown 
						CASE 1
							PRINT Sex:Male 
						CASE 2
							PRINT Sex:Female 
					ENDSELECT
					PRINTFORML Race:%CSVCSTR(ARG, GETNUM(CSTR, "種族"))%
				ELSE
					SELECTCASE CSVTALENT(ARG, GETNUM(TALENT, "性別"))
						CASE 0
							PRINT 性別:不明 
						CASE 1
							PRINT 性別:男性 
						CASE 2
							PRINT 性別:女性 
					ENDSELECT
					PRINTFORML 種族:%CSVCSTR(ARG, GETNUM(CSTR, "種族"))%
				ENDIF
				PRINT 素質
				FOR LCOUNT, 0, VARSIZE("TALENT")
					SELECTCASE TALENTNAME:LCOUNT
						CASE "性別"
							CONTINUE
						CASE "バストサイズ"
							IF LANGUAGE == "ENG"
								SELECTCASE CSVTALENT(ARG, LCOUNT)
									CASE 1
										PRINT 【NoneBreast】
									CASE 2
										PRINT 【LittleBreast】
									CASE 3
										PRINT 【BasicBreast】
									CASE 4
										PRINT 【LargeBreast】
									CASE 5
										PRINT 【XLargeBreast】
									CASE 6
										PRINT 【HugeBreast】
								ENDSELECT
							ELSE
								SELECTCASE CSVTALENT(ARG, LCOUNT)
									CASE 1
										PRINT 【絶壁】
									CASE 2
										PRINT 【貧乳】
									CASE 3
										PRINT 【並乳】
									CASE 4
										PRINT 【巨乳】
									CASE 5
										PRINT 【豊乳】
									CASE 6
										PRINT 【爆乳】
								ENDSELECT
							ENDIF
							CONTINUE
						CASE "体格"
							IF LANGUAGE == "ENG"
								SELECTCASE CSVTALENT(ARG, LCOUNT)
									CASE 1
										PRINT 【Petit】
									CASE 2
										PRINT 【Child】
									CASE 3
										PRINT 【Basic】
									CASE 4
										PRINT 【Tall】
								ENDSELECT
							ELSE
								SELECTCASE CSVTALENT(ARG, LCOUNT)
									CASE 1
										PRINT 【幼児体型】
									CASE 2
										PRINT 【小柄体型】
									CASE 3
										PRINT 【標準体型】
									CASE 4
										PRINT 【長身体型】
								ENDSELECT
							ENDIF
							CONTINUE
					ENDSELECT
					SIF CSVTALENT(ARG, LCOUNT)
						PRINTFORM 【%TALENTDISP(LCOUNT)%】
				NEXT
				PRINTL 
		ENDSELECT
	LOOP 1
ENDIF

RETURN 0

@CHARA_ADD, ARG, クローニング
#DIM DYNAMIC LCOUNT
;今のところは100人まで
#DIMS DYNAMIC 相性設定用, 100
#DIM DYNAMIC 名前例外処理
#DIMS DYNAMIC 相性値, 200
#DIM DYNAMIC クローニング
#DIM DYNAMIC 適正ABL
#DIM DYNAMIC 適正EXP

SIF !クローニング
	購入人数++
ADDCHARA ARG
TARGET = CHARANUM-1
SIF CSTR:種族 == ""
	DEBUGPRINTFORML %NAME:TARGET%に種族が設定されてない
;一部のキャラは性別を選択する
SIF ARG != 0
	CALL 性別選択, TARGET

IF !クローニング
	CCB:ARG = 1
	キャラ未購入 = 0
	IF !ORIGIN
		SELECTCASE ARG
			;マルクトとミリアは片方しか奴隷にできない
			CASE GETNO("マルクト")
				CCB:GETNO("ア・ミリア") = 1
			CASE GETNO("ア・ミリア")
				CCB:GETNO("マルクト") = 1
			;めうめうと白兎先生は片方しか奴隷にできない
			CASE GETNO("芽兎 めう")
				CCB:GETNO("白兎先生") = 1
			CASE GETNO("白兎先生")
				CCB:GETNO("芽兎 めう") = 1
			;つよしは片方しか奴隷にできない
			CASE GETNO("DJつよし")
				CCB:GETNO("OJつよし") = 1
			CASE GETNO("OJつよし")
				CCB:GETNO("DJつよし") = 1
			;テスタメントネメシスとミィム・ミクラーは片方しか奴隷にできない
			CASE GETNO("テスタメントネメシス")
				CCB:GETNO("ミィム・ミクラー") = 1
			CASE GETNO("ミィム・ミクラー")
				CCB:GETNO("テスタメントネメシス") = 1
		ENDSELECT
	ENDIF
ENDIF

;ハートに砂時計使っていた場合の処理
IF ARG == GETNO("マッドラット") && GETCHARANAME("ハート") >= 0 && TRANCERACE:GETCHARANAME("ハート"):0 != ""
	TALENT:回復早い = 0
	TALENT:回復遅い = 1
	TRANCE:TALENTF("回復早い") = 1
	TRANCE:TALENTF("回復遅い") = 1
ENDIF

BUYD = DAYS
BUYM = MONTH
BUYY = YEAR
;初期フラグいろいろ
CFLAG:生殖力担当魔術師 = -1
CFLAG:不妊担当魔術師 = -1
CFLAG:男性器提供者 = -1
CFLAG:モチベーション = 50
CFLAG:憑依 = -1
CALL 初期経験セット, TARGET
CFLAG:本への興味 = MAX(図書室ランク(), 5)
排卵からの経過時間:TARGET:0 = 1+RAND:280
;獣以外でも獣化したときのために発情期フラグは設定する
CFLAG:発情周期 = RAND:26

SIF TALENT:体格 == 0
	TALENT:体格 = 3

;人気持ちは予め人気度が付く
SIF TALENT:人気
	人気度:(NOF(TARGET)) += 100

;MOD適用
;性別不明キャラは上で選んでるので性別MODは適用されない
IF ゲームモード() != "段位認定"
	FOR LCOUNT, 0, VARSIZE("MOD")
		SIF ERDNAME(MOD, LCOUNT) == ""
			CONTINUE
		SELECTCASE ERDNAME(MOD, LCOUNT)
			CASE "男性化", "女性化", "性転換"
				IF CSVTALENT(NOF(TARGET), GETNUM(TALENT, "性別")) != 0
					SIF MOD:LCOUNT
						CALLFORM APPLY_MOD_%ERDNAME(MOD, LCOUNT)%, TARGET
				ENDIF
			CASEELSE
				SIF MOD:LCOUNT
					TRYCALLFORM APPLY_MOD_%ERDNAME(MOD, LCOUNT)%, TARGET
		ENDSELECT
	NEXT
ENDIF

IF PENIS(TARGET)
	CFLAG:ペニスサイズ = 2
	SIF TALENT:体格 <= 2 || TALENT:小人体型
		CFLAG:ペニスサイズ = 1
ENDIF
;上昇可能なABLは上げておく
FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABLNAME:LCOUNT == "魔法技能"
		CONTINUE
	適正ABL = 0
	適正EXP = 0
	REPEAT 10
		VARSET ABLUP
		VARSET ABLUPEXP
		TRYCALLFORM ABL_CHECK_%ABLNAME:LCOUNT%
		CALL ABLUP, ABLNAME:LCOUNT
		IF RESULT
			適正ABL = ABL:LCOUNT
			SIF !適正EXP
				適正EXP = MAXARRAY(ABLUPEXP)
		ENDIF
	REND
	SIF 適正ABL && LCOUNT >= GETNUM(ABL, "料理技能") && ABLNAME:LCOUNT != "栽培技能"
		DEBUGPRINTFORML %NAME:TARGET%の%ABLNAME:LCOUNT%は不適正 適正ABL:{適正ABL} 適正EXP:{適正EXP}
NEXT

MAXBASE:魔力 = ABL:魔法技能*1000
;アニマルなら助手可能にする
SIF TALENT:アニマル
	CFLAG:助手可能 = 1
TFLAG:ページ数 = 0
;MOD:チャレンジ導入中なら性経験を消してバージンにする
IF MOD:チャレンジ
	FOR LCOUNT, 0, 20
		ABL:LCOUNT = 0
	NEXT
	FOR LCOUNT, 0, 100
		EXP:LCOUNT = 0
	NEXT
	SELECTCASE SEX(TARGET)
		CASE 1
			TALENT:童貞 = 1
		CASE 2
			TALENT:処女 = 1
	ENDSELECT
	CALL 初期経験リセット(TARGET)
	CSTR:適用MOD += "[MOD:チャレンジ]"
ENDIF
;相性の設定
SPLIT CSTR:相性, "/", 相性設定用
FOR LCOUNT, 0, RESULT
	;なぜか行連結で行頭にスペースが入るので削る
	相性設定用:LCOUNT = %SUBSTRINGU(相性設定用:LCOUNT, 1)%
	SIF 相性設定用:LCOUNT == ""
		BREAK
	SPLIT 相性設定用:LCOUNT, "*", 相性値
	;サタンだけ相性設定用のアスタリスク文字が名前に入ってるので例外的に処理する
	;「サタン-P」+「L-イブリース」
	名前例外処理 = FINDELEMENTF(相性値, "サタン-P")
	IF 名前例外処理 >= 0
		相性値:名前例外処理 += @"*%相性値:(名前例外処理+1)%"
		ARRAYREMOVE 相性値, 名前例外処理+1, 1
	ENDIF
	SIF GETNO(相性値) >= 0
		RELATION:GETNO(相性値) = TOINT(相性値:1)
NEXT

IF MOD:SCAT
	CALL INITIALIZE_AMOUNT_FOR_DEFECATE, TARGET
	CALL INITIALIZE_AMOUNT_FOR_FECES, TARGET
ENDIF

;;;簡単なほどPTは低くなる
@開発難易度PT計算, 対象
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC PT
#DIM DYNAMIC LCOUNT
#DIM 計算済み, 10000

SIF 計算済み:対象
	RETURNF 計算済み:対象

;体力 男なら2000、女なら1800を基準にする
SELECTCASE CSVTALENT(対象, GETNUM(TALENT, "性別"))
	CASE 1
		PT -= (CSVBASE(対象, GETNUM(BASE, "体力"))-2000)/100
	CASE 2
		PT -= (CSVBASE(対象, GETNUM(BASE, "体力"))-1800)/100
ENDSELECT

;気力 男女ともに1600を基準にする
PT -= (CSVBASE(対象, GETNUM(BASE, "気力"))-1600)/100

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !CSVTALENT(対象, LCOUNT)
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "バストサイズ"
			SELECTCASE CSVTALENT(対象, LCOUNT)
				CASE 1
					PT -= 3
				CASE 2
					PT -= 2
			ENDSELECT
		CASE "体格"
			SELECTCASE CSVTALENT(対象, LCOUNT)
				CASE 1
					PT += 2
				CASE 2
					PT += 1
			ENDSELECT
		CASE "霊体"
			PT += 30
		CASE "ゾンビ", "無機物"
			PT += 15
		CASE "一線越えない", "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "小人体型"
			PT += 10
		CASE "無関心", "痛みに弱い", "快感の否定"
			PT += 5
		CASE "自制心", "感情乏しい", "解放", "濡れにくい", "勃ちにくい", "回復遅い"
			PT += 3
		CASE "保守的", "悲観的", "恥じらい", "暑さに弱い", "寒さに弱い"
			PT += 2
		CASE "習得遅い", "男嫌い", "女嫌い", "同族嫌悪"
			PT += 1
		CASE "恥薄い", "おもらし癖", "両刀", "男好き", "女好き", "子供好き", "吸血鬼", "日光浴", "月光浴"
			PT -= 1
		{
		CASE "好奇心", "楽観的", "目立ちたがり", "濡れやすい", "勃ちやすい", "習得早い", "自慰しやすい",
			 "マゾ", "母乳体質"
		}
			PT -= 2
		{
		CASE "痛みに強い", "快感に素直", "サキュバス", "インキュバス", "ふたなり", "去勢済み",
			 "スライム", "回復早い"
		}
			PT -= 3
		CASE "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "溶ける快"
			PT -= 10
		;これは初期素質には無いはず
		CASE "淫膣", "淫尻", "淫核", "淫乳"
			PT -= 20
		CASE "即落ち"
			PT -= 30
	ENDSELECT
NEXT

計算済み:対象 = PT

RETURNF PT

;;;豊富なほどPTは大きくなる
@性経験PT計算, 対象
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC PT = 1
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 経験
#DIM 計算済み, 10000

SIF 計算済み:対象
	RETURNF 計算済み:対象

SIF !CSVTALENT(対象, GETNUM(TALENT, "処女")) && CSVTALENT(対象, GETNUM(TALENT, "性別")) == 2
	PT += 20
SIF !CSVTALENT(対象, GETNUM(TALENT, "童貞")) && CSVTALENT(対象, GETNUM(TALENT, "性別")) == 1
	PT += 20

FOR LCOUNT, 0, VARSIZE("EXP")
	経験 = CSVEXP(対象,LCOUNT)
	SIF !経験
		CONTINUE
	SELECTCASE EXPNAME:LCOUNT
		CASE "性交経験", "膣射経験"
			PT += 経験/3
		CASE "Ｖ経験", "Ａ経験", "レズ経験", "ゲイ経験", "Ｖ拡張経験", "Ａ拡張経験", "触手経験"
			PT += 経験/5
		CASE "フェラ経験", "パイズリ経験", "射精管理経験", "尿道経験"
			PT += 経験/10
		CASE "絶頂経験", "射精経験", "自慰経験", "噴乳経験", "放尿経験"
			PT += 経験/30
	ENDSELECT
NEXT

計算済み:対象 = PT

RETURNF PT

@売却額PT計算, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 売却額補正
#DIM DYNAMIC LTARGET
#DIM 計算済み, 10000

SIF 計算済み:対象
	RETURN 計算済み:対象

SKIPDISP 1
ADDCHARA 対象
LTARGET = TARGET
TARGET = CHARANUM-1
CALL 売却額補正計算
売却額補正 = RESULT
SKIPDISP 0
DEBUGPRINTFORML 計算完了:%NAME:TARGET%
DELCHARA TARGET
TARGET = LTARGET

計算済み:対象 = 売却額補正*100/700

;4桁族を除くと700あたりが最高なので10～700で計算する
RETURN 売却額補正*100/700

;;;簡単なほどPTは低くなる
@屈服難易度PT計算, 対象
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC PT
#DIM DYNAMIC LCOUNT
#DIM 計算済み, 10000

SIF 計算済み:対象
	RETURNF 計算済み:対象

SIF CSVCSTR(対象, GETNUM(CSTR, "恋心")) != ""
	PT += 15
SIF CSVCSTR(対象, GETNUM(CSTR, "既婚者")) != ""
	PT += 30

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !CSVTALENT(対象, LCOUNT)
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "体格"
			SELECTCASE CSVTALENT(対象, LCOUNT)
				CASE 1
					PT -= 5
				CASE 2
					PT -= 4
				CASE 4
					PT += 2
			ENDSELECT
		CASE "一線越えない"
			PT += 10
		CASE "気丈", "生意気", "プライド高い", "自制心", "無関心", "快感の否定"
			PT += 5
		CASE "反抗的", "ツンデレ", "貞操観念", "抵抗"
			PT += 4
		{
		CASE "大人しい", "強気", "保守的", "抑圧", "中毒しやすい", "男嫌い", "女嫌い",
			 "同族嫌悪", "戦士"
		}
			PT += 3
		CASE "サボり魔", "感情乏しい", "汚臭敏感", "潔癖症", "サド"
			PT += 2
		CASE "ハイテンション", "目立ちたがり", "汚臭鈍感", "汚れ無視", "サキュバス", "インキュバス", "幼稚"
			PT -= 3
		{
		CASE "臆病", "弱気", "好奇心", "楽観的", "貞操無頓着", "弱味", "寂しがり屋", "倒錯的",
			 "両刀", "男好き", "女好き", "マゾ", "子供好き"
		}
			PT -= 4
		CASE "素直", "プライド低い", "解放", "献身的", "快感に素直", "妄信"
			PT -= 5
		CASE "即落ち"
			PT -= 30
	ENDSELECT
NEXT

計算済み:対象 = PT

RETURNF PT

@CREATE_RADOR, CORNER:0, CORNER:1, CORNER:2, CORNER:3
#FUNCTIONS
#DIM DYNAMIC XCOUNT
#DIM DYNAMIC YCOUNT
;四角形の各角の値 時計回りに0～3
#DIM DYNAMIC CORNER, 4
#DIM DYNAMIC DELAY
#DIM 生成済み


#DIM DYNAMIC CM, 5, 5

DELAY = GETMILLISECOND()

CM:0:0 = 0xFF, 0, 0, 0, 0
CM:1:0 = 0, 0xFF, 0, 0, 0
CM:2:0 = 0, 0, 0xFF, 0, 0
CM:3:0 = 0, 0, 0, 0xFF, 0
CM:4:0 = 0, 0x28, 0xFF, 0, 0

[SKIPSTART]
GDISPOSE 10
GDISPOSE 11
GDISPOSE 12
GDISPOSE 13
GDISPOSE 20
GDISPOSE 21
GDISPOSE 22
[SKIPEND]

;1000番台を作ってみる

;DEBUGPRINTFORML {GETMILLISECOND()-DELAY}

IF !SPRITECREATED("右下")
	GDISPOSE 1000
	GCREATE 1000, 100, 100
	FOR XCOUNT, 0, 100
		FOR YCOUNT, 0, 100
			SIF XCOUNT+YCOUNT < 100
				GSETCOLOR 1000, 0xA0FFD700, XCOUNT, YCOUNT
		NEXT
	NEXT
	SPRITECREATE "右下", 1000
ENDIF

IF !SPRITECREATED("右上")
	GDISPOSE 1001
	GCREATE 1001, 100, 100
	GDRAWGWITHROTATE 1001, 1000, -90
	SPRITECREATE "右上", 1001
ENDIF
IF !SPRITECREATED("左下")
	GDISPOSE 1002
	GCREATE 1002, 100, 100
	GDRAWGWITHROTATE 1002, 1000, 90
	SPRITECREATE "左下", 1002
ENDIF
IF !SPRITECREATED("左上")
	GDISPOSE 1003
	GCREATE 1003, 100, 100
	GDRAWGWITHROTATE 1003, 1000, 180
	SPRITECREATE "左上", 1003
ENDIF

;レーダー生成
GDISPOSE 1004
GCREATE 1004, 2000, 2000
GDRAWSPRITE 1004, "右上", 1000, 1000-CORNER:0*10, CORNER:1*10, CORNER:0*10
GDRAWSPRITE 1004, "右下", 1000, 1000, CORNER:1*10, CORNER:2*10
GDRAWSPRITE 1004, "左下", 1000-CORNER:3*10, 1000, CORNER:3*10, CORNER:2*10
GDRAWSPRITE 1004, "左上", 1000-CORNER:3*10, 1000-CORNER:0*10, CORNER:3*10, CORNER:0*10
SPRITEDISPOSE "RADOR"
SPRITECREATE "RADOR", 1004

IF !SPRITECREATED("RADOR_BACK")
	GDISPOSE 1005
	GCREATE 1005, 2400, 2400
	GDRAWSPRITE 1005, "右上", 1200, 200, 1000, 1000, CM:0:0
	GDRAWSPRITE 1005, "右下", 1200, 1200, 1000, 1000, CM:0:0
	GDRAWSPRITE 1005, "左下", 200, 1200, 1000, 1000, CM:0:0
	GDRAWSPRITE 1005, "左上", 200, 200, 1000, 1000, CM:0:0
	FOR XCOUNT, 0, 2400
		FOR YCOUNT, 0, 2400
			SIF INRANGE(XCOUNT, 1195, 1204) || INRANGE(YCOUNT, 1195, 1204)
				GSETCOLOR 1005, 0xFF000000, XCOUNT, YCOUNT
		NEXT
	NEXT
	SPRITECREATE "RADOR_BACK", 1005
ENDIF

GDISPOSE 1006
GCREATE 1006, 2600, 2500
GDRAWSPRITE 1006, "RADOR_BACK"
GDRAWSPRITE 1006, "RADOR", 200, 200

GSETFONT 1006, "Arial", 150
GSETBRUSH 1006, 0xD0FFFFFF

GSETPEN 1006, 0xD0EE82EE, 2
GDRAWTEXT 1006, @"開発難易度 {CORNER:0}", 750, 50
GSETPEN 1006, 0xD0FF0000, 2
GDRAWTEXT 1006, @"性経験 {CORNER:1}", 1800, 1100
GSETPEN 1006, 0xD000FFFF, 2
GDRAWTEXT 1006, @"売却額 {CORNER:2}", 850, 2200
GSETPEN 1006, 0xD000FF00, 2
GDRAWTEXT 1006, "屈服難易度", 0, 1100
GDRAWTEXT 1006, @"{CORNER:3}", 0, 1250

SPRITEDISPOSE "SQUARE_RADOR"
SPRITECREATE "SQUARE_RADOR", 1006

RETURNF "SQUARE_RADOR"

@レーダーチャートデバッグ
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	CALL 売却額PT計算, LCOUNT
	PRINTFORML %CSVCALLNAME(LCOUNT), 20, LEFT% 開発難易度PT:{開発難易度PT計算(LCOUNT), 4, LEFT} 性経験PT:{性経験PT計算(LCOUNT), 4, LEFT} 売却額PT:{RESULT, 4, LEFT} 屈服難易度PT:{屈服難易度PT計算(LCOUNT), 4, LEFT}
NEXT

WAIT
