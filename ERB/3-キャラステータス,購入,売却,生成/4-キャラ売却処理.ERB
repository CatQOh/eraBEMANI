;奴隷売却の処理
@CHARA_SELL, 金額取得
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LASSI
#DIM DYNAMIC PRICE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 金額取得
#DIMS DYNAMIC 追加情報
#DIM DYNAMIC 倍率
#DIM DYNAMIC 口上補正あり

;金額取得の引数を渡されたらそのキャラの金額を取得する
IF 金額取得
	SKIPDISP 1
	LTARGET = TARGET
	TARGET = 金額取得
	GOTO 計算
ENDIF

DRAWLINE
PRINTL □誰を売りますか？
FOR PAGING, 0, CHARANUM
	追加情報 = 
	IF ASSI == PAGING
		追加情報 = (助手)
	;ELSEIF ISASSI:PAGING
	;	追加情報 = (元助手)
	ELSEIF CFLAG:PAGING:助手可能
		追加情報 = (助手可)
	ENDIF
	CALL PAGING, 売却可能(PAGING), @"%追加情報%", "SELL SLAVE"
NEXT

SELECTCASE RESULT
	CASE -1
		RETURN 0
	CASE 0 TO CHARANUM
		;売却処理
		LTARGET = TARGET
		LASSI = ASSI
		TARGET = RESULT
ENDSELECT

SIF TARGET == MASTER
	RESTART

$計算
CALL 売却額計算
PRICE = RESULT

SETCOLOR 100, 150, 250
PRINTFORML 能力価格 = {PRICE}
RESETCOLOR

PRINTL 

CALL 売却額補正計算
倍率 = RESULT

PRINTFORML 素質倍率 = {倍率}\%
;補正値はどんなに低くても最低30%
IF 倍率 < 30
	PRINTL 最低保証 30%
	倍率 = 30
ENDIF 
;補正値の適用
PRICE *= 倍率
PRICE /= 100

SETCOLOR 100, 150, 250
PRINTFORML 能力価格×素質補正 = {PRICE}
RESETCOLOR

PRINTL 
倍率 = 100
IF TALENT:崩壊
	PRINTL 崩壊 -20%
	倍率 -= 20
ENDIF
IF TALENT:妊娠
	PRINTL 妊娠 -30%
	倍率 -= 30
ENDIF
IF TALENT:狂気
	PRINTL 狂気 -30%
	倍率 -= 30
ENDIF
IF TALENT:即落ち
	PRINTL 即落ち -80%
	倍率 -= 80
ENDIF

IF CSTR:眷属主 != "" && CSTR:種族 != "ヴァンパイア"
	PRINTL 眷属化 -20%
	倍率 -= 20
ENDIF

IF CFLAG:眷属化した人数 && CSTR:種族 != "ヴァンパイア"
	PRINTFORML 眷属主 -{20*CFLAG:眷属化した人数}\%
	倍率 -= 20*CFLAG:眷属化した人数
ENDIF

IF CFLAG:生殖力強化 == 1
	PRINTL 不妊治療中 -10%
	倍率 -= 10
ENDIF

IF CFLAG:卵管結紮 == 1
	PRINTL 不妊手術済み -10%
	倍率 -= 10
ENDIF
FOR LCOUNT, GETNUM(CSTR, "右腕の印"), GETNUM(CSTR, "胴体の印")+1
	IF CSTR:LCOUNT != ""
		SELECTCASE CSTRNAME:LCOUNT
			CASE "右腕の印", "左腕の印", "右足の印", "左足の印"
				PRINTFORML %CSTRNAME:LCOUNT% -10\%
				倍率 -= 10
			CASE "背中の印"
				PRINTFORML %CSTRNAME:LCOUNT% -15\%
				倍率 -= 15
			CASE "胴体の印"
				PRINTFORML %CSTRNAME:LCOUNT% -20\%
				倍率 -= 20
		ENDSELECT
	ENDIF
NEXT

IF CFLAG:人工物 && CSTR:種族 != "機械"
	PRINTL 人体錬成によって作られた -50%
	倍率 -= 50
ENDIF

SIF 倍率 != 100
	PRINTFORML 特殊素質補正 = {倍率}\%

IF 倍率 < 10
	PRINTFORML 最低保証10\%
	倍率 = 10
ENDIF

PRICE *= 倍率
PRICE /= 100

IF 倍率 != 100
	SETCOLOR 100, 150, 250
	PRINTFORML 価格×特殊素質補正 = {PRICE}
	RESETCOLOR
ENDIF

;難易度処理
SELECTCASE FLAG:難易度
	CASE -1
		PRINTL 難易度:BEGINNER ×120%
		TIMES PRICE , 1.20
	CASE 1
		PRINTL 難易度:NORMAL ×100%
		TIMES PRICE , 1.00
	CASE 2
		PRINTL 難易度:HYPER ×80%
		TIMES PRICE , 0.80
	CASE 3
		PRINTL 難易度:ANOTHER ×60%
		TIMES PRICE , 0.60
	CASE 4
		PRINTL 難易度:LEGGENDARIA ×200%
		TIMES PRICE , 2.00
ENDSELECT
SETCOLOR 100, 150, 250
PRINTFORML 価格×難易度補正 = {PRICE}
RESETCOLOR
;価格×種族補正
SELECTCASE CSTR:種族
	;人間,人間(？),獣
	CASE "人間", "人間(？)", "獣"
		倍率 = 100
	;宇宙人,獣人,人魚,ヴァンパイア
	CASE "宇宙人", "獣人", "人魚", "ヴァンパイア"
		倍率 = 120
	;改造人間,機械,精霊,妖精
	CASE "改造人間", "機械", "精霊", "妖精"
		倍率 = 110
	;天使,鳳凰
	CASE "天使", "鳳凰"
		倍率 = 140
	;悪魔,魔術師
	CASE "悪魔", "魔術師"
		倍率 = 130
	;神
	CASE "神"
		倍率 = 150
	;ゾンビ
	CASE "ゾンビ"
		倍率 = 95
	;スライム
	CASE "スライム"
		倍率 = 90
	;表記ゆれがあった場合
	CASEELSE
		倍率 = 100
ENDSELECT

IF 倍率 != 100
	PRINTL 
	SETCOLOR 100, 150, 250
	PRINTFORML 価格×種族(%CSTR:種族%)補正:{倍率}\% = {PRICE*倍率/100}
	RESETCOLOR
	PRICE *= 倍率
	PRICE /= 100
ENDIF
PRINTL 

SETCOLOR 100, 150, 250
CALL PASELIL, @"価格:{PRICE}＄＋元の価格:{CFLAG:価格}＄ = {PRICE+CFLAG:価格}＄"
RESETCOLOR
PRICE += CFLAG:価格

;元助手
;IF ISASSI:TARGET == 1
;	PRINTFORML 助手経験あり -50\%
;	TIMES PRICE , 0.50
;ENDIF

IF 金額取得
	TARGET = LTARGET
	SKIPDISP 0
	RETURN PRICE
ENDIF

TRYCALLFORM 売却額補正_%CSVNAMERPS(NO:TARGET)%
IF RESULT
	PRICE = RESULT
	口上補正あり = 1
ENDIF

;売却確認
PRINTFORM %NAME:TARGET%は
SETCOLOR 50, 150, 250
CALL PASELI, @"{PRICE}＄"
RESETCOLOR
PRINTFORML で売れそうです\@ 口上補正あり ? (口上補正) #\@
PRINTL 売却しますか？
PRINTL [0] はい
PRINTL [1] いいえ
CFLAG:売却寸止め = 1
BINPUT

SELECTCASE RESULT
	CASE 0
		CFLAG:売却額 = PRICE
		;売却時のイベント口上
		CALL KOJO_MESSAGE_EVENT, "売却時"
		IF RESULT == 1
			TARGET = LTARGET
			RESTART
		ENDIF
		SELECTCASE 月齢
			CASE 2300 TO 2599
				SIF FALLTYPE(TARGET) == 1
					CALL 実績解除, "下弦の月に願うは君への罰"
		ENDSELECT
		IF NOF(TARGET) != 0
			SELECTCASE CHARANAMEF(TARGET)
				;実績「Σかリアンを素数価格で売却する」
				CASE "Σ", "リアン"
					SIF ISPRIME(PRICE)
						CALL 実績解除, "I only Divide Me"
				;実績「緋浮美を140,000パセリ以上で売却する」
				CASE "梅桐 緋浮美"
					SIF PRICE >= 140000
						CALL 実績解除, "GOLD RUSH"
				;実績:オトベアを売り飛ばす
				CASE "オトベア"
					CALL 実績解除, "お前よりくまモンの方がいい！"
			ENDSELECT
		ENDIF
		バンめし = 0
		IF !CSVTALENT(NOF(MASTER), GETNUM(TALENT, "子供好き"))
			IF TALENT:体格 <= 2
				FLAG:小児性愛スコア += 3
				IF FLAG:小児性愛スコア >= 10 && !TALENT:MASTER:子供好き
					PRINTFORMW 度重なる児童人身売買で、%CALLNAME:MASTER%は子供に対して異常な性的嗜好を持つようになった
					PRINTFORMW %CALLNAME:MASTER%は[子供好き]を得た
					TALENT:MASTER:子供好き = 1
				ENDIF
				CALL SETMIN(FLAG:小児性愛スコア, 20)
			ELSE
				FLAG:小児性愛スコア -= 2
				IF FLAG:小児性愛スコア < 10 && TALENT:MASTER:子供好き
					PRINTFORMW %CALLNAME:MASTER%はしばらく児童の人身売買から離れていたせいか、子供に対する執着が薄れたようだ
					PRINTFORMW %CALLNAME:MASTER%は[子供好き]を失った
					TALENT:MASTER:子供好き = 0
				ENDIF
				CALL SETMAX(FLAG:小児性愛スコア, -20)
			ENDIF
		ENDIF
		CALL PAYMENT, PRICE, @"%NAME:TARGET%売却"
		CALL プレイログ記録, @"%NAME:TARGET%売却"
		CSTR:ロスト要因 = 売却
		CSTR:ロスト詳細 = {PRICE}＄の価格で売却された
		CALL CLEAR_CHARA, TARGET
	CASE 1
		TARGET = LTARGET
		RESTART
ENDSELECT

@CLEAR_CHARA, ARG, ARG:1, 死, 加工
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LASSI
#DIM DYNAMIC 死
#DIM DYNAMIC 加工

;アイドルユニットに属していた場合、そのユニットは解散
SIF CFLAG:ARG:ユニット番号
	CALL BREAKUP, CFLAG:ARG:ユニット番号
;監禁室を外す
SIF CFLAG:ARG:監禁
	監禁室使用状況:(CFLAG:ARG:監禁) = 

CFLAG:ARG:居住 = 0
IF !ARG:1
	CALL 配属解除, ARG, 1
	CALL CLEAR_FLAG, ARG
ENDIF

IF ARG:1
	DELCHARA ARG
ELSE
	CFLAG:ARG:キャラロスト = 1
ENDIF
IF ARG:1
	LTARGET = TARGET
	LASSI = ASSI
	SIF ARG == LTARGET
		TARGET = -1
	SIF ARG == LASSI
		ASSI = -1
	SIF ARG < LTARGET
		TARGET--
	SIF ARG < LASSI
		ASSI--
	SIF !引き継ぎ処理中
		CALLF メモリ解放, "キャラ消去"
ELSE
	SIF TARGET == ARG
		TARGET = -1
	SIF ASSI == ARG
		ASSI = -1
ENDIF

IF 死
	SIF GROUPMATCH(TFLAG:捕食相手, 父親取得(ARG), 母親取得(ARG))
		RETURN
	IF CHARANAMEF(ARG) == "ハート" && TRANCERACE:ARG:0 == ""
		;（主人死亡が面倒なので）主人マッドラットでハートが死んだ時何も起きない不具合が潜在的にあるはず 後で
		;マッドラットが先に死んだ際の処理をどうするかはアイデアがない
		IF !CCB:GETNO("マッドラット")
			PRINTFORML ハートの死により、その心臓の持ち主もどこかで死亡したようだ
			CCB:GETNO("マッドラット") = 1
		ELSEIF GETCHARANAME("マッドラット") > 0
			PRINTFORML マッドラットは心臓が停止し、死亡しました
			CSTR:GETCHARANAME("マッドラット"):ロスト要因 = 死亡
			CSTR:GETCHARANAME("マッドラット"):ロスト詳細 = 心臓が停止した
			CALL CLEAR_CHARA, GETCHARANAME("マッドラット"), 0, 1
		ENDIF
	ENDIF
	IF (GETCHARA(父親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(父親取得(ARG)):配属 != "博物館") || (GETCHARA(母親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(母親取得(ARG)):配属 != "博物館")
		;博物館で加工した場合はそっちで処理済みなので計算しない
		SIF GETCHARA(父親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(父親取得(ARG)):配属 != "博物館"
			PRINTFORM %CALLNAME:GETCHARA(父親取得(ARG))%
		SIF GETCHARA(父親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(父親取得(ARG)):配属 != "博物館" && GETCHARA(母親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(母親取得(ARG)):配属 != "博物館"
			PRINT と
		SIF GETCHARA(母親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(母親取得(ARG)):配属 != "博物館"
			PRINTFORM %CALLNAME:GETCHARA(母親取得(ARG))%
		PRINTFORML に%CALLNAME:ARG%の死を伝えますか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		IF RESULT == 0
			死亡キャラ = ARG
			SIF GETCHARA(父親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(父親取得(ARG)):配属 != "博物館"
				CALL 崩壊処理, GETCHARA(父親取得(ARG)), "子供の死"
			SIF GETCHARA(母親取得(ARG)) > 0 && !加工 && CSTR:GETCHARA(母親取得(ARG)):配属 != "博物館"
				CALL 崩壊処理, GETCHARA(母親取得(ARG)), "子供の死"
		ENDIF
	ENDIF
ENDIF

@売却額計算
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC PRICE
#DIM DYNAMIC 基礎値
#DIM DYNAMIC 初期値

;ABLによる基礎値計算
FOR LCOUNT, 0, VARSIZE("ABL")
	IF EXISTCSV(NOF(TARGET))
		初期値 = CSVABL(NOF(TARGET), LCOUNT)
	ELSE
		初期値 = 0
	ENDIF
	SELECTCASE ABLNAME:LCOUNT
		;従順、欲望、技巧、奉仕精神、露出癖のテーブル
		CASE "従順", "欲望", "技巧", "奉仕精神", "露出癖"
			SELECTCASE ABL:LCOUNT-初期値
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 1000
				CASE 2
					基礎値 = 1500
				CASE 3
					基礎値 = 2500
				CASE 4
					基礎値 = 4000
				CASE 5
					基礎値 = 6000
				CASE 6
					基礎値 = 8500
				CASE 7
					基礎値 = 11500
				CASE 8
					基礎値 = 15000
				CASE 9
					基礎値 = 19000
				CASEELSE
					基礎値 = 25000
			ENDSELECT
		;感度のテーブル
		CASE "Ｃ感覚", "Ｖ感覚", "Ａ感覚", "Ｂ感覚"
			SELECTCASE ABL:LCOUNT-初期値
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 1000
				CASE 2
					基礎値 = 1600
				CASE 3
					基礎値 = 2800
				CASE 4
					基礎値 = 4600
				CASE 5
					基礎値 = 7000
				CASE 6
					基礎値 = 10000
				CASE 7
					基礎値 = 13600
				CASE 8
					基礎値 = 17800
				CASE 9
					基礎値 = 22600
				CASEELSE
					基礎値 = 30000
			ENDSELECT
		;サドっ気、レズっ気、サドっ気、ホモっ気のテーブル
		CASE "マゾっ気", "サドっ気", "レズっ気", "ホモっ気"
			SELECTCASE ABL:LCOUNT-初期値
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 1000
				CASE 2
					基礎値 = 1400
				CASE 3
					基礎値 = 2200
				CASE 4
					基礎値 = 3400
				CASE 5
					基礎値 = 5000
				CASE 6
					基礎値 = 7000
				CASE 7
					基礎値 = 9400
				CASE 8
					基礎値 = 12200
				CASE 9
					基礎値 = 15400
				CASEELSE
					基礎値 = 20000
			ENDSELECT
		;料理技能、家事技能、工作技能、歌唱技能、演奏技能、撮影技能、舞踊技能、魔法技能、飼育技能、栽培技能のテーブル
		CASE "料理技能", "家事技能", "工作技能", "歌唱技能", "演奏技能", "撮影技能", "舞踊技能", "魔法技能", "飼育技能", "栽培技能"
			SELECTCASE ABL:LCOUNT-初期値
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 800
				CASE 2
					基礎値 = 1100
				CASE 3
					基礎値 = 1700
				CASE 4
					基礎値 = 2600
				CASE 5
					基礎値 = 3800
				CASE 6
					基礎値 = 5300
				CASE 7
					基礎値 = 7100
				CASE 8
					基礎値 = 9200
				CASE 9
					基礎値 = 11600
				CASEELSE
					基礎値 = 15000
			ENDSELECT
		;中毒のテーブル
		CASE "自慰中毒", "精液中毒", "レズ中毒", "ゲイ中毒", "性交中毒", "噴乳中毒"
			SELECTCASE ABL:LCOUNT-初期値
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 2000
				CASE 2
					基礎値 = 3000
				CASE 3
					基礎値 = 5000
				CASE 4
					基礎値 = 8000
				CASE 5
					基礎値 = 12000
				CASE 6
					基礎値 = 17000
				CASE 7
					基礎値 = 23000
				CASE 8
					基礎値 = 30000
				CASE 9
					基礎値 = 48000
				CASEELSE
					基礎値 = 50000
			ENDSELECT
		;メス度のテーブル
		;売却額が低くなりがちな男キャラ専用ステータスなので高い
		CASE "メス度"
			SELECTCASE ABL:メス度
				CASE 0
					基礎値 = 0
				CASE 1
					基礎値 = 5000
				CASE 2
					基礎値 = 7000
				CASE 3
					基礎値 = 10000
				CASE 4
					基礎値 = 14000
				CASE 5
					基礎値 = 20000
				CASE 6
					基礎値 = 28000
				CASE 7
					基礎値 = 40000
				CASE 8
					基礎値 = 55000
				CASE 9
					基礎値 = 75000
				CASEELSE
					基礎値 = 100000
			ENDSELECT
	ENDSELECT
	IF ABL:LCOUNT > 0
		IF 初期値
			PRINTFORML %ABLNAME:LCOUNT%LV{ABL:LCOUNT}-初期%ABLNAME:LCOUNT%LV{初期値}=LV{ABL:LCOUNT-初期値} +{基礎値}
		ELSE
			PRINTFORML %ABLNAME:LCOUNT%LV{ABL:LCOUNT} +{基礎値}
		ENDIF
	ENDIF
	PRICE += 基礎値
NEXT

RETURN PRICE

@売却額補正計算
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 倍率

倍率 = 100
;素質による売却額補正値計算
IF TALENT:処女
	倍率 += 100
	PRINTL 処女 +100%
ENDIF
IF TALENT:再生処女
	倍率 += 50
	PRINTL 再生処女 +50%
ENDIF

;素質による増減
;陥落素質
IF TALENT:恋慕
	PRINTL 恋慕 -30%
	倍率 -= 30
ENDIF
IF TALENT:淫乱
	PRINTL 淫乱 +30%
	倍率 += 30
ENDIF
IF TALENT:服従
	PRINTL 服従 +100%
	倍率 += 100
ENDIF
IF TALENT:親愛
	PRINTL 親愛 -50%
	倍率 -= 50
ENDIF
IF TALENT:娼婦
	PRINTL 娼婦 +60%
	倍率 += 60
ENDIF
IF TALENT:隷属
	PRINTL 隷属 +200%
	倍率 += 200
ENDIF

IF SEX(TARGET) == 1
	PRINTL 男 +30%
	倍率 += 30
ENDIF

;その他素質
FOR LCOUNT, 0 ,VARSIZE("TALENT")
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		;バストサイズ
		CASE "バストサイズ"
			SELECTCASE TALENT:バストサイズ
				;絶壁
				CASE 1
					PRINTL 絶壁 -20%
					倍率 -= 20
				;貧乳
				CASE 2
					PRINTL 貧乳 -10%
					倍率 -= 10
				;巨乳
				CASE 4
					PRINTL 巨乳 +20%
					倍率 += 20
				;豊乳
				CASE 5
					PRINTL 豊乳 +25%
					倍率 += 25
				;爆乳
				CASE IS >= 6
					PRINTL 爆乳 +30%
					倍率 += 30
			ENDSELECT
		CASE "体格"
			SELECTCASE TALENT:体格
				CASE 1
					PRINTL 幼児 +20%
					倍率 += 20
				CASE 2
					PRINTL 小柄 +10%
					倍率 += 10
			ENDSELECT
		;長いのは見づらいから数行に分ける
		;[素直]及び[解放]は[抵抗][抑圧]消滅時に付いたものならマイナス補正無効
		CASE "素直"
			IF !(NOF(TARGET) < 10000 && CSVTALENT(NOF(TARGET), GETNUM(TALENT, "抵抗")))
				PRINTL 素直 -20%
				倍率 -= 20
			ENDIF
		CASE "解放"
			IF !(NOF(TARGET) < 10000 && CSVTALENT(NOF(TARGET), GETNUM(TALENT, "抑圧")))
				PRINTL 解放 -20%
				倍率 -= 20
			ENDIF
		;-20%
		CASE "好奇心", "解放", "習得早い", "献身的", "快感に素直", "倒錯的", "回復早い", "弱気", "性器破損", "去勢済み"
			PRINTFORML %TALENTNAME:LCOUNT% -20\%
			倍率 -= 20
		;-10%
		{
		CASE "大人しい", "プライド低い", "楽観的", "目立ちたがり", "貞操無頓着", "恥薄い", "濡れやすい", "自慰しやすい",
			 "汚臭鈍感", "汚れ無視", "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "日光浴", "月光浴", "幼稚", "寂しがり屋",
			 "ナルシスト", "男好き", "女好き", "ハイテンション", "衝動的", "買い物上手", "夢喰い", "サボり魔", "子供好き"
		}
			PRINTFORML %TALENTNAME:LCOUNT% -10\%
			倍率 -= 10
		;+10%
		{
		CASE "臆病", "舌使い", "針さばき", "猫舌", "おもらし癖", "小悪魔", "妖狐", "羽", "尻尾", "ネクロマンサー", "冷静",
			 "世話好き", "弱味", "溶ける快"
		}
			PRINTFORML %TALENTNAME:LCOUNT% +10\%
			倍率 += 10
		;+20%
		{
		CASE "悲観的", "恥じらい", "濡れにくい", "調合知識", "汚臭敏感", "男嫌い", "女嫌い", "ふたなり", "母乳体質", "絶倫",
			 "ファーリー", "潔癖症", "ドラゴン使い", "暑さに弱い", "寒さに弱い", "魔法使い"
		}
			PRINTFORML %TALENTNAME:LCOUNT% +20\%
			倍率 += 20
		;+30%
		{
		CASE "ツンデレ", "無関心", "貞操観念", "習得遅い", "薬毒耐性", "サド", "魅力", "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "母性",
			 "同族嫌悪", "料理人", "専業主婦", "絶対音感", "工学博士", "カメラマン"
		}
			PRINTFORML %TALENTNAME:LCOUNT% +30\%
			倍率 += 30
		;+40%
		CASE "保守的", "妄信", "回復遅い", "恋心", "強気", "感情乏しい", "ヤンデレ"
			PRINTFORML %TALENTNAME:LCOUNT% +40\%
			倍率 += 40
		;+50%
		{
		CASE "反抗的", "気丈", "プライド高い", "生意気", "自制心", "淫核", "淫膣", "淫尻", "淫乳",
			 "謎の魅力", "戦士", "抑圧", "抵抗", "足使用不可"
		}
			PRINTFORML %TALENTNAME:LCOUNT% +50\%
			倍率 += 50
		;+60%
		CASE "一線越えない"
			PRINTFORML %TALENTNAME:LCOUNT% +60\%
			倍率 += 60
		;+70%
		CASE "快感の否定", "小人体型", "無機物", "既婚者"
			PRINTFORML %TALENTNAME:LCOUNT% +70\%
			倍率 += 70
		;+300%
		CASE "リミットバースト"
			PRINTFORML %TALENTNAME:LCOUNT% +300\%
			倍率 += 300
	ENDSELECT
NEXT

;調教が進むと消えるマイナス素質（抑圧、抵抗）
IF NOF(TARGET) < 10000
	IF CSVTALENT(NOF(TARGET), GETNUM(TALENT, "抑圧")) && !TALENT:抑圧
		倍率 += 50
		PRINTL 抑圧消滅済み +50%
	ENDIF
	IF CSVTALENT(NOF(TARGET), GETNUM(TALENT, "抵抗")) && !TALENT:抵抗
		倍率 += 50
		PRINTL 抵抗消滅済み +50%
	ENDIF
ENDIF

;娼館で働かせた日数だけ倍率減
IF TALENT:傷物
	倍率 -= TALENT:傷物
	PRINTFORML 傷物 -{TALENT:傷物}\%
ENDIF

RETURN 倍率
