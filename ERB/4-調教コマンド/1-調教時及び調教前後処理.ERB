;;;BEGIN TRAIN相応の処理をする関数
@調教開始
CALL EVENTUSERTRAIN

DO
	CALL SHOW_STATUS
	CALL SHOW_USERCOM
	;INPUT
	;CALL USERCOM
LOOP TFLAG:調教終了 == 0

CALL 調教終了
SIF RESULT
	GOTO END

CALL ABL上昇画面

$END
CALL ターン終了


;;;調教前の処理
@EVENTUSERTRAIN
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC コマンドフィルタ変数
#DIM DYNAMIC 最大値
#DIM DYNAMIC アイテム種
#DIM DYNAMIC アイテム数
#DIM DYNAMIC 気温ボーナス
#DIM DYNAMIC CSTR始点
#DIM DYNAMIC TEQUIP始点

REDRAW 1

;BEGIN TRAINで行われる変数初期化を行う 上級売春やカジノから飛んでくるときの対策
CALL CVAR_RESET

;トイレ使用中(学園モード)ならオンにする
SIF トイレ使用中
	TEQUIP:トイレ = 1
SIF 野外プレイ中
	TEQUIP:野外プレイ = 1

SIF ASSI == -1 && ASSI:1 > 0
	SWAP ASSI, ASSI:1

TMASTER = MASTER

;初期着衣設定に応じて、服の設定をする
CSTR始点 = GETNUM(CSTR, "特殊")
TEQUIP始点 = GETNUM(TEQUIP, "特殊")
FOR LCOUNT, 0, 7
	SIF CSTR:(CSTR始点+LCOUNT) != ""
		TEQUIP:(TEQUIP始点+LCOUNT) = 1
	SIF CSTR:MASTER:(CSTR始点+LCOUNT) != ""
		TEQUIP:MASTER:(TEQUIP始点+LCOUNT) = 1
	IF ASSI > 0
		SIF CSTR:ASSI:(CSTR始点+LCOUNT) != ""
			TEQUIP:ASSI:(TEQUIP始点+LCOUNT) = 1
	ENDIF
	IF ASSI:1 > 0
		SIF CSTR:(ASSI:1):(CSTR始点+LCOUNT) != ""
			TEQUIP:(ASSI:1):(TEQUIP始点+LCOUNT) = 1
	ENDIF
NEXT

SELECTCASE FLAG:着衣設定
	;下着無し
	CASE 1
		TEQUIP:上半身下着 = 0
		TEQUIP:下半身下着 = 0
	;下着姿
	CASE 2
		TEQUIP:上半身上着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	;全裸
	CASE 3
		VARSET TEQUIP:MASTER:0, 0, GETNUM(TEQUIP, "特殊"), GETNUM(TEQUIP, "足元")+1
		VARSET TEQUIP:TARGET:0, 0, GETNUM(TEQUIP, "特殊"), GETNUM(TEQUIP, "足元")+1
		SIF ASSI > 0
			VARSET TEQUIP:ASSI:0, 0, GETNUM(TEQUIP, "特殊"), GETNUM(TEQUIP, "足元")+1
		SIF ASSI:1 > 0
			VARSET TEQUIP:(ASSI:1):0, 0, GETNUM(TEQUIP, "特殊"), GETNUM(TEQUIP, "足元")+1
	CASE 4
		VARSET TEQUIP:TARGET:0, 0, GETNUM(TEQUIP, "上半身上着"), GETNUM(TEQUIP, "下半身下着")+1
ENDSELECT

SIF CFLAG:死亡 && !TEQUIP:仮死薬
	CFLAG:ストレス値 = 0


;調教対象・助手が特定のキャラの場合、調教開始時に豹変することがある
CALL 豹変イベント, 1

;条件を満たしていればユリア天使化
SIF GROUPMATCH("鬼蝮 ユリア", CHARANAMEF(TARGET), CHARANAMEF(ASSI), CHARANAMEF(ASSI:1)) && GROUPMATCH("イングリット・オーリック・コーネル", CHARANAMEF(TARGET), CHARANAMEF(ASSI))
	CALL ユリア天使化

;実行コマンド、直前の実行コマンドフラグを初期化
PREVCOMS = 
SELECTCOMS = 

;二日酔い
IF CFLAG:二日酔い
	PRINTFORMW %CALLNAME:TARGET%は二日酔いを起こしたそうだ……
	BASE:体力 /= 2
	BASE:気力 /= 2
ENDIF

;服従系陥落なら主人と助手との相性が良くなったりする
IF TALENT:服従
	SIF 相性値(TARGET, MASTER) < 120
		CALL 相性加算, TARGET, MASTER, 120, 120
	SIF ASSI > 0 && 相性値(TARGET, ASSI) < 120
		CALL 相性加算, TARGET, ASSI, 120, 120
	SIF ASSI:1 > 0 && 相性値(TARGET, ASSI:1) < 120
		CALL 相性加算, TARGET, ASSI:1, 120, 120
ELSEIF TALENT:隷属
	SIF 相性値(TARGET, MASTER) < 150
		CALL 相性加算, TARGET, MASTER, 150, 150
	SIF ASSI > 0 && 相性値(TARGET, ASSI) < 150
		CALL 相性加算, TARGET, ASSI, 150, 150
	SIF ASSI > 0 && 相性値(TARGET, ASSI:1) < 150
		CALL 相性加算, TARGET, ASSI:1, 150, 150
ENDIF

;助手もまた然り
IF ASSI > 0
	IF TALENT:ASSI:服従
		SIF 相性値(ASSI, MASTER) < 120
			CALL 相性加算, ASSI, MASTER, 120, 120
		SIF 相性値(ASSI, TARGET) < 120
			CALL 相性加算, ASSI, TARGET, 120, 120
	ELSEIF TALENT:ASSI:隷属
		SIF 相性値(ASSI, MASTER) < 150
			CALL 相性加算, ASSI, MASTER, 150, 150
		SIF 相性値(ASSI, TARGET) < 150
			CALL 相性加算, ASSI, TARGET, 150, 150
	ENDIF
ENDIF

IF ASSI:1 > 0
	IF TALENT:(ASSI:1):服従
		SIF 相性値(ASSI:1, MASTER) < 120
			CALL 相性加算, ASSI:1, MASTER, 120, 120
		SIF 相性値(ASSI:1, TARGET) < 120
			CALL 相性加算, ASSI, TARGET, 120, 120
	ELSEIF TALENT:(ASSI:1):隷属
		SIF 相性値(ASSI:1, MASTER) < 150
			CALL 相性加算, ASSI:1, MASTER, 150, 150
		SIF 相性値(ASSI:1, TARGET) < 150
			CALL 相性加算, ASSI:1, TARGET, 150, 150
	ENDIF
ENDIF

;MAXBASEの設定
最大値 = 10000
;絶倫で半減
SIF TALENT:MASTER:絶倫
	最大値 /= 2
;幼児で2倍
SIF TALENT:MASTER:体格 == 1
	最大値 *= 2
;女性で2倍
SIF SEX(MASTER) == 2
	最大値 *= 2
;Ｃ鈍感なら1.5倍
SIF TALENT:MASTER:Ｃ鈍感
	TIMES 最大値, 1.50
;ミッドナイトスペシャル使用時は半減
SIF CFLAG:MASTER:ミッドナイトスペシャル
	最大値 /= 2
MAXBASE:MASTER:射精 = 最大値

;以下対象と助手にも同様の処理
最大値 = 10000
SIF TALENT:絶倫
	最大値 /= 2
SIF TALENT:体格 == 1
	最大値 *= 2
SIF SEX(TARGET) == 2
	最大値 *= 2
SIF TALENT:Ｃ鈍感
	TIMES 最大値, 1.50
SIF CFLAG:ミッドナイトスペシャル
	最大値 /= 2
MAXBASE:射精 = 最大値

FOR LCOUNT, 0, 2
	IF ASSI:LCOUNT >= 0
		最大値 = 10000
		SIF TALENT:(ASSI:LCOUNT):絶倫
			最大値 /= 2
		SIF TALENT:(ASSI:LCOUNT):体格 == 1
			最大値 *= 2
		SIF SEX(ASSI:LCOUNT) == 2
			最大値 *= 2
		SIF TALENT:(ASSI:LCOUNT):Ｃ鈍感
			TIMES 最大値, 1.50
		SIF CFLAG:(ASSI:LCOUNT):ミッドナイトスペシャル
			最大値 /= 2
		MAXBASE:(ASSI:LCOUNT):射精 = 最大値
	ENDIF
NEXT

IF TALENT:母乳体質
	最大値 = 10000
	SIF TALENT:淫乳
		最大値 /= 2
	SIF TALENT:Ｂ鈍感
		最大値 *= 2
	MAXBASE:母乳 = 最大値
ENDIF

MAXBASE:MASTER:触手射精 = 10000
MAXBASE:MASTER:ドラゴン射精 = 10000
SIF ASSI > 0
	MAXBASE:ASSI:ドラゴン射精 = 10000
SIF ASSI:1 > 0
	MAXBASE:(ASSI:1):ドラゴン射精 = 10000

; SCAT MOD
IF MOD:SCAT
	CALL INITIALIZE_AMOUNT_FOR_DEFECATE, TARGET
	CALL INITIALIZE_AMOUNT_FOR_FECES, TARGET
ENDIF

;SCAT2 MOD
SIF MOD:SCAT2
	CALL 尿意BASE初期設定, TARGET

;主人と調教対象と助手の射精を0に
BASE:MASTER:射精 = 0
BASE:射精 = 0
BASE:MASTER:ドラゴン射精 = 0
IF ASSI >= 0
	BASE:ASSI:射精 = 0
	BASE:ASSI:ドラゴン射精 = 0
ENDIF
IF ASSI:1 >= 0
	BASE:(ASSI:1):射精 = 0
	BASE:(ASSI:1):ドラゴン射精 = 0
ENDIF
;母乳も0に
BASE:母乳 = 0
;触手の射精も0に
BASE:MASTER:触手射精 = 0

;リビドーも0に
BASE:リビドー = 0

;射精限界数の確保
CFLAG:残数 = 10
SIF TALENT:絶倫
	CFLAG:残数 += 5
SIF !PENIS(TARGET) && !TALENT:去勢済み && !ISSLIME(TARGET)
	CFLAG:残数 = 0

CFLAG:MASTER:残数 = 10
SIF TALENT:MASTER:絶倫
	CFLAG:MASTER:残数 += 5
SIF エナジードライブ
	CFLAG:MASTER:残数 += エナジードライブ:1*3
SIF !PENIS(MASTER) && !TALENT:MASTER:去勢済み && !ISSLIME(TARGET)
	CFLAG:MASTER:残数 = 0

FOR LCOUNT, 0, 2
	IF ASSI:LCOUNT >= 0
		CFLAG:(ASSI:LCOUNT):残数 = 10
		SIF TALENT:(ASSI:LCOUNT):絶倫
			CFLAG:(ASSI:LCOUNT):残数 += 5
		SIF !PENIS(ASSI:LCOUNT) && !TALENT:(ASSI:LCOUNT):去勢済み && !ISSLIME(LCOUNT)
			CFLAG:(ASSI:LCOUNT):残数 = 0
	ENDIF
NEXT

CFLAG:射精飾り値 = 0
CFLAG:MASTER:射精飾り値 = 0
FOR LCOUNT, 0, 2
	SIF ASSI:LCOUNT >= 0
		CFLAG:(ASSI:LCOUNT):射精飾り値 = 0
NEXT

;賢者タイムをリセット
CFLAG:MASTER:賢者タイム = 0
CFLAG:賢者タイム = 0
FOR LCOUNT, 0, 2
	SIF ASSI:LCOUNT >= 0
		CFLAG:(ASSI:LCOUNT):賢者タイム = 0
NEXT

;TFLAGをリセット
VARSET TFLAG
ハッキング命令 = 

SIF TEQUIP:仮死薬
	CFLAG:死亡 = 1

;死体は体力気力魔力0
IF CFLAG:死亡 && !TEQUIP:仮死薬
	BASE:体力 = 0
	BASE:気力 = 0
	BASE:魔力 = 0
ENDIF
SIF CFLAG:死亡 || TEQUIP:睡眠
	TFLAG:実行判定無視 = 100

;;屍姦、睡姦中は口上表示しない(暫定)
;TFLAG:口上フラグ退避用 = OPTION:口上表示
;SIF CFLAG:死亡 || TEQUIP:睡眠
;	OPTION:口上表示 = 0

;淫乱なら膣内にヴァギナ汚れを付加
;淫乱ならペニスに先走り付加
SIF TALENT:淫乱 && VAGINA(TARGET) && !CFLAG:死亡
	SETBIT STAIN:膣内, 0
SIF TALENT:淫乱 && PENIS(TARGET) && !CFLAG:死亡
	SETBIT STAIN:ペニス, 1
FOR LCOUNT, 0, 2
	SIF ASSI:LCOUNT >= 0 && TALENT:(ASSI:LCOUNT):淫乱 && VAGINA(ASSI:LCOUNT)
		SETBIT STAIN:ASSI:膣内, 0
	SIF ASSI:LCOUNT >= 0 && TALENT:(ASSI:LCOUNT):淫乱 && PENIS(ASSI:LCOUNT)
		SETBIT STAIN:ASSI:ペニス, 1
NEXT
;主人は無条件で先走りと愛液付加
SIF VAGINA(MASTER)
	SETBIT STAIN:MASTER:ヴァギナ, 0
SIF PENIS(MASTER)
	SETBIT STAIN:MASTER:ペニス, 1

;媚薬の効果をリセット
CFLAG:MASTER:媚薬 = 0
CFLAG:媚薬 = 0
FOR LCOUNT, 0, 2
	SIF ASSI:LCOUNT > 0
		CFLAG:(ASSI:LCOUNT):媚薬 = 0
NEXT

;コンドーム射精フラグをリセット
TFLAG:使用済み避妊具 = 0

;調教者はMASTERにしとく
PLAYER = MASTER

;避妊具設定に応じて避妊具を付ける
IF ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 0) && PENIS(MASTER) && !Ｐ使用不可(MASTER)
	TEQUIP:MASTER:コンドーム = 1
	ITEM:コンドーム--
ENDIF
FOR LCOUNT, 0, 2
	IF ASSI:LCOUNT > 0 && ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 1) && PENIS(ASSI:LCOUNT) && !Ｐ使用不可(ASSI:LCOUNT)
		TEQUIP:(ASSI:LCOUNT):コンドーム = 1
		ITEM:コンドーム--
	ENDIF
NEXT
IF ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 2) && PENIS(TARGET) && !Ｐ使用不可(TARGET)
	TEQUIP:コンドーム = 1
	ITEM:コンドーム--
ENDIF
IF ITEM:搾精コンドーム && GETBIT(FLAG:避妊具設定, 3) && PENIS(TARGET) && !Ｐ使用不可(TARGET)
	TEQUIP:搾精コンドーム = 1
	ITEM:搾精コンドーム--
ENDIF

;ビデオボーナスのリセット
VARSET VIDEOBONUS

;写真関連のリセット
VARSET 写真内容
VARSET 写真価格

;ドラゴンの名前が無い場合、名前を「ドラゴン」に
SIF TALENT:MASTER:ドラゴン使い && CSTR:MASTER:ドラゴン == ""
	CSTR:MASTER:ドラゴン = ドラゴン
FOR LCOUNT, 0, 2
	SIF ASSI:LCOUNT >= 0 && TALENT:(ASSI:LCOUNT):ドラゴン使い && CSTR:(ASSI:LCOUNT):ドラゴン == ""
		CSTR:(ASSI:LCOUNT):ドラゴン = ドラゴン
NEXT

;種族宇宙人の感度変化
CALL 宇宙人感度変化, TARGET

;種族妖精ならランダムで消費アイテム取得
IF CSTR:種族 == "妖精"
	アイテム種 = RAND(200, 210)
	アイテム数 = RAND(1, 4)
	PRINTFORML %CALLNAME:TARGET%の部屋にあった{アイテム数}個の%ITEMNAME:アイテム種%を回収しました
	ITEM:アイテム種 += アイテム数
ENDIF

;鳳凰ならボーナス
IF CSTR:種族 == "鳳凰" && FLAG:気温 > 0
	気温ボーナス = FLAG:気温
	SELECTCASE 天気()
		CASE "晴れ"
		CASE "曇り"
			TIMES 気温ボーナス , 0.50
		CASEELSE
			TIMES 気温ボーナス , 0.20
	ENDSELECT
	;気温と天気で計算したボーナスを一部の初期PALAMに付与する
	FOR LCOUNT, 0, VARSIZE("PALAM")
		SELECTCASE PALAMNAME:LCOUNT
			CASE "快Ａ", "快Ｂ", "潤滑", "恭順", "欲情", "屈服", "習得"
				PALAM:LCOUNT += 気温ボーナス
			CASE "快Ｃ"
				SELECTCASE SEX(TARGET)
					CASE 1
						SIF PENIS(TARGET)
							PALAM:LCOUNT += 気温ボーナス
					CASE 2
						PALAM:LCOUNT += 気温ボーナス
				ENDSELECT
			CASE "快Ｖ"
				SIF VAGINA(TARGET)
					PALAM:LCOUNT += 気温ボーナス
		ENDSELECT
	NEXT
ENDIF

;特質
IF 特質("研ぎ澄まされた感覚")
	PALAM:快Ｃ += 特質("研ぎ澄まされた感覚")*100
	PALAM:快Ｖ += 特質("研ぎ澄まされた感覚")*100
	;処女の場合は99まで
	SIF 処女膜(TARGET) && PALAM:快Ｖ >= 100
		PALAM:快Ｖ = 99
	PALAM:快Ａ += 特質("研ぎ澄まされた感覚")*100
	PALAM:快Ｂ += 特質("研ぎ澄まされた感覚")*100
ENDIF

;ミッドナイトスペシャル使用時は欲情を3000にする
IF CFLAG:ミッドナイトスペシャル
	SIF PALAM:欲情 < 3000
		PALAM:欲情 = 3000
ENDIF

;発情度に応じて勃起、欲情、潤滑を増やす
SIF PENIS(TARGET)
	PALAM:勃起 += 発情度(TARGET)*30
SIF VAGINA(TARGET)
	PALAM:潤滑 += 発情度(TARGET)*30
PALAM:欲情 += 発情度(TARGET)*30

;開始前の愛情経験を入れておく
TFLAG:愛情経験計算用 = EXP:愛情経験

;開始前の各種経験を入れておく
嗜虐経験計算用 = EXP:嗜虐快楽経験
主人ＡＶ経験計算用 = EXP:MASTER:Ａ経験+EXP:MASTER:Ｖ経験
ＡＶ経験計算用 = EXP:Ａ経験+EXP:Ｖ経験

;妊娠確率補正を1/1にする
CVARSET TEQUIP, GETNUM(TEQUIP, "妊娠確率分母"), 1
CVARSET TEQUIP, GETNUM(TEQUIP, "妊娠確率分子"), 1

;対象が霊体、ゾンビ、死体ならネクロ経験の増加
IF TALENT:霊体 || TALENT:ゾンビ || (CFLAG:死亡 && !TEQUIP:仮死薬)
	CFLAG:MASTER:ネクロ経験++
	FOR LCOUNT, 0, 2
		SIF ASSI:LCOUNT > 0
			CFLAG:(ASSI:LCOUNT):ネクロ経験++
	NEXT
ENDIF

;オナホパフェフラグリセット
オナホパフェ = 0

;ワガママ姫段位
IF 受験段位名 == "ワガママ姫たちの接待調教プログラム"
	;年月日のシード値を用いて乱数を制御するため、同じ日付ならリロードしても同じ結果になる
	;制御するのはコマンドフィルタだけなのでDUMPRANDで保存しておく
	DUMPRAND
	IF ターン == "昼"
		RANDOMIZE (YEAR*100000)+(MONTH*1000)+(DAYS*10)
	ELSE
		RANDOMIZE (YEAR*100000)+(MONTH*1000)+(DAYS*10)+1
	ENDIF
	;コマンドフィルタ変数に1000～10000の乱数を入れる
	コマンドフィルタ変数 = RAND(1000, 10000)
	;乱数の状態を戻す
	INITRAND

	;2で割ったときの剰余の有無でフィルタをかける ビット数みたいな感じ
	FLAG:愛撫コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:道具コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:奉仕コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:獣姦コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:Ｖ系コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:Ａ系コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:暴力コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	;スカ系はオフにしとく（暫定）
	FLAG:スカ系コマンドフィルタ = 1
ENDIF

;実績:アイテムを何も持ってない状態で調教を始める
SIF SUMARRAY(ITEM) == 0
	CALL 実績解除, "そんな装備で大丈夫か？"

;実績:ロッテとラッテを対面させる
IF ASSI > 0
	SIF (CHARANAMEF(TARGET) == "ロッテ" && GROUPMATCH("ラッテ", CHARANAMEF(ASSI), CHARANAMEF(ASSI:1))) || (CHARANAMEF(TARGET) == "ラッテ" && GROUPMATCH("ロッテ", CHARANAMEF(ASSI), CHARANAMEF(ASSI:1)))
		CALL 実績解除, "パラドックス"
ENDIF

;実績;パステルくん主人で奴隷と好相性の助手を使用する
SIF CHARANAMEF(MASTER) == "パステルくん" && ((ASSI > 0 && 相性値(TARGET, ASSI) > 100) && ASSI > 0 && (相性値(TARGET, ASSI:1) > 100))
	CALL 実績解除, "「わ～いらくち～ん」"

SIF CHARANAMEF(TARGET) == "小野 美苗"
	美苗チェック = DAY

打カウント = 0

CALL CHECKDRAWLINE
CALL KOJO_START

;;;コマンド終了時に呼ばれる ここでは調教終了処理と媚薬処理
@EVENTUSERCOMEND
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIM DYNAMIC 獣人

;SKIPDISP中は無視
SIF ISSKIP()
	RETURN

IF SELECTCOMS == "蘇生薬投与"
	;5ターン分ほど使用不可にする
	CFLAG:使用不可 = 6
	TFLAG:調教終了 = 1
ENDIF

;ケモノフェロモンもしくはハニーパーティの媚薬効果
FOR LCOUNT, 0, 3
	獣人 = -1
	SIF LCOUNT == 0
		対象 = TARGET
	SIF LCOUNT == 1
		対象 = MASTER
	IF LCOUNT == 2
		SIF ASSI == -1
			BREAK
		対象 = ASSI
	ENDIF
	IF GROUPMATCH(CSTR:MASTER:種族, "獣人", "獣") && 対象 != MASTER
		獣人 = MASTER
	ELSEIF GROUPMATCH(CSTR:TARGET:種族, "獣人", "獣") && 対象 != TARGET
		獣人 = TARGET
	ELSEIF ASSI >= 0 && GROUPMATCH(CSTR:ASSI:種族, "獣人", "獣") && 対象 != ASSI
		獣人 = ASSI
	ENDIF

	IF !TEQUIP:対象:媚薬 && CFLAG:対象:媚薬 >= 10
		SIF !TFLAG:ハニーパーティ && 獣人 >= 0
			PRINTFORML %CALLNAME:獣人%のフェロモンで%CALLNAME:対象%は媚薬を飲まされたかのように発情してしまった……
		TEQUIP:対象:媚薬 = 1
	ENDIF
NEXT

CALL TUTORIAL, "調教終了無効"

IF TEQUIP:睡眠 == 1
	PRINTFORMW （そろそろ%CALLNAME:TARGET%が目を覚ますだろう。行為を終了します）
	TFLAG:調教終了 = 1
ELSEIF 上級売春中
	IF BASE:MASTER:体力 <= 500
		PRINTFORML （%CALLNAME:MASTER%の体力が限界のため、売春を終了します）
		;お客様満足度一定以上
		IF 上級売春中 == 2
			上級売春中 = 4
		ELSE
			上級売春中 = 3
		ENDIF
	ENDIF
ELSE
	;裸体盛りでの終了
	IF TFLAG:裸体盛り
		TFLAG:調教終了 = 1
		PRINTFORML %CALLNAME:TARGET%の裸体盛りをみんなに振る舞うことにした
		SIF CFLAG:死亡 && CHARANUM > 2
			PRINTFORML しかし死体に盛り付けられた料理など、ほとんどの人は気味悪がって手を出さなかった……
	;体力切れ
	ELSEIF BASE:体力 <= 0
		IF CFLAG:死亡
			;蘇生せずに体力0になると本当に死ぬ
			;ガチ屍姦中は体力とか関係無い
			IF TEQUIP:仮死薬
				CALL 仮死薬による死
			ELSE
				RETURN
			ENDIF
		ENDIF
		TFLAG:調教終了 = 1
		;自爆した場合
		IF ハッキング命令 == "自爆"
			PRINTFORML ★★★%CALLNAME:TARGET%は自爆しました★★★
			自爆キャラ:(NOF(TARGET)) = 1
		ELSE
			PRINTFORML （激しい調教で%CALLNAME:TARGET%はひどく消耗したようです　調教を終了します）
			;従順ダウン 陥落済みなら発生しない
			IF ABL:従順 > 0 && !陥落(TARGET)
				ABL:従順--
				PRINTFORML %CALLNAME:TARGET%の従順が1下がった
			ENDIF
			;回復量低下フラグ
			CFLAG:回復量低下 += 2
			CFLAG:ストレス値 += 10
			BASE:体力 = 1
			CALL 種族変化イベント
			;実績:ガッキー博士で奴隷の体力を0にする
			SIF CHARANAMEF(PLAYER) == "ガッキー博士"
				CALL 実績解除, "「科学の発展に犠牲は付き物デース」"
		ENDIF
	ELSEIF BASE:体力 < 500 && !特質("無理強い") && !CFLAG:死亡
		TFLAG:調教終了 = 1
		PRINTL （体力が限界に来ています　調教を終了します）
		CALL 種族変化イベント
	ENDIF
ENDIF

;流産時に体力が削りきれなかった場合は強制的に調教終了
IF TFLAG:流産
	PRINTFORML （%CALLNAME:TARGET%が流産したため、調教を終了します）
	CFLAG:使用不可 += 30
	CFLAG:出産トラウマ += 50
	SIF CFLAG:出産トラウマ >= 100
		CFLAG:出産トラウマ = 100
	CALL 崩壊処理, TARGET, "流産"
	TFLAG:調教終了 = 1
ENDIF

;;;宇宙人の感度はランダム変化する
@宇宙人感度変化, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIMS DYNAMIC 部位
SIF CSTR:対象:種族 != "宇宙人"
	RETURN
FOR LCOUNT, 0, 4
	SIF LCOUNT == 0
		部位 = Ｃ
	SIF LCOUNT == 1
		部位 = Ｂ
	SIF LCOUNT == 2
		部位 = Ａ
	IF LCOUNT == 3
		SIF !VAGINA(対象)
			BREAK
		部位 = Ｖ
	ENDIF
	;MODで無効
	SIF MOD適用判定(対象, @"%部位%素質")
		CONTINUE
	SELECTCASE RAND:3
		CASE 0
			TALENT:対象:@"%部位%敏感" = 0
			TALENT:対象:@"%部位%鈍感" = 1
		CASE 1
			TALENT:対象:@"%部位%敏感" = 0
			TALENT:対象:@"%部位%鈍感" = 0
		CASE 2
			TALENT:対象:@"%部位%敏感" = 1
			TALENT:対象:@"%部位%鈍感" = 0
	ENDSELECT
NEXT

;;;条件を満たしていれば種族変化系のイベントを発生させるか選択できる 助手を使う場合は助手が陥落している必要がある
;;;優先度はスライム＞吸血鬼
@種族変化イベント
;対象が種族[人間]でなければ戻る
SIF CSTR:種族 != "人間"
	RETURN
;スライム侵食イベント
IF スライム感染可能(TARGET) && (ISSLIME(MASTER) || (ASSI >= 0 && ISSLIME(ASSI) && 陥落(ASSI)))
	IF ISSLIME(MASTER)
		PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
		PRINTFORMW このまま%CALLNAME:TARGET%を……
		PRINTL [0] 少しずつスライム化させていこう
		PRINTL [1] 一気にスライムに変えてしまおう
		PRINTL [2] いや、今は休ませておこう
		BINPUT
		SELECTCASE RESULT
			CASE 0
				SELECTCASE CFLAG:スライム度
					CASE 0
						;未感染ならスライム感染
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染初回"
						CALL スライム度増加, 0, TARGET, 1
					CASE IS < 20
						;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染深化"
						CALL スライム度増加, 0, TARGET, 10
					CASEELSE
						;感染済みでスライム度が20以上なら完全スライム化
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染完了"
						CALL スライム度増加, 0, TARGET, 10
						TALENT:スライム = 1
						CSTR:種族 = スライム
						;上位陥落素質ありで妄信イベント
						IF (TALENT:親愛 || TALENT:淫魔 || TALENT:隷属) && !TALENT:妄信
							PRINTFORML %CALLNAME:TARGET%は[妄信]を得た
							TALENT:妄信 = 1
						ENDIF
				ENDSELECT
			CASE 1
				;深度を無視して一気にスライム化
				CALL KOJO_MESSAGE_EVENT, "主人スライム強制変化"
				IF TALENT:MASTER:溶ける快
					CALL スライム度増加, 1, TARGET, 1500
					TALENT:溶ける快 = 1
					TALENT:妄信 = 1
					CALL 相性加算, TARGET, MASTER, 50
					CSTR:種族 = スライム
				ELSE
					CALL スライム度増加, 0, TARGET, 30
					TALENT:スライム = 1
					TALENT:崩壊 = 1
					CSTR:種族 = スライム
					IF 陥落(TARGET)
						PRINTFORML %CALLNAME:TARGET%が%CALLNAME:MASTER%に抱いていた感情は、スライムの本能に塗りつぶされてしまった……
						SIF TALENT:恋慕
							PRINTFORML %CALLNAME:TARGET%は[恋慕]を失った
						SIF TALENT:淫乱
							PRINTFORML %CALLNAME:TARGET%は[淫乱]を失った
						SIF TALENT:服従
							PRINTFORML %CALLNAME:TARGET%は[服従]を失った
						SIF TALENT:親愛
							PRINTFORML %CALLNAME:TARGET%は[親愛]を失った
						SIF TALENT:淫魔
							PRINTFORML %CALLNAME:TARGET%は[淫魔]を失った
						SIF TALENT:隷属
							PRINTFORML %CALLNAME:TARGET%は[隷属]を失った
						TALENT:恋慕 = 0
						TALENT:淫乱 = 0
						TALENT:服従 = 0
						TALENT:親愛 = 0
						TALENT:淫魔 = 0
						TALENT:隷属 = 0
						WAIT
					ENDIF
				ENDIF
		ENDSELECT
	ELSE
		PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ
		PRINTFORML %CALLNAME:ASSI%に襲わせますか？
		PRINTL [0] - 襲わせる
		PRINTL [1] - 襲わせない
		BINPUT
		SELECTCASE RESULT
			CASE 0
				;未感染ならスライム感染
				CALL KOJO_MESSAGE_EVENT, "助手スライム感染初回"
				CALL スライム度増加, 0, TARGET, 1
			CASE IS < 20
				;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
				CALL KOJO_MESSAGE_EVENT, "助手スライム感染深化"
				CALL スライム度増加, 0, TARGET, 10
			CASEELSE
				;感染済みでスライム度が20以上なら完全スライム化
				CALL KOJO_MESSAGE_EVENT, "助手スライム感染完了"
				CALL スライム度増加, 0, TARGET, 10
				TALENT:スライム = 1
				CSTR:種族 = スライム
		ENDSELECT
	ENDIF
	RETURN
ENDIF

;吸血イベント
IF TALENT:MASTER:吸血鬼 || (ASSI >= 0 && TALENT:ASSI:吸血鬼 && 陥落(ASSI))
	IF TALENT:MASTER:吸血鬼
		PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
		PRINTFORMW このまま%CALLNAME:TARGET%を……
		PRINTL [0] 眷属に迎え入れようか
		PRINTL [1] 放っておこうか
		BINPUT
		;吸血
		IF !RESULT
			CALL KOJO_MESSAGE_EVENT, "主人吸血"
			;対象が処女、再生処女、童貞なら吸血鬼化
			IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
				TALENT:吸血鬼 = 1
				CSTR:種族 = ヴァンパイア
				;上位陥落素質ありで妄信イベント
				IF (TALENT:親愛 || TALENT:淫魔 || TALENT:隷属) && !TALENT:妄信
					PRINTFORML %CALLNAME:TARGET%は[妄信]を得た
					TALENT:妄信 = 1
				ENDIF
			;ゾンビ化
			ELSE
				TALENT:ゾンビ = 1
				CSTR:種族 = ゾンビ
			ENDIF
			IF CFLAG:被吸血経験 == 0
				EXP:異常経験++
				PRINTL 異常経験+1
			ENDIF
			CFLAG:被吸血経験++
			EXP:MASTER:吸血経験++
		ENDIF
	ELSE
		PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ
		PRINTFORML %CALLNAME:ASSI%に血を吸わせますか？
		PRINTL [0] - 吸わせる
		PRINTL [1] - 吸わせない
		BINPUT
		IF !RESULT
			CALL KOJO_MESSAGE_EVENT, "助手吸血"
			;対象が処女、再生処女、童貞なら吸血鬼化
			IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
				TALENT:吸血鬼 = 1
				CSTR:種族 = ヴァンパイア
			;ゾンビ化
			ELSE
				TALENT:ゾンビ = 1
				CSTR:種族 = ゾンビ
			ENDIF
			;初めて血を吸われたら異常経験追加
			IF CFLAG:被吸血経験 == 0
				EXP:異常経験++ 
				PRINTL 異常経験+1
			ENDIF
			CFLAG:被吸血経験++
			EXP:ASSI:吸血経験++
		ENDIF
	ENDIF
	RETURN
ENDIF

@仮死薬による死
;TEQUIP:仮死薬は実際の死亡判定に使用されるためここでは解除しない
CALL DISABLE, "EVENT_仮死薬死亡", TARGET
IF RESULT
	;DISABLEされている場合蘇生薬投与と同様の処理
	CFLAG:死亡 = 0
	CFLAG:使用不可 = 6
ELSE
	PRINTFORML ………………
	PRINTFORML …………
	PRINTFORMW ……
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORMW 蘇生薬を投与しても反応が無い。どうやら蘇生が遅すぎたようだ
	PRINTFORML ★★★%CALLNAME:TARGET%は死亡しました★★★
	;陥落済の先天霊体の場合幽体離脱で済む
	IF 陥落(TARGET) && NOF(TARGET) < 10000 && CSVTALENT(NOF(TARGET), GETNUM(TALENT, "霊体"))
		CFLAG:死亡 = 0
		CALL 幽体離脱, -1, TARGET
		TARGET = CHARANUM-1
		TALENT:霊体 = 1
		CFLAG:使用不可 = 3
		CALL KOJO_MESSAGE_EVENT, "調教後幽体離脱", TARGET
	ENDIF
ENDIF

[SKIPSTART]
;調教で【崩壊】を得た場合のイベント
;@COLLAPSE_MIND_TRAIN
;PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
;PRINTFORMW 激しい調教の影響によって、%CALLNAME:TARGET%の精神に異常をきたしてしまったようだ
;PRINTFORMW %NAME:TARGET%は【崩壊】してしまった
;TALENT:崩壊 = 1
;IF 陥落(TARGET)
;	PRINTFORMW 今まで重ねてきた時間も虚しく、%CALLNAME:TARGET%はすっかり心を閉ざしてしまった……
;	SIF TALENT:恋慕
;		PRINTFORMW %CALLNAME:TARGET%は[恋慕]を失った
;	SIF TALENT:淫乱
;		PRINTFORMW %CALLNAME:TARGET%は[淫乱]を失った
;	SIF TALENT:服従
;		PRINTFORMW %CALLNAME:TARGET%は[服従]を失った
;	SIF TALENT:親愛
;		PRINTFORMW %CALLNAME:TARGET%は[親愛]を失った
;	SIF TALENT:淫魔
;		PRINTFORMW %CALLNAME:TARGET%は[淫魔]を失った
;	SIF TALENT:隷属
;		PRINTFORMW %CALLNAME:TARGET%は[隷属]を失った
;	陥落(TARGET) = 0
;ENDIF
[SKIPEND]

;調教終了時のイベント
@調教終了
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ストレス低下量
#DIM DYNAMIC 実績計算用

CALL TUTORIAL, "珠取得"

CALL CHECKDRAWLINE
SIF ハッキング命令 != "自爆"
	CALL KOJO_TURNEND

PRINTW 調教を終了しました

SIF TEQUIP:仮死薬
	CALL 仮死薬による死

CALL 調教称号計算
CALL アイテム修理

;ペニバン解除タイミングが怪しいのでここで外しておく
WHILE FINDCHARA(TEQUIP:ペニスバンド, 1) >= 0
	TEQUIP:FINDCHARA(TEQUIP:ペニスバンド, 1):ペニスバンド = 0
WEND

;屋外プレイで雨、もしくはお風呂場プレイ中に終了した場合、人魚の体力気力を半減させる
IF TEQUIP:お風呂場
	IF CSTR:種族 == "人魚"
		MAXBASE:体力 /= 2
		MAXBASE:気力 /= 2
		BASE:体力 /= 2
		BASE:気力 /= 2
	ENDIF
ELSEIF TEQUIP:野外プレイ
	SELECTCASE 天気()
		CASE "雨", "大雨", "雷雨"
			IF CSTR:種族 == "人魚"
				MAXBASE:体力 /= 2
				MAXBASE:気力 /= 2
				BASE:体力 /= 2
				BASE:気力 /= 2
			ENDIF
	ENDSELECT
ENDIF

IF TEQUIP:女装
	;女装してたら服を元に戻す
	CSTR:特殊 = %服装保存用:0%
	CSTR:上半身上着 = %服装保存用:1%
	CSTR:下半身上着 = %服装保存用:2%
	CSTR:全身上着 = %服装保存用:3%
	CSTR:上半身下着 = %服装保存用:4%
	CSTR:下半身下着 = %服装保存用:5%
	CSTR:足元 = %服装保存用:6%
	CFLAG:挿入可 = 服装フラグ保存用:0
	CFLAG:着衣併用 = 服装フラグ保存用:1
	CFLAG:上着パイズリ = 服装フラグ保存用:2
	CFLAG:下着パイズリ = 服装フラグ保存用:3
	CFLAG:全身上着パイズリ = 服装フラグ保存用:4
	TEQUIP:女装 = 0
ENDIF

IF ハッキング命令 == "自爆"
	;自爆は全部無視してTURNEND
	CALL プレイログ記録, @"%NAME:TARGET%自爆"
	CSTR:ロスト要因 = 自爆
	CSTR:ロスト詳細 = ハッキングにより自爆した
	CALL CLEAR_CHARA, TARGET, 0, 1
	TARGET = -1
	RETURN 1
ENDIF
IF SELECTCOMS == "剥製加工"
	;剥製加工も同様
	CALL プレイログ記録, @"%NAME:TARGET%の死体を剥製に加工"
	CSTR:ロスト要因 = 死亡
	CSTR:ロスト詳細 = 死亡のち剥製に加工された
	CALL CLEAR_CHARA, TARGET
	TARGET = -1
	RETURN 1
ENDIF

IF !CFLAG:死亡 && !TEQUIP:睡眠
	;各ストレス増減処理
	;快感に素直、快感の否定は絶頂回数に応じてストレス増減
	IF TALENT:快感に素直
		CFLAG:ストレス値 -= EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
	ELSEIF TALENT:快感の否定 && !陥落(TARGET)
		CFLAG:ストレス値 += EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
	ENDIF

	;恋慕系陥落で流産してなければ愛情経験に応じてストレス低下
	ストレス低下量 = EXP:愛情経験-TFLAG:愛情経験計算用
	IF ストレス低下量 > 0 && CFLAG:ストレス値 > 0
		IF TALENT:恋慕 && ストレス低下量 >= 2
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の求愛によってストレスが緩和されました
			CFLAG:ストレス値 -= ストレス低下量/2
		ELSEIF TALENT:親愛
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%からの愛を確かめることでことでストレスが緩和されました
			CFLAG:ストレス値 -= ストレス低下量
		ENDIF
	ENDIF

	;淫乱系陥落で絶頂回数に応じてストレス低下
	ストレス低下量 = EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂*EX:Ｂ絶頂+EX:射精
	IF ストレス低下量 > 0
		IF TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%は欲求を満たされることでストレスが緩和しました
			CFLAG:ストレス値 -= ストレス低下量
		ELSEIF TALENT:淫魔
			PRINTFORMW %CALLNAME:TARGET%は肉欲を満たされることでストレスが緩和しました
			CFLAG:ストレス値 -= ストレス低下量*2
		ENDIF
	ENDIF

	;マゾっ気があればPALAM:苦痛のPALAMLVに応じてストレス低下
	IF ABL:マゾっ気 && GETPALAMLV(PALAM:苦痛, 10)
		PRINTFORMW %CALLNAME:TARGET%はマゾヒズムを満たされることでストレスが緩和しました
		CFLAG:ストレス値 -= MIN(ABL:マゾっ気, GETPALAMLV(PALAM:苦痛, 10))
	ENDIF

	;精液中毒 主人と助手の射精回数に応じてストレス低下
	ストレス低下量 = EX:MASTER:射精+EX:MASTER:触手射精
	SIF ASSI > 0
		ストレス低下量 += EX:ASSI:射精
	IF ストレス低下量 > 4 && ABL:精液中毒
		PRINTFORMW %CALLNAME:TARGET%は精液への渇望を満たされることでストレスが緩和しました
		CFLAG:ストレス値 -= MIN(ABL:精液中毒, ストレス低下量/5)
	ENDIF
ENDIF

IF TFLAG:採血量
	PRINTFORMW 今回の調教で計{TFLAG:採血量}mlの血液を採取しました
	リソース:血液 += TFLAG:採血量
ENDIF
;母乳、精液、ビデオ、写真の売却処理
CALL SELL_MILK
CALL SELL_SAMEN
CALL SELL_VIDEO
VARLINE = 0
CALL SELL_PICTURE

IF !CFLAG:死亡
	;調教後行為のチェック 気絶、流産時、裸体盛り時はスルー
	SIF !TFLAG:気絶 && !TFLAG:流産 && !CFLAG:使用不可 && !TFLAG:裸体盛り && !TEQUIP:睡眠
		CALL SELF_CHECK

	;コース「THE 房中術」「何かを間違えた房中術」での処理
	SELECTCASE 受験段位名
		CASE "THE 房中術"
			FLAG:段位ゲージ -= EX:MASTER:射精
		CASE "何かを間違えた房中術"
			CALL PAYMENT, 2000*EX:MASTER:射精, "房中術パワー"
	ENDSELECT
	;実績「1回の調教でフローラに10回以上中出しする」
	IF CHARANAMEF(TARGET) == "フローラ"
		実績計算用 = 0
		実績計算用 += PREG:主人膣内射精
		SIF ASSI > -1
			実績計算用 += PREG:助手膣内射精
		SIF ASSI:1 > -1
			実績計算用 += PREG:助手2膣内射精
		IF 実績計算用 >= 10
			CALL 実績解除, "生命の環を紡いで"
		ENDIF
	ELSEIF ASSI > -1 && CHARANAMEF(ASSI) == "フローラ"
		実績計算用 = 0
		実績計算用 += PREG:ASSI:主人膣内射精
		SIF ASSI:1 > -1
			実績計算用 += PREG:助手2膣内射精
		実績計算用 += PREG:ASSI:奴隷膣内射精
		IF 実績計算用 >= 10
			CALL 実績解除, "生命の環を紡いで"
		ENDIF
	ELSEIF ASSI:1 > -1 && CHARANAMEF(ASSI:1) == "フローラ"
		実績計算用 = 0
		実績計算用 += PREG:ASSI:主人膣内射精
		SIF ASSI > -1
			実績計算用 += PREG:助手膣内射精
		実績計算用 += PREG:ASSI:奴隷膣内射精
		IF 実績計算用 >= 10
			CALL 実績解除, "生命の環を紡いで"
		ENDIF
	ENDIF

	;全体の妊娠判定の処理
	CALL IN_VAGINA


	;獲得した珠を計算して加算、否定の珠の打ち消しチェック
	CALL JUEL_CHECK
	CALL DENIAL_CHECK

	CALL TUTORIAL
	CALL TUTORIAL, "ABL上昇"

	;淫乱or淫魔 奴隷の相性増加
	IF TALENT:淫乱
		IF RAND:3 == 0
			CALL 相性加算, TARGET, MASTER, 1, 150
			SIF ASSI > 0
				CALL 相性加算, TARGET, ASSI, 1, 150
		ENDIF
	ELSEIF TALENT:淫魔
		IF RAND:2 == 0
			CALL 相性加算, TARGET, MASTER, 1, 200
			SIF ASSI > 0
				CALL 相性加算, TARGET, ASSI, 1, 200
		ENDIF
	ENDIF
	;絶倫：調教中の射精回数10回以上（出し尽くす）
	IF CFLAG:残数 <= 0 && !TALENT:絶倫 && PENIS(TARGET)
		CALL KOJO_MESSAGE_EVENT, "絶倫"
		TALENT:絶倫 = 1
	ENDIF
ENDIF


VARSET FCOMT
;OPTION:口上表示 = TFLAG:口上フラグ退避用
FLAG:能力上昇 = 0
VARLINE = 0

IF CFLAG:死亡
	IF TEQUIP:仮死薬
		TEQUIP:仮死薬 = 0
		CALL プレイログ記録, @"%NAME:TARGET%が死亡"
		CSTR:ロスト要因 = 死亡
		CSTR:ロスト詳細 = 屍姦プレイ中に蘇生が遅れた
		CALL CLEAR_CHARA, TARGET, 0, 1
	ENDIF
	TARGET = -1
	RETURN 1
ENDIF
RETURN 0


@アイテム修理
#DIM DYNAMIC LCOUNT
;壊れたアイテムを修理する
FOR LCOUNT, 0, VARSIZE("ITEM")
	IF ITEM:LCOUNT == -1
		PRINTFORML 破損した%ITEMNAME:LCOUNT%を修理しました
		ITEM:LCOUNT = 1
	ENDIF
NEXT

;;;外部から参照可能なように分離してみる
@JUELVALUE, PALAM量
#FUNCTION
#DIM DYNAMIC PALAM量
SELECTCASE GETPALAMLV(PALAM量, 10)
	CASE 0
		RETURNF 0
	CASE 1
		IF PALAM量 < PALAMLV:1*3
			RETURNF 1
		ELSE
			RETURNF 2
		ENDIF
	CASE 2
		IF PALAM量 < PALAMLV:2*3
			RETURNF 10
		ELSE
			RETURNF 20
		ENDIF
	CASE 3
		IF PALAM量 < PALAMLV:3*2
			RETURNF 100
		ELSE
			RETURNF 200
		ENDIF
	CASE 4
		RETURNF 500
	CASE 5
		RETURNF 1000
	CASE 6
		RETURNF 2000
	CASE 7
		RETURNF 3000
	CASE 8
		RETURNF 5000
	CASE 9
		RETURNF 8000
	CASEELSE
		RETURNF PALAM量/20
ENDSELECT


;;;PALAMから獲得量を計算して珠を加算
@JUEL_CHECK, MODE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC MODE

FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	GOTJUEL:LCOUNT += JUELVALUE(PALAM:LCOUNT)
	SELECTCASE PALAMNAME:LCOUNT
		CASE "快Ｃ"
			IF PENIS(TARGET)
				GOTJUEL:LCOUNT += ((EX:射精-EX:膣責め射精-EX:アナル責め射精)*2000)+(EX:潮噴き*3000)
			ELSE
				GOTJUEL:LCOUNT += EX:Ｃ絶頂*1000
			ENDIF
		CASE "快Ｖ"
			GOTJUEL:LCOUNT += (EX:Ｖ絶頂+EX:膣責め射精)*1000
		CASE "快Ａ"
			GOTJUEL:LCOUNT += (EX:Ａ絶頂+EX:アナル責め射精)*1000
		CASE "快Ｂ"
			GOTJUEL:LCOUNT += (EX:Ｂ絶頂)*1000
		CASE "反感", "不快", "抑鬱"
			GOTJUEL:否定 += GOTJUEL:LCOUNT
	ENDSELECT
NEXT

IF MODE
	;学園や上級売春では快と習得以外の珠を破棄
	FOR LCOUNT, 0, VARSIZE("PALAM")
		SELECTCASE PALAMNAME:LCOUNT
			CASE "反感"
				BREAK
			CASE "快Ｃ", "快Ｖ", "快Ａ", "快Ｂ", "習得"
				DEBUGPRINTFORML %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT}+{GOTJUEL:LCOUNT})
				JUEL:LCOUNT += GOTJUEL:LCOUNT
		ENDSELECT
	NEXT
ELSE
	CALL CHECKDRAWLINE
	PRINTL 調教の結果：
	FOR LCOUNT, 0, VARSIZE("PALAM")
		SIF PALAMNAME:LCOUNT == ""
			CONTINUE
		SELECTCASE PALAMNAME:LCOUNT
			CASE "反感"
				PRINTFORML 否定の珠×({JUEL:否定}+{GOTJUEL:否定})
				JUEL:否定 += GOTJUEL:否定
				BREAK
			CASE "潤滑", "勃起"
			CASEELSE
				PRINTFORML %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT}+{GOTJUEL:LCOUNT})
				JUEL:LCOUNT += GOTJUEL:LCOUNT
		ENDSELECT
	NEXT
	PRINTW 以上の珠を得ました
ENDIF

;上限は999,999,999,999
FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE PALAMNAME:LCOUNT
		CASE "反感"
			SIF JUEL:否定 > 999999999999
				JUEL:否定 = 999999999999
			BREAK
		CASEELSE
			SIF JUEL:LCOUNT > 999999999999
				JUEL:LCOUNT = 999999999999
	ENDSELECT
NEXT


;;;否定の珠との打ち消し処理
@DENIAL_CHECK
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 相殺量

SIF JUEL:否定 == 0
	RETURN 0
CALL CHECKDRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています

;欲情、屈服、恭順の珠から順に打ち消される
IF JUEL:否定 > 0 && (JUEL:恭順+JUEL:欲情+JUEL:屈服) > 0
	相殺量 = JUEL:否定
	SIF JUEL:欲情 < JUEL:否定
		相殺量 = JUEL:欲情
	JUEL:欲情 -= 相殺量
	JUEL:否定 -= 相殺量
	SIF 相殺量 > 0
		PRINTFORML 欲情の珠×{相殺量}減少

	相殺量 = JUEL:否定
	SIF JUEL:屈服 < JUEL:否定
		相殺量 = JUEL:屈服
	SIF 相殺量 > 0
		PRINTFORML 屈服の珠×{相殺量}減少
	JUEL:屈服 -= 相殺量
	JUEL:否定 -= 相殺量

	相殺量 = JUEL:否定
	SIF JUEL:恭順 < JUEL:否定
		相殺量 = JUEL:恭順
	JUEL:恭順 -= 相殺量
	JUEL:否定 -= 相殺量
	SIF 相殺量 > 0
		PRINTFORML 恭順の珠×{相殺量}減少
ENDIF

PRINTL その結果、
FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE PALAMNAME:LCOUNT
		CASE "反感"
			PRINTFORML 否定の珠×({JUEL:否定})
			BREAK
		CASE "潤滑", "勃起"
		CASEELSE
			PRINTFORML %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT})
	ENDSELECT
NEXT
PRINTW 以上のように変化しました


@CVAR_RESET
#DIM DYNAMIC 睡眠時間保存用
#DIM DYNAMIC 仮死薬保存用
;調教時のキャラ変数を初期化する BEGIN TRAINで行われる処理と同等
ASSIPLAY = 0
;PREVCOM = -1 使用禁止変数
;NEXTCOM = -1 使用禁止変数
VARSET TFLAG
IF TARGET >= 0
	VARSET GOTJUEL
	睡眠時間保存用 = TEQUIP:睡眠
	仮死薬保存用 = TEQUIP:仮死薬
	VARSET TEQUIP
	TEQUIP:睡眠 = 睡眠時間保存用
	TEQUIP:仮死薬 = 仮死薬保存用
	VARSET EX
	VARSET PALAM
	VARSET SOURCE
	VARSET STAIN
ENDIF
IF ASSI >= 0
	VARSET TEQUIP:ASSI:0
	VARSET EX:ASSI:0
	VARSET STAIN:ASSI:0
ENDIF
VARSET TEQUIP:MASTER:0
VARSET EX:MASTER:0
VARSET STAIN:MASTER:0
PREVCOMS = 
SELECTCOMS = 
SELECTCOM = -1

@TVAR_RESET
;コマンド実行時の変数初期化 NOWEXとTFLAG:100以下
#DIM DYNAMIC LCOUNT

VARSET NOWEX, 0
VARSET NOWEX:MASTER:0, 0
SIF ASSI >= 0
	VARSET NOWEX:ASSI:0, 0
VARSET TFLAG, 0, 0, 100
TFLAG:口上依存度絶対値 = __INT_MAX__
;寸止めフラグをリセット
TFLAG:寸止め = 0
