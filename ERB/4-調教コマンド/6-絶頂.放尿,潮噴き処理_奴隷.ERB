;-------------------------------------------------
;強精神薬による絶頂キャンセル
;-------------------------------------------------
@ECST_CANCEL
#DIM PALAM_C
#DIM PALAM_V
#DIM PALAM_A
#DIM PALAM_B
#DIM ORG_C
#DIM ORG_V
#DIM ORG_A
#DIM ORG_B
PALAM_C = UP:快Ｃ + PALAM:快Ｃ
PALAM_V = UP:快Ｖ + PALAM:快Ｖ
PALAM_A = UP:快Ａ + PALAM:快Ａ
PALAM_B = UP:快Ｂ + PALAM:快Ｂ

ORG_C = 0
ORG_V = 0
ORG_A = 0
ORG_B = 0

;ペニスある場合は計算しない
IF !PENIS(TARGET)
	;絶頂Ｃ
	IF PALAM_C >= 40000 && TALENT:リミットバースト
		ORG_C = PALAM_C/10000
		PRINTFORML 最強Ｃ絶頂+{ORG_C-3}キャンセル
	ELSEIF PALAM_C >= 30000
		ORG_C = 3
		PRINTL 超強Ｃ絶頂キャンセル
	ELSEIF PALAM_C >= 20000
		ORG_C = 2
		PRINTL 強Ｃ絶頂キャンセル
	ELSEIF PALAM_C >= 10000
		ORG_C = 1
		PRINTL Ｃ絶頂キャンセル
	ENDIF
ENDIF

;絶頂Ｖ
IF PALAM_V >= 40000 && TALENT:リミットバースト
	ORG_V = PALAM_V/10000
	PRINTFORML 最強Ｖ絶頂+{ORG_V-3}キャンセル
ELSEIF PALAM_V >= 30000
	ORG_V = 3
	PRINTL 超強Ｖ絶頂キャンセル
ELSEIF PALAM_V >= 20000
	ORG_V = 2
	PRINTL 強Ｖ絶頂キャンセル
ELSEIF PALAM_V >= 10000
	ORG_V = 1
	PRINTL Ｖ絶頂キャンセル
ENDIF

;絶頂Ａ
IF PALAM_A >= 40000 && TALENT:リミットバースト
	ORG_A = PALAM_A/10000
	PRINTFORML 最強Ａ絶頂+{ORG_A-3}キャンセル
ELSEIF PALAM_A >= 30000
	ORG_A = 3
	PRINTL 超強Ａ絶頂キャンセル
ELSEIF PALAM_A >= 20000
	ORG_A = 2
	PRINTL 強Ａ絶頂キャンセル
ELSEIF PALAM_A >= 10000
	ORG_A = 1
	PRINTL Ａ絶頂キャンセル
ENDIF

;絶頂Ｂ
IF PALAM_B >= 40000 && TALENT:リミットバースト
	ORG_B = PALAM_B/10000
	PRINTFORML 最強Ｂ絶頂+{ORG_B-3}キャンセル
ELSEIF PALAM_B >= 30000
	ORG_B = 3
	PRINTL 超強Ｂ絶頂キャンセル
ELSEIF PALAM_B >= 20000
	ORG_B = 2
	PRINTL 強Ｂ絶頂キャンセル
ELSEIF PALAM_B >= 10000
	ORG_B = 1
	PRINTL Ｂ絶頂キャンセル
ENDIF

DOWN:快Ｃ = (PALAM_C/10000)*10000
DOWN:快Ｖ = (PALAM_V/10000)*10000
DOWN:快Ａ = (PALAM_A/10000)*10000
DOWN:快Ｂ = (PALAM_B/10000)*10000

;止められた絶頂回数を対応するTFLAGに記録
TFLAG:Ｃ絶頂カウント += ORG_C
TFLAG:Ｖ絶頂カウント += ORG_V
TFLAG:Ａ絶頂カウント += ORG_A
TFLAG:Ｂ絶頂カウント += ORG_B

RETURN 1

;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
@ORGASM_ADD
#DIM DYNAMIC 絶頂倍率
#DIM DYNAMIC PALAM_C
#DIM DYNAMIC PALAM_V
#DIM DYNAMIC PALAM_A
#DIM DYNAMIC PALAM_B
#DIM DYNAMIC ORG_C
#DIM DYNAMIC ORG_V
#DIM DYNAMIC ORG_A
#DIM DYNAMIC ORG_B
#DIM DYNAMIC SUMEX
#DIM DYNAMIC 欲望変動

ORG_C = 0
ORG_V = 0
ORG_A = 0
ORG_B = 0

;強精神薬・ラブキャンセーブユーの副作用を適用する
SIF 絶頂止め(TARGET)
	CALL REACT_DRUG

PALAM_C = UP:快Ｃ + PALAM:快Ｃ
PALAM_V = UP:快Ｖ + PALAM:快Ｖ
PALAM_A = UP:快Ａ + PALAM:快Ａ
PALAM_B = UP:快Ｂ + PALAM:快Ｂ

;ペニスある場合は計算しない
IF !PENIS(TARGET)
	;絶頂Ｃ
	IF PALAM_C >= 40000 && TALENT:リミットバースト
		ORG_C = PALAM_C/10000
		PRINTFORML 最強Ｃ絶頂+{ORG_C-3}
	ELSEIF PALAM_C >= 30000
		ORG_C = 3
		PRINTL 超強Ｃ絶頂
	ELSEIF PALAM_C >= 20000
		ORG_C = 2
		PRINTL 強Ｃ絶頂
	ELSEIF PALAM_C >= 10000
		ORG_C = 1
		PRINTL Ｃ絶頂
	ENDIF
ENDIF

;絶頂Ｖ
IF PALAM_V >= 40000 && TALENT:リミットバースト
	ORG_V = PALAM_V/10000
	PRINTFORML 最強Ｖ絶頂+{ORG_V-3}
ELSEIF PALAM_V >= 30000
	ORG_V = 3
	PRINTL 超強Ｖ絶頂
ELSEIF PALAM_V >= 20000
	ORG_V = 2
	PRINTL 強Ｖ絶頂
ELSEIF PALAM_V >= 10000
	ORG_V = 1
	PRINTL Ｖ絶頂
ENDIF

;絶頂Ａ
IF PALAM_A >= 40000 && TALENT:リミットバースト
	ORG_A = PALAM_A/10000
	PRINTFORML 最強Ａ絶頂+{ORG_A-3}
ELSEIF PALAM_A >= 30000
	ORG_A = 3
	PRINTL 超強Ａ絶頂
ELSEIF PALAM_A >= 20000
	ORG_A = 2
	PRINTL 強Ａ絶頂
ELSEIF PALAM_A >= 10000
	ORG_A = 1
	PRINTL Ａ絶頂
ENDIF

;絶頂Ｂ
IF PALAM_B >= 40000 && TALENT:リミットバースト
	ORG_B = PALAM_B/10000
	PRINTFORML 最強Ｂ絶頂+{ORG_B-3}
ELSEIF PALAM_B >= 30000
	ORG_B = 3
	PRINTL 超強Ｂ絶頂
ELSEIF PALAM_B >= 20000
	ORG_B = 2
	PRINTL 強Ｂ絶頂
ELSEIF PALAM_B >= 10000
	ORG_B = 1
	PRINTL Ｂ絶頂
ENDIF

DOWN:快Ｃ = (PALAM_C/10000)*10000
DOWN:快Ｖ = (PALAM_V/10000)*10000
DOWN:快Ａ = (PALAM_A/10000)*10000
DOWN:快Ｂ = (PALAM_B/10000)*10000

SIF DOWN:快Ｃ+PALAM_C < 0
	DOWN:快Ｃ = PALAM_C
SIF DOWN:快Ｖ+PALAM_V < 0
	DOWN:快Ｖ = PALAM_V
SIF DOWN:快Ａ+PALAM_A < 0
	DOWN:快Ａ = PALAM_A
SIF DOWN:快Ｂ+PALAM_B < 0
	DOWN:快Ｂ = PALAM_B

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------
TFLAG:絶頂強度 = ORG_C+ORG_V+ORG_A+ORG_B

;快楽刻印の処理
;絶頂の部位と強度の合計が一定以上で刻印を取得する
SUMEX = ORG_C+ORG_V*2+ORG_A*2+ORG_B+NOWEX:射精+NOWEX:潮噴き*2
;絶頂強度が4以上で快楽刻印3に相当
IF SUMEX >= 4 && MARK:快楽刻印 < 3
	TFLAG:快楽刻印フラグ = 3
;絶頂強度が3以上で快楽刻印2に相当
ELSEIF SUMEX >= 3 && MARK:快楽刻印 < 2
	TFLAG:快楽刻印フラグ = 2
;絶頂強度が2以上で快楽刻印1に相当
ELSEIF SUMEX >= 2 && MARK:快楽刻印 < 1
	TFLAG:快楽刻印フラグ = 1
ENDIF

;絶頂倍率計算しておかないと体力気力の消費がヤバイことになる
IF ORG_C && ORG_V && ORG_A && ORG_B
	PRINTL 四 重 絶 頂
	PRINTL (それぞれ8倍の珠が得られます)
	ORG_C *= 8
	ORG_V *= 8
	ORG_A *= 8
	ORG_B *= 8
	絶頂倍率 = 8
	;実績「四重絶頂させる」
	CALL 実績解除, "Insane Techniques"
ELSEIF ORG_C && ORG_V && ORG_A
	PRINTL Ｃ＆Ｖ＆Ａ絶頂
	PRINTL (それぞれ4倍の珠が得られます)
	ORG_C *= 4
	ORG_V *= 4
	ORG_A *= 4
	絶頂倍率 = 4
ELSEIF ORG_B && ORG_V && ORG_A
	PRINTL Ｂ＆Ｖ＆Ａ絶頂
	PRINTL (それぞれ4倍の珠が得られます)
	ORG_B *= 4
	ORG_V *= 4
	ORG_A *= 4
	絶頂倍率 = 4
ELSEIF ORG_C && ORG_B && ORG_A
	PRINTL Ｃ＆Ｂ＆Ａ絶頂
	PRINTL (それぞれ4倍の珠が得られます)
	ORG_C *= 4
	ORG_B *= 4
	ORG_A *= 4
	絶頂倍率 = 4
ELSEIF ORG_C && ORG_V && ORG_B
	PRINTL Ｃ＆Ｖ＆Ｂ絶頂
	PRINTL (それぞれ4倍の珠が得られます)
	ORG_C *= 4
	ORG_V *= 4
	ORG_B *= 4
	絶頂倍率 = 4
ELSEIF ORG_C && ORG_V
	PRINTL Ｃ＆Ｖ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_C *= 2
	ORG_V *= 2
	絶頂倍率 = 2
ELSEIF ORG_C && ORG_A
	PRINTL Ｃ＆Ａ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_C *= 2
	ORG_A *= 2
	絶頂倍率 = 2
ELSEIF ORG_V && ORG_A
	PRINTL Ｖ＆Ａ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_V *= 2
	ORG_A *= 2
	絶頂倍率 = 2
ELSEIF ORG_C && ORG_B
	PRINTL Ｃ＆Ｂ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_C *= 2
	ORG_B *= 2
	絶頂倍率 = 2
	SIF CHARANAMEF(TARGET) == "ヨナ・ライゼ"
		CALL 実績解除, "C＆B"
ELSEIF ORG_V && ORG_B
	PRINTL Ｖ＆Ｂ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_V *= 2
	ORG_B *= 2
	絶頂倍率 = 2
ELSEIF ORG_A && ORG_B
	PRINTL Ａ＆Ｂ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	ORG_A *= 2
	ORG_B *= 2
	絶頂倍率 = 2
ELSE
	絶頂倍率 = 1
ENDIF

SIF ORG_C && SELECTCOMS == "電気按摩" && GROUPMATCH(CHARANAMEF(TARGET), "紳士めばえ", "淑女めばえ")
	CALL 実績解除, "電マの暮らし"

;種族が精霊で曇りなら絶頂倍率アップ
IF CSTR:種族 == "精霊" && 天気() == "曇り"
	TIMES ORG_C , 1.50
	TIMES ORG_V , 1.50
	TIMES ORG_A , 1.50
	TIMES ORG_B , 1.50
	TIMES 絶頂倍率 , 1.50
ENDIF

;ペニス持ちは計算しない
IF ORG_C && !PENIS(TARGET)
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 10
	SOURCE:露出 += 200 * ORG_C
	SOURCE:欲情 += 100 * ORG_C
	SOURCE:屈従 += 500 * ORG_C
	IF TALENT:快感の否定
		SOURCE:反感 += 100 * ORG_C
	ELSE
		SOURCE:反感 += 100 * ORG_C/絶頂倍率
	ENDIF
	;ヴァギナ持ちなら潤滑も追加
	SIF VAGINA(TARGET)
		SOURCE:液体 += 100 * ORG_C
	IF TEQUIP:クリキャップ
		IF TEQUIP:触手
			CFLAG:触手Ｃ絶頂経験++
		ELSE
			CFLAG:クリキャップ絶頂経験++
		ENDIF
	ENDIF
ENDIF
IF ORG_V
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 20
	SOURCE:欲情 += 300 * ORG_V
	SOURCE:恭順 += 500 * ORG_V
	SOURCE:露出 += 300 * ORG_V
	SOURCE:屈従 += 800 * ORG_V
	IF TALENT:快感の否定
		SOURCE:反感 += 200 * ORG_V
	ELSE
		SOURCE:反感 += 200 * ORG_V/絶頂倍率
	ENDIF
	;ヴァギナ持ちなら潤滑も追加
	SIF VAGINA(TARGET)
		SOURCE:液体 += 200 * ORG_V
	IF TEQUIP:バイブ
		IF TEQUIP:触手
			CFLAG:触手Ｖ絶頂経験++
		ELSE
			CFLAG:バイブ絶頂経験++
		ENDIF
	ENDIF
ENDIF
IF ORG_A
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 30
	SOURCE:欲情 += 300 * ORG_A
	SOURCE:恭順 += 300 * ORG_A
	SOURCE:露出 += 300 * ORG_A
	SOURCE:屈従 += 1000 * ORG_A
	;エネマでイったらボーナス
	IF TEQUIP:電動エネマグラ
		SOURCE:欲情 += 200 * ORG_A
		SOURCE:屈従 += 500 * ORG_A
		SOURCE:中毒 += 300 * ORG_A
		SOURCE:性行動 += 100 * ORG_A
	ENDIF
	;メス度に応じてボーナス 最大2.5倍
	SOURCE:欲情 += 75*MIN(ABL:メス度, 10) * ORG_A
	SOURCE:屈従 += 125*MIN(ABL:メス度, 10) * ORG_A
	SOURCE:中毒 += 75*MIN(ABL:メス度, 10) * ORG_A
	SOURCE:性行動 += 25*MIN(ABL:メス度, 10) * ORG_A
	IF TALENT:快感の否定
		SOURCE:反感 += 250 * ORG_A
	ELSE
		SOURCE:反感 += 250 * ORG_A/絶頂倍率
	ENDIF
	;ヴァギナ持ちなら潤滑も追加
	SIF VAGINA(TARGET)
		SOURCE:液体 += 50 * ORG_A
	IF TEQUIP:アナルバイブ
		IF TEQUIP:触手
			CFLAG:触手Ａ絶頂経験++
		ELSE
			CFLAG:アナルバイブ絶頂経験++
		ENDIF
	ENDIF
	SIF TEQUIP:アナルビーズ
		CFLAG:アナルビーズ絶頂経験++
	;実績:ルシェをメスイキさせる
	SIF CHARANAMEF(TARGET) == "ルシェ"
		CALL 実績解除, "「僕は■なのに……」"
ENDIF
IF ORG_B
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 10
	SOURCE:露出 += 200 * ORG_B
	SOURCE:欲情 += 50 * ORG_B
	SOURCE:屈従 += 700 * ORG_B
	IF TALENT:快感の否定
		SOURCE:反感 += 100 * ORG_B
	ELSE
		SOURCE:反感 += 100 * ORG_B/絶頂倍率
	ENDIF
	;ヴァギナ持ちなら潤滑も追加
	SIF VAGINA(TARGET)
		SOURCE:液体 += 30 * ORG_B
	IF TEQUIP:ニプルキャップ
		IF TEQUIP:触手
			CFLAG:触手Ｂ絶頂経験++
		ELSE
			CFLAG:ニプルキャップ絶頂経験++
		ENDIF
	ENDIF
ENDIF

SIF TFLAG:絶頂強度 && 撮影中()
	CFLAG:絶頂を撮影された経験++

;天使による感度アップ
SIF CSTR:種族 == "天使"
	TFLAG:天使の感度ボーナス++

;絶頂による欲望LVアップ
欲望変動 = 0
SIF ORG_C >= 2 || ORG_B >= 2 || ORG_V || ORG_A
	欲望変動 = 1
SIF ORG_V >= 2 || ORG_A >= 2
	欲望変動 = 2
SIF (ORG_C >= 2 && ORG_B >= 2) || (ORG_C >= 2 && ORG_V) || (ORG_C >= 2 && ORG_A) || (ORG_B >= 2 && ORG_V) || (ORG_B >= 2 && ORG_A) && (ORG_V && ORG_A)
	欲望変動 = 2
SIF (ORG_C && ORG_B && ORG_V) || (ORG_C && ORG_B && ORG_A) || (ORG_C && ORG_V && ORG_A) || (ORG_V && ORG_A && ORG_B)
	欲望変動 = 3
SIF ORG_C && ORG_V && ORG_A && ORG_B
	欲望変動 = 4
;自制心があると1下がる
SIF TALENT:自制心
	欲望変動 -= 1

IF ABL:欲望 < 欲望変動
	ABL:欲望 = 欲望変動
	PRINTFORML そして欲望がLV{欲望変動}になった
	;欲望の上昇による[抑圧]の消滅をチェック
	CALL ERASE_TALENT
ENDIF

;NOWEXにデータを入れる（絶頂時口上に使う）
NOWEX:Ｃ絶頂 = ORG_C
NOWEX:Ｖ絶頂 = ORG_V
NOWEX:Ａ絶頂 = ORG_A
NOWEX:Ｂ絶頂 = ORG_B

;絶頂回数を増やす
SIF ORG_C
	EX:Ｃ絶頂 += ORG_C
SIF ORG_V
	EX:Ｖ絶頂 += ORG_V
SIF ORG_A
	EX:Ａ絶頂 += ORG_A
SIF ORG_B
	EX:Ｂ絶頂 += ORG_B

;絶頂経験を増やす
EXP:絶頂経験 += ORG_C+ORG_V+ORG_A+ORG_B

;スライムであれば絶頂回数でスライム度増加
CALL スライム度増加, 0, TARGET, 0, ORG_C+ORG_V+ORG_A+ORG_B

;-------------------------------------------------
;絶頂時の放尿処理
;-------------------------------------------------
CALL ORGASM_ADD_URINE

;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
CALL TARGET_MILK_CHECK

;-------------------------------------------------
;SCAT MOD
;-------------------------------------------------
SIF MOD:SCAT
	CALL TARGET_DEFECATION_CHECK
SIF MOD:SCAT2
	CALL 尿意ゲージ加算

RETURN 1

;-------------------------------------------------
;強精神薬・ラブキャンセーブユーの副作用
;-------------------------------------------------
@REACT_DRUG
;仕様説明：
;@ECST_CANCELでEX:Ｃ絶頂～EX:Ｂ絶頂に記録してきた絶頂回数を解放します。
;記録してきた絶頂回数１回につき、該当する部位の快ソース（UP:快Ｃ,快Ｖ,快Ａ,快Ｂ）を２倍にします。
;１回のコマンドでひとつの部位につき１回ずつこの倍化処理を行い、
;EX:Ｃ絶頂～EX:Ｂ絶頂の合計が０になったとき、反動が解除されます。
;強精神薬強化を行っている場合は追加の値が強化値*10%になる
;-------------------------------------------------
;快Ｃの処理
IF TFLAG:Ｃ絶頂カウント > 0 && UP:快Ｃ > 0
	IF 強精神薬強化値
		UP:快Ｃ += UP:快Ｃ*(強精神薬強化値/10)
	ELSE
		UP:快Ｃ *= 2
	ENDIF
	TFLAG:Ｃ絶頂カウント--
ENDIF
;快Ｖの処理
IF TFLAG:Ｖ絶頂カウント > 0 && UP:快Ｖ > 0
	IF 強精神薬強化値
		UP:快Ｖ += UP:快Ｖ*(強精神薬強化値/10)
	ELSE
		UP:快Ｖ *= 2
	ENDIF
	TFLAG:Ｖ絶頂カウント--
ENDIF
;快Ａの処理
IF TFLAG:Ａ絶頂カウント > 0 && UP:快Ａ > 0
	IF 強精神薬強化値
		UP:快Ａ += UP:快Ａ*(強精神薬強化値/10)
	ELSE
		UP:快Ａ *= 2
	ENDIF
	TFLAG:Ａ絶頂カウント--
ENDIF
;快Ｂの処理
IF TFLAG:Ｂ絶頂カウント > 0 && UP:快Ｂ > 0
	IF 強精神薬強化値
		UP:快Ｂ += UP:快Ｂ*(強精神薬強化値/10)
	ELSE
		UP:快Ｂ *= 2
	ENDIF
	TFLAG:Ｂ絶頂カウント--
ENDIF
;反動の終了チェック
IF TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント == 0
	TEQUIP:強精神薬 = 0
	TFLAG:ラブキャンセーブユー = 0
ENDIF

RETURN 1

;-------------------------------------------------
;絶頂時の放尿処理
;-------------------------------------------------
@ORGASM_ADD_URINE
#DIM DYNAMIC 放尿量

;MODが必要
SIF !MOD:SCAT2
	RETURN 0
;尿道使用中は出ない
SIF Ｕ使用中(TARGET)
	RETURN 0
;屍姦中も出ない
SIF CFLAG:死亡
	RETURN 0
;尿意ゲージ5000必要
SIF BASE:尿意 < 5000
	RETURN 0
;射精と同時には無理
SIF NOWEX:射精
	RETURN 0

SELECTCASE SELECTCOMS
	;首絞めプレイでは漏らす
	;尿道挿入でも漏らす
	{
	CASE "正常位首絞め", "後背位首絞め", "正常位アナル首絞め", "後背位アナル首絞め",
		 "尿道挿入"
	}
		放尿量 = 1
	;尿道スライム
	CASE "スライム尿道責め"
		IF !TEQUIP:尿道スライム
			SIF PENIS(TARGET) && BASE:射精 >= MAXBASE:射精
				RETURN 0
			放尿量 = LIMIT(TFLAG:尿道スライム量/10, 1, 30)
		ENDIF
	;性交させる系コマンドは派生があるので漏れない
	CASE "正常位させる", "後背位させる", "正常位アナルさせる", "後背位アナルさせる"
		RETURN 0
	CASEELSE
		SIF TFLAG:絶頂強度 == 0
			RETURN 0
		;ペニスありの場合はVAB絶頂時のみ
		SIF PENIS(TARGET) && NOWEX:Ｖ絶頂+NOWEX:Ａ絶頂+NOWEX:Ｂ絶頂 == 0
			RETURN 0
ENDSELECT

;放尿コマンド選んだターンでは絶頂放尿しない
SELECTCASE SELECTCOMS
	CASE "放尿", "触手強制放尿", "飲尿する", "体内放尿させる"
		RETURN 0
ENDSELECT

;ゲーセンでトイレじゃない場合は必死に耐える
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURN 0


SELECTCASE TFLAG:絶頂強度
	CASE IS >= 7
		放尿量 += 3
	CASE IS >= 5
		放尿量 += 2
	CASE IS >= 3
		放尿量 += 1
ENDSELECT

IF TEQUIP:利尿剤
	;利尿剤強化を行っている場合は強化値*10%の効果
	IF 利尿剤強化値
		放尿量 += 利尿剤強化値/10
	ELSE
		放尿量++
	ENDIF
ENDIF
SIF TALENT:おもらし癖
	放尿量++

SIF 放尿量 == 0
	RETURN 0

PRINTFORML %CALLNAME:TARGET%放尿
;PRINTFORML 放尿経験+{放尿量}
EXP:放尿経験 += 放尿量
CFLAG:絶頂放尿経験++
EX:放尿++
NOWEX:放尿 = 放尿量
SIF 撮影中()
	CFLAG:放尿を撮影された経験++
SIF TEQUIP:野外プレイ
	CFLAG:野外放尿経験++
SIF TEQUIP:お風呂場
	CFLAG:風呂場放尿経験++
SELECTCASE SELECTCOMS
	CASE "正常位", "後背位", "騎乗位", "背面座位", "対面座位"
		CFLAG:性交放尿経験++
	CASE "正常位アナル", "後背位アナル", "騎乗位アナル", "背面座位アナル", "対面座位アナル"
		CFLAG:アナル性交放尿経験++
ENDSELECT

;消耗は5倍まで
LOSEBASE:体力 += 5*MIN(放尿量, 5)
LOSEBASE:気力 += 10*MIN(放尿量, 5)
SOURCE:露出 += 500*放尿量
;ネガティブPALAMも5倍まで
SOURCE:不潔 += 800*MIN(放尿量, 5)
SOURCE:反感 += 300*MIN(放尿量, 5)

TEQUIP:利尿剤 -= 放尿量
SIF TEQUIP:利尿剤 <= 0
	TEQUIP:利尿剤 = 0

;汚れ追加
IF PENIS(TARGET)
	SETBIT STAIN:ペニス, 7
ELSEIF VAGINA(TARGET)
	SETBIT STAIN:ヴァギナ, 7
ENDIF

;ビデオ撮影時はボーナスカウント
SIF TEQUIP:ビデオカメラ
	VIDEOBONUS:放尿++

IF 避妊具(TARGET)
	CALL 避妊具処理, 0
	TFLAG:避妊具放尿 = 放尿量
ELSE
	;下着穿いてたら下着に尿汚れ追加
	SIF TEQUIP:下半身下着
		SETBIT STAIN:下着, 7

	;獣人、獣による催淫
	SELECTCASE CSTR:種族
		CASE "獣人", "獣"
			CFLAG:PLAYER:媚薬++
			SIF CFLAG:PLAYER:媚薬%5 == 0
				CFLAG:PLAYER:残数++
	ENDSELECT
ENDIF

;-------------------------------------------------
;潮噴き処理
;-------------------------------------------------
@SQUIRT_CHECK
#DIM DYNAMIC SQUIRT

;尿道使用中は出ない
SIF Ｕ使用中(TARGET)
	RETURN 0
;屍姦中も出ない
SIF CFLAG:死亡
	RETURN 0
;射精中も出ない
SIF BASE:射精 >= MAXBASE:射精
	RETURN 0

;男+去勢済みでＡ絶頂強度3以上だと潮を吹く
IF TALENT:去勢済み && SEX(TARGET) == 1 && PALAM:快Ａ+UP:快Ａ >= 30000
	SQUIRT = PALAM:快Ａ+UP:快Ａ
ELSE
	SQUIRT = UP:快Ｃ+PALAM:快Ｃ
ENDIF

IF SQUIRT/10000 >= 1
	PRINTFORML %CALLNAME:TARGET%潮噴き
	NOWEX:潮噴き = SQUIRT / 10000
	EX:潮噴き++
	SIF PENIS(TARGET)
		CFLAG:ペニスで潮を噴いた経験++
	;天使による感度アップ
	SIF CSTR:種族 == "天使"
		TFLAG:天使の感度ボーナス++
	;PALAMのDOWN処理は上の絶頂系で済ませてあるのでやらない
	EXP:絶頂経験 += SQUIRT/10000
	;消耗は5倍まで
	LOSEBASE:体力 += 10*MIN(NOWEX:潮噴き, 5)
	LOSEBASE:気力 += 15*MIN(NOWEX:潮噴き, 5)
	SOURCE:恐怖 += 50*NOWEX:潮噴き
	SOURCE:中毒 += 300*NOWEX:潮噴き
	SOURCE:液体 += 200*NOWEX:潮噴き
	SOURCE:欲情 += 100*NOWEX:潮噴き
	SOURCE:露出 += 200*NOWEX:潮噴き
	;ネガティブPALAMも5倍まで
	SOURCE:逸脱 += 400*MIN(NOWEX:潮噴き, 5)
ENDIF
