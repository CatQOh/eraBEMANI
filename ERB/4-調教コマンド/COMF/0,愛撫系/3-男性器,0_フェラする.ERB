;-------------------------------------------------
;フェラする
;-------------------------------------------------
@USERCOM_ABLE_フェラする, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;性器が汚れている場合（精液、アナル、粘液、血、尿）
コマンド実行条件 += "性器が汚れていると実行できない(技巧LV2以上or素質[汚れ無視]なら可)/"
IF GET_STAIN(STAIN:ペニス, "アナル", "粘液", "破瓜血", "尿")
	SELECTCASE PLAYER
		;主人
		CASE MASTER
			;汚れ無視じゃないと実行できない
			SIF !TALENT:MASTER:汚れ無視 && ABL:MASTER:技巧 <= 2
				COMRESULT = 0
		;助手
		CASE ASSI
			;汚臭敏感で従順3以下の場合フェラはできない（汚れ無視なら可能）
			SIF ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
				COMRESULT = 0
	ENDSELECT
ENDIF
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;対象がペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0

RETURNF COMRESULT

@COM_フェラする

PRINTL フェラする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_フェラチオ(300), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 200
SOURCE:液体 = 50
SOURCE:露出 = 30
SOURCE:不潔 = 10
SOURCE:反感 = 25

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快C , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス汚れは舐め取る
STAIN:ペニス = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 1

CALL EXPUP, "フェラ経験", 1, PLAYER

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1, PLAYER

IF !避妊具(TARGET)
	CALL 魔力回復, 3, PLAYER
	CALL 獣人催淫, 1, PLAYER
ENDIF

;実績:山吹でフェラさせる。もしくは山吹でフェラをする
SIF CHARANAMEF(PLAYER) == "山吹"
	CALL 実績解除, "殿方の笛を鳴らすのもお得意で？"

RETURN 1
