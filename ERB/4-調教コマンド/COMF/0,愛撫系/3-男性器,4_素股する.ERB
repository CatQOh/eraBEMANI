;-------------------------------------------------
;素股する
;-------------------------------------------------
@USERCOM_ABLE_素股する
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;対象がペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;実行者がペニスあるとダメ
コマンド実行条件 += "実行者に男性器があると実行できない/"
SIF PENIS(PLAYER)
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0
;PLAYERがＶ使用中はダメ
コマンド実行条件 += "実行者がV使用中は実行できない/"
SIF Ｖ使用中(PLAYER)
	COMRESULT = 0

RETURNF COMRESULT

@COM_素股する
#DIM DYNAMIC 増加量

PRINTL 素股する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｐ責め(200)

;素肌、下着なら汚れ次第で増加
IF 性器露出(PLAYER)
	SIF GET_STAIN(STAIN:PLAYER:ヴァギナ, "愛液", "精液")
		TIMES 増加量 , 1.50
ELSEIF (TEQUIP:PLAYER:下半身下着 && CFLAG:PLAYER:挿入可) || OVER_GENITAL(PLAYER) == CSTR:PLAYER:下半身下着
	SIF GET_STAIN(STAIN:PLAYER:下着, "愛液", "精液")
		TIMES 増加量 , 1.20
ENDIF

;装飾効果加算
増加量 += 装飾効果加算量(増加量, TARGET, "陰茎")

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 200
SOURCE:達成 = 100
SOURCE:液体 = 50
SOURCE:露出 = 50
SOURCE:屈従 = 100
SOURCE:逸脱 = 200
SOURCE:反感 = 200
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;装飾効果加算
SOURCE:快C += 装飾効果加算量(SOURCE:快C, TARGET, "陰茎")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:快C, TARGET, "陰茎")
;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF (TEQUIP:PLAYER:下半身下着 && CFLAG:PLAYER:挿入可) || OVER_GENITAL(PLAYER) == CSTR:PLAYER:下半身下着
	;下着に擦りつけた場合は下着が汚れる
	STAIN:PLAYER:下着 |= STAIN:ペニス
	STAIN:ペニス |= STAIN:PLAYER:下着
ELSEIF OVER_GENITAL(TARGET) == ""
	;実行者のヴァギナ⇔対象のペニスの汚れが移動
	STAIN:PLAYER:ヴァギナ |= STAIN:ペニス
	STAIN:ペニス |= STAIN:PLAYER:ヴァギナ
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

SIF !避妊具(TARGET)
	CALL 魔力回復, 3, PLAYER

RETURN 1

