;-------------------------------------------------
;パイズリする
;-------------------------------------------------
@USERCOM_ABLE_パイズリする, ARG
#FUNCTION
#DIM DYNAMIC 必要技巧

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;対象がペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0

;パイズリ実行判定
コマンド実行条件 += "実行者が男、絶壁なら技巧LV5以上必要/"
コマンド実行条件 += "実行者が貧乳なら技巧LV4以上必要/"
コマンド実行条件 += "実行者が並乳なら技巧LV3以上必要/"
コマンド実行条件 += "実行者が巨乳、豊乳なら技巧LV2以上必要/"
コマンド実行条件 += "実行者が爆乳なら技巧LV1以上必要/"
SELECTCASE TALENT:PLAYER:バストサイズ
	;男or絶壁なら技巧5以上必要
	CASE 0, 1
		必要技巧 = 5
	;貧乳なら技巧4以上必要
	CASE 2
		必要技巧 = 4
	;普通なら技巧3以上必要
	CASE 3
		必要技巧 = 3
	;巨乳、豊乳なら技巧LV2以上必要
	CASE 4, 5
		必要技巧 = 2
	;爆乳なら技巧LV1以上必要
	CASE 6
		必要技巧 = 1
ENDSELECT
SIF ABL:PLAYER:技巧 < 必要技巧
	COMRESULT = 0

RETURNF COMRESULT

@COM_パイズリする
PRINTL パイズリする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_パイズリする(400), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 300
SOURCE:達成 = 200
SOURCE:液体 = 50
SOURCE:露出 = 100
SOURCE:屈従 = 200
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 9

CALL SAMEN_SHOOT_T, 9, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;実行者の胸⇔対象のペニスの汚れが移動
STAIN:PLAYER:胸 |= STAIN:ペニス
STAIN:ペニス |= STAIN:PLAYER:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "パイズリ経験", 1, PLAYER

パイズリ回数:PLAYER:(NOF(TARGET)) += 1

RETURN 1
