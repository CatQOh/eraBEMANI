;-------------------------------------------------
;パイズリフェラする
;-------------------------------------------------
@USERCOM_ABLE_パイズリフェラする, ARG
#FUNCTION
#DIM DYNAMIC 必要技巧

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;対象がペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0

;パイズリ実行判定
コマンド実行条件 += "実行者が男、絶壁なら技巧LV7以上必要/"
コマンド実行条件 += "実行者が貧乳なら技巧LV6以上必要/"
コマンド実行条件 += "実行者が並乳なら技巧LV5以上必要/"
コマンド実行条件 += "実行者が巨乳、豊乳なら技巧LV4以上必要/"
コマンド実行条件 += "実行者が爆乳なら技巧LV3以上必要/"
SELECTCASE TALENT:PLAYER:バストサイズ
	;男or絶壁なら技巧LV7以上必要
	CASE 0, 1
		必要技巧 = 7
	;貧乳なら技巧LV6以上必要
	CASE 2
		必要技巧 = 6
	;普通なら技巧LV5以上必要
	CASE 3
		必要技巧 = 5
	;巨乳、豊乳なら技巧LV4以上必要
	CASE 4, 5
		必要技巧 = 4
	;爆乳なら技巧LV3以上必要
	CASE 6
		必要技巧 = 3
ENDSELECT
SIF ABL:PLAYER:技巧 < 必要技巧
	COMRESULT = 0

;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
コマンド実行条件 += "着衣状態によって実行できない場合あり/"
IF TEQUIP:PLAYER:上半身上着 || (TEQUIP:PLAYER:全身上着 && CFLAG:PLAYER:着衣併用!=1)
	SELECTCASE CFLAG:PLAYER:上着パイズリ
		CASE 0, 2, 4
			COMRESULT = 0
	ENDSELECT
ENDIF
IF TEQUIP:PLAYER:上半身下着
	SELECTCASE CFLAG:PLAYER:下着パイズリ
		CASE 0, 2, 4
			COMRESULT = 0
	ENDSELECT
ENDIF

RETURNF COMRESULT

@COM_パイズリフェラする
#DIM DYNAMIC 増加量

PRINTL パイズリフェラする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_パイズリする(600)

;TALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 500
SOURCE:達成 = 300
SOURCE:液体 = 50
SOURCE:露出 = 150
SOURCE:屈従 = 300
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;実行者の胸⇔対象のペニスの汚れが移動
STAIN:PLAYER:胸 |= STAIN:ペニス
STAIN:ペニス |= STAIN:PLAYER:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 2

CALL EXPUP, "フェラ経験", 1, PLAYER
CALL EXPUP, "パイズリ経験", 1, PLAYER

IF !避妊具(TARGET)
	CALL 魔力回復, 3, PLAYER
	CALL 獣人催淫, 1, PLAYER
ENDIF

;実績:山吹でフェラさせる。もしくは山吹でフェラをする
SIF CHARANAMEF(PLAYER) == "山吹"
	CALL 実績解除, "殿方の笛を鳴らすのもお得意で？"

RETURN 1
