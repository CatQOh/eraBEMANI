;-------------------------------------------------
;アナル愛撫
;-------------------------------------------------
@USERCOM_ABLE_アナル愛撫
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;プレイヤーが助手で、助手と対象が同姓の場合、潤滑不足で従順3以下・レズっ気ホモっ気3以下の場合アナル愛撫はできない（サドならＯＫ）
コマンド実行条件 += "[サド]の無い助手が実行者で同性の場合、ホモっ気、レズっ気LV3以下だと実行できない"
IF PLAYER != MASTER
	IF SEX(TARGET) == SEX(PLAYER)
		SIF PALAM:潤滑 < PALAMLV:2 && (ABL:PLAYER:従順 <= 3 || (ABL:PLAYER:レズっ気 <= 3 && ABL:PLAYER:ホモっ気 <= 3)) && !TALENT:PLAYER:サド
			COMRESULT = 0
	ENDIF
ENDIF

;異物挿入中はダメ
コマンド実行条件 += "A異物挿入中は実行できない/"
SIF TEQUIP:Ａ異物
	COMRESULT = 0

RETURNF COMRESULT

@COM_アナル愛撫
#DIM DYNAMIC 増加量

PRINTL アナル愛撫
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ責め(50)

;奴隷が男の場合はホモっ気を見る
IF SEX(TARGET) == 1
	SELECTCASE ABL:ホモっ気
		CASE 0
			TIMES 増加量 , 0.15
		CASE 1
			TIMES 増加量 , 0.30
		CASE 2
			TIMES 増加量 , 0.50
		CASE 3
			TIMES 増加量 , 0.75
		CASE 4
			TIMES 増加量 , 1.00
		CASEELSE
			増加量 += (増加量/10)*ABL:ホモっ気-3
	ENDSELECT
ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 80

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 50

SOURCE:快A = 100
SOURCE:苦痛 = 100
SOURCE:恐怖 = 30
SOURCE:屈従 = 100
SOURCE:逸脱 = 30
SOURCE:不潔 = 30
SOURCE:反感 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

CALL 特殊ソース処理_Ａ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手
	;奴隷のアナルに触手の汚れがつく
	SETBIT STAIN:アナル, 5
ELSE
	;奴隷のアナル⇔調教者の手の汚れが移動
	STAIN:アナル |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:アナル
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

SIF !Ａ使用中(TARGET)
	CALL EXPUP, "Ａ経験", 1

RETURN 1
