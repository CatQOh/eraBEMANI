;-------------------------------------------------
;自慰
;-------------------------------------------------
@USERCOM_ABLE_自慰, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は客に素質[マゾ]が必要/"
SIF 上級売春中 && !TALENT:マゾ
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;ペニスバンド装着中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;貞操帯装備中はダメ
コマンド実行条件 += "対象が貞操帯装着中は実行できない/"
SIF CFLAG:貞操帯
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_自慰
#DIM DYNAMIC 実効値, 2
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_自慰
実効値 += RESULT

;難易度上昇
;公開で+10、バイブで+5、アナルバイブで+5、シャワーで＋3
実効値:1 = 33
SIF 撮影中()
	実効値:1 += 10
SIF TEQUIP:バイブ
	実効値:1 += 5
SIF TEQUIP:アナルバイブ
	実効値:1 += 5
SIF TEQUIP:シャワー
	実効値:1 += 3

CALL 実効値判定, 実効値, 実効値:1

RETURN RESULT

@COM_自慰
#DIM DYNAMIC LABL
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 増加量

;自慰
;バイブ、アナルバイブ、ビデオ撮影の時に内容変化

;ビデオ撮影中なら公開オナニー
;シャワー使用中ならシャワーオナニー
;バイブ、アナルバイブ、或いは両方装備でバイブオナニー
;異物あれば異物オナニー
SIF 撮影中()
	PRINT 公開
SIF TEQUIP:シャワー
	PRINT シャワー
IF TEQUIP:バイブ && TEQUIP:アナルバイブ
	PRINT 二穴バイブ
ELSEIF TEQUIP:バイブ
	PRINT バイブ
ELSEIF TEQUIP:アナルバイブ
	PRINT アナルバイブ
ELSEIF TEQUIP:Ｖ異物 && TEQUIP:Ａ異物
	PRINT 両穴異物挿入
ELSEIF TEQUIP:Ｖ異物
	PRINT 異物
ELSEIF TEQUIP:Ａ異物
	PRINT アナル異物
ELSEIF TEQUIP:電動エネマグラ
	PRINT PC筋エネマグラ
ELSEIF TALENT:去勢済み
	PRINT 乳首アナル
ENDIF

IF 撮影中() || TEQUIP:シャワー || TEQUIP:バイブ || TEQUIP:アナルバイブ || TEQUIP:Ｖ異物 || TEQUIP:Ａ異物 || TEQUIP:電動エネマグラ
	PRINTL オナニー
ELSE
	PRINTL 自慰
ENDIF

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
;電動エネマ挿入中はいじらない
IF TEQUIP:電動エネマグラ
	増加量 = 射精ゲージ計算_自慰(5)
ELSE
	増加量 = 射精ゲージ計算_自慰(200)
ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 80

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

IF TALENT:去勢済み || ABL:メス度 >= 5
	SOURCE:快A = 80
ELSE
	SOURCE:快C = 80
ENDIF
SOURCE:快B = 80
SOURCE:性行動 = 150
SOURCE:恐怖 = 150
SOURCE:中毒 = 100
SOURCE:露出 = 300
SOURCE:屈従 = 200
SOURCE:逸脱 = 150
SOURCE:反感 = 300
;バイブ、Ｖ異物
IF TEQUIP:バイブ || TEQUIP:Ｖ異物
	SOURCE:快V += 100
	SOURCE:苦痛 += 50
	SOURCE:液体 += 100
	SOURCE:屈従 += 100
	SOURCE:反感 += 100
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;アナルバイブ、Ａ異物
IF TEQUIP:アナルバイブ || TEQUIP:Ａ異物
	SOURCE:快A += 100
	SOURCE:苦痛 += 150
	SOURCE:液体 += 30
	SOURCE:屈従 += 150
	SOURCE:反感 += 150
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;シャワー
IF TEQUIP:シャワー
	SOURCE:快C += 50
	SOURCE:性行動 += 50
	SOURCE:露出 += 100
	SOURCE:屈従 += 50
	SOURCE:反感 += 80
ENDIF
;ビデオ
IF 撮影中()
	SOURCE:恐怖 += 100
	SOURCE:液体 += 50
	SOURCE:露出 += 200
	SOURCE:欲情 += 100
	SOURCE:反感 += 200
ENDIF
;電動エネマグラ
IF TEQUIP:電動エネマグラ
	SOURCE:快C -= 80
	SOURCE:快A += 300
	SOURCE:露出 += 100
	SOURCE:性行動 += 50
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;中毒による中毒充足のソースは先に処理しておく
;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE 0, 1
		TIMES SOURCE:快C , 1.00
		TIMES SOURCE:快V , 1.00
		TIMES SOURCE:快A , 1.00
		TIMES SOURCE:快B , 1.00
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:快C , 1.10
		TIMES SOURCE:快V , 1.10
		TIMES SOURCE:快A , 1.10
		TIMES SOURCE:快B , 1.10
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:快C , 1.20
		TIMES SOURCE:快V , 1.20
		TIMES SOURCE:快A , 1.20
		TIMES SOURCE:快B , 1.20
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:快C , 1.30
		TIMES SOURCE:快V , 1.30
		TIMES SOURCE:快A , 1.30
		TIMES SOURCE:快B , 1.30
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:快C , 1.50
		TIMES SOURCE:快V , 1.50
		TIMES SOURCE:快A , 1.50
		TIMES SOURCE:快B , 1.50
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:快C , 1.70
		TIMES SOURCE:快V , 1.70
		TIMES SOURCE:快A , 1.50
		TIMES SOURCE:快B , 1.50
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:快C , 2.00
		TIMES SOURCE:快V , 2.00
		TIMES SOURCE:快A , 1.70
		TIMES SOURCE:快B , 1.70
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:快C , 2.30
		TIMES SOURCE:快V , 2.30
		TIMES SOURCE:快A , 2.00
		TIMES SOURCE:快B , 2.00
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:快C , 2.50
		TIMES SOURCE:快V , 2.50
		TIMES SOURCE:快A , 2.50
		TIMES SOURCE:快B , 2.50
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:快C , 3.50
		TIMES SOURCE:快V , 3.50
		TIMES SOURCE:快A , 3.50
		TIMES SOURCE:快B , 3.50
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手⇔奴隷の胸の汚れが移動
STAIN:手 |= STAIN:胸
STAIN:胸 |= STAIN:手

;奴隷の指⇔奴隷のヴァギナの汚れが移動
IF VAGINA(TARGET)
	STAIN:手 |= STAIN:ヴァギナ
	STAIN:ヴァギナ |= STAIN:手
ENDIF
;奴隷の指⇔奴隷のペニスの汚れが移動
IF !TEQUIP:電動エネマグラ
	IF PENIS(TARGET)
		STAIN:手 |= STAIN:ペニス
		STAIN:ペニス |= STAIN:手
	ENDIF
ENDIF

;シャワーオナニーの場合汚れをリセット、更に潤滑半分。
IF TEQUIP:シャワー
	STAIN:手 = 0
	STAIN:ヴァギナ = 0
	STAIN:ペニス = 0
	STAIN:胸 = 0
	PALAM:潤滑 /= 2
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;撮影時の調教内容チェック フロー図的には上書きしてるので問題なし
IF TEQUIP:ビデオカメラ
	IF TEQUIP:バイブ && TEQUIP:アナルバイブ
		ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = 二穴バイブオナニー
	ELSEIF TEQUIP:バイブ
		ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = バイブオナニー
	ELSEIF TEQUIP:アナルバイブ
		ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = アナルバイブオナニー
	ELSEIF TEQUIP:電動エネマグラ
		ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = エネマグラオナニー
	ELSEIF TEQUIP:シャワー
		ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = シャワーオナニー
	ENDIF
ENDIF

増加量 = 1
SIF 撮影中()
	増加量++
SIF TEQUIP:シャワー
	増加量++
SIF TEQUIP:バイブ
	増加量++
SIF TEQUIP:アナルバイブ
	増加量++
SIF TEQUIP:Ｖ異物
	増加量++
SIF TEQUIP:Ａ異物
	増加量++
SIF TEQUIP:電動エネマグラ
	増加量++

CALL EXPUP_BASE, 2, 増加量

;ビデオ撮影時は経験にプラス
IF 撮影中()
	CALL EXPUP, "自慰経験", 2
ELSE
	CALL EXPUP, "自慰経験", 1
ENDIF
;公開オナニー時は異常経験付加
IF 撮影中()
	IF !CFLAG:自慰を撮影された経験
		PRINTL ＜初自慰撮影＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CFLAG:自慰を撮影された経験++
ENDIF

RETURN 1
