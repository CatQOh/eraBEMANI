;-------------------------------------------------
;秘貝開帳
;-------------------------------------------------
@USERCOM_ABLE_秘貝開帳
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は客に素質[マゾ]が必要/"
SIF 上級売春中 && !TALENT:マゾ
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;従順が2未満だとダメ
コマンド実行条件 += "従順LV3以上必要/"
SIF ABL:従順 < 2
	COMRESULT = 0
;処女の場合、従順3以上または露出癖3以上ないとダメ
コマンド実行条件 += "処女の場合、従順LV3以上or露出癖LV3以上必要/"
SIF TALENT:処女 && ABL:従順 < 3 && ABL:露出癖 < 3
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0

;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;膣に何か入ってるとダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_秘貝開帳
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

実効値 = RESULT

IF ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	実効値 += ABL:欲望*3
ENDIF
IF ABL:Ｖ感覚
	PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
	実効値 += ABL:Ｖ感覚*2
ENDIF
IF ABL:奉仕精神 
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
	実効値 += ABL:奉仕精神*4
ENDIF
IF ABL:露出癖
	PRINTFORM +露出癖LV{ABL:露出癖}({ABL:露出癖*3})
	実効値 += ABL:露出癖*3
ENDIF
IF ABL:自慰中毒
	PRINTFORM +自慰中毒LV{ABL:自慰中毒}({ABL:自慰中毒*3})
	実効値 += ABL:自慰中毒*3
ENDIF
IF MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	実効値 += MARK:快楽刻印*3
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	PRINTFORM -潤滑不足(5)
	実効値 -= 5
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

IF TALENT:恥じらい
	PRINTFORM  -恥じらい(2)
	実効値 -= 2
ENDIF
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	PRINTFORM  -快感の否定(5)
	実効値 -= 5
ENDIF
IF TALENT:男嫌い && SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	PRINTFORM  -男嫌い(12)
	実効値 -= 12
ENDIF

;処女
IF TALENT:処女
	PRINTFORM  -処女(20)
	実効値 -= 20
ELSEIF EXP:Ｖ経験 < EXPLV:2
	PRINTFORM  -Ｖ経験不足(5)
	実効値 -= 5
ENDIF

;調教者がペニス持ち
IF PENIS(PLAYER)
	PRINTFORM  -%CALLNAME:PLAYER%がペニス有(4)
	実効値 -= 4
ENDIF

;媚薬
IF TEQUIP:媚薬
	PRINTFORM +媚薬(6)
	実効値 += 6
ENDIF

;18以上で実行
CALL 実効値判定, 実効値, 18

RETURN RESULT

@COM_秘貝開帳
SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF RESULT == 0
	RETURN 0

PRINTL 秘貝開帳
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 30
SOURCE:性行動 = 50
SOURCE:達成 = 10
SOURCE:恐怖 = 150
SOURCE:液体 = 30
SOURCE:露出 = 300
SOURCE:屈従 = 200
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 150

;処女、貞操観念
IF TALENT:処女
	TIMES SOURCE:恐怖 , 1.20
	TIMES SOURCE:屈従 , 1.20
	TIMES SOURCE:反感 , 1.20
	SOURCE:露出 += 300
ENDIF
IF TALENT:貞操観念
	TIMES SOURCE:露出 , 2.00
	TIMES SOURCE:逸脱 , 2.00
ENDIF

;装飾効果加算
SOURCE:快C += 装飾効果加算量(SOURCE:快C, TARGET, "陰核")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:快C, TARGET, "陰核")

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のヴァギナ⇔手の汚れが移動
STAIN:ヴァギナ |= STAIN:手
STAIN:手 |= STAIN:ヴァギナ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

;露出癖Lv3以上で自慰経験加算
SIF ABL:露出癖 >= 3
	CALL EXPUP, "自慰経験", 1

RETURN 1
