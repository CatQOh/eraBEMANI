;-------------------------------------------------
;秘貝開帳見せつけ
;-------------------------------------------------
@USERCOM_ABLE_秘貝開帳見せつけ
#FUNCTION
#DIM DYNAMIC 必要露出癖

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;実行者に膣無いとダメ
コマンド実行条件 += "実行者に女性器が必要/"
SIF !VAGINA(PLAYER)
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "対象が性器露出していないと実行できない/"
SIF !性器露出(PLAYER)
	COMRESULT = 0
;助手だと露出癖2以上必要 恥じらいだと3以上
コマンド実行条件 += "実行者が助手の場合は露出癖LV2以上必要 [恥じらい]持ちだとLV3以上必要/"
IF PLAYER == ASSI
	必要露出癖 = 2
	SIF TALENT:ASSI:恥じらい
		必要露出癖++
	SIF ABL:ASSI:露出癖 < 必要露出癖
		COMRESULT = 0
ENDIF
;ペニスバンド装着中はダメ
コマンド実行条件 += "実行者がペニスバンド装着中は実行できない/"
SIF TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_秘貝開帳見せつけ
PRINTL 秘貝開帳見せつけ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:性行動 = 40
SOURCE:欲情 = 20
SOURCE:恭順 = 20
SOURCE:露出 = 5
SOURCE:不潔 = 50
SOURCE:反感 = 30

;恋心に効果が高い
IF TALENT:恋心 == NOF(PLAYER)
	SOURCE:欲情 *= 2
	SOURCE:恭順 *= 2
	SOURCE:露出 *= 2
	SOURCE:不潔 /= 2
	SOURCE:反感 /= 2
;恥じらいにも効果的
ELSEIF TALENT:恥じらい
	SOURCE:露出 *= 5
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 0
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 0, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者の手⇔調教者のヴァギナの汚れが移動
STAIN:PLAYER:手 |= STAIN:PLAYER:ヴァギナ
STAIN:PLAYER:ヴァギナ |= STAIN:PLAYER:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1
