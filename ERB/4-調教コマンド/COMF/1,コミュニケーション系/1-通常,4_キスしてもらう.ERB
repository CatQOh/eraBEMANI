;-------------------------------------------------
;キスしてもらう
;-------------------------------------------------
@USERCOM_ABLE_キスしてもらう, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
;猫舌の場合は技巧Lv2以上必要
コマンド実行条件 += "対象が[猫舌]の場合は技巧LV2以上必要/"
SIF TALENT:猫舌 && ABL:技巧 < 2
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_キスしてもらう
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
;アニマルは無条件で可
SIF TALENT:アニマル
	RETURN 1

CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_キス
実効値 += RESULT

;恥じらい
IF TALENT:恥じらい
	実効値 -= 3
	PRINTFORM -恥じらい(3)
ENDIF

;25以上で実行
CALL 実効値判定, 実効値, 25

RETURN RESULT

@COM_キスしてもらう
#DIM DYNAMIC 増加量

PRINTL キスしてもらう

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（実行者）
;-------------------------------------------------
増加量 = 射精ゲージ計算_キス実行者(20)

IF TALENT:PLAYER:淫乱
	増加量 *= 2
ELSEIF TALENT:PLAYER:娼婦
	増加量 *= 3
ENDIF

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
IF !TEQUIP:睡眠
	増加量 = 射精ゲージ計算_キス(20)

	;TALENT:陥落系素質をみる
	;IF PLAYER == MASTER
	;	SIF TALENT:淫乱 || TALENT:服従
	;		TIMES 増加量 , 1.50
	;	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
	;		TIMES 増加量 , 2.00
	;	SIF TALENT:親愛
	;		TIMES 増加量 , 3.00
	;ENDIF

	CALL 射精ゲージ増加, 増加量, TARGET
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

IF FALLTYPE(TARGET) == 1
	SOURCE:情愛 = 700
	SOURCE:恭順 = 200
ELSE
	SOURCE:情愛 = 200
	SOURCE:恭順 = 30
ENDIF
SOURCE:達成 = 30
SOURCE:性行動 = 10
SOURCE:恭順 = 30
SOURCE:屈従 = 30
SOURCE:不潔 = 30
SOURCE:反感 = 10
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:情愛 , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:情愛 , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

;愛情経験
SIF FALLTYPE(TARGET) == 1 && !ASSIPLAY
	CALL EXPUP, "愛情経験", 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

;魔力回復 相性良いと1.0%、そうでもないと0.5%
IF 相性値(TARGET, MASTER) > 150
	増加量 = 10
ELSE
	増加量 = 5
ENDIF
CALL 魔力回復, 増加量
CALL 魔力回復, 増加量, PLAYER

CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

RETURN 1
