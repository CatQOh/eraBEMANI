;-------------------------------------------------
;情欲を煽る
;-------------------------------------------------
@USERCOM_ABLE_情欲を煽る
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中はソフトかSM方針じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がソフトかSMじゃないと実行できない/"
SIF 上級売春中 && !GROUPMATCH(1, 3)
	COMRESULT = 0
;連続実行はできない
SIF PREVCOMS == "情欲を煽る"
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_情欲を煽る
PRINTL 情欲を煽る
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 30

DOWNBASE:MASTER:体力 += 10
DOWNBASE:MASTER:気力 += 50

SOURCE:欲情 = ABL:PLAYER:技巧*100
SOURCE:露出 = 30
SOURCE:逸脱 = 200
SOURCE:反感 = 300

;幼稚には効果が薄い
IF TALENT:幼稚; || TALENT:幼児／幼児退行
	SOURCE:欲情 /= 10
	SOURCE:露出 /= 10
	SOURCE:逸脱 /= 10
	SOURCE:反感 /= 10
;感情乏しい、無関心にも効果薄い
ELSEIF TALENT:感情乏しい || TALENT:無関心
	SOURCE:欲情 /= 2
	SOURCE:露出 /= 2
	SOURCE:逸脱 /= 2
	SOURCE:反感 /= 2
;恥じらいに効果が高い
ELSEIF TALENT:恥じらい
	SOURCE:露出 *= 2
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1
