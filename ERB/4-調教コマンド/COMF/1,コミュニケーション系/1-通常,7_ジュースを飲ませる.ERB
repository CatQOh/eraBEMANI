;-------------------------------------------------
;ジュースを飲ませる
;-------------------------------------------------
@USERCOM_ABLE_ジュースを飲ませる
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実行判定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;ジュース持ってない or 保管済み粘液が200ml以上ないとダメ
コマンド実行条件 += "ジュース系アイテム、もしくは保管済み粘液が200ml以上必要/"
実行判定 = 0
SIF リソース:粘液 >= 200
	実行判定 = 1

FOR LCOUNT, 250, 300
	SIF !ITEM:LCOUNT
		CONTINUE
	SELECTCASE ITEMNAME:LCOUNT
		CASE "炭酸水", "コーラ", "ジンジャーエール", "トニックウォーター", "レモンジュース", "ライムジュース", "トマトジュース"
			実行判定 = 1
			BREAK
	ENDSELECT
NEXT

SIF !実行判定
	COMRESULT = 0

RETURNF COMRESULT

@COM_ジュースを飲ませる
PRINTL 何を飲ませますか？

SIF ITEM:炭酸水
	PRINTL [1] 炭酸水
SIF ITEM:コーラ
	PRINTL [2] コーラ
SIF ITEM:ジンジャーエール
	PRINTL [3] ジンジャーエール
SIF ITEM:トニックウォーター
	PRINTL [4] トニックウォーター
SIF ITEM:レモンジュース
	PRINTL [5] レモンジュース
SIF ITEM:ライムジュース
	PRINTL [6] ライムジュース
SIF リソース:粘液 >= 200
	PRINTL [7] スライムジュース
SIF ITEM:トマトジュース
	PRINTL [8] トマトジュース

PRINTL [100]やっぱりやめる

;何飲ませたかは「酒名」に入れる 酒じゃないけど……
SIF ISSKIP()
	RETURN
BINPUT
SELECTCASE RESULT
	CASE 1
		ITEM:炭酸水--
		酒名 = 炭酸水
	CASE 2
		ITEM:コーラ--
		酒名 = コーラ
	CASE 3
		ITEM:ジンジャーエール--
		酒名 = ジンジャーエール
	CASE 4
		ITEM:トニックウォーター--
		酒名 = トニックウォーター
	CASE 5
		ITEM:レモンジュース--
		酒名 = レモンジュース
	CASE 6
		ITEM:ライムジュース--
		酒名 = ライムジュース
	CASE 7
		リソース:粘液 -= 200
		酒名 = スライムジュース
	CASE 8
		ITEM:トマトジュース--
		酒名 = トマトジュース
	CASE 100
		PRINTW ジュースを飲ませるのを止めました
		RETURN 0
ENDSELECT

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
SOURCE:恭順 = 10
SIF 酒名 == "スライムジュース"
	CALL スライム度増加, 0, TARGET, 1, RAND(10, 25)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1
