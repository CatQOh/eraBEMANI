;-------------------------------------------------
;プレゼント
;-------------------------------------------------
@USERCOM_ABLE_プレゼント
#FUNCTION

コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
コマンド実行条件 += "[恋慕][親愛][服従][隷属]かつ実行者が主人じゃないと実行できない/"
SIF (!GROUPMATCH(FALLTYPE(TARGET), 1, 3) || PLAYER != MASTER) && !上級売春中
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@プレゼント名, プレゼント番号
#FUNCTIONS
#DIM DYNAMIC プレゼント番号
SELECTCASE プレゼント番号
	CASE 0
		RETURNF "スマイル"
	CASE 1
		RETURNF "パセリカード"
	CASE 2
		RETURNF "日用品"
	CASE 3
		RETURNF "アクセサリ"
	CASE 4
		RETURNF "服"
	CASE 5
		RETURNF "素敵な指輪"
	CASE 6
		RETURNF "特別仕様のIIDX筐体"
	CASE 7
		RETURNF "特注品のディルド"
	CASE 8
		RETURNF "特殊プレゼント"
	CASE 10
		RETURNF "育てたアルストロメリア"
	CASE 11
		RETURNF "育てた薔薇"
ENDSELECT
RETURNF ""

@COM_プレゼント
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 依存度計算用
#DIM DYNAMIC 価格
#DIM DYNAMIC 増加量
#DIM DYNAMIC 特殊プレゼント価格

PRINTL プレゼント

特殊プレゼント = 

;休日・祝日によってプレゼントできるアイテムが変化
IF ISHOLIDAY("元日")
	特殊プレゼント = お年玉
	特殊プレゼント価格 = 10000
ENDIF
IF ISHOLIDAY("節分")
	特殊プレゼント = 豆
	特殊プレゼント価格 = 50
ENDIF
IF ISHOLIDAY("バレンタインデー")
	特殊プレゼント = 手作りチョコ
	特殊プレゼント価格 = 500
ENDIF
IF ISHOLIDAY("ひな祭り") && SEX(TARGET) == 2
	特殊プレゼント = ひなあられ
	特殊プレゼント価格 = 100
ENDIF
IF ISHOLIDAY("KONAMI創立記念日")
	特殊プレゼント = コナミ株券
	特殊プレゼント価格 = 6000
ENDIF
IF ISHOLIDAY("十五夜")
	特殊プレゼント = 月見団子
	特殊プレゼント価格 = 100
ENDIF
IF ISHOLIDAY("ハロウィン")
	特殊プレゼント = お菓子
	特殊プレゼント価格 = 500
ENDIF
IF ISHOLIDAY("七五三") && TALENT:体格 == 1
	特殊プレゼント = 千歳飴
	特殊プレゼント価格 = 500
ENDIF
IF ISHOLIDAY("クリスマス・イブ") || ISHOLIDAY("クリスマス")
	特殊プレゼント = クリスマスプレゼント
	特殊プレゼント価格 = 10000
ENDIF

CALL PASELIL, @"何を贈りますか？(所持金{MONEY}＄)"

CALLFORM DISABLE, "プレゼント選択", TARGET
DISABLE = RESULT

;DISABLEはビット数で管理
SIF !GETBIT(DISABLE, 0)
	PRINTFORML [0] スマイル(タダ)
SIF !GETBIT(DISABLE, 1)
	CALL PASELIL, @"[1] パセリカード(1000＄)"
SIF !GETBIT(DISABLE, 2)
	CALL PASELIL, @"[2] 日用品(2500＄)"
SIF !GETBIT(DISABLE, 3)
	CALL PASELIL, @"[3] アクセサリ(10000＄)"
SIF !GETBIT(DISABLE, 4)
	CALL PASELIL, @"[4] 服(25000＄)"
SIF !GETBIT(DISABLE, 5)
	CALL PASELIL, @"[5] 素敵な指輪(100000＄)"
SIF !GETBIT(DISABLE, 6)
	CALL PASELIL, @"[6] 特別仕様のIIDX筐体(980000＄)"
SIF !GETBIT(DISABLE, 7)
	CALL PASELIL, @"[7] 特注品のディルド(30000＄)"
SIF 特殊プレゼント != "" && !GETBIT(DISABLE, 8)
	CALL PASELIL, @"[8] %特殊プレゼント%({特殊プレゼント価格}＄)"
SIF 材料判定("アルストロメリア") == 2 && !GETBIT(DISABLE, 10)
	PRINTL [10] 育てたアルストロメリア
SIF 材料判定("薔薇") == 2 && !GETBIT(DISABLE, 11)
	PRINTL [11] 育てた薔薇


PRINTFORML [100] やっぱりやめる

SIF ISSKIP()
	RETURN 0
BINPUT
SELECTCASE RESULT
	CASE 1
		価格 = 1000
	CASE 2
		価格 = 2500
	CASE 3
		価格 = 10000
	CASE 4
		価格 = 25000
	CASE 5
		価格 = 100000
	CASE 6
		価格 = 980000
	CASE 7
		価格 = 30000
	CASE 8
		価格 = 特殊プレゼント価格
	CASE 100
		PRINTFORMW プレゼントを渡すのを止めました
		RETURN 0
ENDSELECT

IF MONEY < 価格 && !ISSKIP()
	PRINTW 所持金が足りません
	RESTART
ENDIF

TFLAG:プレゼント = RESULT

MONEY -= 価格
CALL 現金出納帳記録, @"%NAME:TARGET%へのプレゼント費用/-{価格}"

SELECTCASE RESULT
	CASE 10
		;アルストロメリアをプレゼント 栽培していたアルストロメリアは無くなる
		FOR LCOUNT, 1, 5
			IF プランターの作物:LCOUNT == "アルストロメリア"
				プランターの作物:LCOUNT =
				BREAK
			ENDIF
		NEXT
	CASE 11
		;薔薇をプレゼント 栽培していた薔薇は無くなる
		FOR LCOUNT, 1, 5
			IF プランターの作物:LCOUNT == "薔薇"
				プランターの作物:LCOUNT =
				BREAK
			ENDIF
		NEXT
ENDSELECT

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 20

SELECTCASE TFLAG:プレゼント
	CASE 0
		SOURCE:情愛 = 100
		SOURCE:恭順 = 1000
	CASE 1
		SOURCE:情愛 = 500
		SOURCE:恭順 = 1500
	CASE 2
		SOURCE:情愛 = 1000
		SOURCE:恭順 = 2500
	CASE 3
		SOURCE:情愛 = 2000
		SOURCE:恭順 = 3500
	CASE 4
		SOURCE:情愛 = 2500
		SOURCE:恭順 = 5000
	CASE 5
		IF TALENT:親愛
			SOURCE:情愛 = 10000
			SOURCE:恭順 = 15000
		ELSE
			SOURCE:情愛 = 5000
			SOURCE:恭順 = 8000
		ENDIF
	CASE 6
		SOURCE:恐怖 = 3000
		SOURCE:逸脱 = 5000
	CASE 7
		SOURCE:欲情 = 3000
		SOURCE:恭順 = 1000
	;値段はまちまちだけど効果はスマイルと同じ
	CASE 8
		SOURCE:情愛 = 300
		SOURCE:恭順 = 1000
	CASE 10, 11
		IF TALENT:親愛
			SOURCE:情愛 = 5000
			SOURCE:恭順 = 10000
		ELSE
			SOURCE:情愛 = 3000
			SOURCE:恭順 = 5000
		ENDIF
ENDSELECT

;愛情経験
SELECTCASE TFLAG:プレゼント
	CASE 0, 7, 8
		増加量 = 1
	CASE 1
		増加量 = 5
	CASE 2
		増加量 = 10
	CASE 3
		増加量 = 15
	CASE 4
		増加量 = 20
	CASE 5
		IF TALENT:親愛
			増加量 = 50
		ELSE
			増加量 = 25
		ENDIF
	CASE 6
		IF CFLAG:ありえないものをプレゼントされた経験 == 0
			PRINTL 異常経験+3
			EXP:異常経験 += 3
		ENDIF
		CFLAG:ありえないものをプレゼントされた経験++
	CASE 10, 11
		増加量 = 10
ENDSELECT

SIF TFLAG:プレゼント != 6 && FALLTYPE(TARGET) == 1
	CALL EXPUP, "愛情経験", 増加量

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

SELECTCASE TFLAG:プレゼント
	CASE 1 TO 5
		依存度計算用 = TFLAG:プレゼント
	CASE 7, 8
		依存度計算用 = 1
	CASE 10, 11
		依存度計算用 = 10
ENDSELECT

;依存度補正値
TFLAG:依存度補正 += 依存度計算用

RETURN 1
