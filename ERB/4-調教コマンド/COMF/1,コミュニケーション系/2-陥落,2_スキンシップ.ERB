;-------------------------------------------------
;スキンシップ
;-------------------------------------------------
@USERCOM_ABLE_スキンシップ
#FUNCTION

コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
コマンド実行条件 += "[恋慕][親愛][服従][隷属]かつ実行者が主人じゃないと実行できない/"
SIF (!GROUPMATCH(FALLTYPE(TARGET), 1, 3) || PLAYER != MASTER) && !上級売春中
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_スキンシップ
#DIM DYNAMIC 増加量

PRINTL スキンシップ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
;何かのはずみで触れてしまったら
IF RAND:2 == 0 && !CFLAG:貞操帯
	増加量 = 50
ELSE
	増加量 = 0
ENDIF

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:PLAYER:技巧

;ABL:欲望をみる
増加量 += (増加量/5)*ABL:欲望

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 20

SOURCE:情愛 = 500
SOURCE:性行動 = 100
SOURCE:逸脱 = 10
SOURCE:達成 = 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "愛情経験", 1

RETURN 1
