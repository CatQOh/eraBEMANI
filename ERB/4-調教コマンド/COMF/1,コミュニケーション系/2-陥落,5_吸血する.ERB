;-------------------------------------------------
;吸血する
;-------------------------------------------------
@USERCOM_ABLE_吸血する
#FUNCTION

コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
コマンド実行条件 += "対象が陥落済みじゃないと実行できない/"
SIF !陥落(TARGET)
	COMRESULT = 0
コマンド実行条件 += "実行者が助手の場合は相性が101%以上必要/"
SIF PLAYER != MASTER && 相性値(TARGET, PLAYER) <= 100
	COMRESULT = 0
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
コマンド実行条件 += "対象が[ゾンビ]or[スライム]だと実行できない/"
SIF TALENT:ゾンビ || CSTR:種族 == "スライム"
	COMRESULT = 0
コマンド実行条件 += "実行者に[吸血鬼]が必要/"
SIF !TALENT:PLAYER:吸血鬼
	COMRESULT = 0
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_吸血する
#DIM DYNAMIC 増加量

PRINTL 吸血する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 50

SOURCE:情愛 = 1000
SOURCE:恭順 = 2000
SOURCE:屈従 = 1000
SOURCE:恐怖 = 500
SOURCE:逸脱 = 100

;実行者がスライムで、相手がスライム以外の場合はスライム感染が発生
SIF ISSLIME(PLAYER) && !ISSLIME(TARGET)
	CALL スライム度増加, 0, TARGET, RAND(1, 3)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
SIF PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF !CFLAG:被吸血経験
	CALL EXPUP, "異常経験", 1

CFLAG:被吸血経験++
CALL EXPUP, "吸血経験", 1, PLAYER

;魔力2.0%回復 処女の血なら3倍の6.0%回復
IF TALENT:処女
	増加量 = 60
ELSE
	増加量 = 20
ENDIF
CALL 魔力回復, 増加量, PLAYER
CALL 獣人催淫, 1, PLAYER

RETURN 1
