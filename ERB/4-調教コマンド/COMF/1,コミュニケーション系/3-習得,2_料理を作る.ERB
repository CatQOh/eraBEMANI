;-------------------------------------------------
;料理を作る
;-------------------------------------------------
@USERCOM_ABLE_料理を作る, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;助手可能もしくは陥落済みじゃないとダメ
コマンド実行条件 += "対象が陥落済みか、助手可能でないと実行できない/"
SIF !CFLAG:陥落 && CFLAG:状態 != 2
	COMRESULT = 0
;材料費が無いとダメ
コマンド実行条件 += "材料費1000$が必要/"
SIF MONEY < 1000
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
;お風呂場でもダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;屋外でもダメ
コマンド実行条件 += "野外プレイ中は実行できない/"
SIF TEQUIP:野外プレイ
	COMRESULT = 0
;一回の調教で一回まで
コマンド実行条件 += "1回の調教で1回まで/"
SIF TFLAG:料理
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_料理を作る
PRINTL 料理を作る
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;料理レベルを入れる 調教後に料理を振る舞う際に使う
TFLAG:料理 = ABL:料理技能
;玄人志向エプロンならボーナス
SIF TEQUIP:エプロン && SAVESTR:エプロン == "玄人志向エプロン"
	TFLAG:料理 += 5
SIF TFLAG:料理 == 0
	TFLAG:料理 = -1

;材料費
MONEY -= 1000
CALL 現金出納帳記録, @"料理の材料費/-1000"

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 300

SOURCE:習得 = 2000
SOURCE:恭順 = 100
SOURCE:屈従 = 100
SOURCE:逸脱 = 50
SOURCE:反感 = 50
SOURCE:達成 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;経験に応じてボーナス 低いと上がりやすい
SOURCE:習得 += (SOURCE:習得/100)*(100-EXP:料理経験/10)

;料理技能は先に処理しておく
;料理技能が低いと苦痛が入る おそらく怪我してる
SELECTCASE ABL:料理技能
	CASE 0
		TIMES SOURCE:達成 , 0.50
		SOURCE:苦痛 = 100
	CASE 1
		TIMES SOURCE:達成 , 0.70
		SOURCE:苦痛 = 50
	CASE 2
		TIMES SOURCE:達成 , 0.90
		SOURCE:苦痛 = 10
	CASE 3
		TIMES SOURCE:達成 , 1.10
	CASE 4
		TIMES SOURCE:達成 , 1.30
	CASE 5
		TIMES SOURCE:達成 , 1.50
	CASE 6
		TIMES SOURCE:達成 , 1.70
	CASE 7
		TIMES SOURCE:達成 , 1.90
	CASE 8
		TIMES SOURCE:達成 , 2.30
	CASE 9
		TIMES SOURCE:達成 , 2.70
	CASE IS >= 10
		TIMES SOURCE:達成 , 4.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "料理経験", 3

;実績:ゾンビに料理を作らせる
SIF CSTR:種族 == "ゾンビ"
	CALL 実績解除, "ポイズンクッキング"

RETURN 1
