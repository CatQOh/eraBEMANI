;-------------------------------------------------
;お風呂場プレイ
;-------------------------------------------------
@USERCOM_ABLE_お風呂場プレイ
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は設備無いと不可
コマンド実行条件 += "上級売春中はお風呂場設備が必要/"
SIF 上級売春中 && !BRITEM:お風呂場
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:お風呂場 && COMRESULT
	条件確定 = 1

;従順が1以上ないとダメ
コマンド実行条件 += "対象に従順LV2以上が必要/"
SIF ABL:従順 <= 1
	COMRESULT = 0
コマンド実行条件 += "対象が種族[獣][獣人]の場合、従順LV4以上必要/"
コマンド実行条件 += "対象が[無機物]の場合は実行できない/"
SELECTCASE CSTR:種族
	;対象か助手が種族：獣人、獣で、従順が3以下だとダメ
	CASE "獣人", "獣"
		SIF ABL:従順 <= 3
			COMRESULT = 0
ENDSELECT
;無機物はダメ
SIF TALENT:無機物
	COMRESULT = 0
;野外プレイ中はダメ
コマンド実行条件 += "野外プレイ中は実行できない/"
SIF TEQUIP:野外プレイ
	COMRESULT = 0
;羞恥プレイ中はダメ
コマンド実行条件 += "羞恥プレイ中は実行できない/"
SIF TEQUIP:羞恥プレイ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_お風呂場プレイ
#DIM DYNAMIC LCOUNT

IF TFLAG:ハニーパーティ
	PRINTL 媚薬ローション風呂プレイ
ELSE
	PRINTL お風呂場プレイ
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:お風呂場, 0
;シャワーは初期化
TEQUIP:シャワー = 0

;入る時は服を脱ぐ
IF TEQUIP:お風呂場
	VARSET TEQUIP:MASTER:0, 0, 0, 7
	VARSET TEQUIP:TARGET:0, 0, 0, 7
	SIF ASSI > 0
		VARSET TEQUIP:ASSI:0, 0, 0, 7
	;人魚だと体力気力2倍
	IF CSTR:種族 == "人魚"
		MAXBASE:体力 *= 2
		MAXBASE:気力 *= 2
		BASE:体力 *= 2
		BASE:気力 *= 2
	ENDIF
;終了時は服を着る ソース、経験の加算は行わない
ELSE
	FOR LCOUNT, 0, 7
		SIF CSTR:(LCOUNT+100) != ""
			TEQUIP:LCOUNT = 1
		SIF CSTR:MASTER:(LCOUNT+100) != ""
			TEQUIP:MASTER:LCOUNT = 1
		SIF ASSI > 0 && CSTR:ASSI:(LCOUNT+100) != ""
			TEQUIP:ASSI:LCOUNT = 1
	NEXT
	IF CSTR:種族 == "人魚"
		MAXBASE:体力 /= 2
		MAXBASE:気力 /= 2
		BASE:体力 /= 2
		BASE:気力 /= 2
	ENDIF
	RETURN 1
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 30

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SOURCE:情愛 = 100
SOURCE:恭順 = 50
SOURCE:恐怖 = 50
SOURCE:露出 = 200
SOURCE:逸脱 = 50
SOURCE:反感 = 100
;種族で補正
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		SOURCE:恐怖 += 50
		SOURCE:反感 += 100
	CASE "人魚"
		LOSEBASE:気力 = 0
		SOURCE:恐怖 = 0
ENDSELECT

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;装備時に汚れをリセットする（風呂に来て汚れたままもどうかということで）
RESET_STAIN MASTER
RESET_STAIN TARGET
SIF ASSI > 0
	RESET_STAIN ASSI

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_お風呂場プレイ
IF TFLAG:ハニーパーティ
	PRINTL ＜媚薬ローション風呂プレイ中＞
ELSE
	PRINTL ＜お風呂場プレイ中＞
ENDIF

LOSEBASE:体力 += 10
LOSEBASE:気力 += 10

SOURCE:恐怖 += 50
SOURCE:恭順 += 100
SOURCE:反感 += 50
IF TFLAG:ハニーパーティ
	SOURCE:液体 += 1000
	SOURCE:欲情 += 500
	CFLAG:PLAYER:媚薬++
	CFLAG:媚薬++
	SIF ASSI > 0
		CFLAG:ASSI:媚薬++
ENDIF

;種族で補正
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		SOURCE:恐怖 += 50
		SOURCE:反感 += 50
	CASE "人魚"
		LOSEBASE:体力 -= 10
		LOSEBASE:気力 -= 10
		SOURCE:恐怖 -= 50
		SOURCE:反感 -= 50
ENDSELECT

RETURN 1
