;-------------------------------------------------
;新妻プレイ
;-------------------------------------------------
@USERCOM_ABLE_新妻プレイ, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;奴隷が恋慕か親愛でないとダメ
コマンド実行条件 += "対象に[恋慕][親愛]が必要/"
SIF !TALENT:恋慕 && !TALENT:親愛
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:エプロン && COMRESULT
	条件確定 = 1

;アイテム｢エプロン｣を持っていないとダメ
コマンド実行条件 += "アイテム[エプロン]が必要/"
SIF !ITEM:エプロン
	COMRESULT = 0
;助手じゃダメ
コマンド実行条件 += "実行者が主人じゃないと実行できない/"
SIF PLAYER != MASTER
	COMRESULT = 0
;従順が3以上ないとダメ
コマンド実行条件 += "対象の従順LV3以上が必要/"
SIF ABL:従順 < 3
	COMRESULT = 0
;野外プレイ中はダメ
コマンド実行条件 += "野外プレイ中は実行できない/"
SIF TEQUIP:野外プレイ
	COMRESULT = 0
;羞恥プレイ中はダメ
コマンド実行条件 += "羞恥プレイ中は実行できない/"
SIF TEQUIP:羞恥プレイ
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_新妻プレイ
PRINTL 新妻プレイ
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:エプロン, 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_新妻プレイ
;助手調教時は、エプロンを解除して関数終了
IF PLAYER != MASTER
	TEQUIP:エプロン = 0
	RETURN 0
ENDIF

PRINTL ＜新妻プレイ中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 0

SOURCE:情愛 += 1000
SOURCE:恭順 += 5000
SOURCE:逸脱 += 500

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "愛情経験", 1

RETURN 1
