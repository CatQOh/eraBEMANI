;-------------------------------------------------
;DDRをプレイする
;-------------------------------------------------
@USERCOM_ABLE_DDRをプレイする
#FUNCTION
;ゲーセンに居ないとダメ
コマンド実行条件 += "ゲームセンターでのみ実行可能/"
SIF !TEQUIP:ゲームセンター
	COMRESULT = 0
;トイレに居るとダメ
コマンド実行条件 += "トイレに居る場合は実行できない/"
SIF TEQUIP:トイレ
	COMRESULT = 0
;足使用不可だとできない
コマンド実行条件 += "対象が[足使用不可]だと実行できない/"
SIF TALENT:足使用不可
	COMRESULT = 0
;筐体あるか確認
コマンド実行条件 += "ゲーセンにDDR筐体を置いてないと実行できない/"
SIF !EXISTGAMES("Dance Dance Revolution")
	COMRESULT = 0
;(ゲーセン経営してるなら特に問題は無いと思うけど)所持金無いとダメ
コマンド実行条件 += "2クレジット(200$)が必要 助手が居れば3クレジット(300$)必要/"
IF ASSI > 0
	SIF MONEY < 300
		COMRESULT = 0
ELSE
	SIF MONEY < 200
		COMRESULT = 0
ENDIF

;恥ずかしがり屋さんは露出癖2以上必要
コマンド実行条件 += "対象が[恥じらい]の場合は露出癖LV2以上が必要/"
SIF TALENT:恥じらい && ABL:露出癖 < 2
	COMRESULT = 0

RETURNF COMRESULT

@COM_DDRをプレイする
#DIM DYNAMIC 増加量

PRINTL DDRをプレイする
CALL KOJO_MESSAGE_COM

IF ASSI > 0
	MONEY -= 300
	CALL 現金出納帳記録, @"DDRをプレイ/-300"
ELSE
	MONEY -= 200
	CALL 現金出納帳記録, @"DDRをプレイ/-200"
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 50
SIF TALENT:音ゲーマー
	LOSEBASE:気力 -= 20

SOURCE:恭順 = 100
SOURCE:中毒 = 100
;SOURCE:習得 = 100
SOURCE:露出 = 300
SOURCE:苦痛 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TALENT:習得遅い
	増加量 = 1
ELSEIF TALENT:習得早い
	増加量 = 3
ELSE
	増加量 = 2
ENDIF
CALL EXPUP, "音ゲー経験", 増加量
CALL EXPUP, "ダンス経験", 増加量

IF TALENT:MASTER:習得遅い
	増加量 = 1
ELSEIF TALENT:MASTER:習得早い
	増加量 = 3
ELSE
	増加量 = 2
ENDIF
CALL EXPUP, "音ゲー経験", 増加量, MASTER
CALL EXPUP, "ダンス経験", 増加量, MASTER

IF ASSI > 0
	IF TALENT:ASSI:習得遅い
		増加量 = 1
	ELSEIF TALENT:ASSI:習得早い
		増加量 = 3
	ELSE
		増加量 = 2
	ENDIF
	CALL EXPUP, "音ゲー経験", 増加量, ASSI
	CALL EXPUP, "ダンス経験", 増加量, ASSI
ENDIF

RETURN 1
