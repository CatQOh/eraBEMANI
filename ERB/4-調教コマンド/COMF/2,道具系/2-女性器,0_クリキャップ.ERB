;-------------------------------------------------
;クリキャップ
;-------------------------------------------------
@USERCOM_ABLE_クリキャップ
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:クリキャップ && COMRESULT
	条件確定 = 1
;
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[クリキャップ]が必要/"
SIF ITEM:クリキャップ <= 0
	COMRESULT = 0

;ペニス持ちはダメ
コマンド実行条件 += "対象に男性器があると実行できない/"
SIF PENIS(TARGET)
	COMRESULT = 0
;去勢済みはダメ
コマンド実行条件 += "対象が[去勢済み]だと実行できない/"
SIF TALENT:去勢済み
	COMRESULT = 0
;シャワー中はダメ
コマンド実行条件 += "シャワー中は実行できない/"
SIF TEQUIP:シャワー
	COMRESULT = 0
;ペニスバンド装着中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_クリキャップ
IF TEQUIP:触手
	PRINTL 触手クリ責め
	SELECTCOM = GETNUM(TRAINNAME, "触手クリ責め")
ELSE
	PRINTL 電動クリキャップ
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:クリキャップ, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:快C = 150
SOURCE:露出 = 20
SOURCE:不潔 = 20
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:クリキャップ = -1
	TEQUIP:クリキャップ = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_クリキャップ
IF TEQUIP:触手
	PRINTL ＜触手クリ責め中＞
ELSE
	PRINTL ＜クリキャップ装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 20

SOURCE:快C += 100
SOURCE:露出 += 10
SOURCE:不潔 += 10
SOURCE:逸脱 += 15
SOURCE:反感 += 15

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1

