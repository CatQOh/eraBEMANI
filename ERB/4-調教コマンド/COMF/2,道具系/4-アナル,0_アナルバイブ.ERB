;-------------------------------------------------
;アナルバイブ
;-------------------------------------------------
@USERCOM_ABLE_アナルバイブ, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;電動エネマグラに改造してる場合は別コマンド扱い
SIF アイテム改造:アナルバイブ == "電動エネマグラ"
	COMRESULT = 0

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:アナルバイブ && COMRESULT
	条件確定 = 1

;小人体型持ちは主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[アナルバイブ]が必要/"
コマンド実行条件 += "　上級売春中は経営方針がソフトだと実行できない/"
IF 上級売春中
	;ソフト方針でもダメ
	SIF 娼館方針 == 1
		COMRESULT = 0
	SIF !ITEM:娼館アナルバイブ
		COMRESULT = 0
ELSE
	SIF ITEM:アナルバイブ <= 0
		COMRESULT = 0
ENDIF
;潤滑不足で、従順3以下・レズっ気ホモっ気3以下のASSIの場合アナルバイブはできない（サドならＯＫ）
コマンド実行条件 += @"実行者が助手で対象の潤滑{PALAMLV:2}未満の場合、レズっ気LV4orホモっ気LV4以上必要/"
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || (ABL:ASSI:レズっ気 <= 3 && ABL:ASSI:ホモっ気 <= 3)) && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF

;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_アナルバイブ
#DIM DYNAMIC 増加量
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:触手
	PRINTL アナル触手
	SELECTCOM = GETNUM(TRAINNAME, "アナル触手")
ELSE
	PRINTL アナルバイブ
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:アナルバイブ, 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 80
;改造済みだと変化
IF !TEQUIP:触手
	SELECTCASE アイテム改造:アナルバイブ
		CASE "強力なアナルバイブ"
			増加量 += 70
		CASE "細いアナルバイブ"
			増加量 -= 50
	ENDSELECT
ENDIF

増加量 = 射精ゲージ計算_Ａ挿入(増加量)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.10
	CASE 2
		TIMES 増加量 , 1.30
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/3)*ABL:自慰中毒
ENDSELECT

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快A = 250
SOURCE:苦痛 = 100
SOURCE:露出 = 50
SOURCE:不潔 = 30
SOURCE:屈従 = 50
SOURCE:逸脱 = 50
SOURCE:反感 = 150

IF !TEQUIP:触手
	SELECTCASE アイテム改造:アナルバイブ
		CASE "強力なアナルバイブ"
			SOURCE:快A += 150
		CASE "細いアナルバイブ"
			LOSEBASE:体力 -= 30
			LOSEBASE:気力 -= 70
			SOURCE:快A -= 150
			SOURCE:苦痛 -= 50
			SOURCE:逸脱 -= 30
			SOURCE:反感 -= 100
		CASE "太いアナルバイブ"
			SOURCE:A拡張 = 200
	ENDSELECT
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 70

CALL 特殊ソース処理_Ａ
SIF SOURCE:A拡張
	CALL 特殊ソース処理_Ａ拡張

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:アナルバイブ = -1
	TEQUIP:アナルバイブ = 0
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手使用中なら、触手体液がアナルに付く
SIF TEQUIP:触手
	SETBIT STAIN:アナル, 5

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

IF !TEQUIP:触手 && アイテム改造:アナルバイブ == "太いアナルバイブ"
	IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
		PRINTL ＜初Ａ拡張＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CALL EXPUP, "Ａ拡張経験", 2
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_アナルバイブ
#DIM DYNAMIC 増加量

;アナルバイブ挿入中
IF TEQUIP:触手
	PRINTL ＜アナル触手挿入中＞
ELSE
	PRINTL ＜アナルバイブ挿入中＞
ENDIF

LOSEBASE:体力 += 20
LOSEBASE:気力 += 40

SOURCE:快A += 150
SOURCE:苦痛 += 100
SOURCE:露出 += 25
SOURCE:不潔 += 10
SOURCE:逸脱 += 25
SOURCE:反感 += 40

IF !TEQUIP:触手
	SELECTCASE アイテム改造:アナルバイブ
		CASE "強力なアナルバイブ"
			SOURCE:快A += 50
		CASE "細いアナルバイブ"
			LOSEBASE:体力 -= 10
			LOSEBASE:気力 -= 20
			SOURCE:快A -= 100
			SOURCE:逸脱 -= 10
			SOURCE:反感 -= 20
		CASE "太いアナルバイブ"
			SOURCE:A拡張 += 50
	ENDSELECT
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 100
IF !TEQUIP:触手
	SELECTCASE アイテム改造:アナルバイブ
		CASE "強力なアナルバイブ"
			増加量 += 50
		CASE "細いアナルバイブ"
			増加量 -= 50
	ENDSELECT
ENDIF

増加量 = 射精ゲージ計算_Ａ挿入(増加量)

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.10
	CASE 2
		TIMES 増加量 , 1.30
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/3)*ABL:自慰中毒
ENDSELECT

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 35

CALL 特殊ソース処理_Ａ道具, 100, 40
SIF SOURCE:A拡張
	CALL 特殊ソース処理_Ａ道具拡張, 30, 20, 10

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;出し入れ実行時は経験+1
SELECTCASE SELECTCOMS
	CASE "自慰", "アナル愛撫"
		CALL EXPUP, "Ａ経験", 2
	CASE IS != "アナルバイブ"
		CALL EXPUP, "Ａ経験", 1
ENDSELECT

SIF アイテム改造:アナルバイブ == "太いアナルバイブ" && !TEQUIP:触手 && SELECTCOMS != "アナルバイブ"
	CALL EXPUP, "Ａ拡張経験", 1

SIF TEQUIP:触手
	TENTACLE++

RETURN 1
