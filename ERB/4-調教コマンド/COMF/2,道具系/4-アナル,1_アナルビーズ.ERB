;-------------------------------------------------
;アナルビーズ
;-------------------------------------------------
@USERCOM_ABLE_アナルビーズ, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:アナルビーズ && COMRESULT
	条件確定 = 1

;小人体型持ちは主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
コマンド実行条件 += "アイテム[アナルビーズ]が必要"
;アイテムを持っていないとダメ
SIF ITEM:アナルビーズ <= 0
	COMRESULT = 0

;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_アナルビーズ
#DIM DYNAMIC 増加量

PRINTL アナルビーズ
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:アナルビーズ, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 80

SOURCE:快A = 150
SOURCE:苦痛 = 200
SOURCE:露出 = 50
SOURCE:屈従 = 50
SOURCE:逸脱 = 60
SOURCE:不潔 = 30
SOURCE:反感 = 100

SELECTCASE アイテム改造:アナルビーズ
	CASE "大きなアナルビーズ", "いびつなアナルビーズ"
		LOSEBASE:体力 += 50
		LOSEBASE:気力 += 70
		
		SOURCE:快A += 150
		SOURCE:苦痛 += 300
		SOURCE:逸脱 += 140
		SOURCE:反感 += 200
ENDSELECT


;装着時は快Aは下がる
SIF TEQUIP:アナルビーズ
	TIMES SOURCE:快A , 0.80

;外す時は快Aは上がる
IF !TEQUIP:アナルビーズ
	;いびつ改造だと効果大
	IF アイテム改造:アナルビーズ == "いびつなアナルビーズ"
		LOSEBASE:体力 += 150
		LOSEBASE:気力 += 200
		
		SOURCE:快A *= 10
		SOURCE:苦痛 += 1000
		SOURCE:逸脱 += 500
		SOURCE:反感 += 500
	ELSE
		SOURCE:快A *= 3
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
IF TEQUIP:アナルビーズ
	増加量 = 80
ELSEIF !TEQUIP:アナルビーズ
	増加量 = 200
	SIF アイテム改造:アナルビーズ == "いびつなアナルビーズ"
		増加量 += 800
ENDIF

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:技巧/5

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES 増加量 , 0.60
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.20
	CASE 4
		TIMES 増加量 , 1.40
ENDSELECT

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.10
	CASE 2
		TIMES 増加量 , 1.30
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/3)*ABL:自慰中毒
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += 増加量*(ABL:Ａ感覚-3)
ENDSELECT

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

;奴隷が男の場合はホモっ気を見る
IF SEX(TARGET) == 1
	SELECTCASE ABL:ホモっ気
		CASE 0
			TIMES 増加量 , 0.15
		CASE 1
			TIMES 増加量 , 0.30
		CASE 2
			TIMES 増加量 , 0.50
		CASE 3
			TIMES 増加量 , 0.75
		CASE 4
			TIMES 増加量 , 1.00
		CASEELSE
			増加量 += (増加量/10)*ABL:ホモっ気-3
	ENDSELECT
;女なら無条件で半減
ELSE
	TIMES 増加量 , 0.50
ENDIF

;引き抜くときは快感2倍
SIF !TEQUIP:アナルビーズ
	増加量 *= 2

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抵抗 || TALENT:抑圧
	SOURCE:鬱屈 += 70

CALL 特殊ソース処理_Ａ

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:アナルビーズ = -1
	TEQUIP:アナルビーズ = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

IF !TEQUIP:アナルビーズ
	CALL EXPUP, "Ａ経験", 3
ELSEIF TEQUIP:アナルビーズ
	CALL EXPUP, "Ａ経験", 2
ENDIF

SIF CHARANAMEF(TARGET) == "トリスメギストス" && アイテム改造:アナルビーズ != "アナルビーズ"
	CALL 実績解除, "一番いいのを頼む"

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_アナルビーズ
#DIM DYNAMIC 増加量

;アナルビーズ挿入中
PRINTL ＜アナルビーズ挿入中＞

LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

SOURCE:快A += 70
SOURCE:露出 += 30
SOURCE:屈従 += 30
SOURCE:逸脱 += 30

IF アイテム改造:アナルビーズ == "大きなアナルビーズ"
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 50
	
	SOURCE:快A += 130
	SOURCE:苦痛 += 100
	SOURCE:屈従 += 20
	SOURCE:反感 += 50
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 20
SIF アイテム改造:アナルビーズ == "大きなアナルビーズ"
	増加量 += 30

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:技巧/5

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES 増加量 , 0.60
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.20
	CASE 4
		TIMES 増加量 , 1.40
ENDSELECT

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.10
	CASE 2
		TIMES 増加量 , 1.30
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/3)*ABL:自慰中毒
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += 増加量*(ABL:Ａ感覚-3)
ENDSELECT

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

;引き抜くときは快感2倍
SIF !TEQUIP:アナルビーズ
	増加量 *= 2

CALL 射精ゲージ増加, 増加量, TARGET


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 35

CALL 特殊ソース処理_Ａ道具, 100, 25

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SELECTCASE SELECTCOMS
	CASE "アナル愛撫", "自慰"
		CALL EXPUP, "Ａ経験", 2
	CASE IS != "アナルビーズ"
		CALL EXPUP, "Ａ経験", 1
ENDSELECT

RETURN 1
