;-------------------------------------------------
;浣腸器
;-------------------------------------------------
@USERCOM_ABLE_浣腸器, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

コマンド実行条件 += "スカMODが必要/"
SIF !MOD:SCAT
	COMRESULT = 0
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0

;その調教で既に排便済みなら出来ない
コマンド実行条件 += "1回の調教で1回まで"
SIF TFLAG:腸洗浄済み
	COMRESULT = 0

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:浣腸器 && COMRESULT
	条件確定 = 1

;精液浣腸は保管済み精液が100mlから必要
コマンド実行条件 += "精液浣腸の場合は100mlの保管済み精液が必要/"
SIF アイテム改造:浣腸器 == "精液浣腸" && リソース:精液 < 100
	COMRESULT = 0

;スライム浣腸は保管済み粘液が100mlから必要
コマンド実行条件 += "スライム浣腸の場合は100mlの保管済み粘液が必要/"
SIF アイテム改造:浣腸器 == "スライム浣腸" && リソース:粘液 < 100
	COMRESULT = 0

;無機物はダメ
コマンド実行条件 += "対象が[無機物]だと実行できない/"
SIF TALENT:無機物
	COMRESULT = 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[浣腸器]が必要/"
SIF !ITEM:浣腸器
	COMRESULT = 0
;Ａ経験が100未満じゃできない
コマンド実行条件 += "対象のA経験が100以上必要/"
SIF EXP:Ａ経験 < 100
	COMRESULT = 0
;従順、欲望の合計が6ないとできない
コマンド実行条件 += "対象の従順、欲望の合計がLV6以上必要/"
SIF ABL:従順+ABL:欲望 < 6
	COMRESULT = 0

;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_浣腸器
IF TEQUIP:触手
	PRINTL 触手浣腸
	SELECTCOM = GETNUM(TRAINNAME, "触手浣腸")
ELSE
	PRINTFORML %アイテム改造:浣腸器%
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:浣腸器, 0

;触手浣腸は触手の汚れを追加
SIF TEQUIP:触手
	SETBIT STAIN:アナル, 5
;精液浣腸は精液汚れ追加
IF アイテム改造:浣腸器 == "精液浣腸"
	SETBIT STAIN:アナル, 2
	リソース:精液 -= 100
ENDIF
;解除時
IF !TEQUIP:浣腸器
	IF !CFLAG:浣腸器で脱糞した経験
		PRINTL ＜初浣腸調教＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CFLAG:浣腸器で脱糞した経験++
	SIF 撮影中()
		CFLAG:脱糞を撮影された経験++
	SETBIT STAIN:アナル, 3
	TFLAG:腸洗浄済み = 1
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 150

SOURCE:快A = 300
SOURCE:苦痛 = 500
SOURCE:露出 = 1000
SOURCE:屈従 = 200
SOURCE:不潔 = 800
SOURCE:逸脱 = 800
SOURCE:反感 = 3000
;媚薬浣腸
IF アイテム改造:浣腸器 == "媚薬浣腸" && !TEQUIP:触手
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 50
	SOURCE:欲情 = 500
	CFLAG:媚薬 += 3
	TEQUIP:媚薬 = 1
ENDIF
;精液浣腸
IF アイテム改造:浣腸器 == "精液浣腸" && !TEQUIP:触手
	SOURCE:露出 += 3000
	SOURCE:反感 += 3000
	SOURCE:不潔 += 700
ENDIF
;スライム浣腸
IF アイテム改造:浣腸器 == "スライム浣腸" && !TEQUIP:触手
	SOURCE:露出 += 3000
	SOURCE:反感 += 3000
	SOURCE:不潔 += 700
	IF CSTR:種族 == "スライム"
		CFLAG:スライム度 += RAND(30, 60)
	ELSE
		CFLAG:スライム度 += RAND(1, 3)
	ENDIF
ENDIF
;解除時
IF !TEQUIP:浣腸器
	SOURCE:露出 += 1000
	SOURCE:反感 += 2000
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

CALL 特殊ソース処理_Ａ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

SIF アイテム改造:浣腸器 == "精液浣腸" && !TEQUIP:触手
	CALL EXPUP, "精液経験", 3

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_浣腸器
;浣腸+アナルプラグ挿入中
IF TEQUIP:触手
	PRINTL ＜浣腸触手挿入中＞
ELSE
	PRINTL ＜浣腸器挿入中＞
ENDIF

LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快A += 150
SOURCE:屈従 += 100
SOURCE:露出 += 300
SOURCE:不潔 += 300
SOURCE:逸脱 += 250

IF アイテム改造:浣腸器 == "媚薬浣腸" && !TEQUIP:触手
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 50

	SOURCE:欲情 += 100
	CFLAG:媚薬++
ENDIF
IF アイテム改造:浣腸器 == "精液浣腸" && !TEQUIP:触手
	SOURCE:露出 += 1000
	SOURCE:反感 += 500
	SOURCE:不潔 += 200
ENDIF
IF アイテム改造:浣腸器 == "スライム浣腸" && !TEQUIP:触手
	SOURCE:露出 += 1000
	SOURCE:反感 += 500
	SOURCE:不潔 += 200
	IF CSTR:種族 == "スライム"
		CFLAG:スライム度 += RAND(30, 60)
	ELSE
		CFLAG:スライム度 += RAND(1, 3)
	ENDIF
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 250

CALL 特殊ソース処理_Ａ道具, 250, 250

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
SELECTCASE SELECTCOMS
	CASE "浣腸器"
		CALL EXPUP, "Ａ経験", 3
	CASEELSE
		CALL EXPUP, "Ａ経験", 1
ENDSELECT

SIF アイテム改造:浣腸器 == "精液浣腸" && !TEQUIP:触手 && SELECTCOMS != "浣腸器"
	CALL EXPUP, "精液経験", 1

RETURN 1
