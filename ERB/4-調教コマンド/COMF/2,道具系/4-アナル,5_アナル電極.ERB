;-------------------------------------------------
;アナル電極
;-------------------------------------------------
@USERCOM_ABLE_アナル電極
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;奴隷が服従か隷属でないとダメ
コマンド実行条件 += "対象が[服従][隷属]or実行者に[サド]が必要/"
IF FALLTYPE(TARGET) != 3
	;ただしPLAYERがサドならおＫ
	SIF !TALENT:PLAYER:サド
		COMRESULT = 0
ENDIF

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:アナル電極
	条件確定 = 1

;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[電極プラグ]が必要/"
SIF ITEM:電極プラグ <= 0
	COMRESULT = 0
;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_アナル電極
PRINTL アナル電極
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:アナル電極, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 200

SOURCE:快A = 500
SOURCE:苦痛 = 1500
SOURCE:露出 = 300
SOURCE:屈従 = 800
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

CALL 特殊ソース処理_Ａ強

SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 8.00

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電極プラグ = -1
	TEQUIP:アナル電極 = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;死んでるときは射精ゲージ増加
SIF CFLAG:死亡
	CALL 射精ゲージ増加, 10000, TARGET

CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

;初めてなら異常経験追加
IF !CFLAG:電極で責められた経験
	PRINTL ＜初電極調教＞
	CALL EXPUP, "異常経験", 1
ENDIF
CFLAG:電極で責められた経験++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_アナル電極
PRINTL ＜アナル電極挿入中＞

LOSEBASE:体力 += 90
LOSEBASE:気力 += 150

SOURCE:快A += 250
SOURCE:屈従 += 400
SOURCE:逸脱 += 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

CALL 特殊ソース処理_Ａ道具強, 750, 600

SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 8.00

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "Ａ経験", 5

RETURN 1
