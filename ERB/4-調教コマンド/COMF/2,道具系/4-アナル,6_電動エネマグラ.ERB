;-------------------------------------------------
;電動エネマグラ
;-------------------------------------------------
@USERCOM_ABLE_電動エネマグラ
#FUNCTION
#DIM DYNAMIC 条件確定

;アナルバイブを電動エネマグラに改造してないと不可
SIF アイテム改造:アナルバイブ != "電動エネマグラ"
	COMRESULT = 0

;男性のみ
コマンド実行条件 += "対象が男性じゃないと実行できない/"
SIF SEX(TARGET) == 2
	COMRESULT = 0
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:電動エネマグラ && COMRESULT
	条件確定 = 1

;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "電動エネマグラに改造した[アナルバイブ]が必要/"
SIF ITEM:アナルバイブ <= 0
	COMRESULT = 0
;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_電動エネマグラ
PRINTL 電動エネマグラ
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:電動エネマグラ, 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_Ａ挿入(10), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 40

SOURCE:快A = 30
SOURCE:苦痛 = 50
SOURCE:露出 = 200
SOURCE:不潔 = 30
SOURCE:屈従 = 100
SOURCE:逸脱 = 50
SOURCE:反感 = 150

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 70

CALL 特殊ソース処理_Ａ

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:アナルバイブ = -1
	TEQUIP:電動エネマグラ = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 2

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_電動エネマグラ
PRINTL ＜電動エネマグラ挿入中＞

LOSEBASE:体力 += 10
LOSEBASE:気力 += 20

SOURCE:快A += 300
SOURCE:苦痛 += 50
SOURCE:露出 += 50
SOURCE:不潔 += 10
SOURCE:逸脱 += 50
SOURCE:反感 += 50

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_Ａ挿入(5), TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 35

CALL 特殊ソース処理_Ａ道具, 100, 40

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "Ａ経験", 1

RETURN 1
