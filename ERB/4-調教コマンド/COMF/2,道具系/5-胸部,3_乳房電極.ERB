;-------------------------------------------------
;乳房電極
;-------------------------------------------------
@USERCOM_ABLE_乳房電極, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;奴隷が服従か隷属でないとダメ
コマンド実行条件 += "対象が[服従][隷属]or実行者に[サド]が必要/"
IF !TALENT:服従 && !TALENT:隷属
	;ただしPLAYERがサドならおＫ
	SIF !TALENT:PLAYER:サド
		COMRESULT = 0
ENDIF

;胸が露出してないとダメ
コマンド実行条件 += "対象の乳房が露出していないと実行できない/"
SIF OVER_BUST(TARGET) != ""
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:乳房電極 && COMRESULT
	条件確定 = 1
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[電極プラグ]が必要/"
SIF ITEM:電極プラグ <= 0
	COMRESULT = 0
;胸に道具ついてるとダメ
コマンド実行条件 += "B道具使用中は実行できない/"
SIF Ｂ使用中(TARGET)
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_乳房電極
PRINTL 乳房電極
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:乳房電極, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 180

SOURCE:快B = 500
SOURCE:苦痛 = 1500
SOURCE:露出 = 300
SOURCE:屈従 = 800
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電極プラグ = -1
	TEQUIP:乳房電極 = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 5

;初めてなら異常経験追加
IF !CFLAG:電極で責められた経験
	PRINTL ＜初電極調教＞
	CALL EXPUP, "異常経験", 1
ENDIF
CFLAG:電極で責められた経験++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_乳房電極
PRINTL ＜乳房電極装着中＞

LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快B += 250
SOURCE:苦痛 += 750
SOURCE:屈従 += 400
SOURCE:逸脱 += 600
SOURCE:反感 += 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

RETURN 1
