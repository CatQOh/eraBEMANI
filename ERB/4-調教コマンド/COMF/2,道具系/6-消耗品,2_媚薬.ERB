;-------------------------------------------------
;媚薬
;-------------------------------------------------
@USERCOM_ABLE_媚薬, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中はアイテム無いとダメ
コマンド実行条件 += "アイテム[媚薬]が必要/"
IF 上級売春中
	SIF !BRITEM:媚薬
		COMRESULT = 0
ELSE
	;アイテムを持っているか、触手調教中でないとダメ
	SIF !ITEM:媚薬 && !TEQUIP:触手
		COMRESULT = 0
ENDIF
;薬毒耐性だとダメ
コマンド実行条件 += "対象が[薬毒耐性]を持っていると実行できない/"
SIF TALENT:薬毒耐性
	COMRESULT = 0
;[無機物]か種族:機械だとダメ
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_媚薬
PRINTL 媚薬
CALL KOJO_MESSAGE_COM

SIF !TEQUIP:触手 && !上級売春中
	ITEM:媚薬--

;媚薬強化を行っている場合は強化値と同値を加算
IF 媚薬強化値
	CFLAG:媚薬 += 媚薬強化値
ELSE
	CFLAG:媚薬 += 10
ENDIF
TEQUIP:媚薬 = 1

IF 媚薬強化値
	CFLAG:PLAYER:媚薬 += 媚薬強化値/2
ELSE
	CFLAG:PLAYER:媚薬 += 5
ENDIF
TEQUIP:PLAYER:媚薬 = 1

IF PENIS(TARGET)
	IF 媚薬強化値
		CFLAG:残数 += 媚薬強化値/5
	ELSE
		CFLAG:残数 += 2
	ENDIF
ENDIF
IF PENIS(PLAYER)
	IF 媚薬強化値
		CFLAG:PLAYER:残数 += 媚薬強化値/10
	ELSE
		CFLAG:PLAYER:残数++
	ENDIF
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
;調教者が調合知識を持っていれば消費を抑えられる
IF TALENT:PLAYER:調合知識
	LOSEBASE:体力 += 150
	LOSEBASE:気力 += 150
ELSE
	LOSEBASE:体力 += 300
	LOSEBASE:気力 += 300
ENDIF

DOWNBASE:MASTER:体力 += 100
DOWNBASE:MASTER:気力 += 100

;媚薬効果で欲情のアップが2倍になるので実際には+5000
UP:欲情 = 2500
SOURCE:逸脱 = 1000

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

SELECTCASE CHARANAMEF(TARGET)
	CASE "ユーズ"
		媚薬実績用:0 = 1
	CASE "士朗"
		媚薬実績用:1 = 1
	CASE "エレキ"
		媚薬実績用:2 = 1
ENDSELECT

RETURN 1
