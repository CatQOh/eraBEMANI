;-------------------------------------------------
;ミッドナイトスペシャル
;-------------------------------------------------
@USERCOM_ABLE_ミッドナイトスペシャル
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[ミッドナイトスペシャル]が必要/"
SIF !ITEM:ミッドナイトスペシャル
	COMRESULT = 0
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	COMRESULT = 0
;[無機物]もしくは種族:機械だとダメ
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
;ペニス無いとダメ
コマンド実行条件 += "使用者に男性器が必要/"
SIF !PENIS(TMASTER) && !PENIS(TARGET) && !PENIS(ASSI)
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_ミッドナイトスペシャル
PRINTL ミッドナイトスペシャルを使用するキャラを選んでください
SIF PENIS(TMASTER)
	PRINTFORML [1] - %CALLNAME:TMASTER%
SIF PENIS(TARGET)
	PRINTFORML [2] - %CALLNAME:TARGET%
SIF PENIS(ASSI)
	PRINTFORML [3] - %CALLNAME:ASSI%
PRINTL [0] - やめる
SIF ISSKIP()
	RETURN
BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		IF PENIS(TMASTER)
			TFLAG:ミッドナイトスペシャル使用者 = TMASTER
			CFLAG:TMASTER:ミッドナイトスペシャル = 5
		ENDIF
	CASE 2
		IF PENIS(TARGET)
			TFLAG:ミッドナイトスペシャル使用者 = TARGET
			CFLAG:ミッドナイトスペシャル = 5
			SIF 特質("薬効強化")
				CFLAG:ミッドナイトスペシャル *= 特質("薬効強化")/5
		ENDIF
	CASE 3
		IF PENIS(ASSI)
			TFLAG:ミッドナイトスペシャル使用者 = ASSI
			CFLAG:ASSI:ミッドナイトスペシャル = 5
			SIF 特質("薬効強化", ASSI)
				CFLAG:ASSI:ミッドナイトスペシャル *= 特質("薬効強化", ASSI)/5
		ENDIF
ENDSELECT

CALL KOJO_MESSAGE_COM

ITEM:ミッドナイトスペシャル--

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 5

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1
