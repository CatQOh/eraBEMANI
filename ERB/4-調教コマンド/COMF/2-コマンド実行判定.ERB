@COM_ORDER
#DIM DYNAMIC 実効値, 2

IF OPTION:口上表示 && !CFLAG:口上非表示 && EXISTFUNCTION(@"COM_ORDER_%SELECTCOMS%_%CSVNAMERPS(NO:TARGET)%")
	CALLFORM COM_ORDER_%SELECTCOMS%_%CSVNAMERPS(NO:TARGET)%
	IF RESULT
		実効値 += RESULT
		PRINTFORM \@ SIGN(RESULT) == 1 ? + # - \@口上補正({RESULT})
	ENDIF
ENDIF

IF ABL:従順
	実効値 += ABL:従順*4
	PRINTFORM +従順LV{ABL:従順}({ABL:従順*4})
ENDIF
IF ABL:マゾっ気
	実効値 += ABL:マゾっ気*2
	PRINTFORM +マゾっ気LV{ABL:マゾっ気}({ABL:マゾっ気*2})
ENDIF

;レズ調教
IF SEX(PLAYER) == 2 && SEX(TARGET) == 2
	IF ABL:レズっ気
		実効値 += ABL:レズっ気*3
		PRINTFORM +レズっ気LV{ABL:レズっ気}({ABL:レズっ気*3})
	ENDIF
	IF ABL:レズ中毒
		実効値 += ABL:レズ中毒*3
		PRINTFORM +レズ中毒LV{ABL:レズ中毒}({ABL:レズ中毒*3})
	ENDIF
	IF TALENT:両刀
		実効値 += 10
		PRINTFORM +両刀(10)
	ENDIF
	IF TALENT:好奇心
		実効値 += 7
		PRINTFORM +好奇心(7)
	ENDIF
	IF TALENT:保守的
		実効値 -= 13
		PRINTFORM -保守的(13)
	ENDIF
;ゲイ調教
ELSEIF SEX(PLAYER) == 1 && SEX(TARGET) == 1
	IF ABL:ホモっ気
		実効値 += ABL:ホモっ気*3
		PRINTFORM +ホモっ気LV{ABL:ホモっ気}({ABL:ホモっ気*3})
	ENDIF
	IF ABL:ゲイ中毒
		実効値 += ABL:ゲイ中毒*3
		PRINTFORM +ゲイ中毒LV{ABL:ゲイ中毒}({ABL:ゲイ中毒}*3)
	ENDIF
	IF TALENT:両刀
		実効値 += 10
		PRINTFORM +両刀(10)
	ENDIF
	IF TALENT:好奇心
		実効値 += 7
		PRINTFORM +好奇心(7)
	ENDIF
	IF TALENT:保守的
		実効値 -= 13
		PRINTFORM -保守的(13)
	ENDIF
ELSE
	IF TALENT:好奇心
		実効値 += 5
		PRINTFORM +好奇心(5)
	ENDIF
	IF TALENT:保守的
		実効値 -= 10
		PRINTFORM -保守的(10)
	ENDIF
ENDIF

;刻印チェック
IF MARK:苦痛刻印
	実効値 += MARK:苦痛刻印*5
	PRINTFORM +苦痛刻印LV{MARK:苦痛刻印}({MARK:苦痛刻印*5})
ENDIF

;プライド
IF TALENT:プライド高い
	実効値:1 = 4
ELSEIF TALENT:プライド低い
	実効値:1 = 1
ELSE
	実効値:1 = 2
ENDIF

;屈服刻印および反発刻印とプライド
IF MARK:屈服刻印
	実効値 += MARK:屈服刻印*3*実効値:1
	PRINTFORM +屈服刻印LV{MARK:屈服刻印}({MARK:屈服刻印*3*実効値:1})
ENDIF
IF MARK:反発刻印
	実効値 -= MARK:反発刻印*2*実効値:1
	PRINTFORM -反発刻印LV{MARK:反発刻印}({MARK:反発刻印*2*実効値:1})
ENDIF

実効値:1 = GETPALAMLV(PALAM:恭順, 5)
IF 実効値:1
	実効値 += 実効値:1*3
	PRINTFORM +恭順LV{実効値:1}({実効値:1*3})
ENDIF

;PALAM:恐怖
実効値:1 = GETPALAMLV(PALAM:恐怖, 5)
IF 実効値:1
	実効値 += 実効値:1*3
	PRINTFORM +恐怖LV{実効値:1}({実効値:1*3})
ENDIF

IF TALENT:服従
	実効値 += 10
	PRINTFORM +服従(10)
ENDIF
IF TALENT:隷属
	実効値 += 20
	PRINTFORM +隷属(20)
ENDIF
IF TALENT:反抗的 && !陥落(TARGET)
	実効値 -= 5
	PRINTFORM -反抗的(5)
ENDIF
IF TALENT:気丈
	実効値 -= 5
	PRINTFORM -気丈(5)
ENDIF
IF TALENT:素直
	実効値 += 5
	PRINTFORM +素直(5)
ENDIF
IF TALENT:プライド高い
	実効値 -= 15
	PRINTFORM -プライド高い(15)
ENDIF
IF TALENT:プライド低い
	実効値 += 5
	PRINTFORM +プライド低い(5)
ENDIF
IF TALENT:ツンデレ
	IF ABL:従順 < 4
		実効値 -= 7
		PRINTFORM -ツンデレ(7)
	ELSE
		実効値 += 7
		PRINTFORM +ツンデレ(7)
	ENDIF
ENDIF
IF TALENT:目立ちたがり
	実効値 += 2
	PRINTFORM +目立ちたがり(2)
ENDIF
IF TALENT:抑圧
	実効値 -= 10
	PRINTFORM -抑圧(10)
ENDIF
IF TALENT:抵抗
	実効値 -= 12
	PRINTFORM -抵抗(12)
ENDIF
IF TALENT:弱味
	実効値 += 12
	PRINTFORM +弱味(12)
ENDIF
IF TALENT:弱気
	実効値 += 8
	PRINTFORM +弱気(8)
ENDIF
IF TALENT:強気
	実効値 -= 8
	PRINTFORM -強気(8)
ENDIF
IF TALENT:即落ち
	実効値 += 10
	PRINTFORM +即落ち(10)
ENDIF
IF TALENT:妄信
	実効値 += 8
	PRINTFORM +妄信(8)
ENDIF
IF TALENT:寂しがり屋 && ASSI > 0
	実効値 += 6
	PRINT +寂しがり屋(6)
ENDIF
IF TALENT:同族嫌悪 && CSTR:種族 == CSTR:PLAYER:種族
	実効値 -= 10
	PRINT -同族嫌悪(10)
ENDIF

IF TALENT:男好き && SEX(PLAYER) == 1
	実効値 += 5
	PRINT +男好き(5)
ENDIF
IF TALENT:女好き && SEX(PLAYER) == 2
	実効値 += 5
	PRINT +女好き(5)
ENDIF
IF TALENT:男嫌い && SEX(PLAYER) == 1
	実効値 -= 10
	PRINT -男嫌い(10)
ENDIF
IF TALENT:女嫌い && SEX(PLAYER) == 2
	実効値 -= 10
	PRINT -女嫌い(10)
ENDIF
IF TALENT:サボり魔
	実効値 -= 3
	PRINT -サボり魔(3)
ENDIF
IF TALENT:子供好き && TALENT:PLAYER:体格 <= 2
	実効値 += 3
	PRINT +子供好き(3)
ENDIF
SELECTCASE CFLAG:スライム度
	CASE 1 TO 10
		実効値 += 3
		PRINT +スライム侵食Lv1(3)
	CASE 11 TO 20
		実効値 += 5
		PRINT +スライム侵食Lv2(5)
	CASE 21 TO 30
		実効値 += 7
		PRINT +スライム侵食Lv3(7)
	CASE 31 TO 1499
		実効値 += 7
		PRINT +スライム(9)
	CASE IS >= 1500
		実効値 += 15
		PRINT +溶ける快(15)
ENDSELECT

;調教者の能力、素質
IF ABL:PLAYER:サドっ気
	実効値 += ABL:PLAYER:サドっ気*2
	PRINTFORM +%CALLNAME:PLAYER%のサドっ気LV{ABL:PLAYER:サドっ気}({ABL:PLAYER:サドっ気*2})
ENDIF
IF TALENT:PLAYER:魅惑
	実効値 += 6
	PRINTFORM +%CALLNAME:PLAYER%の魅惑(6)
ENDIF
IF TALENT:PLAYER:謎の魅力
	実効値 += 8
	PRINTFORM +%CALLNAME:PLAYER%の謎の魅力(8)
ENDIF
IF TALENT:PLAYER:サド
	実効値 += 3
	PRINTFORM +%CALLNAME:PLAYER%がサド(3)
ENDIF
IF TALENT:PLAYER:強気
	実効値 += 5
	PRINTFORM +%CALLNAME:PLAYER%が強気(5)
ENDIF
IF TALENT:PLAYER:弱気
	実効値 -= 5
	PRINTFORM -%CALLNAME:PLAYER%が弱気(5)
ENDIF

;助手の素質
IF ASSI >= 0 && TALENT:ASSI:鼓舞
	実効値 += 2
	PRINTFORM +%CALLNAME:ASSI%が鼓舞(2)
ENDIF
IF ASSI >= 0 && TALENT:ASSI:ハイテンション
	実効値 += 2
	PRINTFORM +%CALLNAME:ASSI%がハイテンション(2)
ENDIF

;アニマルは依存度（なつき度）が足りないと猛反発
IF TALENT:アニマル
	SELECTCASE CFLAG:依存度
		CASE IS <= 0
			PRINT -なつき度皆無(100)
			実効値 -= 100
		CASE IS <= 100
			PRINT -懐いていない(70)
			実効値 -= 70
		CASE IS <= 300
			PRINT -警戒している(50)
			実効値 -= 50
		CASE IS <= 500
			PRINT -好かれていない(30)
			実効値 -= 30
		CASE IS <= 700
			PRINT -嫌いじゃない(10)
			実効値 -= 10
		CASE IS <= 1000
			PRINT -対等(3)
			実効値 -= 3
		CASE IS <= 1300
			PRINT +気になる相手(5)
			実効値 += 5
		CASE IS <= 1500
			PRINT +可愛がってくれる相手(10)
			実効値 += 10
		CASE IS <= 2000
			PRINT +飼い主(15)
			実効値 += 15
		CASE IS <= 3000
			PRINT +大好きな飼い主(30)
			実効値 += 30
		CASEELSE
			PRINT +ご主人様♥(50)
			実効値 += 50
	ENDSELECT
ENDIF

;酔
IF CFLAG:酔
	実効値 += CFLAG:酔/10+1
	PRINTFORM +酔({CFLAG:酔/10+1})
ENDIF

;相性チェック
SELECTCASE 相性値(TARGET, PLAYER)
	CASE 0 TO 30
		実効値 -= 10
		PRINT -相性最悪(10)
	CASE 30 TO 70
		実効値 -= 6
		PRINT -相性悪い(6)
	CASE 70 TO 99
		実効値 -= 3
		PRINT -相性いまいち(3)
	CASE 101 TO 130
		実効値 += 3
		PRINT +相性ややよい(3)
	CASE 130 TO 170
		実効値 += 6
		PRINT +相性よい(6)
	CASE IS >= 170
		実効値 += 10
		PRINT +相性最高(10)
ENDSELECT

IF ゲームモード() == "学園モード"
	IF SIGN(好感度合計(TARGET)) == 1
		実効値 += 好感度合計(TARGET)/100
		PRINTFORM +好感度({好感度合計(TARGET)/100})
	ENDIF
	IF CFLAG:倫理観
		実効値 += CFLAG:倫理観
		PRINTFORM +倫理観崩壊({CFLAG:倫理観})
	ENDIF
ENDIF

IF TFLAG:ヒートストローク && FLAG:気温 >= 200
	実効値 += (FLAG:気温-200)/10+1
	PRINTFORM +ヒートストローク({(FLAG:気温-200)/10+1})
ENDIF
IF TFLAG:ホワイトアウト && FLAG:気温 <= 200
	実効値 += ABS((FLAG:気温-200)/10+1)
	PRINTFORM +ホワイトアウト({ABS((FLAG:気温-200)/10+1)})
ENDIF
IF TFLAG:メタリックマインド
	実効値 -= 15
	PRINT -メタリックマインド(15)
ENDIF

IF 特質("意志薄弱")
	実効値 += 特質("意志薄弱")
	PRINTFORM +意志薄弱({特質("意志薄弱")})
ENDIF

RETURN 実効値

@COM_ORDER_欲情, タイプ
#DIM DYNAMIC 実効値, 3
#DIM DYNAMIC タイプ
;欲情Lv(最大で5)に対して、以下のタイプによって別々に変動する
;タイプ0...[衝動的]で×1.5, [冷静]で×0.5, 最後に×3
;タイプ1...[衝動的]で+2, [冷静]で×0.75

IF タイプ == 0
	実効値:1 = GETPALAMLV(PALAM:欲情, 5)
	実効値:2 = 実効値:1
	SIF TALENT:衝動的
		TIMES 実効値:2 , 1.50
	SIF TALENT:冷静
		TIMES 実効値:2 , 0.50
	IF 実効値:2
		実効値 += 実効値:2*3
		PRINTFORM +欲情LV{実効値:1}({実効値:2*3})
	ENDIF
	RETURN 実効値:2*3
ELSEIF タイプ == 1
	実効値:1 = GETPALAMLV(PALAM:欲情, 5)
	実効値:2 = 実効値:1
	SIF TALENT:衝動的
		実効値:2 += 2
	SIF TALENT:冷静
		TIMES 実効値:2 , 0.75
	IF 実効値:2
		実効値 += 実効値:2
		PRINTFORM +欲情LV{実効値:1}({実効値:2})
	ENDIF
	RETURN 実効値:2
ENDIF

;※以下の一連の汎用COM_ORDERを通していないコマンド※
;素股。乳ファック、助手を犯させる、兜合わせ、貝合せ、ダブル素股
@COM_ORDER_自慰
#DIM DYNAMIC 実効値

IF ABL:欲望
	実効値 += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
IF ABL:露出癖
	実効値 += ABL:露出癖*4
	PRINTFORM +露出癖LV{ABL:露出癖}({ABL:露出癖*4})
ENDIF
IF ABL:自慰中毒
	実効値 += ABL:自慰中毒*3
	PRINTFORM +自慰中毒LV{ABL:自慰中毒}({ABL:自慰中毒*3})
ENDIF
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

;自制心
IF TALENT:自制心
	実効値 -= 5
	PRINTFORM -自制心(5)
ENDIF
;恥じらい
IF TALENT:恥じらい
	実効値 -= 5
	PRINTFORM -恥じらい(5)
ENDIF
;恥薄い
IF TALENT:恥薄い
	実効値 += 2
	PRINTFORM +恥薄い(2)
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	実効値 += 5
	PRINTFORM +自慰しやすい(5)
ENDIF
;快感に素直
IF TALENT:快感に素直
	実効値 += 5
	PRINTFORM +快感に素直(5)
ENDIF
;快感の否定 淫乱系で無効
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	実効値 -= 5
	PRINTFORM -快感の否定(5)
ENDIF
;媚薬
IF TEQUIP:媚薬
	実効値 += 8
	PRINTFORM +媚薬(8)
ENDIF

RETURN 実効値

@COM_ORDER_キス
#DIM DYNAMIC 実効値

IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF

IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

;汚臭鈍感
IF TALENT:汚臭鈍感
	実効値 += 1
	PRINTFORM +汚臭鈍感(1)
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	実効値 -= 1
	PRINTFORM -汚臭敏感(1)
ENDIF
;献身的
IF TALENT:献身的
	実効値 += 6
	PRINTFORM +献身的(6)
ENDIF
;快感の否定 淫乱系で無効
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	実効値 -= 1
	PRINTFORM -快感の否定(1)
ENDIF
;恋慕
IF TALENT:恋慕 && !ASSIPLAY
	実効値 += 5
	PRINTFORM +恋慕(5)
ENDIF
;親愛
IF TALENT:親愛 && !ASSIPLAY
	実効値 += 10
	PRINTFORM +親愛(10)
ENDIF

RETURN 実効値

@COM_ORDER_手淫
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	実効値 += ABL:精液中毒*2
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*2})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				実効値:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				実効値:1 = 5
		CASE "淫魔", "隷属"
			実効値:1 = 10
		CASE "淫乱", "服従"
			実効値:1 = 5
		CASE "恥じらい"
			実効値:1 = -2
		CASE "献身的"
			実効値:1 = 6
		CASE "汚臭敏感"
			実効値:1 = -5
		CASE "汚臭鈍感"
			実効値:1 = 3
		CASE "サキュバス"
			実効値:1 = 10
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

実効値:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	実効値:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	実効値:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	実効値:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 4)
	実効値:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	実効値:1 += 5

SIF TALENT:汚れ無視
	実効値:1 /= 3
SIF TALENT:潔癖症
	実効値:1 *= 2

;汚れあり
IF 実効値:1
	実効値 -= 実効値:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({実効値:1})
ENDIF

RETURN 実効値

@COM_ORDER_フェラ, 奉仕対象 = -1
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2
#DIM DYNAMIC 奉仕対象

SIF 奉仕対象 == -1
	奉仕対象 = PLAYER
;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	実効値 += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "親愛"
			SIF 奉仕対象 == MASTER
				実効値:1 = 10
		CASE "恋慕"
			SIF 奉仕対象 == MASTER
				実効値:1 = 5
		CASE "淫魔", "隷属"
			実効値:1 = 10
		CASE "淫乱", "服従"
			実効値:1 = 5
		CASE "恥じらい"
			実効値:1 = -3
		CASE "献身的"
			実効値:1 = 6
		CASE "汚臭敏感"
			実効値:1 = -7
		CASE "汚臭鈍感"
			実効値:1 = 2
		CASE "サキュバス"
			実効値:1 = 10
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

実効値:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:奉仕対象:ペニス, 0)
	実効値:1 += 2
;精液の汚れ
SIF GETBIT(STAIN:奉仕対象:ペニス, 2)
	実効値:1 += 5
;アナルの汚れ
SIF GETBIT(STAIN:奉仕対象:ペニス, 3)
	実効値:1 += 7
;母乳汚れ
SIF GETBIT(STAIN:奉仕対象:ペニス, 4)
	実効値:1 += 1
;尿
SIF GETBIT(STAIN:奉仕対象:ペニス, 7)
	実効値:1 += 7

SIF TALENT:汚れ無視
	実効値:1 /= 3
SIF TALENT:潔癖症
	実効値:1 *= 2

;汚れあり
IF 実効値:1
	実効値 -= 実効値:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({実効値:1})
ENDIF

RETURN 実効値

@COM_ORDER_奉仕
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "淫魔"
			実効値:1 = 10
		CASE "淫乱"
			実効値:1 = 5
		CASE "恥じらい"
			実効値:1 = -2
		CASE "献身的"
			実効値:1 = 6
		CASE "汚臭敏感"
			実効値:1 = -5
		CASE "汚臭鈍感"
			実効値:1 = 3
		CASE "サキュバス"
			実効値:1 = 10
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

RETURN 実効値

@COM_ORDER_道具奉仕
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				実効値:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				実効値:1 = 5
		CASE "献身的"
			実効値:1 = 3
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

RETURN 実効値

@COM_ORDER_セックス
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	実効値 += ABL:Ｖ感覚*2
	PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
ENDIF
;ABL:性交中毒
IF ABL:性交中毒
	実効値 += ABL:性交中毒
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	実効値 -= 5
	PRINT -潤滑不足(5)
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "親愛"
			SELECTCASE SELECTCOMS
				CASE "膣を犯す（ドラゴン）", "アナルを犯す（ドラゴン）", "ペットに犯させる"
				CASEELSE
					SIF PLAYER == MASTER
						実効値:1 = 20
			ENDSELECT
		CASE "恋慕"
			SELECTCASE SELECTCOMS
				CASE "膣を犯す（ドラゴン）", "アナルを犯す（ドラゴン）", "ペットに犯させる"
				CASEELSE
					SIF PLAYER == MASTER
						実効値:1 = 15
			ENDSELECT
		CASE "恥じらい"
			実効値:1 = -2
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				実効値:1 = -5
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -12
		CASE "処女"
			実効値:1 = -40
		CASE "サキュバス"
			実効値:1 = 20
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;V経験
IF !TALENT:処女 && EXP:Ｖ経験 < 30
	実効値 -= 15
	PRINT -Ｖ経験不足(15)
ENDIF

;出産トラウマ サキュバスで無効
IF CFLAG:出産トラウマ && !TALENT:サキュバス
	実効値 -= CFLAG:出産トラウマ
	PRINTFORM -妊娠出産トラウマ({CFLAG:出産トラウマ})
ENDIF

;媚薬
IF TEQUIP:媚薬
	実効値 += 6
	PRINT +媚薬(6)
ENDIF

;PLAYERが搾精コンドーム付ける時無いと思う
IF PENIS(PLAYER) && !TEQUIP:PLAYER:コンドーム && !TEQUIP:PLAYER:搾精コンドーム
	IF ゲームモード() == "学園モード"
		実効値 -= 50
		PRINT -ゴム無し(50)
	ELSEIF !陥落(TARGET)
		実効値 -= 20
		PRINT -ゴム無し(20)
	ENDIF
ENDIF

IF SELECTCOMS != "膣を犯す（ドラゴン）"
	実効値:1 = 0
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		実効値:1 += 5
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		実効値:1 += 7
	;母乳
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		実効値:1 += 2
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		実効値:1 += 4

	SIF !TALENT:汚れ無視
		実効値:1 /= 3
	SIF TALENT:潔癖症
		実効値:1 *= 2

	;汚れあり
	IF 実効値:1
		実効値 -= 実効値:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({実効値:1})
	ENDIF
ENDIF

RETURN 実効値

@COM_ORDER_アナルセックス
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
;ABL:Ａ感覚
IF ABL:Ａ感覚
	実効値 += ABL:Ａ感覚*2
	PRINTFORM +Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚*2})
ENDIF
;ABL:性交中毒
IF ABL:性交中毒
	実効値 += ABL:性交中毒
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	実効値 -= 10
	PRINT -潤滑不足(10)
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "淫魔"
			SIF PLAYER == MASTER
				実効値:1 = 20
		CASE "淫乱"
			SIF PLAYER == MASTER
				実効値:1 = 15
		CASE "恥じらい"
			実効値:1 = -7
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				実効値:1 = -5
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -12
		CASE "サキュバス"
			実効値:1 = 20
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;A経験
IF EXP:Ａ経験 < 100
	実効値 -= 30
	PRINT -Ａ経験不足(30)
ENDIF

;媚薬
IF TEQUIP:媚薬
	実効値 += 6
	PRINT +媚薬(6)
ENDIF

IF PENIS(PLAYER) && !TEQUIP:PLAYER:コンドーム && !TEQUIP:PLAYER:搾精コンドーム
	IF ゲームモード() == "学園モード"
		実効値 -= 50
		PRINT -ゴム無し(50)
	ELSEIF !陥落(TARGET)
		実効値 -= 20
		PRINT -ゴム無し(20)
	ENDIF
ENDIF

IF SELECTCOMS != "アナルを犯す（ドラゴン）"
	実効値:1 = 0
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 0)
		実効値:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		実効値:1 += 3
	;母乳
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		実効値:1 += 2
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		実効値:1 += 7

	SIF TALENT:汚れ無視
		実効値:1 /= 3
	SIF TALENT:潔癖症
		実効値:1 *= 2

	;汚れあり
	IF 実効値:1
		実効値 -= 実効値:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({実効値:1})
	ENDIF
ENDIF

RETURN 実効値

@COM_ORDER_セックスさせる
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
;ABL:性交中毒
IF ABL:性交中毒
	実効値 += ABL:性交中毒*2
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒*2})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "恥じらい"
			SIF SELECTCOMS == "逆レイプ"
				CONTINUE
			実効値:1 = -7
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				実効値:1 = -10
		CASE "貞操観念"
			SIF SELECTCOMS == "逆レイプ"
				CONTINUE
			SIF FALLTYPE(TARGET) != 1 || PLAYER != MASTER
				実効値:1 = -10
		CASE "貞操無頓着"
			SIF SELECTCOMS == "逆レイプ"
				CONTINUE
			実効値:1 = 3
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -12
		CASE "女嫌い"
			SIF SEX(PLAYER) == 2 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -12
		CASE "インキュバス"
			実効値:1 = 20
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;媚薬
IF TEQUIP:媚薬
	実効値 += 6
	PRINT +媚薬(6)
ENDIF

IF PENIS(TARGET) && !TEQUIP:コンドーム && !TEQUIP:搾精コンドーム
	IF ゲームモード() == "学園モード"
		実効値 -= 50
		PRINT -ゴム無し(50)
	ELSEIF !陥落(TARGET)
		実効値 -= 20
		PRINT -ゴム無し(20)
	ENDIF
ENDIF

IF SELECTCOMS != "逆レイプ"
	実効値:1 = 0
	SELECTCASE SELECTCOMS
		CASE "正常位させる", "後背位させる"
			;アナルの汚れ
			SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
				実効値:1 += 6
			;尿
			SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
				実効値:1 += 3
		CASE "正常位アナルさせる", "後背位アナルさせる"
			;尿
			SIF GETBIT(STAIN:PLAYER:アナル, 7)
				実効値:1 += 3
	ENDSELECT

	SIF TALENT:汚れ無視
		実効値:1 /= 3
	SIF TALENT:潔癖症
		実効値:1 *= 2

	;汚れあり
	IF 実効値:1
		実効値 -= 実効値:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({実効値:1})
	ENDIF
ENDIF

RETURN 実効値

@COM_ORDER_フィストさせる
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF

CALL COM_ORDER_欲情, 0
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "女嫌い"
			SIF SEX(PLAYER) == 2 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -12
		CASE "潔癖症"
			実効値:1 = 6
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

RETURN 実効値

@COM_ORDER_シックスナイン
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*2
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				実効値:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				実効値:1 = 5
		CASE "恥じらい"
			実効値:1 = -5
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				実効値:1 = -3
		CASE "献身的"
			実効値:1 = 6
		CASE "汚臭敏感"
			実効値:1 = -5
		CASE "汚臭鈍感"
			実効値:1 = 3
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;媚薬
IF TEQUIP:媚薬
	実効値 += 8
	PRINTFORM +媚薬(8)
ENDIF

実効値:1 = 0
IF PENIS(PLAYER)
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 0)
		実効値:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		実効値:1 += 3
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		実効値:1 += 5
	;母乳汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		実効値:1 += 1
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		実効値:1 += 3

	SIF TALENT:汚れ無視
		実効値:1 /= 3
	SIF TALENT:潔癖症
		実効値:1 *= 2

	;汚れあり
	IF 実効値:1
		実効値 -= 実効値:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({実効値:1})
	ENDIF
ELSE
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ヴァギナ, 0)
		実効値:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
		実効値:1 += 3
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
		実効値:1 += 5
	;母乳汚れ
	SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
		実効値:1 += 1
	;尿
	SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
		実効値:1 += 3

	SIF TALENT:汚れ無視
		実効値:1 /= 3
	SIF TALENT:潔癖症
		実効値:1 *= 2

	;汚れあり
	IF 実効値:1
		実効値 -= 実効値:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({実効値:1})
	ENDIF
ENDIF

RETURN 実効値

@実効値判定, 実効値, 基準値
#DIM DYNAMIC 実効値
#DIM DYNAMIC 基準値

;合計を表示
PRINTFORM  = {実効値}

SELECTCASE 実効値
	CASE IS < 基準値
		PRINT  < 
	CASE IS > 基準値
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{基準値}

WAIT

;実行できない
IF 実効値 < 基準値
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF
