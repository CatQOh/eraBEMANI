;-------------------------------------------------
;対面座位
;-------------------------------------------------
@USERCOM_ABLE_対面座位
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;縄使用中はダメ
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0

;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_性交()
	COMRESULT = 0
SIF !USERCOM_ABLE_性交助手()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_対面座位
#DIM DYNAMIC 実効値

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
SIF TEQUIP:縄
	RETURN 1

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_セックス
実効値 += RESULT

;40以上で実行
CALL 実効値判定, 実効値, 40

RETURN RESULT

@COM_対面座位
#DIM DYNAMIC 増加量

PRINTL 対面座位

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		CSTR:PLAYER:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:PLAYER:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入実行者(800)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入(800)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 120

DOWNBASE:MASTER:体力 += 80
DOWNBASE:MASTER:気力 += 50

SOURCE:快V = 400
SOURCE:快C = 200
SOURCE:情愛 = 500
SOURCE:苦痛 = 200
SOURCE:中毒 = 500
SOURCE:恭順 = 500
SOURCE:逸脱 = 300
SOURCE:反感 = 500

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "精液", "アナル", "尿")
	SOURCE:不潔 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 4, 2

CALL EXPUP, "Ｖ経験", 1
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;愛情経験
SIF TALENT:恋慕 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF TALENT:親愛 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 4

;魔力回復
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3

経験回数:PLAYER:(NOF(TARGET)) += 1
経験回数:TARGET:(NOF(PLAYER)) += 1

RETURN 1
