;-------------------------------------------------
;下着ずらし挿入アナル
;-------------------------------------------------
@USERCOM_ABLE_下着ずらし挿入アナル
#FUNCTION
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

;下半身下着をつけていないとダメ(下着一体型全身上着は例外)
コマンド実行条件 += "対象の下半身がずらせる服を着ていないと実行できない/"
SIF !TEQUIP:下半身下着 && CFLAG:着衣併用 != 2
	COMRESULT = 0
;下着一体型全身上着は着ていないとダメ
SIF !TEQUIP:全身上着 && CFLAG:着衣併用 == 2
	COMRESULT = 0
;下の空いてない上着を着ているとダメ
IF (TEQUIP:下半身上着 || TEQUIP:全身上着) && CFLAG:着衣併用 != 2
	SIF !CFLAG:挿入可
		COMRESULT = 0
ENDIF

;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_Ａ性交()
	COMRESULT = 0
SIF !USERCOM_ABLE_Ａ性交助手()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_下着ずらし挿入アナル
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理（正常位アナルのものを使用）
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
SIF TEQUIP:縄
	RETURN 1

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_アナルセックス
実効値 += RESULT

IF TALENT:羽
	実効値 -= 5
	PRINT -羽(5)
ENDIF

;30以上で実行
CALL 実効値判定, 実効値, 30

RETURN RESULT

@COM_下着ずらし挿入アナル
#DIM DYNAMIC 増加量

PRINTL 下着ずらし挿入アナル

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		CSTR:PLAYER:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:PLAYER:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ挿入実行者(600)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ挿入(300)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 150

SOURCE:快A = 400
SOURCE:情愛 = 300
SOURCE:苦痛 = 500
SOURCE:中毒 = 200
SOURCE:露出 = 200
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:恭順 = 150

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "精液", "尿")
	SOURCE:不潔 = 150

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者のペニスにアナル汚れ
SETBIT STAIN:PLAYER:ペニス, 3
;奴隷のアナル⇔調教者のペニスの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "Ａ経験", 2
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;魔力回復
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3

経験回数:PLAYER:(NOF(TARGET)) += 1
経験回数:TARGET:(NOF(PLAYER)) += 1

RETURN 1
