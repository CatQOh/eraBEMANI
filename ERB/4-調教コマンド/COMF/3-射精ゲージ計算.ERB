@射精ゲージ計算_クンニ射精, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += 上昇値*ABL:PLAYER:技巧/5

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｖ感覚-3)
ENDSELECT

;奴隷のABL:マゾっ気をみる
上昇値 += 上昇値*ABL:マゾっ気/5

RETURNF 上昇値

@射精ゲージ計算_フェラチオ, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += 上昇値*ABL:PLAYER:技巧/2

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｃ感覚-3)
ENDSELECT

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES 上昇値 , 1.50

;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES 上昇値 , 0.75

RETURNF 上昇値

@射精ゲージ計算_パイズリする, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += ((上昇値/10)*8)*ABL:PLAYER:技巧

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASE IS >= 5
		上昇値 += 上昇値*(ABL:Ｃ感覚-3)
ENDSELECT

;TALENT:バストサイズをみる
SELECTCASE TALENT:PLAYER:バストサイズ
	CASE 1
		TIMES 上昇値 , 0.70
	CASE 2
		TIMES 上昇値 , 0.85
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 1.20
	CASE 5
		TIMES 上昇値 , 1.40
	CASE IS >= 6
		上昇値 += 上昇値*(TALENT:PLAYER:バストサイズ)/10
ENDSELECT
;対象が小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:体格 <= 2 || TALENT:小人体型
	TIMES 上昇値 , 1.15

;無機物の場合、ある程度減らす
IF TALENT:PLAYER:無機物
	TIMES 上昇値 , 0.60
ELSEIF TALENT:PLAYER:無機物
	TIMES 上昇値 , 0.80
ENDIF

;実行者が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:PLAYER:体格 <= 2 || TALENT:PLAYER:小人体型
	TIMES 上昇値 , 1.25
;肋骨抜かれてると柔らかい
SIF TALENT:PLAYER:肋骨除去済み
	TIMES 上昇値 , 1.50

RETURNF 上昇値

@射精ゲージ計算_キス実行者, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:技巧をみる
上昇値 += 上昇値*ABL:技巧

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.20
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 1.80
	CASEELSE
		上昇値 += (上昇値/3)*ABL:奉仕精神
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 上昇値 , 2.00

;相性を見る
上昇値 *= 相性値(PLAYER, TARGET)
上昇値 /= 100

RETURNF 上昇値

@射精ゲージ計算_キス, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;依存度を見る
;SIF PLAYER == MASTER
;	上昇値 += CFLAG:依存度/10

;ABL:調教者の技巧をみる
上昇値 += (上昇値*ABL:PLAYER:技巧)/10

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/5)*ABL:欲望

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.20
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 1.80
	CASEELSE
		上昇値 += (上昇値/3)*ABL:奉仕精神
ENDSELECT

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES 上昇値 , 2.00

;相性を見る
上昇値 *= 相性値(TARGET, PLAYER)
上昇値 /= 100

RETURNF 上昇値

@射精ゲージ計算_オナホール, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += 上昇値*ABL:PLAYER:技巧/2

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.10
	CASE 2
		TIMES 上昇値 , 1.30
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += (上昇値/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 0.20
	CASE 1
		TIMES 上昇値 , 0.50
	CASE 2
		TIMES 上昇値 , 0.80
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｃ感覚-2)
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_Ｐ責め, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += ((上昇値/10)*8)*ABL:PLAYER:技巧

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｃ感覚-3)
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_Ｐ道具, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += (上昇値/5)*ABL:PLAYER:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.10
	CASE 2
		TIMES 上昇値 , 1.30
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += (上昇値/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

RETURNF 上昇値

@射精ゲージ計算_Ｖ責め, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += 上昇値*ABL:PLAYER:技巧/5

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;PALAM:潤滑をみる
上昇値 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(上昇値/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.50
	CASE 2
		TIMES 上昇値 , 2.50
	CASE 3
		TIMES 上昇値 , 4.00
	CASE 4
		TIMES 上昇値 , 6.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｖ感覚+2)
ENDSELECT

;奴隷のABL:マゾっ気をみる
上昇値 += 上昇値*ABL:マゾっ気/5

RETURNF 上昇値

;Ｖ道具
@射精ゲージ計算_Ｖ道具, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += (上昇値/5)*ABL:PLAYER:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.10
	CASE 2
		TIMES 上昇値 , 1.30
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += (上昇値/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 上昇値 , 0.20
	CASE 1
		TIMES 上昇値 , 0.50
	CASE 2
		TIMES 上昇値 , 0.80
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｖ感覚-2)
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_Ｖ挿入実行者, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;PALAM:潤滑をみる
上昇値 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(上昇値/5)

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.60
	CASE 2
		TIMES 上昇値 , 2.30
	CASE 3
		TIMES 上昇値 , 3.10
	CASE 4
		TIMES 上昇値 , 4.00
	CASEELSE
		上昇値 += 上昇値*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;処女だと増える
SIF TALENT:処女
	TIMES 上昇値 , 1.50

;子供は締まりが良いので増える
SELECTCASE TALENT:体格
	CASE 1
		TIMES 上昇値 , 1.30
	CASE 2
		TIMES 上昇値 , 1.10
ENDSELECT

;Ｖ感覚でも微増させる
上昇値 += 上昇値*ABL:Ｖ感覚/100

;サキュバスだと増える
SIF TALENT:サキュバス
	TIMES 上昇値 , 2.50

RETURNF 上昇値

@射精ゲージ計算_Ｖ挿入, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順

;ABL:欲望をみる
上昇値 += (上昇値/5)*ABL:欲望

;PALAM:潤滑をみる
上昇値 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(上昇値/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += (上昇値/2)*ABL:Ｖ感覚
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_Ｖ挿入強, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += (上昇値/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
上昇値 += (上昇値/10)*ABL:技巧

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順

;ABL:欲望をみる
上昇値 += (上昇値/5)*ABL:欲望

;PALAM:潤滑をみる
上昇値 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(上昇値/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 2.00
	CASE 2
		TIMES 上昇値 , 4.00
	CASE 3
		TIMES 上昇値 , 7.00
	CASE 4
		TIMES 上昇値 , 10.00
	CASEELSE
		上昇値 *= ABL:Ｖ感覚*3
ENDSELECT

;奴隷のABL:マゾっ気をみる
上昇値 += (上昇値/5)*ABL:マゾっ気

RETURNF 上昇値

;Ａ責めするときの射精ゲージ
@射精ゲージ計算_Ａ責め, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:調教者の技巧をみる
上昇値 += 上昇値*ABL:PLAYER:技巧/5

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES 上昇値 , 0.40
	CASE 1
		TIMES 上昇値 , 0.60
	CASE 2
		TIMES 上昇値 , 0.60
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 1.20
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 上昇値 , 0.20
	CASE 1
		TIMES 上昇値 , 0.50
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 2.00
	CASE 4
		TIMES 上昇値 , 3.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ａ感覚-1)
ENDSELECT

;奴隷のABL:マゾっ気をみる
上昇値 += 上昇値*ABL:マゾっ気/5

RETURNF 上昇値

@射精ゲージ計算_Ａ挿入実行者, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;PALAM:潤滑をみる
上昇値 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(上昇値/5)

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.60
	CASE 2
		TIMES 上昇値 , 2.30
	CASE 3
		TIMES 上昇値 , 3.10
	CASE 4
		TIMES 上昇値 , 4.00
	CASEELSE
		上昇値 += 上昇値*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;子供は締まりが良いので増える
SELECTCASE TALENT:体格
	CASE 1
		TIMES 上昇値 , 1.30
	CASE 2
		TIMES 上昇値 , 1.10
ENDSELECT

;サキュバスだと増える
SIF TALENT:サキュバス
	TIMES 上昇値 , 1.50

RETURNF 上昇値

@射精ゲージ計算_Ａ挿入, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:欲望をみる
上昇値 += (上昇値/5)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES 上昇値 , 0.05
	CASE 1
		TIMES 上昇値 , 0.20
	CASE 2
		TIMES 上昇値 , 0.50
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 1.50
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += (上昇値/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
上昇値 += (上昇値/5)*ABL:マゾっ気

;奴隷が男の場合はホモっ気を見る
IF SEX(TARGET) == 1
	SELECTCASE ABL:ホモっ気
		CASE 0
			TIMES 上昇値 , 0.15
		CASE 1
			TIMES 上昇値 , 0.30
		CASE 2
			TIMES 上昇値 , 0.50
		CASE 3
			TIMES 上昇値 , 0.75
		CASE 4
			TIMES 上昇値 , 1.00
		CASEELSE
			上昇値 += (上昇値/10)*ABL:ホモっ気-3
	ENDSELECT
;女なら無条件で半減
ELSE
	TIMES 上昇値 , 0.50
ENDIF

RETURNF 上昇値

@射精ゲージ計算_実行者受け, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:奉仕精神をみる
上昇値 += (上昇値/10)*ABL:奉仕精神

;プレイヤーのABL:Ｖ感覚、Ａ感覚をみる
SELECTCASE TFLAG:奴隷挿入箇所
	CASE 1
		SELECTCASE ABL:PLAYER:Ｖ感覚
			CASE 0
				TIMES 上昇値 , 1.00
			CASE 1
				TIMES 上昇値 , 1.50
			CASE 2
				TIMES 上昇値 , 2.00
			CASE 3
				TIMES 上昇値 , 2.50
			CASE 4
				TIMES 上昇値 , 3.50
			CASEELSE
				上昇値 += 上昇値*(ABL:PLAYER:Ｖ感覚-1)
		ENDSELECT
	CASE 2
		SELECTCASE ABL:PLAYER:Ａ感覚
			CASE 0
				TIMES 上昇値 , 1.00
			CASE 1
				TIMES 上昇値 , 1.50
			CASE 2
				TIMES 上昇値 , 2.00
			CASE 3
				TIMES 上昇値 , 2.50
			CASE 4
				TIMES 上昇値 , 3.50
			CASEELSE
				上昇値 += 上昇値*(ABL:PLAYER:Ａ感覚-1)
		ENDSELECT
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_奴隷挿入, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.40
	CASE 2
		TIMES 上昇値 , 1.80
	CASE 3
		TIMES 上昇値 , 2.20
	CASE 4
		TIMES 上昇値 , 2.60
	CASEELSE
		上昇値 += (上昇値/2)*ABL:Ｃ感覚
ENDSELECT

;子供は締まりが良いので増える
IF SELECTCOMS == "助手を犯させる"
	SIF TALENT:ASSI:体格 <= 2
		TIMES 上昇値 , 1.20
ELSE
	SIF TALENT:PLAYER:体格 <= 2
		TIMES 上昇値 , 1.20
ENDIF

RETURNF 上昇値

@射精ゲージ計算_奉仕, 上昇値, 対象 = -1
#FUNCTION
#DIM DYNAMIC 上昇値
#DIM DYNAMIC 対象
SIF 対象 == -1
	対象 = PLAYER

;ABL:技巧をみる
上昇値 += (上昇値/5)*ABL:技巧

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.20
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 1.80
	CASEELSE
		上昇値 += 上昇値/3*ABL:奉仕精神
ENDSELECT

;奉仕対象のABL:Ｃ感覚をみる
SELECTCASE ABL:対象:Ｃ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.50
	CASE 2
		TIMES 上昇値 , 2.00
	CASE 3
		TIMES 上昇値 , 2.50
	CASE 4
		TIMES 上昇値 , 3.50
	CASEELSE
		上昇値 += 上昇値*(ABL:対象:Ｃ感覚-1)
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_Ａ奉仕, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値

;ABL:技巧をみる
上昇値 += (上昇値/5)*ABL:技巧

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.20
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 1.80
	CASEELSE
		上昇値 += 上昇値/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ａ感覚をみる
SELECTCASE ABL:PLAYER:Ａ感覚
	CASE 0
		TIMES 上昇値 , 1.00
	CASE 1
		TIMES 上昇値 , 1.50
	CASE 2
		TIMES 上昇値 , 2.00
	CASE 3
		TIMES 上昇値 , 2.50
	CASE 4
		TIMES 上昇値 , 3.50
	CASEELSE
		上昇値 += 上昇値*(ABL:PLAYER:Ａ感覚-1)
ENDSELECT

RETURNF 上昇値

@射精ゲージ計算_自慰, 上昇値
#FUNCTION
#DIM DYNAMIC 上昇値
#DIM DYNAMIC LABL

;ABL:技巧をみる
上昇値 += 上昇値*ABL:技巧/5

;ABL:従順をみる
上昇値 += (上昇値/10)*ABL:従順 - (上昇値/10)*2

;ABL:欲望をみる
上昇値 += (上昇値/10)*ABL:欲望

IF TEQUIP:電動エネマグラ || TALENT:去勢済み
	LABL = ABL:Ａ感覚
ELSE
	LABL = ABL:Ｃ感覚
ENDIF
;奴隷のABL:ＣorＡ感覚をみる
SELECTCASE LABL
	CASE 0
		TIMES 上昇値 , 0.50
	CASE 1
		TIMES 上昇値 , 0.80
	CASE 2
		TIMES 上昇値 , 1.00
	CASE 3
		TIMES 上昇値 , 1.50
	CASE 4
		TIMES 上昇値 , 2.00
	CASEELSE
		上昇値 += 上昇値*(ABL:Ｃ感覚-3)
ENDSELECT

;奴隷のABL:露出癖をみる
SELECTCASE ABL:露出癖
	CASE 0
		TIMES 上昇値 , 0.20
	CASE 1
		TIMES 上昇値 , 0.40
	CASE 2
		TIMES 上昇値 , 0.80
	CASE 3
		TIMES 上昇値 , 1.00
	CASE 4
		TIMES 上昇値 , 1.10
	CASEELSE
		上昇値 += (上昇値/10)*ABL:露出癖-2
ENDSELECT

;奴隷のABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES 上昇値 , 0.80
	CASE 1
		TIMES 上昇値 , 1.00
	CASE 2
		TIMES 上昇値 , 1.20
	CASE 3
		TIMES 上昇値 , 1.40
	CASE 4
		TIMES 上昇値 , 1.60
	CASEELSE
		上昇値 += (上昇値/5)*ABL:自慰中毒
ENDSELECT

RETURNF 上昇値

@射精ゲージ増加, 上昇値, 対象 = -1
#DIM DYNAMIC 上昇値
#DIM DYNAMIC 対象
SIF 対象 == -1
	対象 = TARGET

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:(対象):賢者タイム > 0
	上昇値 /= 20

;メス度が1上がるにつれ8%減る
上昇値 -= (上昇値*MIN(ABL:(対象):メス度, 10)*8)/100

;去勢済み、屍姦中は極端に射精しづらい（一応射精はする）
SIF TALENT:(対象):去勢済み || (対象 == TARGET && CFLAG:死亡)
	上昇値 /= 20

;媚薬効果
SIF CFLAG:(対象):媚薬
	上昇値 += 上昇値*CFLAG:媚薬/100

;リビドー絶頂カウントに応じて増加 1回につき10%
SIF 対象 == TARGET
	上昇値 += 上昇値*TFLAG:リビドー絶頂カウント/10

;子供好きで対象が子供だとイきやすい
IF 対象 == TARGET
	SIF TALENT:子供好き && TALENT:PLAYER:体格 <= 2
		TIMES 上昇値 , 1.20
ELSEIF 対象 == TMASTER
	SIF TALENT:TMASTER:子供好き && TALENT:体格 <= 2
		TIMES 上昇値 , 1.20
ELSEIF 対象 == ASSI
	SIF TALENT:ASSI:子供好き && TALENT:体格 <= 2
		TIMES 上昇値 , 1.20
ENDIF

SIF PENIS(対象) || TALENT:(対象):去勢済み
	BASE:(対象):射精 += 上昇値
