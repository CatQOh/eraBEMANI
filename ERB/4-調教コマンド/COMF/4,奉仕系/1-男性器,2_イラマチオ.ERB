;-------------------------------------------------
;イラマチオ
;-------------------------------------------------
@USERCOM_ABLE_イラマチオ, ARG
#FUNCTION
;上級売春中は方針がSMで対象がマゾじゃないとできない
SIF 上級売春中 && BR:方針 != 3 && !TALENT:マゾ
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;従順、欲望の合計が6以上ないとダメ
コマンド実行条件 += "対象の従順と欲望の合計LV6以上必要/"
SIF ABL:従順+ABL:欲望 < 6
	COMRESULT = 0
;猫舌、小柄体型の奴隷の場合は技巧Lv2以上必要
コマンド実行条件 += "対象が[猫舌]or小柄以下の場合は技巧LV2以上必要/"
SIF (TALENT:猫舌 || TALENT:体格 <= 2) && ABL:技巧 < 2
	COMRESULT = 0
;小人体型持ちは、調教者と調教対象が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合は、実行者に[小人体型]か、主人に[禁断の知識]が無いと実行できない/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_イラマチオ
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フェラ
実効値 += RESULT

;36以上で実行
CALL 実効値判定, 実効値, 36

RETURN RESULT

@COM_イラマチオ
#DIM DYNAMIC 増加量

PRINTL イラマチオ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF !CFLAG:PLAYER:挿入可
	TEQUIP:PLAYER:下半身上着 = 0
	TEQUIP:PLAYER:全身上着 = 0
ENDIF
TEQUIP:PLAYER:下半身下着 = 0

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(600)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 40

SOURCE:性行動 = 800
SOURCE:苦痛 = 700
SOURCE:恐怖 = 500
SOURCE:屈従 = 1000
SOURCE:逸脱 = 700
SOURCE:反感 = 700
SOURCE:不潔 = 100

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 4

CALL SAMEN_SHOOT, 4, NOWEX:PLAYER:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る（ボールギャグ使用時は除く）
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "フェラ経験", 1
CALL EXPUP, "嗜虐快楽経験", 1, PLAYER

IF !避妊具(TARGET)
	CALL 魔力回復, 7
	CALL 獣人催淫, 1
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1
