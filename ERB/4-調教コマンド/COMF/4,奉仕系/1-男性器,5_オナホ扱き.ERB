;-------------------------------------------------
;オナホ扱き
;-------------------------------------------------
@USERCOM_ABLE_オナホ扱き, ARG
#FUNCTION
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[オナホール]が必要/"
SIF ITEM:オナホール <= 0
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_オナホ扱き
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_手淫
実効値 += RESULT

;22以上で実行
CALL 実効値判定, 実効値, 22

RETURN RESULT

@COM_オナホ扱き
#DIM DYNAMIC 増加量

PRINTL オナホ扱き

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:下半身下着
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 400
;電動オナホだと快感アップ
SIF アイテム改造:オナホール == "電動オナホール"
	増加量 += 200

;ABL:技巧をみる
増加量 += (増加量/10*4)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 増加量 , 0.80
	CASE 1
		TIMES 増加量 , 1.00
	CASE 2
		TIMES 増加量 , 1.30
	CASE 3
		TIMES 増加量 , 1.70
	CASE 4
		TIMES 増加量 , 2.20
	CASEELSE
		TIMES 増加量 , 2.70
		増加量 += 増加量/2*(ABL:奉仕精神-5)
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 25
LOSEBASE:気力 += 50

SOURCE:性行動 = 500
SOURCE:達成 = 180
SOURCE:屈従 = 550
SOURCE:逸脱 = 450
SOURCE:反感 = 450
SOURCE:不潔 = 50

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF TALENT:衝動的 && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:オナホール = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;嗜虐快楽経験
SIF ABL:サドっ気 >= 5
	CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/5

;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
