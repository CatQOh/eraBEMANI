;-------------------------------------------------
;縦パイズリ
;-------------------------------------------------
@USERCOM_ABLE_縦パイズリ, ARG
#FUNCTION
;巨乳か豊乳か爆乳じゃないと問答無用で不可
コマンド実行条件 += "対象が巨乳、豊乳、爆乳じゃないと実行できない/"
SELECTCASE TALENT:バストサイズ
	CASE 0 TO 3
		COMRESULT = 0
ENDSELECT
;技巧4以上必要
コマンド実行条件 += "対象の技巧LV4以上が必要/"
SIF ABL:技巧 < 4
	COMRESULT = 0
;服を着ている場合正面から挿入できる構造じゃないとダメ
コマンド実行条件 += "対象の服装によっては実行できない場合がある/"
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用 != 1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 0, 1, 4
			COMRESULT = 0
	ENDSELECT
ENDIF
IF TEQUIP:上半身下着
	SELECTCASE CFLAG:下着パイズリ
		CASE 0, 1, 4
			COMRESULT = 0
	ENDSELECT
ENDIF
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_縦パイズリ
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フェラ
実効値 += RESULT

;36以上で実行
CALL 実効値判定, 実効値, 36

RETURN RESULT

@COM_縦パイズリ
#DIM DYNAMIC 増加量

SIF OVER_BUST(TARGET) != ""
	PRINT 着衣
PRINTL 縦パイズリ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(550)

;TALENT:バストサイズをみる
;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
IF TALENT:体格 == 1 || TALENT:小人体型
	SELECTCASE TALENT:バストサイズ
		CASE 4
			TIMES 増加量 , 1.20
		CASE 5
			TIMES 増加量 , 1.65
		CASE 6
			TIMES 増加量 , 2.10
		CASE 7
			TIMES 増加量 , 2.40
		CASE IS >= 8
			TIMES 増加量 , 2.70
	ENDSELECT
ELSEIF TALENT:体格 == 2
	SELECTCASE TALENT:バストサイズ
		CASE 4
			TIMES 増加量 , 1.10
		CASE 5
			TIMES 増加量 , 1.46
		CASE 6
			TIMES 増加量 , 1.82
		CASE 7
			TIMES 増加量 , 2.06
		CASE IS >= 8
			TIMES 増加量 , 2.30
	ENDSELECT
ELSE
	SELECTCASE TALENT:バストサイズ
		CASE 4
			TIMES 増加量 , 1.00
		CASE 5
			TIMES 増加量 , 1.30
		CASE 6
			TIMES 増加量 , 1.60
		CASE 7
			TIMES 増加量 , 1.80
		CASE IS >= 8
			TIMES 増加量 , 2.00
	ENDSELECT
ENDIF

増加量 *= (600 + MIN(EXP:パイズリ経験, 1000))
増加量 /= 800

;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので増やす
SIF TALENT:PLAYER:体格 <= 2 || TALENT:PLAYER:小人体型
	TIMES 増加量 , 1.40

;機械・無機物の場合、ある程度減らす
IF CSTR:種族 == "機械" && TALENT:無機物
	TIMES 増加量 , 0.60
ELSEIF CSTR:種族 == "機械" || TALENT:無機物
	TIMES 増加量 , 0.80
ENDIF

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 90

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

IF MOD:パイズリ奉仕特化 && ゲームモード() != "段位認定"
	SOURCE:情愛 = 385
	SOURCE:恭順 = 385
	SOURCE:性行動 = 880
	SOURCE:達成 = 550
	SOURCE:屈従 = 990
ELSE
	SOURCE:快B = 20
	SOURCE:情愛 = 350
	SOURCE:恭順 = 350
	SOURCE:性行動 = 800
	SOURCE:達成 = 500
	SOURCE:屈従 = 900
ENDIF
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 80

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 5
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 5, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "パイズリ経験", 2

TFLAG:奉仕快楽 = 1

パイズリ回数:TARGET:(NOF(PLAYER)) += 1

RETURN 1
