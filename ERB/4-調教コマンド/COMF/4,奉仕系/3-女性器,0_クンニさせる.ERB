;-------------------------------------------------
;クンニさせる
;-------------------------------------------------
@USERCOM_ABLE_クンニさせる
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;調教者がヴァギナ無いとダメ
コマンド実行条件 += "実行者に女性器が必要/"
SIF !VAGINA(PLAYER)
	COMRESULT = 0
;猫舌の奴隷の場合は技巧Lv2以上必要
コマンド実行条件 += "対象が[猫舌]の場合は技巧LV2以上必要/"
SIF TALENT:猫舌 && ABL:技巧 < 2
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;縄使用中はダメ
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;PLAYERがペニスバンド装着中はダメ
コマンド実行条件 += "実行者がペニスバンド装着中は実行できない/"
SIF TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_クンニさせる
#DIM DYNAMIC 実効値, 2
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_奉仕
実効値 += RESULT

実効値:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 0)
	実効値:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
	実効値:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
	実効値:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
	実効値:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
	実効値:1 += 3

SIF TALENT:汚れ無視
	実効値:1 /= 3
SIF TALENT:潔癖症
	実効値:1 *= 2

;汚れあり
IF 実効値:1
	実効値 -= 実効値:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({実効値:1})
ENDIF

;23以上で実行
CALL 実効値判定, 実効値, 23

RETURN RESULT

@COM_クンニさせる
#DIM DYNAMIC 増加量

PRINTL クンニさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF !CFLAG:PLAYER:挿入可
	TEQUIP:PLAYER:下半身上着 = 0
	TEQUIP:PLAYER:全身上着 = 0
ENDIF
TEQUIP:PLAYER:下半身下着 = 0

;-------------------------------------------------
;射精ゲージチェック（ふたなりの場合）
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(100)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:性行動 = 600
SOURCE:達成 = 100
SOURCE:屈従 = 500
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:不潔 = 150

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 2

;奴隷が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

CALL 魔力回復, 5
CALL 獣人催淫, 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
