;-------------------------------------------------
;アイマスク
;-------------------------------------------------
@USERCOM_ABLE_アイマスク, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:アイマスク && COMRESULT
	条件確定 = 1

;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[アイマスク]が必要/"
SIF !ITEM:アイマスク
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_アイマスク
PRINTL アイマスク
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:アイマスク, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 80

SOURCE:恐怖 = 50
SOURCE:露出 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES LOSEBASE:気力 , 1.00
	CASE 3
		TIMES LOSEBASE:気力 , 0.90
	CASE 4
		TIMES LOSEBASE:気力 , 0.70
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 3

CALL EXPUP, "緊縛経験", 2

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_アイマスク
#DIM DYNAMIC 消費量

PRINTL ＜アイマスク装着中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 40

SOURCE:恐怖 += 200
SOURCE:露出 += 50
SOURCE:逸脱 += 150
SOURCE:反感 += 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
消費量 = 40
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES 消費量 , 1.00
	CASE 3
		TIMES 消費量 , 0.90
	CASE 4
		TIMES 消費量 , 0.70
ENDSELECT
LOSEBASE:気力 += 消費量

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF SELECTCOMS != "アイマスク"
	CALL EXPUP, "緊縛経験", 1

RETURN 1
