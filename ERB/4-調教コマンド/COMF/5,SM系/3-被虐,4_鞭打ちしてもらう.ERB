;-------------------------------------------------
;鞭打ちしてもらう
;-------------------------------------------------
@USERCOM_ABLE_鞭打ちしてもらう
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針がSMか狂気じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がSMor狂気じゃないと実行できない/"
SIF 上級売春中 && !GROUPMATCH(3, 4)
	COMRESULT = 0
;サド、もしくはサドっ気3以上必要
コマンド実行条件 += "対象にサドっ気LV3以上or[サド]が必要/"
SIF ABL:サドっ気 < 3 && !TALENT:サド
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;助手の場合はマゾかマゾっ気3以上必要
コマンド実行条件 += "実行者が助手の場合、助手にマゾっ気LV3以上or[マゾ]が必要/"
IF PLAYER != MASTER
	SIF ABL:PLAYER:マゾっ気 < 3 && !TALENT:マゾ
		COMRESULT = 0
ENDIF
;ムチが無いとだめ
コマンド実行条件 += "アイテム[鞭]が必要/"
SIF !ITEM:鞭
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_鞭打ちしてもらう
PRINTL 鞭打ちしてもらう
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 100
DOWNBASE:MASTER:気力 += 80

SOURCE:性行動 = 500
SOURCE:達成 = 200
SOURCE:中毒 = 400
SOURCE:逸脱 = 300
SOURCE:反感 = 100
SOURCE:不潔 = 50

SIF アイテム改造:鞭 == "粘液鞭"
	CALL スライム度増加, 0, TARGET, RAND(1, 3), RAND(10, 25)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

CALL EXPUP, "嗜虐快楽経験", 5
CALL EXPUP, "調教経験", 5

RETURN 1
