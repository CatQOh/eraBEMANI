;-------------------------------------------------
;スライム尿道責め
;-------------------------------------------------
@USERCOM_ABLE_スライム尿道責め, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

コマンド実行条件 += "小スカMODが必要/"
SIF !MOD:SCAT2
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;実行者がスライムじゃないと駄目
コマンド実行条件 += "実行者に[スライム]or[溶ける快]が必要/"
SIF !ISSLIME(PLAYER)
	COMRESULT = 0
;対象がスライムだと実行できない
コマンド実行条件 += "対象がスライムだと実行できない/"
SIF ISSLIME(TARGET)
	COMRESULT = 0
;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:尿道スライム && COMRESULT
	条件確定 = 1

;ペニバン使用中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0
;オナホ使用中もダメ
コマンド実行条件 += "対象がオナホール装着中は実行できない/"
SIF TEQUIP:オナホール
	COMRESULT = 0
;搾精機
コマンド実行条件 += "対象が搾精機装着中は実行できない/"
SIF TEQUIP:搾精器
	COMRESULT = 0
;避妊具
コマンド実行条件 += "対象が避妊具装着中は実行できない/"
SIF TEQUIP:コンドーム || TEQUIP:搾精コンドーム
	COMRESULT = 0
;尿道使用中は無理
コマンド実行条件 += "尿道使用中は実行不可/"
SIF Ｕ使用中(TARGET)
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_スライム尿道責め
#DIM DYNAMIC 上昇量

IF TEQUIP:尿道スライム
	TFLAG:尿道スライム量 = TEQUIP:尿道スライム
	TEQUIP:尿道スライム = 0
ELSE
	;注入する量を選ぶ
	PRINTFORML 注入する量(ml)を入力してください(最大{LIMIT(EXP:尿道経験*2, 10, 3000)}ml)
	DO
		IF ISSKIP()
			TEQUIP:尿道スライム = 1
			BREAK
		ENDIF
		PRINTL [0] やめる
		PRINTL [10] 10ml
		SIF EXP:尿道経験 >= 25
			PRINTL [50] 50ml
		SIF EXP:尿道経験 >= 50
			PRINTL [100] 100ml
		SIF EXP:尿道経験 >= 150
			PRINTL [300] 300ml
		SIF EXP:尿道経験 >= 250
			PRINTL [500] 500ml
		SIF EXP:尿道経験 >= 500
			PRINTL [1000] 1000ml
		SIF EXP:尿道経験 >= 1000
			PRINTL [2000] 2000ml
		PRINTFORML [{MIN(EXP:尿道経験*2, 3000)}] 限界まで
		INPUT
		SIF !INRANGE(RESULT, 0, EXP:尿道経験*2) && !INRANGE(RESULT, 0, 10)
			CONTINUE
		RESULT = LIMIT(RESULT, 0, 3000)
		BREAK
	LOOP 1
	SIF RESULT == 0
		RETURN 0
	TEQUIP:尿道スライム = RESULT
ENDIF

PRINTL スライム尿道責め
CALL KOJO_MESSAGE_COM

;スライム度増加判定
CALL スライム度増加, 0, TARGET, RAND(1, 4)

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50
;尿道経験より多い量を注入すると割合に応じて最大2倍まで消費増える
IF TEQUIP:尿道スライム > EXP:尿道経験
	LOSEBASE:体力 += 30*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
	LOSEBASE:気力 += 50*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
ENDIF

SOURCE:屈従 = 100
SOURCE:苦痛 = 300
SOURCE:恐怖 = 1000
SOURCE:露出 = 300
SOURCE:逸脱 = 1000
SOURCE:反感 = 600
SOURCE:不潔 = 2000

IF TEQUIP:尿道スライム > EXP:尿道経験
	SOURCE:苦痛 += 300*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
	SOURCE:反感 += 600*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
上昇量 = 1
;注入量に応じて最大+2
SIF TEQUIP:尿道スライム > EXP:尿道経験
	上昇量++
SIF TEQUIP:尿道スライム == EXP:尿道経験*2
	上昇量++
CALL EXPUP, "尿道経験", 上昇量

SIF PENIS(TARGET)
	CALL EXPUP, "射精管理経験", 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_スライム尿道責め
#DIM DYNAMIC 上昇量
PRINTL ＜スライム尿道閉塞中＞

LOSEBASE:体力 += 30
LOSEBASE:気力 += 50
;尿道経験より多い量を注入すると割合に応じて最大2倍まで消費増える
IF TEQUIP:尿道スライム > EXP:尿道経験
	LOSEBASE:体力 += 30*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
	LOSEBASE:気力 += 50*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
ENDIF

SOURCE:屈従 = 100
SOURCE:苦痛 = 200
SOURCE:恐怖 = 500
SOURCE:露出 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 500
SOURCE:不潔 = 500

IF TEQUIP:尿道スライム > EXP:尿道経験
	SOURCE:苦痛 += 200*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
	SOURCE:反感 += 500*TEQUIP:尿道スライム/MAX(EXP:尿道経験, 1)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
上昇量 = 1
SIF TEQUIP:尿道スライム > EXP:尿道経験
	上昇量++
SIF SELECTCOMS != "尿道スライム"
	CALL EXPUP, "尿道経験", 上昇量

RETURN 1
