@USERCOM_ABLE_飲尿させる, ARG
#FUNCTION
コマンド実行条件 += "小スカMODが必要/"
SIF !MOD:SCAT2
	COMRESULT = 0
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;Ｐ使用中
コマンド実行条件 += "実行者の男性器に道具使用中は実行できない/"
SIF Ｐ使用中(PLAYER) || 避妊具(PLAYER)
	COMRESULT = 0
;尿道使用中は無理
;コマンド実行条件 += "尿道にアイテム使用中は実行できない/"
;SIF Ｕ使用中(TARGET)
;	COMRESULT = 0
;利尿剤使用中かおもらし癖ありでないとダメ
;コマンド実行条件 += "[利尿剤]効果発動中か[おもらし癖]を持ってないと実行できない/"
;SIF !TEQUIP:利尿剤 && !TALENT:おもらし癖
;	COMRESULT = 0
;コマンド実行条件 += "尿意ゲージ5000以上必要/"
;SIF BASE:尿意 < 5000
;	COMRESULT = 0
コマンド実行条件 += "対象が陥落済みかつ従順LV8以上必要/"
SIF ABL:従順 < 8 || !陥落(TARGET)
	COMRESULT = 0
;連続実行はできない
;コマンド実行条件 += "連続実行は不可/"
;SIF PREVCOMS == "放尿"
;	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_飲尿させる
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られてても断られる
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

IF TALENT:汚れ無視
	実効値 += 5
	PRINT +汚れ無視(5)
ENDIF
IF TALENT:潔癖症
	実効値 -= 20
	PRINT -潔癖症(20)
ENDIF
IF TALENT:汚臭敏感
	実効値 -= 10
	PRINT -汚臭敏感(10)
ENDIF

;80以上で実行
CALL 実効値判定, 実効値, 80

RETURN RESULT

@COM_飲尿させる
#DIM DYNAMIC 増加量

PRINTL 飲尿させる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 400

SOURCE:恐怖 = 500
SOURCE:露出 = 500
SOURCE:屈従 = 1000
SOURCE:逸脱 = 2000
SOURCE:反感 = 5000
SOURCE:不潔 = 5000
SOURCE:性行動 = 100
SOURCE:中毒 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;ペニスの場合は咥えるのでキレイになる
IF PENIS(PLAYER)
	STAIN:PLAYER:ペニス = 0
ELSEIF VAGINA(PLAYER)
	SETBIT STAIN:PLAYER:ヴァギナ, 7
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;放尿経験
CALL EXPUP_BASE, 1, 3

CALL EXPUP, "放尿経験", 2, PLAYER

CFLAG:PLAYER:口内放尿された経験++

CALL 魔力回復, 5

CALL 獣人催淫, 2, TARGET

RETURN 1
