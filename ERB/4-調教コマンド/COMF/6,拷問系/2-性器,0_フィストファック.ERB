;-------------------------------------------------
;フィストファック
;-------------------------------------------------
@USERCOM_ABLE_フィストファック, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && BR:方針 != 4
	COMRESULT = 0
;奴隷が淫乱系陥落か服従系陥落じゃないとダメ
コマンド実行条件 += "上級売春中以外は対象に[淫乱][娼婦][服従][隷属]が必要/"
SIF !上級売春中 && !GROUPMATCH(FALLTYPE(TARGET), 2, 3)
	COMRESULT = 0
;一定のＶ経験が必要 PLAYERが小柄なら手も小さいので緩和
コマンド実行条件 += "実行者が小柄以下の場合、対象のＶ経験100以上必要 実行者が普通体型以上の場合、対象のＶ経験150以上必要/"
IF TALENT:PLAYER:体格 <= 2
	SIF EXP:Ｖ経験 < 100
		COMRESULT = 0
ELSE
	SIF EXP:Ｖ経験 < 150
		COMRESULT = 0
ENDIF
;膣に何か入ってるとダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;ASSIの場合、従順4以上かサドじゃないとできない。
コマンド実行条件 += "実行者が助手の場合、助手に[サド]or従順LV4以上必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
	COMRESULT = 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
コマンド実行条件 += "対象が小柄以下の場合、対象に従順LV4以上とマゾっ気LV4以上が必要/"
IF TALENT:体格 <= 2
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		COMRESULT = 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;シャワー中はダメ
コマンド実行条件 += "シャワー中は実行できない/"
SIF TEQUIP:シャワー
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

RETURNF COMRESULT

@COM_フィストファック
#DIM DYNAMIC 増加量

PRINTL フィストファック
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入(200)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 300
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快V = 200
SOURCE:苦痛 = 1500
SOURCE:恐怖 = 500
SOURCE:露出 = 100
SOURCE:屈従 = 1200
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;フィストなどのVへの大きな苦痛は先に処理しておく
CALL 特殊ソース処理_Ｖ強

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の手の汚れが移動
STAIN:膣内 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 5

CALL EXPUP, "Ｖ経験", 15

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	PRINTL ＜初Ｖ拡張＞
	CALL EXPUP, "異常経験", 1
ENDIF

CALL EXPUP, "Ｖ拡張経験", 5

RETURN 1
