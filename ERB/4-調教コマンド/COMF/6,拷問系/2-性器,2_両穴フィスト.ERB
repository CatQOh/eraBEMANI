;-------------------------------------------------
;両穴フィスト
;-------------------------------------------------
@USERCOM_ABLE_両穴フィスト
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && BR:方針 != 4
	COMRESULT = 0
;奴隷が淫乱系陥落か服従系陥落じゃないとダメ
コマンド実行条件 += "上級売春中以外は対象に[淫乱][娼婦][服従][隷属]が必要/"
SIF !上級売春中 && !GROUPMATCH(FALLTYPE(TARGET), 2, 3)
	COMRESULT = 0
;膣に何か入ってるとダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;アナルに何か入ってるとダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;300以上のＶ・Ａ経験が必要 PLAYERが小柄なら手も小さいので緩和
コマンド実行条件 += "実行者が小柄以下の場合、対象のＶ経験とＡ経験150以上必要/"
コマンド実行条件 += "実行者が普通体型以上の場合、対象のＶ経験とＡ経験300以上必要/"
IF TALENT:PLAYER:体格 <= 2
	SIF EXP:Ｖ経験 < 150 || EXP:Ａ経験 < 150
		COMRESULT = 0
ELSE
	SIF EXP:Ｖ経験 < 300 || EXP:Ａ経験 < 300
		COMRESULT = 0
ENDIF
;ASSIの場合、従順4以上かサドじゃないとできない。
コマンド実行条件 += "実行者が助手の場合、助手に[サド]or従順LV4以上必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上かつ拡張経験10以上じゃないと実行できない
コマンド実行条件 += "対象が小柄以下の場合、対象に従順、マゾっ気LV4以上とＶ拡張経験、Ａ拡張経験10以上が必要/"
IF TALENT:体格 <= 2
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4 || EXP:Ｖ拡張経験 < 10 || EXP:Ａ拡張経験 < 10
		COMRESULT = 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

RETURNF COMRESULT

@COM_両穴フィスト
#DIM DYNAMIC 増加量

PRINTL 両穴フィスト
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 250

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順

;ABL:欲望をみる
増加量 += (増加量/5)*ABL:欲望

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(増加量/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += (増加量/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 500
LOSEBASE:気力 += 300

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 30

SOURCE:快V = 200
SOURCE:快A = 200
SOURCE:苦痛 = 2500
SOURCE:恐怖 = 1500
SOURCE:露出 = 200
SOURCE:屈従 = 2000
SOURCE:逸脱 = 2000
SOURCE:反感 = 2000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
CALL 特殊ソース処理_Ｖ強
CALL 特殊ソース処理_Ａ強

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔調教者の手の汚れが移動
;奴隷の膣内⇔調教者の手の汚れが移動
SETBIT STAIN:PLAYER:手, 3
STAIN:PLAYER:手 |= STAIN:膣内
STAIN:PLAYER:手 |= STAIN:アナル
STAIN:アナル |= STAIN:PLAYER:手
STAIN:膣内 |= STAIN:PLAYER:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 6

CALL EXPUP, "Ｖ経験", 15
CALL EXPUP, "Ａ経験", 15

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	PRINTL ＜初Ｖ&Ａ拡張＞
	CALL EXPUP, "異常経験", 2
ENDIF

CALL EXPUP, "Ｖ拡張経験", 5
CALL EXPUP, "Ａ拡張経験", 5

RETURN 1
