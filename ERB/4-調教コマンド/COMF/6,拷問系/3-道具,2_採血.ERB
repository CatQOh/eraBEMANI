;-------------------------------------------------
;採血
;-------------------------------------------------
@USERCOM_ABLE_採血
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中はダメ
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手召喚中は別コマンド
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:採血 && COMRESULT
	条件確定 = 1

;血液パックが必要
コマンド実行条件 += "アイテム[血液パック]が必要/"
SIF !ITEM:血液パック
	COMRESULT = 0
;対象が無機物、もしくは種族:機械だとダメ
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
;対象がゾンビだとダメ
コマンド実行条件 += "対象が[ゾンビ]だと実行できない/"
SIF TALENT:ゾンビ
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;風呂場では実行できない
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;技巧4以上、針さばき、治療、禁断の知識いずれかが必要
コマンド実行条件 += "実行者に技巧LV4以上or[針さばき]or医療技能or[禁断の知識]が必要/"
SIF ABL:PLAYER:技巧 < 4 && !TALENT:PLAYER:針さばき && !ABL:PLAYER:医療技能 && !TALENT:PLAYER:禁断の知識
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_採血
PRINTL 採血

INVERTBIT TEQUIP:採血, 0

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力 継続処理関数で行う
IF TEQUIP:採血
	SOURCE:苦痛 = 1000
	SOURCE:恐怖 = 1500
	SOURCE:反感 = 1500
	SOURCE:不潔 = 500
	SOURCE:鬱屈 = 500

	DOWNBASE:MASTER:体力 += 30
	DOWNBASE:MASTER:気力 += 50
	ITEM:血液パック--
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 3
CALL EXPUP, "医療経験", 2, PLAYER

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_採血
PRINTL ＜採血中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;採血のLOSEBASE:体力だけは軽減不可の特殊処理を行う(ソース計算を参照)
LOSEBASE:気力 += 100

SOURCE:苦痛 = 300
SOURCE:恐怖 = 1000
SOURCE:反感 = 1000
SOURCE:不潔 = 300
SOURCE:鬱屈 = 300

;1ターンで30ml
TFLAG:採血量 += 30

CALL DNAデータ解析, TARGET

CALL EXPUP, "医療経験", 1, PLAYER

RETURN 1
