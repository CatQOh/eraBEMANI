;-------------------------------------------------
;フィストファックさせる
;-------------------------------------------------
@USERCOM_ABLE_フィストファックさせる
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針がSM、狂気じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がSMor狂気じゃないと実行できない/"
SIF 上級売春中 && !GROUPMATCH(BR:方針, 3, 4)
	COMRESULT = 0
;サド、もしくはサドっ気3以上必要(BR:方針が狂気ならスルー)
コマンド実行条件 += "狂気方針の上級売春でない場合、対象に[サド]orサドっ気LV3が必要/"
SIF !(上級売春中 && BR:方針 == 4) && !TALENT:サド && ABL:サドっ気 < 3
	COMRESULT = 0

;実行者のＶ経験100以上orＶ感覚3以上
コマンド実行条件 += "実行者のＶ経験100以上orＶ感覚LV3以上が必要/"
SIF EXP:PLAYER:Ｖ経験 < 100 && ABL:Ｖ感覚 <= 3
	COMRESULT = 0

;実行者がヴァギナ無いとダメ
コマンド実行条件 += "実行者に女性器が必要/"
SIF !VAGINA(PLAYER)
	COMRESULT = 0
;助手の場合は助手が陥落していないとダメ
コマンド実行条件 += "実行者が助手の場合、助手が陥落していないと実行できない/"
SIF (PLAYER == ASSI && !陥落(ASSI)) || (PLAYER == ASSI:1 && !陥落(ASSI:1))
	COMRESULT = 0
;調教者が小人体型のとき主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "実行者が[小人体型]の場合、対象も[小人体型]か、主人に[禁断の知識]が必要/"
SIF TALENT:PLAYER:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:小人体型
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;縄使用中は出来ない
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;PLAYERがＶ使用中はダメ
コマンド実行条件 += "実行者がV使用中は実行できない/"
SIF Ｖ使用中(PLAYER)
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_フィストファックさせる
#DIM DYNAMIC 実効値, 2
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フィストさせる
実効値 += RESULT

実効値:1 = 0
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
	実効値:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
	実効値:1 += 6
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
	実効値:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
	実効値:1 += 3

SIF TALENT:汚れ無視
	実効値:1 /= 3
SIF TALENT:潔癖症
	実効値:1 *= 2

;汚れあり
IF 実効値:1
	実効値 -= 実効値:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({実効値:1})
ENDIF

;32以上で実行
CALL 実効値判定, 実効値, 32

RETURN RESULT

@COM_フィストファックさせる
#DIM DYNAMIC 増加量

PRINTL フィストファックさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷フラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
TFLAG:奴隷挿入箇所 = 1
増加量 = 射精ゲージ計算_実行者受け(400)

;ABL:技巧をみる
増加量 += 増加量/10*4*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 120
DOWNBASE:MASTER:気力 += 100

SOURCE:恐怖 = 50
SOURCE:性行動 = 500
SOURCE:屈従 = 300
SOURCE:逸脱 = 1000
SOURCE:反感 = 500
SOURCE:中毒 = 200
SOURCE:不潔 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

IF TALENT:PLAYER:処女
	IF TALENT:PLAYER:性転換済
		IF PLAYER != MASTER
			IF 陥落(PLAYER)
				CFLAG:PLAYER:性転換処女の奪われ方 = 2
			ELSEIF 相性値(PLAYER, TARGET)
				CFLAG:PLAYER:性転換処女の奪われ方 = 3
			ELSE
				CFLAG:PLAYER:性転換処女の奪われ方 = 1
			ENDIF
		ENDIF
		CSTR:PLAYER:性転換処女 = %NAME:TARGET%
	ELSE
		IF PLAYER != MASTER
			IF 陥落(PLAYER)
				CFLAG:PLAYER:処女の奪われ方 = 2
			ELSEIF 相性値(PLAYER, TARGET)
				CFLAG:PLAYER:処女の奪われ方 = 3
			ELSE
				CFLAG:PLAYER:処女の奪われ方 = 1
			ENDIF
		ENDIF
		CSTR:PLAYER:処女 = %NAME:TARGET%
	ENDIF
ENDIF

IF TALENT:PLAYER:処女 || TALENT:PLAYER:再生処女
	PRINTFORMW %CALLNAME:PLAYER%処女喪失
	SETBIT STAIN:PLAYER:膣内, 6
ENDIF

TALENT:PLAYER:処女 = 0
TALENT:PLAYER:再生処女 = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精時の処理
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手⇔調教者の膣内の汚れが移動
STAIN:手 |= STAIN:PLAYER:膣内
STAIN:PLAYER:膣内 |= STAIN:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "Ｖ経験", 5, PLAYER
CALL EXPUP, "Ｖ拡張経験", 2, PLAYER
CALL EXPUP, "苦痛快楽経験", 2, PLAYER
CALL EXPUP, "嗜虐快楽経験", 3

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
