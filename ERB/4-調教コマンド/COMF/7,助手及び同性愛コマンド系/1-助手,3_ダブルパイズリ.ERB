;-------------------------------------------------
;ダブルパイズリ
;-------------------------------------------------
@USERCOM_ABLE_ダブルパイズリ, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;助手じゃないとダメ
コマンド実行条件 += "実行者が助手じゃないと実行できない/"
SIF PLAYER != ASSI
	COMRESULT = 0
;助手、奴隷共に女性じゃないとダメ
コマンド実行条件 += "助手、対象共に女性じゃないと実行できない/"
SIF SEX(TARGET) == 1 || SEX(ASSI) == 1
	COMRESULT = 0
;主人がペニス無いとダメ
コマンド実行条件 += "主人に男性器が必要/"
SIF !PENIS(MASTER)
	COMRESULT = 0
;主人が[魅惑]か[謎の魅力]、もしくは奴隷と助手両方陥落してないとダメ
コマンド実行条件 += "主人に[魅惑]or[謎の魅力]が必要 または助手、対象共に陥落していれば実行可能/"
SIF ASSI > 0 && !TALENT:MASTER:魅惑 && !TALENT:MASTER:謎の魅力 && (!CFLAG:陥落 || !CFLAG:ASSI:陥落)
	COMRESULT = 0
;助手が奉仕精神LV3以上、技巧LV4以上じゃないとダメ
コマンド実行条件 += "助手の奉仕精神LV3以上と技巧LV4以上が必要/"
SIF ASSI > 0 && (ABL:ASSI:技巧 < 4 || ABL:ASSI:奉仕精神 < 3)
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "助手が[アニマル]だと実行できない/"
SIF ASSI > 0 && TALENT:ASSI:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_ダブルパイズリ
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フェラ
実効値 += RESULT

IF 相性値(ASSI, TARGET) > 100 && 相性値(TARGET, ASSI) > 100
	実効値 += 3
	PRINTFORM +%CALLNAME:TARGET%と%CALLNAME:ASSI%の相性良好(3)
ENDIF

;38以上で実行
CALL 実効値判定, 実効値, 38

RETURN RESULT

@COM_ダブルパイズリ
#DIM DYNAMIC 増加量

PRINTL ダブルパイズリ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実績「クロノスとカイロスにダブルパイズリをさせる」
IF CHARANAMEF(TARGET) == "クロノス" || CHARANAMEF(TARGET) == "カイロス"
	IF ASSI > -1
		IF CHARANAMEF(ASSI) == "クロノス" || CHARANAMEF(ASSI) == "カイロス"
			CALL 実績解除, "竿、包み込む。弐つの双丘"
		ENDIF
	ENDIF
ENDIF

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(600)

;ABL:助手の技巧をみる
増加量 += (増加量/5)*ABL:ASSI:技巧

;ABL:助手の従順をみる
増加量 += (増加量/10)*ABL:ASSI:従順 - (増加量/10)*2

;ABL:助手の奉仕精神をみる
SELECTCASE ABL:ASSI:奉仕精神
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		増加量 += 増加量/3*ABL:奉仕精神
ENDSELECT

;TALENT:バストサイズをみる
SELECTCASE TALENT:バストサイズ
	CASE 1
		TIMES 増加量 , 0.70
	CASE 2
		TIMES 増加量 , 0.85
	CASE 3
		TIMES 増加量 , 1.00
	CASE 4, 5
		TIMES 増加量 , 1.15
	CASEELSE
		TIMES 増加量 , 1.30
ENDSELECT

;機械・無機物の場合、ある程度減らす
IF CSTR:種族 == "機械" && TALENT:無機物
	TIMES 増加量 , 0.60
ELSEIF CSTR:種族 == "機械" || TALENT:無機物
	TIMES 増加量 , 0.80
ENDIF

;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES 増加量 , 1.25

;TALENT:助手のバストサイズをみる
SELECTCASE TALENT:ASSI:バストサイズ
	CASE 1
		TIMES 増加量 , 0.70
	CASE 2
		TIMES 増加量 , 0.85
	CASE 3
		TIMES 増加量 , 1.00
	CASE 4, 5
		TIMES 増加量 , 1.15
	CASEELSE
		TIMES 増加量 , 1.30
ENDSELECT

;2つのバストサイズが同じとき効果を高くする
SIF TALENT:バストサイズ == TALENT:ASSI:バストサイズ
	TIMES 増加量 , 1.20

;機械・無機物の場合、ある程度減らす
IF CSTR:ASSI:種族 == "機械" && TALENT:ASSI:無機物
	TIMES 増加量 , 0.60
ELSEIF CSTR:ASSI:種族 == "機械" || TALENT:ASSI:無機物
	TIMES 増加量 , 0.80
ENDIF

;助手が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:ASSI:小柄体型 || TALENT:ASSI:小人体型
	TIMES 増加量 , 1.25

;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES 増加量 , 1.15

CALL 射精ゲージ増加, 増加量, MASTER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快B = 200
SOURCE:情愛 = 900
SOURCE:恭順 = 300
SOURCE:性行動 = 600
SOURCE:達成 = 750
SOURCE:屈従 = 900
SOURCE:逸脱 = 400
SOURCE:反感 = 400
SOURCE:不潔 = 100

;汚れ
SIF GET_STAIN(STAIN:MASTER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精するのは主人なので専用の関数を呼ぶ
CALL SAMEN_CHECK_M, 5
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 5, NOWEX:MASTER:射精, MASTER
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:胸

;助手の胸⇔調教者のペニスの汚れが移動
STAIN:ASSI:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:ASSI:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛経験
;助手と奴隷が女性じゃないと実行できないのでレズ経験のみ
IF SEX(MASTER) == 2
	CALL EXPUP, "レズ経験", 7
	CALL EXPUP, "レズ経験", 7, ASSI
	CALL EXPUP, "レズ経験", 7, MASTER
ELSE
	CALL EXPUP, "レズ経験", 4
	CALL EXPUP, "レズ経験", 4, ASSI
	CALL EXPUP, "レズ経験", 4, MASTER
ENDIF

;調教経験
CALL EXPUP, "主人調教経験", 3
CALL EXPUP, "助手調教経験", 1

CALL EXPUP, "パイズリ経験", 1
CALL EXPUP, "パイズリ経験", 1, ASSI

IF !避妊具(MASTER)
	CALL 魔力回復, 3
	CALL 魔力回復, 3, ASSI
	CALL 獣人催淫, 1, TARGET, MASTER
	CALL 獣人催淫, 1, ASSI, MASTER
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1
