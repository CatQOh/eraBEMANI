;-------------------------------------------------
;兜合わせ
;-------------------------------------------------
@USERCOM_ABLE_兜合わせ
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は客が男嫌いだとダメ
コマンド実行条件 += "上級売春中は客が[男嫌い]だと実行できない/"
SIF 上級売春中 && TALENT:男嫌い
	COMRESULT = 0
;対象にペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;調教者にもペニス無いとダメ
コマンド実行条件 += "実行者に男性器が必要/"
SIF !PENIS(PLAYER)
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "対象か実行者が男性期に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET) || Ｐ使用中(PLAYER)
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_兜合わせ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 2
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

実効値 = RESULT

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望*2
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	実効値 += ABL:精液中毒
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	実効値 += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "淫魔"
			実効値:1 = 10
		CASE "淫乱"
			実効値:1 = 5
		CASE "恥じらい"
			実効値:1 = -1
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				実効値:1 = -3
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				実効値:1 = -7
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;媚薬
IF TEQUIP:媚薬
	実効値 += 6
	PRINT +媚薬(6)
ENDIF

実効値:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	実効値:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	実効値:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	実効値:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 4)
	実効値:1 += 2
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	実効値:1 += 3

SIF TALENT:汚れ無視
	実効値:1 /= 3
SIF TALENT:潔癖症
	実効値:1 *= 2

;汚れあり
IF 実効値:1
	実効値 -= 実効値:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({実効値:1})
ENDIF

;20以上で実行
CALL 実効値判定, 実効値, 20

RETURN RESULT

@COM_兜合わせ
#DIM DYNAMIC 増加量

PRINTL 兜合わせ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:下半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用 != 1)
	IF !CFLAG:挿入可
		TEQUIP:全身上着 = 0
		TEQUIP:下半身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_奉仕(500), PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 500

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		増加量 += 増加量/3*ABL:奉仕精神
ENDSELECT

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 300
SOURCE:情愛 = 500
SOURCE:性行動 = 500
SOURCE:恭順 = 300
SOURCE:達成 = 500
SOURCE:屈従 = 300
SOURCE:露出 = 50
SOURCE:逸脱 = 100
SOURCE:反感 = 100
SOURCE:不潔 = 60

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のペニス⇔調教者のペニスの汚れが移動
STAIN:ペニス |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 2

;魔力回復
IF !避妊具(PLAYER) && !避妊具(TARGET)
	CALL 魔力回復, 3
	CALL 魔力回復, 3, PLAYER
	CALL 獣人催淫, 1
	CALL 獣人催淫, 1, PLAYER
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1
