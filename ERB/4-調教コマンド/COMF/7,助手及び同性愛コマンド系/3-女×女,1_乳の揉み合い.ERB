;-------------------------------------------------
;乳の揉み合い
;-------------------------------------------------
@USERCOM_ABLE_乳の揉み合い
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;互いに女じゃないとダメ
コマンド実行条件 += "対象、実行者共にバストサイズ絶壁以上でないと実行できない/"
SIF TALENT:バストサイズ == 0 || TALENT:PLAYER:バストサイズ == 0
	COMRESULT = 0
;レズっ気3以上限定
コマンド実行条件 += "対象の欲望LV3以上必要/"
SIF ABL:欲望 < 3
	COMRESULT = 0
;上半身下着または上半身上着または全身上着をつけているとダメ
コマンド実行条件 += "対象の乳房が露出していないと実行できない/"
SIF OVER_BUST(TARGET) != ""
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_乳の揉み合い
PRINTL 乳の揉み合い
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 70

SOURCE:快B = 200
SOURCE:性行動 = 300
SOURCE:恭順 = 200
SOURCE:露出 = 100
SOURCE:不潔 = 50
SOURCE:逸脱 = 100
SOURCE:反感 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;調教者が［幼児退行］
;IF TALENT:PLAYER:幼児／幼児退行
;	TIMES SOURCE:快B , 1.20
;	TIMES SOURCE:性行動 , 1.20
;ENDIF

;調教者が［幼稚］
IF TALENT:PLAYER:幼稚
	TIMES SOURCE:快B , 1.20
	TIMES SOURCE:性行動 , 1.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の指の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

RETURN 1
