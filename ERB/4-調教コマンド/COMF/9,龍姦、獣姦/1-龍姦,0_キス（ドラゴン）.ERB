;-------------------------------------------------
;キス（ドラゴン）
;-------------------------------------------------
@USERCOM_ABLE_キス（ドラゴン）, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないと不可
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && 娼館方針 != 4
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;風呂場だとダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;PLAYERがドラゴン使い持ってないとダメ
コマンド実行条件 += "実行者に[ドラゴン使い]が必要/"
SIF !TALENT:PLAYER:ドラゴン使い
	COMRESULT = 0
;PLAYERが助手で未陥落だとダメ
コマンド実行条件 += "実行者が助手の場合は助手が陥落済みじゃないと実行できない/"
SIF PLAYER == ASSI && !CFLAG:PLAYER:陥落
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_キス（ドラゴン）
#DIM DYNAMIC 増加量
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;技巧が高いとキス・オブ・ザ・ドラゴンに派生
IF ABL:PLAYER:技巧 >= 3
	PRINTL キス・オブ・ザ・ドラゴン
	;このターンで-1されるので2
	TFLAG:実行判定無視 = 2
ELSE
	PRINTL キス（ドラゴン）
ENDIF

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 50

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		増加量 += (増加量/3)*ABL:奉仕精神
ENDSELECT

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES 増加量 , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES 増加量 , 2.00
	SIF TALENT:親愛
		TIMES 増加量 , 3.00
ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 25

DOWNBASE:MASTER:体力 += 10
DOWNBASE:MASTER:気力 += 10

SOURCE:情愛 = 50
SOURCE:性行動 = 10
SOURCE:恭順 = 10
SOURCE:屈従 = 30
SOURCE:恐怖 = 200
SOURCE:不潔 = 100
SOURCE:反感 = 50
SOURCE:逸脱 = 30
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 += 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 0, 1

;魔力回復
CALL 魔力回復, 10

RETURN 1

