;-------------------------------------------------
;舐め回す（ドラゴン）
;-------------------------------------------------
@USERCOM_ABLE_舐め回す（ドラゴン）
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないと不可
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && BR:方針 != 4
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;風呂場だとダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;PLAYERがドラゴン使い持ってないとダメ
コマンド実行条件 += "実行者に[ドラゴン使い]が必要/"
SIF !TALENT:PLAYER:ドラゴン使い
	COMRESULT = 0
;PLAYERが助手で未陥落だとダメ
コマンド実行条件 += "実行者が助手の場合は助手が陥落済みじゃないと実行できない/"
SIF PLAYER == ASSI && !陥落(PLAYER)
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_舐め回す（ドラゴン）
#DIM DYNAMIC 増加量

PRINTL 舐め回す（ドラゴン）
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 100

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:PLAYER:技巧/2

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += 増加量*(ABL:Ｃ感覚-3)
ENDSELECT

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 15

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 100
SOURCE:快B = 100
SOURCE:液体 = 1000
SOURCE:屈従 = 100
SOURCE:露出 = 100
SOURCE:恐怖 = 300
SOURCE:不潔 = 300
SOURCE:逸脱 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精, ASSI

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 0, 1

;魔力回復
CALL 魔力回復, 7

RETURN 1
