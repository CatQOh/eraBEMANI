;-------------------------------------------------
;膣を犯す（ドラゴン）
;-------------------------------------------------
@USERCOM_ABLE_膣を犯す（ドラゴン）
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないと不可
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && BR:方針 != 4
	COMRESULT = 0
;膣使用中はダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;小人体型持ちは物理的に無理
コマンド実行条件 += "対象が[小人体型]だと実行できない/"
SIF TALENT:小人体型
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;PLAYERがドラゴン使い持ってないとダメ
コマンド実行条件 += "実行者に[ドラゴン使い]が必要/"
SIF !TALENT:PLAYER:ドラゴン使い
	COMRESULT = 0
;PLAYERが助手で未陥落だとダメ
コマンド実行条件 += "実行者が助手の場合は助手が陥落済みじゃないと実行できない/"
SIF PLAYER == ASSI && !陥落(PLAYER)
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_膣を犯す（ドラゴン）
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
SIF TEQUIP:縄
	RETURN 1

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_セックス
実効値 += RESULT

;46以上で実行
CALL 実効値判定, 実効値, 46

RETURN RESULT

@COM_膣を犯す（ドラゴン）
#DIM DYNAMIC 増加量

PRINTL 膣を犯す（ドラゴン）

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;処女なら異常経験追加
IF TALENT:処女
	PRINTL ＜獣姦処女姦通＞
	CALL EXPUP, "異常経験", 1
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（ドラゴン）
;-------------------------------------------------
増加量 = 1000

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(増加量/5)

;処女だと増える
SIF TALENT:処女
	TIMES 増加量 , 1.50

BASE:PLAYER:ドラゴン射精 += 増加量

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入(500)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 200

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SOURCE:快V = 700
SOURCE:情愛 = 100
SOURCE:苦痛 = 1200
SOURCE:中毒 = 300
SOURCE:露出 = 100
SOURCE:反感 = 500
SOURCE:液体 = 500
SOURCE:恐怖 = 800
SOURCE:逸脱 = 300
SOURCE:不潔 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで淫乱、娼婦持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 2
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_DRAGON
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_DRAGON, NOWEX:PLAYER:ドラゴン射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 0, 3

CALL EXPUP, "Ｖ経験", 3
CALL EXPUP, "性交経験", 1

;異種姦経験
IF CFLAG:異種姦経験 == 0
	PRINTL ＜初異種姦＞
	CALL EXPUP, "異常経験", 1
ENDIF

CFLAG:異種姦経験++

CALL 魔力回復, 10

RETURN 1
