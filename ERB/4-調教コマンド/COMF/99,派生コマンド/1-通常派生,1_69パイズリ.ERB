;-------------------------------------------------
;69パイズリ
;-------------------------------------------------
@USERCOM_ABLE_69パイズリ, ARG
#FUNCTION
#DIM DYNAMIC 必要技巧

;69パイズリ
コマンド実行条件 += "実行者が助手の場合、助手に[小悪魔]or欲望LV3以上が必要/"
IF ASSIPLAY
	;欲望3以下のASSIの場合シックスナインはできない（小悪魔ならＯＫ）
	SIF ABL:ASSI:欲望 <= 3 && !TALENT:ASSI:小悪魔
		COMRESULT = 0
ENDIF

;従順3以上、奉仕精神3以上必要
コマンド実行条件 += "対象に従順と奉仕精神LV3以上が必要/"
SIF ABL:従順 < 3 || ABL:奉仕精神 < 3
	COMRESULT = 0

コマンド実行条件 += "対象が貧乳なら技巧LV6以上必要/"
コマンド実行条件 += "対象が並乳なら技巧LV5以上必要/"
コマンド実行条件 += "対象が巨乳、豊乳なら技巧LV4以上必要/"
コマンド実行条件 += "対象が爆乳なら技巧LV3以上必要/"
SELECTCASE TALENT:バストサイズ
	;男or絶壁だとダメ
	CASE 0, 1
		COMRESULT = 0
	;貧乳だと技巧LV6以上必要
	CASE 2
		必要技巧 = 6
	;普通だと技巧LV5以上必要
	CASE 3
		必要技巧 = 5
	;巨乳、豊乳なら技巧LV4以上必要
	CASE 4, 5
		必要技巧 = 4
	;爆乳なら技巧LV3以上必要
	CASE 6
		必要技巧 = 3
ENDSELECT
SIF ABL:技巧 < 必要技巧
	COMRESULT = 0
;調教者がペニス無いとダメ
コマンド実行条件 += "実行者に男性器が必要/"
SIF !PENIS(PLAYER)
	COMRESULT = 0
;ペニスバンド使用中はダメ
コマンド実行条件 += "対象か実行者がペニスバンド装着中は実行できない/"
SIF TEQUIP:ペニスバンド || TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
コマンド実行条件 += "対象の服装によっては実行できない場合がある/"
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 0, 2, 4
			COMRESULT = 0
	ENDSELECT
ENDIF
IF TEQUIP:上半身下着
	SELECTCASE CFLAG:下着パイズリ
		CASE 0, 2, 4
			COMRESULT = 0
	ENDSELECT
ENDIF
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;特殊確認
;前回と今回の調教者が同じで、縄とボールギャグを使ってない。ただしゲームセンター中はダメ
コマンド実行条件 += "前回と今回の実行者が同じで、[縄][ボールギャグ]を使用していない場合、/"
コマンド実行条件 += "パイズリ、縦パイズリ、パイズリフェラ、クンニ、クンニさせる、フェラする、シックスナイン、69パイズリから派生可能/"
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:縄 && !TEQUIP:ボールギャグ && !TEQUIP:ゲームセンター
	;前コマンド
	SELECTCASE PREVCOMS
		;前回の調教がパイズリ系か舐める系か69パイズリだと派生可能
		CASE "パイズリ", "縦パイズリ", "パイズリフェラ", "クンニ", "フェラする", "シックスナイン", "69パイズリ"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@実行判定_69パイズリ
#DIM DYNAMIC 実効値

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フェラ
実効値 += RESULT

;45以上で実行
CALL 実効値判定, 実効値, 45

RETURN RESULT

@COM_69パイズリ
#DIM DYNAMIC 増加量

PRINTL 69パイズリ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(500)

;TALENT:バストサイズをみる
;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
IF TALENT:体格 == 1 || TALENT:小人体型
	SELECTCASE TALENT:バストサイズ
		CASE 0, 1
			TIMES 増加量 , 0.40
		CASE 2
			TIMES 増加量 , 0.85
		CASE 3
			TIMES 増加量 , 1.30
		CASE 4
			TIMES 増加量 , 1.60
		CASE 5
			TIMES 増加量 , 1.90
		CASE 6
			TIMES 増加量 , 2.10
		CASE 7
			TIMES 増加量 , 2.40
		CASE IS >= 8
			TIMES 増加量 , 2.70
	ENDSELECT
ELSEIF TALENT:体格 == 2
	SELECTCASE TALENT:バストサイズ
		CASE 0, 1
			TIMES 増加量 , 0.40
		CASE 2
			TIMES 増加量 , 0.76
		CASE 3
			TIMES 増加量 , 1.12
		CASE 4
			TIMES 増加量 , 1.36
		CASE 5
			TIMES 増加量 , 1.60
		CASE 6
			TIMES 増加量 , 1.84
		CASE 7
			TIMES 増加量 , 2.08
		CASE IS >= 8
			TIMES 増加量 , 2.32
	ENDSELECT
ELSE
	SELECTCASE TALENT:バストサイズ
		CASE 0, 1
			TIMES 増加量 , 0.40
		CASE 2
			TIMES 増加量 , 0.70
		CASE 3
			TIMES 増加量 , 1.00
		CASE 4
			TIMES 増加量 , 1.20
		CASE 5
			TIMES 増加量 , 1.40
		CASE 6
			TIMES 増加量 , 1.60
		CASE 7
			TIMES 増加量 , 1.80
		CASE IS >= 8
			TIMES 増加量 , 2.00
	ENDSELECT
ENDIF

増加量 *= (600 + MIN(EXP:パイズリ経験, 1200))
増加量 /= 800

;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:PLAYER:体格 <= 2 || TALENT:PLAYER:小人体型
	TIMES 増加量 , 1.15

;無機物の場合、ある程度減らす
IF TALENT:無機物
	TIMES 増加量 , 0.60
ELSEIF TALENT:無機物
	TIMES 増加量 , 0.80
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 増加量 , 1.50

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
;PLAYERにも胸があればパイズる
IF TALENT:PLAYER:バストサイズ >= 2 && SEX(PLAYER) == 2
	増加量 = 射精ゲージ計算_パイズリする(700)
ELSE
	増加量 = 射精ゲージ計算_フェラチオ(700)
ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 120

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 30

IF MOD:パイズリ奉仕特化 && ゲームモード() != "段位認定"
	SOURCE:情愛 = 880
	SOURCE:性行動 = 1320
	SOURCE:達成 = 550
	SOURCE:恭順 = 770
	SOURCE:屈従 = 1320
ELSE
	SOURCE:快B = 10
	SOURCE:情愛 = 800
	SOURCE:性行動 = 1200
	SOURCE:達成 = 500
	SOURCE:恭順 = 700
	SOURCE:屈従 = 1200
ENDIF
SOURCE:快C = 80
SOURCE:液体 = 100
SOURCE:露出 = 100
SOURCE:逸脱 = 700
SOURCE:反感 = 700
SOURCE:不潔 = 60

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 5
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT, 5, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸
;奴隷のヴァギナ、ペニス汚れを舐め取る
SIF VAGINA(TARGET)
	STAIN:ヴァギナ = 0
SIF PENIS(TARGET)
	STAIN:ペニス = 0

;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 4, 2

CALL EXPUP, "フェラ経験", 1
CALL EXPUP, "パイズリ経験", 1

IF PENIS(TARGET)
	CALL EXPUP, "フェラ経験", 1, PLAYER
	SIF TALENT:PLAYER:バストサイズ >= 2 && SEX(PLAYER) == 2
		CALL EXPUP, "パイズリ経験", 1, PLAYER
ENDIF

;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奴隷が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

;調教者が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1, PLAYER

IF VAGINA(PLAYER) || !避妊具(PLAYER)
	CALL 魔力回復, 5
	CALL 獣人催淫, 1
ENDIF

IF VAGINA(TARGET) || !避妊具(TARGET)
	CALL 魔力回復, 5, PLAYER
	CALL 獣人催淫, 1, PLAYER
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

パイズリ回数:TARGET:(NOF(PLAYER)) += 1

RETURN 1
