;-------------------------------------------------
;岩清水
;-------------------------------------------------
@USERCOM_ABLE_岩清水
#FUNCTION
;岩清水実行判定
;羞恥プレイ中じゃないとダメ
コマンド実行条件 += "大鏡使用中である/"
SIF !TEQUIP:羞恥プレイ
	COMRESULT = 0
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;性器が汚れている場合（愛液、母乳の汚れ以外）
コマンド実行条件 += "性器が汚れていると実行できない(素質[汚れ無視]なら可)/"
IF GET_STAIN(STAIN:ヴァギナ, "先走り", "精液", "アナル", "粘液", "破瓜血", "尿")
	;実行者
	SELECTCASE PLAYER
		;主人
		CASE MASTER
			;汚れ無視を持っていない場合はクンニできない
			SIF !TALENT:MASTER:汚れ無視
				COMRESULT = 0
		;助手
		CASE ASSI, ASSI:1
			;汚臭敏感で従順3以下の場合クンニはできない（汚れ無視なら可能）
			SIF ABL:PLAYER:従順 <= 3 && TALENT:PLAYER:汚臭敏感 && !TALENT:PLAYER:汚れ無視
				COMRESULT = 0
	ENDSELECT
ENDIF

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;Ｖ使用中はダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_岩清水
PRINTL 岩清水
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 90

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 150
SOURCE:恐怖 = 100
SOURCE:液体 = 100
SOURCE:露出 = 200
SOURCE:逸脱 = 300
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のヴァギナ汚れを舐め取る
STAIN:ヴァギナ = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

CALL 魔力回復, 12, PLAYER
CALL 獣人催淫, 1, PLAYER

RETURN 1
