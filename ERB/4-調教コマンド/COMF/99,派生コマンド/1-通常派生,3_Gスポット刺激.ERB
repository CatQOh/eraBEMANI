;-------------------------------------------------
;Gスポット刺激
;-------------------------------------------------
@USERCOM_ABLE_Gスポット刺激
#FUNCTION
;Gスポット刺激
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;潤滑不足で、従順3以下のASSIの場合はできない（サドならＯＫ）
コマンド実行条件 += "実行者が助手で、対象が潤滑不足の場合、助手に[サド]or従順LV4以上必要/"
IF PALAM:潤滑 < PALAMLV:2 && PLAYER == ASSI
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF
;処女だとダメ
コマンド実行条件 += "対象が[処女][再生処女]だと実行できない/"
SIF TALENT:処女 || TALENT:再生処女
	COMRESULT = 0
;主人の技巧が3以下はだめ
コマンド実行条件 += "実行者の技巧LV3以上が必要/"
SIF ABL:PLAYER:技巧 < 3
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;ペニスバンド装着中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0
;異物挿入中はダメ
コマンド実行条件 += "V異物挿入中は実行できない/"
SIF TEQUIP:Ｖ異物
	COMRESULT = 0

;派生確認
;前コマンドと今回の実行者が同じ
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "指挿入れ、Gスポット刺激から派生可能/"
IF PLAYER == TFLAG:前コマンド実行者
	;前コマンド
	SELECTCASE PREVCOMS
		;非処女で前回の調教が指挿入れかつ実行者の技巧3以上だと可能
		CASE "指挿入れ"
			SIF ABL:PLAYER:技巧 >= 3 && !TALENT:処女 && !TALENT:再生処女
				RETURNF COMRESULT
		;前回の調教がGスポット刺激なら続行可能
		CASE "Gスポット刺激"
			RETURNF COMRESULT
	ENDSELECT
ENDIF
COMRESULT = 0
RETURNF 0

@COM_Gスポット刺激
PRINTL Gスポット刺激
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快V = 300
SOURCE:苦痛 = 20
SOURCE:液体 = 50
SOURCE:露出 = 80
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_Ｖ責め(300), TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の手の汚れが移動
STAIN:膣内 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 2

CALL EXPUP, "Ｖ経験", 1

RETURN 1

