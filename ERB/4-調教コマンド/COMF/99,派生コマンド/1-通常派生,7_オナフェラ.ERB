;-------------------------------------------------
;オナフェラ
;-------------------------------------------------
@USERCOM_ABLE_オナフェラ
#FUNCTION
;従順3以上、奉仕精神3以上必要
コマンド実行条件 += "対象の従順と奉仕精神LV3以上が必要/"
SIF ABL:従順 < 3 || ABL:奉仕精神 < 3
	COMRESULT = 0
;PLAYERにペニスが必要
コマンド実行条件 += "実行者に男性器が必要/"
SIF !PENIS(PLAYER)
	COMRESULT = 0

;小人体型持ちは、双方が小人体型ならOK
;猫舌の奴隷の場合は技巧Lv2以上必要
コマンド実行条件 += "対象が[猫舌]の場合は技巧LV2以上必要/"
SIF TALENT:猫舌 && ABL:技巧 < 2
	COMRESULT = 0
;ペニスバンド使用中はダメ
コマンド実行条件 += "実行者か対象がペニスバンド使用中は実行できない/"
SIF TEQUIP:ペニスバンド || TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0

;貞操帯使用中は無理
コマンド実行条件 += "対象が貞操帯装着中は実行できない/"
SIF CFLAG:貞操帯
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;前回と今回の調教者が同じ
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "フェラチオ、イラマチオ、自慰、胸自慰、オナフェラから派生可能/"
IF PLAYER == TFLAG:前コマンド実行者
	;フェラ系、自慰系、オナフェラなら可能
	SELECTCASE PREVCOMS
		CASE "フェラチオ", "イラマチオ", "自慰", "胸自慰", "オナフェラ"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@実行判定_オナフェラ
#DIM DYNAMIC 実効値

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_フェラ
実効値 += RESULT

;30以上で実行
CALL 実効値判定, 実効値, 30

RETURN RESULT

@COM_オナフェラ
#DIM DYNAMIC 増加量

PRINTL オナフェラ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(500)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_自慰(200), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30


SOURCE:性行動 = 800
SOURCE:達成 = 150
SOURCE:屈従 = 700
SOURCE:逸脱 = 200
SOURCE:反感 = 200
SOURCE:不潔 = 80

IF TALENT:去勢済み
	SOURCE:快A = 80
ELSE
	SOURCE:快C = 80
ENDIF
SOURCE:情愛 = 100
SOURCE:性行動 = 1000
SOURCE:達成 = 300
SOURCE:恐怖 = 50
SOURCE:中毒 = 100
SOURCE:露出 = 500
SOURCE:屈従 = 1500
SOURCE:逸脱 = 500
SOURCE:反感 = 500
SOURCE:不潔 = 150
;バイブ、Ｖ異物
IF TEQUIP:バイブ || TEQUIP:Ｖ異物
	SOURCE:快V += 100
	SOURCE:苦痛 += 50
	SOURCE:液体 += 100
	SOURCE:屈従 += 100
	SOURCE:反感 += 100
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;アナルバイブ、Ａ異物
IF TEQUIP:アナルバイブ || TEQUIP:Ａ異物
	SOURCE:快A += 100
	SOURCE:苦痛 += 150
	SOURCE:液体 += 30
	SOURCE:屈従 += 150
	SOURCE:反感 += 150
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;オナホ
IF TEQUIP:オナホール
	SOURCE:快C += 100
	SOURCE:液体 += 30
ENDIF

;装飾効果加算
SOURCE:快C += 装飾効果加算量(SOURCE:快C, TARGET, "陰核")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:快C, TARGET, "陰核")
;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 4
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 4, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 4, 3

CALL EXPUP, "フェラ経験", 1
CALL EXPUP, "自慰経験", 2

;奴隷が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

IF !避妊具(PLAYER)
	CALL 魔力回復, 5
	CALL 獣人催淫, 1
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
