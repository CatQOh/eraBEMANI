@USERCOM_ABLE_体内放尿させる, ARG
#FUNCTION
コマンド実行条件 += "小スカMODが必要/"
SIF !MOD:SCAT2
	COMRESULT = 0
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
コマンド実行条件 += "対象にペニスが必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
コマンド実行条件 += "尿意ゲージ5000以上必要/"
SIF BASE:尿意 < 5000
	COMRESULT = 0
コマンド実行条件 += "避妊具使用時は実行不可/"
SIF 避妊具(TARGET)
	COMRESULT = 0
;従順5以上でないとダメ
コマンド実行条件 += "対象の従順LV5以上必要/"
コマンド実行条件 += "　[恥薄い]ならLV3以上/"
SIF (ABL:従順 < 5 && !TALENT:恥薄い) || ABL:従順 < 3
	COMRESULT = 0

;前回と今回の調教者が同じ
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "性交系から派生可能/"
IF TFLAG:前コマンド実行者 == PLAYER
	;対応する体位か連続実行なら派生可能
	SELECTCASE PREVCOMS
		CASE "正常位させる", "後背位させる", "正常位アナルさせる", "後背位アナルさせる",;暫定的に派生コマンドからは派生しないようにしてみる
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@COM_体内放尿させる
#DIM DYNAMIC 増加量
#DIM DYNAMIC 射精先

SELECTCASE PREVCOMS
	CASE "正常位させる", "後背位させる"
		PRINTL 膣内放尿させる
		TFLAG:奴隷挿入箇所 = 1
	CASE "正常位アナルさせる", "後背位アナルさせる"
		PRINTL 腸内放尿させる
		TFLAG:奴隷挿入箇所 = 2
ENDSELECT
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 射精ゲージ計算_実行者受け(400)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += 増加量/10*4*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 200

SOURCE:快C = 200
SOURCE:恐怖 = 300
SOURCE:性行動 = 400
SOURCE:屈従 = 500
SOURCE:逸脱 = 1500
SOURCE:反感 = 1000
SOURCE:露出 = 1000
SOURCE:恭順 = 200
SOURCE:不潔 = 3000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	SOURCE:反感 += 1000

SELECTCASE PREVCOMS
	CASE "正常位", "後背位", "騎乗位", "対面座位", "背面座位", "下着ずらし挿入"
		CALL 特殊ソース処理_Ｖ
		CALL 特殊ソース処理_セックス
	CASE "正常位アナル", "後背位アナル", "騎乗位アナル", "対面座位アナル", "背面座位アナル", "下着ずらし挿入アナル"
		CALL 特殊ソース処理_Ａ
		CALL 特殊ソース処理_セックス
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
SELECTCASE PREVCOMS
	CASE "正常位させる", "後背位させる"
		SETBIT STAIN:PLAYER:膣内, 7
		;奴隷の膣内⇔調教者のペニスの汚れが移動
		STAIN:PLAYER:膣内 |= STAIN:ペニス
		STAIN:ペニス |= STAIN:PLAYER:膣内
	CASE "正常位アナルさせる", "後背位アナルさせる"
		SETBIT STAIN:PLAYER:アナル, 7
		;奴隷の膣内⇔調教者のペニスの汚れが移動
		STAIN:PLAYER:アナル |= STAIN:ペニス
		STAIN:ペニス |= STAIN:PLAYER:アナル
	;CASE "臍挿入"
	;CASE "乳管挿入"
	;CASE "尿道挿入"
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 4, 2

SELECTCASE PREVCOMS
	CASE "正常位させる", "後背位させる"
		CALL EXPUP, "Ｖ経験", 1
	CASE "正常位アナルさせる", "後背位アナルさせる"
		CALL EXPUP, "Ａ経験", 1
ENDSELECT
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

CALL 魔力回復, 5, PLAYER

CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

経験回数:PLAYER:(NOF(TARGET)) += 1
経験回数:TARGET:(NOF(PLAYER)) += 1

RETURN 1
