;-------------------------------------------------
;3P
;セックス系派生コマンド
;-------------------------------------------------
@USERCOM_ABLE_3P, ARG
#FUNCTION
#DIM DYNAMIC 本数

;3P実行判定
;助手がいないとダメ
コマンド実行条件 += "助手が必要/"
SIF ASSI < 1
	COMRESULT = 0
;処女で、従順4以下・レズっ気4以下のASSIの場合3Pはできない（男もしくはサドならＯＫ）
コマンド実行条件 += "対象が処女で助手が女性の場合、助手に[サド]or従順とレズっ気LV4以上が必要/"
コマンド実行条件 += @"対象が潤滑{PALAMLV:2}未満の場合、助手に[サド]or従順LV4以上が必要/"
IF ASSI < 1
	COMRESULT = 0
ELSE
	IF TALENT:処女
		SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && !TALENT:ASSI:サド && SEX(ASSI) == 2
			COMRESULT = 0
	;潤滑不足で、従順3以下のASSIの場合3Pはできない（サドならＯＫ）
	ELSEIF PALAM:潤滑 < PALAMLV:2
		SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
			COMRESULT = 0
	ENDIF
ENDIF

本数 = 0
;MASTERがペニス持ち
コマンド実行条件 += "主人に男性器が必要(ペニスバンドで代用可能)/"
SIF PENIS(MASTER) || TEQUIP:MASTER:ペニスバンド
	本数++
;ASSIがペニス持ち
コマンド実行条件 += "助手に男性器が必要(ペニスバンドで代用可能)/"
SIF ASSI > 0 && (PENIS(ASSI) || TEQUIP:ASSI:ペニスバンド) && !Ｐ使用不可(ASSI)
	本数++
;ペニスバンドとあわせて二本ないとだめ
SIF 本数 < 2
	COMRESULT = 0
;膣使用中はダメ
コマンド実行条件 += "対象がV使用中、かつA使用中は実行できない/"
SIF Ｖ使用中(TARGET) && Ａ使用中(TARGET)
	COMRESULT = 0
;主人、調教対象、助手のいずれかに小人体型がいる場合、主人が禁断の知識を持っていないとダメ
コマンド実行条件 += "主人、助手、対象のいずれかが[小人体型]の場合、主人に禁断の知識が必要/"
IF TALENT:MASTER:小人体型 || (ASSI > 0 && TALENT:ASSI:小人体型) || TALENT:小人体型
	SIF !TALENT:MASTER:禁断の知識
		COMRESULT = 0
ENDIF
;一定のＡ経験が必要
コマンド実行条件 += @"Ａを使う場合は対象のＡ経験{EXPLV:3}以上が必要/"
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "助手が[アニマル]の場合は実行できない/"
SIF ASSI > 0 && TALENT:ASSI:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;以下のコマンド実行時
コマンド実行条件 += "3P、性交系コマンド、フェラ系コマンドから派生可能/"
SELECTCASE PREVCOMS
	;3Pなら続行可能
	CASE "3P"
		RETURNF COMRESULT
	;既に膣か尻か口に入ってると派生可能
	CASE "正常位", "後背位", "騎乗位", "背面座位", "下着ずらし挿入", "正常位アナル", "後背位アナル", "騎乗位アナル", "背面座位アナル", "下着ずらし挿入アナル", "フェラチオ", "イラマチオ"
		RETURNF COMRESULT
ENDSELECT

COMRESULT = 0
RETURNF 0

@COM_3P
#DIM DYNAMIC 一本目
#DIM DYNAMIC 二本目
#DIM DYNAMIC 増加量
#DIM DYNAMIC 射精先, 3
#DIM DYNAMIC プレイヤー交代

;前コマンドとPLAYERが同じなら自動でPLAYERを交代する
IF TFLAG:前コマンド実行者 == PLAYER && PREVCOMS != "3P"
	IF PLAYER == MASTER
		PLAYER = ASSI
	ELSEIF PLAYER == ASSI
		PLAYER = MASTER
	ENDIF
	プレイヤー交代 = 1
ENDIF

;-------------------------------------------------
;挿入部位管理
;-------------------------------------------------
;1 = ヴァギナ
;2 = アナル
;3 = 口
;3Pは今回の実行者(PLAYER)と前回の実行者(TFLAG:前コマンド実行者)が不一致でないと発生しない
;派生条件は正常位等の各コマンド内で行っているので、膣に挿入しながら膣に挿入して3Pなんてことにはならないはず
;前回のコマンドが3Pなら挿入部位は変わらず
IF PREVCOMS != "3P"
	SELECTCASE PREVCOMS
		CASE "正常位", "後背位", "騎乗位", "背面座位", "下着ずらし挿入"
			一本目 = 1
		CASE "正常位アナル", "後背位アナル", "騎乗位アナル", "背面座位アナル", "下着ずらし挿入アナル"
			一本目 = 2
		CASE "フェラチオ", "イラマチオ"
			一本目 = 3
	ENDSELECT
	;対象に穴が2つしか無ければ確定
	IF !VAGINA(TARGET)
		SELECTCASE 一本目
			CASE 2
				二本目 = 3
			CASE 3
				二本目 = 2
		ENDSELECT
	ELSE
		;どこに挿れるか選ぶ
		PRINTFORM \@ PLAYER == MASTER ? %CALLNAME:ASSI% # %CALLNAME:MASTER% \@のペニスは%CALLNAME:TARGET%の
		SELECTCASE 一本目
			CASE 1
				PRINT 膣に挿れられています。
			CASE 2
				PRINT アナルに挿れられています。
			CASE 3
				PRINT 口に挿れられています。
		ENDSELECT
		PRINTFORML どこを犯しますか？
		SIF 一本目 != 1
			PRINTL [1] - 膣穴
		SIF 一本目 != 2 && EXP:Ａ経験 >= EXPLV:3
			PRINTL [2] - アナル
		SIF 一本目 != 3
			PRINTL [3] - 口
		DO
			SIF ISSKIP()
				BREAK
			INPUT
			SIF 一本目 == RESULT
				CONTINUE
			SIF RESULT == 2 && EXP:Ａ経験 >= EXPLV:3
				CONTINUE
		LOOP LOOPRES(1, 2, 3)
		二本目 = RESULT
	ENDIF

	IF PLAYER == MASTER
		TFLAG:助手挿入箇所 = 一本目
		TFLAG:主人挿入箇所 = 二本目
	ELSEIF PLAYER == ASSI
		TFLAG:主人挿入箇所 = 一本目
		TFLAG:助手挿入箇所 = 二本目
	ENDIF
ENDIF

PRINTL 3P
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
SIF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	TFLAG:膣コマンド = 1

SIF FALLTYPE(TARGET) == 1 && TALENT:処女 && TFLAG:主人挿入箇所 == 1
	TFLAG:恋慕処女 = 1

;童貞喪失
SELECTCASE TFLAG:主人挿入箇所
	CASE 1, 2
		IF TALENT:MASTER:童貞
			TALENT:MASTER:童貞 = 0
			IF TALENT:MASTER:性転換済
				CSTR:MASTER:性転換童貞 = %NAME:TARGET%
			ELSE
				CSTR:MASTER:童貞 = %NAME:TARGET%
			ENDIF
		ENDIF
ENDSELECT

SELECTCASE TFLAG:助手挿入箇所
	CASE 1, 2
		IF TALENT:ASSI:童貞
			TALENT:ASSI:童貞 = 0
			IF TALENT:ASSI:性転換済
				CSTR:ASSI:性転換童貞 = %NAME:TARGET%
			ELSE
				CSTR:ASSI:童貞 = %NAME:TARGET%
			ENDIF
		ENDIF
ENDSELECT

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 1000

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;VとAに挿入時、PALAM:潤滑をみる
SELECTCASE TFLAG:主人挿入箇所
	CASE 1, 2
		増加量 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(増加量/5)
		;子供は締まりが良いので増える
		SIF TALENT:体格 <= 2
			TIMES 増加量 , 1.20
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:MASTER:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:MASTER:Ｃ感覚-1)
ENDSELECT

;EXP:Ｖ経験をみる
;処女だと増える
SIF TFLAG:主人挿入箇所 == 1 && TALENT:処女
	TIMES 増加量 , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:賢者タイム > 0
	増加量 /= 20

BASE:MASTER:射精 += 増加量

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
増加量 = 1000

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;VとAに挿入時、PALAM:潤滑をみる
SELECTCASE TFLAG:助手挿入箇所
	CASE 1, 2
		増加量 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(増加量/5)
		;子供は締まりが良いので増える
		SIF TALENT:体格 <= 2
			TIMES 増加量 , 1.20
ENDSELECT

;助手のABL:Ｃ感覚をみる
SELECTCASE ABL:ASSI:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:ASSI:Ｃ感覚-1)
ENDSELECT

;EXP:Ｖ経験をみる
;処女だと増える
SIF TFLAG:助手挿入箇所 == 1 && TALENT:処女
	TIMES 増加量 , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:賢者タイム > 0
	増加量 /= 20

BASE:ASSI:射精 += 増加量

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 600

;ABL:主人の技巧をみる
増加量 += (増加量/5)*ABL:MASTER:技巧

;ABL:助手の技巧をみる
増加量 += (増加量/5)*ABL:ASSI:技巧

;ABL:欲望をみる
増加量 += (増加量/5)*ABL:欲望

;奴隷のABL:Ｖ感覚、Ａ感覚をみる
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	増加量 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(増加量/5)

	SELECTCASE ABL:Ｖ感覚
		CASE 0
			TIMES 増加量 , 0.50
		CASE 1
			TIMES 増加量 , 0.80
		CASE 2
			TIMES 増加量 , 1.00
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.00
		CASEELSE
			増加量 += (増加量/2)*ABL:Ｖ感覚
	ENDSELECT
ENDIF
IF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	;PALAM:潤滑をみる
	SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
		CASE 0
			TIMES 増加量 , 0.20
		CASE 1
			TIMES 増加量 , 0.50
		CASE 2
			TIMES 増加量 , 1.00
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.50
	ENDSELECT

	SELECTCASE ABL:Ａ感覚
		CASE 0
			TIMES 増加量 , 0.50
		CASE 1
			TIMES 増加量 , 0.80
		CASE 2
			TIMES 増加量 , 1.00
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.00
		CASEELSE
			増加量 += (増加量/2)*ABL:Ａ感覚
	ENDSELECT
ENDIF

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/2)*ABL:マゾっ気

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	増加量 /= 20

BASE:射精 += 増加量

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 300

SOURCE:情愛 = 300
SOURCE:性行動 = 700
SOURCE:中毒 = 500
SOURCE:恐怖 = 300
SOURCE:屈従 = 500
SOURCE:逸脱 = 500
SOURCE:反感 = 500

;挿入している部位によって、ソースの数値が変化する
;Vに挿入している場合
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	SOURCE:快V += 400
	SOURCE:性行動 += 100
	SOURCE:苦痛 += 500
ENDIF
;Aに挿入している場合
IF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	SOURCE:快A += 400
	SOURCE:性行動 += 100
	SOURCE:苦痛 += 800
ENDIF
;口に挿入している場合
IF TFLAG:主人挿入箇所 == 3 || TFLAG:助手挿入箇所 == 3
	SOURCE:性行動 += 420
	SOURCE:達成 += 150
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 1
	SOURCE:反感 += 1000

;Vに挿入している場合
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女
		SOURCE:苦痛 += 5500
		SOURCE:反感 += 3000
	ENDIF
	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:再生処女
		SOURCE:苦痛 += 5500
		SOURCE:反感 += 1000
	ENDIF
	CALL 特殊ソース処理_Ｖ
ENDIF

;Aに挿入している場合
SIF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	CALL 特殊ソース処理_Ａ

CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック（主人）
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		射精先 = 1
	CASE 2
		射精先 = 2
	CASE 3
		射精先 = 4
ENDSELECT
SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		射精先:1 = 1
	CASE 2
		射精先:1 = 2
	CASE 3
		射精先:1 = 4
ENDSELECT
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	射精先:2 = 4
ELSEIF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	射精先:2 = 5
ELSE
	射精先:2 = 0
ENDIF

CALL SAMEN_CHECK_M, 射精先
CALL SAMEN_CHECK_A, 射精先:1
CALL SAMEN_CHECK_T, 射精先:2

CALL SAMEN_SHOOT, 射精先, NOWEX:MASTER:射精, MASTER
CALL SAMEN_SHOOT, 射精先:1, NOWEX:ASSI:射精, ASSI
CALL SAMEN_SHOOT_T, 射精先:2, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔主人のペニスの汚れが移動
		IF PENIS(MASTER)
			STAIN:膣内 |= STAIN:MASTER:ペニス
			STAIN:MASTER:ペニス |= STAIN:膣内
		ENDIF
	CASE 2
		;奴隷のアナル⇔主人のペニスの汚れが移動
		IF PENIS(MASTER)
			SETBIT STAIN:MASTER:ペニス, 3
			STAIN:アナル |= STAIN:MASTER:ペニス
			STAIN:MASTER:ペニス |= STAIN:アナル
		ENDIF
ENDSELECT

SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔助手のペニスの汚れが移動
		IF PENIS(ASSI)
			STAIN:膣内 |= STAIN:ASSI:ペニス
			STAIN:ASSI:ペニス |= STAIN:膣内
		ENDIF
	CASE 2
		;奴隷のアナル⇔助手のペニスの汚れが移動
		IF PENIS(ASSI)
			SETBIT STAIN:ASSI:ペニス, 3
			STAIN:アナル |= STAIN:ASSI:ペニス
			STAIN:ASSI:ペニス |= STAIN:アナル
		ENDIF
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛経験
IF SEX(TARGET) == SEX(MASTER) && SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			CALL EXPUP, "ゲイ経験", 6
			CALL EXPUP, "ゲイ経験", 3, MASTER
			CALL EXPUP, "ゲイ経験", 3, ASSI
		CASE 2
			CALL EXPUP, "レズ経験", 6
			CALL EXPUP, "レズ経験", 3, MASTER
			CALL EXPUP, "レズ経験", 3, ASSI
	ENDSELECT
ELSEIF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			CALL EXPUP, "ゲイ経験", 3
			SIF SEX(MASTER) == 1
				CALL EXPUP, "ゲイ経験", 2, MASTER
			SIF SEX(ASSI) == 1
				CALL EXPUP, "ゲイ経験", 2, ASSI
		CASE 2
			CALL EXPUP, "レズ経験", 3
			SIF SEX(MASTER) == 2
				CALL EXPUP, "レズ経験", 2, MASTER
			SIF SEX(ASSI) == 2
				CALL EXPUP, "レズ経験", 2, ASSI
	ENDSELECT
ENDIF

CALL EXPUP, "主人調教経験", 2
CALL EXPUP, "助手調教経験", 2

;Vに挿入している場合
SIF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	CALL EXPUP, "Ｖ経験", 2

;Aに挿入している場合
SIF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	CALL EXPUP, "Ａ経験", 2

;性交経験
;VとAに挿入している場合（口に挿入していない場合）
IF TFLAG:主人挿入箇所 != 3 && TFLAG:助手挿入箇所 != 3
	CALL EXPUP, "性交経験", 2
	CALL EXPUP, "性交経験", 1, MASTER
	CALL EXPUP, "性交経験", 1, ASSI
	経験回数:MASTER:(NOF(TARGET)) += 1
	経験回数:ASSI:(NOF(TARGET)) += 1
	経験回数:TARGET:(NOF(MASTER)) += 1
	経験回数:TARGET:(NOF(ASSI)) += 1
;主人だけ挿入している場合
ELSEIF TFLAG:主人挿入箇所 != 3
	CALL EXPUP, "性交経験", 1
	CALL EXPUP, "性交経験", 1, MASTER
	経験回数:MASTER:(NOF(TARGET)) += 1
	経験回数:TARGET:(NOF(MASTER)) += 1
;助手だけ挿入している場合
ELSE
	CALL EXPUP, "性交経験", 1
	CALL EXPUP, "性交経験", 1, ASSI
	経験回数:ASSI:(NOF(TARGET)) += 1
	経験回数:TARGET:(NOF(ASSI)) += 1
ENDIF

;魔力回復
SIF !避妊具(MASTER) && PENIS(MASTER)
	CALL 魔力回復, 3
SIF !避妊具(ASSI) && PENIS(ASSI)
	CALL 魔力回復, 3

;PLAYER交代していた場合は元に戻す
IF プレイヤー交代
	IF PLAYER == MASTER
		PLAYER = ASSI
	ELSEIF PLAYER == ASSI
		PLAYER = MASTER
	ENDIF
ENDIF

RETURN 1
