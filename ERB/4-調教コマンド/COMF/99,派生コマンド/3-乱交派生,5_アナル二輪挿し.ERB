;-------------------------------------------------
;アナル二輪挿し
;セックス系派生コマンド
;-------------------------------------------------
@USERCOM_ABLE_アナル二輪挿し
#FUNCTION
#DIM DYNAMIC 本数

;助手がいないとダメ
コマンド実行条件 += "助手が必要/"
SIF ASSI < 1
	COMRESULT = 0
コマンド実行条件 += @"対象が潤滑{PALAMLV:2}未満の場合、助手に[サド]or従順LV4以上が必要/"
;潤滑不足で、従順3以下のASSIの場合二輪挿しはできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2
	SIF ASSI > 0 && ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF

本数 = 0
;TMASTERがペニス持ち
コマンド実行条件 += "主人に男性器が必要(ペニスバンドで代用可能)/"
SIF PENIS(TMASTER) || TEQUIP:TMASTER:ペニスバンド
	本数++
;ASSIがペニス持ち
コマンド実行条件 += "助手に男性器が必要(ペニスバンドで代用可能)/"
SIF ASSI > 0 && (PENIS(ASSI) || TEQUIP:ASSI:ペニスバンド) && !Ｐ使用不可(ASSI)
	本数++
;ペニスバンドとあわせて二本ないとだめ
SIF 本数 < 2
	COMRESULT = 0
;膣使用中はダメ
コマンド実行条件 += "対象がA使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
;主人、調教対象、助手のいずれかに小人体型がいる場合、主人が禁断の知識を持っていないとダメ
コマンド実行条件 += "主人、助手、対象のいずれかが[小人体型]の場合、主人に禁断の知識が必要/"
IF TALENT:TMASTER:小人体型 || (ASSI > 0 && TALENT:ASSI:小人体型) || TALENT:小人体型
	SIF !TALENT:TMASTER:禁断の知識
		COMRESULT = 0
ENDIF
;一定のＡ拡張経験が必要
コマンド実行条件 += @"対象のＡ拡張経験50以上が必要/"
SIF EXP:Ａ拡張経験 < 50
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "助手が[アニマル]の場合は実行できない/"
SIF ASSI > 0 && TALENT:ASSI:アニマル
	COMRESULT = 0

;以下のコマンド実行時
コマンド実行条件 += "性交系コマンドから派生可能/"
SELECTCASE PREVCOMS
	CASE "正常位アナル", "後背位アナル", "騎乗位アナル", "背面座位アナル", "対面座位アナル", "下着ずらし挿入アナル"
		RETURNF COMRESULT
ENDSELECT

COMRESULT = 0
RETURNF 0

@COM_アナル二輪挿し
#DIM DYNAMIC 増加量

PRINTL アナル二輪挿し
CALL KOJO_MESSAGE_COM

;童貞喪失
IF TALENT:TMASTER:童貞
	TALENT:TMASTER:童貞 = 0
	IF TALENT:TMASTER:性転換済
		CSTR:TMASTER:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:TMASTER:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

IF TALENT:ASSI:童貞
	TALENT:ASSI:童貞 = 0
	IF TALENT:ASSI:性転換済
		CSTR:ASSI:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:ASSI:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 1000

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(増加量/5)
;子供は締まりが良いので増える
SIF TALENT:体格 <= 2
	TIMES 増加量 , 1.20

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:TMASTER:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:TMASTER:Ｃ感覚-1)
ENDSELECT

;EXP:Ａ拡張経験をみる
増加量 += 増加量/10*GETEXPLV(EXP:Ａ拡張経験, 10)

;装飾効果加算
増加量 += 装飾効果加算量(増加量, TMASTER, "陰茎")

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:TMASTER:賢者タイム > 0
	増加量 /= 20

BASE:TMASTER:射精 += 増加量

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
増加量 = 1000

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-2)*(増加量/5)
;子供は締まりが良いので増える
SIF TALENT:体格 <= 2
	TIMES 増加量 , 1.20

;助手のABL:Ｃ感覚をみる
SELECTCASE ABL:ASSI:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:ASSI:Ｃ感覚-1)
ENDSELECT

;EXP:Ａ拡張経験をみる
増加量 += 増加量/10*GETEXPLV(EXP:Ａ拡張経験, 10)

;装飾効果加算
増加量 += 装飾効果加算量(増加量, ASSI, "陰茎")

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:賢者タイム > 0
	増加量 /= 20

BASE:ASSI:射精 += 増加量

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 400

;ABL:主人の技巧をみる
増加量 += (増加量/5)*ABL:TMASTER:技巧

;ABL:助手の技巧をみる
増加量 += (増加量/5)*ABL:ASSI:技巧

;ABL:欲望をみる
増加量 += (増加量/5)*ABL:欲望

;ABL:Ｖ感覚を見る
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 増加量 , 0.20
	CASE 1
		TIMES 増加量 , 0.50
	CASE 2
		TIMES 増加量 , 0.80
	CASE 3
		TIMES 増加量 , 1.20
	CASE 4
		TIMES 増加量 , 1.50
	CASEELSE
		増加量 += (増加量/2)*ABL:Ａ感覚
ENDSELECT

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES 増加量 , 0.20
	CASE 1
		TIMES 増加量 , 0.50
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.50
ENDSELECT

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/2)*ABL:マゾっ気

;EXP:Ａ拡張経験をみる
増加量 += 増加量/5*GETEXPLV(EXP:Ａ拡張経験, 10)

;装飾効果加算
増加量 += 装飾効果加算量(増加量, TMASTER, "陰茎")
増加量 += 装飾効果加算量(増加量, ASSI, "陰茎")

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	増加量 /= 20

BASE:射精 += 増加量

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 250
LOSEBASE:気力 += 350

SOURCE:快A += 600
SOURCE:情愛 = 200
SOURCE:性行動 = 1000
SOURCE:中毒 = 700
SOURCE:恐怖 = 1000
SOURCE:苦痛 = 1500
SOURCE:屈従 = 1000
SOURCE:逸脱 = 1500
SOURCE:反感 = 1500

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 1
	SOURCE:反感 += 1000

;装飾効果加算
SOURCE:快A += 装飾効果加算量(SOURCE:快A, TMASTER, "陰茎")
SOURCE:快A += 装飾効果加算量(SOURCE:快A, ASSI, "陰茎")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:苦痛, TMASTER, "陰茎")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:苦痛, ASSI, "陰茎")

CALL 特殊ソース処理_Ａ

CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_M, 2
CALL SAMEN_CHECK_A, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:TMASTER:射精, TMASTER
CALL SAMEN_SHOOT, 2, NOWEX:ASSI:射精, ASSI
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔主人のペニスの汚れが移動
IF PENIS(TMASTER)
	STAIN:アナル |= STAIN:TMASTER:ペニス
	STAIN:TMASTER:ペニス |= STAIN:アナル
ENDIF

;奴隷のアナル⇔助手のペニスの汚れが移動
IF PENIS(ASSI)
	STAIN:アナル |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:アナル
ENDIF

;主人のペニス⇔助手のペニスの汚れが移動
IF PENIS(TMASTER) && PENIS(ASSI)
	STAIN:TMASTER:ペニス |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:TMASTER:ペニス
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛経験
IF SEX(TARGET) == SEX(TMASTER) && SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			CALL EXPUP, "ゲイ経験", 6
			CALL EXPUP, "ゲイ経験", 3, TMASTER
			CALL EXPUP, "ゲイ経験", 3, ASSI
		CASE 2
			CALL EXPUP, "レズ経験", 6
			CALL EXPUP, "レズ経験", 3, TMASTER
			CALL EXPUP, "レズ経験", 3, ASSI
	ENDSELECT
ELSEIF SEX(TARGET) == SEX(TMASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			CALL EXPUP, "ゲイ経験", 3
			SIF SEX(TMASTER) == 1
				CALL EXPUP, "ゲイ経験", 2, TMASTER
			SIF SEX(ASSI) == 1
				CALL EXPUP, "ゲイ経験", 2, ASSI
		CASE 2
			CALL EXPUP, "レズ経験", 3
			SIF SEX(TMASTER) == 2
				CALL EXPUP, "レズ経験", 2, TMASTER
			SIF SEX(ASSI) == 2
				CALL EXPUP, "レズ経験", 2, ASSI
	ENDSELECT
ENDIF

IF TMASTER == MASTER
	CALL EXPUP, "主人調教経験", 2
	CALL EXPUP, "助手調教経験", 2
ELSE
	CALL EXPUP, "助手調教経験", 4
ENDIF

CALL EXPUP, "Ａ経験", 3
CALL EXPUP, "Ａ拡張経験", 2

CALL EXPUP, "性交経験", 2
CALL EXPUP, "性交経験", 1, TMASTER
CALL EXPUP, "性交経験", 1, ASSI
経験回数:TMASTER:(NOF(TARGET)) += 1
経験回数:ASSI:(NOF(TARGET)) += 1
経験回数:TARGET:(NOF(TMASTER)) += 1
経験回数:TARGET:(NOF(ASSI)) += 1

;魔力回復
SIF !避妊具(TMASTER) && PENIS(TMASTER)
	CALL 魔力回復, 3
SIF !避妊具(ASSI) && PENIS(ASSI)
	CALL 魔力回復, 3

RETURN 1
