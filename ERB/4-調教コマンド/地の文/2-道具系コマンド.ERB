;-------------------------------------------------
;ローター
;-------------------------------------------------
@MESSAGE_COM_ローター
;着衣時
IF OVER_GENITAL(TARGET) != ""
	;ペニス持ち
	IF PENIS(TARGET)
		IF TEQUIP:睡眠
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%を起こさないように弱めの振動でローターを押し当てた……
		ELSEIF CFLAG:死亡
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の股間に震えるローターを押し当てた……
		ELSEIF PALAM:欲情 >= PALAMLV:3
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の硬く膨らんだペニスに震えるローターを押し当てた……
		ELSE
			PRINTFORML %OVER_GENITAL(TARGET)%の中で大人しくしているペニスを奮い勃たせるように、振動するローターを陰部に押し付けた……
		ENDIF
	ELSEIF VAGINA(TARGET)
		IF CFLAG:死亡
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の股間にローターを押し当てた……
		ELSEIF PALAM:潤滑 >= PALAMLV:3
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の股間にローターを押し当てると、\@ TEQUIP:睡眠 ? 眠っているにも関わらず #\@そこは溢れ出る愛液で湿りを帯びていた……
		ELSE
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%\@ TEQUIP:睡眠 ? を起こさないように弱めの振動で # の陰部を \@ローターで責め立てた……
		ENDIF
	ELSEIF TALENT:去勢済み
		IF CFLAG:死亡
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の股間にローターを押し当てるた……
		ELSE
			PRINTFORML %OVER_GENITAL(TARGET)%の上から%CALLNAME:TARGET%の股間にローターを押し当てると、%CALLNAME:TARGET%はむず痒そうにしている……
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SELECTCASE SEX(TARGET)
		;男
		CASE 1
			IF TALENT:去勢済み
				PRINTL 尿道を中心に、去勢痕をローターでなぞった……
			ELSEIF CFLAG:死亡
				PRINTL 萎縮した冷たいペニスに震えるローターを押し当てた……
			;欲情のPALAMLV3以上
			ELSEIF PALAM:欲情 >= PALAMLV:3
				PRINTL 硬くそそり勃ったペニスに震えるローターを押し当てた……
			ELSE
				PRINTL ペニスに震えるローターを押し当てた……
			ENDIF
		;女
		CASE 2
			IF CFLAG:死亡
				PRINTL 冷たい女性機に震えるローターを押し当てた……
			ELSEIF TALENT:ふたなり
				;潤滑のPALAMLV3以上
				IF PALAM:潤滑 >= PALAMLV:3
					PRINTL ぬかるんだ秘唇に震えるローターを押し当てた……
				ELSE
					PRINTL 秘唇に震えるローターを押し当てた……
				ENDIF
			ELSE
				;欲情のPALAMLV3以上
				IF PALAM:欲情 >= PALAMLV:3
					PRINTL 硬く尖ったクリトリスに震えるローターを押し当てた……
				ELSE
					PRINTL クリトリスに震えるローターを押し当てた……
				ENDIF
			ENDIF
	ENDSELECT
	SIF TEQUIP:お風呂場
		PRINTL 浴槽内で震えるローターが鈍い音を響かせている……
ENDIF
IF 上級売春中
	SELECTCASE BR:方針
		CASE 1
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のご奉仕にご満悦のようだ……
		CASEELSE
			PRINTFORML しかし%CALLNAME:TARGET%には物足りないようだ……
	ENDSELECT
ENDIF
WAIT

;-------------------------------------------------
;電気按摩
;-------------------------------------------------
@MESSAGE_COM_電気按摩
SIF OVER_GENITAL(TARGET) != ""
	PRINTFORM %OVER_GENITAL(TARGET)%の上から
SIF TEQUIP:お風呂場
	PRINT 湯船の中で
PRINTFORML %CALLNAME:TARGET%の\@ SEX(TARGET) == 1 ? 股間 # 秘所 \@に振動している電気按摩を押しつけた
PRINTFORM %CALLNAME:TARGET%は
IF TEQUIP:睡眠
	PRINTFORML 鈍い振動音にも目を覚ますことなく、体で正直な反応を見せている……
ELSEIF CFLAG:死亡
	PRINTFORML 何の反応も無く、鈍い振動音だけが虚しく響いている……
ELSEIF TALENT:去勢済み && !VAGINA(TARGET)
	PRINTFORML 既に快感を得られなくなった秘部に対する振動で切なそうな声を上げている……
ELSEIF TEQUIP:ゲームセンター
	PRINTFORML 外に誰かが居るかもしれないということも忘れて息を荒げている……
ELSE
	;Ｃ感覚LV3未満
	IF ABL:Ｃ感覚 < 3
		IF TALENT:アニマル
			PRINTL 半ば苦痛になっているのか、痛がるような鳴き声を上げている……
		ELSE
			;欲情のPALAMLV3未満
			IF PALAM:欲情 < PALAMLV:3
				PRINTL とてもくすぐったがいらしく、唇を噛み締めて震えている……
			;欲情のPALAMLV3以上
			ELSE
				PRINTL 、……何も言えなくなっているようだ……
			ENDIF
		ENDIF
	ELSEIF TALENT:アニマル
		;欲情レベル
		SELECTCASE PALAM:欲情
			;LV3未満
			CASE IS < PALAMLV:3
				PRINTL 明らかに快感を得ている様子で、大人しく電気按摩での調教を受け入れている……
			;LV3以上 LV5未満
			CASE PALAMLV:3 TO PALAMLV:5
				PRINTL 舌を出して涎を零しながら、低く唸っている……
			;LV5以上
			CASE IS >= PALAMLV:5
				PRINTL 自ら腹部をさらけ出して前膝を折り曲げ、快感に身を委ねている……
		ENDSELECT
	;陥落済
	ELSEIF CFLAG:陥落 || (TALENT:アニマル && CFLAG:依存度 >= 1500)
		;欲情レベル
		SELECTCASE PALAM:欲情
			;LV3未満
			CASE IS < PALAMLV:3
				IF TALENT:足使用不可
					PRINTFORM %CALLNAME:PLAYER%に身を委ね、
				ELSE
					PRINT 待ちわびたように足を開き、
				ENDIF
				PRINTL 刺激を一身に受け入れている……
			;LV3以上 LV5未満
			CASE PALAMLV:3 TO PALAMLV:5
				PRINTFORML 快楽に涎をたらしながら、\@ TALENT:アニマル ? うめき声を上げた…… # 何度も%CALLNAME:PLAYER%の名を呼んだ…… \@
			;LV5以上
			CASE IS >= PALAMLV:5
				PRINTFORML 欲情に満ちた体を大きく震わせながら快感に浸っている……
		ENDSELECT
	ELSE
		;欲情レベル
		SELECTCASE PALAM:欲情
			;LV3未満
			CASE IS < PALAMLV:3
				PRINTL 強い快感に顔を赤らめている……
			;LV3以上 LV5未満
			CASE PALAMLV:3 TO PALAMLV:5
				PRINTL 惚けた目で快楽を貪っている……
			;LV5以上
			CASE IS >= PALAMLV:5
				PRINTL 今にも絶頂に達してしまいそうな様子だ……
		ENDSELECT
	ENDIF
ENDIF
WAIT

;-------------------------------------------------
;クリキャップ
;-------------------------------------------------
@MESSAGE_COM_クリキャップ
;服着てる
SIF OVER_GENITAL(TARGET) != ""
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
SIF TEQUIP:睡眠
	PRINT 眠っている
;解除時
IF TEQUIP:クリキャップ
	PRINTFORMW %CALLNAME:TARGET%のクリトリスから電動クリキャップを外した
ELSE
	PRINTFORMW %CALLNAME:TARGET%のクリトリスに電動クリキャップを取り付けた
ENDIF

;-------------------------------------------------
;バイブ
;-------------------------------------------------
@MESSAGE_COM_バイブ
;服着てる
SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
;解除時
IF TEQUIP:バイブ
	PRINTFORML %CALLNAME:TARGET%から蠢くバイブをゆっくりと引き抜いた
	IF 上級売春中
		SELECTCASE BR:方針
			CASE 1
				PRINTFORML %CALLNAME:TARGET%には少々乱暴な行為だったようだ。もう少しソフトなプレイを所望している……
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%はまだまだ物足りず、早急な次の行為を望んでいる……
		ENDSELECT
	ELSEIF TEQUIP:睡眠
		PRINTFORML 眠っている%CALLNAME:TARGET%は僅かな呻きを上げ、バイブで緩んだ膣口を無意識に開閉させている……
	ELSEIF CFLAG:死亡
		PRINTFORML 死体となった%CALLNAME:TARGET%には括約筋の制御もできず、バイブで拡げられた膣穴が閉じずに内側を覗かせている……
	ELSE
		IF TALENT:無機物
			PRINTFORML 無機質な%CALLNAME:TARGET%の膣内はバイブの形に拡がっている……
		ELSE
			SELECTCASE EXP:Ｖ経験
				;200以上
				CASE IS >= 200
					SIF !CFLAG:陥落
						GOTO NOTFALL
					PRINTFORML %CALLNAME:TARGET%は、膣内の喪失感に耐えかねて切なそうな顔をしている……
				;150以上
				CASE IS >= 150
					$NOTFALL
					PRINTFORML %CALLNAME:TARGET%の膣内に溜まっていた粘液が掻き出されて滴り、水溜りを作っている……
				;120以上
				CASE IS >= 120
					PRINTFORML バイブを抜かれた%CALLNAME:TARGET%の肉襞からぼたぼたと愛液が滴っている……
				;70以上
				CASE IS >= 70
					PRINTFORML %CALLNAME:TARGET%はもじもじと股を擦り合わせて%CALLNAME:PLAYER%の方を見ている……
				;40以上
				CASE IS >= 40
					PRINTFORML %CALLNAME:TARGET%は熱のこもった息を吐いている……
				;20以上
				CASE IS >= 20
					PRINTFORML %CALLNAME:TARGET%はゆるんだ膣口をひくつかせている……
				;20未満
				CASE IS < 20
					PRINTFORML %CALLNAME:TARGET%はホッとしたような表情をしている……
			ENDSELECT
		ENDIF
	ENDIF
;装着時
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	;ケモ
	IF TALENT:ファーリー
		IF CFLAG:死亡
			PRINT 毛艶が悪くなった
		ELSE
			;潤滑LV5以上
			SIF PALAM:潤滑 >= PALAMLV:5
				PRINT 溢れ出る
			;潤滑LV3以上
			SIF PALAM:潤滑 >= PALAMLV:3
				PRINT 愛液に濡れた
		ENDIF
		PRINT 体毛を掻き分け、牝穴
	;無機物
	ELSEIF TALENT:無機物
		PRINT 硬く無機質な膣
	ELSE
		IF CFLAG:死亡
			PRINT 弛緩して簡単に拡がる
		ELSE
			;潤滑LV5以上
			SIF PALAM:潤滑 >= PALAMLV:5
				PRINT たっぷりと
			;潤滑LV3以上
			SIF PALAM:潤滑 >= PALAMLV:3
				PRINT 粘液をたたえた
		ENDIF
		;小柄体型
		IF TALENT:小柄体型
			SELECTCASE RAND:2
				CASE 0
					PRINT 幼い割れ目
				CASE 1
					PRINT 未成熟な秘裂
			ENDSELECT
		ELSE
			PRINT 秘裂
		ENDIF
	ENDIF
	SELECTCASE アイテム改造:バイブ
		CASE "強力なバイブ"
			PRINTL に改造した強力なバイブの先端をあてがい馴染ませながら、奥へと沈めていく……
		CASE "細いバイブ"
			PRINTL に細く改良したバイブを突き入れる……
		CASE "太いバイブ"
			IF CFLAG:死亡
				PRINTL は、生前なら膣が避けてしまいそうなほどに巨大なバイブを何の抵抗も無く飲み込んだ……
			ELSE
				PRINTL が怪我をしないように、時間をかけて太いバイブを飲み込ませていく……
			ENDIF
		CASEELSE
			SELECTCASE RAND:2
				CASE 0
					PRINTL にバイブの先端をあてがい馴染ませながら、奥へと沈めていく……
				CASE 1
					PRINTL を震える張り型でゆっくりと貫いていく……
			ENDSELECT
	ENDSELECT

	IF TALENT:アニマル
		PRINTFORML %CALLNAME:TARGET%はバイブの挿入を生殖行為と錯覚してか、舌を出して息を荒げて大人しくしている……
	ELSEIF 上級売春中
		IF TALENT:処女
			PRINTFORML %CALLNAME:TARGET%は処女を散らす覚悟で%BRS:店名%に訪れたようだ。その望みは叶った……
		ELSE
			SELECTCASE BR:方針
				CASE 1
					PRINTFORML バイブを手に取る%CALLNAME:PLAYER%を、%CALLNAME:TARGET%は少し驚いた表情で見ている。膣を使うのは予定外らしい……
				CASEELSE
					IF TALENT:マゾ
						PRINTFORML %CALLNAME:TARGET%は求めていた快感を得られて満足気だ……
					ELSEIF TALENT:サド
						PRINTFORML %CALLNAME:TARGET%は何故自分が責められなくてはいけないのかと悪態をついている……
					ELSE
						PRINTFORML %CALLNAME:TARGET%は自分が責められるだけでは満足いかないようだ……
					ENDIF
			ENDSELECT
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		IF TEQUIP:睡眠
			PRINTL 眠っているせいか、体が本能的にそれを受け入れるために愛液を滾らせている……
		ELSEIF CFLAG:死亡
			PRINTL 死んでいるせいで喘ぐことも愛液を滾らせることもなく、肉穴の中でバイブがただ震えているだけだった……
		;処女
		ELSEIF TALENT:処女
			;娼婦 処女ビッチ
			IF TALENT:娼婦
				PRINTL 女性器に入り込んでくるバイブを見つめては、自分が処女である事を忘れているかのような笑みを浮かべている……
			;恋慕or親愛
			ELSEIF TALENT:恋慕 || TALENT:親愛
				PRINTFORML 初めてを散らすのがその淫具であることを悟ると、少し寂しそうな目で%CALLNAME:PLAYER%を見つめた……
			;淫乱
			ELSEIF TALENT:淫乱
				PRINTL 自分が処女である事もお構いなしに、股を開いては挿入をせがんでいる……
			;服従or隷属
			ELSEIF TALENT:服従 || TALENT:隷属
				PRINTFORML 処女を捨てる覚悟はとうに出来ていたかのように、力を抜いて%CALLNAME:PLAYER%に体を委ねた……
			;一線越えない 貞操観念
			ELSEIF TALENT:一線越えない || TALENT:貞操観念
				PRINTL 何かを諦めた表情で目にうっすら涙を浮かべ、振動するバイブを受け入れた……
			;その他
			ELSE
				PRINTL それで処女を散らすことを察すると、喪失の恐怖に怯えながら震えている……
			ENDIF
		ELSE
			IF アイテム改造:バイブ == "太いバイブ"
				SELECTCASE GETEXPLV(EXP:Ｖ拡張経験, 10)
					CASE IS <= 1
						PRINTL 自らの膣には大きすぎるバイブに痛みを訴えている……
					CASE IS <= 3
						PRINTL 膣の拡張にも慣れてきたようだが、未だ痛みを感じるのかうっすらと涙を浮かべている……
					CASE IS <= 5
						PRINTL 膣内を拡げられることにも慣れてきたのか、申し分ない快感を得ているようだ……
					CASEELSE
						PRINTL すっかり拡張しきった膣にめり込む太いバイブの快楽に溺れている……
				ENDSELECT
			ELSE
				;服従or隷属 Ｖ経験70以上
				IF FALLTYPE(TARGET) == 3 && EXP:Ｖ経験 >= 70
					;Ｖ経験
					SELECTCASE EXP:Ｖ経験
						;200以上
						CASE IS >= 200
							PRINTL 挿入への期待だけでガクガクと腰を震わせてイきそうになっている……
						;150以上
						CASE IS >= 150
							PRINTFORML とろけきった目で%CALLNAME:PLAYER%を見つめ、自分から陰唇を擦りつけて受け入れた……
						;120以上
						CASE IS >= 120
							PRINTL 恍惚とした表情で待ちきれないように腰をくねらせている……
						;70以上
						CASE IS >= 70
							PRINTFORML もの欲しそうに目を潤ませ%CALLNAME:PLAYER%を見つめながら、膣内を掻き回される快感に悦んでいる……
					ENDSELECT
				;服従隷属無し もしくはＶ経験70未満
				ELSE
					;Ｖ経験
					SELECTCASE EXP:Ｖ経験
						;40以上
						CASE IS >= 40
							PRINTL 膣内を抉るバイブの感触に陶酔しきった吐息をついて、揺さぶられる膣穴から愛液を止め処なく垂れ流している……
						;20以上
						CASE IS >= 20
							PRINTL 敏感な膣壁をバイブでくすぐられる感触に身をよじらせ愛液を溢れさせている……
						;20未満
						CASE IS < 20
							PRINTL バイブの擦れる感触に陰唇をひくつかせている……
					ENDSELECT
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
WAIT

;-------------------------------------------------
;ペニスバンド
;-------------------------------------------------
@MESSAGE_COM_ペニスバンド
IF FINDCHARA(TEQUIP:ペニスバンド, 1) >= 0
	PRINTFORML %CALLNAME:FINDCHARA(TEQUIP:ペニスバンド, 1)%はペニスバンドを外した
ELSE
	SELECTCASE TFLAG:ペニバン着用者
		CASE MASTER
			PRINTFORM %CALLNAME:MASTER%は
			;着衣中
			IF 下半身着衣(MASTER)
				IF CFLAG:MASTER:挿入可
					SIF TEQUIP:MASTER:下半身下着
						PRINTFORM %CSTR:MASTER:下半身下着%を下ろし、
					PRINTFORML 股間にペニスバンドを取り付けて%OVER_GENITAL(MASTER)%の裾を持ち上げた……
				ELSE
					PRINTFORM %OVER_GENITAL(MASTER)%
					SIF TEQUIP:MASTER:下半身下着
						PRINTFORM と%CSTR:MASTER:下半身下着%
					PRINTL を脱いで、股間にペニスバンドを取り付けた……
				ENDIF
			ELSE
				SIF TEQUIP:MASTER:下半身下着
					PRINTFORM %CSTR:MASTER:下半身下着%を下ろし、
				PRINTFORML 股間にペニスバンドを取り付けた……
			ENDIF
		CASE ASSI
			PRINTFORM %CALLNAME:ASSI%は
			;着衣中
			IF 下半身着衣(ASSI)
				IF CFLAG:ASSI:挿入可
					SIF TEQUIP:ASSI:下半身下着
						PRINTFORM %CSTR:ASSI:下半身下着%を下ろし、
					PRINTFORML 股間にペニスバンドを取り付けて%OVER_GENITAL(ASSI)%の裾を持ち上げた……
				ELSE
					PRINTFORM %OVER_GENITAL(ASSI)%
					SIF TEQUIP:ASSI:下半身下着
						PRINTFORM と%CSTR:ASSI:下半身下着%
					PRINTL を脱いで、股間にペニスバンドを取り付けた……
				ENDIF
			ELSE
				SIF TEQUIP:ASSI:下半身下着
					PRINTFORM %CSTR:ASSI:下半身下着%を下ろし、
				PRINTFORML 股間にペニスバンドを取り付けた……
			ENDIF
		CASE TARGET
			PRINTFORML %CALLNAME:TARGET%にペニスバンドを装着させた
			SIF FALLTYPE(TARGET) == 2
				PRINTFORML %CALLNAME:TARGET%は自らの剛直を撫でながら%CALLNAME:PLAYER%を見つめ、舌なめずりしている……
	ENDSELECT
ENDIF
WAIT

;-------------------------------------------------
;オナホール
;-------------------------------------------------
@MESSAGE_COM_オナホール
;服着てる
SIF OVER_GENITAL(TARGET) != ""
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
;解除時
IF TEQUIP:オナホール
	PRINTFORML %CALLNAME:TARGET%から\@ アイテム改造:オナホール == "電動オナホール" ? 振動する # \@\@ アイテム改造:オナホール == "スライムオナホール" ? ぬっとりと絡みつくスライムでできた # \@オナホールを外した\@ TALENT:ファーリー ? 。樹脂製の筒には先走りに濡れた%CALLNAME:TARGET%の体毛がこべりついている #  \@
	IF TEQUIP:睡眠
		PRINTFORMW %CALLNAME:TARGET%はまだ起きず、眠ったまま先走りにまみれたペニスを反り立たせている……
	;欲情LV3以上
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINTFORMW 勃起したペニスを包み込むものがなくなった%CALLNAME:TARGET%は少し物足りなさそうだ……
	ELSE
		PRINTFORMW まだその気になってないのか、ペニスは勃起しきれていないようだ……
	ENDIF
;装着時
ELSE
	IF アイテム改造:オナホール == "電動オナホール"
		PRINTFORML %CALLNAME:TARGET%のペニスを電動オナホールに挿入した
	ELSEIF アイテム改造:オナホール == "スライムオナホール"
		PRINTFORML %CALLNAME:TARGET%のペニスにスライムでできたオナホールを被せた
	ELSE
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML %CALLNAME:TARGET%のペニスをオナホールに挿入した
			CASE 1
				PRINTFORML %CALLNAME:TARGET%のペニスに柔らかなオナホールを被せた
		ENDSELECT
	ENDIF
	IF TEQUIP:睡眠
		PRINTFORMW 眠っている%CALLNAME:TARGET%は意識は無くとも体は反応し、ペニスをさらに硬くしている……
	ELSEIF TALENT:アニマル
		PRINTFORMW %CALLNAME:TARGET%は自らの生殖器に嵌められた道具が気になり、身をよじらせてペニスの先端を舐めている……
	ELSE
		IF アイテム改造:オナホール == "電動オナホール"
			PRINTFORML 精液を搾り取るように振動するオナホールの刺激に、%CALLNAME:TARGET%は射精を堪えきれない様子だ……
		ELSEIF アイテム改造:オナホール == "スライムオナホール"
			PRINTFORML 意思を持ってるかのように蠢くスライムオナホールの刺激に、%CALLNAME:TARGET%は射精を堪えきれない様子だ……
		ELSE
			SELECTCASE RAND:2
				CASE 0
					PRINTFORMW 敏感なモノを包まれる感覚に%CALLNAME:TARGET%は悶えている……
				CASE 1
					PRINTFORMW オナホールにペニスを飲み込まれた%CALLNAME:TARGET%は気持ち良さそうな表情をしている……
			ENDSELECT
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;搾精コンドーム
;-------------------------------------------------
@MESSAGE_COM_搾精コンドーム
;服着てる
SIF OVER_GENITAL(TARGET) != ""
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
;解除時
IF TEQUIP:搾精コンドーム
	PRINTFORMW %CALLNAME:TARGET%から搾精用のコンドームを取り外した
ELSE
	PRINTFORMW %CALLNAME:TARGET%に搾精用のコンドームを取り付けた
ENDIF

;-------------------------------------------------
;コンドーム
;-------------------------------------------------
@MESSAGE_COM_コンドーム
SELECTCASE TFLAG:避妊具着用者
	CASE MASTER
		;解除時
		IF TEQUIP:MASTER:コンドーム
			PRINTFORML %CALLNAME:MASTER%は自分のペニスにコンドームを被せた
			SIF (FALLTYPE(TARGET) == 2 || (上級売春中 && TALENT:男好き)) && !CFLAG:死亡 && !TEQUIP:睡眠
				PRINTFORML %CALLNAME:TARGET%は少しがっかりした様子だ……
		ELSE
			PRINTFORML %CALLNAME:MASTER%はコンドームを外した
			SIF (FALLTYPE(TARGET) == 2 || (上級売春中 && TALENT:男好き)) && !CFLAG:死亡 && !TEQUIP:睡眠
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の生の射精を求めてうずうずしている……
		ENDIF
	CASE ASSI
		;解除時
		IF TEQUIP:ASSI:コンドーム
			PRINTFORML %CALLNAME:ASSI%は自分のペニスにコンドームを被せた
		ELSE
			PRINTFORML %CALLNAME:ASSI%はコンドームを外した
		ENDIF
	CASE TARGET
		;服着てる
		SIF OVER_GENITAL(TARGET) != ""
			PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
		;解除時
		IF TEQUIP:コンドーム
			PRINTFORML %CALLNAME:TARGET%にコンドームを取り付けた
			IF 上級売春中
				IF TALENT:マゾ
					PRINTFORML %CALLNAME:TARGET%は射精管理への期待にペニスをびくびくと震わせている……
				ELSE
					PRINTFORML %CALLNAME:TARGET%は生でやりたい様子だが、店の方針としてなだめた……
				ENDIF
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:TARGET%からコンドームを取り外した
		ENDIF
ENDSELECT
WAIT

;-------------------------------------------------
;コンドーム精飲（奴隷）
;-------------------------------------------------
@MESSAGE_COM_コンドーム精飲（奴隷）
SIF ABL:精液中毒 >= 3
	PRINT 膨らんだ避妊具を見て欲情する
PRINTFORM %CALLNAME:TARGET%
SIF ABL:精液中毒 <= 2
	PRINT の頬
PRINT に、
SELECTCASE TFLAG:使用済み避妊具
	;主人の精液
	CASE 1
		PRINTFORM %CALLNAME:MASTER%の精液が溜まった
	;助手の精液
	CASE 2
		PRINTFORM %CALLNAME:ASSI%の精液が溜まった
	;奴隷の精液
	CASE 3
		PRINTFORM %CALLNAME:TARGET%自身の精液で満たされた
ENDSELECT
PRINT 生暖かい使用済みコンドームを
SELECTCASE ABL:精液中毒
	CASE IS >= 3
		PRINTL 手渡した
	CASE 0 TO 2
		PRINTL 押し当てる
ENDSELECT
;精液中毒
SELECTCASE ABL:精液中毒
	;LV5以上
	CASE IS >= 5
		SIF TFLAG:使用済み避妊具 == 3
			PRINT 自分が出した精液にも関わらず、
		PRINTFORMW %CALLNAME:TARGET%はコンドームを裏返し、名残惜しそうに残り汁までもしゃぶっている……
	;LV3,4
	CASE 3, 4
		SIF TFLAG:使用済み避妊具 == 3
			PRINT 自分が出した精液にも関わらず、
		PRINTFORMW %CALLNAME:TARGET%は恍惚とした顔で、口内に流し込まれたゴム臭い精液を味わっている……
	;LV1,2
	CASE 1, 2
		PRINTFORM %CALLNAME:TARGET%は微かに嫌がるような素振りをしつつも、舌を出して
		SIF TFLAG:使用済み避妊具 == 3
			PRINT 自分が出した
		PRINTW 精液を甘受した……
	;LV0
	CASE 0
		PRINTFORM 強く閉じられた%CALLNAME:TARGET%の口をこじ開け、
		IF TFLAG:使用済み避妊具 == 3
			PRINTFORMW %CALLNAME:TARGET%が出した精液を再び体の中に戻した……
		ELSE
			PRINTW ゴム臭の混ざった生臭い精液を流し込んだ……
		ENDIF
ENDSELECT

;-------------------------------------------------
;コンドーム精飲（助手）
;-------------------------------------------------
@MESSAGE_COM_コンドーム精飲（助手）
PRINTFORM %CALLNAME:MASTER%は%CALLNAME:ASSI%に、
SELECTCASE TFLAG:使用済み避妊具
	;主人の精液
	CASE 1
		PRINTFORM %CALLNAME:MASTER%の精液が溜まった
	;助手の精液
	CASE 2
		PRINTFORM %CALLNAME:ASSI%自身の精液で満たされた
	;奴隷の精液
	CASE 3
		PRINTFORM %CALLNAME:TARGET%の精液が溜まった
ENDSELECT
PRINTL 生暖かい使用済みコンドームを手渡した

IF TEQUIP:睡眠 || CFLAG:死亡
	PRINTFORM %CALLNAME:ASSI%は、
;助手がサド
ELSEIF TALENT:ASSI:サド
	PRINTFORM %CALLNAME:ASSI%は%CALLNAME:TARGET%が舌をのばせば届きそうなぐらいの距離で、
;助手は奴隷のこと好きじゃない
ELSEIF 相性値(ASSI, TARGET) < 100
	PRINTFORM %CALLNAME:ASSI%は%CALLNAME:TARGET%に見せつけながら、
ELSE
	PRINTFORM %CALLNAME:ASSI%は%CALLNAME:TARGET%に痴態を見られつつも、
ENDIF

IF CFLAG:陥落
	;主人の精液
	IF TFLAG:使用済み避妊具 == 1
		PRINTFORML コンドームに溜まった%CALLNAME:MASTER%の精液を愛おしげに味わっている……
	;奴隷の精液 助手が奴隷のこと好き
	ELSEIF TFLAG:使用済み避妊具 == 3 && 相性値(ASSI, TARGET) > 100
		PRINTFORML %CALLNAME:TARGET%の勃起するペニスを眺めながら、そこから放たれた精液を美味しそうに味わっている……
	;助手の精液中毒がLV5以上
	ELSEIF ABL:ASSI:精液中毒 >= 5
		PRINTFORML 恍惚の表情で\@ TFLAG:使用済み避妊具 == 2 ? 自分の #  \@精液を味わっている……
	ELSE
		PRINTFORML おいしそうに\@ TFLAG:使用済み避妊具 == 2 ? 自分の #  \@精液を味わっている……
	ENDIF
ELSE
	PRINTFORML コンドームから溢れる\@ TFLAG:使用済み避妊具 == 2 ? 自分の #  \@精液を舌で舐めとっている……
ENDIF
IF CFLAG:死亡 || TEQUIP:睡眠
	WAIT
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	;精液中毒
	SELECTCASE ABL:精液中毒
		;LV5以上
		CASE IS >= 5
			PRINTW 我慢できないのか、涎を垂らしながらその光景を見ている……
		;LV3,4
		CASE 3, 4
			PRINTW 物欲しげな表情でじっとその光景を見ている……
		;LV1,2
		CASE 1, 2
			PRINTW 必死に目をそらそうとしているが、その光景に釘付けになっている……
		;LV0
		CASE 0
			PRINTW その光景に呆れた顔をしている……
	ENDSELECT
ENDIF

;-------------------------------------------------
;搾精器
;-------------------------------------------------
@MESSAGE_COM_搾精器
IF !TEQUIP:搾精器
	PRINTFORMW %CALLNAME:TARGET%のペニスに搾精器を取り付けた……
ELSE
	PRINTFORMW %CALLNAME:TARGET%のペニスから搾精器を取り外した……
ENDIF

;-------------------------------------------------
;貞操帯
;-------------------------------------------------
@MESSAGE_COM_貞操帯
IF !CFLAG:貞操帯
	PRINTFORMW %CALLNAME:TARGET%のペニスに貞操帯を取り付けた……
ELSE
	PRINTFORMW %CALLNAME:TARGET%のペニスから貞操帯を取り外した……
ENDIF

;-------------------------------------------------
;アナルバイブ
;-------------------------------------------------
@MESSAGE_COM_アナルバイブ
;服着てる
SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
;解除時
IF TEQUIP:アナルバイブ
	PRINTFORML %CALLNAME:TARGET%のアヌスからアナルバイブを引き抜いた
	IF 上級売春中
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:マゾ
			PRINTFORML 肛門だけでは飽き足らず、さらなる辱めを求めている……
		ELSE
			IF SEX(TARGET) == 1 && !ABL:ゲイ中毒
				PRINTFORML バイブに犯された尻穴に触れては、男のプライドを傷つけられたことに落ち込んでいる……
			ELSE
				PRINTFORML そのバイブで%CALLNAME:PLAYER%を責めてみたくてたまらないようだ……
			ENDIF
		ENDIF
	ELSEIF CFLAG:死亡
		PRINTFORML %CALLNAME:TARGET%の死体は括約筋を締めることももはや無いため、バイブの大きさに肛門が拡がったままになっている……
	ELSEIF TEQUIP:睡眠
		PRINTFORML 眠っている%CALLNAME:TARGET%の体は無意識に括約筋が緩み、バイブで馴染んだ肛門が見るからに柔らかくなっている……
	ELSEIF !TALENT:アニマル
		PRINTFORM %CALLNAME:TARGET%は
		;淫乱or娼婦 Ａ経験70以上
		IF FALLTYPE(TARGET) == 2 && EXP:Ａ経験 >= 70
			;Ａ経験
			SELECTCASE EXP:Ａ経験
				;180以上
				CASE IS >= 180
					;男
					IF SEX(TARGET) == 1
						SELECTCASE RAND:2
							CASE 0
								PRINTL 亀頭から前立腺液をたらしながら、尻穴を埋めるモノを求めている……
							CASE 1
								PRINTL くぐもった声を上げながら、ペニスの先端から先走りを垂らしている……
						ENDSELECT
					ELSE
						PRINTL 秘唇と口から涎をたらしながら、アナルを広げ続けている……
					ENDIF
				;140以上
				CASE IS >= 140
					PRINTL 潤んだ瞳で懇願するようにお尻を振っている……
				;100以上
				CASE IS >= 100
					PRINTL 物足りなさそうにお尻を突き出している……
				;70以上
				CASE IS >= 70
					PRINTL 少し名残惜しそうにお尻を揺らしている……
			ENDSELECT
		;淫乱娼婦無し もしくはＡ経験70未満
		ELSE
			IF TALENT:アニマル
				PRINTL 拡がった尻穴の上で尻尾を振っている……
			ELSE
				;Ａ経験
				SELECTCASE EXP:Ａ経験
					;40以上
					CASE IS >= 40
						PRINTL お尻をもじもじさせつつも、どこかほっとした表情をうかべている……
					;20以上
					CASE IS >= 20
						PRINTL 排泄感に似たような異物感に頬を染めている……
					;20未満
					CASE IS < 20
						PRINTL まだ小慣れていない尻穴をひくつかせながら苦痛に悶えている……
				ENDSELECT
			ENDIF
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%の尻穴に
	SELECTCASE アイテム改造:アナルバイブ
		CASE "強力なアナルバイブ"
			PRINTL 改造した強力なアナルバイブをあてがう……
		CASE "細いアナルバイブ"
			PRINTL 改良した細いアナルバイブの先端をのめり込ませる……
		CASE "太いアナルバイブ"
			PRINTL 通常よりも二回りほど太く改造したアナルバイブをあてがう……
		CASEELSE
			SELECTCASE SEX(TARGET)
				CASE 1
					IF CFLAG:死亡 || !PENIS(TARGET)
						PRINTL アナルバイブをあてがった……
					ELSE
						PRINTL アナルバイブをあてがうと、ペニスがびくんと揺れる……
					ENDIF
				CASE 2
					PRINTL アナルバイブをあてがった……
			ENDSELECT
	ENDSELECT
	SIF TEQUIP:睡眠
		PRINT 眠っている
	PRINTFORM %CALLNAME:TARGET%
	IF 上級売春中
		PRINT は
		IF TALENT:マゾ
			PRINTFORML 自分を卑下する言葉と奴隷のような忠誠で%CALLNAME:PLAYER%に媚びると、バイブを受け入れていった……
		ELSE
			SELECTCASE SEX(TARGET)
				CASE 1
					IF ABL:ゲイ中毒
						PRINTFORML バイブでアナルの内側を抉られ喘いではいるものの、道具よりも本物が欲しいようだ
					ELSE
						GOTO NOTGAY
					ENDIF
				CASE 2
					$NOTGAY
					IF ABL:Ａ感覚
						PRINTFORML 少し驚いたが、普段から慣らしているらしい肛門は素直にバイブを受け入れた……
					ELSE
						PRINTFORML 尻穴に挿れられることは夢にも思ってなかった様子で、痛みと不快感で悲鳴を上げている……
					ENDIF
			ENDSELECT
		ENDIF
	ELSEIF TALENT:アニマル
		SELECTCASE EXP:Ａ経験
			CASE IS <= 30
				PRINTL 痛みを感じているのか、挿入時に甲高い鳴き声を上げた……
			CASE IS <= 100
				PRINTL アナルも大分解れ、鳴き声を上げることもなくそれを受け入れた……
			CASE IS <= 200
				PRINT すっかりアナルを弄られることにも慣れ、
				SELECTCASE SEX(TARGET)
					CASE 1
						PRINTL 獣のペニスの先端から先走りを溢れさせている……
					CASE 2
						PRINTL 膣周りを濡らしている……
				ENDSELECT
		ENDSELECT
	ELSE
		IF アイテム改造:アナルバイブ == "太いアナルバイブ"
			IF CFLAG:死亡
				PRINTL の死体は弛緩しており、非常識な太さのバイブも少し馴染ませるだけで簡単に入っていった……
			ELSE
				SELECTCASE GETEXPLV(EXP:Ａ拡張経験, 10)
					CASE IS <= 1
						PRINTL のアナルが裂けないように注意を払うも、やはり拡張が足りないのかわずかに血が漏れ出している……
					CASE IS <= 3
						PRINTL のアナルはかろうじてバイブを飲み込むものの、あまり激しく動かせるほど緩んではいないようだ……
					CASE IS <= 5
						PRINTL の拡がったアナルは奥までバイブを受け入れ、さらなる拡張を欲している……
					CASEELSE
						PRINT の十分に緩んだアナルは難なくバイブを飲み込み、
						IF PENIS(TARGET)
							PRINTL ペニスからは押し出された先走りが溢れ出している……
						ELSE
							PRINTL 膣からも愛液を滴らせている……
						ENDIF
				ENDSELECT
			ENDIF
		ELSE
			IF TEQUIP:睡眠
				PRINT は無意識に括約筋が緩んでおり、挿入と同時に
				SELECTCASE SEX(TARGET)
					CASE 1
						IF PENIS(TARGET)
							PRINTL 陰茎から先走りが溢れた……
						ELSE
							PRINTL 去勢痕から前立腺液を溢れさせた……
						ENDIF
					CASE 2
						PRINTL 膣口が締まり愛液が搾り出された……
				ENDSELECT
			ELSEIF CFLAG:死亡
				IF TALENT:小柄体型
					PRINTL 幼く短い直腸には長すぎるバイブだったが、弛緩して感覚も無くなった死体は不釣り合いなバイブを根本まで飲み込んだ……
				ELSE
					PRINTL の尻穴は弛緩し、抵抗も感じずにバイブが根本まで入っていった……
				ENDIF
			ELSE
				PRINT は
				;淫乱or娼婦 Ａ経験70以上
				IF FALLTYPE(TARGET) == 2 && EXP:Ａ経験 >= 70
					;Ａ経験
					SELECTCASE EXP:Ａ経験
						;180以上
						CASE IS >= 180
							PRINTL とろけきった表情で自らの尻穴を広げ、バイブを奥まで誘い込んだ……
						;140以上
						CASE IS >= 140
							PRINTL 挿入を待ち焦がれるように腰をくねらせ、ゆっくりとバイブを飲み込んでいった……
						;100以上
						CASE IS >= 100
							PRINTL 頬を染め、期待にお尻を揺らめかせながら、作り物の亀頭を受け入れていく……
						;70以上
						CASE IS >= 70
							PRINTL すぼまりに与えられるバイブの刺激に身体を震わせながら、挿入されるバイブを締め付けている……
					ENDSELECT
				;淫乱娼婦無し もしくはＡ経験70未満
				ELSE
					;Ａ経験
					SELECTCASE EXP:Ａ経験
						;40以上
						CASE IS >= 40
							PRINTL 期待と否定の入り混じったような複雑な表情をしながら、肛内で震えるそれを締め付けている……
						;20以上
						CASE IS >= 20
							PRINTL 自らの尻穴で震えるバイブの音を聞いては、恥じらいに染まった顔を伏せている……
						;20未満
						CASE IS < 20
							PRINTL 不浄の穴を犯される恥辱と屈辱感にまみれた表情をしている……
					ENDSELECT
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
WAIT

;-------------------------------------------------
;アナルビーズ
;-------------------------------------------------
@MESSAGE_COM_アナルビーズ
;服着てる
SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り、
IF TEQUIP:アナルビーズ
	PRINTFORML %CALLNAME:TARGET%のアヌスからアナルビーズを勢い良く引き抜いた……
	IF CFLAG:死亡
		PRINTFORML 命を失い弛緩した肛門からは引っかかりも無く簡単に引き抜けた……
	ELSE
		SIF アイテム改造:アナルビーズ == "いびつなアナルビーズ"
			PRINTFORML ひとつひとつの珠をいびつな形に改造されたアナルビーズは、%CALLNAME:TARGET%の腸内を激しく抉っている……
	ENDIF
ELSE
	PRINTFORML %CALLNAME:TARGET%の菊穴にアナルビーズをひとつづつ飲み込ませていった……
	IF CFLAG:死亡
		PRINTFORML 締まりも無く押し出すことも無い肛門はいとも簡単に根本までアナルビーズを飲み込んだ……
	ELSE
		SIF アイテム改造:アナルビーズ == "大きなアナルビーズ"
			PRINTFORML ひとつひとつの珠を大きく改造されたアナルビーズが肛門を押し広げている……
	ENDIF
ENDIF
WAIT

;-------------------------------------------------
;浣腸器
;-------------------------------------------------
@MESSAGE_COM_浣腸器
;解除時
IF TEQUIP:浣腸器
	IF TEQUIP:トイレ
		PRINTFORM 便座に座る%CALLNAME:TARGET%のプラグを抜くと、
	ELSE
		PRINTFORM %CALLNAME:TARGET%は肛門栓を抜かれ、
	ENDIF
	SELECTCASE アイテム改造:浣腸器
		CASE "精液浣腸"
			PRINTL 汚物混じりの白濁液を撒き散らした
		CASE "媚薬浣腸"
			PRINTL 媚薬入りの浣腸液が汚物と共に放り出された
		CASE "スライム浣腸"
			PRINTL 茶色く汚れた粘液の塊を撒き散らした
		CASEELSE
			PRINTL 腸内の汚物を肛門から撒き散らした
	ENDSELECT
	IF TALENT:アニマル
		PRINTFORMW %CALLNAME:TARGET%は獣ゆえか恥ずかしげもなく、体勢を変えて排泄を続けている……
	ELSE
		;マゾっ気
		SELECTCASE ABL:マゾっ気
			CASE 0
				PRINTFORMW %CALLNAME:TARGET%は苦悶と恥辱の表情を浮かべている
			CASE 1
				PRINTFORMW %CALLNAME:TARGET%は脂汗を浮かべ、いまだ下腹に残る排泄感に堪えている
			CASE 2
				PRINTFORMW %CALLNAME:TARGET%は恥ずかしがりながらも、開放感にほっとした表情を浮かべている
			CASE 3
				PRINTFORMW %CALLNAME:TARGET%は肛門を通り過ぎる排泄物の感覚に時折艶っぽい喘ぎ声を上げている
			CASE 4
				PRINTFORMW %CALLNAME:TARGET%はうっとりした表情を浮かべ、排泄感を愉しんでいる
			CASE IS >= 5
				PRINTFORML %CALLNAME:TARGET%は排泄感と恥辱を心ゆくまで味わっている
				PRINTFORMW %CALLNAME:TARGET%の表情は快感にだらしなく緩み、肛門以外の穴からもだらだらと体液を垂らしている
		ENDSELECT
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%の直腸に
	SELECTCASE アイテム改造:浣腸器
		CASE "精液浣腸"
			PRINT 保管していた精液
		CASE "スライム浣腸"
			PRINT 保管していた粘液
		CASE "媚薬浣腸"
			PRINT 媚薬入りの浣腸液
		CASEELSE
			PRINT 浣腸液
	ENDSELECT
	PRINTL を流し込み、栓をした
	IF !TALENT:アニマル
		SELECTCASE アイテム改造:浣腸器
			CASE "媚薬浣腸"
				PRINTFORMW %CALLNAME:TARGET%は直腸から媚薬を吸収して顔を赤くしながら情欲と排泄感に悶えている……
			CASEELSE
				;マゾっ気
				SELECTCASE ABL:マゾっ気
					CASE 0
						PRINTFORMW %CALLNAME:TARGET%は苦悶の表情を浮かべている
					CASE 1
						PRINTFORMW %CALLNAME:TARGET%は脂汗を浮かべ、排泄感に堪えている
					CASE 2
						PRINTFORMW %CALLNAME:TARGET%は苦しそうな表情をしながらも、顔を紅潮させもじもじとお尻を振っている
					CASE 3
						PRINTFORMW %CALLNAME:TARGET%は、排泄感に苛まれながらも、時折恍惚とした表情を浮かべている
					CASE 4
						PRINTFORMW %CALLNAME:TARGET%は、うっとりした表情を浮かべ、排泄感と腹痛を愉しんでいる
					CASE IS >= 5
						PRINTFORML %CALLNAME:TARGET%は、排泄感を心ゆくまで味わっている
						PRINTFORMW %CALLNAME:TARGET%の表情は快感にだらしなく緩み、その口の端からは涎を滴らせている
				ENDSELECT
		ENDSELECT
	ENDIF
ENDIF

;-------------------------------------------------
;空気浣腸
;-------------------------------------------------
@MESSAGE_COM_空気浣腸
PRINTFORM %CALLNAME:TARGET%の
SIF OVER_GENITAL(TARGET) != ""
	PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げて無防備に晒された
PRINTL 菊穴に、腹が膨れるくらいポンプで空気を送り込んで栓をした
PRINTFORMW しばらくしてから栓を抜くと、%CALLNAME:TARGET%の菊穴からけたたましい放屁の音が響いた……

;-------------------------------------------------
;拡張バルーン
;-------------------------------------------------
@MESSAGE_COM_拡張バルーン
;解除時
IF TEQUIP:拡張バルーン
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL 広がりきった菊穴からバルーンを抜いてやった……
	IF CFLAG:死亡
		PRINTL 弛緩した尻穴は簡単に拡張され、締め付けるための意識も無いため拡がったままになっている……
	ELSEIF !TALENT:アニマル
		;Ａ感覚
		SELECTCASE ABL:Ａ感覚
			CASE 0
				PRINTFORMW 異物を失った%CALLNAME:TARGET%の肛門はゆっくりと閉じていった
			CASE 1
				PRINTFORMW %CALLNAME:TARGET%の肛門はひくつきながらゆるゆると閉じていった
			CASE 2
				PRINTFORMW %CALLNAME:TARGET%の肛門はぽっかり口を開いている
			CASE 3
				PRINTFORMW %CALLNAME:TARGET%の肛門は拡がったままなかなか戻らないようだ
			CASE 4
				PRINTFORMW %CALLNAME:TARGET%の肛門から腸の中が丸見えになっている
			CASE IS >= 5
				PRINTFORMW %CALLNAME:TARGET%は、自分の肛門を指で拡げ、恍惚としている
		ENDSELECT
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL 菊穴にエアバルーンを挿入し、空気を送り込んだ……
ENDIF

;-------------------------------------------------
;アナル電極
;-------------------------------------------------
@MESSAGE_COM_アナル電極
;解除時
IF TEQUIP:アナル電極
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL アヌスから電極を抜いてやった……
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL 菊穴に電極を挿入してスイッチを入れた……
	SIF CFLAG:死亡
		PRINTFORML 死体となった%CALLNAME:TARGET%は電流に呼応して全身がぴくぴくと痙攣し始めた……
ENDIF
WAIT

;-------------------------------------------------
;電動エネマグラ
;-------------------------------------------------
@MESSAGE_COM_電動エネマグラ
SIF TEQUIP:睡眠
	PRINT 眠っている
;解除時
IF TEQUIP:電動エネマグラ
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL 尻穴から電動エネマグラを引き抜いた……
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF OVER_GENITAL(TARGET) != "" && OVER_GENITAL(TARGET) != "貞操帯"
		PRINTFORM %OVER_GENITAL(TARGET)%を捲り上げ、
	PRINTL 尻穴に電動エネマグラを挿入した……
ENDIF
WAIT

;-------------------------------------------------
;ニプルキャップ
;-------------------------------------------------
@MESSAGE_COM_ニプルキャップ
SIF TEQUIP:睡眠
	PRINT 眠っている
IF TEQUIP:ニプルキャップ
	PRINTFORMW %CALLNAME:TARGET%からクリップローターを取り外した
ELSE
	PRINTFORMW %CALLNAME:TARGET%の乳首にクリップローターを貼り付けた
ENDIF

;-------------------------------------------------
;乳首ローター
;-------------------------------------------------
@MESSAGE_COM_乳首ローター
IF OVER_BUST(TARGET) != ""
	PRINTFORM %OVER_BUST(TARGET)%の上から%CALLNAME:TARGET%の乳首\@ 上級売春中 ? にローターでご奉仕す # をローターで責め立て \@
	;母乳体質
	SIF TALENT:母乳体質
		PRINT ると、溢れ出る母乳が衣服を濡らしてい
	PRINTL た……
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	;母乳体質
	SIF TALENT:母乳体質
		PRINT 母乳が滴る
	;欲情のPLAMLV3以上
	SIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬く尖った
	PRINTFORML 乳首に震えるローター\@ 上級売春中 ? でご奉仕し # を押し当て \@た……
	SIF TEQUIP:お風呂場
		PRINTL 浴槽内で震えるローターが鈍い音を響かせている……
ENDIF
WAIT

;-------------------------------------------------
;搾乳器
;-------------------------------------------------
@MESSAGE_COM_搾乳器
SIF TEQUIP:睡眠
	PRINT 眠っている
;解除時
IF TEQUIP:搾乳器
	PRINTFORMW %CALLNAME:TARGET%から搾乳器を取り外した
ELSE
	PRINTFORMW %CALLNAME:TARGET%に搾乳器を取り付けた
ENDIF

;-------------------------------------------------
;乳房電極
;-------------------------------------------------
@MESSAGE_COM_乳房電極
;解除時
IF TEQUIP:乳房電極
	PRINTFORMW %CALLNAME:TARGET%の\@ TALENT:バストサイズ <= 1 ? 胸部 # 乳房 \@から電極を外してやった……
ELSE
	PRINTFORMW %CALLNAME:TARGET%の\@ TALENT:バストサイズ <= 1 ? 胸部 # 乳房 \@に電極を貼り付けてスイッチを入れた……
ENDIF

;-------------------------------------------------
;ローション
;-------------------------------------------------
@MESSAGE_COM_ローション
PRINTFORMW %CALLNAME:TARGET%にローションをぬりたくった……

;-------------------------------------------------
;媚薬
;-------------------------------------------------
@MESSAGE_COM_媚薬
;触手召喚中
IF TEQUIP:触手
	PRINTFORML 催淫効果のある液体が触手から染み出してきた
ELSEIF TALENT:アニマル
	PRINTFORMW %CALLNAME:TARGET%に媚薬を混ぜた水を飲ませた……
	RETURN
ELSE
	PRINTFORM %CALLNAME:TARGET%に
	;調教者の技巧LV3以上
	SIF ABL:PLAYER:技巧 >= 3
		PRINT 口移しで
	;従順LV2以下
	SIF ABL:従順 <= 2
		PRINT 無理やり
	PRINTFORML 媚薬を飲ませた
ENDIF
IF CFLAG:陥落 || 上級売春中
	PRINTFORMW %CALLNAME:TARGET%は飲まされたのが媚薬だと悟ると、これからの淫行に期待するような笑みを浮かべた……
ELSE
	PRINTFORMW %CALLNAME:TARGET%は荒い息をついて肌を紅潮させている……
ENDIF

;-------------------------------------------------
;利尿剤
;-------------------------------------------------
@MESSAGE_COM_利尿剤
;触手召喚中
IF TEQUIP:触手
	PRINTFORML 極細い触手が、利尿作用のある液体を尿道から直接流し込んだ……
ELSE
	PRINTFORM %CALLNAME:TARGET%に
	;調教者の技巧LV3以上
	SIF ABL:PLAYER:技巧 >= 3
		PRINT 口移しで
	;従順LV2以下
	SIF ABL:従順 <= 2
		PRINT 無理やり
	PRINTFORML 利尿剤を飲ませた
ENDIF
SELECTCASE ABL:露出癖
	CASE 0 TO 2
		PRINTFORMW %CALLNAME:TARGET%は荒い息をつき、涙を浮かべて尿意に耐えている……
	CASE 3 TO 5
		PRINTFORMW %CALLNAME:TARGET%は薬で強引に引き出される激しい尿意に、性器をひくひくと震わせて耐えている……
	CASE IS >= 6
		PRINTFORMW %CALLNAME:TARGET%は激しい尿意に苛まれているにも関わらず、心地良さそうに艶のある声を漏らしている……
ENDSELECT

;-------------------------------------------------
;強精神薬
;-------------------------------------------------
@MESSAGE_COM_強精神薬
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に
;調教者の技巧がLV3以上
SIF ABL:PLAYER:技巧 >= 3
	PRINT 口移しで
;従順LV2以下
SIF ABL:従順 <= 2
	PRINT 無理やり
PRINTL 強精神剤を飲ませた
PRINTFORM %CALLNAME:TARGET%は
;従順LV3以上かつ反発刻印LV3未満
;（・∀・）ﾆﾔﾆﾔ
IF ABL:従順 >= 3 && MARK:反発刻印 < 3
	PRINTFORMW 困惑した表情を浮かべ、ニヤニヤと笑う%CALLNAME:MASTER%を見つめている……
;反発刻印LV1以上
ELSEIF MARK:反発刻印 >= 1
	PRINTFORMW 警戒心を露わにしてニヤニヤと笑う%CALLNAME:MASTER%を睨んでいる……
ELSE
	PRINTFORMW 一瞬驚いたような表情をしたがニヤニヤと笑う%CALLNAME:MASTER%を見て探るような眼で睨んでいる……
ENDIF

;-------------------------------------------------
;ローション注入
;-------------------------------------------------
@MESSAGE_COM_ローション注入
SIF TEQUIP:睡眠
	PRINT 眠っている
PRINTFORMW %CALLNAME:TARGET%のアナルにローションを注入した……

;-------------------------------------------------
;ミッドナイトスペシャル
;-------------------------------------------------
@MESSAGE_COM_ミッドナイトスペシャル
PRINTFORMW %CALLNAME:(TFLAG:ミッドナイトスペシャル使用者)%はミッドナイトスペシャルを服用した……

;-------------------------------------------------
;魅惑の粘液
;-------------------------------------------------
@MESSAGE_COM_魅惑の粘液
PRINTFORM %CALLNAME:PLAYER%は瓶から甘い香りを放つ粘液を取り出し、
SIF TALENT:PLAYER:溶ける快 || TALENT:PLAYER:スライム
	PRINT 自身の身体を構成する粘液と混ぜ合わせながら
PRINTFORML %CALLNAME:TARGET%の身体に塗り付けた
IF TALENT:無機物 || CSTR:種族 == "機械"
	PRINTFORMW しかし、粘液は%CALLNAME:TARGET%の身体に馴染む事なく、その表面を濡らすだけであった……
ELSEIF TALENT:スライム
	PRINTFORMW スライムとなった%CALLNAME:TARGET%の身体と粘液が混ざりあって、その香りはより濃厚になっていく……
ELSEIF TALENT:溶ける快
	PRINTFORMW %CALLNAME:TARGET%は、塗り付けられた粘液を取り込み、とろんとした表情で身体を疼かせている……
ELSE
	PRINTFORMW 塗り付けられた粘液は、%CALLNAME:TARGET%の肌と一体化するように馴染んでいく……
ENDIF

;-------------------------------------------------
;ビデオカメラ
;-------------------------------------------------
@MESSAGE_COM_ビデオカメラ
;解除時
IF TEQUIP:ビデオカメラ
	PRINTFORMW %CALLNAME:TARGET%の撮影をやめた……
ELSE
	PRINTFORMW ビデオカメラで%CALLNAME:TARGET%の撮影を始めた……
ENDIF

;-------------------------------------------------
;写真撮影,
;-------------------------------------------------
@MESSAGE_COM_写真撮影
PRINTFORML %CALLNAME:PLAYER%はセットしていたカメラで%CALLNAME:TARGET%の痴態を写真に収めた
IF TEQUIP:睡眠 || CFLAG:死亡
	WAIT
	RETURN
ENDIF
;感情乏しいor淫乱or娼婦or恥薄いorアニマル
IF TALENT:感情乏しい || FALLTYPE(TARGET) == 2 || TALENT:恥薄い || TALENT:アニマル
	PRINTFORMW %CALLNAME:TARGET%は恥ずかしい格好のまま写真を撮られたことを全く気にしていないようだ……
;恥じらいor露出癖LV3未満
ELSEIF TALENT:恥じらい || ABL:露出癖 < 3
	PRINTFORMW %CALLNAME:TARGET%は恥ずかしい格好のまま写真を撮られたことに気づいたのか僅かに顔をこわばらせている……
ENDIF

;-------------------------------------------------
;ライブ配信
;-------------------------------------------------
@MESSAGE_COM_ライブ配信
;解除時
IF TEQUIP:ライブ配信
	PRINTFORMW ライブ配信を終了した……
ELSE
	PRINTFORMW %CALLNAME:TARGET%の痴態をストリーミング配信することにした……
ENDIF

;-------------------------------------------------
;蜂蜜ローション
;-------------------------------------------------
@MESSAGE_COM_蜂蜜ローション
PRINTFORMW %CALLNAME:TARGET%の体に蜂蜜をぬりたくった……

;-------------------------------------------------
;蘇生薬投与
;-------------------------------------------------
@MESSAGE_COM_蘇生薬投与
PRINTFORML %CALLNAME:TARGET%に蘇生薬を投与した
PRINTFORMW %CALLNAME:TARGET%の体は徐々に体温と血色を取り戻し、息を吹き返したが、当人はかなり気分が悪そうだ……
