;-------------------------------------------
;コマンド実行後のテキスト
;-------------------------------------------
@AFTER_MESSAGE
#DIM DYNAMIC ウェイト

;アイテム破壊時のテキスト
IF TFLAG:アイテム破壊
	SELECTCASE SELECTCOMS
		CASE "ローター", "電気按摩", "バイブ", "オナホール", "アナルバイブ", "アナルビーズ", "拡張バルーン", "ニプルキャップ", "搾乳器", "電動エネマグラ", "搾精器", "クリキャップ"
			;PLAYERとTARGETどちらも衝動的ならTARGETが壊したことになる
			IF TALENT:衝動的
				IF CFLAG:陥落
					PRINTFORMW %CALLNAME:TARGET%は興奮のあまり、つい%SELECTCOMS%を壊してしまった……
				ELSE
					PRINTFORMW 道具で責められる%CALLNAME:TARGET%は怒りのあまり、衝動的に%SELECTCOMS%を壊してしまった……
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%が衝動的に%SELECTCOMS%を扱うばかりに、%SELECTCOMS%は壊れてしまった……
			ENDIF
		CASE "乳首ローター"
			IF TALENT:衝動的
				IF CFLAG:陥落
					PRINTFORMW %CALLNAME:TARGET%は興奮のあまり、ついローターを壊してしまった……
				ELSE
					PRINTFORMW 道具で責められる%CALLNAME:TARGET%は怒りのあまり、衝動的にローターを壊してしまった……
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%が衝動的にローターを扱うばかりに、ローターは壊れてしまった……
			ENDIF
		CASE "空気浣腸"
			IF TALENT:衝動的
				IF ABL:露出癖 >= 3
					PRINTFORMW %CALLNAME:TARGET%は興奮のあまり、つい空気ポンプを壊してしまった……
				ELSE
					PRINTFORMW 道具で責められる%CALLNAME:TARGET%は恥ずかしさのあまり、衝動的に空気ポンプを壊してしまった……
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%が衝動的に空気ポンプを扱うばかりに、空気ポンプは壊れてしまった……
			ENDIF
		CASE "アナル電極", "乳房電極"
			IF TALENT:衝動的
				IF CFLAG:陥落
					PRINTFORMW %CALLNAME:TARGET%は興奮のあまり、つい電極プラグを壊してしまった……
				ELSE
					PRINTFORMW 道具で責められる%CALLNAME:TARGET%は怒りのあまり、衝動的に電極プラグを壊してしまった……
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%が衝動的に電極プラグを扱うばかりに、電極プラグは壊れてしまった……
			ENDIF
		CASE "ローター愛撫させる"
			IF TALENT:衝動的
				PRINTFORMW %CALLNAME:TARGET%が乱暴に扱うせいでローターは壊れてしまった……
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%が衝動的にローターを扱うばかりに、ローターは壊れてしまった……
			ENDIF
		CASE "バイブ被挿入"
			PRINTFORMW %CALLNAME:TARGET%が乱暴に扱うせいでバイブは壊れてしまった……
		CASE "アナルバイブ被挿入"
			PRINTFORMW %CALLNAME:TARGET%が乱暴に扱うせいでアナルバイブは壊れてしまった……
		CASE "電気按摩愛撫させる"
			PRINTFORMW %CALLNAME:TARGET%が乱暴に扱うせいで電気按摩は壊れてしまった……
		CASE "羞恥プレイ"
			IF ABL:露出癖 >= 3 || TALENT:ナルシスト
				PRINTFORMW %CALLNAME:TARGET%は興奮のあまり、つい大鏡を割ってしまった……
			ELSE
				PRINTFORMW 自らの痴態を目の当たりにする%CALLNAME:TARGET%は恥ずかしさのあまり、衝動的に大鏡を叩き割ってしまった……
			ENDIF
	ENDSELECT
ENDIF

;処女喪失で触手が射精
IF NOWEX:MASTER:触手射精 && TFLAG:コマンド破瓜 && !CFLAG:死亡
	PRINTFORML %CALLNAME:TARGET%の秘唇から、汚液に混ざった破瓜の血が醜い触手を伝い落ちていった……
	ウェイト++
ENDIF

;処女喪失で射精なし
IF !NOWEX:PLAYER:射精 && !NOWEX:MASTER:触手射精 && TFLAG:コマンド破瓜 && !CFLAG:死亡
	PRINTFORML %CALLNAME:TARGET%の秘唇から、処女の証である赤い血が垂れ落ちていった……
	ウェイト++
ENDIF

;何もしない
IF SELECTCOMS == "何もしない" && !CFLAG:死亡 && !TEQUIP:睡眠
	SIF PALAM:欲情 >= PALAMLV:3
		PRINTFORM %CALLNAME:TARGET%は息を荒げ
	SIF PALAM:欲情 >= PALAMLV:5
		PRINT 熱っぽい瞳でこちらを見つめ
	SIF PALAM:欲情 >= PALAMLV:3 && TEQUIP:媚薬
		PRINT 、ガクガクと震える身体を抱きしめ
	SIF PALAM:欲情 >= PALAMLV:4
		PRINT ながら、しきりに太腿を擦りあわせ
	SIF PALAM:欲情 >= PALAMLV:3
		PRINTL ている……
	SIF PALAM:欲情 >= PALAMLV:3
		ウェイト++
ENDIF

;着衣パイズリで射精したら脱ぐ
SELECTCASE SELECTCOMS
	CASE "パイズリ", "縦パイズリ", "パイズリフェラ", "69パイズリ"
		IF NOWEX:PLAYER:射精 && OVER_BUST(TARGET) != "" && !TEQUIP:縄
			IF CFLAG:陥落
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の精液で汚れた%OVER_BUST(TARGET)%を脱ぎ始めた……
			ELSE
				IF TALENT:恥じらい
					PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の精液で汚れた%OVER_BUST(TARGET)%を唖然とした表情で見つめ、顔を真っ赤にしながらそれを脱いだ……
				ELSE
					PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の精液で汚れた%OVER_BUST(TARGET)%を脱ぎ始めた……
				ENDIF
			ENDIF
			ウェイト++
		ENDIF
	CASE "ダブルパイズリ"
		IF NOWEX:MASTER:射精 && (OVER_BUST(TARGET) != "" || OVER_BUST(ASSI) != "") && !TEQUIP:縄
			SIF OVER_BUST(TARGET) != ""
				PRINTFORM %CALLNAME:TARGET%
			SIF OVER_BUST(TARGET) != "" && OVER_BUST(ASSI) != ""
				PRINT と
			SIF OVER_BUST(ASSI) != ""
				PRINTFORM %CALLNAME:ASSI%
			PRINTFORML は、%CALLNAME:MASTER%の精液で汚れた衣服を脱ぎ始めた……
			ウェイト++
		ENDIF
ENDSELECT

;-------------------------------------------------
;避妊具に射精した時はここで表示する
;-------------------------------------------------
IF (GETBIT(TFLAG:避妊具射精, 0) || GETBIT(TFLAG:避妊具射精, 1)) && !CFLAG:死亡 && !TEQUIP:睡眠
	PRINTFORML 萎びたペニスから避妊具が外れ、先端にたっぷりと子種の溜まったコンドームを%CALLNAME:TARGET%に見せ付けた……
	IF !TALENT:アニマル
		IF 上級売春中
			IF TALENT:恥じらい
				PRINTFORML %CALLNAME:TARGET%は恥ずかしそうに膨らんだコンドームから目をそらしている……
			ELSE
				PRINTFORML %CALLNAME:TARGET%は膨らんだコンドームを面白そうに見ている……
			ENDIF
		;未陥落
		ELSEIF !CFLAG:陥落
			PRINTFORML 水風船のように膨らんだコンドームを見て、%CALLNAME:TARGET%は\@ SEX(TARGET) == 2 ? 膣内射精を想像し青ざめている…… # 気味が悪そうにしている…… \@
		;射精したのが主人で陥落済み
		ELSEIF NOWEX:MASTER:射精 && CFLAG:陥落
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%のペニスとコンドームを交互に見つめると、
				SELECTCASE FALLTYPE(TARGET)
					;恋慕ルートは細分化したい気もするが一旦ここまで
					CASE 1
						PRINTL 顔を赤らめてもじもじした
					CASE 2
						PRINTL 淫靡に舌なめずりをした……
					CASE 3
						PRINTL 美しい宝石でも見るかのようにうっとりとしている……
				ENDSELECT
		ENDIF
	ENDIF
	ウェイト++
ENDIF

;フェラで射精後 屍姦と睡眠姦ではフェラ実行できないから除外は必要なし？
IF TFLAG:お掃除フェラ
	PRINT その後、
	IF NOWEX:MASTER:射精 && (ASSI > 0 && NOWEX:ASSI:射精)
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%と%CALLNAME:ASSI%のペニスを交互に咥え、汚れを舐め取っていった……
	ELSE
		SIF TFLAG:お掃除フェラ >= 2
			PRINTFORM %CALLNAME:ASSI%と
		PRINTFORML %CALLNAME:TARGET%はペニスの汚れを綺麗に舐めとっていった……
	ENDIF
	TFLAG:お掃除フェラ = 0
	ウェイト++
ENDIF

;酔っている
IF CFLAG:酔 && !CFLAG:死亡 && !TEQUIP:睡眠
	IF TALENT:酒豪
		SELECTCASE CFLAG:酔
			CASE IS <= 20
				PRINTFORML 酒は回っているようだが、%CALLNAME:TARGET%にとってはまだまだ足りないらしい……
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%はお酒が回って気分が良いようだ……
		ENDSELECT
	ELSEIF TALENT:下戸
		SELECTCASE CFLAG:酔
			CASE IS >= 20
				PRINTFORML %CALLNAME:TARGET%は少し飲みすぎたようで気分が悪いと言っている……
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%はお酒が回って気分が良いようだ……
		ENDSELECT
	ELSE
		SELECTCASE CFLAG:酔
			CASE IS >= 50
				PRINTFORML %CALLNAME:TARGET%は少し飲みすぎたようで気分が悪いと言っている……
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%はお酒が回って気分が良いようだ……
		ENDSELECT
	ENDIF
	ウェイト++
ENDIF

;テキストを読むためのウエイト
;表示があるときのみ
SIF ウェイト
	WAIT

;-------------------------------------------
;パラメータ変化時テキスト　作成中
;-------------------------------------------
@PALAM_MESSAGE
;欲情がLV2になったとき　などなど

;-------------------------------------------
;刻印取得時のテキスト
;-------------------------------------------
;苦痛刻印
@MARK_MESSAGE_1
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:苦痛刻印
		CASE 1
			PRINTFORM %CALLNAME:TARGET%は
			SIF !TALENT:無関心 && !TALENT:感情乏しい
				PRINT 顔を歪ませながら
			PRINTL 苦痛を堪えている……
		CASE 2
			PRINTFORM %CALLNAME:TARGET%はしばらくの間、
		 	SIF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINT かすかに
			SIF TALENT:プライド高い
				PRINT 押し殺した
			PRINTL 苦鳴を漏らし続けていた……
		CASE 3
			PRINTFORM %CALLNAME%は余りの苦痛に
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINTL 動揺の気配が見える……
			ELSE
				SIF TALENT:プライド高い
					PRINTFORM %CALLNAME:PLAYER%を睨みながらも
				IF TALENT:戦士
					PRINTL 体が震えるのを堪え歯を食いしばっていた……
				ELSE
					SELECTCASE SEX(TARGET)
						CASE 1
							PRINTFORML 身を縮こまらせている……
						CASE 2
							PRINTFORML 涙を流している……
					ENDSELECT
				ENDIF
			ENDIF
	ENDSELECT
ENDIF

;快楽刻印
@MARK_MESSAGE_2
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:快楽刻印
		CASE 1
			PRINTFORML %CALLNAME:TARGET%は激しい快楽に身を打ち震わせていた……
		CASE 2
			PRINTFORM %CALLNAME:TARGET%は
			SIF (!TALENT:無関心 && !TALENT:感情乏しい) && TALENT:プライド高い || TALENT:恥じらい
				PRINTFORM %CALLNAME:PLAYER%から表情を隠すようにしつつも
			PRINT 快楽の余韻に
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINT しばらく動けずに
			ELSE
				PRINT 顔を緩ませて
			ENDIF
			PRINTL いた……
		CASE 3
			PRINTFORML %CALLNAME:TARGET%は強烈な快楽に身も心も焦がしているようだ……
	ENDSELECT
ENDIF

;快楽による屈服刻印
@MARK_MESSAGE_3
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:屈服刻印
		CASE 1
			PRINTFORML %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINTFORML やや体を跳ねさせ、%CALLNAME:PLAYER%から目を逸らした……
			ELSEIF TALENT:恥薄い
				PRINTL 思わず声を漏らしてしまったことに気づき、へらへらと薄い笑みを浮かべて誤魔化している……
			ELSE
				PRINT 思わず声を漏らしてしまい、
				SIF TALENT:プライド高い
					PRINT 不本意そうに
				SIF TALENT:恥じらい
					PRINT 顔を真っ赤にして
				SIF !TALENT:恥じらい
					PRINT 顔を赤くして
				PRINTFORML %CALLNAME:PLAYER%から目を逸らした……
			ENDIF
		CASE 2
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい  || TALENT:冷静
				PRINTL 快楽に翻弄されながらも、まだ求めようとはしてこない……
			ELSEIF TALENT:恥薄い || TALENT:快感に素直
				PRINTL 快楽に震えながら、媚びるような視線を送っていた……
			ELSE
				PRINT 快楽に震えながら、
				IF TALENT:プライド高い
					PRINTL 唇を引き結んで堪えている……
				ELSEIF TALENT:恥じらい
					PRINTFORML 顔を真っ赤にして%CALLNAME:PLAYER%の様子をうかがっている……
				ELSE
					PRINTL 物欲しげな視線を送っていた……
				ENDIF
			ENDIF
		CASE 3
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINTL 与えられる快楽にとうとう膝を折ったようだ……
			ELSEIF TALENT:恥薄い || TALENT:快感に素直
				PRINTL 快楽に乱れながらも、より激しい行為を懇願してきた……
			ELSE
				IF TALENT:プライド高い
					PRINTL 快楽によって乱れまいとしているが、さらなる行為を求めているのは明白だ……
				ELSEIF TALENT:恥じらい
					PRINTFORML 目を伏せて%CALLNAME:PLAYER%の視線を避けながら、さらなる行為を懇願してきた……
				ELSE
					PRINTL 快楽に乱れながらも、さらなる行為を懇願してきた……
				ENDIF
			ENDIF
	ENDSELECT
ENDIF

;苦痛ソース、恐怖ソースによる屈服刻印
@MARK_MESSAGE_4
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:屈服刻印
		CASE 1
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:戦士
				PRINTL わずかに体を強張らせているように思える……
			ELSEIF TALENT:痛みに弱い || TALENT:臆病
				PRINTFORML %CALLNAME:PLAYER%を見ると、怯えたように体を縮めた……
			ELSE
				PRINTFORM %CALLNAME:PLAYER%を見ると、
				SIF TALENT:プライド高い || TALENT:気丈
					PRINT  睨みつけながらも
				PRINTL 心なしか体が震えているように見えた……
			ENDIF
		CASE 2
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:戦士
				PRINTL 身を強張らせて息を吐いている……
			ELSEIF TALENT:痛みに弱い || TALENT:臆病
				PRINTL 見るからに体を縮ませて、強い怯えの篭った視線を送っている……
			ELSE
				PRINT 体を縮ませて、
				SIF TALENT:プライド高い || TALENT:気丈
					PRINT かすかに
				PRINTL 怯えの篭った視線を送っている……
			ENDIF
		CASE 3
			PRINTFORM %CALLNAME%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:戦士
				PRINTL 意思と関係なく震える身体を強張らせている……
			ELSEIF TALENT:痛みに弱い || TALENT:臆病
				PRINTL 完全に萎縮して、怯えの視線を向けながら震え上がっていた……
			ELSE
				PRINTFORML 恐怖のあまり\@ !TALENT:プライド高い && !TALENT:気丈 ? 完全に #  \@萎縮し、震え上がっていた……
			ENDIF
	ENDSELECT
ENDIF

;屈服ソース、恭順ソースによる屈服刻印
@MARK_MESSAGE_5
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:屈服刻印
		CASE 1
			PRINTFORM %CALLNAMEE:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:プライド低い
				SIF TALENT:プライド低い
					PRINT 元々強くはない
				PRINTL 抵抗を少し弱めたように見える……
			ELSE
				SIF TALENT:プライド高い
					PRINT 怒りと
				PRINTL 屈辱に顔を歪めていた……
			ENDIF
		CASE 2
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:プライド低い
				PRINTL 強い反抗の意志を見せる気配はない……
			ELSE
				SIF TALENT:プライド高い || TALENT:気丈
					PRINT 振る舞いに反して
				PRINTL 明らかに反抗の意志が弱まっていた……
			ENDIF
		CASE 3
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:ヤンデレ || TALENT:狂気
				PRINTL 暗く濁った瞳をどこか安心したように細めた……
			ELSEIF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 || TALENT:プライド低い
				PRINTFORML もはや%CALLNAME:PLAYER%に抵抗する気はないようだ……
			ELSE
				SIF TALENT:献身的
					PRINT どこか嬉々としながら
				PRINTFORML すっかり%CALLNAME:PLAYER%に身を任せている……
			ENDIF
	ENDSELECT
ENDIF

;反発刻印
@MARK_MESSAGE_10
IF !TALENT:アニマル && !CFLAG:死亡 && !TEQUIP:睡眠
	SELECTCASE MARK:反発刻印
		CASE 1
			PRINTFORM %CALLNAME:TARGET%は
			IF TEQUIP:アイマスク
				PRINTL 閉ざされた視界の中で
				IF TALENT:無関心 || TALENT:感情乏しい
					PRINTL 無気力に溜め息をついた……
				ELSEIF TALENT:生意気 || TALENT:反抗的
					PRINTL 舌打ちを一つした……
				ELSEIF TALENT:プライド高い
					PRINTL ぎりぎりと歯ぎしりを立てている……
				ELSE
					PRINTL ぐっと唇を噛んだ……
				ENDIF
			ELSE
				IF TALENT:無関心 || TALENT:感情乏しい
					PRINT 無感動な
				ELSEIF TALENT:臆病 || TALENT:大人しい
					PRINT 責めるような
				ELSE
					PRINT 険しい
				ENDIF
				PRINTL 視線をこちらに向けている……
			ENDIF
		CASE 2
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静
				PRINT うっすらと失望が透けて見える
			ELSEIF TALENT:臆病 || TALENT:大人しい
				PRINT 諦観を窺わせる
			ELSE
				PRINT 怒りを滲ませた
			ENDIF
			PRINTL 表情をしている……
		CASE 3
			PRINTFORM %CALLNAME:TARGET%は
			IF TALENT:ヤンデレ || TALENT:狂気
				PRINTL 爪を噛みながら虚空へ語りかけている……
			ELSEIF TALENT:無関心 || TALENT:感情乏しい || TALENT:冷静 
				PRINTFORML %CALLNAME:PLAYER%への関心を失っている……
			ELSEIF TALENT:臆病 || TALENT:大人しい
				PRINTL 両目から涙をこぼし、視線はこちらと合わせようとしない……
		 	ELSEIF TALENT:反抗的 || TALENT:生意気
				PRINTL 思いつく限りの罵詈雑言を並べ立ててくる……
			ELSE
				PRINTL 憤怒に顔を歪ませて、恨み言を呟いている……
			ENDIF
	ENDSELECT
ENDIF
