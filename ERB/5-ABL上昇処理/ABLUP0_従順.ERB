@ABL_CHECK_従順
SELECTCASE ABL:従順
	CASE 0
		ABLUP:屈服 = 1
		ABLUPEXP:主人調教経験 = 10
	CASE 1
		ABLUP:屈服 = 20
		ABLUPEXP:主人調教経験 = 50
	CASE 2
		ABLUP:屈服 = 1000
		ABLUPEXP:主人調教経験 = 100
	CASE 3
		ABLUP:屈服 = 8000
		ABLUPEXP:主人調教経験 = 150
	CASE 4
		ABLUP:屈服 = 24000
		ABLUPEXP:主人調教経験 = 200
	CASE 5
		ABLUP:屈服 = 30000
		ABLUPEXP:主人調教経験 = 250
	CASE 6
		ABLUP:屈服 = 36000
		ABLUPEXP:主人調教経験 = 300
	CASE 7
		ABLUP:屈服 = 45000
		ABLUPEXP:主人調教経験 = 400
	CASE 8
		ABLUP:屈服 = 55000
		ABLUPEXP:主人調教経験 = 600
	CASE 9
		ABLUP:屈服 = 70000
		ABLUPEXP:主人調教経験 = 800
	CASE IS >= 10
		ABLUP:屈服 = 800 * POWER(ABL:従順, 2)
ENDSELECT

;恋慕
SIF TALENT:恋慕
	TIMES ABLUP:屈服 , 0.80
;親愛
SIF TALENT:親愛
	TIMES ABLUP:屈服 , 0.60
;淫乱
SIF TALENT:淫乱
	TIMES ABLUPEXP:主人調教経験 , 1.10
;淫魔
SIF TALENT:淫魔
	TIMES ABLUPEXP:主人調教経験 , 1.30
;服従
IF TALENT:服従
	TIMES ABLUP:屈服 , 0.50
	TIMES ABLUPEXP:主人調教経験 , 0.50
ENDIF
;隷属
IF TALENT:隷属
	TIMES ABLUP:屈服 , 0.20
	TIMES ABLUPEXP:主人調教経験 , 0.20
ENDIF
;一線越えない&未陥落
IF TALENT:一線越えない && !陥落(TARGET)
	TIMES ABLUP:屈服 , 1.50
	TIMES ABLUPEXP:主人調教経験 , 1.50
ENDIF
;プライド高い、生意気
SIF TALENT:プライド高い || TALENT:生意気
	TIMES ABLUPEXP:主人調教経験 , 1.50
;大人しい、素直、プライド低い、感情乏しい、臆病
SIF TALENT:大人しい || TALENT:素直 || TALENT:プライド低い || TALENT:感情乏しい || TALENT:臆病
	TIMES ABLUPEXP:主人調教経験 , 0.80

;屈服刻印 1ごとに必要な屈服の珠10%引き 最大30%オフ！
ABLUP:屈服 -= ABLUP:屈服/10*MARK:屈服刻印

;LV4からは異常経験もしくは屈服刻印LV3必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
IF ABL:従順 >= 7 && !陥落(TARGET)
	ABLUPEXP:異常経験 = 3
ELSEIF ABL:従順 >= 4 && !陥落(TARGET)
	ABLUPEXP:異常経験 = 1
ENDIF

;@CHECK_JUEL_従順
SELECTCASE ABL:従順
	CASE 4
		;異常経験が規定値以上もしくは屈服刻印LV3必要
		SIF EXP:異常経験 < ABLUPEXP:異常経験 && MARK:屈服刻印 < 3
			RETURN 0
	CASE 7
		;異常経験が規定値に満たないと上昇できない
		SIF EXP:異常経験 < ABLUPEXP:異常経験
			RETURN 0
ENDSELECT

;屈服の珠、調教経験が必要
SIF JUEL:屈服 >= ABLUP:屈服 && EXP:主人調教経験 >= ABLUPEXP:主人調教経験
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

[SKIPSTART]
@ABLUP_MESSAGE_従順
;LV4からは異常経験もしくは屈服刻印LV3必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
SELECTCASE ABL:従順
	CASE 4
		SIF EXP:異常経験 < ABLUPEXP:異常経験 && MARK:屈服刻印 < 3
			SETCOLORBYNAME gray
		SIF ABLUPEXP:異常経験
			PRINTFORM [%EXPDISP(GETNUM(EXP, "異常経験"))%:{ABLUPEXP:異常経験}%SYSTXTDISP("以上")% or %MARKDISP(GETNUM(MARK, "屈服刻印"))%LV3]
		RESETCOLOR
	CASE 7
		SIF EXP:異常経験 < ABLUPEXP:異常経験
			SETCOLORBYNAME gray
		SIF ABLUPEXP:異常経験
			PRINTFORM [%EXPDISP(GETNUM(EXP, "異常経験"))%:{ABLUPEXP:異常経験}%SYSTXTDISP("以上")%]
		RESETCOLOR
ENDSELECT

SIF JUEL:屈服 < ABLUP:屈服
	SETCOLORBYNAME gray
PRINTFORM [%JEWELTXTDISP(GETNUM(PALAM, "屈服"))%×{ABLUP:屈服}]
RESETCOLOR

IF ABLUPEXP:主人調教経験
	SIF EXP:主人調教経験 < ABLUPEXP:主人調教経験
		SETCOLORBYNAME gray
	PRINTFORM [%EXPDISP(GETNUM(EXP, "主人調教経験"))%\:{ABLUPEXP:主人調教経験}%SYSTXTDISP("以上")%]
	RESETCOLOR
ENDIF
[SKIPEND]
