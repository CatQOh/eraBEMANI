@ABL_CHECK_サドっ気
SELECTCASE ABL:サドっ気
	CASE 0
		ABLUP:欲情 = 100
		ABLUPEXP:嗜虐快楽経験 = 5
	CASE 1
		ABLUP:欲情 = 500
		ABLUPEXP:嗜虐快楽経験 = 10
	CASE 2
		ABLUP:欲情 = 1500
		ABLUPEXP:嗜虐快楽経験 = 20
	CASE 3
		ABLUP:欲情 = 3000
		ABLUPEXP:嗜虐快楽経験 = 50
	CASE 4
		ABLUP:欲情 = 5000
		ABLUPEXP:嗜虐快楽経験 = 80
	CASE 5
		ABLUP:欲情 = 8000
		ABLUPEXP:嗜虐快楽経験 = 100
	CASE 6
		ABLUP:欲情 = 12000
		ABLUPEXP:嗜虐快楽経験 = 150
	CASE 7
		ABLUP:欲情 = 15000
		ABLUPEXP:嗜虐快楽経験 = 200
	CASE 8
		ABLUP:欲情 = 25000
		ABLUPEXP:嗜虐快楽経験 = 250
	CASE 9
		ABLUP:欲情 = 30000
		ABLUPEXP:嗜虐快楽経験 = 300
	CASE IS >= 10
		ABLUP:欲情 = 400 * POWER(ABL:サドっ気, 2)
ENDSELECT

;一線越えない及び未陥落
IF TALENT:一線越えない && !CFLAG:陥落
	TIMES ABLUP:欲情 , 2.00
	TIMES ABLUPEXP:嗜虐快楽経験 , 2.00
ENDIF
;衝動的、プライド高い
IF TALENT:衝動的 || TALENT:プライド高い
	TIMES ABLUP:欲情 , 0.80
	TIMES ABLUPEXP:嗜虐快楽経験 , 0.80
ENDIF
;サド
IF TALENT:サド
	TIMES ABLUP:欲情 , 0.50
	TIMES ABLUPEXP:嗜虐快楽経験 , 0.50
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
IF !TALENT:サド
	IF ABL:サドっ気 >= 8
		ABLUPEXP:異常経験 = 5
	ELSEIF ABL:サドっ気 >= 4
		ABLUPEXP:異常経験 = 3
	ELSEIF ABL:サドっ気 >= 3
		ABLUPEXP:異常経験 = 1
	ENDIF
ENDIF

;@CHECK_JUEL_サドっ気
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:異常経験
	RETURN 0

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	RETURN 0

;欲情の珠、嗜虐快楽経験が必要
SIF JUEL:欲情 >= ABLUP:欲情 && EXP:嗜虐快楽経験 >= ABLUPEXP:嗜虐快楽経験
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE_サドっ気
SELECTCASE ABL:サドっ気
	CASE 3, 4, 8
		;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:異常経験
			SETCOLORBYNAME gray
		SIF ABLUPEXP:異常経験
			PRINTFORM [異常経験{ABLUPEXP:異常経験}以上]
		RESETCOLOR
ENDSELECT

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	SETCOLORBYNAME gray
PRINTFORM [欲望LV{ABL:サドっ気+1}以上]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:欲情
	SETCOLORBYNAME gray
PRINTFORM [欲情の珠×{ABLUP:欲情}]
RESETCOLOR

IF ABLUPEXP:嗜虐快楽経験
	SIF EXP:嗜虐快楽経験 < ABLUPEXP:嗜虐快楽経験
		SETCOLORBYNAME gray
	PRINTFORM [嗜虐快楽経験{ABLUPEXP:嗜虐快楽経験}以上]
	RESETCOLOR
ENDIF
