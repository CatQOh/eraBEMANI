@ABLUP13
#DIM DYNAMIC LCOUNT
SIF ABL:自慰中毒 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:自慰中毒 < 0
	RETURN 0
CALL NEED_JUEL_自慰中毒
CALL CHECK_JUEL_自慰中毒
IF RESULT
	CALL NEED_JUEL_自慰中毒
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:自慰中毒++
ENDIF

@NEED_JUEL_自慰中毒
SELECTCASE ABL:自慰中毒
	CASE 0
		ABLUP:GETPALAM("快Ｃ") = 10000
		ABLUP:GETPALAM("欲情") = 3000
		ABLUP:GETPALAM("恐怖") = 1000
		ABLUPEXP:GETEXP("自慰経験") = 100
	CASE 1
		ABLUP:GETPALAM("快Ｃ") = 25000
		ABLUP:GETPALAM("欲情") = 6000
		ABLUP:GETPALAM("恐怖") = 3000
		ABLUPEXP:GETEXP("自慰経験") = 250
	CASE 2
		ABLUP:GETPALAM("快Ｃ") = 50000
		ABLUP:GETPALAM("欲情") = 12000
		ABLUP:GETPALAM("恐怖") = 6000
		ABLUPEXP:GETEXP("自慰経験") = 500
	CASE 3
		ABLUP:GETPALAM("快Ｃ") = 100000
		ABLUP:GETPALAM("欲情") = 20000
		ABLUP:GETPALAM("恐怖") = 15000
		ABLUPEXP:GETEXP("自慰経験") = 1000
	CASE 4
		ABLUP:GETPALAM("快Ｃ") = 300000
		ABLUP:GETPALAM("欲情") = 40000
		ABLUP:GETPALAM("恐怖") = 40000
		ABLUPEXP:GETEXP("自慰経験") = 2000
	CASE 5
		ABLUP:GETPALAM("快Ｃ") = 350000
		ABLUP:GETPALAM("欲情") = 45000
		ABLUP:GETPALAM("恐怖") = 45000
		ABLUPEXP:GETEXP("自慰経験") = 2500
	CASE 6
		ABLUP:GETPALAM("快Ｃ") = 400000
		ABLUP:GETPALAM("欲情") = 50000
		ABLUP:GETPALAM("恐怖") = 50000
		ABLUPEXP:GETEXP("自慰経験") = 3000
	CASE 7
		ABLUP:GETPALAM("快Ｃ") = 450000
		ABLUP:GETPALAM("欲情") = 55000
		ABLUP:GETPALAM("恐怖") = 55000
		ABLUPEXP:GETEXP("自慰経験") = 3000
	CASE 8
		ABLUP:GETPALAM("快Ｃ") = 500000
		ABLUP:GETPALAM("欲情") = 60000
		ABLUP:GETPALAM("恐怖") = 60000
		ABLUPEXP:GETEXP("自慰経験") = 3500
	CASE 9
		ABLUP:GETPALAM("快Ｃ") = 600000
		ABLUP:GETPALAM("欲情") = 70000
		ABLUP:GETPALAM("恐怖") = 70000
		ABLUPEXP:GETEXP("自慰経験") = 5000
	CASE IS >= 10
		ABLUP:GETPALAM("快Ｃ") = 7000 * POWER(ABL:自慰中毒, 2)
		ABLUP:GETPALAM("欲情") = 800 * POWER(ABL:自慰中毒, 2)
		ABLUP:GETPALAM("恐怖") = 800 * POWER(ABL:自慰中毒, 2)
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｃ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
		TIMES ABLUP:GETPALAM("恐怖") , 2.00
		TIMES ABLUPEXP:GETEXP("自慰経験") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ｃ") , 0.70
		TIMES ABLUP:GETPALAM("欲情") , 0.70
		TIMES ABLUP:GETPALAM("恐怖") , 0.70
		TIMES ABLUPEXP:GETEXP("自慰経験") , 0.70
	ENDIF
ENDIF

;自制心
IF TALENT:自制心
	;欲望LV4以下
	IF ABL:欲望 <= 4
		TIMES ABLUP:GETPALAM("快Ｃ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
		TIMES ABLUP:GETPALAM("恐怖") , 2.00
		TIMES ABLUPEXP:GETEXP("自慰経験") , 2.00
	;欲望LV5以上
	ELSEIF ABL:欲望 >= 5
		TIMES ABLUP:GETPALAM("快Ｃ") , 0.80
		TIMES ABLUP:GETPALAM("欲情") , 0.80
		TIMES ABLUP:GETPALAM("恐怖") , 0.80
		TIMES ABLUPEXP:GETEXP("自慰経験") , 0.80
	ENDIF
ENDIF

;中毒しやすい、自慰しやすい
IF TALENT:中毒しやすい || TALENT:自慰しやすい
	TIMES ABLUP:GETPALAM("快Ｃ") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
	TIMES ABLUP:GETPALAM("恐怖") , 0.50
	TIMES ABLUPEXP:GETEXP("自慰経験") , 0.50
ENDIF

;臆病
SIF TALENT:臆病
	TIMES ABLUP:GETPALAM("恐怖") , 0.70
;濡れやすい、勃ちやすい
SIF TALENT:濡れやすい || TALENT:勃ちやすい
	TIMES ABLUPEXP:GETEXP("自慰経験") , 0.70
;小柄体型、抑圧、解放
IF TALENT:小柄体型 || TALENT:抑圧 || TALENT:解放
	TIMES ABLUP:GETPALAM("快Ｃ") , 0.70
	TIMES ABLUP:GETPALAM("恐怖") , 0.70
	TIMES ABLUPEXP:GETEXP("自慰経験") , 0.70
ENDIF
;去勢済み 快Ｃを快Ｂに置き換える
IF TALENT:去勢済み
	TIMES ABLUP:GETPALAM("快Ｃ") , 1.50
	ABLUP:GETPALAM("快Ｂ") = ABLUP:GETPALAM("快Ｂ")
	TIMES ABLUP:GETPALAM("恐怖") , 1.50
	TIMES ABLUPEXP:GETEXP("自慰経験") , 1.50
ENDIF

;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][淫核]なら無視できる）
IF !TALENT:中毒しやすい && !TALENT:淫核
	IF ABL:自慰中毒 >= 8
		ABLUPEXP:GETEXP("異常経験") = 7
	ELSEIF ABL:自慰中毒 >= 4
		ABLUPEXP:GETEXP("異常経験") = 3
	ELSEIF ABL:自慰中毒 >= 3
		ABLUPEXP:GETEXP("異常経験") = 2
	ELSEIF ABL:自慰中毒 >= 2
		ABLUPEXP:GETEXP("異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL_自慰中毒
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0

;自慰中毒+1の露出癖が必要
SIF ABL:露出癖 <= ABL:自慰中毒
	RETURN 0

;欲情の珠、快Ｃの珠、恐怖の珠、自慰経験で上げる
;去勢済みの場合は代わりに快Ｂで上げる
IF TALENT:去勢済み
	SIF JUEL:快Ｂ >= ABLUP:GETPALAM("快Ｂ") && JUEL:欲情 >= ABLUP:GETPALAM("欲情") && JUEL:恐怖 >= ABLUP:GETPALAM("恐怖") && EXP:自慰経験 >= ABLUPEXP:GETEXP("自慰経験")
		RETURN 1
ELSE
	SIF JUEL:快Ｃ >= ABLUP:GETPALAM("快Ｃ") && JUEL:欲情 >= ABLUP:GETPALAM("欲情") && JUEL:恐怖 >= ABLUP:GETPALAM("恐怖") && EXP:自慰経験 >= ABLUPEXP:GETEXP("自慰経験")
		RETURN 1
ENDIF

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_自慰中毒
SELECTCASE ABL:自慰中毒
	CASE 2, 3, 4, 8
		;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][淫核]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLORBYNAME gray
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:露出癖 <= ABL:自慰中毒
	SETCOLORBYNAME gray
PRINTFORM [露出癖LV{ABL:自慰中毒+1}以上]
RESETCOLOR

;去勢済みなら快Ｂを代わりに使う
IF TALENT:去勢済み
	SIF JUEL:快Ｂ < ABLUP:GETPALAM("快Ｂ")
		SETCOLORBYNAME gray
	PRINTFORM [快Ｂの珠×{ABLUP:GETPALAM("快Ｂ")}]
	RESETCOLOR
ELSE
	SIF JUEL:快Ｃ < ABLUP:GETPALAM("快Ｃ")
		SETCOLORBYNAME gray
	PRINTFORM [快Ｃの珠×{ABLUP:GETPALAM("快Ｃ")}]
	RESETCOLOR
ENDIF

SIF JUEL:欲情 < ABLUP:GETPALAM("欲情")
	SETCOLORBYNAME gray
PRINTFORM [欲情の珠×{ABLUP:GETPALAM("欲情")}]
RESETCOLOR

SIF JUEL:恐怖 < ABLUP:GETPALAM("恐怖")
	SETCOLORBYNAME gray
PRINTFORM [恐怖の珠×{ABLUP:GETPALAM("恐怖")}]
RESETCOLOR

IF ABLUPEXP:GETEXP("自慰経験")
	SIF EXP:自慰経験 < ABLUPEXP:GETEXP("自慰経験")
		SETCOLORBYNAME gray
	PRINTFORM [自慰経験{ABLUPEXP:GETEXP("自慰経験")}以上]
	RESETCOLOR
ENDIF
