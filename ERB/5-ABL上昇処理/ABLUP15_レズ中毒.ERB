@ABL_CHECK_レズ中毒
SELECTCASE ABL:レズ中毒
	CASE 0
		ABLUP:恭順 = 5000
		ABLUP:屈服 = 5000
		ABLUPEXP:レズ経験 = 500
	CASE 1
		ABLUP:恭順 = 12000
		ABLUP:屈服 = 12000
		ABLUPEXP:レズ経験 = 1000
	CASE 2
		ABLUP:恭順 = 20000
		ABLUP:屈服 = 20000
		ABLUPEXP:レズ経験 = 1500
	CASE 3
		ABLUP:恭順 = 35000
		ABLUP:屈服 = 35000
		ABLUPEXP:レズ経験 = 2000
	CASE 4
		ABLUP:恭順 = 60000
		ABLUP:屈服 = 60000
		ABLUPEXP:レズ経験 = 2500
	CASE 5
		ABLUP:恭順 = 90000
		ABLUP:屈服 = 90000
		ABLUPEXP:レズ経験 = 3000
	CASE 6
		ABLUP:恭順 = 120000
		ABLUP:屈服 = 120000
		ABLUPEXP:レズ経験 = 3500
	CASE 7
		ABLUP:恭順 = 150000
		ABLUP:屈服 = 150000
		ABLUPEXP:レズ経験 = 4000
	CASE 8
		ABLUP:恭順 = 200000
		ABLUP:屈服 = 200000
		ABLUPEXP:レズ経験 = 4500
	CASE 9
		ABLUP:恭順 = 250000
		ABLUP:屈服 = 250000
		ABLUPEXP:レズ経験 = 5000
	CASE IS >= 10
		ABLUP:恭順 = 3000 * POWER(ABL:レズ中毒, 2)
		ABLUP:屈服 = 3000 * POWER(ABL:レズ中毒, 2)
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:恭順 , 2.00
		TIMES ABLUP:屈服 , 2.00
		TIMES ABLUPEXP:レズ経験 , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:恭順 , 0.80
		TIMES ABLUP:屈服 , 0.80
		TIMES ABLUPEXP:レズ経験 , 0.80
	ENDIF
ENDIF
;自制心
IF TALENT:自制心
	;欲望LV4以下
	IF ABL:欲望 <= 4
		TIMES ABLUP:恭順 , 2.00
		TIMES ABLUP:屈服 , 2.00
		TIMES ABLUPEXP:レズ経験 , 2.00
	;欲望LV5以上
	ELSEIF ABL:欲望 >= 5
		TIMES ABLUP:恭順 , 0.80
		TIMES ABLUP:屈服 , 0.80
		TIMES ABLUPEXP:レズ経験 , 0.80
	ENDIF
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES ABLUP:恭順 , 0.50
	TIMES ABLUP:屈服 , 0.50
	TIMES ABLUPEXP:レズ経験 , 0.50
ENDIF
;女好き
IF TALENT:女好き
	TIMES ABLUP:恭順 , 0.70
	TIMES ABLUP:屈服 , 0.70
	TIMES ABLUPEXP:レズ経験 , 0.70
ENDIF
;女嫌い
IF TALENT:女嫌い
	TIMES ABLUP:屈服 , 1.50
	TIMES ABLUPEXP:レズ経験 , 1.50
ENDIF

;レズっ気が先行してるとレズっ気に応じて必要な珠が下がる 最大50%オフ
IF ABL:レズっ気 > ABL:レズ中毒
	ABLUP:恭順 -= ABLUP:恭順/10*LIMIT(ABL:レズっ気-ABL:レズ中毒, 0, 5)
	ABLUP:屈服 -= ABLUP:屈服/10*LIMIT(ABL:レズっ気-ABL:レズ中毒, 0, 5)
ENDIF

;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][両刀][女好き]なら無視できる）
IF !TALENT:中毒しやすい && !TALENT:両刀 && !TALENT:女好き
	SELECTCASE ABL:レズ中毒
		CASE IS >= 8
			ABLUPEXP:異常経験 = 7
		CASE IS >= 4
			ABLUPEXP:異常経験 = 3
		CASE IS >= 3
			ABLUPEXP:異常経験 = 2
		CASE IS >= 2
			ABLUPEXP:異常経験 = 1
	ENDSELECT
ENDIF

;@CHECK_JUEL_レズ中毒
;以上経験が必要な場合
SIF EXP:異常経験 < ABLUPEXP:異常経験
	RETURN 0

;奉仕精神が必要
SIF ABL:奉仕精神 <= ABL:レズ中毒
	RETURN 0

;恭順の珠、欲情の珠、屈服の珠、レズ経験で上げる
SIF JUEL:恭順 >= ABLUP:恭順 && JUEL:屈服 >= ABLUP:屈服 && EXP:レズ経験 >= ABLUPEXP:レズ経験
	RETURN 1

RETURN 0

@ABLUP_MESSAGE_レズ中毒
SELECTCASE ABL:レズ中毒
	CASE 2, 3, 4, 8
		;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][両刀][女好き]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:異常経験
			SETCOLORBYNAME gray
		SIF ABLUPEXP:異常経験
			PRINTFORM [異常経験{ABLUPEXP:異常経験}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:レズ中毒+1 > ABL:奉仕精神
	SETCOLORBYNAME gray
PRINTFORM [奉仕精神LV{ABL:レズ中毒+1}以上]
RESETCOLOR

SIF JUEL:恭順 < ABLUP:恭順
	SETCOLORBYNAME gray
PRINTFORM [恭順の珠×{ABLUP:恭順}]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:屈服
	SETCOLORBYNAME gray
PRINTFORM [屈服の珠×{ABLUP:屈服}]
RESETCOLOR

IF ABLUPEXP:レズ経験
	SIF EXP:レズ経験 < ABLUPEXP:レズ経験
		SETCOLORBYNAME gray
	PRINTFORM [レズ経験{ABLUPEXP:レズ経験}以上]
	RESETCOLOR
ENDIF
