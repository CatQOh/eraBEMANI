@ABL_CHECK_ゲイ中毒
SELECTCASE ABL:ゲイ中毒
	CASE 0
		ABLUP:恭順 = 5000
		ABLUP:屈服 = 5000
		ABLUPEXP:ゲイ経験 = 500
	CASE 1
		ABLUP:恭順 = 12000
		ABLUP:屈服 = 12000
		ABLUPEXP:ゲイ経験 = 1000
	CASE 2
		ABLUP:恭順 = 20000
		ABLUP:屈服 = 20000
		ABLUPEXP:ゲイ経験 = 1500
	CASE 3
		ABLUP:恭順 = 35000
		ABLUP:屈服 = 35000
		ABLUPEXP:ゲイ経験 = 2000
	CASE 4
		ABLUP:恭順 = 60000
		ABLUP:屈服 = 60000
		ABLUPEXP:ゲイ経験 = 2500
	CASE 5
		ABLUP:恭順 = 90000
		ABLUP:屈服 = 90000
		ABLUPEXP:ゲイ経験 = 3000
	CASE 6
		ABLUP:恭順 = 120000
		ABLUP:屈服 = 120000
		ABLUPEXP:ゲイ経験 = 3500
	CASE 7
		ABLUP:恭順 = 150000
		ABLUP:屈服 = 150000
		ABLUPEXP:ゲイ経験 = 4000
	CASE 8
		ABLUP:恭順 = 200000
		ABLUP:屈服 = 200000
		ABLUPEXP:ゲイ経験 = 4500
	CASE 9
		ABLUP:恭順 = 250000
		ABLUP:屈服 = 250000
		ABLUPEXP:ゲイ経験 = 5000
	CASE IS >= 10
		ABLUP:恭順 = 3000 * POWER(ABL:ゲイ中毒, 2)
		ABLUP:屈服 = 3000 * POWER(ABL:ゲイ中毒, 2)
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:恭順 , 2.00
		TIMES ABLUP:屈服 , 2.00 
		TIMES ABLUPEXP:ゲイ経験 , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:恭順 , 0.80
		TIMES ABLUP:屈服 , 0.80 
		TIMES ABLUPEXP:ゲイ経験 , 0.80
	ENDIF
ENDIF
;自制心
IF TALENT:自制心
	;欲望LV4以下
	IF ABL:欲望 <= 4
		TIMES ABLUP:恭順 , 2.00
		TIMES ABLUP:屈服 , 2.00
		TIMES ABLUPEXP:ゲイ経験 , 2.00
	;欲望LV5以上
	ELSEIF ABL:欲望 >= 5
		TIMES ABLUP:恭順 , 0.80
		TIMES ABLUP:屈服 , 0.80
		TIMES ABLUPEXP:ゲイ経験 , 0.80
	ENDIF
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES ABLUP:恭順 , 0.50
	TIMES ABLUP:屈服 , 0.50
	TIMES ABLUPEXP:ゲイ経験 , 0.50
ENDIF
;男好き
IF TALENT:男好き
	TIMES ABLUP:屈服 , 0.70
	TIMES ABLUPEXP:ゲイ経験 , 0.70
ENDIF
;男嫌い
IF TALENT:男嫌い
	TIMES ABLUP:屈服 , 1.50
	TIMES ABLUPEXP:ゲイ経験 , 1.50
ENDIF

;ホモっ気が先行してるとホモっ気に応じて必要な珠が下がる 最大50%オフ
IF ABL:ホモっ気 > ABL:ゲイ中毒
	ABLUP:恭順 -= ABLUP:恭順/10*LIMIT(ABL:ホモっ気-ABL:ゲイ中毒, 0, 5)
	ABLUP:屈服 -= ABLUP:屈服/10*LIMIT(ABL:ホモっ気-ABL:ゲイ中毒, 0, 5)
ENDIF

;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][両刀][男好き]なら無視できる）
IF !TALENT:中毒しやすい && !TALENT:両刀 && !TALENT:男好き
	SELECTCASE ABL:ゲイ中毒
		CASE IS >= 8
			ABLUPEXP:異常経験 = 7
		CASE IS >= 4
			ABLUPEXP:異常経験 = 3
		CASE IS >= 3
			ABLUPEXP:異常経験 = 2
		CASE IS >= 2
			ABLUPEXP:異常経験 = 1
	ENDSELECT
ENDIF

;@CHECK_JUEL_ゲイ中毒
;異常経験が必要
SIF EXP:異常経験 < ABLUPEXP:異常経験
	RETURN 0

;奉仕精神が必要
SIF ABL:ゲイ中毒 >= ABL:奉仕精神
	RETURN 0

;恭順、欲情、屈服の珠、ゲイ経験で上げる
SIF JUEL:恭順 >= ABLUP:恭順 && JUEL:屈服 >= ABLUP:屈服 && EXP:ゲイ経験 >= ABLUPEXP:ゲイ経験
	RETURN 1

;上記の条件を満たせないと上げられない
RETURN 0

@ABLUP_MESSAGE_ゲイ中毒
SELECTCASE ABL:ゲイ中毒
	CASE 2, 3, 4, 8
		;LV2からは異常経験必要 LV3からは異常経験2以上必要 LV4からは異常経験3以上必要 LV8からは異常経験7以上必要（素質：[中毒しやすい][両刀][男好き]なら無視できる）
		SIF EXP:異常経験 <= ABLUPEXP:異常経験
			SETCOLORBYNAME gray
		SIF ABLUPEXP:異常経験
			PRINTFORM [異常経験{ABLUPEXP:異常経験}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:奉仕精神 <= ABL:ゲイ中毒
	SETCOLORBYNAME gray
PRINTFORM [奉仕精神LV{ABL:ゲイ中毒+1}以上]
RESETCOLOR

SIF JUEL:恭順 < ABLUP:恭順
	SETCOLORBYNAME gray
PRINTFORM [恭順の珠×{ABLUP:恭順}]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:屈服
	SETCOLORBYNAME gray
PRINTFORM [屈服の珠×{ABLUP:屈服}]
RESETCOLOR

IF ABLUPEXP:ゲイ経験
	SIF EXP:ゲイ経験 < ABLUPEXP:ゲイ経験
		SETCOLORBYNAME gray
	PRINTFORM [ゲイ経験{ABLUPEXP:ゲイ経験}以上]
	RESETCOLOR
ENDIF
