@ABL_CHECK_飼育技能
SELECTCASE ABL:飼育技能
	CASE 0
		ABLUPEXP:飼育経験 = 5
	CASE 1
		ABLUPEXP:飼育経験 = 20
	CASE 2
		ABLUPEXP:飼育経験 = 50
	CASE 3
		ABLUPEXP:飼育経験 = 100
	CASE 4
		ABLUPEXP:飼育経験 = 150
	CASE 5
		ABLUPEXP:飼育経験 = 200
	CASE 6
		ABLUPEXP:飼育経験 = 250
	CASE 7
		ABLUPEXP:飼育経験 = 300
	CASE 8
		ABLUPEXP:飼育経験 = 400
	CASE 9
		ABLUPEXP:飼育経験 = 500
ENDSELECT

;栽培技能と飼育技能の習得の珠はゆるい
;2.17で撤廃
;ABLUP:習得 = ABL:飼育技能*100

;習得早い
SIF TALENT:習得早い
	TIMES ABLUPEXP:飼育経験 , 0.90
;習得遅い、サボり魔
SIF TALENT:習得遅い || TALENT:サボり魔
	TIMES ABLUPEXP:飼育経験 , 1.20

;種族適性
SELECTCASE CSTR:種族
	CASE "獣人", "獣", "人魚"
		TIMES ABLUPEXP:飼育経験 , 0.90
ENDSELECT

;@CHECK_JUEL_飼育技能
;習得の珠、飼育経験で上げる
SIF EXP:飼育経験 >= ABLUPEXP:飼育経験
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_飼育技能
SIF EXP:飼育経験 < ABLUPEXP:飼育経験
	SETCOLORBYNAME gray
PRINTFORM [飼育経験{ABLUPEXP:飼育経験}以上]
RESETCOLOR
