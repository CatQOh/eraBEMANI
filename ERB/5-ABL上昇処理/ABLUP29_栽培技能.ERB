@ABL_CHECK_栽培技能
SELECTCASE ABL:栽培技能
	CASE 0
		ABLUPEXP:栽培経験 = 0
	CASE 1
		ABLUPEXP:栽培経験 = 20
	CASE 2
		ABLUPEXP:栽培経験 = 50
	CASE 3
		ABLUPEXP:栽培経験 = 100
	CASE 4
		ABLUPEXP:栽培経験 = 150
	CASE 5
		ABLUPEXP:栽培経験 = 200
	CASE 6
		ABLUPEXP:栽培経験 = 250
	CASE 7
		ABLUPEXP:栽培経験 = 300
	CASE 8
		ABLUPEXP:栽培経験 = 400
	CASE 9
		ABLUPEXP:栽培経験 = 500
ENDSELECT

;栽培技能と飼育技能の習得の珠はゆるい
;2.17で撤廃
;ABLUP:習得 = ABL:栽培技能*100

;習得早い、調合知識、専業主婦
SIF TALENT:習得早い || TALENT:調合知識 || TALENT:専業主婦
	TIMES ABLUPEXP:栽培経験 , 0.90
;習得遅い、サボり魔
SIF TALENT:習得遅い || TALENT:サボり魔
	TIMES ABLUPEXP:栽培経験 , 1.20

;種族適性
SELECTCASE CSTR:種族
	CASE "精霊", "妖精", "獣人", "獣"
		TIMES ABLUPEXP:栽培経験 , 0.90
ENDSELECT

;植生学
SIF TALENT:植生学
	TIMES ABLUPEXP:栽培経験 , 0.80
;主に昼の仕事なのでヴァンパイアと吸血鬼はかなり伸ばしづらい
SIF CSTR:種族 == "ヴァンパイア" || TALENT:吸血鬼
	TIMES ABLUPEXP:栽培経験 , 2.00

;@CHECK_JUEL_栽培技能
;習得の珠、栽培経験で上げる
SIF EXP:栽培経験 >= ABLUPEXP:栽培経験
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_栽培技能
SIF EXP:栽培経験 < ABLUPEXP:栽培経験
	SETCOLORBYNAME gray
PRINTFORM [栽培経験{ABLUPEXP:栽培経験}以上]
RESETCOLOR
