;追加処理及びサブイベント
;-------------------------------------------------
;ターン終了時
;-------------------------------------------------
@ターン終了
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LTARGET
#DIM 着床告知;DYNAMICじゃない変数 プレーヤーへの告知用なので起動ごとに破棄してもよい
#DIM DYNAMIC 人数
#DIM DYNAMIC 売却可
#DIM DYNAMIC 最大体力
#DIM DYNAMIC 妖精イベント, 2
#DIM DYNAMIC 鳳凰イベント, 3
#DIM DYNAMIC 関数カウント
#DIMS DYNAMIC 関数名, 1000
#DIM DYNAMIC DEUIL
#DIM DYNAMIC YORIDORI
#DIM DYNAMIC GUMINDAYS
#DIM DYNAMIC 気温補正

REDRAW 1

IF OPTION:口上表示
	FOR LCOUNT, 0, CHARANUM
		SIF !CFLAG:LCOUNT:口上非表示
			TRYCALLFORM KOJO_DAYEND_%CSVNAMERPS(NO:LCOUNT)%
	NEXT
ENDIF

IF TARGET >= 0 && !CFLAG:キャラロスト
	;売却可能条件は専用関数で判定
	売却可 = 売却可能条件(TARGET)

	;段位モード専用の特殊条件
	IF ゲームモード() == "段位認定"
		;段位認定裏七級の場合、陥落していないと売却できない
		IF 受験段位名 == "裏七級"
			SIF !陥落(TARGET)
				売却可 = 0
		;「炎の孕ませポップン学園」の場合、陥落させたうえで着床を認知していないと売却できない
		ELSEIF 受験段位名 == "炎の孕ませポップン学園"
			売却可 = 0
			IF 陥落(TARGET)
				IF TALENT:妊娠
					売却可 = 1
				ELSEIF FALLTYPE(TARGET) == 1 && PREG:TARGET:妊娠確定フラグ
					売却可 = 1
					;この場合一度だけ着床したことを教えてもらえる(再起動でフラグが消失するのは仕様)
					;変数「着床告知」にビットで管理
					;(GETBIT)0:ミニッツ 1:ルート 2:翠里 3:鈴花 4:氷海
					SELECTCASE CALLNAME:TARGET
						CASE "ミニッツ"
							IF !GETBIT(着床告知, 0)
								SETBIT 着床告知, 0
								PRINTFORML ミニッツは%CALLNAME:MASTER%に妊娠検査薬の結果をこっそりと見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%はミニッツの幼い体に子種を植え付けたようだ……
							ENDIF
						CASE "ルート"
							IF !GETBIT(着床告知, 1)
								SETBIT 着床告知, 1
								PRINTFORML ルートは%CALLNAME:MASTER%に妊娠の診断結果を教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%はルートとの交尾によって受精させたようだ……
							ENDIF
						CASE "翠里"
							IF !GETBIT(着床告知, 2)
								SETBIT 着床告知, 2
								PRINTFORML 翠里は%CALLNAME:MASTER%に妊娠した可能性が高いことを教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%は翠里の自称超絶名器の奥に遺伝子を残せたようだ……
							ENDIF
						CASE "鈴花"
							IF !GETBIT(着床告知, 3)
								SETBIT 着床告知, 3
								PRINTFORML 鈴花は%CALLNAME:MASTER%に妊娠検査薬の結果を嬉しそうに見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は鈴花のめしべとの交配に成功したようだ……
							ENDIF
						CASE "氷海"
							IF !GETBIT(着床告知, 4)
								SETBIT 着床告知, 4
								PRINTFORML 氷海は%CALLNAME:MASTER%に妊娠の診断結果が陽性であることを伝えてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は氷海への膣内射精で着床させたようだ……
							ENDIF
					ENDSELECT
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;憑依中は売却可能・助手可能にならない
	IF 売却可 && CFLAG:憑依 == -1
		IF !CFLAG:売却可能 && ゲームモード() != "新婚モード"
			PRINTFORMW %NAME:TARGET%が売却可能になりました
			CALL プレイログ記録, @"%NAME:TARGET%が売却可能になる"
			CFLAG:売却可能 = 1
			;実績:愛撫コマンドのみで売却可能
			SIF 愛撫双:TARGET:0 == 0
				CALL 実績解除, "I've so Happy"
			CALL 経験値取得, TARGET, "売却可能"
		ENDIF
		;さらに従順+欲望+技巧が14以上なら助手可能
		IF 助手可能条件(TARGET) && !CFLAG:助手可能
			PRINTFORMW %NAME:TARGET%が助手可能になりました
			CALL プレイログ記録, @"%NAME:TARGET%が助手可能になる"
			CFLAG:助手可能 = 1
		ENDIF
	ENDIF
ENDIF

;段位認定におけるゲージ処理
SIF ゲームモード() == "段位認定" && !CFLAG:MASTER:使用不可
	CALL 段位ゲージ処理
CALL CHECKDRAWLINE

IF ASSI >= 0
	IF TALENT:ASSI:妖狐 && ITEM:媚薬 == 0 && !CFLAG:MASTER:使用不可
		PRINTFORMW [妖狐]の%CALLNAME:ASSI%が媚薬を調達してきたようです
		ITEM:媚薬++
	ENDIF
ENDIF
CALL CHECKDRAWLINE

CALL ミゼラン変身時間経過処理

FOR LCOUNT, 0, CHARANUM
	;休憩フラグを消すが、配属中のキャラは下記収入関数でフラグ管理してるので対象外
	SIF CSTR:LCOUNT:配属 == ""
		CFLAG:LCOUNT:休憩 = 0
NEXT

IF ターン == "夜"
	LASTMONEY = MONEY
	;主人が行動不可だと収入入らない
	SIF !CFLAG:MASTER:使用不可
		CALL STRATEGY_INCOME
ENDIF
;主人が行動不可だと発生しない
IF !CFLAG:MASTER:使用不可
	CALL TURNEND_IDOL
	CALL TURNEND_出版社
	SIF ターン == "夜"
		CALL TURNEND_監禁室
ENDIF

;畜産農業関係
CALL TURNEND_畜産
CALL TURNEND_農業
;家畜の体調次第で食材が変わるので献立を確認する
CALL 献立条件確認

;温泉
CALL TURNEND_温泉探索

SIF ITEM:実験室
	CALL TURNEND_実験室

;施設の計算と利用人数表示
IF !CFLAG:MASTER:使用不可 && ゲームモード() != "新婚モード"
	SIF ITEM:図書室
		CALL TURNEND_図書室
	SIF ITEM:医務室
		CALL TURNEND_医務室
	CALL CHECKDRAWLINEW
ENDIF

;居住部屋
CALL TURNEND_居住部屋

;一部能力の自動上昇
LTARGET = TARGET
FOR LCOUNT, 1, CHARANUM
	TARGET = LCOUNT
	FOR LCOUNT2, 0, VARSIZE("ABL")
		SELECTCASE ABLNAME:LCOUNT2
			CASE "料理技能", "家事技能", "工作技能", "歌唱技能", "演奏技能", "撮影技能", "舞踊技能", "魔法技能", "飼育技能", "栽培技能", "医療技能"
				SIF ABL:LCOUNT2 < 10
					CALL ABLUP, ABLNAME:LCOUNT2
		ENDSELECT
	NEXT
NEXT
TARGET = LTARGET

;新婚モード以外で所持金が5000万以上なら裏カジノへの招待状が届く
IF ゲームモード() != "新婚モード" && MONEY >= TONUM("5000万") && !FLAG:裏カジノ
	CALL KOJO_MESSAGE_EVENT, "裏カジノ招待"
	CALL プレイログ記録, @"裏カジノへの招待状が届く"
	PRINTW 裏カジノが使用可能になりました
	FLAG:裏カジノ = 1
ENDIF

FOR LCOUNT, 0, CHARANUM
	;GUMINDAYSだけロスト済でもカウント
	SIF GROUPMATCH("GUMIN", CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３)
		GUMINDAYS++
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	CALL 服装プリセット選出, LCOUNT
	;主人が使用不可だと体力・気力・ストレス値等は変動しない
	IF !CFLAG:MASTER:使用不可
		;ミッドナイトスペシャル
		IF CFLAG:LCOUNT:ミッドナイトスペシャル > 0
			SIF --CFLAG:LCOUNT:ミッドナイトスペシャル == 0
				PRINTFORMW %CALLNAME:LCOUNT%のミッドナイトスペシャルの効果が切れました
		ENDIF
		;夜なら貞操帯装着日数増加
		SIF CFLAG:LCOUNT:貞操帯 && ターン == "夜"
			CFLAG:LCOUNT:貞操帯++
		;以下主人は除外
		SIF LCOUNT == MASTER
			CONTINUE
		;休憩フラグが立っていて配属中でない場合はキャラ固有の休憩フラグが立つ
		;配属先でいろいろあって休憩してる場合はそちらで休憩フラグが立っている
		IF FLAG:休憩フラグ
			SIF CSTR:LCOUNT:配属 == ""
				CFLAG:LCOUNT:休憩 = 1
		ENDIF

		;種族妖精ならランダムで消費アイテム取得 休憩中のみ、使用不可や監禁中は除外
		IF CSTR:LCOUNT:種族 == "妖精" && 陥落(LCOUNT) && CFLAG:LCOUNT:休憩 && !CFLAG:LCOUNT:使用不可 && !CFLAG:LCOUNT:監禁
			妖精イベント = RAND(GETNUM(ITEM, "ローション"), GETNUM(ITEM, "魅惑の粘液")+1)
			妖精イベント:1 = RAND(1, 4)
			PRINTFORMW %CALLNAME:LCOUNT%が{妖精イベント:1}個の%ITEMNAME:妖精イベント%を拾ってきました
			ITEM:妖精イベント += 妖精イベント:1
		ENDIF

		;ストレス値の計算
		CALL ストレス値計算, LCOUNT

		;魔剤フラグ消去
		CFLAG:LCOUNT:魔剤 = 0
		;魔剤耐性減少
		IF TALENT:LCOUNT:小人体型
			CFLAG:LCOUNT:魔剤耐性 -= 5
		ELSEIF TALENT:LCOUNT:体格 <= 2
			CFLAG:LCOUNT:魔剤耐性 -= 6
		ELSE
			CFLAG:LCOUNT:魔剤耐性 -= 7
		ENDIF
		;魔剤耐性は1000～2000の間で変動する
		SIF CFLAG:LCOUNT:魔剤耐性 < 1000
			CFLAG:LCOUNT:魔剤耐性 = 1000

		;気力の計算→体力の計算
		CALL 気力計算, LCOUNT
		CALL 体力計算, LCOUNT

		;医務室利用判定をリセット
		CFLAG:LCOUNT:医務室利用 = 0

		;依存度は-1000～5000
		CFLAG:LCOUNT:依存度 = LIMIT(CFLAG:LCOUNT:依存度, -1000, 5000)

		;回復量低下処理
		SIF CFLAG:LCOUNT:回復量低下 > 0
			CFLAG:LCOUNT:回復量低下--

		;狂気度を下げる
		SIF CFLAG:LCOUNT:狂気度 > 0
			CFLAG:LCOUNT:狂気度--
	ENDIF
	;以下は主人使用不可でも処理する
	CFLAG:LCOUNT:二日酔い = 0
	;酔が抜ける 必ずしも1ターンで全快するわけではない
	SELECTCASE ターン
		;昼→夜
		CASE "昼"
			;若いと抜けやすいし年取ると抜けにくい（涙）
			IF TALENT:LCOUNT:児童
				CFLAG:LCOUNT:酔 -= 5
			ELSE
				SIF CFLAG:LCOUNT:酔
					CFLAG:LCOUNT:酔 += 3
			ENDIF
			IF TALENT:LCOUNT:酒豪
				CFLAG:LCOUNT:酔 -= 20+(特質("代謝強化", LCOUNT)/2)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
			ELSEIF TALENT:LCOUNT:下戸
				CFLAG:LCOUNT:酔 -= 5+(特質("代謝強化", LCOUNT)/4)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/5
			ELSE
				CFLAG:LCOUNT:酔 -= 10+(特質("代謝強化", LCOUNT)/3)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
			ENDIF
		;夜→昼
		CASE "夜"
			IF TALENT:LCOUNT:児童
				CFLAG:LCOUNT:酔 -= 10
			ELSE
				SIF CFLAG:LCOUNT:酔
					CFLAG:LCOUNT:酔 += 5
			ENDIF
			IF TALENT:LCOUNT:酒豪
				CFLAG:LCOUNT:酔 -= 50+(特質("代謝強化", LCOUNT)/2)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/2
			ELSEIF TALENT:LCOUNT:下戸
				CFLAG:LCOUNT:酔 -= 10+(特質("代謝強化", LCOUNT)/4)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
			ELSE
				CFLAG:LCOUNT:酔 -= 30+(特質("代謝強化", LCOUNT)/3)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
			ENDIF
			;これで酔いが残っていると二日酔いを引き起こす
			IF CFLAG:LCOUNT:酔 > 0
				CFLAG:LCOUNT:二日酔い = 1
				CFLAG:LCOUNT:酔 = 0
			ENDIF
			;発情周期もここで処理してみる
			;基本26日周期だが5分の1で周期が進まないときがある
			SIF RANDPER(80)
				CFLAG:LCOUNT:発情周期++
			SIF CFLAG:LCOUNT:発情周期 >= 26
				CFLAG:LCOUNT:発情周期 -= 26
	ENDSELECT
	TEQUIP:LCOUNT:ウォッカ = 0
	SIF CFLAG:LCOUNT:酔 < 0
		CFLAG:LCOUNT:酔 = 0

	;使用不可フラグ
	IF CFLAG:LCOUNT:使用不可 != 0
		SIF LCOUNT == MASTER
			CONTINUE
		SIF LCOUNT == TARGET
			TARGET = -1
		SIF LCOUNT == ASSI
			ASSI = -1
		CALL 配属解除, LCOUNT
		CFLAG:LCOUNT:使用不可--
		IF CFLAG:LCOUNT:使用不可 <= 0
			SELECTCASE CFLAG:LCOUNT:使用不可状態
				CASE 0
					PRINTFORMW %CALLNAME:LCOUNT%が再び調教可能になりました
				CASE 1
					PRINTFORMW %CALLNAME:LCOUNT%は体外受精を終えたため、再び調教可能になりました
				CASE 2
					PRINTFORMW %CALLNAME:LCOUNT%は3日間の研修を受け、帰宅しました
					CALL ABLマイナス打ち消し, LCOUNT
			ENDSELECT
			CFLAG:LCOUNT:使用不可 = 0
		ENDIF
	ENDIF
	TEQUIP:LCOUNT:仮死薬 = 0
	TEQUIP:LCOUNT:睡眠 = 0
NEXT
IF TFLAG:料理
	IF !TFLAG:裸体盛り
		CALL KOJO_MESSAGE_EVENT, "料理"
	ELSE
		CALL KOJO_MESSAGE_EVENT, "裸体盛り"
	ENDIF
ENDIF
SIF TFLAG:お菓子
	CALL KOJO_MESSAGE_EVENT, "お菓子"
TFLAG:料理 = 0
TFLAG:お菓子 = 0
TFLAG:裸体盛り = 0

;調理室の処理は回復後に行う
CALL TURNEND_調理室

;スライム化処理
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF CFLAG:LCOUNT:スライム度 <= 0
		CONTINUE
	IF FLAG:抗スライム && !ISSLIME(LCOUNT)
		CFLAG:LCOUNT:スライム度 = 0
		CONTINUE
	ENDIF
	;スライム度は1日1～3ランダムで増加
	CALL スライム度増加, 0, LCOUNT, RAND(1, 4)
	;30を超えるとスライム化する
	IF CFLAG:LCOUNT:スライム度 >= 30 && !ISSLIME(LCOUNT)
		CALL KOJO_MESSAGE_EVENT, "スライム化", LCOUNT
		LOADGLOBAL
		スライム化イベント回数++
		SAVEGLOBAL
		SIF スライム化イベント回数 >= 10
			CALL 実績解除, "そして世界は粘液に満ちた"
		TALENT:LCOUNT:スライム = 1
		CSTR:LCOUNT:種族 = スライム
		CALL CLEAR_FLAG, LCOUNT
	ENDIF
	;1500に到達する事で、もう元には戻れない完全なスライムとなる
	IF CFLAG:LCOUNT:スライム度 >= 1500 && TALENT:LCOUNT:スライム
		CALL KOJO_MESSAGE_EVENT, "完全スライム化", LCOUNT
		TALENT:LCOUNT:溶ける快 = 1
		TALENT:LCOUNT:妄信 = 1
		TALENT:LCOUNT:スライム = 0
		CALL CLEAR_FLAG, LCOUNT
	ENDIF
	SIF CFLAG:LCOUNT:スライム度 >= 1500
		CFLAG:LCOUNT:スライム度 = 1500
NEXT

CALL EVENTCHECK_ABL
FLAG:休憩フラグ = 0

;午後なら次の日、午前なら午後にする
SELECTCASE ターン
	CASE "昼"
		PRINTW 夜になった……
		ターン = 夜
	CASE "夜"
		;日付変更時のイベント呼び出し
		CALL EVENT_NEXTDAY
		;色仕掛けイベント
		SIF !CFLAG:MASTER:使用不可
			CALL EVENT_HONEYTRAP
		;主人が使用不可の場合、期限的な日付加算は行わない
		IF !CFLAG:MASTER:使用不可
			DAY++
			DAY:1 = DAY
		;もし主人が妊娠で使用不可だった場合のために、妊娠フラグを進めるためのDAY:1はカウントする
		ELSE
			DAY:1++
		ENDIF
		PRINTW 一日が終わった……
		;実績：調教対象が居ない状態で夜を過ごす
		SIF TARGET == -1 && !CFLAG:MASTER:使用不可
			CALL 実績解除, "Without you tonight"
		ターン = 昼
		;ゲームモード7(新婚モード)か9(PREMIUM FREE)なら10日に一度、借金の返済・新規借入ができる
		IF GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード") && DAY%10 == 0
			CALL DEBT
		;借金がある場合は10日に一度返済を要求される
		ELSEIF 借金
			CALL DEBT, 1
		ENDIF
		;バッドエンドの判定
		;期限切れ
		SIF (DAY > 100 && ゲームモード() == "通常モード") || (DAY > 300 && ゲームモード() == "経営モード")
			JUMP BADENDING, 1
		;金欠エンド
		;ただしゲームモード7(新婚モード)と9(PREMIUM FREE)では呼び出されない
		IF MONEY < 0 && !GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード")
			;実績:STRATEGYモードで金欠エンドを迎える
			CALL 実績解除, "無能な経営者"
			JUMP BADENDING, 2
		ENDIF
		;朝になった時のイベント呼び出し
		CALL EVENT_NEWDAY
		CALL EVENT_NEWDAY_SELF
ENDSELECT

;GUMINDAYSキャラ5人以上陥落でユリア天使化イベント
SIF GUMINDAYS >= 5
	CALL ユリア天使化

;実績「タルタでラジェ・ガレト・ヴァロ全員の童貞を奪う」
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:童貞 == "タルタ" || CSTR:LCOUNT:性転換童貞 == "タルタ"
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ラジェ"
				SETBIT タルタ童貞喰い, 0
			CASE "ガレト"
				SETBIT タルタ童貞喰い, 1
			CASE "ヴァロ"
				SETBIT タルタ童貞喰い, 2
		ENDSELECT
	ENDIF
NEXT
SIF GETBIT(タルタ童貞喰い, 0) && GETBIT(タルタ童貞喰い, 1) && GETBIT(タルタ童貞喰い, 2)
	CALL 実績解除, "I LOVE SAKURA"

;実績：陥落したイロドリミドリの全員と同棲する
;実績：ユーリ、アッシュ、スマイルの三人を陥落させる
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	IF 陥落(LCOUNT)
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる"
				YORIDORI++
			CASE "ユーリ", "アッシュ", "スマイル"
				DEUIL++
		ENDSELECT
		SIF YORIDORI == 5
			CALL 実績解除, "ヨリドリミドリ"
		SIF DEUIL == 3
			CALL 実績解除, "Deuilの熱烈すぎる追っかけ"
	ENDIF
NEXT

;実績:育江の出産から7年を経過させる
SIF エレジー && (YEAR*10000+MONTH*100+DAYS) - エレジー >= 70000
	CALL 実績解除, "この子の七つのお祝いに"

;実績:6928日過ごす
SIF DAY >= 6928
	CALL 実績解除, "INFINITE PLAYER"

SIF GETCHARANAME("小野 美苗") >= 1 && DAY-美苗チェック >= 365
	CALL 実績解除, "みなえをチェック！してあげて……"

;気温の算出
CALL TEMPERATURE
;特定のキャラがいる場合、気温に補正を加える(ただし昼のみ)
;主人・奴隷・助手の場合の方が影響が強い
IF ターン == "昼"
	FOR LCOUNT, 0, CHARANUM
		SIF CFLAG:LCOUNT:キャラロスト
			CONTINUE
		気温補正 = 0
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ウェルダン"
				気温補正 = 10
			CASE "フローラ"
				気温補正 = 6
			CASE "茜"
				気温補正 = 5
			CASE "烈"
				気温補正 = 3
			CASE "氷海"
				気温補正 = -3
			CASE "スノーフェアリー"
				気温補正 = -6
			CASE "氷雪ちゃん"
				気温補正 = -15
		ENDSELECT
		SIF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
			気温補正 *= 10
		FLAG:気温 += 気温補正
	NEXT
	;魔法の影響
	FLAG:気温 -= フリージングアトモスフィア*30
	FLAG:気温 += バーニンザフロア*30
ENDIF
;天気の決定
CALL WEATHER

FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	;フローラ専用イベント
	IF CHARANAMEF(LCOUNT) == "フローラ"
		最大体力 = MAXBASE:LCOUNT:体力
		MAXBASE:LCOUNT:体力 = CSVBASE(NOF(LCOUNT), 0) + (FLAG:気温-180)*2
		SIF TALENT:LCOUNT:妊娠
			MAXBASE:LCOUNT:体力 -= 500
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-最大体力
		;体力、気力の値整理
		CALL SETBASE, LCOUNT
	ENDIF
	;鳳凰専用イベント
	IF CSTR:LCOUNT:種族 == "鳳凰" && FLAG:気温 > 0
		鳳凰イベント = FLAG:気温
		SELECTCASE 天気()
			CASE "晴れ"
			CASE "曇り"
				TIMES 鳳凰イベント , 0.50
			CASEELSE
				TIMES 鳳凰イベント , 0.20
		ENDSELECT
		;気温と天気で計算したボーナス分体力気力が増加
		鳳凰イベント:1 = MAXBASE:LCOUNT:体力
		鳳凰イベント:2 = MAXBASE:LCOUNT:気力
		MAXBASE:LCOUNT:体力 = MAXBASE:LCOUNT:鳳凰基礎体力+鳳凰イベント
		MAXBASE:LCOUNT:気力 = MAXBASE:LCOUNT:鳳凰基礎気力+鳳凰イベント
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-鳳凰イベント:1
		BASE:LCOUNT:気力 += MAXBASE:LCOUNT:気力-鳳凰イベント:2
		;体力、気力の値整理
		CALL SETBASE, LCOUNT
	ENDIF
NEXT

;主人が使用不可の場合はTARGETとASSIを外してSHOP画面表示し、収入と支出の一切を中断して時間だけ経過させる。DAYは増えないので期限には影響無し
IF CFLAG:MASTER:使用不可 > 0
	TARGET = -1
	ASSI = -1

	;曜日計算
	FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

	;祝日・記念日の設定
	CALL HOLIDAY

	カラム入力無視 = 1
	CALL カラム生成, "SHOP", "メイン"

	IF --CFLAG:MASTER:使用不可 == 0
		PRINTFORMW %CALLNAME:MASTER%が復帰しました。次のターンから再開できます
	ELSE
		PRINTFORMW %CALLNAME:MASTER%の都合により行動を終了します
	ENDIF
	TARGET = -1
	ASSI = -1
	FLAG:休憩フラグ = 1
	RESTART
ENDIF

;魔法の効果終了は最後
CALL TURNEND_魔法効果

VARLINE = LINECOUNT

CALL SYSTEM_AUTOSAVE

;JUMP SHOP画面

;-------------------------------------------------
;日付が変わった時のイベント
;-------------------------------------------------
@EVENT_NEXTDAY
#DIM DYNAMIC LCOUNT

;豹変中のキャラを元に戻す
CALL 豹変イベント, 0

;特定キャラの日付変更時イベント
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	;らいむ専用イベント 陥落済みでどこにも配属していないフリーな状態である場合に発生
	IF CHARANAMEF(LCOUNT) == "高須 らいむ"
		IF 陥落(LCOUNT) && CSTR:LCOUNT:配属 == "" && !CFLAG:LCOUNT:使用不可 && !TALENT:LCOUNT:育児中 && !TALENT:LCOUNT:妊娠
			IF TALENT:LCOUNT:溶ける快
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めると、とろとろにとろけた秘所からはしたなく粘液を溢れさせて瓶に入れ、それを渡してきた。
						PRINTFORML 瓶の中で、%CALLNAME:LCOUNT%の獲れたて粘液が音を立てて蠢いている…　とりあえず保管する事にした。
						PRINTFORMW ＜保管粘液量が100ml増加した＞
						リソース:粘液 += 100
					CASE 1
						PRINTFORML %CALLNAME:LCOUNT%は、もじもじとした様子で%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML どうやら彼女の粘液のようだが、調子が悪いのか微妙に粘度が低いようだ。
						PRINTFORMW ＜この粘液は、ローションとして使わせてもらう事にした＞
						ITEM:ローション++
					CASE 2
						PRINTFORML 熱っぽいような表情をした%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 粘液のようにも見えるが、なんてことのない、ただの媚薬のようだ。
						PRINTFORML 媚薬と自身の粘液を混ぜ合わせる研究をしていたようだが今日のところは失敗に終わったのだろう。
						PRINTFORMW ＜%CALLNAME:LCOUNT%の色をしているだけの媚薬を手に入れた＞
						ITEM:媚薬++
					CASE 3
						PRINTFORML 全身を淫らに震わせながら%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 中で蠢く%CALLNAME:LCOUNT%の粘液と思わしきそれは、密封状態であるにも関わらず濃厚な甘い香りを漂わせている。
						PRINTFORML %CALLNAME:LCOUNT%は、にちゃぁと水音を立てて笑うと上機嫌で自室へと戻っていった…
						PRINTFORMW ＜%CALLNAME:LCOUNT%手製の魅惑の粘液を２回分手に入れた＞
						ITEM:魅惑の粘液 += 2
				ENDSELECT
			ELSEIF TALENT:LCOUNT:スライム
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めると、秘所からとろりとした粘液を垂らして瓶に入れ、それを渡してきた。
						PRINTFORML 瓶の中で、%CALLNAME:LCOUNT%の獲れたて粘液が蠢いている…　とりあえず保管する事にした。
						PRINTFORMW ＜保管粘液量が30ml増加した＞
						リソース:粘液 += 30
					CASE 1
						PRINTFORML %CALLNAME:LCOUNT%は、もじもじとした様子で%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML どうやら彼女の粘液のようだが、粘度が低い。ローションの代わりとしてなら使えそうだ。
						PRINTFORMW ＜この粘液は、ローションとして使わせてもらう事にした＞
						ITEM:ローション++
					CASE 2
						PRINTFORML 熱っぽいような表情をした%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 粘液のようにも見えるが、なんてことのない、ただの媚薬のようだ。
						PRINTFORML 媚薬と自身の粘液を混ぜ合わせる研究をしていたようだが今日のところは失敗に終わったのだろう。
						PRINTFORMW ＜%CALLNAME:LCOUNT%の色をしているだけの媚薬を手に入れた＞
						ITEM:媚薬++
					CASE 3
						PRINTFORML ほんのりと甘い香りを漂わせた%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 中で蠢く%CALLNAME:LCOUNT%の粘液と思わしきそれは、密封状態であるにも関わらず甘い香りを漂わせている。
						PRINTFORML %CALLNAME:LCOUNT%は、満足そうに自室へと戻っていった…
						PRINTFORMW ＜%CALLNAME:LCOUNT%手製の魅惑の粘液を手に入れた＞
						ITEM:魅惑の粘液++
				ENDSELECT
			ELSE
				PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めて、研究過程で生じた副産物として、スライムの粘液を渡してきた。
				PRINTFORML 瓶の中で、粘液が微かに蠢いている…　とりあえず保管する事にした。
				PRINTFORMW ＜保管粘液量が10ml増加した＞
				リソース:粘液 += 10
			ENDIF
		ENDIF
	ENDIF
NEXT


;スライム奴隷の処女膜再生イベント
;スライム化しているキャラであり、かつ処女を喪失しており、再生処女を持たない場合
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	IF ISSLIME(LCOUNT) && TALENT:LCOUNT:処女 == 0 && TALENT:LCOUNT:再生処女 == 0 && VAGINA(LCOUNT)
		TALENT:LCOUNT:再生処女 = 1
		PRINTFORMW ＜%CALLNAME:LCOUNT%は、スライムの身体が持つ再生力で失った処女膜を再生させた＞
	ENDIF
NEXT

;奴隷の幼児退行イベント
[SKIPSTART]
IF TARGET >= 0
	;反発刻印3があり、かつ幼稚である時に
	IF TALENT:幼稚 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が7以上になる
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && EXP:異常経験 >= 7
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	;反発刻印3があり、かつ幼稚がない時
	ELSEIF TALENT:幼稚 == 0 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が9以上に加え露出癖5とおもらし癖がある
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && ABL:露出癖 >= 5 && EXP:異常経験 >= 9 && TALENT:おもらし癖 == 1
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	ENDIF
ENDIF
[SKIPEND]

;-------------------------------------------------
;朝になった時のイベント
;-------------------------------------------------
@EVENT_NEWDAY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 治療コスト
#DIM DYNAMIC 関数カウント
#DIMS DYNAMIC 関数名, 1000
#DIM DYNAMIC ばらつき
#DIM DYNAMIC 排卵進行度合い

;日付加算関数
CALL NEXTDAY

ENUMFUNCBEGINSWITH "KOJO_DAYSTART_"
関数カウント = RESULT
ARRAYCOPY "RESULTS", "関数名"
FOR LCOUNT, 0, 関数カウント
	CALLFORM %関数名:LCOUNT%
NEXT

;祝日・記念日の処理はSHOPで行うようにしました

;定休日の場合は休みにする
;営業フラグが-1の場合はコマンドでの休業中なのでオープンしない
IF GC定休日() || GC:営業中 == -1
	SIF GC:営業中 != -1
		GC:営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "ゲームセンター"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF GC:営業中 != -1
	GC:営業中 = DAY-1
ENDIF

IF BR定休日() || BR:営業中 == -1
	SIF BR:営業中 != -1
		BR:営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "娼館"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF BR:営業中 != -1
	BR:営業中 = DAY-1
ENDIF

IF INN定休日() || INN:営業中 == -1 || INNS:業種 == ""
	SIF INN:営業中 != -1
		INN:営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "飲食店"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF INN:営業中 != -1
	INN:営業中 = DAY-1
ENDIF

;実績:新年を迎える
SIF MONTH == 1 && DAYS == 1
	CALL 実績解除, "あけおめアゲイ～ン"

;放尿経験100以上でおもらし癖がつく
;[幼稚]なら放尿経験30以上でつく
;アイテムで治した場合は発生しない
IF TARGET >= 0
	IF !TALENT:おもらし癖 && !CFLAG:おもらし改善フラグ
		IF (TALENT:幼稚 && EXP:放尿経験 >= 30) || (!TALENT:幼稚 && EXP:放尿経験 >= 100)
			CALL CHECKDRAWLINE
			CALL KOJO_MESSAGE_EVENT, "おもらし癖"
			TALENT:おもらし癖 = 1
		ENDIF
	ENDIF
ENDIF

;調合知識習得イベント
IF ASSI > 0 && !TALENT:MASTER:調合知識
	;助手が調合知識持ちで陥落済み
	IF TALENT:ASSI:調合知識 && 陥落(ASSI)
		;技巧LV3以上 奉仕精神LV3以上
		IF ABL:ASSI:技巧 >= 3 && ABL:ASSI:奉仕精神 >= 3
			;主人の技巧LV4以上
			IF ABL:MASTER:技巧 >= 4
				CALL CHECKDRAWLINE
				CALL KOJO_MESSAGE_EVENT, "調合知識伝授", ASSI
				TALENT:MASTER:調合知識 = 1
				CFLAG:ASSI:主人に調合知識を伝授した経験++
			ENDIF
		ENDIF
	ENDIF
ENDIF

;全体の妊娠発覚時のイベント
CALL GET_CHILD
;出産準備のイベント
CALL PREG_READY

;排卵周期を回す処理
;排卵誘発剤の効果終了と、不妊治療薬のコスト処理
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	IF MOD:排卵周期
		IF 生殖機能♀(LCOUNT)
			IF 排卵からの経過時間:LCOUNT:0 <= 0
				排卵からの経過時間:LCOUNT:0 = 1+RAND:280
			ELSE
				ばらつき = 50
				;排卵周期は平均で10進むが、さまざまな要因で進む値にブレが生じてくる
				;体格が小さかったりストレスが大きかったりするとブレが大きくなる
				;正規分布関数RAND_Nは、95%の確率で-196～+196、99%の確率で-258～+258をとる(RAND_Nの取る範囲は-387～+387)
				;RAND_Nでとった値に変数「ばらつき」を掛けて10000で割ってブレを出す
				;デフォルトのばらつき50なら95%強の確率で全くブレない			
				IF TALENT:LCOUNT:小人体型
					ばらつき += 133
				ELSE
					SELECTCASE TALENT:LCOUNT:体格
						CASE 1
							ばらつき += 111
						CASE 2
							ばらつき += 44
					ENDSELECT
				ENDIF
				;ストレス値10につき、ばらつき1増加(小数切り上げ)
				ばらつき += (CFLAG:LCOUNT:ストレス値+9)/10

				;ここから増加処理
				排卵からの経過時間:LCOUNT:0 += 10
				排卵からの経過時間:LCOUNT:0 += RAND_N()*ばらつき/10000
				;1～280の値をとるので、280より大きくなったら280を引く
				SIF 排卵からの経過時間:LCOUNT:0 > 280
					排卵からの経過時間:LCOUNT:0 -= 280
			ENDIF
			;排卵日とその直前(妊娠確率最大)ではない10～260であるとき、排卵誘発剤・排卵マッサージに応じて260まで割合で進行させる
			IF INRANGE(排卵からの経過時間:LCOUNT:0, 10, 260)
				;単位は%、100%なら260まで進行する
				排卵進行度合い = 0
				SIF CFLAG:LCOUNT:排卵誘発剤
					排卵進行度合い = 80
				SIF CFLAG:LCOUNT:排卵マッサージ
					排卵進行度合い += 3*CFLAG:LCOUNT:排卵マッサージ
				SIF 排卵進行度合い > 0
					排卵進行度合い += RAND:41 - 20;-20～+20
				LIMIT 排卵進行度合い, 0, 100
				SIF 排卵進行度合い
					排卵からの経過時間:LCOUNT:0 += (260-排卵からの経過時間:LCOUNT:0)*排卵進行度合い/100
			ENDIF
			;ただし、妊娠確定していて140以上の値をとっているとき139(生理直前)で止まる
			SIF PREG:LCOUNT:妊娠確定フラグ && 排卵からの経過時間:LCOUNT:0 >= 140
				排卵からの経過時間:LCOUNT:0 = 139
		ENDIF
	ENDIF
	IF TALENT:LCOUNT:初潮前
		;1日ごとに[経過日数]/4000の確率で初潮を迎える
		;確率は25日で約8.8%、50日で約27.4%、75日で約51.2%、100日で約72.0%
		IF RAND:4000 < TALENT:LCOUNT:初潮前
			PRINTFORMW %CALLNAME:LCOUNT%が初潮を迎えたようだ
			排卵からの経過時間:LCOUNT:0 = 140
			TALENT:LCOUNT:初潮前 = 0
			TALENT:LCOUNT:排卵不安定 = 1
		ELSE
			TALENT:LCOUNT:初潮前++
		ENDIF
	ENDIF
	IF CFLAG:LCOUNT:排卵誘発剤 > 0
		SIF --CFLAG:LCOUNT:排卵誘発剤 <= 0
			PRINTFORMW %CALLNAME:LCOUNT%の排卵誘発剤の効果が切れたようだ
	ENDIF
	IF CFLAG:LCOUNT:生殖力強化 == 1
		治療コスト = 300
		SIF TALENT:LCOUNT:体格 <= 2
			治療コスト += 200
		SIF !ORIGIN
			CALL PASELIW, @"%CALLNAME:LCOUNT%の不妊治療薬に{治療コスト}＄の費用が発生しました"
		CALL 現金出納帳記録, @"%NAME:LCOUNT%の不妊治療/-{治療コスト}"
		MONEY -= 治療コスト
		CFLAG:LCOUNT:ストレス値 += 5
	ENDIF
NEXT

;懸賞が届く 現状は必ず当選する
IF FLAG:ナンプレ懸賞
	IF --FLAG:ナンプレ懸賞 <= 0
		PRINTL 応募していた懸賞の景品が届いた
		PRINTW 「ミッドナイトスペシャル」を手に入れた
		ITEM:ミッドナイトスペシャル++
	ENDIF
ENDIF

IF !ORIGIN
	;毎月1日に仕送りが届く
	IF DAYS == 1 && FLAG:仕送り
		CALL PASELIW, @"%CALLNAME:MASTER%の子供たちから{FLAG:仕送り*30000}＄の仕送りを受け取りました"
		CALL 現金出納帳記録, @"子供たちからの仕送り/{FLAG:仕送り*30000}"
		MONEY += FLAG:仕送り*30000
	ENDIF
ENDIF
;コナステの決済
IF (コナステ/10000 != YEAR || コナステ%10000/100 != MONTH)
	IF ITEM:コナステベーシックコース
		IF コナステ%100 == DAYS
			SELECTCASE ITEM:コナステベーシックコース
				CASE 1
					ITEM:コナステベーシックコース = 0
				CASE 2
					CALL PASELIW, @"コナステの利用料金{ITEMPRICE:コナステベーシックコース}＄を支払いました"
					CALL 現金出納帳記録, @"コナステ決済/{ITEMPRICE:コナステベーシックコース}"
					MONEY -= ITEMPRICE:コナステベーシックコース
					コナステ = YEAR*10000+MONTH*100+DAYS
			ENDSELECT
		;29～31が無ければ1日に決済
		ELSEIF MONTH != 1 && DAYS == 1 && !EXIST_DAY(YEAR, MONTH-1, コナステ%100)
			SELECTCASE ITEM:コナステベーシックコース
				CASE 1
					ITEM:コナステベーシックコース = 0
				CASE 2
					CALL PASELIW, @"コナステの利用料金{ITEMPRICE:コナステベーシックコース}＄を支払いました"
					CALL 現金出納帳記録, @"コナステ決済/{ITEMPRICE:コナステベーシックコース}"
					MONEY -= ITEMPRICE:コナステベーシックコース
					コナステ = YEAR*10000+MONTH*100+DAYS
			ENDSELECT
		ENDIF
	ENDIF
ENDIF

;人気度は毎日1下がる 上限は1000
FOR LCOUNT, 0, 20000
	人気度:LCOUNT = LIMIT(人気度:LCOUNT-1, 0, 1000)
NEXT


;-------------------------------------------------
;豹変イベント関連
;-------------------------------------------------
;;;調教の場にいれば1 一応睡眠と死亡をここで弾いておく
@豹変可能, 対象
#FUNCTION
#DIM DYNAMIC 対象
SIF 対象 == -1
	RETURNF 0
SIF CFLAG:対象:死亡 || TEQUIP:対象:睡眠
	RETURNF 0
;既に弾いてるからASSI不在に一致することはないはず
IF GROUPMATCH(対象, TARGET, MASTER, ASSI)
	RETURNF 1
ENDIF
RETURNF 0

;;;豹変キャラの追加はこの関数内と下のTRANCE_ONだけで完結する
@豹変イベント, オン
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC オン
#DIM DYNAMIC 対象
#DIM DYNAMIC 豹変フラグ

FOR LCOUNT, 0, 3
	豹変フラグ = 0
	SELECTCASE LCOUNT
		CASE 0
			対象 = TARGET
		CASE 1
			対象 = MASTER
		CASE 2
			対象 = ASSI
	ENDSELECT
	SIF 対象 == -1
		CONTINUE

	IF オン && 豹変可能(対象) && !TRANCEFLAG:対象:豹変中
		;キャラごとの条件を設定
		SELECTCASE CHARANAMEF(対象)
			CASE "紅刃"
				SIF RANDPER(20)
					豹変フラグ = 1
			CASE "青雨 冷音"
				SIF GROUPMATCH(天気(), "雨", "大雨", "雷雨")
					豹変フラグ = 1
			CASE "シュープリス"
				SIF RANDPER(50)
					豹変フラグ = 1
		ENDSELECT
		IF 豹変フラグ
			IF CHARANAMEF(対象) == "シュープリス"
				PRINTFORMW ＜%CALLNAME:対象%は記憶を思い出そうと同時に湧き上がる破壊衝動で性格が豹変した……＞
			ELSE
				PRINTFORMW ＜%CALLNAME:対象%は性格が豹変した＞
			ENDIF
			CALL TRANCE_ON, 対象
			TRANCEFLAG:対象:豹変中 = 1
		ENDIF
	ELSE
		IF TRANCEFLAG:対象:豹変中
			PRINTFORMW ＜%CALLNAME:対象%は元の性格に戻った＞
			;冷音は気力を元に戻す
			SIF CHARANAMEF(対象) == "青雨 冷音"
				MAXBASE:対象:気力 = TRANCEFLAG:対象:気力保存
			;変更されている素質を全て元に戻す 今のところ他の素質変化システム（獣変化とミゼランとハート）とは競合しないので不都合はないはず
			FOR LCOUNT, 0, VARSIZE("TALENT")
				SIF TRANCE:対象:LCOUNT
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 999
			NEXT
			TRANCEFLAG:対象:豹変中 = 0
		ENDIF
	ENDIF
	CALL CHECKDRAWLINE
NEXT

;;;長くなるので変動する素質はこちらで指定
@TRANCE_ON, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
SELECTCASE CHARANAMEF(対象)
	CASE "紅刃"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "臆病", "大人しい", "保守的", "一線越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
	CASE "青雨 冷音"
		;雨が降ると気力にボーナス 無条件に700増えるほかに、Y=1000/XのグラフでのX=1～X=[雨量]までの面積
		;つまり1000LOG([雨量])だけ増加する ただし、整数への切り捨ての関係で100LOG([雨量]^10)としている
		;雨量に対する気力の参考値 雨量1:1700 雨量2:2300 雨量3:2700 雨量4:3000 雨量5:3300 雨量10:4000 雨量15:4400 雨量20:4600 雨量78以上:6000
		TRANCEFLAG:対象:気力保存 = MAXBASE:対象:気力
		MAXBASE:対象:気力 += 700
		MAXBASE:対象:気力 += 100*LOG(POWER(LIMIT(FLAG:降水量, 1, 78), 10))
		BASE:対象:気力 = MAXBASE:対象:気力

		;以下素質処理
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "大人しい", "無関心", "一線越えない", "貞操観念", "恥じらい"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
	CASE "シュープリス"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "臆病", "プライド低い", "弱気", "寂しがり屋", "自制心", "保守的", "悲観的", "抑圧", "恥じらい", "痛みに弱い", "回復遅い", "習得遅い"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "サド", "無関心", "強気", "痛みに強い", "好奇心", "生意気", "倒錯的", "戦士"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
ENDSELECT

;;;TRANCE変数をオンオフする（要改良）
;;;1で習得、0で消滅 既に習得している素質に習得処理を行った場合などはその状態が保存されて消滅処理を行った時フラグを折るだけになる
;;;999で強制的に元に戻す 変身時に1か0を指定して解除時に全て999を指定など
@TRANCETALENT, 対象, 素質名, 数値
#DIM DYNAMIC 対象
#DIMS DYNAMIC 素質名
#DIM DYNAMIC 数値

IF 数値 == 999
	IF TRANCE:対象:TALENTF(素質名) == -1
		TRANCE:対象:TALENTF(素質名) = 0
	ELSEIF 素質名 == "バストサイズ" || 素質名 == "体格"
		TALENT:対象:TALENTF(素質名) = TRANCE:対象:TALENTF(素質名)
	ELSEIF TRANCE:対象:TALENTF(素質名) == 1
		INVERTBIT TALENT:対象:TALENTF(素質名), 0
	ENDIF
	TRANCE:対象:TALENTF(素質名) = 0
	RETURN
ENDIF
SELECTCASE 素質名
	CASE "バストサイズ"
		SELECTCASE TRANCE:対象:TALENTF("バストサイズ")
			CASE -1
				TALENT:対象:バストサイズ = 0
				TRANCE:対象:TALENTF("バストサイズ") = 0
			CASE 0
				;男のバストサイズ無しの場合は-1にする
				TRANCE:対象:TALENTF("バストサイズ") = TALENT:対象:バストサイズ > 0 ? TALENT:対象:バストサイズ # -1
				TALENT:対象:バストサイズ = 数値
			CASEELSE
				TALENT:対象:バストサイズ = TRANCE:対象:TALENTF("バストサイズ")
				TRANCE:対象:TALENTF("バストサイズ") = 0
		ENDSELECT
	CASE "体格"
		IF TRANCE:対象:TALENTF("体格")
			TALENT:対象:体格 = TRANCE:対象:TALENTF("体格")
			TRANCE:対象:TALENTF("体格") = 0
		ELSE
			TRANCE:対象:TALENTF("体格") = TALENT:対象:体格
			TALENT:対象:体格 = 数値 > 0 ? 数値 # 3
		ENDIF
	CASEELSE
		IF TRANCE:対象:TALENTF(素質名) == -1
			TRANCE:対象:TALENTF(素質名) = 0
		ELSE
			IF TALENT:対象:TALENTF(素質名) != 数値
				TALENT:対象:TALENTF(素質名) = 数値
				INVERTBIT TRANCE:対象:TALENTF(素質名), 0
			ELSE
				 TRANCE:対象:TALENTF(素質名) = -1
			ENDIF
		ENDIF
ENDSELECT

;-------------------------------------------------
;ユリア天使化
;-------------------------------------------------
@ユリア天使化
#DIM DYNAMIC LTARGET

IF GETCHARANAME("鬼蝮 ユリア") >= 0 && !CFLAG:GETCHARANAME("鬼蝮 ユリア"):キャラロスト
	LTARGET = TARGET
	TARGET = GETCHARANAME("鬼蝮 ユリア")
	CALL KOJO_MESSAGE_EVENT, "天使化"
	PRINTW 鬼蝮 ユリアは大天使ユリアになった
	NAME:TARGET = 大天使ユリア
	SIF EXP:歌唱経験 < 200
		EXP:歌唱経験 = 200
	SIF EXP:ダンス経験 < 100
		EXP:ダンス経験 = 50
	SIF ABL:歌唱技能 < 5
		ABL:歌唱技能 = 5
	SIF ABL:舞踊技能 < 2
		ABL:舞踊技能 = 2
	SIF ABL:魔法技能 < 3
		ABL:魔法技能 = 3
	SIF MAXBASE:魔力 < 3000
		MAXBASE:魔力 = 3000
	TALENT:保守的 = 1
	TALENT:薬毒耐性 = 1
	TALENT:献身的 = 1
	TALENT:両刀 = 1
	TALENT:魅惑 = 1
	TALENT:統率 = 1
	TALENT:羽 = 1
	TALENT:回復早い = 1
	;TALENT:治療 = 1
	TALENT:母性 = 1
	TALENT:魔法使い = 1
	TALENT:絶対音感 = 1
	TARGET = LTARGET
ENDIF

;-------------------------------------------------
;女主人による色仕掛けイベント
;-------------------------------------------------
@EVENT_HONEYTRAP
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ポイント

ポイント = 0
;調教後夜這いが発生してたら発生しない
IF TFLAG:調教後夜這い
	TFLAG:調教後夜這い = 0
	RETURN 0
ENDIF

;ペニス使用不可では発生しない
SIF Ｐ使用不可(TARGET)
	RETURN 0

SIF !OPTION:色仕掛け
	RETURN 0
SIF TARGET <= 0
	RETURN 0
SIF TALENT:MASTER:性別 != 2
	RETURN 0

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "生意気", "無関心", "貞操観念", "抑圧", "抵抗", "冷静", "強気"
			ポイント -= 1
		CASE "素直", "衝動的", "弱味", "弱気", "献身的", "妄信"
			ポイント += 1
		CASE "反抗的", "ツンデレ", "自制心", "一線越えない", "快感の否定"
			ポイント -= 2
		CASE "好奇心", "貞操無頓着", "解放", "快感に素直", "女好き"
			ポイント += 2
		CASE "潔癖症", "汚臭敏感"
			SIF TALENT:MASTER:ファーリー
				ポイント -= 2
		CASE "同族嫌悪"
			SIF CSTR:種族 == CSTR:MASTER:種族
				ポイント -= 2
	ENDSELECT
NEXT

IF TALENT:性別 == 2
	IF TALENT:両刀
		ポイント += 1
	ELSE
		ポイント -= 2
	ENDIF
ENDIF
IF TALENT:恋心
	IF CSTR:恋心 != CSVNAMEF(NOF(MASTER), 1) && !陥落(TARGET)
		ポイント -= 3
	ELSEIF CSTR:恋心 == CSVNAMEF(NOF(MASTER), 1)
		ポイント += 3
	ENDIF
ENDIF
IF TALENT:既婚者
	IF CSTR:既婚者 != CSVNAMEF(NOF(MASTER), 1) && !陥落(TARGET)
		ポイント -= 5
	ELSEIF CSTR:既婚者 == CSVNAMEF(NOF(MASTER), 1)
		ポイント += 5
	ENDIF
ENDIF

SIF TALENT:MASTER:魅力
	ポイント += 1
SIF TALENT:MASTER:魅惑
	ポイント += 1
SIF TALENT:MASTER:謎の魅力
	ポイント += 2
SIF TALENT:MASTER:体格 <= 2 && !TALENT:子供好き
	ポイント -= 1
SIF ヒプノティッククライシス
	ポイント += RAND(15, 21)

ポイント += ABL:従順
ポイント += ABL:欲望
ポイント += ABL:奉仕精神
ポイント -= MARK:反発刻印*2
ポイント -= MARK:苦痛刻印

SIF 陥落(TARGET)
	ポイント += 20

;地の文分岐に使う
TFLAG:誘惑ポイント = ポイント

CALL CHECKDRAWLINE
PRINTPLAINFORM 就寝間際、寝付こうとしている%CALLNAME:TARGET%の姿を目撃した 
PRINTL [0] イベント非表示 オプションから再度表示出来ます
PRINTL [1] 夜這いをかける
PRINTL [2] 誘惑する
PRINTL [3] 今日はやめとく
BINPUT
SELECTCASE RESULT
	CASE 0
		OPTION:色仕掛け = 0
		RETURN 0
	CASE 1
		IF TFLAG:誘惑ポイント >= 12
			TFLAG:色仕掛け = 1
		ELSE
			TFLAG:色仕掛け = 2
		ENDIF
	CASE 2
		IF TFLAG:誘惑ポイント >= 16
			TFLAG:色仕掛け = 3
		ELSE
			TFLAG:色仕掛け = 4
		ENDIF
	CASE 3
		RETURN 0
ENDSELECT

;夜這い
CALL KOJO_MESSAGE_EVENT, "色仕掛け"

SELECTCASE TFLAG:色仕掛け
	CASE 1
		SIF TFLAG:誘惑ポイント < 1
			TFLAG:誘惑ポイント = 1
		;童貞喪失処理
		IF PENIS(TARGET) && TALENT:童貞
			PRINTFORML ＜%CALLNAME:TARGET%童貞喪失＞
			TALENT:童貞 = 0
			IF TALENT:性転換済
				CSTR:性転換童貞 = %NAME:MASTER%
			ELSE
				CSTR:童貞 = %NAME:MASTER%
			ENDIF
		ENDIF
		;主人の処女喪失処理
		IF TALENT:MASTER:処女 && PENIS(TARGET)
			PRINTFORML ＜%CALLNAME:MASTER%処女喪失＞
			TALENT:MASTER:処女 = 0
			IF TALENT:MASTER:性転換済
				CSTR:MASTER:性転換処女 = %NAME:TARGET%
			ELSE
				CSTR:MASTER:処女 = %NAME:TARGET%
			ENDIF
		ENDIF
		IF PENIS(TARGET)
			中出し回数:(NOF(MASTER))++
			中出され回数:MASTER:(NOF(TARGET))++
			CALL EXPUP, "射精経験", 2
			CALL EXPUP, "絶頂経験", 2
			CALL EXPUP, "Ｖ経験", 1, MASTER
			CALL EXPUP, "膣射経験", 1, MASTER
		ELSE
			CALL EXPUP, "絶頂経験", 3
		ENDIF
		IF PENIS(MASTER)
			CALL EXPUP, "精液経験", 1
			CALL EXPUP, "射精経験", 1, MASTER
			CALL EXPUP, "絶頂経験", 1, MASTER
		ENDIF
		IF TALENT:性別 == 2
			CALL EXPUP, "レズ経験", 3
			CALL EXPUP, "レズ経験", 3, MASTER
		ENDIF
		IF PENIS(TARGET)
			CALL EXPUP, "性交経験", 1
			CALL EXPUP, "性交経験", 1, MASTER
			経験回数:MASTER:(NOF(TARGET))++
			経験回数:(NOF(MASTER))++
		ENDIF
		CALL EXPUP, "主人調教経験", 2
		CFLAG:依存度 += TFLAG:誘惑ポイント
		PRINTL ＜依存度増加＞
		CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*50
		CALL JUELUP, "恭順", TFLAG:誘惑ポイント*50
		CALL JUELUP, "欲情", TFLAG:誘惑ポイント*70
		CALL JUELUP, "屈服", TFLAG:誘惑ポイント*100
	CASE 2
		IF TFLAG:誘惑ポイント > 0
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
			CALL JUELUP, "恐怖", TFLAG:誘惑ポイント*30
		ENDIF
	CASE 3
		SIF TFLAG:誘惑ポイント < 1
			TFLAG:誘惑ポイント = 1
		;童貞喪失処理
		IF PENIS(TARGET) && TALENT:童貞
			PRINTFORML ＜%CALLNAME:TARGET%童貞喪失＞
			TALENT:童貞 = 0
			IF TALENT:性転換済
				CSTR:性転換童貞 = %NAME:MASTER%
			ELSE
				CSTR:童貞 = %NAME:MASTER%
			ENDIF
		ENDIF
		;主人の処女喪失処理
		IF TALENT:MASTER:処女 && PENIS(TARGET)
			PRINTFORML ＜%CALLNAME:MASTER%処女喪失＞
			TALENT:MASTER:処女 = 0
			IF TALENT:MASTER:性転換済
				CSTR:MASTER:性転換処女 = %NAME:TARGET%
			ELSE
				CSTR:MASTER:処女 = %NAME:TARGET%
			ENDIF
		ENDIF
		IF PENIS(TARGET)
			中出し回数:(NOF(MASTER)) += 2
			中出され回数:MASTER:(NOF(TARGET)) += 2
			CALL EXPUP, "射精経験", 2
			CALL EXPUP, "絶頂経験", 2
			CALL EXPUP, "Ｖ経験", 2, MASTER
			CALL EXPUP, "膣射経験", 2, MASTER
		ELSE
			CALL EXPUP, "絶頂経験", 3
		ENDIF
		IF PENIS(MASTER)
			CALL EXPUP, "精液経験", 2
			CALL EXPUP, "射精経験", 2, MASTER
			CALL EXPUP, "絶頂経験", 2, MASTER
		ENDIF
		IF TALENT:性別 == 2
			CALL EXPUP, "レズ経験", 5
			CALL EXPUP, "レズ経験", 5, MASTER
		ENDIF
		IF PENIS(TARGET)
			CALL EXPUP, "性交経験", 2
			CALL EXPUP, "性交経験", 2, MASTER
			経験回数:MASTER:(NOF(TARGET)) += 2
			経験回数:(NOF(MASTER)) += 2
		ENDIF
		CALL EXPUP, "奉仕快楽経験", 3
		CFLAG:依存度 += TFLAG:誘惑ポイント*3/2
		PRINTL ＜依存度増加＞
		CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*70
		CALL JUELUP, "恭順", TFLAG:誘惑ポイント*100
		CALL JUELUP, "欲情", TFLAG:誘惑ポイント*150
		CALL JUELUP, "屈服", TFLAG:誘惑ポイント*50
		CALL JUELUP, "習得", TFLAG:誘惑ポイント*70
	CASE 4
		IF TFLAG:誘惑ポイント > 0
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
			CALL JUELUP, "恥情", TFLAG:誘惑ポイント*30
		ENDIF
ENDSELECT
RETURN
