;奴隷からの奉仕などのイベント
;-------------------------------------------------
;朝に起こるイベント
;-------------------------------------------------
@EVENT_NEWDAY_SELF
#DIM DYNAMIC 不発確率
#DIM DYNAMIC 不発
#DIM DYNAMIC 分岐
#DIM DYNAMIC LCOUNT

;調教対象が居ない場合は終了
SIF TARGET <= 0
	RETURN 0
;抑制してる場合も終了
SIF CFLAG:奉仕イベント抑制
	RETURN 0

;DISABLEチェック
CALL EVENT_CHECK, "朝イベント"
SIF RESULT == 0
	RETURN 0

;アニマルで懐いてる場合は朝に甘えるイベント
IF TALENT:アニマル && CFLAG:依存度 >= 1500
	CALL EVENT_CHECK, "アニマル"
	RETURN
ENDIF

;陥落素質が無い場合は関数終了
SIF !陥落(TARGET) || TALENT:アニマル
	RETURN 0

;対象の性経験が乏しい（経営のパートナーなど）の場合は関数終了
SIF ABL:欲望 < 2 || ABL:技巧 < 2 || ABL:従順 < 2
	RETURN 0

;主人が射精ができない場合は関数終了
SIF !PENIS(MASTER)
	RETURN 0

;対象が二日酔い起こしてると発生しない
SIF CFLAG:二日酔い
	RETURN 0

;上位イベントの条件満たしている場合に各奉仕イベントがランダムで発生するように
IF MOD:朝パイズリ特化 && ゲームモード() != "段位認定"
	分岐 = 1
ELSE
	分岐 = RAND:3
ENDIF
DO
	SELECTCASE 分岐
		CASE 0
			;朝騎乗位判定
			;主人童貞だと発生しない（情け）
			IF TALENT:MASTER:童貞
				分岐 = RAND:3
				CONTINUE
			ENDIF
			;出産トラウマがある場合は発生しない時がある
			不発確率 = CFLAG:出産トラウマ
			;RAND:(トラウマ値/10+2) <= (トラウマ値/10)だと発生しない 1あるだけでも50%で不発
			IF ((!TALENT:処女 && !TALENT:再生処女 && ABL:Ｖ感覚 >= 3) || ABL:Ａ感覚 >= 3) && ABL:奉仕精神 >= 7
				IF 不発確率 && RAND:(不発確率/10+2) <= 不発確率/10
					不発 = 1
				ELSEIF !不発
					CALL EVENT_CHECK, "朝騎乗位"
					BREAK
				ENDIF
			ENDIF
		CASE 1
			;朝パイズリ判定
			IF ABL:奉仕精神 >= 3 && TALENT:バストサイズ >= 1 && TALENT:バストサイズ+ABL:奉仕精神 >= 7
				CALL EVENT_CHECK, "朝パイズリ"
				BREAK
			ENDIF
		CASE 2
			;朝フェラ判定
			CALL EVENT_CHECK, "朝フェラ"
			BREAK
	ENDSELECT
	分岐 = RAND:3
LOOP 1

SELECTCASE FALLTYPE(TARGET)
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%たちは\@ ゲームモード() == "新婚モード" ? 朝から濃厚な # 調教そっちのけで \@情事に耽っている……
		CALL EXPUP, "愛情経験", 5
		CALL JUELUP, "恭順", 250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			CALL EXPUP, "ゲイ経験", 5
			CALL EXPUP, "ゲイ経験", 5, MASTER
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			CALL EXPUP, "レズ経験", 5
			CALL EXPUP, "レズ経験", 5, MASTER
		ENDIF
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の腕を離さずに次の夢へと誘っている……
		CALL EXPUP, "精液経験", 5
		CALL JUELUP, "欲情", 250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			CALL EXPUP, "ゲイ経験", 5
			CALL EXPUP, "ゲイ経験", 5, MASTER
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			CALL EXPUP, "レズ経験", 5
			CALL EXPUP, "レズ経験", 5, MASTER
		ENDIF
	CASE 3
		PRINTFORMW %CALLNAME:TARGET%は今朝も%CALLNAME:MASTER%への忠誠を誓っている……
		CALL EXPUP, "主人調教経験", 20
		CALL JUELUP, "屈服", 250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			CALL EXPUP, "ゲイ経験", 5
			CALL EXPUP, "ゲイ経験", 5, MASTER
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			CALL EXPUP, "レズ経験", 5
			CALL EXPUP, "レズ経験", 5, MASTER
		ENDIF
ENDSELECT

CALL CHECKDRAWLINEW

@EVENT_朝フェラ
#DIM DYNAMIC フェラ回数
#DIM DYNAMIC 射精回数

CALL CHECKDRAWLINE
CALL KOJO_MESSAGE_EVENT, "朝フェラ"

;ここからフェラ回数の計算
フェラ回数 = 相性値(TARGET, MASTER)/100
フェラ回数 += ABL:精液中毒

SIF TALENT:汚臭鈍感
	フェラ回数++
SIF TALENT:汚臭敏感
	フェラ回数--
SIF TALENT:潔癖症
	フェラ回数--
SIF TALENT:献身的
	フェラ回数++
SIF TALENT:淫乱
	フェラ回数 += 2
SIF TALENT:淫魔
	フェラ回数 += 3

SIF フェラ回数 < 1
	フェラ回数 = 1

;ここから射精回数の計算
;ただしどれだけ多くてもフェラ回数よりは多く射精しない
射精回数 = 10*フェラ回数;整数しか扱えない関係で一旦10倍に
SIF !TALENT:汚れ無視
	TIMES 射精回数, 0.90
SIF !TALENT:舌使い
	TIMES 射精回数, 0.80
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
射精回数 *= ABL:技巧
射精回数 /= 30
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
射精回数 /= 10
射精回数 = LIMIT(射精回数, 1, フェラ回数)
IF TALENT:MASTER:絶倫
	射精回数 = MIN(射精回数, 15)
ELSE
	射精回数 = MIN(射精回数, 10)
ENDIF

PRINTFORMW 今朝は%CALLNAME:TARGET%のフェラで{射精回数}回射精した

CALL EXPUP, "フェラ経験", フェラ回数
CALL EXPUP, "精液経験", 射精回数
CALL JUELUP, "恭順", フェラ回数*100
CALL JUELUP, "屈服", フェラ回数*30
CALL JUELUP, "習得", フェラ回数*40

CALL EXPUP, "射精経験", 射精回数, MASTER
CALL EXPUP, "絶頂経験", 射精回数, MASTER
WAIT

CFLAG:朝フェラ経験 += 1
CFLAG:MASTER:朝フェラ経験 += 1
CFLAG:口で射精させた経験 += 射精回数
SIF ISSLIME(MASTER)
	CALL スライム度増加, 0, TARGET, 5
IF CFLAG:TARGET:スライム度 >= 30 && ISSLIME(MASTER) && !ISSLIME(TARGET)
	LOADGLOBAL
	スライム化イベント回数++
	SAVEGLOBAL
	SIF スライム化イベント回数 >= 10
		CALL 実績解除, "そして世界は粘液に満ちた"
	TALENT:スライム = 1
	CSTR:種族 = スライム
ENDIF

@EVENT_朝パイズリ
#DIM DYNAMIC パイズリカウント
#DIM DYNAMIC 射精回数

CALL CHECKDRAWLINE
CALL KOJO_MESSAGE_EVENT, "朝パイズリ"

;ここからパイズリカウントの計算
パイズリカウント = 相性値(TARGET, MASTER)/75
パイズリカウント += ABL:従順/2
パイズリカウント += ABL:奉仕精神/2
パイズリカウント += ABL:精液中毒/3
パイズリカウント += ABL:Ｂ感覚/10

SIF TALENT:汚臭鈍感
	パイズリカウント++
SIF TALENT:汚臭敏感
	パイズリカウント--
SIF TALENT:潔癖症
	パイズリカウント--
SIF TALENT:献身的
	パイズリカウント += 2
SIF TALENT:淫乱
	パイズリカウント += 2
SIF TALENT:淫魔
	パイズリカウント += 3
SIF TALENT:淫乳
	パイズリカウント += 3
SIF TALENT:恋慕
	パイズリカウント += 4
SIF TALENT:親愛
	パイズリカウント += 5

SIF MOD:朝パイズリ特化 && ゲームモード() != "段位認定"
	TIMES パイズリカウント, 1.30

SIF パイズリカウント < 1
	パイズリカウント = 1

;ここから射精回数の計算
;ただしどれだけ多くてもパイズリカウントよりは多く射精しない
射精回数 = 100*パイズリカウント;整数しか扱えない関係で一旦100倍に
SELECTCASE TALENT:バストサイズ
	CASE 1
		TIMES 射精回数, 0.40
	CASE 2
		TIMES 射精回数, 0.70
	CASE 3
		TIMES 射精回数, 1.00
	CASE 4
		TIMES 射精回数, 1.20
	CASE 5
		TIMES 射精回数, 1.40
	CASE 6
		TIMES 射精回数, 1.60
	CASE 7
		TIMES 射精回数, 1.80
	CASE IS >= 8
		TIMES 射精回数, 2.00
ENDSELECT
SIF !TALENT:汚れ無視
	TIMES 射精回数, 0.90
SIF !TALENT:舌使い
	TIMES 射精回数, 0.80
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
射精回数 *= (600 + MIN(EXP:パイズリ経験, 1000))
射精回数 /= 800
SELECTCASE ABL:技巧
	CASE 1
		TIMES 射精回数, 0.01
	CASE 2
		TIMES 射精回数, 0.03
	CASE 3
		TIMES 射精回数, 0.06
	CASE 4
		TIMES 射精回数, 0.10
	CASE 5
		TIMES 射精回数, 0.15
	CASE 6
		TIMES 射精回数, 0.21
	CASE 7
		TIMES 射精回数, 0.28
	CASE 8
		TIMES 射精回数, 0.36
	CASE 9
		TIMES 射精回数, 0.45
	CASE IS >= 10
		TIMES 射精回数, 0.55
ENDSELECT
射精回数 /= 100
射精回数 = LIMIT(射精回数, 1, パイズリカウント)
IF !MOD:朝パイズリ特化 || ゲームモード() == "段位認定"
	IF TALENT:MASTER:絶倫
		射精回数 = MIN(射精回数, 15)
	ELSE
		射精回数 = MIN(射精回数, 10)
	ENDIF
ENDIF

PRINTFORMW その後も%CALLNAME:TARGET%のパイズリを楽しみ、今朝は{射精回数}回射精した

CALL EXPUP, "パイズリ経験", パイズリカウント
SIF TALENT:バストサイズ >= 3 && パイズリカウント >= 2
	CALL EXPUP, "フェラ経験", パイズリカウント/2
CALL EXPUP, "精液経験", 射精回数
IF MOD:パイズリ奉仕特化 && ゲームモード() != "段位認定"
	CALL JUELUP, "恭順", パイズリカウント*110
	CALL JUELUP, "屈服", パイズリカウント*33
	CALL JUELUP, "習得", パイズリカウント*44
ELSE
	CALL JUELUP, "快Ｂ", パイズリカウント*5
	CALL JUELUP, "恭順", パイズリカウント*100
	CALL JUELUP, "屈服", パイズリカウント*30
	CALL JUELUP, "習得", パイズリカウント*40
ENDIF

CALL EXPUP, "射精経験", 射精回数, MASTER
CALL EXPUP, "絶頂経験", 射精回数, MASTER
WAIT

CFLAG:朝パイズリ経験 += 1
CFLAG:MASTER:朝パイズリ経験 += 1
CFLAG:胸で射精させた経験 += 射精回数
パイズリ回数:TARGET:(NOF(PLAYER)) += パイズリカウント
挟射回数:TARGET:(NOF(PLAYER)) += 射精回数
SIF ISSLIME(MASTER)
	CALL スライム度増加, 0, TARGET, 5
IF CFLAG:TARGET:スライム度 >= 30 && ISSLIME(MASTER) && !ISSLIME(TARGET)
	LOADGLOBAL
	スライム化イベント回数++
	SAVEGLOBAL
	SIF スライム化イベント回数 >= 10
		CALL 実績解除, "そして世界は粘液に満ちた"
	TALENT:スライム = 1
	CSTR:種族 = スライム
ENDIF

@EVENT_朝騎乗位
#DIM DYNAMIC CHK_V;1ならＶ、0ならＡ
#DIM DYNAMIC CHK_A
#DIM DYNAMIC プレイ回数
#DIM DYNAMIC 射精回数

CALL CHECKDRAWLINE

;どこの穴でするか
IF ABL:Ｖ感覚 >= 3 && VAGINA(TARGET) && !処女膜(TARGET)
	CHK_V = 1
	TFLAG:主人挿入箇所 = 1
ELSE
	CHK_V = 0
	TFLAG:主人挿入箇所 = 2
ENDIF
;Ａ感覚3以上ならＶとＡの感度で乱数分岐する
IF ABL:Ａ感覚 >= 3 && CHK_V
	IF (RAND:(ABL:Ｖ感覚+ABL:Ａ感覚))+1 > ABL:Ｖ感覚
		CHK_V = 0
		TFLAG:主人挿入箇所 = 2
	ENDIF
ENDIF

CALL KOJO_MESSAGE_EVENT, "朝騎乗位"

;何回するか
プレイ回数 = 相性値(TARGET, MASTER)/60
プレイ回数 += ABL:性交中毒
プレイ回数 += ABL:精液中毒/2
プレイ回数 += ABL:奉仕精神/4
SIF TALENT:献身的
	プレイ回数++
SIF TALENT:淫乱
	プレイ回数 += 2
SIF TALENT:淫魔
	プレイ回数 += 3
IF CHK_V
	IF TALENT:淫膣
		プレイ回数 += 5
		プレイ回数 += ABL:Ｖ感覚/10
	ENDIF
ELSE
	IF TALENT:淫尻
		プレイ回数 += 5
		プレイ回数 += ABL:Ａ感覚/10
	ENDIF
ENDIF
SIF プレイ回数 < 1
	プレイ回数 = 1

;何発出したか
射精回数 = 10*プレイ回数;小数を扱うため一旦10倍
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
SIF TALENT:体格 <= 2
	TIMES 射精回数, 1.20
射精回数 *= ABL:技巧
射精回数 /= 30
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
射精回数 /= 10
射精回数 = LIMIT(射精回数, 1, プレイ回数)
IF TALENT:MASTER:絶倫
	射精回数 = MIN(射精回数, 15)
ELSE
	射精回数 = MIN(射精回数, 10)
ENDIF

PRINTFORM %CALLNAME:MASTER%のペニスを咥え込んで離さない%CALLNAME:TARGET%は、卑猥な腰使いで
SIF 射精回数 > 1
	PRINTFORM {射精回数}回ほど
PRINTW 精液を搾り取っていった

CALL EXPUP, "性交経験", プレイ回数
経験回数:MASTER:(NOF(TARGET)) += プレイ回数
経験回数:TARGET:(NOF(MASTER)) += プレイ回数

CALL EXPUP, "精液経験", 射精回数

IF CHK_V
	CALL EXPUP, "Ｖ経験", プレイ回数
	CALL EXPUP, "膣射経験", 射精回数
	CALL JUELUP, "快Ｖ", プレイ回数*100
	中出し回数:MASTER:(NOF(TARGET)) += 射精回数
	中出され回数:(NOF(MASTER)) += 射精回数
	CFLAG:騎乗位で射精させた経験 += 射精回数
	IF PENIS(TARGET)
		CALL EXPUP, "射精経験", 1
		CALL EXPUP, "絶頂経験", 1
		CFLAG:膣を責められて射精した経験++
		CFLAG:犯されて射精した経験++
	ENDIF
	PREG:主人膣内射精 += 射精回数
	CALL IN_VAGINA
ELSE
	CALL EXPUP, "Ａ経験", プレイ回数
	CALL JUELUP, "快Ａ", プレイ回数*100
	アナル中出し回数:MASTER:(NOF(TARGET)) += 射精回数
	アナル中出され回数:(NOF(MASTER)) += 射精回数
	CFLAG:アナル騎乗位で射精させた経験 += 射精回数
	IF PENIS(TARGET)
		CALL EXPUP, "射精経験", 1
		CALL EXPUP, "絶頂経験", 1
		CFLAG:アナルを責められて射精した経験++
		CFLAG:アナルを犯されて射精した経験++
	ENDIF
ENDIF

CALL JUELUP, "恭順", プレイ回数*100
CALL JUELUP, "屈服", プレイ回数*30
CALL JUELUP, "習得", プレイ回数*40

CALL EXPUP, "性交経験", プレイ回数, MASTER
CALL EXPUP, "射精経験", 射精回数, MASTER
CALL EXPUP, "絶頂経験", 射精回数, MASTER
WAIT

CFLAG:朝騎乗位経験++
CFLAG:MASTER:朝騎乗位経験++
SIF ISSLIME(MASTER)
	CALL スライム度増加, 0, TARGET, 5
IF CFLAG:TARGET:スライム度 >= 30 && ISSLIME(MASTER) && !ISSLIME(TARGET)
	LOADGLOBAL
	スライム化イベント回数++
	SAVEGLOBAL
	SIF スライム化イベント回数 >= 10
		CALL 実績解除, "そして世界は粘液に満ちた"
	TALENT:スライム = 1
	CSTR:種族 = スライム
ENDIF

;-------------------------------------------------
;調教後に起こるイベント
;-------------------------------------------------
@SELF_CHECK
#DIM DYNAMIC 不発確率
;調教者がペニス持ちで、調教者の射精残数が0の場合は発生しない
;SIF PENIS(MASTER) && CFLAG:MASTER:残数 == 0
;	RETURN 0

;抑制中なら発生しない
SIF CFLAG:奉仕イベント抑制
	RETURN 0

SIF CFLAG:使用不可
	RETURN 0

SIF CFLAG:死亡
	RETURN 0

;アニマルだと発生しない
SIF TALENT:アニマル
	RETURN 0

;対象が霊体かつ主人に霊感もネクロマンサーも無いと発生しない
SIF TALENT:霊体 && !TALENT:MASTER:霊感 && !TALENT:MASTER:ネクロマンサー
	RETURN 0

;調教後3P、調教後セックスどちらか一つ
;対象の体力500以上必要、強精神剤の反動が起こる時は無効
SIF BASE:体力 < 500 || TEQUIP:強精神薬 > 2
	GOTO ELSE
;助手がいる場合は3P判定 対象と助手が陥落済み、対象と助手間の相性がそれぞれ100より↑
IF ASSI >= 0 && 陥落(TARGET) && 陥落(ASSI) && 相性値(TARGET, ASSI) > 100 && 相性値(ASSI, TARGET) > 100
	;どちらかが貞操帯つけてると発生しない
	SIF CFLAG:貞操帯 || CFLAG:ASSI:貞操帯
		GOTO ELSE
	;メス度10以上でも発生しない
	SIF ABL:メス度 >= 10 || ABL:ASSI:メス度 >= 10
		GOTO ELSE
	;去勢済みだと発生しない（暫定）
	SIF TALENT:去勢済み || TALENT:ASSI:去勢済み
		GOTO ELSE
	;ただし出産トラウマがある場合は発生しない時がある
	不発確率 = CFLAG:出産トラウマ+CFLAG:ASSI:出産トラウマ
	;RAND:(合計値/10+2) <= (合計値/10)
	SIF !(不発確率 && RAND:(不発確率/10+2) <= 不発確率/10)
		CALL EVENT_CHECK, "調教後3P"
;ふたりエッチ判定 対象が陥落済み
ELSEIF 陥落(TARGET) && BASE:体力 >= 500
	;貞操帯装着中は発生しない
	SIF CFLAG:貞操帯
		GOTO ELSE
	;出産トラウマがある場合はry 式は上と同じ
	不発確率 = CFLAG:出産トラウマ
	SIF !(不発確率 && RAND:(不発確率/10+2) <= 不発確率/10)
		CALL EVENT_CHECK, "調教後セックス"
ENDIF

$ELSE

;強精神薬の反動、調教後搾乳、調教後自慰。どれか一つ
IF TEQUIP:強精神薬 || TFLAG:ラブキャンセーブユー
	CALL EVENT_CHECK, "強精神薬の反動"
ELSEIF ABL:Ｂ感覚 >= 4 && (ABL:噴乳中毒 > ABL:自慰中毒) && TALENT:母乳体質
	CALL EVENT_CHECK, "調教後搾乳"
ELSEIF ABL:自慰中毒 >= 3 && !CFLAG:貞操帯
	CALL EVENT_CHECK, "調教後自慰"
ENDIF

;調教後夜這い
SIF 陥落(TARGET) && ターン == "夜"
	CALL EVENT_CHECK, "調教後夜這い"

;調教後3P
@EVENT_調教後3P
#DIM DYNAMIC プレイ回数
#DIM DYNAMIC 射精回数基礎値, 3;0,1,2でMASTER,ASSI,TARGET
#DIM DYNAMIC 射精回数, 3, 3;射精回数:(出す人):(出される人)
#DIM DYNAMIC V2
#DIM DYNAMIC 奉仕ポイント
#DIM DYNAMIC 受けＡ

射精回数基礎値:0 = CFLAG:MASTER:残数/5+1
射精回数基礎値:1 = CFLAG:ASSI:残数/5+1
射精回数基礎値:2 = CFLAG:TARGET:残数/5+1

;イベント口上呼び出し
CALL CHECKDRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後3P"
奉仕ポイント = 1

;対象：主人か助手に同性がいればレズ中毒、ゲイ中毒を見る
IF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	IF SEX(TARGET) == 1
		SELECTCASE ABL:ゲイ中毒
			CASE 1
				奉仕ポイント += 1
			CASE 2
				奉仕ポイント += 2
			CASE 3
				奉仕ポイント += 3
			CASE 4
				奉仕ポイント += 5
			CASE IS >= 5
				奉仕ポイント += 7
		ENDSELECT
	ELSEIF SEX(TARGET) == 2
		SELECTCASE ABL:レズ中毒
			CASE 1
				奉仕ポイント += 1
			CASE 2
				奉仕ポイント += 2
			CASE 3
				奉仕ポイント += 3
			CASE 4
				奉仕ポイント += 5
			CASE IS >= 5
				奉仕ポイント += 7
		ENDSELECT
	ENDIF
;いなければ奉仕精神を見る
ELSE
	SELECTCASE ABL:奉仕精神
		CASE 1 TO 4
			奉仕ポイント += ABL:奉仕精神+1
		CASE IS >= 5
			奉仕ポイント += 6
	ENDSELECT
ENDIF

;助手：奴隷が同性ならレズ中毒、ゲイ中毒を見る
IF SEX(ASSI) == SEX(TARGET)
	IF SEX(TARGET) == 1
		SELECTCASE ABL:ゲイ中毒
			CASE 1
				奉仕ポイント += 1
			CASE 2
				奉仕ポイント += 2
			CASE 3
				奉仕ポイント += 5
			CASE 4
				奉仕ポイント += 8
			CASE IS >= 5
				奉仕ポイント += 13
		ENDSELECT
	ELSEIF SEX(TARGET) == 2
		SELECTCASE ABL:レズ中毒
			CASE 1
				奉仕ポイント += 1
			CASE 2
				奉仕ポイント += 2
			CASE 3
				奉仕ポイント += 5
			CASE 4
				奉仕ポイント += 8
			CASE IS >= 5
				奉仕ポイント += 13
		ENDSELECT
	ENDIF
;同性でなければ技巧を見る
ELSE
	SELECTCASE ABL:技巧
		CASE 1 TO 4
			奉仕ポイント += ABL:技巧+2
		CASE IS >= 5
			奉仕ポイント += 7
	ENDSELECT
ENDIF

;両刀
SIF TALENT:両刀
	奉仕ポイント += 3

;相性処理
奉仕ポイント *= 相性値(TARGET, ASSI)
奉仕ポイント /= 100

;主人か助手に同性がいれば同性愛経験増加
IF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			CALL EXPUP, "ゲイ経験", 奉仕ポイント
		CASE 2
			CALL EXPUP, "レズ経験", 奉仕ポイント
	ENDSELECT
ENDIF

;数値次第で絶頂
SIF 奉仕ポイント*100*ABL:従順/2500 > 0
	CALL EXPUP, "絶頂経験", 奉仕ポイント*100*ABL:従順/2500
CALL JUELUP, "快Ｃ", 奉仕ポイント*10*ABL:Ｃ感覚 
CALL JUELUP, "欲情", 奉仕ポイント*50

;奴隷が男で他に竿役がいれば奴隷掘られる
IF SEX(TARGET) == 1 && (PENIS(MASTER) || PENIS(ASSI)) && ABL:TARGET:Ａ感覚 >= 3
	SELECTCASE ABL:Ａ感覚
		CASE 0 TO 4
			受けＡ = ABL:Ａ感覚+1
		CASE IS >= 5
			受けＡ = 6
	ENDSELECT
	プレイ回数 = (奉仕ポイント/5+1)*受けＡ
	IF PENIS(MASTER)
		EXP:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:MASTER:(NOF(TARGET)) += プレイ回数
		経験回数:(NOF(MASTER)) += プレイ回数
		射精回数:0:2 += 射精回数基礎値:0
		アナル中出し回数:MASTER:(NOF(TARGET)) += 射精回数基礎値:0
		アナル中出され回数:(NOF(MASTER)) += 射精回数基礎値:0
	ENDIF
	IF PENIS(ASSI)
		EXP:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:ASSI:(NOF(TARGET)) += プレイ回数
		経験回数:(NOF(ASSI)) += プレイ回数
		射精回数:1:2 += 射精回数基礎値:1
		アナル中出し回数:ASSI:(NOF(TARGET)) += 射精回数基礎値:1
		アナル中出され回数:(NOF(ASSI)) += 射精回数基礎値:1
	ENDIF
	SIF PENIS(MASTER) && PENIS(ASSI)
		プレイ回数 *= 2
		
	CALL EXPUP, "Ａ経験", プレイ回数
	CALL EXPUP, "苦痛快楽経験", プレイ回数/2+1
	CALL JUELUP, "快Ａ", プレイ回数*80
ENDIF

;助手が男で他に竿役がいれば助手掘られる
IF SEX(ASSI) == 1 && (PENIS(MASTER) || PENIS(TARGET)) && ABL:ASSI:Ａ感覚 >= 3
	SELECTCASE ABL:ASSI:Ａ感覚
		CASE 0 TO 4
			受けＡ = ABL:ASSI:Ａ感覚+1
		CASE IS >= 5
			受けＡ = 6
	ENDSELECT
	プレイ回数 = (奉仕ポイント/5+1)*受けＡ
	IF PENIS(MASTER)
		EXP:MASTER:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:MASTER:(NOF(ASSI)) += プレイ回数
		経験回数:ASSI:(NOF(MASTER)) += プレイ回数
		射精回数:0:1 += 射精回数基礎値:0
		アナル中出し回数:MASTER:(NOF(ASSI)) += 射精回数基礎値:0
		アナル中出され回数:ASSI:(NOF(MASTER)) += 射精回数基礎値:0
	ENDIF
	IF PENIS(TARGET)
		EXP:TARGET:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:TARGET:(NOF(ASSI)) += プレイ回数
		経験回数:ASSI:(NOF(TARGET)) += プレイ回数
		射精回数:2:1 += 射精回数基礎値:2
		アナル中出し回数:(NOF(ASSI)) += 射精回数基礎値:2
		アナル中出され回数:ASSI:(NOF(TARGET)) += 射精回数基礎値:2
	ENDIF
	SIF PENIS(MASTER) && PENIS(TARGET)
		プレイ回数 *= 2
		
	CALL EXPUP, "Ａ経験", プレイ回数, ASSI
	CALL EXPUP, "苦痛快楽経験", プレイ回数/2+1, ASSI
	CALL JUELUP, "快Ａ", プレイ回数*80, ASSI
ENDIF

;主人が男で他に竿役がいれば主人掘られる
IF SEX(MASTER) == 1 && (PENIS(ASSI) || PENIS(TARGET)) && ABL:MASTER:Ａ感覚 >= 3
	SELECTCASE ABL:MASTER:Ａ感覚
		CASE 0 TO 4
			受けＡ = ABL:MASTER:Ａ感覚+1
		CASE IS >= 5
			受けＡ = 6
	ENDSELECT
	プレイ回数 = (奉仕ポイント/5+1)*受けＡ
	IF PENIS(TARGET)
		EXP:TARGET:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:TARGET:(NOF(MASTER)) += プレイ回数
		経験回数:MASTER:(NOF(TARGET)) += プレイ回数
		射精回数:2:0 += 射精回数基礎値:2
		アナル中出し回数:(NOF(MASTER)) += 射精回数基礎値:2
		アナル中出され回数:MASTER:(NOF(TARGET)) += 射精回数基礎値:2
	ENDIF
	IF PENIS(ASSI)
		EXP:ASSI:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:MASTER:(NOF(ASSI)) += プレイ回数
		経験回数:ASSI:(NOF(MASTER)) += プレイ回数
		射精回数:1:0 += 射精回数基礎値:1
		アナル中出し回数:ASSI:(NOF(MASTER)) += 射精回数基礎値:1
		アナル中出され回数:MASTER:(NOF(ASSI)) += 射精回数基礎値:1
	ENDIF
	SIF PENIS(MASTER) && PENIS(TARGET)
		プレイ回数 *= 2
	
	CALL EXPUP, "Ａ経験", プレイ回数, MASTER
	CALL EXPUP, "苦痛快楽経験", プレイ回数/2+1, MASTER
ENDIF

;奴隷に竿があり、主人か助手がヴァギナあれば中出しされる
IF PENIS(TARGET)
	IF VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:MASTER:Ｖ感覚, 8)
		SIF TALENT:MASTER:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数, MASTER
		PRINTFORML %CALLNAME:MASTER%の膣射経験(%CALLNAME:TARGET%の精液)+{射精回数基礎値:2}
		EXP:MASTER:膣射経験 += 射精回数基礎値:2
		経験回数:(NOF(MASTER)) += プレイ回数
		経験回数:MASTER:(NOF(TARGET)) += プレイ回数
		中出し回数:(NOF(MASTER)) += 射精回数基礎値:2
		中出され回数:MASTER:(NOF(TARGET)) += 射精回数基礎値:2
		射精回数:2:0 += 射精回数基礎値:2
		;既に誰かの精液がある場合はランダムで上書きされる
		IF PREG:MASTER:最新中出し == ASSI || RAND:3 == 0
			PREG:MASTER:主人への膣内射精++
			PREG:MASTER:最新中出し = TARGET
		ENDIF
	ENDIF
	IF VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:ASSI:Ｖ感覚, 8)
		SIF TALENT:ASSI:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数, ASSI
		PRINTFORML %CALLNAME:ASSI%の膣射経験(%CALLNAME:TARGET%の精液)+{射精回数基礎値:2}
		EXP:ASSI:膣射経験 += 射精回数基礎値:2
		経験回数:(NOF(ASSI)) += プレイ回数
		経験回数:ASSI:(NOF(TARGET)) += プレイ回数
		中出し回数:(NOF(ASSI)) += 射精回数基礎値:2
		中出され回数:ASSI:(NOF(TARGET)) += 射精回数基礎値:2
		射精回数:2:0 += 射精回数基礎値:2
		PREG:ASSI:奴隷膣内射精 += 射精回数基礎値:2
	ENDIF
ENDIF

;助手に竿があり、主人か奴隷がヴァギナあれば中出しされる
IF PENIS(ASSI)
	IF VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:MASTER:Ｖ感覚, 8)
		SIF TALENT:MASTER:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数, MASTER
		PRINTFORML %CALLNAME:MASTER%の膣射経験(%CALLNAME:ASSI%の精液)+{射精回数基礎値:1}
		EXP:MASTER:膣射経験 += 射精回数基礎値:1
		経験回数:ASSI:(NOF(MASTER)) += プレイ回数
		経験回数:MASTER:(NOF(ASSI)) += プレイ回数
		中出し回数:ASSI:(NOF(MASTER)) += 射精回数基礎値:1
		中出され回数:MASTER:(NOF(ASSI)) += 射精回数基礎値:1
		射精回数:1:0 += 射精回数基礎値:1
		;既に誰かの精液がある場合はランダムで上書きされる
		IF PREG:MASTER:最新中出し == TARGET || RAND:3 == 0
			PREG:MASTER:主人への膣内射精++
			PREG:MASTER:最新中出し = ASSI
		ENDIF
	ENDIF
	IF VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
		プレイ回数 = MIN(2+ABL:Ｖ感覚, 8)
		SIF TALENT:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数
		PRINTFORML %CALLNAME:TARGET%の膣射経験(%CALLNAME:ASSI%の精液)+{射精回数基礎値:1}
		EXP:膣射経験 += 射精回数基礎値:1
		経験回数:ASSI:(NOF(TARGET)) += プレイ回数
		経験回数:(NOF(ASSI)) += プレイ回数
		中出し回数:ASSI:(NOF(TARGET)) += 射精回数基礎値:1
		中出され回数:(NOF(ASSI)) += 射精回数基礎値:1
		射精回数:1:2 += 射精回数基礎値:1
		PREG:助手膣内射精 += 射精回数基礎値:1
	ENDIF
ENDIF

;主人に竿があり、助手か奴隷がヴァギナあれば中出しされる
IF PENIS(MASTER)
	IF VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女
		プレイ回数 = MIN(2+ABL:ASSI:Ｖ感覚, 8)
		SIF TALENT:ASSI:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数, ASSI
		PRINTFORML %CALLNAME:ASSI%の膣射経験(%CALLNAME:MASTER%の精液)+{射精回数基礎値:0}
		EXP:ASSI:膣射経験 += 射精回数基礎値:0
		経験回数:MASTER:(NOF(ASSI)) += プレイ回数
		経験回数:ASSI:(NOF(MASTER)) += プレイ回数
		中出し回数:MASTER:(NOF(ASSI)) += 射精回数基礎値:0
		中出され回数:ASSI:(NOF(MASTER)) += 射精回数基礎値:0
		射精回数:0:1 += 射精回数基礎値:0
		PREG:ASSI:主人膣内射精 += 射精回数基礎値:0
	ENDIF
	IF VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
		プレイ回数 = MIN(2+ABL:Ｖ感覚, 8)
		SIF TALENT:淫膣
			プレイ回数 += 2
		CALL EXPUP, "Ｖ経験", プレイ回数
		PRINTFORML %CALLNAME:TARGET%の膣射経験(%CALLNAME:MASTER%の精液)+{射精回数基礎値:0}
		EXP:膣射経験 += 射精回数基礎値:0
		経験回数:MASTER:(NOF(TARGET)) += プレイ回数
		経験回数:(NOF(MASTER)) += プレイ回数
		中出し回数:MASTER:(NOF(TARGET)) += 射精回数基礎値:0
		中出され回数:(NOF(MASTER)) += 射精回数基礎値:0
		射精回数:0:2 += 射精回数基礎値:0
		PREG:主人膣内射精 += 射精回数基礎値:0
	ENDIF
ENDIF

;上記イベントが発生した場合は竿役のバージンが失われる
IF TALENT:童貞
	IF (SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)) && (SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女))
		IF TALENT:性転換済
			IF RAND:2
				CSTR:性転換童貞 = %NAME:MASTER%
			ELSE
				CSTR:性転換童貞 = %NAME:ASSI%
			ENDIF
		ELSE
			IF RAND:2
				CSTR:童貞 = %NAME:MASTER%
			ELSE
				CSTR:童貞 = %NAME:ASSI%
			ENDIF
		ENDIF
		TALENT:童貞 = 0
	ELSEIF SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)
		IF TALENT:性転換済
			CSTR:性転換童貞 = %NAME:MASTER%
		ELSE
			CSTR:童貞 = %NAME:MASTER%
		ENDIF
		TALENT:童貞 = 0
	ELSEIF SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女)
		IF TALENT:性転換済
			CSTR:性転換童貞 = %NAME:ASSI%
		ELSE
			CSTR:童貞 = %NAME:ASSI%
		ENDIF
		TALENT:童貞 = 0
	ENDIF
ENDIF
IF TALENT:MASTER:童貞
	IF (SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)) && (SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女))
		IF TALENT:MASTER:性転換済
			IF RAND:2
				CSTR:MASTER:性転換童貞 = %NAME:TARGET%
			ELSE
				CSTR:MASTER:性転換童貞 = %NAME:ASSI%
			ENDIF
		ELSE
			IF RAND:2
				CSTR:MASTER:童貞 = %NAME:TARGET%
			ELSE
				CSTR:MASTER:童貞 = %NAME:ASSI%
			ENDIF
		ENDIF
		TALENT:MASTER:童貞 = 0
	ELSEIF SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)
		IF TALENT:MASTER:性転換済
			CSTR:MASTER:性転換童貞 = %NAME:TARGET%
		ELSE
			CSTR:MASTER:童貞 = %NAME:TARGET%
		ENDIF
		TALENT:MASTER:童貞 = 0
	ELSEIF SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女)
		IF TALENT:MASTER:性転換済
			CSTR:MASTER:性転換童貞 = %NAME:ASSI%
		ELSE
			CSTR:MASTER:童貞 = %NAME:ASSI%
		ENDIF
		TALENT:MASTER:童貞 = 0
	ENDIF
ENDIF
IF TALENT:ASSI:童貞
	IF (SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)) && (SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女))
		IF TALENT:ASSI:性転換済
			IF RAND:2
				CSTR:ASSI:性転換童貞 = %NAME:MASTER%
			ELSE
				CSTR:ASSI:性転換童貞 = %NAME:TARGET%
			ENDIF
		ELSE
			IF RAND:2
				CSTR:ASSI:童貞 = %NAME:MASTER%
			ELSE
				CSTR:ASSI:童貞 = %NAME:TARGET%
			ENDIF
		ENDIF
		TALENT:ASSI:童貞 = 0
	ELSEIF SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)
		IF TALENT:ASSI:性転換済
			CSTR:ASSI:性転換童貞 = %NAME:MASTER%
		ELSE
			CSTR:ASSI:童貞 = %NAME:MASTER%
		ENDIF
		TALENT:ASSI:童貞 = 0
	ELSEIF SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)
		IF TALENT:ASSI:性転換済
			CSTR:ASSI:性転換童貞 = %NAME:TARGET%
		ELSE
			CSTR:ASSI:童貞 = %NAME:TARGET%
		ENDIF
		TALENT:ASSI:童貞 = 0
	ENDIF
ENDIF

IF PENIS(MASTER)
	プレイ回数 = 奉仕ポイント
	IF TALENT:バストサイズ >= 4 && TALENT:ASSI:バストサイズ >= 4
		プレイ回数++
		SIF TALENT:バストサイズ >= 5 && TALENT:ASSI:バストサイズ >= 5
			プレイ回数++
		CALL EXPUP, "パイズリ経験", プレイ回数
		CALL EXPUP, "パイズリ経験", プレイ回数, ASSI
	ENDIF
	CALL EXPUP, "フェラ経験", プレイ回数
	CALL EXPUP, "フェラ経験", プレイ回数, ASSI
	射精回数:0:1 += 射精回数基礎値:0
	射精回数:0:2 += 射精回数基礎値:0
ENDIF

;それぞれの射精回数に応じて経験増加
IF 射精回数:2:0+射精回数:2:1 > 0
	CALL EXPUP, "射精経験", 射精回数:2:0+射精回数:2:1
	CALL EXPUP, "絶頂経験", 射精回数:2:0+射精回数:2:1
ENDIF
IF 射精回数:1:0+射精回数:1:2 > 0
	CALL EXPUP, "射精経験", 射精回数:1:0+射精回数:1:2, ASSI
	CALL EXPUP, "絶頂経験", 射精回数:1:0+射精回数:1:2, ASSI
ENDIF
IF 射精回数:0:1+射精回数:0:2 > 0
	CALL EXPUP, "射精経験", 射精回数:0:1+射精回数:0:2, MASTER
	CALL EXPUP, "絶頂経験", 射精回数:0:1+射精回数:0:2, MASTER
ENDIF

SIF 射精回数:0:2+射精回数:1:2 > 0
	CALL EXPUP, "精液経験", 射精回数:0:2+射精回数:1:2
SIF 射精回数:0:1+射精回数:2:1 > 0
	CALL EXPUP, "精液経験", 射精回数:0:1+射精回数:2:1, ASSI
SIF 射精回数:1:0+射精回数:2:0 > 0
	CALL EXPUP, "精液経験", 射精回数:1:0+射精回数:2:0, MASTER

CALL EXPUP, "奉仕快楽経験", 奉仕ポイント
CALL JUELUP, "欲情", 奉仕ポイント*100
CALL JUELUP, "屈服", 奉仕ポイント*10*(ABL:技巧+ABL:奉仕精神)/2
CALL JUELUP, "習得", 奉仕ポイント*10*(ABL:技巧+ABL:奉仕精神)/2

CFLAG:調教後3P経験++
CFLAG:ASSI:調教後3P経験++
CFLAG:MASTER:調教後3P経験++

SIF (CHARANAMEF(TARGET) == "野増菜 かなで" && CHARANAMEF(ASSI) == "粒焼 りむる") || (CHARANAMEF(TARGET) == "粒焼 りむる" && CHARANAMEF(ASSI) == "野増菜 かなで")
	CALL 実績解除, "帝国軍ご奉仕部隊"

WAIT
DRAWLINE

;調教後セックス
@EVENT_調教後セックス
#DIM DYNAMIC 奉仕ポイント
#DIM DYNAMIC 性交経験加算済み
#DIMS DYNAMIC 部位
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後セックス"

奉仕ポイント = 3

;ＶかＡの高いほう
IF !VAGINA(TARGET)
	部位 = Ａ
ELSEIF ABL:Ｖ感覚 < ABL:Ａ感覚
	部位 = Ａ
ELSE
	部位 = Ｖ
ENDIF

SELECTCASE 部位
	CASE "Ｖ"
		SELECTCASE ABL:Ｖ感覚
			CASE 1 TO 4
				奉仕ポイント += ABL:Ｖ感覚
			CASE IS >= 5
				奉仕ポイント += 5
		ENDSELECT
	CASE "Ａ"
		IF SEX(TARGET) == 1
			SELECTCASE ABL:Ａ感覚
				CASE 1 TO 4
					奉仕ポイント += ABL:Ａ感覚+2
				CASE IS >= 5
					奉仕ポイント += 7
			ENDSELECT
		ELSEIF SEX(TARGET) == 2
			SELECTCASE ABL:Ａ感覚
				CASE 1 TO 4
					奉仕ポイント += ABL:Ａ感覚
				CASE IS >= 5
					奉仕ポイント += 5
			ENDSELECT
		ENDIF
ENDSELECT

;欲望と奉仕精神によるボーナス
IF ABL:欲望 >= 5 && ABL:奉仕精神 >= 5 && PALAM:恭順 >= PALAMLV:5
	奉仕ポイント += 3
ELSEIF ABL:欲望 >= 4 && ABL:奉仕精神 >= 4 && PALAM:恭順 >= PALAMLV:4
	奉仕ポイント += 2
ELSEIF ABL:欲望 >= 3 && ABL:奉仕精神 >= 3 && PALAM:恭順 >= PALAMLV:3
	奉仕ポイント += 1
ENDIF

;淫膣
SIF TALENT:淫膣 && 部位 == "Ｖ"
	奉仕ポイント += 3
;淫尻
SIF TALENT:淫尻 && 部位 == "Ａ"
	奉仕ポイント += 3

;主人の射精制限
;SIF 奉仕ポイント > CFLAG:MASTER:残数
;	奉仕ポイント = CFLAG:MASTER:残数

;場合によってはオーラル、手淫に
IF !調教後セックス判定()
	IF PENIS(MASTER)
		CALL EXPUP, "精液経験", CFLAG:MASTER:残数/5+1
		CALL EXPUP, "フェラ経験", 奉仕ポイント
		CALL EXPUP, "射精経験", CFLAG:MASTER:残数/5+1, MASTER
	ENDIF
	IF PENIS(TARGET)
		CALL EXPUP, "精液経験", CFLAG:残数/5+1, MASTER
		CALL EXPUP, "フェラ経験", 奉仕ポイント, MASTER
		CALL EXPUP, "射精経験", CFLAG:残数/5+1
	ENDIF
	CALL EXPUP, "絶頂経験", CFLAG:MASTER:残数/5+1, MASTER
	CALL EXPUP, "絶頂経験", CFLAG:残数/5+1
	CALL EXPUP, "奉仕快楽経験", 奉仕ポイント
	CALL JUELUP, "習得", 奉仕ポイント*150
	IF SEX(TARGET) == 1 && SEX(MASTER) == 1
		CALL EXPUP, "ゲイ経験", 奉仕ポイント/2+1
		CALL EXPUP, "ゲイ経験", 奉仕ポイント/2+1, MASTER
	ENDIF
	IF SEX(TARGET) == 2 && SEX(MASTER) == 2
		CALL EXPUP, "レズ経験", 奉仕ポイント/2+1
		CALL EXPUP, "レズ経験", 奉仕ポイント/2+1, MASTER
	ENDIF
	CFLAG:調教後奉仕経験++
	CFLAG:MASTER:調教後奉仕経験++
ELSE
	IF PENIS(MASTER)
		IF 部位 == "Ｖ"
			CALL EXPUP, "Ｖ経験", 奉仕ポイント
			CALL EXPUP, "膣射経験", CFLAG:MASTER:残数/2+1
			CALL JUELUP, "快Ｖ", 奉仕ポイント*400
			CALL EXPUP, "性交経験", 奉仕ポイント
			CALL EXPUP, "性交経験", 奉仕ポイント, MASTER
			経験回数:MASTER:(NOF(TARGET)) += 奉仕ポイント
			経験回数:(NOF(MASTER)) += 奉仕ポイント
			中出し回数:MASTER:(NOF(TARGET)) += CFLAG:MASTER:残数/2+1
			中出され回数:(NOF(MASTER)) += CFLAG:MASTER:残数/2+1
			PREG:主人膣内射精 += CFLAG:MASTER:残数/2+1
			性交経験加算済み = 1
		ELSEIF ABL:Ａ感覚 > 3 && 部位 == "Ａ"
			CALL EXPUP, "Ａ経験", 奉仕ポイント
			CALL EXPUP, "精液経験", CFLAG:MASTER:残数
			CALL JUELUP, "快Ａ", 奉仕ポイント*500
			CALL EXPUP, "性交経験", 奉仕ポイント
			CALL EXPUP, "性交経験", 奉仕ポイント, MASTER
			経験回数:MASTER:(NOF(TARGET)) += 奉仕ポイント
			経験回数:(NOF(MASTER)) += 奉仕ポイント
			アナル中出し回数:MASTER:(NOF(TARGET)) += CFLAG:MASTER:残数
			アナル中出され回数:(NOF(MASTER)) += CFLAG:MASTER:残数
			性交経験加算済み = 1
		ENDIF
		CALL EXPUP, "精液経験", CFLAG:MASTER:残数
		CALL EXPUP, "射精経験", CFLAG:MASTER:残数, MASTER
		CALL EXPUP, "絶頂経験", CFLAG:MASTER:残数, MASTER
	ENDIF
	IF PENIS(TARGET) && !Ｐ使用不可(TARGET)
		CALL EXPUP, "射精経験", CFLAG:残数/2+1
		CALL EXPUP, "絶頂経験", CFLAG:残数/2+1
		CALL EXPUP, "精液経験", CFLAG:残数/2+1, MASTER
		IF VAGINA(MASTER)
			CALL EXPUP, "Ｖ経験", 奉仕ポイント/2+1, MASTER
			CALL EXPUP, "膣射経験", CFLAG:残数/2+1, MASTER
			IF !性交経験加算済み
				CALL EXPUP, "性交経験", 奉仕ポイント
				CALL EXPUP, "性交経験", 奉仕ポイント, MASTER
				経験回数:MASTER:(NOF(TARGET)) += 奉仕ポイント
				経験回数:(NOF(MASTER)) += 奉仕ポイント
			ENDIF
			中出し回数:(NOF(MASTER)) += CFLAG:残数/2+1
			中出され回数:MASTER:(NOF(TARGET)) += CFLAG:残数/2+1
			PREG:主人への膣内射精 += CFLAG:残数/2+1
			PREG:最新中出し = TARGET
		ELSE
			CALL EXPUP, "Ａ経験", 奉仕ポイント/2+1, MASTER
			IF !性交経験加算済み
				CALL EXPUP, "性交経験", 奉仕ポイント
				CALL EXPUP, "性交経験", 奉仕ポイント, MASTER
				経験回数:MASTER:(NOF(TARGET)) += 奉仕ポイント
				経験回数:(NOF(MASTER)) += 奉仕ポイント
			ENDIF
			アナル中出し回数:(NOF(MASTER)) += CFLAG:残数/2+1
			アナル中出され回数:MASTER:(NOF(TARGET)) += CFLAG:残数/2+1
		ENDIF
	ENDIF 
	IF SEX(MASTER) == 2 && SEX(TARGET) == 2
		CALL EXPUP, "レズ経験", 奉仕ポイント/2+1
		CALL EXPUP, "レズ経験", 奉仕ポイント/2+1, MASTER
	ENDIF
	IF SEX(MASTER) == 1 && SEX(TARGET) == 1
		CALL EXPUP, "ゲイ経験", 奉仕ポイント/2+1
		CALL EXPUP, "ゲイ経験", 奉仕ポイント/2+1, MASTER
	ENDIF
	CALL EXPUP, "奉仕快楽経験", 奉仕ポイント
	CALL JUELUP, "恭順", 奉仕ポイント*200
	CALL JUELUP, "欲情", 奉仕ポイント*400
	CFLAG:調教後セックス経験++
	CFLAG:MASTER:調教後セックス経験++
ENDIF

WAIT

@調教後セックス判定
#FUNCTION

SIF TALENT:童貞 && !Ｐ使用不可(TARGET)
	RETURNF 0
SIF TALENT:MASTER:童貞
	RETURNF 0
SIF 処女膜(TARGET)
	RETURNF 0
SIF 処女膜(MASTER)
	RETURNF 0
SIF !PENIS(MASTER) && Ｐ使用不可(TARGET)
	RETURNF 0
SIF TALENT:妊娠
	RETURNF 0
SIF !PENIS(MASTER) && !PENIS(TARGET)
	RETURNF 0
;互いにＡもＶも足りてなければ起きない
SIF !TALENT:MASTER:童貞 && ABL:Ａ感覚 < 3 && ABL:Ｖ感覚 < 3
	RETURNF 0
SIF !TALENT:童貞 && ABL:MASTER:Ａ感覚 < 3 && ABL:MASTER:Ｖ感覚 < 3
	RETURNF 0

RETURNF 1

;強精神薬の反動
@EVENT_強精神薬の反動
#DIM DYNAMIC 絶頂強度
;イベント口上呼び出し
DRAWLINE

;強精神薬による絶頂チェック
IF TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント > 0
	CALL KOJO_MESSAGE_EVENT, "強精神薬の反動"
	;強精神薬の効果が残ったまま調教を終了した場合の処理
	;通常時の効果が再現できそうにないので同時多重絶頂したという処理に変更
	;絶頂強度は多重絶頂の種類を示す(1の場合は○絶頂、2の場合は○＋△絶頂、4の場合は四重絶頂となっている)
	SIF TFLAG:Ｃ絶頂カウント
		絶頂強度++
	SIF TFLAG:Ｖ絶頂カウント
		絶頂強度++
	SIF TFLAG:Ａ絶頂カウント
		絶頂強度++
	SIF TFLAG:Ｂ絶頂カウント
		絶頂強度++

	CALL EXPUP, "絶頂経験", TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント
	SIF TFLAG:Ｃ絶頂カウント > 0
		CALL JUELUP, "快Ｃ", 絶頂強度*TFLAG:Ｃ絶頂カウント*1000
	SIF TFLAG:Ｖ絶頂カウント > 0
		CALL JUELUP, "快Ｖ", 絶頂強度*TFLAG:Ｖ絶頂カウント*1000
	SIF TFLAG:Ａ絶頂カウント > 0
		CALL JUELUP, "快Ａ", 絶頂強度*TFLAG:Ａ絶頂カウント*1000
	SIF TFLAG:Ｂ絶頂カウント > 0
		CALL JUELUP, "快Ｂ", 絶頂強度*TFLAG:Ｂ絶頂カウント*1000
	PRINTL 

	;絶頂強度合計が10以上で四重絶頂or20以上だとその経験が加算される
	SELECTCASE TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント
		CASE IS >= 20
			SIF !CFLAG:調教後強精神薬反動経験強
				CALL EXPUP, "異常経験", 1
			CFLAG:調教後強精神薬反動経験強++
		CASE IS >= 10
			IF 絶頂強度 == 4
				SIF !CFLAG:調教後強精神薬反動経験強
					CALL EXPUP, "異常経験", 1
				CFLAG:調教後強精神薬反動経験強++
			ENDIF
	ENDSELECT

	;リセット
	TFLAG:Ｃ絶頂カウント = 0
	TFLAG:Ｖ絶頂カウント = 0
	TFLAG:Ａ絶頂カウント = 0
	TFLAG:Ｂ絶頂カウント = 0
	TEQUIP:強精神薬 = 0
	CFLAG:調教後強精神薬反動経験++

	;副作用で体力が500に
	SIF BASE:体力 > 500
		BASE:体力 = 500
	WAIT
ENDIF

;調教後搾乳
@EVENT_調教後搾乳
#DIM DYNAMIC 奉仕ポイント
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後搾乳"

;噴乳中毒による搾乳チェック
;男はだめ。母乳体質、噴乳中毒、一定能力、体力が必要
奉仕ポイント = 1

SELECTCASE ABL:噴乳中毒
	CASE 1
		奉仕ポイント += 1
	CASE 2
		奉仕ポイント += 2
	CASE 3
		奉仕ポイント += 3
	CASE 4
		奉仕ポイント += 5
	CASE IS >= 5
		奉仕ポイント += 7
ENDSELECT

;欲望と奉仕精神によるボーナス
IF ABL:欲望 >= 5 && ABL:奉仕精神 >= 5 && PALAM:恭順 >= PALAMLV:5
	奉仕ポイント += 3
ELSEIF ABL:欲望 >= 4 && ABL:奉仕精神 >= 4 && PALAM:恭順 >= PALAMLV:4
	奉仕ポイント += 2
ELSEIF ABL:欲望 >= 3 && ABL:奉仕精神 >= 3 && PALAM:恭順 >= PALAMLV:3
	奉仕ポイント += 1
ENDIF

;自慰しやすい
SIF TALENT:自慰しやすい
	奉仕ポイント += 2
;淫乳
SIF TALENT:淫乳
	奉仕ポイント += 3

CALL EXPUP, "噴乳経験", 奉仕ポイント
CALL JUELUP, "快Ｂ", 奉仕ポイント*300
CALL JUELUP, "欲情", 奉仕ポイント*50
CALL JUELUP, "習得", 奉仕ポイント*20
SIF ABL:従順+ABL:露出癖+ABL:マゾっ気 >= 10
	CALL JUELUP, "恥情", 奉仕ポイント*150
CFLAG:調教後搾乳経験++
WAIT

;調教後自慰
@EVENT_調教後自慰
#DIM DYNAMIC 奉仕ポイント
#DIMS DYNAMIC 部位
#DIMS DYNAMIC 珠
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後自慰"

;CVAの一番高いところでやる 優先度はＶ→Ｃ→Ａ
SELECTCASE MAX(ABL:Ｃ感覚, ABL:Ｖ感覚, ABL:Ａ感覚)
	CASE ABL:Ｖ感覚
		IF 処女膜(TARGET)
			SELECTCASE MAX(ABL:Ｃ感覚, ABL:Ａ感覚)
				CASE ABL:Ｃ感覚
					部位 = Ｃ感覚
					珠 = 快Ｃ
				CASE ABL:Ａ感覚
					部位 = Ａ感覚
					珠 = 快Ａ
			ENDSELECT
		ELSE
			部位 = Ｖ感覚
			珠 = 快Ｖ
		ENDIF
	CASE ABL:Ｃ感覚
		部位 = Ｃ感覚
		珠 = 快Ｃ
	CASE ABL:Ａ感覚
		部位 = Ａ感覚
		珠 = 快Ａ
ENDSELECT

;自慰中毒による自慰チェック
奉仕ポイント = 1
;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE IS <= 3
		奉仕ポイント += 3
	CASE 4
		奉仕ポイント += 4
	CASE 5
		奉仕ポイント += 5
	CASE 6
		奉仕ポイント += 7
	CASE IS >= 7
		奉仕ポイント += 9
ENDSELECT

;欲望とＣ感覚によるボーナス
IF ABL:欲望 >= 5 && ABL:部位 >= 5 && PALAM:恭順 >= PALAMLV:5
	奉仕ポイント += 3
ELSEIF ABL:欲望 >= 4 && ABL:部位 >= 4 && PALAM:恭順 >= PALAMLV:4
	奉仕ポイント += 2
ELSEIF ABL:欲望 >= 3 && ABL:部位 >= 3 && PALAM:恭順 >= PALAMLV:3
	奉仕ポイント += 1
ENDIF

SIF TALENT:自慰しやすい
	奉仕ポイント += 2

SELECTCASE 部位
	CASE "Ｃ感覚"
		SIF TALENT:淫核
			奉仕ポイント += 3
	CASE "Ｖ感覚"
		SIF TALENT:淫膣
			奉仕ポイント += 3
	CASE "Ａ感覚"
		SIF TALENT:淫尻
			奉仕ポイント += 3
ENDSELECT

;助手が鼓舞
IF ASSI >= 0
	SIF TALENT:ASSI:鼓舞
		奉仕ポイント += 1
ENDIF

CALL EXPUP, "自慰経験", 奉仕ポイント
SIF PENIS(TARGET)
	CALL EXPUP, "射精経験", 奉仕ポイント/3+1
CALL JUELUP, 珠, 奉仕ポイント*500
CALL JUELUP, "恭順", 奉仕ポイント*100
CALL JUELUP, "欲情", 奉仕ポイント*250
SIF ABL:自慰中毒+ABL:部位+ABL:欲望 >= 10 && ターン == "昼"
	CALL JUELUP, "恥情", 奉仕ポイント*200
CFLAG:調教後自慰経験++
WAIT

;調教後夜這い
@EVENT_調教後夜這い
#DIM DYNAMIC 奉仕ポイント
DRAWLINE
IF SEX(TARGET) == 1 || TALENT:処女 || TALENT:再生処女 || !PENIS(MASTER) || !CFLAG:MASTER:残数 || BASE:体力 < 500 || CFLAG:貞操帯
	TFLAG:調教後夜這い = 0
ELSE
	TFLAG:調教後夜這い = 1
ENDIF

CALL KOJO_MESSAGE_EVENT, "調教後夜這い"

IF !TFLAG:調教後夜這い
	;対象が男or対象が処女or主人竿無しor残数0or体力500未満で添い寝イベント
	;添い寝イベントなので体力が大きく回復する
	PRINTFORMW %CALLNAME:TARGET%の体力が大きく回復しました
	BASE:体力 += EXP:愛情経験+200
	SIF BASE:体力 > MAXBASE:体力
		BASE:体力 = MAXBASE:体力
	CFLAG:調教後添い寝経験++
	CFLAG:MASTER:調教後添い寝経験++
	;実績;添い寝してくるキュベリアと一夜を過ごす
	SIF CHARANAMEF(TARGET) == "キュベリア"
		CALL 実績解除, "天空の夜明け"
ELSEIF SEX(TARGET) == 2
	奉仕ポイント = 1
	;V感覚
	SELECTCASE ABL:Ｖ感覚
		CASE 1, 2
			奉仕ポイント += 1
		CASE 3, 4
			奉仕ポイント += 2
		CASE IS >= 5
			奉仕ポイント += 3
	ENDSELECT

	;淫膣
	SIF TALENT:淫膣
		奉仕ポイント += 3

	;主人の射精制限
	SIF 奉仕ポイント > CFLAG:MASTER:残数
		奉仕ポイント = CFLAG:MASTER:残数

	CALL EXPUP, "Ｖ経験", 奉仕ポイント
	CALL EXPUP, "性交経験", 奉仕ポイント
	CALL EXPUP, "性交経験", 奉仕ポイント, MASTER
	CALL EXPUP, "膣射経験", 奉仕ポイント/2
	CALL EXPUP, "精液経験", 奉仕ポイント/2
	CALL EXPUP, "射精経験", 奉仕ポイント/2, MASTER
	CALL JUELUP, "快Ｖ", 奉仕ポイント*600
	CALL JUELUP, "恭順", 奉仕ポイント*400
	CALL JUELUP, "欲情", 奉仕ポイント*400
	経験回数:MASTER:(NOF(TARGET)) += 奉仕ポイント
	経験回数:(NOF(MASTER)) += 奉仕ポイント
	中出し回数:MASTER:(NOF(TARGET)) += 奉仕ポイント/2
	中出され回数:(NOF(MASTER)) += 奉仕ポイント/2
	PREG:主人膣内射精 += 奉仕ポイント/2
	CFLAG:MASTER:残数 -= 奉仕ポイント
	CFLAG:調教後夜這い経験++
	CFLAG:MASTER:調教後夜這い経験++
	SIF ISSLIME(MASTER)
		CALL スライム度増加, 0, TARGET, 5
	IF CFLAG:TARGET:スライム度 >= 30 && ISSLIME(MASTER) && !ISSLIME(TARGET)
		LOADGLOBAL
		スライム化イベント回数++
		SAVEGLOBAL
		SIF スライム化イベント回数 >= 10
			CALL 実績解除, "そして世界は粘液に満ちた"
		TALENT:スライム = 1
		CSTR:種族 = スライム
	ELSEIF CFLAG:TARGET:スライム度 >= 1450 && TALENT:MASTER:溶ける快 && !TALENT:TARGET:溶ける快
		CALL スライム度増加, 1, TARGET, 1500
		TALENT:妄信 = 1
		TALENT:溶ける快 = 1
		TALENT:スライム = 0
	ENDIF
	WAIT
ENDIF

@EVENT_アニマル
DRAWLINE
;イベント口上呼び出し
CALL KOJO_MESSAGE_EVENT, "アニマル"

;効果は単純 恭順と愛情経験
CALL EXPUP, "愛情経験", 3
CALL JUELUP, "恭順", 500
WAIT

@EVENT_奉仕イベント抑制
CALL KOJO_MESSAGE_EVENT, "奉仕イベント抑制"
