;所持金マイナスでゲームオーバーにならないモードの借金の処理
@DEBT, 返済専用
#DIM DYNAMIC 金額
#DIM DYNAMIC 方向
#DIM DYNAMIC 返済専用
#DIM DYNAMIC 利子

;所持金がマイナスの場合は借金に繰り入れる
IF MONEY < 0
	借金 -= MONEY
	MONEY = 0
ENDIF
利子 = 借金

IF 借金 > 0
	DRAWLINE
	PRINTL □金融会社 ジェラート金融
	PRINTL 
	CALL PASELI, @"現在の借金{借金}＄に"
	;利率はNORMAL:5%, HYPER:7%, ANOTHER:10%
	SELECTCASE FLAG:難易度
		CASE 1, -1
			PRINT 5%の利子
			TIMES 利子, 1.05
		CASE 2
			PRINT 7%の利子
			TIMES 利子, 1.07
		CASE 3
			PRINT 10%の利子
			TIMES 利子, 1.10
		CASE 4
			PRINT 30%の利子
			TIMES 利子, 1.30
	ENDSELECT
	利子 -= 借金
	CALL PASELIW, @"({利子}＄)がつき、合計{借金+利子}＄になりました。"
	借金 += 利子
ELSE
	SIF !OPTION:借入
		RETURN 0
	DRAWLINE
	PRINTL □金融会社 ジェラート金融
	PRINTL 
	PRINT 新たに借金をすることができます(10日ごとに利子が
	SELECTCASE FLAG:難易度
		CASE 1, -1
			PRINT 5%
		CASE 2
			PRINT 7%
		CASE 3
			PRINT 10%
		CASE 4
			PRINT 30%
	ENDSELECT
	PRINTL つきます)
	PRINTL [0] する
	PRINTL [1] しない
	PRINTL [2] そんなつもりは無い(借金がないとき非表示 オプションから再度表示出来ます)
	CALL INPUTF, 0, 1, 2
	SIF RESULT == 1
		RETURN 0
	IF RESULT == 2
		OPTION:借入 = 0
		RETURN 0
	ENDIF
ENDIF

;変数「方向」は、1なら返済で0なら借入
SIF 借金 > 0
	方向 = 1

DO
	DRAWLINE
	IF 返済専用
		PRINTL 返済する金額を入力してください
	ELSE
		PRINTL 返済・借入する金額を入力してください
	ENDIF
	PRINTL キーボードで直接入力もできます
	PRINTL 
	IF 方向
		CALL PASELIL, @"借金……{借金-金額}＄"
	ELSE
		CALL PASELIL, @"借金……{借金+金額}＄"
	ENDIF
	SELECTCASE 方向
		CASE 1
			PRINTL 　　　　↑
		CASEELSE
			PRINTL 　　　　｜
	ENDSELECT
	CALL PASELI, @"　　　　｜{金額}＄"
	SELECTCASE 方向
		CASE 1
			PRINTL 返済
		CASEELSE
			PRINTL 借入
	ENDSELECT
	SELECTCASE 方向
		CASE 0
			PRINTL 　　　　↓
		CASEELSE
			PRINTL 　　　　｜
	ENDSELECT
	IF 方向
		CALL PASELIL, @"所持金…{MONEY-金額}＄"
	ELSE
		CALL PASELIL, @"所持金…{MONEY+金額}＄"
	ENDIF
	
	PRINTL 
	IF !返済専用
		PRINTBUTTON "[CHANGE] 返済・借入の変更", "CHANGE"
		PRINTL 
	ENDIF
	IF 方向
		PRINTBUTTON "[ MAX ] 持ち金全てを返済にあてる", "MAX"
		PRINTL 
	ELSE
		PRINTL 
	ENDIF
	PRINTBUTTON "[+5000000]", "+5000000"
	PRINTBUTTON "[+500000]", "+500000"
	PRINTBUTTON "[+50000]", "+50000"
	PRINTBUTTON "[+5000]", "+5000"
	PRINTL 
	PRINTBUTTON "[+2000000]", "+2000000"
	PRINTBUTTON "[+200000]", "+200000"
	PRINTBUTTON "[+20000]", "+20000"
	PRINTBUTTON "[+2000]", "+2000"
	PRINTL 
	PRINTBUTTON "[+1000000]", "+1000000"
	PRINTBUTTON "[+100000]", "+100000"
	PRINTBUTTON "[+10000]", "+10000"
	PRINTBUTTON "[+1000]", "+1000"
	PRINTL 
	PRINTL [                0               ]
	PRINTBUTTON "[-1000000]", "-1000000"
	PRINTBUTTON "[-100000]", "-100000"
	PRINTBUTTON "[-10000]", "-10000"
	PRINTBUTTON "[-1000]", "-1000"
	PRINTL 
	PRINTBUTTON "[-2000000]", "-2000000"
	PRINTBUTTON "[-200000]", "-200000"
	PRINTBUTTON "[-20000]", "-20000"
	PRINTBUTTON "[-2000]", "-2000"
	PRINTL 
	PRINTBUTTON "[-5000000]", "-5000000"
	PRINTBUTTON "[-500000]", "-500000"
	PRINTBUTTON "[-50000]", "-50000"
	PRINTBUTTON "[-5000]", "-5000"
	PRINTL 
	PRINTBUTTON "[ENTER]", "ENTER"
	
	INPUTS
	SELECTCASE RESULTS
		CASE "", "ENTER", "enter", "Enter", "E", "e"
			BREAK
		CASE "CHANGE", "change", "Change", "C", "c"
			IF 方向 && !返済専用
				方向 = 0
			ELSE
				方向 = 1
			ENDIF
		CASE "MAX", "max", "Max", "M", "m"
			SIF 方向
				金額 = MIN(借金, MONEY)
		CASE "0"
			金額 = 0
		CASEELSE
			IF CHARATU(RESULTS, 0) == "+"
				SIF ISNUMERIC(SUBSTRING(RESULTS, 1))
					金額 += TOINT(SUBSTRING(RESULTS, 1))
			ELSEIF CHARATU(RESULTS, 0) == "-"
				SIF ISNUMERIC(SUBSTRING(RESULTS, 1))
					金額 -= TOINT(SUBSTRING(RESULTS, 1))
			ELSEIF ISNUMERIC(RESULTS)
				金額 = TOINT(RESULTS)
			ENDIF
	ENDSELECT
	SIF 金額 < 0
		金額 = 0
	;借りている額より多く返せないし、持っている額より多く返せない
	SIF 方向
		金額 = LIMIT(金額, 0, MIN(借金, MONEY))
	CLEARLINE 21
LOOP 1

IF 方向
	CALL 現金出納帳記録, @"借金返済/-{金額}"
	MONEY -= 金額
	借金 -= 金額
ELSE
	CALL 現金出納帳記録, @"借入金/{金額}"
	MONEY += 金額
	借金 += 金額
ENDIF
