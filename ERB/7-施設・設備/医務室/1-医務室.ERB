@医務室
#DIM DYNAMIC 配属人数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 補充費
#DIM DYNAMIC 名前文字数
#DIM DYNAMIC 禁断の知識あり
#DIM DYNAMIC 調合知識あり
#DIM DYNAMIC 植生学人数
#DIM DYNAMIC LTARGET

SIF TALENT:MASTER:禁断の知識
	禁断の知識あり = 1
SIF TALENT:MASTER:調合知識
	調合知識あり = 1

配属人数 = CMATCH(CSTR:配属, "医務室")
DRAWLINE
PRINTL □医務室
PRINTL 　医療班を配属させたり、医薬品の補充によってキャラクターの健康管理が可能です
PRINTL 　配属中のキャラの能力に応じて体力・気力・ストレス値の回復量が変動します
PRINT 　医薬品の在庫:
SELECTCASE リソース:医薬品
	CASE 0
		SETCOLORBYNAME gray
	CASE 1 TO 15
		SETCOLORBYNAME red
	CASE 16 TO 50
		SETCOLORBYNAME yellow
	CASE 51 TO 100
		SETCOLORBYNAME lime
ENDSELECT
PRINTFORM {リソース:医薬品}\%
RESETCOLOR
CALL PASELIL, @" 配属人数:{配属人数}/3 \@ !ORIGIN ? 所持金:{MONEY}＄ #\@"
PRINTL □担当医
IF CMATCH(CSTR:配属, "医務室")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "医務室"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	LTARGET = TARGET
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "医務室"
			PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 
			SIF !CFLAG:LCOUNT:配属完了
				PRINT 【配属準備中】
			PRINTFORM 医療技能:{ABL:LCOUNT:医療技能} 
			SIF 統率者("医務室") && !TALENT:LCOUNT:統率
				PRINT 統率者ボーナス+1 
			SIF 特質("ワーカホリック", LCOUNT)
				PRINTFORM [ワーカホリック+{特質("ワーカホリック", LCOUNT)/10}]
			CALL ISTALENT, LCOUNT, "調合知識"
			SIF TALENT:LCOUNT:調合知識
				調合知識あり = 1
			;CALL ISTALENT, LCOUNT, "治療"
			IF TALENT:LCOUNT:禁断の知識
				PRINT [禁断の知識]
				禁断の知識あり = 1
			ENDIF
			IF TALENT:LCOUNT:植生学
				PRINT [植生学]
				植生学人数++
			ENDIF
			IF TALENT:LCOUNT:謎の魅力
				PRINT [謎の魅力]
			ELSEIF TALENT:LCOUNT:魅力
				PRINT [魅力]
			ELSEIF TALENT:LCOUNT:魅惑
				PRINT [魅惑]
			ELSEIF TALENT:LCOUNT:人気
				PRINT [人気]
			ENDIF
			CALL ISTALENT, LCOUNT, "統率"
			TARGET = LCOUNT
			CALL ABL_CHECK_医療技能
			SIF RESULT && ABL:LCOUNT:医療技能 < 10
				PRINTBUTTON "[医療技能上昇可能]", LCOUNT+10
			PRINTL 
		ENDIF
	NEXT
	TARGET = LTARGET
ELSE
	PRINTL 　[無し]
ENDIF
PRINTL 
;医薬品 主人を除くキャラ人数*5000
;浪費癖一人につき一人頭1000アップ
;植生学一人につき一人頭500削減
補充費 = (CHARANUM-1)*(5000+浪費癖人数("医務室")*1000-植生学人数*500)

CALL PASELIL, @"　[1] - 医薬品を補充する\@ !ORIGIN ? ({補充費}＄)#\@"
PRINTL 　[2] - キャラを配属させる
PRINTL 　[3] - 利用履歴を確認する（過去5日分）
IF 禁断の知識あり
	PRINTL 　[4] - 身体改造を行う
ELSE
	CALL PRINTCOLORL, "　[-] - 身体改造を行う(主人か配属中のキャラが[禁断の知識]を持っている必要があります)", "gray"
ENDIF
IF 調合知識あり
	PRINTL 　[5] - 薬品系アイテムを強化する
ELSE
	CALL PRINTCOLORL, "　[-] - 薬品系アイテムを強化する(主人か配属中のキャラが[調合知識]を持っている必要があります)", "gray"
ENDIF
IF 禁断の知識あり
	PRINTL 　[6] - 人体錬成をする
ELSE
	CALL PRINTCOLORL, "　[-] - 人体錬成をする(主人か配属中のキャラが[禁断の知識]を持っている必要があります)", "gray"
ENDIF
IF MOD:STS
	PRINTL 　[7] - リソースを確認する
	PRINTL 　[8] - キャラを安楽死させる
	PRINTL 　[9] - キャラを解体する
ENDIF

;IF 配属人数
;	PRINTL [3] - 健康診断を行う
;ELSE
;	SETCOLORBYNAME gray
;	PRINTL [-] - 健康診断を行う(キャラを配属する必要があります)
;	RESETCOLOR
;ENDIF
DRAWLINE
PRINTPLAIN 　医務室　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		IF リソース:医薬品 >= 100
			PRINTW 医薬品は十分に揃っています
		ELSEIF MONEY > 補充費 || ORIGIN
			PRINTW 医薬品を補充しました
			MONEY -= 補充費
			CALL 現金出納帳記録, @"医薬品の補充費/-{補充費}"
			リソース:医薬品 = 100
		ELSE
			PRINTW 所持金が足りません
		ENDIF
	CASE 2
		CALL STRATEGY_PERSONAL, "医務室"
		;フラグの再チェック
		禁断の知識あり = 0
		調合知識あり = 0
		FOR LCOUNT, 0, CHARANUM
			IF CSTR:LCOUNT:配属 == "医務室" || LCOUNT == 0
				SIF TALENT:LCOUNT:禁断の知識
					禁断の知識あり = 1
				SIF TALENT:LCOUNT:調合知識
					調合知識あり = 1
			ENDIF
		NEXT
	CASE 3
		CALL 医務室利用者
	CASE 4
		CALL BODY_MOD_1
		SIF RESULT
			JUMP ターン終了
	CASE 5
		CALL MED_MOD
	CASE 6
		CALL 人体錬成
		SIF RESULT
			JUMP ターン終了
	CASE 7
		CALL リソース一覧
	CASE 8
		CALL STS_KILL
		SIF RESULT
			JUMP ターン終了
	CASE 9
		CALL STS_BLOOD
		SIF RESULT
			JUMP ターン終了
	CASEELSE
		LTARGET = TARGET
		TARGET = RESULT-10
		SIF CSTR:配属 == "医療技能"
			CALL ABLUP, "医療技能"
		TARGET = LTARGET
	;CASE 3
	;	DRAWLINE
	;	FOR PAGING, 0, CHARANUM
	;		;色変えしたいのでここだけ特別処理
	;		CALL PAGING, PAGING != 0, "健康診断用", "健康診断"
	;	NEXT
	;	SIF RESULT == -1
	;		RESTART
	;	CALL 健康診断, RESULT
ENDSELECT

RESTART

@健康診断, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC クジャクアイ
;孔雀の瞳を持ってるキャラが主人・助手・担当医に居るかを見る
SIF TALENT:MASTER:孔雀の瞳
	クジャクアイ = 1
SIF ASSI > 0 && TALENT:ASSI:孔雀の瞳
	クジャクアイ = 1
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室" && TALENT:LCOUNT:孔雀の瞳
		クジャクアイ = 1
NEXT

DRAWLINE
PRINTL □患者
PRINTFORML 　名前:%NAME:対象%
PRINTFORML 　体力%BARBASE("体力", 対象, 32)%
PRINTFORML 　気力%BARBASE("気力", 対象, 32)%
PRINT 　ストレス値:
IF クジャクアイ
	SELECTCASE CFLAG:対象:ストレス値
		CASE 1 TO 99
			SETCOLORBYNAME lime
		CASE 100 TO 299
			SETCOLORBYNAME yellow
		CASE 300 TO 499
			SETCOLORBYNAME red
		CASE IS >= 500
			SETCOLORBYNAME darkred
	ENDSELECT
	PRINTFORML {CFLAG:対象:ストレス値}/999
	RESETCOLOR
ELSE
	SELECTCASE CFLAG:対象:ストレス値
		CASE 0
			PRINTL 無
		CASE 1 TO 99
			SETCOLORBYNAME lime
			PRINTL 低
		CASE 100 TO 299
			SETCOLORBYNAME yellow
			PRINTL 中
		CASE 300 TO 499
			SETCOLORBYNAME red
			PRINTL 高
		CASE IS >= 500
			SETCOLORBYNAME darkred
			PRINTL 過多
	ENDSELECT
	RESETCOLOR
ENDIF

PRINTL 

@TURNEND_医務室
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 男性医師
#DIM DYNAMIC 女性医師
#DIM DYNAMIC 医師魅力
#DIM DYNAMIC 利用確率
#DIM DYNAMIC 利用人数
#DIM DYNAMIC 調合知識持ち
#DIM DYNAMIC 利用確率補正
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC モチベ加算値
#DIM DYNAMIC 補正後

医務室体力補正 = 0
医務室気力補正 = 0
医務室ストレス補正 = 0

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室" && !CFLAG:LCOUNT:口上非表示
		TRYCALLFORM TURNEND_医務室_%CSVNAMERPS(NOF(LCOUNT))%
NEXT

;以下の処理は日付終了時のみ
SIF ターン == "昼"
	RETURN

;配属中のキャラを調べる
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		IF CFLAG:LCOUNT:配属完了
			医務室体力補正 += 30
			医務室気力補正 += 30
			医務室ストレス補正++
			;奉仕精神、医療技能を見る
			IF 統率者("医務室") && !TALENT:LCOUNT:統率
				医務室体力補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/2*10
				医務室気力補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/2*10
				医務室ストレス補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/3
				;ワーカホリックの補正が乗るのは医療技能だけ
				補正後 = ABL:LCOUNT:医療技能+1
				補正後 += 特質("ワーカホリック", LCOUNT)/10
			ELSE
				医務室体力補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/2*10
				医務室気力補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/2*10
				医務室ストレス補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/3
				;ワーカホリックの補正が乗るのは医療技能だけ
				補正後 = ABL:LCOUNT:医療技能
				補正後 += 特質("ワーカホリック", LCOUNT)/10
			ENDIF
			医務室体力補正 += LIMIT(ABL:LCOUNT:医療技能, 0, 10)/2*20
			医務室気力補正 += LIMIT(ABL:LCOUNT:医療技能, 0, 10)/2*20
			医務室ストレス補正 += LIMIT(ABL:LCOUNT:医療技能, 0, 10)/2
			SELECTCASE SEX(LCOUNT)
				CASE 1
					男性医師++
				CASE 2
					女性医師++
			ENDSELECT
			;医師の素質を見る
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "生意気", "倒錯的"
						医務室ストレス補正--
					CASE "気丈", "冷静", "自制心"
						医務室体力補正 += 10
						医務室気力補正 += 10
					CASE "衝動的"
						医務室体力補正 -= 10
						医務室気力補正 -= 10
					CASE "プライド高い"
						医務室体力補正 += 20
						医務室気力補正 += 20
						医務室ストレス補正--
					CASE "ハイテンション", "解放", "鼓舞"
						医務室気力補正 += 10
						医務室ストレス補正++
					;サボり魔はモチベに応じて基礎補正を打ち消す
					CASE "サボり魔"
						IF サボり判定(LCOUNT) && !ワーカホリックレベル(LCOUNT)
							医務室体力補正 -= 50
							医務室気力補正 -= 50
							医務室ストレス補正--
						ENDIF
					CASE "無関心"
						医務室体力補正 += 10
					CASE "楽観的"
						医務室体力補正 -= 10
						医務室気力補正 += 10
						医務室ストレス補正++
					CASE "悲観的"
						医務室体力補正 += 20
						医務室気力補正 -= 10
						医務室ストレス補正--
					CASE "針さばき", "潔癖症"
						医務室体力補正 += 20
						医務室気力補正 += 20
					CASE "汚れ無視"
						医務室体力補正 -= 20
						医務室気力補正 -= 20
					CASE "魅惑", "魅力", "人気"
						医務室気力補正 += 10
						利用確率補正 += 3
					CASE "謎の魅力"
						医務室気力補正 += 20
						医務室ストレス補正++
						利用確率補正 += 5
					CASE "治療"
						医務室体力補正 += 50
						医務室気力補正 += 50
						医務室ストレス補正 += 2
						利用確率補正 += 10
					CASE "母性"
						医務室体力補正 += 10
						医務室気力補正 += 10
						医務室ストレス補正++
						利用確率補正 += 3
					CASE "調合知識"
						調合知識持ち++
					CASE "世話好き"
						医務室体力補正 += 10
						医務室気力補正 += 20
						医務室ストレス補正++
						利用確率補正 += 3
				ENDSELECT
			NEXT
			
			;モチベ計算用にもう一度見る
			モチベ計算用 = 0
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "プライド高い", "冷静", "世話好き", "自制心", "楽観的", "調合知識", "潔癖症", "サド", "母性", "治療"
						モチベ計算用++
					CASE "衝動的", "弱気", "無関心", "悲観的", "痛みに弱い", "男嫌い", "女嫌い", "同族嫌悪"
						モチベ計算用--
				ENDSELECT
			NEXT

			モチベ加算値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数("医務室")+(ABL:LCOUNT:奉仕精神/3)
			SIF TALENT:LCOUNT:世話好き
				モチベ加算値 -= 世話好き人数("医務室")
			;配属先のキャラの相性で計算
			モチベ加算値 += 相性モチベ補正(LCOUNT)
			CFLAG:LCOUNT:モチベーション = LIMIT(モチベ加算値, 1, 100)

			;実務経験加算
			EXP:LCOUNT:実務経験 += 2
			EXP:LCOUNT:医療経験++
			;習得早ければ+1
			SIF TALENT:LCOUNT:習得早い
				EXP:LCOUNT:実務経験++
		ENDIF
	ENDIF
NEXT

医務室体力補正 = MAX(医務室体力補正, 0)
医務室気力補正 = MAX(医務室気力補正, 0)
医務室ストレス補正 = MAX(医務室ストレス補正, 0)

;医薬品が足りてない場合はそれぞれ/10
IF リソース:医薬品 <= 0
	医務室体力補正 /= 10
	医務室気力補正 /= 10
	医務室ストレス補正 /= 10
ENDIF

;医務室利用履歴の変数を後ろに詰める 2次元配列ではARRAYSHIFTできないので力技
FOR LCOUNT, 3, -1, -1
	FOR LCOUNT2, 0, 1000
		医務室利用履歴:(LCOUNT+1):LCOUNT2 = %医務室利用履歴:LCOUNT:LCOUNT2%
		SIF LCOUNT == 0
			医務室利用履歴:LCOUNT:LCOUNT2 = 
	NEXT
NEXT

;利用者の判定
FOR LCOUNT, 0, CHARANUM
	;MASTERは除外
	SIF LCOUNT == MASTER
		CONTINUE
	;配属中の医師も除外
	SIF CSTR:LCOUNT:配属 == "医務室"
		CONTINUE
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	;残り体力と気力とストレス値から確率を割り出す
	;各値それぞれ最大100%(ストレス値は99%)で、合計を利用確率にする
	利用確率 = 100-(100*BASE:LCOUNT:体力)/MAXBASE:LCOUNT:体力
	利用確率 += 100-(100*BASE:LCOUNT:気力)/MAXBASE:LCOUNT:気力
	利用確率 += CFLAG:LCOUNT:ストレス値/10
	
	;利用確率があるなら素質での補正
	IF 利用確率
		利用確率 += 利用確率補正
		FOR LCOUNT2, 0, VARSIZE("TALENT")
			SIF !TALENT:LCOUNT:LCOUNT2
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT2
				CASE "気丈", "プライド高い", "強気", "痛みに強い"
					利用確率 -= 5
				CASE "生意気", "反抗的", "楽観的", "恥じらい", "薬毒耐性", "マゾ", "戦士"
					利用確率 -= 3
				CASE "臆病", "素直", "冷静", "悲観的", "寂しがり屋", "中毒しやすい", "暑さに弱い", "寒さに弱い"
					利用確率 += 3
				CASE "弱気", "痛みに弱い", "治療", "妊娠", "育児中"
					利用確率 += 5
			ENDSELECT
		NEXT
		;苦痛刻印*3
		利用確率 += MARK:LCOUNT:苦痛刻印*3
		;二日酔い +5
		SIF CFLAG:LCOUNT:二日酔い
			利用確率 += 5
	ENDIF
	;0以下だったら0に合わせる
	利用確率 = MAX(0, 利用確率)
	
	;男好き、女好きは元気でも来訪して薬を使いやがる
	SIF TALENT:LCOUNT:男好き
		利用確率 += 男性医師*10
	SIF TALENT:LCOUNT:女好き
		利用確率 += 女性医師*10
	
	;利用確率を0～100に合わせる
	利用確率 = LIMIT(利用確率, 0, 100)
	
	;利用するなら利用フラグを立てる
	IF 利用確率 >= RAND(1, 101)
		CFLAG:LCOUNT:医務室利用 = 1
		;利用履歴変数に名前を入れる
		医務室利用履歴:0:利用人数 = %NAME:LCOUNT%
		利用人数++
	ENDIF
NEXT

;利用者の数*30習得を増やす
;IF 利用人数 > 0
;	FOR LCOUNT, 0, CHARANUM
;		SIF CSTR:LCOUNT:配属 == "医務室" && CFLAG:LCOUNT:配属完了
;			JUEL:LCOUNT:習得 += 利用人数*30
;	NEXT
;ENDIF

;最後に利用者の割合に応じて医薬品を減らす 1ターンで最大10
;調合知識持ちだけ医薬品の消費を抑える
SIF 利用人数
	リソース:医薬品 -= MAX((利用人数*10)/CHARANUM-調合知識持ち, 1)

リソース:医薬品 = LIMIT(リソース:医薬品, 0, 100)

PRINTFORML {利用人数}人が医務室を利用しました

;利用人数に応じてさらに医療経験加算
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		SIF CFLAG:LCOUNT:配属完了
			EXP:LCOUNT:医療経験 += LIMIT(利用人数, 0, 3)
		;配属完了にする
		CFLAG:LCOUNT:配属完了 = 1
	ENDIF
NEXT

RETURN

@医務室利用者
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日
#DIM DYNAMIC 利用年, 5
#DIM DYNAMIC 利用月, 5
#DIM DYNAMIC 利用日, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ

;5日遡ってその日時を入れておく
年 = YEAR
月 = MONTH
日 = DAYS
FOR LCOUNT, 0, 5
	DO
		日--
		IF 日 <= 0
			月--
			IF 月 <= 0
				年--
				月 = 12
			ENDIF
			日 = 31
		ENDIF
	LOOP !EXIST_DAY(年, 月, 日)
	利用年:LCOUNT = 年
	利用月:LCOUNT = 月
	利用日:LCOUNT = 日
NEXT

VARLINE = 0

$LOOP
SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

DRAWLINE
PRINTFORML □{利用年:ページ}年{利用月:ページ}月{利用日:ページ}日の医務室利用者
FOR LCOUNT, 0, 1000
	IF 医務室利用履歴:ページ:LCOUNT == ""
		IF LCOUNT == 0
			PRINTL 
			PRINTL 利用者無し
			PRINTL 
		ENDIF
		BREAK
	ENDIF
	PRINTFORML 　%医務室利用履歴:ページ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAIN 　医務室利用履歴　
CALL PRINTSPACE, ページ > 0, "[1] 翌日の履歴 "
PRINT [0] 戻る 
CALL PRINTSPACE, ページ < 4, "[9] 前日の履歴 "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		VARLINE = 0
		RETURN
	CASE 1
		ページ--
		GOTO LOOP
	CASE 9
		ページ++
		GOTO LOOP
ENDSELECT

@出産用医務室ランク, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 医務室ランク

;配属中のキャラを調べる
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		IF CFLAG:LCOUNT:配属完了
			;医師の素質を見る
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "母性", "鼓舞"
						医務室ランク++
						医務室ランク++
					CASE "汚れ無視"
						医務室ランク--
				ENDSELECT
			NEXT
			IF 統率者("医務室") && !TALENT:LCOUNT:統率
				医務室ランク += MIN((ABL:LCOUNT:医療技能+1+特質("ワーカホリック", LCOUNT)/10)/2, 3)
			ELSE
				医務室ランク += MIN((ABL:LCOUNT:医療技能+特質("ワーカホリック", LCOUNT)/10)/2, 3)
			ENDIF
		ENDIF
	ENDIF
NEXT

;3人まで配属できるので最大12
RETURNF 医務室ランク

@STS_KILL
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象

DRAWLINE

PRINTL □安楽死させるキャラを選んでください
表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF TALENT:LCOUNT:無機物
		CONTINUE
	SIF TALENT:LCOUNT:スライム || TALENT:LCOUNT:溶ける快
		CONTINUE
	SIF TALENT:LCOUNT:ゾンビ || TALENT:LCOUNT:霊体
		CONTINUE
	SIF ゲームモード() == "経営モード" && CHARANAMEF(LCOUNT) == パートナー
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+20)
		CONTINUE
	CALLFORM DISABLE, "EVENT_安楽死", LCOUNT
	SIF RESULT
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT%
	PRINTL 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　安楽死可能なキャラは居ません
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　安楽死.{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[-1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, 次ページあり, "[-9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASEELSE
		対象 = RESULT
		PRINTFORML %NAME:対象%を安楽死させます。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT
			RESTART
		PRINTFORMW %CALLNAME:対象%に薬を投与すると、%CALLNAME:対象%は眠るように死んだ……
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 薬の投与で安楽死した
		CALL プレイログ記録, @"%NAME:対象%が死亡"
		CALL CLEAR_CHARA, 対象, 0, 1
		CFLAG:対象:死亡 = 1
		RETURN 1
ENDSELECT

RESTART

@STS_BLOOD
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象
#DIM DYNAMIC 獲得リソース数
#DIMS DYNAMIC 心臓リソース
#DIM DYNAMIC Ｖ挿入回数
#DIM DYNAMIC Ａ挿入回数
#DIM DYNAMIC Ｖ射精回数
#DIM DYNAMIC Ａ射精回数
#DIM DYNAMIC 最低保証
#DIM DYNAMIC 苦痛獲得数
#DIM DYNAMIC 恐怖獲得数
#DIM DYNAMIC 否定獲得数
#DIM DYNAMIC 絶頂回数

DRAWLINE

PRINT □解体するキャラを選んでください。
FONTBOLD
PRINTL 解体したキャラは死亡します
FONTREGULAR

表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF TALENT:LCOUNT:無機物
		CONTINUE
	SIF TALENT:LCOUNT:霊体
		CONTINUE
	SIF ゲームモード() == "経営モード" && CHARANAMEF(LCOUNT) == パートナー
		CONTINUE
	CALL DISABLE, "EVENT_解体", LCOUNT
	SIF RESULT
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+19)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT% 回収できる物:
	PRINTFORM \@ ISSLIME(LCOUNT) ? 粘液 # 血液 \@{獲得リソース数(LCOUNT, "体液")}ml 
	SIF 獲得リソース数(LCOUNT, "精液")
		PRINTFORM 精液{獲得リソース数(LCOUNT, "精液")}ml 
	SIF 獲得リソース数(LCOUNT, "母乳")
		PRINTFORM 母乳{獲得リソース数(LCOUNT, "母乳")}ml 
	SIF PENIS(LCOUNT)
		PRINT 男性器 
	SIF VAGINA(LCOUNT)
		PRINT 女性器 
	SIF TALENT:LCOUNT:バストサイズ >= 3
		PRINT 乳房 
	SIF TALENT:LCOUNT:動物耳
		PRINT ケモミミが付いた頭 
	SIF TALENT:LCOUNT:羽
		PRINT 翼の生えた胴体 
	IF !GROUPMATCH(CHARANAMEF(LCOUNT), "にんじん", "ハート")
		IF TALENT:LCOUNT:足使用不可
			PRINT 人魚の下半身 
		ELSE
			PRINT 臀部 
		ENDIF
	ENDIF
	SIF TALENT:LCOUNT:尻尾
		PRINT 切断された尻尾 
	SELECTCASE CSTR:LCOUNT:種族
		CASE "悪魔", "天使", "神", "魔術師"
			心臓リソース '= CSTR:LCOUNT:種族 + "の心臓"
		CASEELSE
			心臓リソース = 
	ENDSELECT
	SIF 心臓リソース != ""
		PRINTFORM %心臓リソース% 
	PRINTL 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　解体可能なキャラは居ません
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　解体.{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[-1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, 次ページあり, "[-9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASE 1 TO CHARANUM
		対象 = RESULT
		SIF TALENT:対象:無機物
			CONTINUE
		SIF ゲームモード() == "経営モード" && CHARANAMEF(対象) == パートナー
			CONTINUE
		PRINTFORML %NAME:対象%を解体します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		PRINTL [2] - 楽しみながら解体する
		BINPUT
		SIF RESULT == 1
			RESTART
		SETCOLORBYNAME red
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		PRINTFORML %NAME:対象%を解体しました
		;楽しみながら解体の処理
		IF RESULT == 2
			;挿入経験
			Ｖ挿入回数 = RAND(7, 14)
			Ａ挿入回数 = RAND(7, 14)
			Ｖ射精回数 = RAND(4, Ｖ挿入回数)
			Ａ射精回数 = RAND(4, Ａ挿入回数)
			IF VAGINA(対象)
				IF TALENT:対象:処女
					PRINTL ＜処女喪失＞
					TALENT:対象:処女 = 0
					CSTR:対象:処女 = %NAME:MASTER%
					IF TALENT:対象:性転換済
						CFLAG:対象:性転換処女の奪われ方 = 5
					ELSE
						CFLAG:対象:処女の奪われ方 = 5
					ENDIF
				ELSEIF TALENT:対象:再生処女
					PRINTL ＜再生処女喪失＞
					TALENT:対象:再生処女 = 0
				ENDIF
				CALL EXPUP, "Ｖ経験", Ｖ挿入回数, 対象
				CALL EXPUP, "Ａ経験", Ａ挿入回数, 対象
			ELSE
				CALL EXPUP, "Ａ経験", Ｖ挿入回数+Ａ挿入回数, 対象
			ENDIF
			IF PENIS(MASTER)
				IF VAGINA(対象)
					CALL EXPUP, "膣射経験", Ｖ射精回数, 対象
					中出され回数:対象:(NOF(MASTER)) += Ｖ射精回数
					中出し回数:MASTER:(NOF(対象)) += Ｖ射精回数
					アナル中出され回数:対象:(NOF(MASTER)) += Ａ射精回数
					アナル中出し回数:MASTER:(NOF(対象)) += Ａ射精回数
				ELSE
					アナル中出され回数:対象:(NOF(MASTER)) += Ｖ射精回数+Ａ射精回数
					アナル中出し回数:MASTER:(NOF(対象)) += Ｖ射精回数+Ａ射精回数
				ENDIF
				CALL EXPUP, "精液経験", Ｖ射精回数+Ａ射精回数, 対象
				CALL EXPUP, "射精経験", Ｖ射精回数+Ａ射精回数, MASTER
				経験回数:対象:(NOF(MASTER)) += Ｖ挿入回数+Ａ挿入回数
				経験回数:MASTER:(NOF(対象)) += Ｖ挿入回数+Ａ挿入回数
			ENDIF
			;その他経験
			絶頂回数 = 3+RAND(0, ABL:MASTER:技巧+ABL:対象:Ｖ感覚+ABL:対象:Ａ感覚+ABL:対象:マゾっ気)
			CALL EXPUP, "絶頂経験", RAND(5, 11), 対象
			IF PENIS(対象)
				TIMES 絶頂回数, 0.75
					CALL EXPUP, "射精経験", 絶頂回数, 対象
					CALL EXPUP, "精液経験", 絶頂回数, MASTER
			ENDIF
			IF MOD:SCAT2
				最低保証 = TALENT:対象:おもらし癖 ? 3 # 0
				CALL EXPUP, "放尿経験", 最低保証+RAND(1, 5), 対象
			ENDIF
			IF TALENT:対象:妊娠
				CALL EXPUP, "流産経験", 1, 対象
				TALENT:対象:妊娠 = 0
				SIF !TALENT:隷属
					否定獲得数 += RAND(150, 300)
			ENDIF
			CALL EXPUP, "異常経験", 5, 対象
			;珠
			苦痛獲得数 += RAND(100, 500)
			恐怖獲得数 += RAND(100, 500)
			IF TALENT:MASTER:サド
				苦痛獲得数 += 200
				恐怖獲得数 += 200
			ENDIF
			IF TALENT:MASTER:狂気
				苦痛獲得数 += 100
				恐怖獲得数 += 300
			ENDIF
			IF TALENT:対象:痛みに弱い
				TIMES 苦痛獲得数, 1.50
				TIMES 恐怖獲得数, 1.20
			ELSEIF TALENT:対象:痛みに強い
				TIMES 苦痛獲得数, 0.75
			ENDIF
			SIF TALENT:対象:臆病
				恐怖獲得数 += 100
			SIF FALLTYPE(対象) == 1
				否定獲得数 += RAND(150, 300)
			IF TALENT:対象:感情乏しい
				TIMES 恐怖獲得数, 0.80
				TIMES 否定獲得数, 0.80
			ENDIF
			CALL JUELUP, "苦痛", 苦痛獲得数*100, 対象
			CALL JUELUP, "恐怖", 恐怖獲得数*100, 対象
			SIF 否定獲得数
				CALL JUELUP, "否定", 否定獲得数*100, 対象
			SIF TALENT:対象:マゾ
				CALL JUELUP, "欲情", RAND(50, 200), 対象
			最低保証 = 恐怖獲得数/100
			CALL EXPUP, "嗜虐快楽経験", 最低保証+RAND(5, 10), MASTER
			IF !TALENT:MASTER:狂気
				TALENT:MASTER:狂気 = 1
				PRINTFORML %CALLNAME:MASTER%は【狂気】を得た
			ENDIF
		ENDIF
		;リソース（液体）
		PRINTFORML 保管済み\@ ISSLIME(対象) ? 粘液 # 血液 \@+{獲得リソース数(対象, "体液")}ml
		IF ISSLIME(対象)
			リソース:粘液 += 獲得リソース数(対象, "体液")
		ELSE
			リソース:血液 += 獲得リソース数(対象, "体液")
		ENDIF
		IF 獲得リソース数(対象, "精液")
			PRINTFORML 保管済み精液+{獲得リソース数(対象, "精液")}ml
			リソース:精液 += 獲得リソース数(対象, "精液")
		ENDIF
		IF 獲得リソース数(対象, "母乳")
			PRINTFORML 保管済み母乳+{獲得リソース数(対象, "母乳")}ml
			リソース:母乳 += 獲得リソース数(対象, "母乳")
		ENDIF
		;リソース（部位）
		IF PENIS(対象)
			PRINTFORML %NAME:対象%の男性器+1
			性器リソース:NOF(対象):男性器++
		ENDIF
		IF VAGINA(対象)
			PRINTFORML %NAME:対象%の女性器+1
			性器リソース:NOF(対象):女性器++
		ENDIF
		IF TALENT:対象:バストサイズ >= 3
			PRINTFORML %NAME:対象%の乳房+1
			性器リソース:NOF(対象):乳房++
		ENDIF
		IF TALENT:対象:動物耳
			PRINTL ケモミミが付いた頭+1
			リソース:ケモミミが付いた頭++
		ENDIF
		IF TALENT:対象:羽
			PRINTL 翼の生えた胴体+1
			リソース:翼の生えた胴体++
		ENDIF
		IF TALENT:対象:足使用不可 && !GROUPMATCH(CHARANAMEF(対象), "にんじん", "ハート")
			PRINTL 人魚の下半身+1
			リソース:人魚の下半身++
		ELSE
			PRINTFORML %NAME:対象%の臀部+1
			性器リソース:NOF(対象):臀部++
		ENDIF
		IF TALENT:対象:尻尾
			PRINTL 切断された尻尾+1
			リソース:切断された尻尾++
		ENDIF
		SELECTCASE CSTR:対象:種族
			CASE "悪魔", "天使", "神", "魔術師"
				心臓リソース '= CSTR:対象:種族 + "の心臓"
			CASEELSE
				心臓リソース = 
		ENDSELECT
		IF 心臓リソース != ""
			PRINTFORML %心臓リソース%+1
			リソース:心臓リソース++
		ENDIF

		CALL DNAデータ解析, 対象
		SIF TALENT:対象:ゾンビ && !CSVTALENT(NOF(対象), GETNUM(TALENT, "ゾンビ"))
			CALL 実績解除, "ロメロ再殺部隊"
		WAIT

		IF TALENT:対象:妊娠	
			PRINTFORML %CALLNAME:対象%の子宮からまだ未発達な胎児を摘出した……
			PRINTFORML [0] - 捨てる
			PRINTFORML [1] - 資源にする(血液+100ml)
			SIF ITEM:博物館
				PRINTFORML [2] - ホルマリン漬けにする
				;（加工スタッフ:%加工者% 加工技術:{加工技術合計()}）
			BINPUT
			SELECTCASE RESULT
				CASE 0
					PRINTFORMW %CALLNAME:対象%がお腹に宿していた子は生ゴミとして廃棄処分されました
				CASE 1
					PRINTFORML %CALLNAME:対象%の子供を資源として再利用する事にしました
					PRINTFORMW 保管済み血液+100ml
					リソース:血液 += 100
				CASE 2
					PRINTFORMW %CALLNAME:対象%が遺した子をホルマリンに漬け、その姿を保存しておく事にしました
					CALL 作品加工共通処理, "ホルマリン漬け", 対象, 1800, "胎児"
			ENDSELECT
		ENDIF

		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 解体されて血液や内臓を摘出された
		CALL CLEAR_CHARA, 対象, 0, 1
		CALL プレイログ記録, @"%NAME:対象%を解体"
		RETURN 1
ENDSELECT

RESTART


@獲得リソース数, 対象, 種別
#FUNCTION
#DIM DYNAMIC 対象
#DIMS DYNAMIC 種別
#DIM DYNAMIC リソース数

SELECTCASE 種別
	CASE "体液"
		IF TALENT:対象:小人体型
			リソース数 = 600
		ELSE
			SELECTCASE TALENT:対象:体格
				CASE 1
					リソース数 = 2500
				CASE 2
					リソース数 = 3000
				CASE 3
					リソース数 = 4000
				CASE 4
					リソース数 = 5000
			ENDSELECT
		ENDIF
	CASE "精液"
		IF 生殖機能♂(対象)
			IF TALENT:対象:小人体型
				リソース数 = 30
			ELSE
				リソース数 = 100
			ENDIF
			SIF TALENT:対象:絶倫
				リソース数 *= 2
		ENDIF
	CASE "母乳"
		SIF TALENT:対象:母乳体質
			リソース数 = 200
ENDSELECT

RETURNF リソース数

@リソース一覧
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 性器種別 = "男性器"

DRAWLINE
PRINTL 　□体液
PRINT 　　
FOR LCOUNT, GETNUM(リソース, "母乳"), GETNUM(リソース, "母乳")+50
	SIF リソース:LCOUNT
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT}ml
NEXT
PRINTL 

PRINTL 　□身体部位
PRINT 　　
FOR LCOUNT, GETNUM(リソース, "ケモミミが付いた頭"), GETNUM(リソース, "ケモミミが付いた頭")+50
	SIF リソース:LCOUNT
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT} 
NEXT
PRINTL 

PRINTL 　□機械部品
PRINT 　　
FOR LCOUNT, GETNUM(リソース, "マテリアル"), GETNUM(リソース, "マテリアル")+50
	SIF リソース:LCOUNT
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT} 
NEXT
PRINTL 

PRINTPLAIN 　□性器一覧 
FOR LCOUNT, 0, VARSIZE("性器リソース", 2)
	SIF ERDNAME(性器リソース, LCOUNT, 2) != ""
		PRINTFORM [{LCOUNT}] - %ERDNAME(性器リソース, LCOUNT, 2)% 
NEXT
PRINTL 
CALL 性器一覧, 性器種別, 1

SELECTCASE RESULT
	CASE -1
		RETURN
	CASE 0 TO 10
		性器種別 = %ERDNAME(性器リソース, RESULT, 2)%
ENDSELECT
RESTART

@性器一覧, 性器種別, 表示のみ
#DIMS DYNAMIC 性器種別
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示のみ

次ページあり = 0
表示数 = 0
飛ばした数 = 0

FOR LCOUNT, 0, 20000
	IF 性器リソース:LCOUNT:性器種別
		IF 飛ばした数 < ページ数*20
			飛ばした数++
			CONTINUE
		ENDIF
		IF 表示数 >= 20
			次ページあり = 1
			BREAK
		ENDIF
		IF 表示のみ
			PRINTFORML 　　%CSVNAMEF(LCOUNT)%の%性器種別%({性器リソース:LCOUNT:性器種別}個)
		ELSE
			PRINTFORML 　　[{LCOUNT, 5}] %CSVNAMEF(LCOUNT)%の%性器種別%({性器リソース:LCOUNT:性器種別}個)
		ENDIF
		表示数++
	ENDIF
NEXT

IF !表示数
	PRINTL 
	PRINTL 　----------何もありません----------
	PRINTL 
ELSE
	SIF 表示数 < 3
		PRINTL 
	SIF 表示数 < 2
		PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　%性器種別%一覧　
CALL PRINTSPACE, ページ数 > 0, @"[-1] - 前ページ "
PRINT [-5] - 戻る 
CALL PRINTSPACE, 次ページあり, @"[-9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE -1
		ページ数--
		SIF 表示のみ
			RETURN 0
	CASE -9
		ページ数++
		SIF 表示のみ
			RETURN 0
	CASE -5
		ページ数 = 0
		RETURN -1
	CASEELSE
		ページ数 = 0
		RETURN RESULT
ENDSELECT
RESTART
