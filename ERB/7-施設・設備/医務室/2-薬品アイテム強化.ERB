@MED_MOD
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 強化値, 3
#DIM DYNAMIC 名前文字数
#DIM DYNAMIC 人数
DRAWLINE

PRINTL □薬品系アイテム強化
PRINTL 　キャラの能力に応じた強化値を薬品系アイテムに付与できます
PRINTL 　いつでも切り替え可能ですが、付与時点での強化値がコマンドに反映されます
PRINTL □現在の強化値
人数 = 0
VARSET 強化値
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室"
		CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
NEXT
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT%:
		IF !CFLAG:LCOUNT:配属完了
			PRINT 【配属準備中】
		ELSE
			強化値:人数 += ABL:LCOUNT:工作技能
			強化値:人数 += ABL:LCOUNT:魔法技能
			SIF TALENT:LCOUNT:治療
				強化値:人数 += 5
			SIF TALENT:LCOUNT:調合知識
				強化値:人数 += 10
			SIF TALENT:LCOUNT:禁断の知識
				強化値:人数 += 5
			SIF TALENT:LCOUNT:治療
				強化値:人数 *= 2
			SIF TALENT:LCOUNT:調合知識
				強化値:人数 *= 2
			SIF TALENT:LCOUNT:禁断の知識
				強化値:人数 *= 2
			PRINTFORML {強化値:人数}
		ENDIF
		人数++
	ENDIF
NEXT
PRINTFORML 　%"合計", 名前文字数, LEFT%:{SUMARRAY(強化値)}
PRINTL
IF SUMARRAY(強化値) < 10
	PRINTL 
	PRINTL 　強化値合計が10以上でないと強化はできません
	PRINTL 
ELSE
	IF ローション強化値
		PRINTFORML 　[1] - ローションの強化を元に戻す(現在:{ローション強化値})
	ELSE
		PRINTL 　[1] - ローションを強化する
	ENDIF
	IF 媚薬強化値
		PRINTFORML 　[2] - 媚薬の強化を元に戻す(現在:{媚薬強化値})
	ELSE
		PRINTL 　[2] - 媚薬を強化する
	ENDIF
	IF 利尿剤強化値
		PRINTFORML 　[3] - 利尿剤の強化を元に戻す(現在:{利尿剤強化値})
	ELSE
		PRINTL 　[3] - 利尿剤を強化する
	ENDIF
	IF 強精神薬強化値
		PRINTFORML 　[4] - 強精神薬の強化を元に戻す(現在:{強精神薬強化値})
	ELSE
		PRINTL 　[4] - 強精神薬を強化する
	ENDIF
ENDIF

DRAWLINE
PRINTPLAIN 　薬品強化　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			SIF SUMARRAY(強化値) < 10
				CONTINUE
			IF ローション強化値
				PRINTW ローションの強化を元に戻しました
				ローション強化値 = 0
			ELSE
				PRINTW ローションを強化しました
				ローション強化値 = SUMARRAY(強化値)
			ENDIF
			RESTART
		CASE 2
			SIF SUMARRAY(強化値) < 10
				CONTINUE
			IF 媚薬強化値
				PRINTW 媚薬の強化を元に戻しました
				媚薬強化値 = 0
			ELSE
				PRINTW 媚薬を強化しました
				媚薬強化値 = SUMARRAY(強化値)
			ENDIF
			RESTART
		CASE 3
			SIF SUMARRAY(強化値) < 10
				CONTINUE
			IF 利尿剤強化値
				PRINTW 利尿剤の強化を元に戻しました
				利尿剤強化値 = 0
			ELSE
				PRINTW 利尿剤を強化しました
				利尿剤強化値 = SUMARRAY(強化値)
			ENDIF
			RESTART
		CASE 4
			SIF SUMARRAY(強化値) < 10
				CONTINUE
			IF 強精神薬強化値
				PRINTW 強精神薬の強化を元に戻しました
				強精神薬強化値 = 0
			ELSE
				PRINTW 強精神薬を強化しました
				強精神薬強化値 = SUMARRAY(強化値)
			ENDIF
			RESTART
	ENDSELECT
LOOP 1

