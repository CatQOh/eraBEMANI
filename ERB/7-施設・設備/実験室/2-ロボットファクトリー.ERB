@ロボットファクトリー
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIM DYNAMIC 魔法技能記録用
#DIM DYNAMIC オリジナル

PRINTL □ここではロボットの制作ができます
PRINT 　
FOR LCOUNT, GETNUM(リソース, "マテリアル"), GETNUM(リソース, "マテリアル")+50
	IF リソース:LCOUNT
		SIF LINEISEMPTY()
			PRINT 　
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT} 
	ENDIF
NEXT
CALL NEWLINE

CALL 実験室レベル計算
PRINTFORML □研究レベル:{基礎研究レベル}/50 欲望レベル:{欲望研究レベル}/50
PRINTFORML 　サディストレベル:{サド研究レベル}/50 マゾヒストレベル:{マゾ研究レベル}/50
PRINTL 
PRINTL 　[1] - ロボットを制作する
PRINTL 　[2] - 分析データからロボットを複製する
SIF MOD:STS
	PRINTL 　[3] - ロボットを廃棄する
CALL PASELIL, @"　[4] - 人格をロボットに移植する\@ !ORIGIN ? (100000＄が必要)#\@"
PRINTL 　[5] - ロボットを改造する
DRAWLINE
PRINTPLAIN 　ロボットファクトリー　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		LTARGET = TARGET
		CALL ADD_ランダムキャラ, RAND(1, 3), -1, 1
		TARGET = CHARANUM-1
		CSTR:種族 = 機械
		TALENT:無機物 = 1
		MAXBASE:体力 = 1500
		MAXBASE:気力 = 1500
		VARSET TALENT:0, 0, 1
	CASE 2
		LTARGET = TARGET
		CALL 所持DNA, 1
		SIF RESULT == 0
			RESTART
		オリジナル = RESULT
		CALL CHARA_ADD, オリジナル
		TARGET = CHARANUM-1
		CFLAG:複製 = オリジナル
		魔法技能記録用 = ABL:魔法技能
		;EXPとABLを初期化するが魔法技能だけ例外
		VARSET EXP:0
		VARSET ABL:0
		ABL:魔法技能 = 魔法技能記録用
		;相性や関係も初期化する
		VARSET RELATION:0, 100
		CALL 初期経験リセット, TARGET
		SIF PENIS(TARGET)
			TALENT:童貞 = 1
		SIF VAGINA(TARGET)
			TALENT:処女 = 1
	CASE 3
		CALL STS_MACHINE
		SIF RESULT
			RETURN 1
		RESTART
	CASE 4
		IF MONEY < 100000 && !ORIGIN
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		CALL 機械化
		SIF RESULT
			RETURN 1
		RESTART
	CASE 5
		PRINTL □改造するロボットを選んでください
		FOR PAGING, 1, CHARANUM
			CALL PAGING, CSTR:PAGING:種族 == "機械" || TALENT:PAGING:無機物
		NEXT
		SELECTCASE RESULT
			CASE -1
				RESTART
			CASEELSE
				CALL ロボット改造, RESULT
				SIF RESULT
					RETURN 1
		ENDSELECT
		RESTART
ENDSELECT

;10000以降のNOを使うので子供の数と共用
NO:TARGET = FLAG:子供の数+10001
CFLAG:人工物 = 1
IF CFLAG:複製
	NAME:TARGET = %NAME:TARGET%(クローン\@ 分析データ:(CFLAG:複製) >= 2 ? {分析データ:(CFLAG:複製)} #\@)
	CALLNAME:TARGET = %NAME:TARGET%
ENDIF
CALL ロボット制作
SIF !RESULT
	TARGET = LTARGET
SIF RESULT
	RETURN 1

RESTART

@ロボット制作
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC アイテムあり
;余分に要素数を取っておく
#DIM DYNAMIC 素質変更可能, 1000
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 耐久コスト
#DIM DYNAMIC 製作コスト
#DIM DYNAMIC 素質コスト
#DIM DYNAMIC コスト基準額
#DIM DYNAMIC 感覚合計
#DIM DYNAMIC 交友合計
#DIM DYNAMIC 中毒合計

コスト基準額 = 30000-(基礎研究レベル*400)

;選択可能な素質とコストを設定する
VARSET 素質変更可能
製作コスト = 5
素質コスト = 0
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		;コスト-1
		CASE "小柄体型"
			素質変更可能:LCOUNT = -1
			SIF TALENT:LCOUNT
				素質コスト -= 1
		;コスト1
		{
		CASE "生意気", "臆病", "素直", "反抗的", "ハイテンション", "大人しい", "世話好き",
			 "浪費癖", "無関心", "好奇心", "痛みに弱い", "習得遅い", "潔癖症", "汚れ無視",
			 "快感の否定", "倒錯的", "男好き", "女好き", "サド", "マゾ", "小悪魔", "遊び人",
			 "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "動物耳", "回復遅い", "鼓舞", "幼稚", 
		}
			素質変更可能:LCOUNT = 1
			SIF TALENT:LCOUNT
				素質コスト += 1
		;コスト2
		{
		CASE "痛みに強い", "習得早い", "舌使い", "買い物上手", "献身的", "快感に素直", "魅力", "魅惑",
			 "統率", "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "ふたなり", "回復早い", "治療", "孔雀の瞳"
		}
			素質変更可能:LCOUNT = 2
			SIF TALENT:LCOUNT
				素質コスト += 2
		;コスト3
		CASE "調合知識", "謎の魅力", "ネクロマンサー", "禁断の知識"
			素質変更可能:LCOUNT = 3
			SIF TALENT:LCOUNT
				素質コスト += 3
	ENDSELECT
NEXT

TALENT:童貞 = PENIS(TARGET)
TALENT:処女 = VAGINA(TARGET)

製作コスト += 素質コスト

PRINTL □ロボットを制作します
FOR LCOUNT, GETNUM(リソース, "マテリアル"), GETNUM(リソース, "マテリアル")+50
	IF リソース:LCOUNT
		SIF LINEISEMPTY()
			PRINT 　
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT} 
		SIF ERDNAME(リソース, LCOUNT) != "マテリアル"
			アイテムあり = 1
	ENDIF
NEXT
CALL NEWLINE

PRINTL 
CALL PASELIL, @"□完成後の性能(基礎研究レベル:{基礎研究レベル} 1コストにつき{コスト基準額}＄必要)"
PRINTFORML 　名前:%NAME:TARGET%
PRINTFORML 　性別:\@ TALENT:性別 == 1 ? 男性 # 女性 \@ [8] 変更する
PRINTFORM 　体力:{MAXBASE:体力} 気力:{MAXBASE:気力} 
PRINTPLAIN  
IF MAXBASE:体力 > 1500
	PRINT [-9] -300/コスト-1 
ELSE
	PRINTPLAIN 　　　　　　　　　
ENDIF
	
PRINTL [9] +300/コスト+1 
;初期値は1500 300ごとにコスト1上がる
耐久コスト = (MAXBASE:体力-1500)/300
製作コスト += 耐久コスト
PRINT 　素質:
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別", "恋心", "既婚者", "バストサイズ"
		CASEELSE
			SIF TALENT:LCOUNT
				PRINTFORM [%TALENTNAME:LCOUNT%]
	ENDSELECT
NEXT
PRINTL 
SIF 素質コスト > 10 && !CFLAG:複製
	CALL PRINTCOLORL, @"　※警告※ コスト超過 製造中に性格プロトコルに異常発生の恐れ(素質コスト:{素質コスト}/10)", "red"
SIF !ORIGIN
	CALL PASELIL, @"　基礎コスト{コスト基準額*5}＄+耐久コスト{耐久コスト*コスト基準額}+素質総コスト:{素質コスト*コスト基準額}＄ = 必要製作コスト:{製作コスト*コスト基準額}＄"
PRINTL 
IF !CFLAG:複製
	PRINTL 　[1] - 素質を変更する
	PRINTL 　[2] - 素質をプリセットから選ぶ
	SIF アイテムあり
		PRINTL 　[3] - 部品を使用する
	PRINTL 　[4] - 名前を変更する
ENDIF
DRAWLINE
PRINTPLAIN 　ロボットファクトリー　
PRINTL [0] - 戻る [10] - 作成する

BINPUT
SELECTCASE RESULT
	CASE 0
		CALL CLEAR_CHARA, TARGET, 1
		RETURN 0
	CASE 1
		VARLINE = 0
		$TALENT
		SIF VARLINE
			CLEARLINE LINECOUNT-VARLINE
		VARLINE = LINECOUNT
		DRAWLINE
		PRINTL □素質を選択してください 括弧内は製作コスト
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF 素質変更可能:LCOUNT
				PRINTFORMLC [{LCOUNT, 3}] %TALENTNAME:LCOUNT%({素質変更可能:LCOUNT}) \@ TALENT:LCOUNT ? ○ # × \@
		NEXT
		CALL NEWLINE
		SIF TALENT:性別 == 2
			PRINTFORML [{TALENTF("バストサイズ")}] バストサイズ:%バストサイズ(TALENT:バストサイズ)%
		DRAWLINE
		PRINTPLAIN 　ロボットファクトリー　
		PRINTL [-1] - 戻る
		BINPUT
		SIF RESULT == -1
			RESTART
		LRESULT = RESULT
		IF INRANGE(LRESULT, 0, VARSIZE("TALENT"))
			IF TALENTNAME:LRESULT == "バストサイズ" && TALENT:性別 == 2
				PRINTL [1] 絶壁にする
				PRINTL [2] 貧乳にする
				PRINTL [3] 並乳にする
				PRINTL [4] 巨乳にする
				PRINTL [5] 豊乳にする
				PRINTL [6] 爆乳にする
				BINPUT
				TALENT:バストサイズ = RESULT
				GOTO TALENT
			ELSEIF 素質変更可能:LRESULT
				INVERTBIT TALENT:LRESULT, 0
				GOTO TALENT
			ENDIF
		ENDIF
		RESTART
	CASE 2
		PRINTL プリセットを選択してください
		PRINTL [1] - 従順
		PRINTL [2] - 家事手伝い
		PRINTL [3] - セクサロイド
		PRINTL [4] - ポンコツ
		PRINTL [0] - 戻る
		BINPUT
		SELECTCASE RESULT
			CASE 1
				VARSET TALENT:0, 0, 1
				TALENT:素直 = 1
				TALENT:快感に素直 = 1
				TALENT:大人しい = 1
				TALENT:献身的 = 1
				TALENT:汚れ無視 = 1
			CASE 2
				VARSET TALENT:0, 0, 1
				TALENT:世話好き = 1
				TALENT:習得早い = 1
				TALENT:買い物上手 = 1
				TALENT:潔癖症 = 1
				TALENT:鼓舞 = 1
			CASE 3
				VARSET TALENT:0, 0, 1
				TALENT:好奇心 = 1
				TALENT:汚れ無視 = 1
				TALENT:回復早い = 1
				TALENT:男好き = 1
				TALENT:女好き = 1
				TALENT:舌使い = 1
				TALENT:小悪魔 = 1
			CASE 4
				VARSET TALENT:0, 0, 1
				TALENT:反抗的 = 1
				TALENT:回復遅い = 1
				TALENT:浪費癖 = 1
				TALENT:痛みに弱い = 1
				TALENT:習得遅い = 1
				TALENT:遊び人 = 1
		ENDSELECT
		RESTART
	CASE 3
		VARLINE = 0
		$ABL
		SIF VARLINE
			CLEARLINE LINECOUNT-VARLINE
		VARLINE = LINECOUNT
		感覚合計 = 0
		IF リソース:性感センサー
			PRINTFORML □感覚の強化 性感センサーを使用(現在{リソース:性感センサー}個所持)
			FOR LCOUNT, 0, 100
				SELECTCASE ABLNAME:LCOUNT
					CASE "Ｖ感覚"
						SIF !VAGINA(TARGET)
							CONTINUE
					CASE "Ｃ感覚", "Ａ感覚", "Ｂ感覚"
					CASEELSE
						CONTINUE
				ENDSELECT
				感覚合計 += ABL:LCOUNT
				PRINTPLAINFORM 　%ABLNAME:LCOUNT%Lv{ABL:LCOUNT} 
				PRINTFORM [-{LCOUNT+1}] %ABLNAME:LCOUNT%-1 
				PRINTFORM [{LCOUNT+1}] %ABLNAME:LCOUNT%+1
				PRINTL 
			NEXT
		ENDIF
		交友合計 = 0
		IF リソース:交友インターフェイス
			PRINTFORML □コミュニケーション能力を強化 交友インターフェイスを使用(現在{リソース:交友インターフェイス}個所持)
			FOR LCOUNT, 0, 100
				SELECTCASE ABLNAME:LCOUNT
					CASE "従順", "欲望", "技巧", "奉仕精神"
					CASEELSE
						CONTINUE
				ENDSELECT
				交友合計 += ABL:LCOUNT
				PRINTPLAINFORM 　%ABLNAME:LCOUNT%Lv{ABL:LCOUNT} 
				PRINTFORM [-{LCOUNT+1}] %ABLNAME:LCOUNT%-1 
				PRINTFORM [{LCOUNT+1}] %ABLNAME:LCOUNT%+1
				PRINTL 
			NEXT
		ENDIF
		中毒合計 = 0
		IF リソース:制御不能基板
			PRINTFORML □不規則性プログラムを追加 制御不能基板が必要(現在{リソース:制御不能基板}個所持)
			FOR LCOUNT, 0, 100
				SELECTCASE ABLNAME:LCOUNT
					CASE "自慰中毒", "性液中毒", "レズ中毒", "ゲイ中毒", "性交中毒"
					CASEELSE
						CONTINUE
				ENDSELECT
				中毒合計 += ABL:LCOUNT
				PRINTPLAINFORM 　%ABLNAME:LCOUNT%Lv{ABL:LCOUNT} 
				PRINTFORM [-{LCOUNT+1}] %ABLNAME:LCOUNT%-1 
				PRINTFORM [{LCOUNT+1}] %ABLNAME:LCOUNT%+1
				PRINTL 
			NEXT
		ENDIF
		SIF リソース:感情再現デバイス（恋慕）
			PRINTL [100] - 恋慕を付ける
		SIF リソース:感情再現デバイス（淫乱）
			PRINTL [101] - 淫乱を付ける
		SIF リソース:感情再現デバイス（服従）
			PRINTL [102] - 服従を付ける
		SIF FALLTYPE(TARGET)
			PRINTL [103] - 陥落素質を付けない
		DRAWLINE
		PRINTPLAIN 　アイテム使用　
		PRINTL [0] - 戻る
		BINPUT
		LRESULT = RESULT
		SELECTCASE ABS(LRESULT)
			CASE 0
				IF 感覚合計 > リソース:性感センサー
					PRINTW 性感センサーが足りません
					GOTO ABL
				ENDIF
				IF 交友合計 > リソース:交友インターフェイス
					PRINTW 交友インターフェイスが足りません
					GOTO ABL
				ENDIF
				IF 中毒合計 > リソース:制御不能基板
					PRINTW 制御不能基板が足りません
					GOTO ABL
				ENDIF
				RESTART
			CASE 1 TO 99
				IF SIGN(LRESULT) == -1
					SIF ABL:(ABS(LRESULT)-1) > 0
						ABL:(ABS(LRESULT)-1)--
				ELSE
					SIF ABL:(LRESULT-1) < 10
						ABL:(LRESULT-1)++
				ENDIF
			CASE 100
				TALENT:恋慕 = 1
				TALENT:淫乱 = 0
				TALENT:服従 = 0
			CASE 101
				TALENT:恋慕 = 0
				TALENT:淫乱 = 1
				TALENT:服従 = 0
			CASE 102
				TALENT:恋慕 = 0
				TALENT:淫乱 = 0
				TALENT:服従 = 1
			CASE 103
				TALENT:恋慕 = 0
				TALENT:淫乱 = 0
				TALENT:服従 = 0
		ENDSELECT
		GOTO ABL
	CASE 4
		PRINTL [1] - ランダムで命名する
		PRINTL [2] - 名前を入力する
		BINPUT
		SELECTCASE RESULT
			CASE 1
				NAME:TARGET = %ランダム命名(RAND(1, 4))%
				CALLNAME:TARGET = %NAME:TARGET%
				RESTART
			CASE 2
				PRINTL 名前を入力してください（何も入力しなかった場合は変更されません）
				INPUTS
				SIF RESULTS == ""
					RESTART
				NAME:TARGET = %RESULTS%
				CALLNAME:TARGET = %RESULTS%
				RESTART
		ENDSELECT
	CASE 8
		SELECTCASE TALENT:性別
			CASE 1
				TALENT:性別 = 2
			CASE 2
				TALENT:性別 = 1
				TALENT:バストサイズ = 0
		ENDSELECT
		RESTART
	CASE -9
		SIF MAXBASE:体力 <= 1500
			CONTINUE
		MAXBASE:体力 -= 300
		MAXBASE:気力 -= 300
		RESTART
	CASE 9
		MAXBASE:体力 += 300
		MAXBASE:気力 += 300
		RESTART
	CASE 10
		IF 製作コスト*コスト基準額 > MONEY+(リソース:マテリアル*コスト基準額) && !ORIGIN
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		IF ORIGIN
			PRINTL このロボットを作成します。よろしいですか？
		ELSE
			IF リソース:マテリアル >= 製作コスト
				PRINTFORML マテリアル{製作コスト}個を使用してこのロボットを作成します。よろしいですか？
			ELSEIF リソース:マテリアル
				CALL PASELIL, @"マテリアルを{リソース:マテリアル}個と{(製作コスト-リソース:マテリアル)*コスト基準額}＄を使用してこのロボットを作成します。よろしいですか？"
			ELSE
				CALL PASELIL, @"{製作コスト*コスト基準額}＄を使用してこのロボットを作成します。よろしいですか？"
			ENDIF
		ENDIF
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			RESTART
		IF リソース:マテリアル >= 製作コスト
			リソース:マテリアル -= 製作コスト
		ELSEIF リソース:マテリアル
			MONEY -= (製作コスト-リソース:マテリアル)*コスト基準額
			CALL 現金出納帳記録, @"ロボット製作/-{(製作コスト-リソース:マテリアル)*コスト基準額}"
			リソース:マテリアル = 0
		ELSE
			MONEY -= 製作コスト*コスト基準額
			CALL 現金出納帳記録, @"ロボット製作/-{製作コスト*コスト基準額}"
		ENDIF
		;子供と同じ扱い
		FLAG:子供の数++
		BUYY = YEAR
		BUYM = MONTH
		BUYD = DAYS
		CFLAG:誕生日 = MONTH*100+DAYS
		CFLAG:モチベーション = 50
		リソース:性感センサー -= 感覚合計
		リソース:交友インターフェイス -= 交友合計
		リソース:制御不能基板 -= 中毒合計
		TALENT:無機物 = 1
		CALL SETBASE, TARGET
		SELECTCASE FALLTYPE(TARGET)
			CASE 1
				リソース:感情再現デバイス（恋慕）--
			CASE 2
				リソース:感情再現デバイス（淫乱）--
			CASE 3
				リソース:感情再現デバイス（服従）--
		ENDSELECT
		IF 素質コスト > 10 && !CFLAG:複製
			;超過分だけ回して余分な素質が付く
			FOR LCOUNT, 0, 素質コスト-10
				SELECTCASE RAND:17
					CASE 0
						IF RAND:2
							TALENT:プライド高い = 1
							TALENT:プライド低い = 0
						ELSE
							TALENT:プライド高い = 0
							TALENT:プライド低い = 1
						ENDIF
					CASE 1
						TALENT:ツンデレ = 1
					CASE 2
						TALENT:衝動的 = 1
					CASE 3
						IF RAND:2
							TALENT:強気 = 1
							TALENT:弱気 = 0
						ELSE
							TALENT:強気 = 0
							TALENT:弱気 = 1
						ENDIF
					CASE 4
						TALENT:サボり魔 = 1
					CASE 5
						TALENT:保守的 = 1
					CASE 6
						IF RAND:2
							TALENT:楽観的 = 1
							TALENT:悲観的 = 0
						ELSE
							TALENT:楽観的 = 0
							TALENT:悲観的 = 1
						ENDIF
					CASE 7
						TALENT:一線越えない = 1
					CASE 8
						TALENT:目立ちたがり = 1
					CASE 9
						IF RAND:2
							TALENT:貞操観念 = 1
							TALENT:貞操無頓着 = 0
						ELSE
							TALENT:貞操観念 = 0
							TALENT:貞操無頓着 = 1
						ENDIF
					CASE 10
						IF RAND:2
							TALENT:恥じらい = 1
							TALENT:恥薄い = 0
						ELSE
							TALENT:恥じらい = 0
							TALENT:恥薄い = 1
						ENDIF
					CASE 11
						TALENT:寂しがり屋 = 1
					CASE 12
						TALENT:自慰しやすい = 1
					CASE 13
						TALENT:犬嫌い = 1
					CASE 14
						TALENT:猫嫌い = 1
					CASE 15
						SIF !TALENT:男好き
							TALENT:男嫌い = 1
					CASE 16
						SIF !TALENT:女好き
							TALENT:女好き = 1
				ENDSELECT
			NEXT
		ENDIF
		PRINTFORMW %NAME:TARGET%を制作しました
		IF CFLAG:複製
			IF 分析データ:(CFLAG:複製) == 1
				CALL プレイログ記録, @"%CSVNAMEF(CFLAG:複製)%のコピーを生成"
			ELSE
				CALL プレイログ記録, @"%CSVNAMEF(CFLAG:複製)%の{分析データ:(CFLAG:複製)}体目のコピーを生成"
			ENDIF
			分析データ:(CFLAG:複製)++
		ELSE
			CALL プレイログ記録, @"ロボット「%NAME:TARGET%」を生成"
		ENDIF
		RETURN 1
ENDSELECT

@ロボット改造, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 対象
#DIM DYNAMIC 必要マテリアル
#DIM DYNAMIC 素質変更可能, 1000
#DIM DYNAMIC 素質変更, 1000

LTARGET = TARGET
TARGET = 対象

$RESTART
DRAWLINE
PRINTFORML □%CALLNAME:TARGET%を改造します マテリアル所持数:{リソース:マテリアル}個
PRINT 　素質:
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別", "恋心", "既婚者"
			SIF TALENT:LCOUNT && TALENT:LCOUNT != 10000
				PRINTFORM [%TALENTNAME:LCOUNT%]
		CASE "バストサイズ"
			IF TALENT:LCOUNT
				IF !素質変更:LCOUNT || 素質変更:LCOUNT == TALENT:バストサイズ
					PRINTFORM [%バストサイズ(TALENT:バストサイズ)%]
				ELSE
					CALL PRINTCOLOR, @"[%バストサイズ(素質変更:LCOUNT)%]", "deepskyblue"
				ENDIF
			ENDIF
		CASEELSE
			IF TALENT:LCOUNT
				SIF 素質変更:LCOUNT
					SETCOLORBYNAME gray
				PRINTFORM [%TALENTNAME:LCOUNT%]
				RESETCOLOR
			ELSEIF 素質変更:LCOUNT
				CALL PRINTCOLOR, @"[%TALENTNAME:LCOUNT%]", "deepskyblue"
			ENDIF
	ENDSELECT
NEXT
PRINTL 
CALL PRINTCOLOR, "　青色", "deepskyblue"
PRINT は追加素質、
CALL PRINTCOLOR, "灰色", "gray"
PRINTL は除去素質

必要マテリアル = 0
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		;コスト1
		{
		CASE "生意気", "臆病", "素直", "反抗的", "ハイテンション", "大人しい", "世話好き",
			 "浪費癖", "無関心", "好奇心", "痛みに弱い", "習得遅い", "潔癖症", "汚れ無視",
			 "快感の否定", "倒錯的", "男好き", "女好き", "サド", "マゾ", "小悪魔", "遊び人",
			 "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "動物耳", "回復遅い", "鼓舞", "幼稚", 
		}
			素質変更可能:LCOUNT = 1
			SIF 素質変更:LCOUNT
				必要マテリアル += 1
		;コスト2
		{
		CASE "痛みに強い", "習得早い", "舌使い", "買い物上手", "献身的", "快感に素直", "魅力", "魅惑",
			 "統率", "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "ふたなり", "回復早い", "治療", "孔雀の瞳"
		}
			素質変更可能:LCOUNT = 2
			SIF 素質変更:LCOUNT
				必要マテリアル += 2
		;コスト3
		CASE "調合知識", "謎の魅力", "ネクロマンサー", "禁断の知識", "小柄体型"
			素質変更可能:LCOUNT = 3
			SIF 素質変更:LCOUNT
				必要マテリアル += 3
		CASE "バストサイズ"
			SIF TALENT:LCOUNT && 素質変更:LCOUNT
				必要マテリアル += ABS(TALENT:LCOUNT-素質変更:LCOUNT)
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAIN 　ロボット改造　
PRINTFORM [0] - 決定(必要マテリアル:{必要マテリアル}個) 
PRINT [1] - 戻る 
PRINTL [2] - 素質を変更する

BINPUT
SELECTCASE RESULT
	CASE 0
		SIF !必要マテリアル
			RETURN 0
		IF リソース:マテリアル < 必要マテリアル
			PRINTW マテリアルが足りません
			GOTO RESTART
		ENDIF
		PRINTFORML マテリアルを{必要マテリアル}個使用して%CALLNAME:TARGET%を改造します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			GOTO RESTART
		PRINTFORMW %CALLNAME:TARGET%に改造を施しました
		FOR LCOUNT, 0, VARSIZE("TALENT")
			IF 素質変更:LCOUNT
				SELECTCASE TALENTNAME:LCOUNT
					CASE "バストサイズ"
						TALENT:LCOUNT = 素質変更:LCOUNT
					CASEELSE
						INVERTBIT TALENT:LCOUNT, 0
				ENDSELECT
			ENDIF
		NEXT
		リソース:マテリアル -= 必要マテリアル
		TARGET = LTARGET
		RETURN 1
	CASE 1
		TARGET = LTARGET
		RETURN 0
	CASE 2
		VARLINE = 0
		$TALENT
		SIF VARLINE
			CLEARLINE LINECOUNT-VARLINE
		VARLINE = LINECOUNT
		DRAWLINE
		PRINTL □素質を選択してください 括弧内は変更コスト
		FOR LCOUNT, 0, VARSIZE("TALENT")
			IF 素質変更可能:LCOUNT
				IF 素質変更:LCOUNT
					IF TALENT:LCOUNT
						SETCOLORBYNAME gray
					ELSE
						SETCOLORBYNAME deepskyblue
					ENDIF
					PRINTFORMLC [{LCOUNT, 3}] %TALENTNAME:LCOUNT%({素質変更可能:LCOUNT}) \@ !TALENT:LCOUNT ? ○ # × \@
					RESETCOLOR
				ELSE
					PRINTFORMLC [{LCOUNT, 3}] %TALENTNAME:LCOUNT%({素質変更可能:LCOUNT}) \@ TALENT:LCOUNT ? ○ # × \@
				ENDIF
			ENDIF
		NEXT
		CALL NEWLINE
		IF TALENT:性別 == 2
			IF 素質変更:TALENTF("バストサイズ") && 素質変更:TALENTF("バストサイズ") != TALENT:バストサイズ
				CALL PRINTCOLORL, @"[{TALENTF("バストサイズ")}] バストサイズ:%バストサイズ(素質変更:TALENTF("バストサイズ"))%", "deepskyblue"
			ELSE
				PRINTFORML [{TALENTF("バストサイズ")}] バストサイズ:%バストサイズ(TALENT:バストサイズ)%
			ENDIF
		ENDIF
		DRAWLINE
		PRINTPLAIN 　ロボットファクトリー　
		PRINTL [-1] - 戻る
		BINPUT
		SIF RESULT == -1
			GOTO RESTART
		LRESULT = RESULT
		IF INRANGE(LRESULT, 0, VARSIZE("TALENT"))
			IF TALENTNAME:LRESULT == "バストサイズ" && TALENT:性別 == 2
				PRINTL [1] 絶壁にする
				PRINTL [2] 貧乳にする
				PRINTL [3] 並乳にする
				PRINTL [4] 巨乳にする
				PRINTL [5] 豊乳にする
				PRINTL [6] 爆乳にする
				BINPUT
				素質変更:LRESULT = RESULT
				GOTO TALENT
			ELSEIF 素質変更可能:LRESULT
				INVERTBIT 素質変更:LRESULT, 0
				GOTO TALENT
			ENDIF
		ENDIF
ENDSELECT

@STS_MACHINE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象
#DIM DYNAMIC 入手数

DRAWLINE

PRINT □解体するキャラを選んでください。
FONTBOLD
PRINTL 解体したキャラはロストします
FONTREGULAR

表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF !TALENT:LCOUNT:無機物
		CONTINUE
	SIF ゲームモード() == "経営モード" && CHARANAMEF(LCOUNT) == パートナー
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+20)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT% 回収できる物:
	IF TALENT:LCOUNT:小柄体型
		PRINT マテリアル3個 
	ELSE
		PRINT マテリアル5個 
	ENDIF

	入手数 = ABL:LCOUNT:Ｃ感覚+ABL:LCOUNT:Ｖ感覚+ABL:LCOUNT:Ａ感覚+ABL:LCOUNT:Ｂ感覚
	SIF 入手数
		PRINTFORM 性感センサー{入手数}個 

	SELECTCASE FALLTYPE(LCOUNT)
		CASE 1
			PRINT 感情再現デバイス（恋慕） 
		CASE 2
			PRINT 感情再現デバイス（淫乱） 
		CASE 3
			PRINT 感情再現デバイス（服従） 
	ENDSELECT

	入手数 = ABL:LCOUNT:従順+ABL:LCOUNT:欲望+ABL:LCOUNT:技巧+ABL:LCOUNT:奉仕精神
	SIF 入手数
		PRINTFORM 交友インターフェイス{入手数}個 

	入手数 = ABL:LCOUNT:自慰中毒+ABL:LCOUNT:精液中毒+ABL:LCOUNT:レズ中毒+ABL:LCOUNT:ゲイ中毒+ABL:LCOUNT:性交中毒+ABL:LCOUNT:噴乳中毒
	SIF 入手数
		PRINTFORM 制御不能基板{入手数}個 
	PRINTL 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　解体可能なキャラは居ません
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　解体.{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[-1] - 前ページ "
PRINT [0] - 戻る
CALL PRINTSPACE, 次ページあり, "[-9] - 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASE 1 TO CHARANUM
		対象 = RESULT
		SIF !TALENT:対象:無機物
			CONTINUE
		SIF ゲームモード() == "経営モード" && CHARANAMEF(対象) == パートナー
			CONTINUE
		PRINTFORML %NAME:対象%を解体します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			RESTART
		DRAWLINE
		RESETCOLOR
		PRINTFORML %NAME:対象%を解体しました
		CALL プレイログ記録, @"%NAME:対象%を解体"
		IF TALENT:対象:小柄体型
			PRINTL マテリアル+3
			リソース:マテリアル += 3
		ELSE
			PRINTL マテリアル+5
			リソース:マテリアル += 5
		ENDIF

		入手数 = ABL:対象:Ｃ感覚+ABL:対象:Ｖ感覚+ABL:対象:Ａ感覚+ABL:対象:Ｂ感覚
		IF 入手数
			PRINTFORML 性感センサー+{入手数}
			リソース:性感センサー += 入手数
		ENDIF

		SELECTCASE FALLTYPE(対象)
			CASE 1
				PRINTL 感情再現デバイス（恋慕）+1
				リソース:感情再現デバイス（恋慕）++
			CASE 2
				PRINTL 感情再現デバイス（淫乱）+1
				リソース:感情再現デバイス（淫乱）++
			CASE 3
				PRINTL 感情再現デバイス（服従）+1
				リソース:感情再現デバイス（服従）++
		ENDSELECT

		入手数 = ABL:対象:従順+ABL:対象:欲望+ABL:対象:技巧+ABL:対象:奉仕精神
		IF 入手数
			PRINTFORML 交友インターフェイス+{入手数}
			リソース:交友インターフェイス += 入手数
		ENDIF

		入手数 = ABL:対象:自慰中毒+ABL:対象:精液中毒+ABL:対象:レズ中毒+ABL:対象:ゲイ中毒+ABL:対象:性交中毒+ABL:対象:噴乳中毒
		IF 入手数
			PRINTFORML 制御不能基板+{入手数}
			リソース:制御不能基板 += 入手数
		ENDIF

		IF NOF(対象) < 10000 && !分析データ:NOF(対象)
			PRINTFORML %CALLNAME:対象%の構成データを解析できるようになりました
			分析データ:NOF(対象) = 1
		ENDIF
		WAIT
		CSTR:対象:ロスト要因 = 解体
		CSTR:対象:ロスト詳細 = 体中のパーツを分解された
		CALL CLEAR_CHARA, 対象, 1
		RETURN 1
ENDSELECT

@機械化
#DIM DYNAMIC 可否
#DIM DYNAMIC 対象

DRAWLINE

PRINTL □誰を機械化しますか？
FOR PAGING, 0, CHARANUM
	可否 = 1
	SIF PAGING == MASTER
		可否 = 0
	SIF TALENT:PAGING:無機物 || CSTR:PAGING:種族 == "機械"
		可否 = 0
	SIF TALENT:PAGING:妊娠
		可否 = 0
	CALL PAGING, 可否
NEXT

SIF RESULT == -1
	RETURN
対象 = RESULT

PRINTFORML %NAME:対象%の人格を機械の体に移植します。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

CALL INPUTF, 0, 1

SIF RESULT == 1
	RETURN

;一度幽体離脱処理を行う
CALL 幽体離脱, -1, 対象

;対象は幽体離脱でCHARANUM-1になる
対象 = CHARANUM-1

;機械にする
TALENT:対象:無機物 = 1
CSTR:対象:種族 = 機械

;経験が無ければ童貞、処女にする
SELECTCASE SEX(対象)
	CASE 1
		SIF CSTR:対象:童貞 == ""
			TALENT:対象:童貞 = 1
	CASE 2
		SIF CSTR:対象:処女 == ""
			TALENT:対象:処女 = 1
ENDSELECT

PRINTFORMW %CALLNAME:対象%の人格を機械の体に移植し、身体は依代として保管しました
MONEY -= 100000
CALL 現金出納帳記録, @"%NAME:対象%の機械化/-100000"

RETURN 1
