@人体錬成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIM 性別設定 = 1
#DIM 体型設定 = 3
#DIM 小人体型
#DIM スライム設定
#DIM DYNAMIC 魔法技能記録用
#DIM DYNAMIC オリジナル
DRAWLINE

PRINTL □保管した血液から新しいキャラを作ることができます
PRINTFORM 　保管済み血液:{リソース:血液}ml 保管済み粘液:{リソース:粘液}ml 
FOR LCOUNT, GETNUM(リソース, "ケモミミが付いた頭"), GETNUM(リソース, "ケモミミが付いた頭")+50
	SIF リソース:LCOUNT
		PRINTFORM %ERDNAME(リソース, LCOUNT)%×{リソース:LCOUNT} 
NEXT

PRINTL 
PRINTL □人体錬成の条件を選択してください
PRINTFORML 　[1] - 性別を変更する(\@ 性別設定 == 1 ? 男性 # 女性 \@)
PRINTFORML 　[2] - 体格を変更する(\@小人体型 ? 小人 # %体格(体型設定)%\@:{人体錬成_消費血液量(体型設定, 小人体型)}mlの血液が必要)

PRINTFORML 　[3] - スライムの粘液(\@ スライム設定 == 1 ? 混ぜる:500ml必要 # 混ぜない \@)
PRINTL 　[4] - 上記の条件でシミュレートする
PRINTL 　[5] - DNAデータからクローンを作る
PRINTL 　[6] - 霊体に依代を用意する
DRAWLINE
PRINTPLAIN 　人体錬成　
PRINTL 　[0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		IF 性別設定 == 1
			性別設定 = 2
		ELSE
			性別設定 = 1
		ENDIF
		RESTART
	CASE 2
		PRINTL 体格を選んでください
		PRINTL [1] - 幼児
		PRINTL [2] - 小柄
		PRINTL [3] - 普通
		PRINTL [4] - 長身
		BINPUT
		体型設定 = RESULT
		RESTART
	CASE 3
		INVERTBIT スライム設定, 0
		RESTART
	CASE 4
		$REROLL
		CALL ADD_ランダムキャラ, 性別設定, 体型設定, 1
		対象 = CHARANUM-1
	CASE 5
		CALL 所持DNA
		SIF RESULT == 0
			RESTART
		オリジナル = RESULT
		CALL 特殊クローン作成, オリジナル, "医務室"
		SIF RESULT
			RETURN 1
		CALL CHARA_ADD, オリジナル, 1
		対象 = CHARANUM-1
		CFLAG:対象:複製 = オリジナル
		CFLAG:対象:クローン番号 = DNAデータ:オリジナル
		魔法技能記録用 = ABL:対象:魔法技能
		;EXPとABLを初期化するが魔法技能だけ例外
		VARSET EXP:対象:0
		VARSET ABL:対象:0
		ABL:対象:魔法技能 = 魔法技能記録用
		;相性や関係も初期化する
		VARSET RELATION:対象:0, 100
		CALL 初期経験リセット, 対象
		SIF PENIS(対象)
			TALENT:対象:童貞 = 1
		SIF VAGINA(対象)
			TALENT:対象:処女 = 1
		TALENT:対象:恋心 = 0
		TALENT:対象:既婚者 = 0
		CSTR:対象:恋心 = 
		CSTR:対象:既婚者 = 
		CSTR:対象:種族嫌悪 = 
		TALENT:対象:犬嫌い = 0
		TALENT:対象:猫嫌い = 0
		;ゾンビと霊体は生前の姿に戻す
		SIF CSTR:対象:種族 == "幽霊"
			CSTR:対象:種族 = 人間
		SIF CSTR:対象:種族 == "ゾンビ"
			CSTR:対象:種族 = 人間
		TALENT:対象:霊体 = 0
		TALENT:対象:ゾンビ = 0
	CASE 6
		CALL ANIMATE_BODY
		SIF RESULT
			RETURN 1
		RESTART
ENDSELECT

IF リソース:血液 < 人体錬成_消費血液量(TALENT:対象:体格, TALENT:対象:小人体型)
	PRINTW 材料の血液が足りません
	CALL CLEAR_CHARA, 対象, 1
	RESTART
ENDIF

IF スライム設定
	IF リソース:粘液 < 500
		PRINTW 材料の粘液が足りません
		CALL CLEAR_CHARA, 対象, 1
		RESTART
	ENDIF
	TALENT:対象:スライム = 1
	CSTR:対象:種族 = スライム
ENDIF

;種族をセーブしておく
TRANCERACE:対象:0 = %CSTR:対象:種族%

CFLAG:対象:人工物 = 1
VARSET HIDE_TALENT
IF CFLAG:対象:複製
	NAME:対象 = %NAME:対象%(クローン\@ DNAデータ:(CFLAG:対象:複製) >= 2 ? {DNAデータ:(CFLAG:対象:複製)} #\@)
	CALLNAME:対象 = %CALLNAME:対象%(クローン\@ DNAデータ:(CFLAG:対象:複製) >= 2 ? {DNAデータ:(CFLAG:対象:複製)} #\@)
ENDIF

CALL 人体錬成_調整, 対象

;再生成
SIF RESULT == 1
	GOTO REROLL
;生成した=ターン終了
SIF RESULT == 2
	RETURN 1

RESTART


;人間とロボの組み合わせの択一キャラが追加された場合は処理の見直しが必要
@特殊クローン作成, オリジナル, 配属先
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC オリジナル
#DIM DYNAMIC 作成キャラ
#DIMS DYNAMIC 配属先
#DIM DYNAMIC 配属魔法技能合計
#DIMS DYNAMIC 択一キャラリスト, 10
#DIM DYNAMIC 択一キャラ数
#DIM DYNAMIC 作成可能リスト, 10
#DIM DYNAMIC 作成可能数
#DIMS DYNAMIC プレイログテキスト

;クローン元が手元に存在する、クローン元がクローンではない、対象が手元に存在しない、主人＋配属キャラの魔法技能合計10以上
;以上の条件で択一キャラをクローン生成可能
SIF 択一キャラ(CSVNAME(オリジナル)) == ""
	RETURN
SIF GETCHARANAME(CSVNAME(オリジナル)) == -1
	RETURN
SIF CFLAG:GETCHARANAME(CSVNAME(オリジナル)):クローン番号
	RETURN
SPLIT 択一キャラ(CSVNAME(オリジナル)), "/", 択一キャラリスト
択一キャラ数 = RESULT
FOR LCOUNT, 0, 択一キャラ数
	SELECTCASE 択一キャラリスト:LCOUNT
		;ニア・バテシバは二人揃っているかつDNAデータがニア側の必要がある
		CASE "ニア・バテシバ"
			SIF GETCHARANAME("ニア・ユーディット") == -1 || GETCHARANAME("バテシバ・アヒトフェル") == -1
				RETURN
			SIF オリジナル != GETNO("ニア・ユーディット")
				RETURN
	ENDSELECT
	SIF GETCHARANAME(択一キャラリスト:LCOUNT) == -1
		作成可能リスト:(作成可能数++) = GETNO(択一キャラリスト:LCOUNT)
NEXT
SIF !作成可能数
	RETURN

FOR LCOUNT, 0, CHARANUM
	SIF LCOUNT == MASTER || (CSTR:LCOUNT:配属 == 配属先 && CFLAG:LCOUNT:配属完了)
		配属魔法技能合計 += ABL:LCOUNT:魔法技能
NEXT
SIF 配属魔法技能合計 < 10
	RETURN

SELECTCASE CSVNAME(オリジナル)
	CASE "ニア・ユーディット"
		PRINTFORM ニア・ユーディットのDNAとバテシバ・アヒトフェルの魂
	CASEELSE
		PRINTFORM %CSVNAME(オリジナル)%の\@ 配属先 == "実験室" ? 分析データ # DNA \@と魂
ENDSELECT
PRINTFORML を元に特殊なクローンを作成しますか？
PRINTFORML [0] はい
PRINTFORML [1] いいえ
BINPUT
SIF RESULT
	RETURN
FOR LCOUNT, 0, 作成可能数
	PRINTFORML [{作成可能リスト:LCOUNT, 4}] %CSVNAME(作成可能リスト:LCOUNT)%
NEXT
PRINTFORML [0] やっぱりやめる
BINPUT
SIF !RESULT
	RETURN
作成キャラ = RESULT

CALL CHARA_ADD, 作成キャラ
;ここからNOを入れていた変数を使い回す（危険）
オリジナル = GETCHARA(オリジナル)
作成キャラ = CHARANUM-1
;経験の削除等は行わない

;オリジナルが存在する事を先に判定してるからCCBへの影響はないはず
IF 配属先 == "実験室"
	IF MONEY < 100000
		PRINTW 所持金が足りません
		CALL CLEAR_CHARA, 作成キャラ, 1
		RETURN
	ENDIF
ELSE
	IF リソース:血液 < 人体錬成_消費血液量(TALENT:作成キャラ:体格, TALENT:作成キャラ:小人体型)
		PRINTW 材料の血液が足りません
		CALL CLEAR_CHARA, 作成キャラ, 1
		RETURN
	ENDIF
	IF !ISSLIME(作成キャラ)
		IF リソース:粘液 < 500
			PRINTW 材料の粘液が足りません
			CALL CLEAR_CHARA, 作成キャラ, 1
			RETURN
		ENDIF
	ENDIF
ENDIF

SELECTCASE NAME:作成キャラ
	CASE "ニア・バテシバ"
		PRINTFORM ニア・ユーディットのDNAから生成した肉体をベースにバテシバ・アヒトフェルの魂を分け、
	CASEELSE
		PRINTFORM %NAME:オリジナル%の魂を分け、
ENDSELECT
PRINTFORML %NAME:作成キャラ%を作成しました
;CSVありキャラに複製フラグつけるのが不都合だったら対処
CFLAG:作成キャラ:複製 = NOF(オリジナル)
IF 配属先 == "実験室"
	;ロボットファクトリーから来る場合のコストがまだ適当
	CFLAG:作成キャラ:クローン番号 = 分析データ:NOF(オリジナル)
	分析データ:NOF(オリジナル)++
	CALL PASELIL, @"作成費用として100000＄消費しました"
	CALL PAYMENT, -100000, "ロボット制作"
ELSE
	CFLAG:作成キャラ:クローン番号 = DNAデータ:NOF(オリジナル)
	DNAデータ:NOF(オリジナル)++
	PRINTFORML 保管済み血液を{人体錬成_消費血液量(TALENT:作成キャラ:体格, TALENT:作成キャラ:小人体型)}ml使用しました
	リソース:血液 -= 人体錬成_消費血液量(TALENT:作成キャラ:体格, TALENT:作成キャラ:小人体型)
	IF ISSLIME(TARGET)
		PRINTL 保管済み粘液を500ml使用しました
		リソース:粘液 -= 500
	ENDIF
ENDIF
CFLAG:作成キャラ:人工物 = 1

;オリジナルからそのまま分離してそうなキャラは後天的に得た能力の一部をコピーしてみる（肉体由来のものはコピーしない）
;陥落状態もコピーされる（意図的）
IF GROUPMATCH(CHARANAMEF(オリジナル), "DJつよし", "OJつよし")
	FOR LCOUNT, 0, VARSIZE("MARK")
		MARK:作成キャラ:LCOUNT = MARK:オリジナル:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("ABL")
		IF !BODYSTUFF("ABL", ABLNAME:LCOUNT) && ABL:オリジナル:LCOUNT > CSVABL(NOF(オリジナル), LCOUNT)
			;栽培技能だけ最低保証が1あるため特殊
			IF ABLNAME:LCOUNT == "栽培技能" && CSVABL(NOF(オリジナル), LCOUNT) == 0
				SIF ABL:オリジナル:LCOUNT-(CSVABL(NOF(オリジナル), LCOUNT)+1) >= 1
					ABL:作成キャラ:LCOUNT += ABL:オリジナル:LCOUNT-(CSVABL(NOF(オリジナル), LCOUNT)+1)
			ELSE
				ABL:作成キャラ:LCOUNT += ABL:オリジナル:LCOUNT-CSVABL(NOF(オリジナル), LCOUNT)
			ENDIF
		ENDIF
		SIF !TALENT:作成キャラ:リミットバースト && ABL:作成キャラ:LCOUNT > 10
			ABL:作成キャラ:LCOUNT = 10
	NEXT
	FOR LCOUNT, 0, VARSIZE("EXP")
		SIF !BODYSTUFF("EXP", EXPNAME:LCOUNT) && EXP:オリジナル:LCOUNT > CSVEXP(NOF(オリジナル), LCOUNT)
			EXP:作成キャラ:LCOUNT += EXP:オリジナル:LCOUNT-CSVEXP(NOF(オリジナル), LCOUNT)
	NEXT
	FOR LCOUNT, 0, VARSIZE("JUEL")
		SIF !BODYSTUFF("JUEL", PALAMNAME:LCOUNT)
			JUEL:作成キャラ:LCOUNT += JUEL:オリジナル:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF !BODYSTUFF("TALENT", TALENTNAME:LCOUNT) && TALENT:オリジナル:LCOUNT != CSVTALENT(NOF(オリジナル), LCOUNT)
			TALENT:作成キャラ:LCOUNT = TALENT:オリジナル:LCOUNT
	NEXT
	;一部CFLAGを手動編集
	CFLAG:作成キャラ:依存度 = CFLAG:オリジナル:依存度
	CFLAG:オリジナル:ストレス値 = CFLAG:オリジナル:ストレス値/3*2
	CFLAG:作成キャラ:ストレス値 = CFLAG:オリジナル:ストレス値
	CFLAG:オリジナル:狂気度 = CFLAG:オリジナル:狂気度/3*2
	CFLAG:作成キャラ:狂気度 = CFLAG:オリジナル:狂気度
	CFLAG:オリジナル:出産トラウマ = CFLAG:オリジナル:出産トラウマ/3*2
	CFLAG:作成キャラ:出産トラウマ = CFLAG:オリジナル:出産トラウマ
	CFLAG:作成キャラ:本への興味 = CFLAG:オリジナル:本への興味
	;主人への相性も一旦手動
	RELATION:作成キャラ:NOF(MASTER) = RELATION:オリジナル:NOF(MASTER)
ENDIF

;口上表示→反発イベントDISABLEの順（イベントの有無に関わらず口上は出る）
CALL KOJO_MESSAGE_EVENT, "特殊クローン生成", オリジナル
SIF NAME:作成キャラ == "ニア・バテシバ"
	CALL KOJO_MESSAGE_EVENT, "特殊クローン生成", GETCHARANAME("バテシバ・アヒトフェル")
CALL KOJO_MESSAGE_EVENT, "特殊クローンとして誕生", 作成キャラ
;反発イベントはちょっと様子見
[SKIPSTART]
CALLFORM DISABLE, "EVENT_特殊クローン生成反発", オリジナル
IF !TALENT:オリジナル:隷属 && !TALENT:オリジナル:崩壊 && !RESULT
	CALL JUELUP, "否定", 10000, オリジナル
	IF MARK:オリジナル:反発刻印 < 3
		PRINTFORML オリジナルの%NAME:オリジナル%の反発刻印がLv3になった
		MARK:オリジナル:反発刻印 = 3
	ENDIF
ENDIF
[SKIPEND]

プレイログテキスト = %NAME:オリジナル%の特別な
IF 配属先 == "実験室"
	SIF DNAデータ:NOF(オリジナル) > 1
		プレイログテキスト += @"{分析データ:NOF(オリジナル)}体目の"
	プレイログテキスト += "コピーとして%NAME:作成キャラ%を作成"
ELSE
	SIF DNAデータ:NOF(オリジナル) > 1
		プレイログテキスト += @"{DNAデータ:NOF(オリジナル)}体目の"
	プレイログテキスト += "クローンとして%NAME:作成キャラ%を錬成"
ENDIF
CALL プレイログ記録, プレイログテキスト

SIF GETCHARANAME("DJつよし") > 0 && GETCHARANAME("OJつよし") > 0
	CALL 実績解除, "二人二役"

RETURN 1



@人体錬成_調整, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 服装選出用
#DIM DYNAMIC 服装変更フラグ
#DIM DYNAMIC 選択中リソース, 150

CALL ADD_ランダムキャラ_STATUS, 対象, 1

DRAWLINE
IF !CFLAG:対象:複製
	FOR LCOUNT, GETNUM(リソース, "ケモミミが付いた頭"), GETNUM(リソース, "ケモミミが付いた頭")+50
		IF リソース:LCOUNT
			SIF GETBIT(選択中リソース:LCOUNT, 0)
				SETCOLORBYNAME deepskyblue
			PRINTFORMLC 　[{LCOUNT}]%ERDNAME(リソース, LCOUNT)%×{(リソース:LCOUNT)-GETBIT(選択中リソース:LCOUNT, 0)} 
			RESETCOLOR
		ENDIF
	NEXT
ENDIF
CALL NEWLINE

CALL CHECKDRAWLINE
PRINTPLAIN 　人体錬成　
PRINT [0] - 戻る 
PRINT [1] - このキャラを錬成する 
PRINT [2] - 名前を変更する 
SIF !CFLAG:対象:複製
	PRINT [3] - シミュレートし直す 
IF CFLAG:対象:複製
	PRINT [4] - 性別を変更する 
	PRINT [5] - 体型を変更する 
	SIF SEX(対象) == 2
		PRINT [6] - バストサイズを変更する
ENDIF
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		CALL CLEAR_CHARA, 対象, 1
		RETURN 0
	CASE 1
		TARGET = 対象
		PRINTFORML %NAME:TARGET%を錬成しました
		;子供と同じ扱い
		FLAG:子供の数++
		NO:対象 = FLAG:子供の数+10000
		CFLAG:対象:CSV無し = 1
		CHILDNAME:(FLAG:子供の数) = %NAME:対象%
		CFLAG:誕生日 = MONTH*100+DAYS
		PRINTFORML 保管済み血液を{人体錬成_消費血液量(TALENT:体格, TALENT:小人体型)}ml使用しました
		リソース:血液 -= 人体錬成_消費血液量(TALENT:体格, TALENT:小人体型)
		IF ISSLIME(TARGET)
			PRINTL 保管済み粘液を500ml使用しました
			リソース:粘液 -= 500
		ENDIF
		IF !CFLAG:対象:複製
			IF TALENT:動物耳
				PRINTL ケモミミが付いた頭を付けました
				リソース:ケモミミが付いた頭--
			ENDIF
			IF TALENT:羽
				PRINTL 翼の生えた胴体を付けました
				リソース:翼の生えた胴体--
			ENDIF
			IF TALENT:尻尾
				PRINTL 切断された尻尾を使用しました
				リソース:切断された尻尾--
			ENDIF
			IF TALENT:足使用不可
				PRINTL 人魚の下半身を付けました
				リソース:人魚の下半身--
			ENDIF
			SELECTCASE CSTR:種族
				CASE "悪魔"
					PRINTL 悪魔の心臓を植え付けました
					リソース:悪魔の心臓--
				CASE "天使"
					PRINTL 天使の心臓を植え付けました
					リソース:天使の心臓--
				CASE "神"
					PRINTL 神の心臓を植え付けました
					リソース:神の心臓--
				CASE "魔術師"
					PRINTL 魔術師の心臓を植え付けました
					リソース:魔術師の心臓--
					ABL:魔法技能 = 3
					MAXBASE:魔力 = 3000
					BASE:魔力 = 3000
			ENDSELECT
			TRANCERACE:TARGET:0 = 
			CALL プレイログ記録, @"キャラクター「%NAME:TARGET%」を錬成"
		ELSE
			IF GETCHARA(CFLAG:対象:複製) > 0 && !CFLAG:GETCHARA(CFLAG:対象:複製):キャラロスト
				;口上表示→反発イベントDISABLEの順（イベントの有無に関わらず口上は出る）
				CALL KOJO_MESSAGE_EVENT, "クローン生成", GETCHARA(CFLAG:対象:複製)
				服装変更フラグ = RESULT
				CALLFORM DISABLE, "EVENT_クローン生成反発", GETCHARA(CFLAG:対象:複製)
				IF !TALENT:GETCHARA(CFLAG:対象:複製):隷属 && !TALENT:GETCHARA(CFLAG:対象:複製):崩壊 && !RESULT
					CALL JUELUP, "否定", 10000, GETCHARA(CFLAG:対象:複製)
					IF MARK:GETCHARA(CFLAG:対象:複製):反発刻印 < 3
						PRINTFORML オリジナルの%NAME:GETCHARA(CFLAG:対象:複製)%の反発刻印がLv3になった
						MARK:GETCHARA(CFLAG:対象:複製):反発刻印 = 3
					ENDIF
				ENDIF
			ENDIF
			IF DNAデータ:(CFLAG:対象:複製) == 1
				CALL プレイログ記録, @"%CSVNAME(CFLAG:対象:複製)%のクローンを錬成"
			ELSE
				CALL プレイログ記録, @"%CSVNAME(CFLAG:対象:複製)%の{DNAデータ:(CFLAG:対象:複製)}体目のクローンを錬成"
			ENDIF
			DNAデータ:(CFLAG:対象:複製)++
		ENDIF
		IF !服装変更フラグ
			PRINTL 服を着せますか？
			PRINTL [1] - 何も着せない
			PRINTL [2] - 適当に何か着せる
			SIF CFLAG:対象:複製
				PRINTL [3] - オリジナルと同じ服を着せる
			BINPUT
			服装変更フラグ = RESULT
		ENDIF
		SELECTCASE 服装変更フラグ
			CASE 1
				FOR LCOUNT, 100, 107
					CSTR:LCOUNT =
				NEXT
				CFLAG:挿入可 = 0
				CFLAG:着衣併用 = 0
				CFLAG:上着パイズリ = 0
				CFLAG:下着パイズリ = 0
				CFLAG:全身上着パイズリ = 0
			CASE 2
				服装選出用 = RAND(1, 10000)
				WHILE !EXISTCSV(服装選出用) || CSVTALENT(服装選出用, GETNUM(TALENT, "性別")) != SEX(対象)
					服装選出用 += RAND:10+1
					SIF 服装選出用 >= 10000
						服装選出用 = 1
				WEND
				FOR LCOUNT, 100, 107
					CSTR:LCOUNT = %CSVCSTR(服装選出用, LCOUNT)%
				NEXT
				CFLAG:挿入可 = CSVCFLAG(服装選出用, GETNUM(CFLAG, "挿入可"))
				CFLAG:着衣併用 = CSVCFLAG(服装選出用, GETNUM(CFLAG, "着衣併用"))
				CFLAG:上着パイズリ = CSVCFLAG(服装選出用, GETNUM(CFLAG, "上着パイズリ"))
				CFLAG:下着パイズリ = CSVCFLAG(服装選出用, GETNUM(CFLAG, "下着パイズリ"))
				CFLAG:全身上着パイズリ = CSVCFLAG(服装選出用, GETNUM(CFLAG, "全身上着パイズリ"))
		ENDSELECT
		RETURN 2
	CASE 2
		PRINTL [1] - ランダムで命名する
		PRINTL [2] - 名前を入力する
		SIF CFLAG:複製
			PRINTL [3] - オリジナルと同じ名前にする
		BINPUT
		SELECTCASE RESULT
			CASE 1
				NAME:対象 = %ランダム命名(RAND(1, 4))%
				CALLNAME:対象 = %NAME:対象%
			CASE 2
				PRINTL 名前を入力してください（何も入力しなかった場合は変更されません）
				INPUTS
				SIF RESULTS == ""
					RESTART
				NAME:対象 = %RESULTS%
				CALLNAME:対象 = %RESULTS%
			CASE 3
				NAME:対象 = %CSVNAME(CFLAG:対象:複製)%(クローン\@ DNAデータ:(CFLAG:対象:複製) >= 2 ? {DNAデータ:(CFLAG:対象:複製)} #\@)
				CALLNAME:対象 = %CSVCALLNAME(CFLAG:対象:複製)%(クローン\@ DNAデータ:(CFLAG:対象:複製) >= 2 ? {DNAデータ:(CFLAG:対象:複製)} #\@)
		ENDSELECT
	CASE 3
		CALL CLEAR_CHARA, 対象, 1
		RETURN 1
	CASE 4
		SELECTCASE TALENT:対象:性別
			CASE 1
				TALENT:対象:性別 = 2
				TALENT:対象:バストサイズ = TALENT:対象:体格
			CASE 2
				TALENT:対象:性別 = 1
				TALENT:対象:バストサイズ = 0
		ENDSELECT
		TALENT:対象:童貞 = PENIS(対象)
		TALENT:対象:処女 = VAGINA(対象)
	CASE 5
		PRINTL 体格を選んでください
		PRINTL [1] - 幼児体型
		PRINTL [2] - 小柄体型
		PRINTL [3] - 普通体型
		PRINTL [4] - 長身体型
		PRINTL [5] - 小人体型
		BINPUT
		SELECTCASE RESULT
			CASE 1 TO 4
				TALENT:対象:体格 = RESULT
				TALENT:対象:小人体型 = 0
			CASE 5
				TALENT:対象:体格 = 3
				TALENT:対象:小人体型 = 1
		ENDSELECT
	CASE 6
		PRINTL バストサイズを選んでください
		PRINTL [1] - 絶壁
		PRINTL [2] - 貧乳
		PRINTL [3] - 並乳
		PRINTL [4] - 巨乳
		PRINTL [5] - 豊乳
		PRINTL [6] - 爆乳
		BINPUT
		TALENT:対象:バストサイズ = RESULT
	CASEELSE
		LRESULT = RESULT
		IF INRANGE(LRESULT, リソース, VARSIZE("リソース")) && リソース:LRESULT
			SELECTCASE ERDNAME(リソース, LRESULT)
				CASE "ケモミミが付いた頭"
					INVERTBIT TALENT:対象:動物耳, 0
					INVERTBIT 選択中リソース:LRESULT, 0
				CASE "翼の生えた胴体"
					INVERTBIT TALENT:対象:羽, 0
					INVERTBIT 選択中リソース:LRESULT, 0
				CASE "人魚の下半身"
					INVERTBIT TALENT:対象:足使用不可, 0
					INVERTBIT 選択中リソース:LRESULT, 0
				CASE "切断された尻尾"
					INVERTBIT TALENT:対象:尻尾, 0
					INVERTBIT 選択中リソース:LRESULT, 0
				CASE "悪魔の心臓"
					IF CSTR:対象:種族 == "悪魔"
						CSTR:対象:種族 = %TRANCERACE:対象:0%
						INVERTBIT 選択中リソース:LRESULT, 0
					ELSE
						CSTR:対象:種族 = 悪魔
						INVERTBIT 選択中リソース:LRESULT, 0
						;とりあえずの処置
						CLEARBIT 選択中リソース:GETNUM(リソース, "天使の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "神の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "魔術師の心臓"), 0
					ENDIF
				CASE "天使の心臓"
					IF CSTR:対象:種族 == "天使"
						CSTR:対象:種族 = %TRANCERACE:対象:0%
						INVERTBIT 選択中リソース:LRESULT, 0
					ELSE
						CSTR:対象:種族 = 天使
						INVERTBIT 選択中リソース:LRESULT, 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "悪魔の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "神の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "魔術師の心臓"), 0
					ENDIF
				CASE "神の心臓"
					IF CSTR:対象:種族 == "神"
						CSTR:対象:種族 = %TRANCERACE:対象:0%
						INVERTBIT 選択中リソース:LRESULT, 0
					ELSE
						CSTR:対象:種族 = 神
						INVERTBIT 選択中リソース:LRESULT, 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "悪魔の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "天使の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "魔術師の心臓"), 0
					ENDIF
				CASE "魔術師の心臓"
					IF CSTR:対象:種族 == "魔術師"
						CSTR:対象:種族 = %TRANCERACE:対象:0%
						INVERTBIT 選択中リソース:LRESULT, 0
					ELSE
						CSTR:対象:種族 = 魔術師
						INVERTBIT 選択中リソース:LRESULT, 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "悪魔の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "天使の心臓"), 0
						CLEARBIT 選択中リソース:GETNUM(リソース, "神の心臓"), 0
					ENDIF
			ENDSELECT
		ENDIF
ENDSELECT

RESTART

@所持DNA, ロボット
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ロボット

DRAWLINE
PRINTFORML □誰の\@ ロボット ? データ # DNA \@を使用しますか？
表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, 10000
	IF (!ロボット && DNAデータ:LCOUNT) || (ロボット && 分析データ:LCOUNT)
		SIF !EXISTCSV(LCOUNT)
			CONTINUE
		CALL DISABLE, "EVENT_クローン生成", LCOUNT
		SIF RESULT
			CONTINUE
		表示数++
		IF 表示数 > ページ数*20+20
			次ページあり = 1
			BREAK
		ENDIF
		SIF 表示数 > ページ数*20
			PRINTFORML 　[{LCOUNT, 5}]- %CSVNAME(LCOUNT)%
	ENDIF
NEXT
DRAWLINE
PRINTPLAINFORM 　\@ ロボット ? データ # DNA \@.{ページ数+1}　
CALL PRINTSPACE, ページ数, "[-1]  - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, 次ページあり, "[-9]  - 次ページ"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASEELSE
		RETURN RESULT
ENDSELECT

RESTART

@ANIMATE_BODY
#DIM DYNAMIC 対象

PRINTL □霊体に生身の身体を用意します。対象を選んでください
PRINTFORML 　保管済み血液:{リソース:血液}ml 小人体型は1000ml、幼児体型は3000ml、小柄体型は4000ml、普通体型は5000ml、長身体型は6000ml必要

FOR PAGING, 0, CHARANUM
	CALL PAGING, TALENT:PAGING:霊体, @"%体格(TALENT:PAGING:体格)%体型"
NEXT

SIF RESULT == -1
	RETURN 0

対象 = RESULT

IF リソース:血液 < 人体錬成_消費血液量(TALENT:対象:体格, TALENT:対象:小人体型)
	PRINTFORMW 血液が{人体錬成_消費血液量(TALENT:対象:体格, TALENT:対象:小人体型)}ml必要です。材料の血液が足りません
	RESTART
ENDIF

PRINTFORML 血液を{人体錬成_消費血液量(TALENT:対象:体格, TALENT:対象:小人体型)}ml使用して%NAME:対象%の身体を用意します。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT
SIF RESULT == 1
	RESTART

リソース:血液 -= 人体錬成_消費血液量(TALENT:対象:体格, TALENT:対象:小人体型)

PRINTFORMW 身体を作り、%NAME:対象%を宿しました
TARGET = 対象
CSTR:種族 = 人間
TALENT:霊体 = 0

RETURN 1


@人体錬成_消費血液量, 体格値 = 3, 小人体型フラグ
#FUNCTION
#DIM DYNAMIC 消費量
#DIM DYNAMIC 体格値
#DIM DYNAMIC 小人体型フラグ

IF 小人体型フラグ
	消費量 = 1000
ELSE
	消費量 = (体格値+2)*1000
ENDIF

RETURNF 消費量

@DNAデータ解析, 対象
#DIM DYNAMIC 対象
IF NOF(対象) < 10000 && !DNAデータ:(NOF(対象))
	PRINTFORMW %CALLNAME:対象%のDNAデータを解析できるようになりました
	DNAデータ:(NOF(対象)) = 1
ENDIF
