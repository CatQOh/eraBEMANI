@図書室
#DIM DYNAMIC 補充費
#DIM DYNAMIC 割引率
#DIM DYNAMIC 司書魅力
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 名前文字数

CALL TUTORIAL, "施設_2"

;本の補充費と司書レベルを計算
補充費 = 10000
割引率 = 0
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "図書室" && CFLAG:LCOUNT:配属完了
		;司書の実務経験に応じて減額 一人につき最大1000パセリ
		補充費 -= LIMIT(EXP:LCOUNT:実務経験/10*10, 0, 1000)
		SIF TALENT:LCOUNT:買い物上手
			割引率++
		SIF TALENT:LCOUNT:読書家
			割引率++
	ENDIF
NEXT
;買い物上手と読書家一人につき補充費10％減 効果は重複する
補充費 -= 補充費/10*割引率

;浪費癖一人につき補充費10%増
補充費 += 補充費/10*浪費癖人数("図書室")

;最後に*CHARANUM
補充費 *= CHARANUM

DRAWLINE
PRINTL □図書室
PRINTL 　知識の泉を充実させることで、利用者が様々な知識を得られるようになります
PRINTL 　司書を配属することで効率的に新書を補充したり、珍しい奇書を手に入れることができるかもしれません
PRINTFORM 　図書室のランク:{図書室ランク()}/30 
SIF リソース:本の最終補充日
	PRINTFORM 本の最終補充日:{リソース:本の最終補充日/10000}年{リソース:本の最終補充日/100%100}月{リソース:本の最終補充日%100}日
SIF 本の補充経過日数
	PRINTFORM ({本の補充経過日数}日前)
PRINTL 
PRINTFORML □担当司書:{CMATCH(CSTR:配属, "図書室")}/3
IF CMATCH(CSTR:配属, "図書室")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "図書室"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "図書室"
			PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 
			IF !CFLAG:LCOUNT:配属完了
				SETCOLORBYNAME gray
				PRINT 【配属準備中】
			ELSE
				SIF 統率者("図書室") && !TALENT:LCOUNT:統率
					PRINT  統率者ボーナス+1 
			ENDIF
			CALL ISTALENT, LCOUNT, "買い物上手"
			CALL ISTALENT, LCOUNT, "潔癖症"
			CALL ISTALENT, LCOUNT, "禁断の知識"
			CALL ISTALENT, LCOUNT, "読書家"
			IF TALENT:LCOUNT:謎の魅力
				PRINT [謎の魅力]
			ELSEIF TALENT:LCOUNT:魅力
				PRINT [魅力]
			ENDIF
			CALL ISTALENT, LCOUNT, "統率"
			PRINTL 
			RESETCOLOR
		ENDIF
	NEXT
ELSE
	CALL PRINTCOLORL, "　[無し]", "gray"
ENDIF
PRINTL 
CALL PASELIL, @"　[1] - 本を補充する\@ !ORIGIN ? （{補充費}＄必要 所持金:{MONEY}＄） #\@"
IF CMATCH(CSTR:配属, "図書室")
	PRINTL 　[2] - 図書室ランクのボーナスを確認する
ELSE
	HTML_PRINT SET_TOOLTIP("　[-] - 図書室ランクのボーナスを確認する", "司書を配属する必要があります", 0, "gray")
ENDIF
PRINTL 　[3] - 司書を配属する
PRINTL 　[4] - 利用履歴を確認する（過去5日分）
PRINTL 　[5] - 図書室の説明
DRAWLINE
PRINTPLAIN 　図書室　
PRINTL [0] - 戻る

CALL TUTORIAL

BINPUT
SELECTCASE RESULT
	CASE 0
		CALL TUTORIAL, "経営_1"
		RETURN
	CASE 1
		IF リソース:本の最終補充日 == YEAR*10000+MONTH*100+DAYS
			PRINTW 本は補充したばかりです
			RESTART
		ENDIF
		IF MONEY < 補充費 && !ORIGIN
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		CALL PASELIL, @"\@ !ORIGIN ? {補充費}＄の経費で #\@本を補充します。よろしいですか？"
		PRINTL [0] はい
		PRINTL [1] いいえ
		BINPUT
		IF RESULT == 0
			;本を補充するとキャラの本への興味が施設ランクに合わせられる
			;どんなにランクが低くても5は保証
			CVARSET CFLAG, GETNUM(CFLAG, "本への興味"), MAX(図書室ランク(), 5)
			CALL PAYMENT, -補充費, "本の補充費"
			リソース:本の最終補充日 = YEAR*10000+MONTH*100+DAYS
			本の補充経過日数 = 0
		ENDIF
	CASE 2
		DRAWLINE
		司書魅力 = 0
		PRINTL □詳細情報
		FOR LCOUNT, 0, CHARANUM
			SIF CSTR:LCOUNT:配属 == "図書室"
				PRINTFORML 　%NAME:LCOUNT%の司書レベル:{司書レベル(LCOUNT)}
			SIF TALENT:LCOUNT:魅力 && TALENT:LCOUNT:謎の魅力
				司書魅力 = 1
		NEXT
		PRINTFORML 　司書一人ごとの能力上限:{30/CMATCH(CSTR:配属, "図書室")}
		PRINTL 
		CALL PASELIL, @"　利用者一人ごとの本の補充費：{補充費/CHARANUM}＄"
		SIF 司書魅力
			PRINTL 　魅力的な司書が居るため利用者増加
		PRINTL □ランクに応じて購入可能な本
		PRINTL 　図書室ランク10以上で購入可能な本
		SIF 図書室ランク() < 10
			SETCOLORBYNAME gray
		FOR LCOUNT, 450, 470
			SIF ITEMNAME:LCOUNT != ""
				PRINTFORML 　　%ITEMNAME:LCOUNT%
		NEXT
		RESETCOLOR
		PRINTL 　図書室ランク20以上で購入可能な本
		SIF 図書室ランク() < 20
			SETCOLORBYNAME gray
		FOR LCOUNT, 470, 500
			SIF ITEMNAME:LCOUNT != ""
				PRINTFORML 　　%ITEMNAME:LCOUNT%
		NEXT
		RESETCOLOR
		PRINTL 上記の本はSHOPの アイテム＞本・特殊技能 からお買い求めください
		WAIT
	CASE 3
		CALL TUTORIAL, "配属"
		CALL STRATEGY_PERSONAL, "図書室"
	CASE 4
		CALL 図書室利用者
	CASE 5
		CALL MANUAL_図書室
ENDSELECT

RESTART

@図書室ランク
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ランク

FOR LCOUNT, 0, CHARANUM
	;司書の実務経験に応じて施設ランク上昇
	;合計最大レベルは30 配属人数に応じて一人頭の能力にキャップを設ける 一人なら30、二人なら15、三人なら10
	SIF CSTR:LCOUNT:配属 == "図書室" && CFLAG:LCOUNT:配属完了
		ランク += LIMIT(司書レベル(LCOUNT), 0, 30/CMATCH(CSTR:配属, "図書室"))
NEXT

RETURNF ランク

@司書レベル, 対象
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC レベル
;実務経験/100
レベル = EXP:対象:実務経験/100
SIF 統率者("図書室") && !TALENT:対象:統率
	レベル++
;買い物上手 +1
SIF TALENT:対象:買い物上手
	レベル++
;読書家 +2
SIF TALENT:対象:読書家
	レベル += 2
;潔癖症 +2
SIF TALENT:対象:潔癖症
	レベル += 2
;禁断の知識 +2
SIF TALENT:対象:禁断の知識
	レベル += 2
;汚れ無視 -1
SIF TALENT:対象:汚れ無視
	レベル--
;サボり魔 -1
SIF TALENT:対象:サボり魔 && !ワーカホリックレベル(対象)
	レベル--

RETURNF レベル

@TURNEND_図書室
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 司書魅力
#DIM DYNAMIC 男性司書
#DIM DYNAMIC 女性司書
#DIM DYNAMIC 意欲
#DIM DYNAMIC 興味
#DIM DYNAMIC 利用人数
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC モチベ合計値
#DIM DYNAMIC 上昇経験

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "図書室"
		CALL KOJO_STRATEGY, LCOUNT, "図書室", "TURNEND"
NEXT

;以下の処理は日付変更時のみ
SIF ターン == "昼"
	RETURN

;まず配属中のキャラの処理
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "図書室"
		;配属完了にする
		CFLAG:LCOUNT:配属完了 = 1
		;実務経験加算
		EXP:LCOUNT:実務経験 += 2
		;習得早ければ+1
		SIF TALENT:LCOUNT:習得早い
			EXP:LCOUNT:実務経験++
		;魅力・謎の魅力持ちなら利用者増える
		SIF TALENT:LCOUNT:謎の魅力 || TALENT:LCOUNT:魅力
			司書魅力++
		;男女数計算
		SELECTCASE SEX(LCOUNT)
			CASE 1
				男性司書++
			CASE 2
				女性司書++
		ENDSELECT
	ENDIF
NEXT

;図書室利用履歴の変数を後ろに詰める 2次元配列ではARRAYSHIFTできないので力技
FOR LCOUNT, 3, -1, -1
	FOR LCOUNT2, 0, 1000
		図書室利用履歴:(LCOUNT+1):LCOUNT2 = %図書室利用履歴:LCOUNT:LCOUNT2%
		SIF LCOUNT == 0
			図書室利用履歴:LCOUNT:LCOUNT2 = 
	NEXT
NEXT

;ここからは全員の処理
FOR LCOUNT, 0, CHARANUM
	;MASTERは除外
	SIF LCOUNT == MASTER
		CONTINUE
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	;知識欲・学習意欲を計算
	意欲 = 1
	SIF TALENT:LCOUNT:大人しい
		意欲++
	SIF TALENT:LCOUNT:好奇心
		意欲++
	SIF TALENT:LCOUNT:悲観的
		意欲++
	SIF TALENT:LCOUNT:恥じらい
		意欲++
	SIF TALENT:LCOUNT:習得早い
		意欲++
	SIF ABL:LCOUNT:欲望 >= 3
		意欲++
	SIF TALENT:LCOUNT:読書家
		意欲 += 2
	SIF TALENT:LCOUNT:調合知識 || TALENT:LCOUNT:地質学 || TALENT:LCOUNT:植生学 || TALENT:LCOUNT:魔法使い
		意欲++
	;興味を計算
	興味 = CFLAG:LCOUNT:本への興味
	;魅力・謎の魅力持ちの人数*5
	興味 += 司書魅力*5
	;男好き・女好きなら司書の数*5
	SIF TALENT:LCOUNT:男好き
		興味 += 男性司書*5
	SIF TALENT:LCOUNT:女好き
		興味 += 女性司書*5
	;嫌いなら司書の数*-5
	SIF TALENT:LCOUNT:男嫌い
		興味 -= 男性司書*5
	SIF TALENT:LCOUNT:女嫌い
		興味 -= 女性司書*5
	;{意欲*興味}%の確率で図書室を利用する
	IF RAND(1, 101) <= 意欲*興味
		;利用した場合は{CFLAG:本への興味*20}の習得の珠とランダムな性経験以外の経験を入手
		;本への興味が皆無で司書目当てでも10/1は入る
		;本への興味は最大30なので、ボーナス最大値は習得の珠+600、経験+7になる
		IF CFLAG:LCOUNT:本への興味 < 0
			JUEL:LCOUNT:習得 += 10
			上昇経験 = RAND(GETEXP("料理経験"), GETEXP("医療経験")+1)
			EXP:LCOUNT:上昇経験++
			;利用履歴変数に名前を入れる
			図書室利用履歴:0:利用人数 = %NAME:LCOUNT%(習得の珠+10 %EXPNAME:上昇経験%+1)
		;習得遅いと半減する
		ELSEIF TALENT:LCOUNT:習得遅い
			JUEL:LCOUNT:習得 += CFLAG:LCOUNT:本への興味*10
			上昇経験 = RAND(GETEXP("料理経験"), GETEXP("医療経験")+1)
			EXP:LCOUNT:上昇経験 += CFLAG:LCOUNT:本への興味/10+1
			;利用履歴変数に名前を入れる
			図書室利用履歴:0:利用人数 = %NAME:LCOUNT%(習得の珠+{CFLAG:LCOUNT:本への興味*10} %EXPNAME:上昇経験%+{CFLAG:LCOUNT:本への興味/10+1})
		ELSE
			JUEL:LCOUNT:習得 += CFLAG:LCOUNT:本への興味*20
			上昇経験 = RAND(GETEXP("料理経験"), GETEXP("医療経験")+1)
			EXP:LCOUNT:上昇経験 += CFLAG:LCOUNT:本への興味/5+1
			;利用履歴変数に名前を入れる
			図書室利用履歴:0:利用人数 = %NAME:LCOUNT%(習得の珠+{CFLAG:LCOUNT:本への興味*20} %EXPNAME:上昇経験%+{CFLAG:LCOUNT:本への興味/5+1})
		ENDIF
		利用人数++
		;利用した場合は素質に応じて興味が下がる（読書家は下がりにくい）
		SIF TALENT:LCOUNT:反抗的 && (!TALENT:LCOUNT:読書家 || RAND:2)
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:衝動的 && (!TALENT:LCOUNT:読書家 || RAND:2)
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:サボり魔 && (!TALENT:LCOUNT:読書家 || RAND:2)
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:無関心 && (!TALENT:LCOUNT:読書家 || RAND:2)
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:保守的 && (!TALENT:LCOUNT:読書家 || RAND:2)
			CFLAG:LCOUNT:本への興味--
	ENDIF
	;利用しなくても興味は下がっていく
	CFLAG:LCOUNT:本への興味--
NEXT

PRINTFORML {利用人数}人が図書室を利用しました

;図書室配属中のキャラのモチベを計算する
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "図書室"
		CONTINUE
	モチベ計算用 = 0
	FOR LCOUNT2, 0, VARSIZE("TALENT")
		SIF !TALENT:LCOUNT:LCOUNT2
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT2
			CASE "臆病", "素直", "プライド高い", "大人しい", "冷静", "弱気", "感情乏しい", "好奇心", "習得早い", "潔癖症", "魔法使い", "工学博士", "読書家"
				モチベ計算用++
			CASE "衝動的", "ハイテンション", "保守的", "目立ちたがり", "寂しがり屋", "習得遅い", "汚れ無視", "倒錯的", "幼稚"
				モチベ計算用--
		ENDSELECT
	NEXT
	
	モチベ合計値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数("図書室")
	SIF TALENT:LCOUNT:世話好き
		モチベ合計値 -= 世話好き人数("図書室")
	;配属先のキャラの相性で計算
	モチベ合計値 += 相性モチベ補正(LCOUNT)
	CFLAG:LCOUNT:モチベーション = LIMIT(モチベ合計値, 1, 100)
	;利用人数に応じて習得増加
	;SIF 利用人数 > 0
	;	JUEL:LCOUNT:習得 += 利用人数*10
NEXT
本の補充経過日数++

RETURN

@図書室利用者
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日
#DIM DYNAMIC 利用年, 5
#DIM DYNAMIC 利用月, 5
#DIM DYNAMIC 利用日, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ

;5日遡ってその日時を入れておく
年 = YEAR
月 = MONTH
日 = DAYS
FOR LCOUNT, 0, 5
	DO
		日--
		IF 日 <= 0
			月--
			IF 月 <= 0
				年--
				月 = 12
			ENDIF
			日 = 31
		ENDIF
	LOOP !EXIST_DAY(年, 月, 日)
	利用年:LCOUNT = 年
	利用月:LCOUNT = 月
	利用日:LCOUNT = 日
NEXT

VARLINE = 0

$LOOP
CALL LOOP_CLEARLINE

DRAWLINE
PRINTFORML □{利用年:ページ}年{利用月:ページ}月{利用日:ページ}日の図書室利用者
FOR LCOUNT, 0, 1000
	IF 図書室利用履歴:ページ:LCOUNT == ""
		IF LCOUNT == 0
			PRINTL 
			PRINTL 利用者無し
			PRINTL 
		ENDIF
		BREAK
	ENDIF
	PRINTFORML 　%図書室利用履歴:ページ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAIN 　図書室利用履歴　
CALL PRINTSPACE, ページ > 0, "[1] 翌日の履歴 "
PRINT [0] 戻る 
CALL PRINTSPACE, ページ < 4, "[9] 前日の履歴"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 0
		VARLINE = 0
		RETURN
	CASE 1
		ページ--
		GOTO LOOP
	CASE 9
		ページ++
		GOTO LOOP
ENDSELECT

@MANUAL_図書室
DRAWLINE
PRINTL □図書室について
PRINTL 　[1] - 図書室のシステム
PRINTL 　[2] - 図書室ランクとは？
DRAWLINE
PRINTPLAIN 　図書室　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		PRINTL 図書室はキャラクターたちが日常的に使用する施設であり、使用者は習得の珠と、学問、文化、仕事、作業の知識を経験として得ることができます
		PRINTL キャラクターたちもそれぞれ本に対する興味や関心を持っており、積極的に通うキャラもいれば興味の無いキャラもいます
		PRINTL しかし本棚が代わり映えしないことにはどのキャラも次第に興味を失っていき、最終的には図書室を利用しなくなります
		PRINTL 定期的に本を補充し、キャラクターたちの興味を惹きましょう
		PRINTL 本の補充費は司書たちの能力（司書レベル）で変動し、仕事慣れしていたり本の扱いが丁寧なキャラは補充費を安く抑えられます
		PRINTL その他、キャラクターたちは本だけでなく司書との交流を目的として利用することもあります
		PRINTL いろいろなキャラで司書の適性を試してみるとよいでしょう
		WAIT
		RESTART
	CASE 2
		PRINTL 図書室ランクとは、配属した司書の実務経験に応じて上昇するランクです。司書レベルとは違うので注意してください
		PRINTL 最大値は30で、司書の人数に応じて一人あたりの上限が変わります。少人数でランクを上げるには相応の実務経験が必要になります
		PRINTL 逆に多人数でランクを上げるには個々の司書が実務経験を重ねていないとランクが上昇しづらくなります
		PRINTL ランクが上昇することで、素質を取得できる本などの特別なアイテムを購入できるようになります
		PRINTL 購入可能なアイテムは「図書室ランクのボーナスを確認する」から見てみましょう
		WAIT
		RESTART
ENDSELECT
