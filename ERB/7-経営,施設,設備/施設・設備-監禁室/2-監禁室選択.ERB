@監禁室選択
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 監禁室使用キャラ, 11

PRINTL □使用する監禁施設を選択してください
FOR LCOUNT, 1, ITEM:監禁室+1
	PRINTFORM 　[{LCOUNT, 2}] - 監禁室{LCOUNT, 2, LEFT} 
	IF 監禁室使用状況:LCOUNT == ""
		CALL PRINTCOLORL, "[空き]", "gray"
	ELSE
		監禁室使用キャラ:LCOUNT = FINDCHARA(CFLAG:監禁, LCOUNT)
		PRINTFORML [使用中:%NAME:(監禁室使用キャラ:LCOUNT)%(%監禁室使用状況:LCOUNT%)]
	ENDIF
NEXT
IF !ITEM:監禁室
	PRINTL 
	PRINTL -----監禁室の部屋を購入してください-----
	PRINTL
ENDIF

DRAWLINE
PRINTPLAIN 　監禁室　
PRINTL [0] - 戻る

BINPUT
LRESULT = RESULT
SELECTCASE LRESULT
	CASE 0
		RETURN
	CASE 1 TO 10
		IF 監禁室使用キャラ:LRESULT > 0
			PRINTFORML 監禁室{LRESULT}には現在%CALLNAME:(監禁室使用キャラ:LRESULT)%を監禁中です。ここを使いますか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			PRINTL [2] - 解放する
			BINPUT
			SELECTCASE RESULT
				CASE 1
					RESTART
				CASE 2
					IF FINDCHARA(CFLAG:監禁, LRESULT) > 0
						監禁室使用状況:LRESULT = 
						CFLAG:FINDCHARA(CFLAG:監禁, LRESULT):監禁 = 0
						監禁室使用キャラ:LRESULT = 0
						RESTART
					ENDIF
			ENDSELECT
		ENDIF
		CALL 監禁室使用, LRESULT
		RESTART
ENDSELECT


@監禁室使用, 監禁室番号
#DIM DYNAMIC 監禁室番号
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 配属人数
#DIM DYNAMIC 配属スライム
#DIM DYNAMIC 配属触手
#DIM DYNAMIC 監禁可能
#DIM DYNAMIC 監禁対象
#DIMS DYNAMIC 監禁内容

IF CHARANUM == 1 || CHARANUM-CMATCH(CFLAG:キャラロスト, 1) == 1
	PRINTW 監禁できるキャラが居ません
	RETURN
ENDIF

配属人数 = 0
配属スライム = 0
FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:配属 == "監禁室"
		配属人数++
		SIF ISSLIME(LCOUNT)
			配属スライム++
		SIF TALENT:LCOUNT:触手使い
			配属触手++
	ENDIF
NEXT

SIF TALENT:MASTER:触手使い
	配属触手++

PRINTL □誰を監禁しますか？
FOR PAGING, 1, CHARANUM
	CALLFORM DISABLE, "EVENT_監禁", PAGING
	CALL PAGING, !RESULT && !CFLAG:PAGING:監禁 && CSTR:PAGING:配属 == "", @"\@ TALENT:PAGING:ふたなり ? [ふたなり] # \@\@ TALENT:PAGING:去勢済み ? [去勢済み] # \@\@ TALENT:PAGING:母乳体質 ? [母乳体質] # \@\@ TALENT:PAGING:スライム ? [スライム] # \@\@ TALENT:PAGING:溶ける快 ? [溶ける快] # \@"
NEXT
SELECTCASE RESULT
	CASE -1
		RETURN
	CASEELSE
		監禁対象 = RESULT
ENDSELECT

PRINTL □どのように監禁しますか？
IF 配属触手
	PRINTL 　[1] - 触手監禁(触手による各性感帯の開発)
ELSE
	CALL PRINTCOLORL, "　[-] - 触手監禁(主人か配属中のキャラに[触手使い]が必要)", "gray"
ENDIF
IF TALENT:監禁対象:母乳体質
	PRINTL 　[2] - 搾乳監禁(母乳の採集)
ELSE
	CALL PRINTCOLORL, "　[-] - 搾乳監禁(対象に母乳体質が必要)", "gray"
ENDIF
IF PENIS(監禁対象)
	PRINTL 　[3] - 搾精監禁(精液の採集)
ELSE
	CALL PRINTCOLORL, "　[-] - 搾精監禁(対象に男性器が必要)", "gray"
ENDIF
IF TALENT:監禁対象:無機物 || CSTR:監禁対象:種族 == "機械" || TALENT:監禁対象:ゾンビ || TALENT:監禁対象:霊体
	CALL PRINTCOLORL, "　[-] - 採血監禁(対象に血が通った肉体が必要)", "gray"
ELSE
	PRINTL 　[4] - 採血監禁(血液の採集)
ENDIF
IF ISSLIME(監禁対象)
	PRINTL 　[5] - スライム監禁(粘液の採集)
ELSEIF FLAG:抗スライム
	CALL PRINTCOLORL, "　[-] - スライム監禁(抗スライム薬による感染予防中)", "gray"
ELSEIF !スライム感染可能(監禁対象)
	CALL PRINTCOLORL, "　[-] - スライム監禁(対象はスライム感染不可)", "gray"
ELSEIF 配属スライム
	PRINTL 　[5] - スライム監禁(スライム侵食度の加速)
ELSE
	CALL PRINTCOLORL, "　[-] - スライム監禁(スライムが必要)", "gray"
ENDIF
IF MOD:STS
	IF TALENT:監禁対象:無機物 || CSTR:監禁対象:種族 == "機械"
		CALL PRINTCOLORL, "　[-] - 処刑(対象が生物である必要)", "gray"
	ELSEIF ISSLIME(監禁対象)
		CALL PRINTCOLORL, "　[-] - 処刑(スライムは処刑不可)", "gray"
	ELSEIF TALENT:監禁対象:ゾンビ || TALENT:監禁対象:霊体
		CALL PRINTCOLORL, "　[-] - 処刑(対象が既に死んでいる)", "gray"
	ELSE
		PRINTL 　[6] - 処刑
	ENDIF
ENDIF
DRAWLINE
PRINTPLAIN 　監禁室　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		$触手監禁
		PRINTL □どこを開発しますか？
		IF VAGINA(監禁対象) && !処女膜(監禁対象)
			PRINTFORML 　[1] - Ｖ(\@ GETBIT(CFLAG:監禁対象:監禁, 1) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - Ｖ", "gray"
		ENDIF
		IF !TALENT:監禁対象:去勢済み
			PRINTFORML 　[2] - Ｃ(\@ GETBIT(CFLAG:監禁対象:監禁, 2) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - Ｃ", "gray"
		ENDIF
		PRINTFORML 　[3] - Ａ(\@ GETBIT(CFLAG:監禁対象:監禁, 3) ? ○ # ×\@)
		PRINTFORML 　[4] - Ｂ(\@ GETBIT(CFLAG:監禁対象:監禁, 4) ? ○ # ×\@)
		PRINTFORML 　[5] - 尿道(\@ GETBIT(CFLAG:監禁対象:監禁, 5) ? ○ # ×\@)
		PRINTL 　[0] - 戻る [10] - 確定
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CFLAG:監禁対象:監禁 = 0
				RESTART
			CASE 10
				IF CFLAG:監禁対象:監禁 == 0
					PRINTW 開発する部位を選んでください
					GOTO 触手監禁
				ENDIF
				監禁内容 = 触手監禁/
				SIF GETBIT(CFLAG:監禁対象:監禁, 1)
					監禁内容 += "Ｖ/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 2)
					監禁内容 += "Ｃ/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 3)
					監禁内容 += "Ａ/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 4)
					監禁内容 += "Ｂ/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 5)
					監禁内容 += "尿道/"
			CASE 1 TO 5
				INVERTBIT CFLAG:監禁対象:監禁, RESULT
				GOTO 触手監禁
		ENDSELECT
	CASE 2
		$搾乳監禁
		PRINTL □搾精の方法を選んでください
		IF 配属人数
			PRINTFORML 　[1] - スタッフの手で行う(\@ GETBIT(CFLAG:監禁対象:監禁, 1) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - スタッフの手で行う(人員の配属が必要)", "gray"
		ENDIF
		IF 基礎研究レベル
			PRINTFORML 　[2] - 道具と機械を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 2) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - 道具と機械を使用する(実験室ランクが必要)", "gray"
		ENDIF
		IF CMATCH(CSTR:配属, "医務室")
			PRINTFORML 　[3] - 薬を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 3) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - 薬を使用する(医務室ランクが必要)", "gray"
		ENDIF
		PRINTL 　[0] - 戻る [10] - 確定
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CFLAG:監禁対象:監禁 = 0
				RESTART
			CASE 10
				IF CFLAG:監禁対象:監禁 == 0
					PRINTW 搾乳方法を選んでください
					GOTO 搾乳監禁
				ENDIF
				監禁内容 = 搾乳監禁/
				SIF GETBIT(CFLAG:監禁対象:監禁, 1)
					監禁内容 += "スタッフの手/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 2)
					監禁内容 += "道具機械/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 3)
					監禁内容 += "薬品/"
			CASE 1 TO 3
				INVERTBIT CFLAG:監禁対象:監禁, RESULT
				GOTO 搾乳監禁
		ENDSELECT
	CASE 3
		$搾精監禁
		PRINTL □搾精の方法を選んでください
		IF 配属人数
			PRINTFORML 　[1] - スタッフの手で行う(\@ GETBIT(CFLAG:監禁対象:監禁, 1) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - スタッフの手で行う(人員の配属が必要)", "gray"
		ENDIF
		IF 基礎研究レベル
			PRINTFORML 　[2] - 道具と機械を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 2) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - 道具と機械を使用する(実験室ランクが必要)", "gray"
		ENDIF
		IF CMATCH(CSTR:配属, "医務室")
			PRINTFORML 　[3] - 薬を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 3) ? ○ # ×\@)
			PRINTFORML 　[4] - 尿道を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 4) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - 薬を使用する(医務室ランクが必要)", "gray"
			CALL PRINTCOLORL, "　[-] - 尿道を使用する(医務室ランクが必要)", "gray"
		ENDIF
		PRINTL 　[0] - 戻る [10] - 確定
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CFLAG:監禁対象:監禁 = 0
				RESTART
			CASE 10
				IF CFLAG:監禁対象:監禁 == 0
					PRINTW 搾精方法を選んでください
					GOTO 搾精監禁
				ENDIF
				監禁内容 = 搾精監禁/
				SIF GETBIT(CFLAG:監禁対象:監禁, 1)
					監禁内容 += "スタッフの手/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 2)
					監禁内容 += "道具機械/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 3)
					監禁内容 += "薬品/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 4)
					監禁内容 += "尿道/"
			CASE 1 TO 4
				INVERTBIT CFLAG:監禁対象:監禁, RESULT
				GOTO 搾精監禁
		ENDSELECT
	CASE 4
		$採血監禁
		PRINTL □採血の方法を選んでください
		IF 配属人数
			PRINTFORML 　[1] - スタッフの手で行う(\@ GETBIT(CFLAG:監禁対象:監禁, 1) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - スタッフの手で行う(人員の配属が必要)", "gray"
		ENDIF
		IF 基礎研究レベル
			PRINTFORML 　[2] - 道具と機械を使用する(\@ GETBIT(CFLAG:監禁対象:監禁, 2) ? ○ # ×\@)
		ELSE
			CALL PRINTCOLORL, "　[-] - 道具と機械を使用する(実験室ランクが必要)", "gray"
		ENDIF
		PRINTL 　[0] - 戻る [10] - 確定
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CFLAG:監禁対象:監禁 = 0
				RESTART
			CASE 10
				IF CFLAG:監禁対象:監禁 == 0
					PRINTW 採血方法を選んでください
					GOTO 採血監禁
				ENDIF
				監禁内容 = 採血監禁/
				SIF GETBIT(CFLAG:監禁対象:監禁, 1)
					監禁内容 += "スタッフの手/"
				SIF GETBIT(CFLAG:監禁対象:監禁, 2)
					監禁内容 += "道具機械/"
			CASE 1 TO 2
				CFLAG:監禁対象:監禁 = 0
				INVERTBIT CFLAG:監禁対象:監禁, RESULT
				GOTO 採血監禁
		ENDSELECT
	CASE 5
		PRINTFORML %CALLNAME:監禁対象%をスライム監禁します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SELECTCASE RESULT
			CASE 0
				監禁内容 = スライム監禁/
				SIF FINDCHARA(CFLAG:監禁, 監禁室番号) > 0
					CFLAG:FINDCHARA(CFLAG:監禁, 監禁室番号):監禁 = 0
				CFLAG:監禁対象:監禁 = 監禁室番号
			CASE 1
				CFLAG:監禁対象:監禁 = 0
				RESTART
		ENDSELECT
	CASE 6
		;処刑のバリエーションは増やしたい
		PRINTFORML %CALLNAME:監禁対象%を処刑します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW %CALLNAME:監禁対象%を処刑しました
				CALL プレイログ記録, @"%NAME:監禁対象%が死亡"
				CSTR:監禁対象:ロスト要因 = 死亡
				CSTR:監禁対象:ロスト詳細 = 監禁室で処刑された
				CALL CLEAR_CHARA, 監禁対象, 0, 1
				RETURN 1
			CASE 1
				CFLAG:監禁対象:監禁 = 0
				RESTART
		ENDSELECT
ENDSELECT
;最後の余計なスラッシュを取る
監禁室使用状況:監禁室番号 = %SUBSTRINGU(監禁内容 , 0, STRLENSU(監禁内容 )-1)%
SIF FINDCHARA(CFLAG:監禁, 監禁室番号) > 0
	CFLAG:FINDCHARA(CFLAG:監禁, 監禁室番号):監禁 = 0
CFLAG:監禁対象:監禁 = 監禁室番号
CFLAG:監禁対象:居住 = 0
SIF 監禁対象 == TARGET
	TARGET = -1
SIF 監禁対象 == ASSI
	ASSI = -1
SIF 監禁対象 == ASSI:1
	ASSI:1 = -1
RETURN 1

@監禁解除, 対象
#DIM DYNAMIC 対象
監禁室使用状況:(CFLAG:対象:監禁) = 
CFLAG:対象:監禁 = 0
