@出版社経営
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象雑誌
#DIM DYNAMIC 頁数
#DIM DYNAMIC 雑誌あり

雑誌あり = 0
FOR LCOUNT, 0, 3
	IF 誌名:LCOUNT:発売中 != ""
		雑誌あり = 1
		BREAK
	ENDIF
NEXT

DRAWLINE
CALL PASELIL, @"□累計売上:{PU:総利益}＄ 前日の売上:{PU:前日の利益}＄"
PRINTFORML 　名声:{PU:名声} 購読者数:{PU:購読者}
IF 雑誌あり
	PRINTL □現在発売中の雑誌
	FOR LCOUNT, 0, 3
		SIF 誌名:LCOUNT:発売中 == ""
			CONTINUE
		PRINTFORM 　%誌名:LCOUNT:発売中% 
		IF 出版物:LCOUNT:発売日 > 0
			PRINTFORML 発売まであと{(出版物:LCOUNT:発売日+1)/2}日
		ELSE
			PRINTFORM 価格:{出版物:LCOUNT:価格} 残り部数:{出版物:LCOUNT:部数} 品質:{出版物:LCOUNT:品質} 需要:
			SELECTCASE 出版物:LCOUNT:需要
				CASE IS >= 95
					CALL PRINTCOLOR, "最高", "gold"
				CASE IS >= 70
					CALL PRINTCOLOR, "高", "magenta"
				CASE IS >= 40
					CALL PRINTCOLOR, "中", "deepskyblue"
				CASE IS >= 10
					CALL PRINTCOLOR, "低", "lime"
				CASE IS >= 1
					PRINT 最低
				CASE 0
					CALL PRINTCOLOR, "皆無", "gray"
			ENDSELECT
			PRINTFORML  [{LCOUNT+100}] 読む [{LCOUNT+200}] 増刷する(現在:\@ 出版物:LCOUNT:刷数 == 1 ? 初版 # 第{出版物:LCOUNT:刷数}刷 \@) [{LCOUNT+300}] 廃刊する
		ENDIF
	NEXT
ENDIF
PRINTL 
PRINTL 　[0] - 編集、発行する
PRINTL 　[1] - 記者を配属する
PRINTL 　[2] - バックナンバーを読む
PRINTL 　[9] - 出版社経営について

DRAWLINE
PRINTPLAIN 　PUBLISHING　
PRINTL [10] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 10
		RETURN
	CASE 0
		CALL 雑誌編集
	CASE 1
		CALL STRATEGY_PERSONAL, "出版社"
	CASE 2
		CALL バックナンバー
	CASE 9
		CALL TUTORIAL_出版社
	CASE 100 TO 102
		対象雑誌 = RESULT-100
		SIF 誌名:対象雑誌:発売中 == ""
			CONTINUE
		DRAWLINE
		PRINTFORML □%誌名:対象雑誌:発売中%
		FOR LCOUNT, 0, 20
			SIF 出版物情報:対象雑誌:LCOUNT:内容 == ""
				CONTINUE
			PRINTFORML 　{LCOUNT+1, 2}ページ目,%出版物情報:対象雑誌:LCOUNT:内容%
		NEXT
		WAIT
	CASE 200 TO 202
		対象雑誌 = RESULT-200
		SIF 誌名:対象雑誌:発売中 == ""
			CONTINUE
		;1ページ10パセリ
		頁数 = 0
		FOR LCOUNT, 0, 20
			SIF 出版物情報:対象雑誌:LCOUNT:内容 != ""
				頁数++
		NEXT
		$REINPUT
		CALL PASELIL, @"「%誌名:対象雑誌:発売中%」の増刷には1部ごとに{頁数*10}＄必要です。何部増刷しますか？(所持金:{MONEY}＄)"
		PRINTL 需要と供給の目安:最高で1万部 高で7千部 中で4千部 低で1千部 最低で100部 皆無は売れる見込み無し 
		PRINTL [100][1000][4000][7000][10000]
		PRINTL [0] 戻る
		INPUT
		SELECTCASE RESULT
			CASE 0
				RESTART
			CASE IS > 0
				IF RESULT*頁数*10 > MONEY
					PRINTW 所持金が足りません
					GOTO REINPUT
				ENDIF
				PRINTFORMW 「%誌名:対象雑誌:発売中%」を{RESULT}部増刷しました
				出版物:対象雑誌:部数 += RESULT
				CALL PAYMENT, -(RESULT*頁数*10), "増刷費用"
				出版物:対象雑誌:刷数++
			CASEELSE
				GOTO REINPUT
		ENDSELECT
	CASE 300 TO 302
		対象雑誌 = RESULT-300
		SIF 誌名:対象雑誌:発売中 == ""
			CONTINUE
		PRINTFORML 「%誌名:対象雑誌:発売中%」を廃刊します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			RESTART
		PRINTFORMW 「%誌名:対象雑誌:発売中%」を廃刊しました
		CALL 雑誌廃刊, 対象雑誌
ENDSELECT
RESTART

@TUTORIAL_出版社
DRAWLINE
PRINTL 出版社経営は、調教中に撮影した写真を雑誌にまとめて発売する経営システムです。事業開発オフィスが必要になります
PRINTL 出版には雑誌を編集し、印刷代を支払って編集期間を終えると発行できるようになります
PRINTL 雑誌の編集中は、妊娠等のやむを得ない理由が無い限りキャラの配属先を変えられないのでご注意ください
PRINTW またモデルのキャラの人気度に応じて品質も上がります。本が売れるほど出演者の人気度も上昇します

@雑誌編集
#DIM DYNAMIC 主要キャラ = -1
#DIM DYNAMIC 編集者レベル
#DIM DYNAMIC カメラマン総数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象雑誌
#DIM DYNAMIC 対象ページ
#DIMS DYNAMIC ページ情報
#DIM DYNAMIC ページ情報NO, 2
#DIMS DYNAMIC SPLITVAR, 3
#DIM DYNAMIC 品質計算用
#DIM DYNAMIC 価格計算用
#DIM DYNAMIC 出演者フラグ, 20000
#DIM DYNAMIC 空きスロット
#DIM DYNAMIC 発行部数
#DIM DYNAMIC 頁数
#DIM DYNAMIC 費用
#DIM DYNAMIC 発売予定日
#DIM DYNAMIC 発刊可能

編集者レベル = 0
カメラマン総数 = 0
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 != "出版社"
		CONTINUE
	編集者レベル += MIN(ABL:LCOUNT:撮影技能, 10)
	編集者レベル += MIN(EXP:LCOUNT:実務経験/100, 10)
	SIF TALENT:LCOUNT:カメラマン
		カメラマン総数++
NEXT

DRAWLINE
PRINTFORML □雑誌編集
PRINTFORML 　現在の編集者レベル:{編集者レベル+カメラマン総数*20}/200
PRINTL 
発刊可能 = 0
FOR LCOUNT, 0, 3
	IF 誌名:LCOUNT:発売中 == ""
		空きスロット = LCOUNT
		発刊可能 = 1
	ENDIF
	PRINTFORML 　[{LCOUNT}] - \@ 誌名:LCOUNT:編集中 != "" ? 「%誌名:LCOUNT:編集中%」を編集する # 新しい雑誌を編集する \@
NEXT
DRAWLINE
PRINTPLAIN 　雑誌編集　
PRINTL [10] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 10
		RETURN
	CASE 0 TO 2
		対象雑誌 = RESULT
ENDSELECT

$編集
DRAWLINE
IF 主要キャラ == -1
	PRINTL □雑誌編集中
ELSE
	PRINTFORML □\@ 誌名:対象雑誌:編集中 == "" ? [ 0] タイトルを付ける # %誌名:対象雑誌:編集中% [0] 変更する \@
ENDIF
;編集中雑誌内容は「写真名/キャラNO/アルバムNO」の書式
品質計算用 = 0
頁数 = 0
VARSET 写真使用中
VARSET 出演者フラグ
FOR LCOUNT, 0, 20
	IF 出版物情報:対象雑誌:LCOUNT:編集中内容 != ""
		SPLIT 出版物情報:対象雑誌:LCOUNT:編集中内容, "/", SPLITVAR
		ページ情報 = %SPLITVAR:0%
		ページ情報NO:0 = TOINT(SPLITVAR:1)
		ページ情報NO:1 = TOINT(SPLITVAR:2)
		;編集中に使ってる写真が消えてるか上書きされてたらそのページを削除
		IF PICS:(ページ情報NO:0):(ページ情報NO:1) != ページ情報
			出版物情報:対象雑誌:LCOUNT:編集中内容 = 
			GOTO PAGEELSE
		ENDIF
		PRINTFORML 　[{LCOUNT+1, 2}]ページ目,%ページ情報%
		;価格が品質に加算される
		品質計算用 += PIC:(ページ情報NO:0):(ページ情報NO:1)
		;出演者の人気も加算される これは各キャラ1回のみ
		SIF 出演者フラグ:(ページ情報NO:0) == 0
			品質計算用 += 人気度:(ページ情報NO:0)
		写真使用中:(ページ情報NO:0):(ページ情報NO:1) = 1
		出演者フラグ:(ページ情報NO:0)++
		SIF 主要キャラ == -1 || 出演者フラグ:主要キャラ < 出演者フラグ:(ページ情報NO:0)
			主要キャラ = ページ情報NO:0
		頁数++
	ELSE
		$PAGEELSE
		CALL PRINTCOLORL, @"　[{LCOUNT+1, 2}]ページ目,"+"-"*30, "gray"
	ENDIF
NEXT

;編集者レベル1につき1%アップ
品質計算用 += 品質計算用*編集者レベル/100

;品質/(10+編集者レベル*5)*1ターン(品質/(20+編者レベル*10)*1日)
発売予定日 = 品質計算用/(10+編集者レベル*5)
;カメラマン一人につきさらに10ターン減(最低2)
発売予定日 = MAX(発売予定日-カメラマン総数*10, 2)

PRINTPLAINFORM 　品質:{品質計算用} 
CALL PASELI, @"価格:{価格計算用}＄ [30] 価格を変更する"
PRINTPLAIN  
PRINTFORML 部数:{発行部数} [31] 部数を変更する

;費用は頁数*10*部数
費用 = 頁数*10*発行部数

DRAWLINE
PRINTPLAIN 　雑誌編集　
PRINT [100] - 戻る 
SIF 品質計算用 > 0 && 価格計算用 > 0 && 発行部数 > 0
	CALL PASELI, @"[101] - この内容で発刊する(発刊までの必要日数:約{発売予定日/2+1}日 必要費用:{費用}＄)"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 100
		RESTART
	CASE 101
		SIF !(品質計算用 > 0 && 価格計算用 > 0 && 発行部数 > 0)
			CONTINUE
		IF 誌名:対象雑誌:編集中 == ""
			PRINTW タイトルを決めてください
			GOTO 編集
		ENDIF
		IF !発刊可能
			PRINTW 既に3冊発売中です。新しく発刊するには既存の雑誌を廃刊する必要があります
			GOTO 編集
		ENDIF
		IF 費用 > MONEY
			PRINTW 費用が足りません
			GOTO 編集
		ENDIF
		PRINTFORML 「%誌名:対象雑誌:編集中%」を発刊します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			GOTO 編集
		CALL PAYMENT, -費用, "出版費用"
		誌名:空きスロット:発売中 = %誌名:対象雑誌:編集中%
		FOR LCOUNT, 0, 20
			SPLIT 出版物情報:対象雑誌:LCOUNT:編集中内容, "/", SPLITVAR
			出版物情報:空きスロット:LCOUNT:内容 = %SPLITVAR:0%
			出版物情報:空きスロット:LCOUNT:出演者 = %SPLITVAR:1%
			CALL 写真破棄, TOINT(SPLITVAR:1), 0, SPLITVAR:0
		NEXT
		出版物:空きスロット:部数 = 発行部数
		出版物:空きスロット:価格 = 価格計算用
		出版物:空きスロット:品質 = 品質計算用
		;雑誌需要(最大100)は品質=価格で50
		;品質>価格で品質超過10%ごとに需要1増加 品質>価格*5で需要100になる
		;品質<価格で価格超過2%ごとに需要1低下 品質*2<価格で需要0になる
		IF 価格計算用 > 品質計算用
			出版物:空きスロット:需要 = MAX(50-(価格計算用-品質計算用)*100/品質計算用/2, 0)
		ELSEIF 価格計算用 < 品質計算用
			出版物:空きスロット:需要 = MIN(50+品質計算用*100/価格計算用/10, 100)
		ELSEIF 品質計算用 == 価格計算用
			出版物:空きスロット:需要 = 50
		ENDIF
		出版物:空きスロット:刷数 = 1
		出版物:空きスロット:発売日 = 発売予定日
		PRINTFORML 「%誌名:空きスロット:発売中%」の発刊作業を開始します
		誌名:対象雑誌:編集中 = 
		FOR LCOUNT, 0, 20
			出版物情報:対象雑誌:LCOUNT:編集中内容 = 
		NEXT
		SIF CSVNAMEF(主要キャラ) == "乙姫" && 主要キャラ < 10000
			CALL 実績解除, "R壱萬"
		WAIT
		RESTART
	CASE 0
		GOTO 雑誌名
	CASE 1 TO 20
		対象ページ = RESULT-1
		CALL 所持写真一覧, 1
		SIF RESULT == -1
			RESTART
		SIF PICS:(RESULT:0):(RESULT:1) == ""
			GOTO 編集
		出版物情報:対象雑誌:対象ページ:編集中内容 = %PICS:(RESULT:0):(RESULT:1)%/{RESULT:0}/{RESULT:1}
		SIF 主要キャラ == -1
			主要キャラ = RESULT:0
		GOTO 編集
	CASE 30
		PRINTL 価格を入力してください 目安:品質＞価格 品質の割合が高いほど需要が高く売れやすい
		INPUT
		価格計算用 = LIMIT(RESULT, 0, __INT_MAX__)
		GOTO 編集
	CASE 31
		PRINTL 発行部数を入力してください(増刷可能 まずは低部数から始めるのがおすすめです)
		PRINTL [100][1000][10000]
		INPUT
		発行部数 = LIMIT(RESULT, 0, __INT_MAX__)
		GOTO 編集
ENDSELECT

$雑誌名
PRINTL 雑誌名を入力してください 
PRINTBUTTON "[ランダム命名]", "ランダム命名"

INPUTS

IF RESULTS == "ランダム命名"
	STRDATA RESULTS
		DATAFORM %CSVCALLNAMEF(主要キャラ)%のエロ本
		DATAFORM %CSVCALLNAMEF(主要キャラ)%の薄い本
		DATAFORM %CSVCALLNAMEF(主要キャラ)%の春画
		DATAFORM %CSVCALLNAMEF(主要キャラ)%が%CALLNAME:MASTER%にあれこれされる本
		DATAFORM 月刊%CSVCALLNAMEF(主要キャラ)%{YEAR}年{MONTH}月号
		DATAFORM 週刊%CSVCALLNAMEF(主要キャラ)%
		DATAFORM 月刊\@ EXISTCSV(主要キャラ) && CSVCSTR(主要キャラ, GETNUM(CSTR, "登場作品")) != "" ? %CSVCSTR(主要キャラ, GETNUM(CSTR, "登場作品"))%の裏側大暴露 # %CSVCALLNAMEF(主要キャラ)%の裏側大暴露 \@{YEAR}年{MONTH}月号
		DATAFORM %CSVCALLNAMEF(主要キャラ)%総受け合同
		DATAFORM 月刊エロカディア{YEAR}年{MONTH}月号
		DATAFORM 月刊調教のすゝめ{YEAR}年{MONTH}月号
		DATAFORM 月刊ヘヴンリームーン{YEAR}年{MONTH}月号
		DATAFORM 月刊天国へようこそ{YEAR}年{MONTH}月号
		DATAFORM 月刊ラブ♡シャイン{YEAR}年{MONTH}月号
		DATAFORM 月刊ECSTACY{YEAR}年{MONTH}月号
		DATAFORM 月刊THE DEEP THROATER{YEAR}年{MONTH}月号
		DATAFORM 月刊セクシープラネット{YEAR}年{MONTH}月号
		DATAFORM 月刊THE LUST STRIKER{YEAR}年{MONTH}月号
		DATAFORM 月刊EROICA{YEAR}年{MONTH}月号
		DATAFORM 月刊SEX-Plan{YEAR}年{MONTH}月号
		DATAFORM 月刊エッチダナァ{YEAR}年{MONTH}月号
		DATAFORM 月刊ハードコアマニア{YEAR}年{MONTH}月号
		DATAFORM 月刊HOTな夜の㊙ブック{YEAR}年{MONTH}月号
		DATAFORM 月刊アナルスパイダー{YEAR}年{MONTH}月号
		DATAFORM 月刊AV革命桜色{YEAR}年{MONTH}月号
		DATAFORM 月刊E-ROTIC{YEAR}年{MONTH}月号
	ENDDATA
ENDIF
PRINTFORML 「%RESULTS%」でよろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ
BINPUT
SIF RESULT == 1
	GOTO 雑誌名
誌名:対象雑誌:編集中 = %RESULTS%
GOTO 編集

@TURNEND_出版社
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC PAGE
#DIM DYNAMIC 売上部数
#DIM DYNAMIC 名声上昇値
#DIM DYNAMIC 出演者計算済み, 20000
#DIM DYNAMIC 総人気度
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC モチベ加算値
#DIM DYNAMIC 支出
#DIM DYNAMIC 出演者計算用

VARLINE = LINECOUNT
PU:前日の利益 = 0
FOR LCOUNT, 0, 3
	SIF 誌名:LCOUNT:発売中 != ""
		GOTO 売上計算
NEXT
RETURN

$売上計算
FOR LCOUNT, 0, 3
	SIF 誌名:LCOUNT:発売中 == ""
		CONTINUE
	売上部数 = 0
	IF 出版物:LCOUNT:発売日 > 0
		SIF --出版物:LCOUNT:発売日 == 0
			PRINTFORML 「%誌名:LCOUNT:発売中%」の編集と発行が終わりました。全国の本屋に出荷します
		CONTINUE
	ENDIF
	;収入は1日の終わりのみ計算
	IF ターン == "夜"
		;需要*10に±20%の揺らぎを入れて売上に入れる
		売上部数 += 出版物:LCOUNT:需要*RAND(800, 1201)/100
		;売上部数＞発行部数なら発行部数に合わせる
		SIF 売上部数 > 出版物:LCOUNT:部数
			売上部数 = 出版物:LCOUNT:部数
		;購読者を除いた売上部数に応じて名声が上がる
		名声上昇値 += 売上部数/10

		;発売初日なら購読者*需要%を売上に入れる
		IF 出版物:LCOUNT:発売日 == 0
			売上部数 += PU:購読者*(出版物:LCOUNT:需要)/100
			出版物:LCOUNT:発売日 = -1
		ENDIF
		;売上部数＞発行部数なら発行部数に合わせる（もう一度）
		SIF 売上部数 > 出版物:LCOUNT:部数
			売上部数 = 出版物:LCOUNT:部数
		;売上部数の分発行部数を減らす
		出版物:LCOUNT:部数 -= 売上部数

		;売上部数の1%～5%が購読者に追加される
		SIF 売上部数 > 100
			PU:購読者 += RAND(売上部数/100, 売上部数/20)

		;需要を低下させる
		出版物:LCOUNT:需要 -= 20
		;品質と価格が釣り合ってなければさらに低下させる
		SIF 出版物:LCOUNT:価格 > 出版物:LCOUNT:品質
			出版物:LCOUNT:需要 -= (出版物:LCOUNT:価格-出版物:LCOUNT:品質)/出版物:LCOUNT:品質+1
		;名声と出演者の人気度に応じて回復させる ただし名声での回復量はかなり重い(100万で1)
		;部数が余ってる場合は需要が増加することはない
		;出演者の人気度を合計する 2ページ以上出演してるキャラは2ページ目以降2割
		VARSET 出演者計算済み
		総人気度 = 0
		FOR PAGE, 0, 20
			出演者計算用 = TOINT(出版物情報:LCOUNT:PAGE:出演者)
			IF 出演者計算済み:出演者計算用
				総人気度 += 人気度:出演者計算用/5
			ELSE
				総人気度 += 人気度:出演者計算用
				出演者計算済み:出演者計算用 = 1
				;人気度の上昇もここでやっておく 売上部数/100(最大10)
				人気度:出演者計算用 += LIMIT(売上部数/100, 0, 10)
			ENDIF
		NEXT
		IF 出版物:LCOUNT:部数
			;総人気度は最大1000*20で2万 部数があるときは総人気度/1000で回復させる(最大19)
			出版物:LCOUNT:需要 += LIMIT(総人気度/1000+PU:名声/1000000, 0, 19)
		ELSE
			;部数が無いときは総人気度/500で回復させる 上限は無し
			出版物:LCOUNT:需要 += 総人気度/1000+PU:名声/1000000
		ENDIF
		出版物:LCOUNT:需要 = LIMIT(出版物:LCOUNT:需要, 0, 100)

		;売れないときもある
		IF 売上部数 == 0
			PRINTFORML 「%誌名:LCOUNT:発売中%」は1冊も売れませんでした……
		ELSE
			CALL PASELIL, @"「%誌名:LCOUNT:発売中%」が{売上部数}冊売れて、総売上{売上部数*出版物:LCOUNT:価格}＄から取り分80\%の{売上部数*出版物:LCOUNT:価格*80/100}＄を得ました(残り部数:{出版物:LCOUNT:部数}冊)"
			CALL PAYMENT, 売上部数*出版物:LCOUNT:価格*80/100, "雑誌の売上"
			PU:総利益 += 売上部数*出版物:LCOUNT:価格*80/100
			PU:前日の利益 += 売上部数*出版物:LCOUNT:価格*80/100
			出版物:LCOUNT:総売上部数 += 売上部数
		ENDIF
	ENDIF
NEXT

SIF 名声上昇値
	PRINTFORML 名声が{名声上昇値}増加しました
PU:名声 += 名声上昇値

;モチベ計算
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "出版社"
		CONTINUE
	モチベ計算用 = 0
	;素質での計算は編集中の出版物があるときのみ
	IF 雑誌編集中()
		FOR LCOUNT2, 0, VARSIZE("TALENT")
			SIF TALENT:LCOUNT:LCOUNT2
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT2
				CASE "服従", "淫乱", "大人しい", "好奇心", "保守的", "貞操無頓着", "自慰しやすい", "倒錯的"
					モチベ計算用++
				CASE "カメラマン", "隷属", "淫魔"
					モチベ計算用 += 2
				CASE "自制心", "無関心", "一線越えない", "貞操観念", "恥じらい", "潔癖症", "快感の否定"
					モチベ計算用--
			ENDSELECT
		NEXT
	ENDIF
	モチベ加算値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数("出版社")
	SIF TALENT:LCOUNT:世話好き
		モチベ加算値 -= 世話好き人数("出版社")
	;配属先のキャラの相性で計算
	モチベ加算値 += 相性モチベ補正(LCOUNT)
	CFLAG:LCOUNT:モチベーション = LIMIT(モチベ加算値, 1, 100)

	;売上の2%が給料 浪費癖は3% 最低500
	IF TALENT:LCOUNT:浪費癖
		支出 += MAX(PU:前日の利益*3/100, 500)
	ELSE
		支出 += MAX(PU:前日の利益*2/100, 500)
	ENDIF
	;実務経験と撮影経験を加算
	EXP:LCOUNT:実務経験++
	EXP:LCOUNT:撮影経験++
	;習得を加算
	;JUEL:LCOUNT:習得 += CFLAG:LCOUNT:モチベーション*10
NEXT
IF 支出 && ターン == "夜"
	CALL PASELIL, @"出版社のスタッフに給料{支出}＄を払いました"
	CALL PAYMENT, -支出, "出版スタッフへの給料"
ENDIF

SIF VARLINE != LINECOUNT
	WAIT
CALL CHECKDRAWLINE

RETURN

@バックナンバー
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ数
#DIM DYNAMIC 終端
#DIMS DYNAMIC SPLITVAR, 2

DRAWLINE
終端 = FINDELEMENT(バックナンバー名, "", 0, VARSIZE("バックナンバー名"), 1)

;バックナンバーの配列は詰めている
FOR LCOUNT, 0, 終端
	SIF LCOUNT >= ページ数*20+20
		BREAK
	IF LCOUNT >= ページ数*20
		;SPLITVAR:0=雑誌名 1=配列
		SPLIT バックナンバー名:LCOUNT, "/", SPLITVAR
		PRINTFORML [{LCOUNT+1, 2}] %SPLITVAR:0% 総売上部数:{バックナンバー売上:TOINT(SPLITVAR:1)}
	ENDIF
NEXT
IF 終端 == 0
	PRINTL
	PRINTL ---バックナンバーはありません---
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAIN 　BUCKNUMBER　
CALL PRINTSPACE, ページ数 > 0, "[-1] 前ページ"
PRINT [-5] 戻る
CALL PRINTSPACE, LCOUNT < 終端-1, "[-9] 次ページ"
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE -5
		RETURN -1
	CASE -1
		ページ数--
	CASE -9
		ページ数++
	CASE 0 TO 終端-1
		DRAWLINE
		SPLIT バックナンバー名:RESULT, "/", SPLITVAR
		PRINTFORML □%SPLITVAR:0%
		FOR LCOUNT, 0, 20
			SIF バックナンバー:TOINT(SPLITVAR:1):LCOUNT == ""
				CONTINUE
			PRINTFORML 　{LCOUNT, 2}ページ目,%バックナンバー:TOINT(SPLITVAR:1):LCOUNT%
		NEXT
		WAIT
ENDSELECT
RESTART

@雑誌廃刊, 対象雑誌
#DIM DYNAMIC 対象雑誌
#DIM DYNAMIC 配列
#DIM DYNAMIC LCOUNT

;空いてる配列を探す
FOR LCOUNT, 0, 50000
	IF バックナンバー:LCOUNT:0 == ""
		配列 = LCOUNT
		BREAK
	ENDIF
NEXT

;バックナンバーに雑誌名と配列を記録する
ARRAYSHIFT バックナンバー名, 1, "", 1
バックナンバー名:0 = %誌名:対象雑誌:発売中%/{配列}

;上で探した空いてる配列に内容を入れる
FOR LCOUNT, 0, 20
	バックナンバー:配列:LCOUNT = %出版物情報:対象雑誌:LCOUNT:内容%
NEXT
バックナンバー売上:配列 = 出版物:対象雑誌:総売上部数

誌名:対象雑誌:発売中 =
出版物:対象雑誌:部数 = 0
出版物:対象雑誌:価格 = 0
出版物:対象雑誌:品質 = 0
出版物:対象雑誌:需要 = 0
出版物:対象雑誌:刷数 = 0
出版物:対象雑誌:発売日 = 0
出版物:対象雑誌:総売上部数 = 0
FOR LCOUNT, 0, 20
	出版物情報:対象雑誌:LCOUNT:内容 = 
	出版物情報:対象雑誌:LCOUNT:出演者 = 
NEXT

@所持写真一覧, 雑誌編集
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 総枚数
#DIM DYNAMIC 対象
#DIM DYNAMIC ページ
#DIM DYNAMIC 処理数
#DIM DYNAMIC 次ページあり
#DIM CONST 表示上限 = 20
#DIM DYNAMIC 雑誌編集

DRAWLINE
PRINTL □写真一覧を表示します
処理数 = 0
次ページあり = 0
FOR LCOUNT, 0, 20000
	SIF PICS:LCOUNT:0 == ""
		CONTINUE
	IF 処理数 < ページ*表示上限
		処理数++
		CONTINUE
	ENDIF
	IF 処理数 >= ページ*表示上限+表示上限
		次ページあり = 1
		BREAK
	ENDIF
	総枚数 = 0
	FOR LCOUNT2, 0, 50
		SIF PICS:LCOUNT:LCOUNT2 != ""
			総枚数++
	NEXT
	PRINTFORML 　[{LCOUNT, 5}] %CSVNAMEF(LCOUNT)%のアルバム({総枚数}/50枚)
	処理数++
NEXT
IF 処理数 == 0
	PRINTL 
	PRINTL 　---写真がありません---
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　PICTURE.{ページ+1}　
CALL PRINTSPACE, ページ > 0, "[-1] 前ページ "
PRINT [-5] 戻る 
CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "

BINPUT
SELECTCASE RESULT
	CASE -5
		RETURN -1
	CASE -1
		ページ--
	CASE -9
		ページ++
	CASE 0 TO 19999
		CALL アルバム, RESULT, 雑誌編集
		SIF 雑誌編集
			RETURN RESULT:0, RESULT:1
ENDSELECT
RESTART

@アルバム, 対象, 雑誌編集
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
#DIM DYNAMIC ページ
#DIM DYNAMIC 売却対象
#DIM DYNAMIC 総枚数
#DIM DYNAMIC 雑誌編集

総枚数 = 0
FOR LCOUNT, 0, 50
	SIF PICS:対象:LCOUNT != ""
		総枚数++
NEXT

DRAWLINE
PRINTFORML □%CSVNAMEF(対象)%のアルバム
FOR LCOUNT, 0, 50
	SIF ページ == 0 && LCOUNT >= 25
		BREAK
	SIF ページ == 1 && LCOUNT < 25
		CONTINUE
	SIF 雑誌編集 && 写真使用中:対象:LCOUNT
		CONTINUE
	SIF LCOUNT >= 総枚数
		BREAK
	CALL PASELIL, @"　%PICS:対象:LCOUNT%({PIC:対象:LCOUNT}＄) [{LCOUNT}] \@ 雑誌編集 ? 使用 # 売却 \@"
NEXT
DRAWLINE
PRINTPLAINFORM 　ALBUM.{ページ+1}　
CALL PRINTSPACE, ページ > 0, "[-1] 前ページ "
PRINT [-5] 戻る 
CALL PRINTSPACE, ページ < 1 && 総枚数 >= 25, "[-9] 次ページ "

BINPUT
SELECTCASE RESULT
	CASE -5
		RETURN -1
	CASE -1, -9
		INVERTBIT ページ, 0
	CASE 0 TO 49
		SIF PICS:対象:RESULT == ""
			CONTINUE
		SIF 雑誌編集
			RETURN 対象, RESULT
		売却対象 = RESULT
		CALL PASELIL, @"%PICS:対象:RESULT%を{PIC:対象:RESULT}＄で売却します。よろしいですか？"
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SIF RESULT == 1
			RESTART
		CALL PASELIW, @"%PICS:対象:売却対象%を{PIC:対象:売却対象}＄で売却しました"
		CALL PAYMENT, PIC:対象:売却対象, @"%CSVNAMEF(対象)%の写真売却"
		CALL 写真破棄, 対象, 売却対象
ENDSELECT
RESTART

@写真破棄, アルバム, 写真, 写真名
#DIM DYNAMIC アルバム
#DIM DYNAMIC 写真
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 写真名
#DIM DYNAMIC FOUND

;二次元配列にはARRAYREMOVEが使えないので手作業
FOR LCOUNT, 0, 50
	;写真名を渡されたときは写真名で探して消す
	IF 写真名 != ""
		SIF 写真名 == PICS:アルバム:LCOUNT
			FOUND = 1
		SIF !FOUND
			CONTINUE
	ELSE
		SIF LCOUNT < 写真
			CONTINUE
	ENDIF
	IF LCOUNT == 49
		PIC:アルバム:LCOUNT = 0
		PICS:アルバム:LCOUNT =
		BREAK
	ENDIF
	PIC:アルバム:LCOUNT = PIC:アルバム:(LCOUNT+1)
	PICS:アルバム:LCOUNT = %PICS:アルバム:(LCOUNT+1)%
NEXT

@雑誌編集中
#FUNCTION
SIF 出版物:0:発売日 > 0 || 出版物:1:発売日 > 0 || 出版物:2:発売日 > 0
	RETURNF 1

RETURNF 0
