@博物館
#DIM DYNAMIC 改装費用
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 名前文字数
#DIMS DYNAMIC COLORNAME

SIF MU:広さ < 10
	MU:広さ = 10

改装費用 = POWER(MU:広さ, 3)*1000

DRAWLINE
PRINTL □博物館の運営をします
PRINTL 　博物館に展示できるオブジェは、魔法「ジュエリーストーム(レベル5)」やキャラを加工して作成できます
CALL PASELIL, @"　現在の名声:{MU:名声} 昨日の収入:{MU:収入}＄ 現在の所持金:{MONEY}＄"
PRINTL □配属中のスタッフ
IF CMATCH(CSTR:配属, "博物館")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "博物館"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "博物館"
			PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 
			IF !CFLAG:LCOUNT:配属完了
				SETCOLORBYNAME gray
				PRINT 【配属準備中】
			ENDIF
			PRINTFORM 医療技能:{ABL:LCOUNT:医療技能} 
			PRINTFORM 工作技能:{ABL:LCOUNT:工作技能} 
			SIF 統率者("博物館") && !TALENT:LCOUNT:統率
				PRINT 統率者ボーナス+1 
			SIF 特質("ワーカホリック", LCOUNT)
				PRINTFORM [ワーカホリック+{特質("ワーカホリック", LCOUNT)/10}]
			CALL ISTALENT, LCOUNT, "ネクロマンサー"
			CALL ISTALENT, LCOUNT, "潔癖症"
			CALL ISTALENT, LCOUNT, "汚れ無視"
			CALL ISTALENT, LCOUNT, "倒錯的"
			CALL ISTALENT, LCOUNT, "禁断の知識"
			CALL ISTALENT, LCOUNT, "戦士"
			PRINTL 
			RESETCOLOR
		ENDIF
	NEXT
ELSE
	CALL PRINTCOLORL, "　[無し]", "gray"
ENDIF

PRINTL 
PRINTL 　[1] - 作品を展示する
PRINTL 　[2] - 作品を作る・廃棄する
IF MU:広さ >= 30
	PRINTL 　[-] - 博物館の広さは最大です
ELSE
	CALL PASELIL, @"　[3] - 博物館を改装する（現在の広さ:{MU:広さ} 改装費用:{改装費用}＄）"
ENDIF
PRINTL 　[4] - キャラを配属する
PRINTL 　[5] - 客層を確認する
PRINTL 　[9] - 博物館マニュアル
DRAWLINE
PRINTPLAIN 　MUSEUM　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL 博物館_MAP
	CASE 2
		CALL 博物館_CREATE
		;何もしないで戻ってきた場合
		SIF RESULT == 0
			RESTART
		;作品を作ったらターンエンド
		JUMP ターン終了
	CASE 3
		IF MONEY < 改装費用
			PRINTW 改装費用が足りません
			RESTART
		ENDIF
		CALL PAYMENT, -改装費用, "博物館改築"
		MU:広さ++
	CASE 4
		CALL STRATEGY_PERSONAL, "博物館"
	CASE 5
		CALL カテゴリ計算用
		FOR LCOUNT, 0, VARSIZE("博物館客層")
			SIF ERDNAME(博物館客層, LCOUNT) == ""
				BREAK
			PRINTFORM %ERDNAME(博物館客層, LCOUNT), 14%:
			SELECTCASE ERDNAME(博物館客層, LCOUNT)
				CASE "剥製"
					COLORNAME = red
				CASE "白骨死体"
					COLORNAME = ivory
				CASE "ホルマリン漬け"
					COLORNAME = yellowgreen
				CASE "透明標本"
					COLORNAME = cyan
				CASEELSE
					COLORNAME = silver
			ENDSELECT
			HTML_PRINT IMGTOTAG(GBAR(博物館客層:LCOUNT, 100, 10, COLORNAME, "gray", @"{博物館客層:LCOUNT}/100", "black")), 1
			PRINTL 
		NEXT
		WAIT
	CASE 9
		PRINTL □博物館経営について
		PRINTL 　博物館は作成したオブジェを展示して収益を得られる経営要素です
		PRINTL 　レベル5魔法「ジュエリーストーム」や、キャラを物理的に加工して展示することが可能です
		PRINTL 　上記の手段でオブジェ化したキャラは、売却時と同じくロスト扱いになるのでご注意ください
		PRINTL 　オブジェにはそれぞれ集客力のパラメータがあります。長期間同じものを置いていると客が飽きて集客力が下がります
		PRINTL 　展示していないオブジェは徐々に集客力が回復していきます。こまめに展示物を変えましょう
		PRINTL 　オブジェ配置画面で、"実用"を許可することによって、来客数に応じてオブジェが汚されることがありますが、収入への影響はありません
		PRINTL 　また、クローン個体をオブジェ化したものを、オリジナル含め複数体展示した場合、贋作として価値が半減します。注意しましょう
		WAIT
ENDSELECT
RESTART

@博物館_MAP
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 処理数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC XRES
#DIM DYNAMIC YRES
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIMS DYNAMIC HTML用

CALL LOOP_CLEARLINE

VARSET 設置済み
FOR LCOUNTY, 0, MU:広さ
	HTML用 = 
	FOR LCOUNTX, 0, MU:広さ
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			ID = 0
		ENDIF
		IF ID > 0
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", @"%MUSEUM:ID:名前% 価値:%MUSEUM:ID:価値% \@ MUSEUM:ID:種類 == "性具" ? 耐久度:%MUSEUM:ID:耐久度%\% # 集客力:%MUSEUM:ID:集客力%\% \@ \@ MU:実用 ? 汚された回数:%MUSEUM:ID:汚れ% #\@", MUSEUM:ID:色)
			設置済み:ID = 1
		ELSE
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", "未設置")
		ENDIF
	NEXT
	HTML_PRINT HTML用
NEXT

CALL カテゴリ計算用

PRINT 　各種類の割合　
FOR LCOUNT, 0, VARSIZE("博物館客層")
	SIF ERDNAME(博物館客層, LCOUNT) == ""
		BREAK
	PRINTFORM %ERDNAME(博物館客層, LCOUNT)%:{RESULT:LCOUNT}\% 
NEXT
PRINTL 

DRAWLINE
PRINTPLAIN 　MUSEUM MAP　
PRINTFORML [-1] 戻る [-2] 全て片付ける [-3] 実用の許可/不許可(現在:\@ MU:実用 ? 許可 # 不許可 \@)

INPUT

SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		VARSET 博物館マップ
		RESTART
	CASE -3
		INVERTBIT MU:実用, 0
		RESTART
	CASEELSE
		XRES = RESULT/100
		YRES = RESULT%100
		SIF !INRANGE(XRES, 0, MU:広さ-1) || !INRANGE(YRES, 0, MU:広さ-1)
			RESTART
		ID = 博物館マップ:YRES:XRES
		IF ID
			PRINTFORML %MUSEUM:ID:名前%が設置されています。どうしますか？
			PRINTL [0] - 別の物に置き換える
			PRINTL [1] - 片付ける
			PRINTL [2] - 何もしない
			BINPUT
			SELECTCASE RESULT
				CASE 0
					GOTO SELECT_OBJECT
				CASE 1
					博物館マップ:YRES:XRES = 0
			ENDSELECT
			RESTART
		ENDIF
		$SELECT_OBJECT
		処理数 = 0
		PRINTL □何を設置しますか？
		FOR LCOUNT, 1, 1000
			IF MUSEUM:LCOUNT:種類 != "" && !設置済み:LCOUNT
				IF 処理数 >= ページ数*20+20
					次ページあり = 1
					BREAK
				ENDIF
				処理数++
				SIF ページ数*20 >= 処理数
					CONTINUE
				IF MUSEUM:LCOUNT:廃棄済み == "1"
					SETCOLORBYNAME gray
					CALL PASELI, @"　[--] %MUSEUM:LCOUNT:名前% 価値:%MUSEUM:LCOUNT:価値%＄ 廃棄済み \@ MUSEUM:LCOUNT:汚れ != "0" ? 汚された回数:%MUSEUM:LCOUNT:汚れ+" "% #\@"
					PRINT 色:
					CALL PRINTCOLORL, "■", MUSEUM:LCOUNT:色
				ELSE
					CALL PASELI, @"　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前% 価値:%MUSEUM:LCOUNT:価値%＄ \@ MUSEUM:LCOUNT:種類 == "性具" ? 耐久度:%MUSEUM:LCOUNT:耐久度%\% # 集客力:%MUSEUM:LCOUNT:集客力%\% \@ \@ MUSEUM:LCOUNT:汚れ != "0" ? 汚された回数:%MUSEUM:LCOUNT:汚れ +" " % #\@"
					PRINT 色:
					CALL PRINTCOLORL, "■", MUSEUM:LCOUNT:色
				ENDIF
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　OBJECT　
		CALL PRINTSPACE, ページ数, "[-1] 前ページ "
		PRINT [0] 戻る 
		CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "
		PRINTL 
		BINPUT
		SELECTCASE RESULT
			CASE 0
				RESTART
			CASE -1
				ページ数--
				GOTO SELECT_OBJECT
			CASE -9
				ページ数++
				次ページあり = 0
				GOTO SELECT_OBJECT
			CASEELSE
				ID = RESULT
				博物館マップ:YRES:XRES = ID
				RESTART
		ENDSELECT
ENDSELECT

@博物館_CREATE
#DIM DYNAMIC 対象, 2
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC 空き変数カウント
#DIM DYNAMIC 妊娠フラグ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 処理数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIMS DYNAMIC 加工方法
#DIM DYNAMIC 加工技術
#DIMS DYNAMIC 加工者
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC ID
#DIMS DYNAMIC 性器種別

PRINTL □博物館用の作品を作ります 鉱物は魔法コマンドから作成してください
PRINT 　[1] - キャラを
CALL PRINTCOLOR, "加工", "red"
PRINTL する
;PRINTL 　[2] - 既存の作品を組み合わせる
PRINTL 　[3] - 既存の作品を解体する
PRINTL 　[4] - リソースから作成する
PRINTL 　[5] - 作品を破棄する
PRINTL 　[0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL 博物館_CREATE_CORPSE
		SIF RESULT == -1
			RESTART
		RETURN RESULT
	;CASE 2
	;	GOTO JOIN_CORPSE
	CASE 3
		GOTO DESTRUCT_CORPSE
	CASE 4
		GOTO CREATE_CORPSE_BY_RESOURCE
	CASE 5
		GOTO DISPOSE_CORPSE
ENDSELECT

[SKIPSTART]
$JOIN_CORPSE
PRINTL □同じ種類の2つの作品を組み合わせます 何を組み合わせますか？
処理数 = 0
ページ数 = 0
FOR LCOUNT, 1, 1000
	SIF !GROUPMATCH(MUSEUM:LCOUNT:種類, "剥製", "白骨死体", "ホルマリン漬け", "透明標本")
		CONTINUE
	IF 処理数 < ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:種類%
NEXT
[SKIPEND]

$DESTRUCT_CORPSE
PRINTL □作品を解体して展示物にできます 何を解体しますか？
処理数 = 0
FOR LCOUNT, 1, 1000
	;人形の解体は一旦まだ
	SIF !GROUPMATCH(MUSEUM:LCOUNT:種類, "剥製", "白骨死体", "ホルマリン漬け", "透明標本")
		CONTINUE
	SIF MUSEUM:LCOUNT:廃棄済み == "1"
		CONTINUE
	IF 処理数 > ページ数*10+10
		次ページあり = 1
		BREAK
	ENDIF
	IF 処理数 <= ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前%
	処理数++
NEXT
DRAWLINE
PRINTPLAINFORM 　解体　
CALL PRINTSPACE, ページ数, "[-1] 前ページ "
PRINT [0] 戻る 
CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE -1
		ページ数--
		GOTO DESTRUCT_CORPSE
	CASE 0
		RESTART
	CASE -9
		ページ数++
		GOTO DESTRUCT_CORPSE
ENDSELECT

対象 = RESULT

PRINTFORML %MUSEUM:対象:名前%を解体して展示物にします。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT
SIF RESULT == 1
	GOTO DESTRUCT_CORPSE

FOR LCOUNT2, 1, VARSIZE("MUSEUM", 1)
	IF MUSEUM:LCOUNT2:種類 == ""
		空き変数カウント++
		IF 空き変数カウント == 4
			空き変数 = LCOUNT2-3
			BREAK
		ENDIF
	ELSE
		空き変数カウント = 0
	ENDIF
NEXT

STRFIND MUSEUM:対象:名前, "妊娠した"
SIF RESULT != -1 && MUSEUM:対象:種類 != "白骨死体"
	妊娠フラグ = 1

FOR LCOUNT, 0, 4
	MUSEUM:空き変数:種類 = 部位/
	MUSEUM:空き変数:名前 = %CSVNAMEF(TOINT(MUSEUM:対象:キャラナンバー))%
	STRFIND MUSEUM:対象:名前, "(スライム)"
	SIF RESULT != -1
		MUSEUM:空き変数:名前 += "(スライム)"
	MUSEUM:空き変数:名前 += "の"
	SELECTCASE LCOUNT
		CASE 0
			MUSEUM:空き変数:名前 += "頭"
			MUSEUM:空き変数:種類 += "頭"
		CASE 1
			IF 妊娠フラグ
				SELECTCASE MUSEUM:対象:種類
					CASE "透明標本"
						MUSEUM:空き変数:名前 += "胎児が入った"
					CASEELSE
						MUSEUM:空き変数:名前 += "お腹の膨らんだ"
				ENDSELECT
			ENDIF
			MUSEUM:空き変数:名前 += "胴体"
			MUSEUM:空き変数:種類 += "胴体"
		CASE 2
			MUSEUM:空き変数:名前 += "腕"
			MUSEUM:空き変数:種類 += "腕"
		CASE 3
			IF TALENT:GETCHARANAME(CSVNAMEF(TOINT(MUSEUM:対象:キャラナンバー)),1):足使用不可 == 1
				MUSEUM:空き変数:名前 += "尾"
			ELSE
				MUSEUM:空き変数:名前 += "足"
			ENDIF
			MUSEUM:空き変数:種類 += "足"
	ENDSELECT
	SELECTCASE MUSEUM:対象:種類
		CASE "白骨死体"
			MUSEUM:空き変数:名前 += "(白骨)"
		CASE "ホルマリン漬け"
			MUSEUM:空き変数:名前 += "(液浸標本)"
		CASE "透明標本"
			MUSEUM:空き変数:名前 += "(透明標本)"
		CASE "人形"
			MUSEUM:空き変数:名前 += "パーツ"
	ENDSELECT

	IF 妊娠フラグ
		IF LCOUNT == 1
			MUSEUM:空き変数:価値 = {TOINT(MUSEUM:対象:価値)/2}
		ELSE
			;なんだこの変な計算
			MUSEUM:空き変数:価値 = {((TOINT(MUSEUM:対象:価値)/5*4)-TOINT(MUSEUM:対象:価値)/2)/3}
		ENDIF
	ELSE
		MUSEUM:空き変数:価値 = {TOINT(MUSEUM:対象:価値)/5}
	ENDIF
	MUSEUM:空き変数:キャラナンバー = %MUSEUM:対象:キャラナンバー%
	MUSEUM:空き変数:色 = %MUSEUM:対象:色%
	MUSEUM:空き変数:血縁コード = %MUSEUM:対象:血縁コード%
	MUSEUM:空き変数:オリジナルナンバー = 0
	MUSEUM:空き変数:集客力 = 100
	MUSEUM:空き変数:汚れ = 0
	MUSEUM:空き変数:価値減少 = %MUSEUM:対象:価値減少%
	空き変数++
NEXT

PRINTFORMW %MUSEUM:対象:1%を解体しました
FOR LCOUNT, 0, 20
	MUSEUM:対象:LCOUNT '= ""
NEXT

RETURN 1

$CREATE_CORPSE_BY_RESOURCE
PRINTL □リソースから作品を作成できます。何を使いますか？
FOR LCOUNT, 0, VARSIZE("性器リソース", 2)
	SIF ERDNAME(性器リソース, LCOUNT, 2) == ""
		CONTINUE
	PRINTFORML 　[{LCOUNT}] - %ERDNAME(性器リソース, LCOUNT, 2)%
NEXT
DRAWLINE
PRINTPLAIN 　リソース　
PRINTL [10] - 戻る
BINPUT

SIF RESULT == 10
	RESTART

性器種別 = %ERDNAME(性器リソース, RESULT, 2)%

CALL 性器一覧, 性器種別

SIF RESULT == -1
	GOTO CREATE_CORPSE_BY_RESOURCE

対象 = RESULT

PRINTFORM %CSVNAMEF(対象)%の%性器種別%を加工して

SELECTCASE 性器種別
	CASE "男性器"
		PRINT ディルド
	CASE "女性器", "臀部", "乳房"
		PRINT オナホ
ENDSELECT

PRINTL のオブジェクトを作成します。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT
SIF RESULT == 1
	GOTO CREATE_CORPSE_BY_RESOURCE

CALL 性器リソース加工, 対象, 性器種別

RETURN 1

$DISPOSE_CORPSE
PRINTL □作品を廃棄できます 何を廃棄しますか？
処理数 = 0
FOR LCOUNT, 1, 1000
	SIF MUSEUM:LCOUNT:廃棄済み == "1"
		CONTINUE
	SIF MUSEUM:LCOUNT:名前 == ""
		CONTINUE
	IF 処理数 > ページ数*10+10
		次ページあり = 1
		BREAK
	ENDIF
	IF 処理数 <= ページ数*10
		処理数++
		CONTINUE
	ELSE
		PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前%
	ENDIF
	処理数++
NEXT
DRAWLINE
PRINTPLAINFORM 　廃棄　
CALL PRINTSPACE, ページ数, "[-1] 前ページ "
PRINT [0] 戻る 
CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE -1
		ページ数--
		GOTO DISPOSE_CORPSE
	CASE 0
		RESTART
	CASE -9
		ページ数++
		GOTO DISPOSE_CORPSE
ENDSELECT

対象 = RESULT

PRINTFORML %MUSEUM:対象:名前%を廃棄します。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT
SIF RESULT == 1
	GOTO DESTRUCT_CORPSE

PRINTFORMW %MUSEUM:対象:名前%はゴミとして廃棄されました
MUSEUM:対象:廃棄済み = 1

FOR LCOUNTY, 0, 30
	FOR LCOUNTX, 0, 30
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID == 対象
			博物館マップ:LCOUNTY:LCOUNTX = 0
			RESTART
		ENDIF
	NEXT
NEXT

RESTART

@カテゴリ計算用
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 客層計算用, VARSIZE("博物館客層")
#DIM DYNAMIC 総カテゴリ数
#DIM DYNAMIC カテゴリ割合, VARSIZE("博物館客層")
#DIM DYNAMIC ID
#DIM DYNAMIC 総設置数

FOR LCOUNTY, 0, 30
	FOR LCOUNTX, 0, 30
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			CONTINUE
		ENDIF
		IF ID
			SELECTCASE MUSEUM:ID:種類
				CASE "剥製", "白骨死体", "ホルマリン漬け", "透明標本", "人形", "鉱物"
					客層計算用:GETNUM(博物館客層, MUSEUM:ID:種類)++
				CASE "胎児"
					客層計算用:GETNUM(博物館客層, "ホルマリン漬け")++
				CASE "白骨"
					客層計算用:GETNUM(博物館客層, "白骨死体")++
				CASEELSE
					客層計算用:GETNUM(博物館客層, "部位")++
			ENDSELECT
			総設置数++
		ENDIF
	NEXT
NEXT

SIF 総設置数 == 0
	GOTO RETURN

;全体設置数に対する種類の割合で客層を変動させる
FOR LCOUNT, 0, VARSIZE("博物館客層")
	IF ERDNAME(博物館客層, LCOUNT) == ""
		総カテゴリ数 = LCOUNT
		BREAK
	ENDIF
NEXT

FOR LCOUNT, 0, 総カテゴリ数
	カテゴリ割合:LCOUNT = 客層計算用:LCOUNT*100/総設置数
NEXT

$RETURN
RETURN カテゴリ割合:0, カテゴリ割合:1, カテゴリ割合:2, カテゴリ割合:3, カテゴリ割合:4, カテゴリ割合:5, カテゴリ割合:6, カテゴリ割合:7, カテゴリ割合:8, カテゴリ割合:9

@INCOME_博物館
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIM DYNAMIC 総価値
#DIM DYNAMIC 客数
#DIM DYNAMIC 名声上昇量
#DIM DYNAMIC 入場料
#DIM DYNAMIC 集客力平均
#DIM DYNAMIC 対象
#DIM DYNAMIC 総設置数
#DIM DYNAMIC 総カテゴリ数
#DIM DYNAMIC カテゴリ割合, VARSIZE("博物館客層")
#DIM DYNAMIC 価値計算用
#DIM DYNAMIC 手コレクション

SIF !ITEM:博物館
	RETURN

手コレクション = 1
FOR LCOUNTY, 0, 30
	FOR LCOUNTX, 0, 30
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF (ID > 0 && MUSEUM:ID:種類 == "") || MUSEUM:ID:廃棄済み == "1"
			博物館マップ:LCOUNTY:LCOUNTX = 0
			CONTINUE
		ENDIF
		SIF MUSEUM:ID:種類 != "部位/腕"
			手コレクション = 0
		;性具かつ実用不許可だと無視
		SIF MUSEUM:ID:種類 == "性具" && !MU:実用
			CONTINUE
		IF ID
			設置済み:ID = ID
			総価値 += TOINT(MUSEUM:ID:価値)*TOINT(MUSEUM:ID:集客力)/100
			集客力平均 = (集客力平均+TOINT(MUSEUM:ID:集客力))/2
			総設置数++
		ENDIF
	NEXT
NEXT

;全体設置数に対する種類の割合で客層を変動させる
CALL カテゴリ計算用

ARRAYCOPY "RESULT", "カテゴリ割合"
総カテゴリ数 = FINDELEMENT(カテゴリ割合, 0)

FOR LCOUNT, 0, VARSIZE("博物館客層")
	博物館客層:LCOUNT = LIMIT(博物館客層:LCOUNT+(カテゴリ割合:LCOUNT/10)-2, 0, 100)
NEXT

;同じキャラ(クローン含む)を一緒に設置した場合は価値が半減する
FOR LCOUNT, 1, 1000
	IF 設置済み:LCOUNT
		SIF MUSEUM:(設置済み:LCOUNT):種類 == "胎児"
			CONTINUE
		FOR LCOUNT2, 1, 1000
			SIF !設置済み:LCOUNT2
				CONTINUE
			SIF LCOUNT == LCOUNT2
				CONTINUE
			SIF MUSEUM:(設置済み:LCOUNT2):種類 == "胎児"
				CONTINUE
			;双方のクローンフラグが無ければスルー
			SIF MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == "0" && MUSEUM:(設置済み:LCOUNT2):オリジナルナンバー == "0"
				CONTINUE
			IF MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == MUSEUM:(設置済み:LCOUNT2):キャラナンバー || MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == MUSEUM:(設置済み:LCOUNT2):オリジナルナンバー
				IF MUSEUM:(設置済み:LCOUNT):価値減少 == ""
					MUSEUM:(設置済み:LCOUNT):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT):価値)/2}
					MUSEUM:(設置済み:LCOUNT):価値減少 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT):名前%は贋作の発覚により価値が半減しました
				ENDIF
				IF MUSEUM:(設置済み:LCOUNT2):価値減少 == ""
					MUSEUM:(設置済み:LCOUNT2):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT2):価値)/2}
					MUSEUM:(設置済み:LCOUNT2):価値減少 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT2):名前%は贋作の発覚により価値が半減しました
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT

;総価値0=何も設置してなければ計算しない
SIF 総価値 == 0
	GOTO STAFF

;入場料は価値の平均+設置数*10 10未満切り上げ
入場料 = 総価値/総設置数
入場料 += 総設置数*10

入場料 = 入場料/10*10

;総価値/100or名声+広さ が一日の来客数上限
客数 = MAX(総価値/100, MU:名声+MU:広さ)

;カテゴリ割合と客層に応じて増やす カテゴリ特化かつ客層ポイントが最大(100)で100人増える
FOR LCOUNT, 0, 総カテゴリ数
	客数 += カテゴリ割合:LCOUNT*博物館客層:LCOUNT/100
NEXT

;さらに集客力平均で下げる
客数 = 客数*集客力平均/100
;そこからさらにRANDでふるいにかける
客数 = RAND:(客数+1)

;客数だけ名声も上がる 一日に上がる名声はMU:広さと同じ値まで
名声上昇量 = MIN(客数, MU:広さ)
;ただし客数以上の名声にはならない
SIF MU:名声+名声上昇量 > 客数
	名声上昇量 = MAX(客数-MU:名声, 0)

MU:名声 += 名声上昇量

MU:収入 = 入場料*客数
CALL PAYMENT, MU:収入, "博物館収入"

IF 客数 == 0
	PRINTL 博物館には1人も客が来ませんでした
ELSE
	CALL PASELIL, @"博物館には{客数}人の客が訪れ、一人あたり入場料{入場料}＄で合計{入場料*客数}＄の収入がありました"
ENDIF
SIF 名声上昇量 > 0
	PRINTFORML 博物館の名声が{名声上昇量}上がりました
SIF 手コレクション && 総設置数 >= 5
	CALL 実績解除, "下品なんですが……フフ……"

;集客力の変動
FOR LCOUNT, 1, 1000
	;設置中なら3下げる 最低1
	IF 設置済み:LCOUNT
		;性具は集客力の代わりに耐久度を下げる
		IF MUSEUM:LCOUNT:種類 == "性具"
			;ただし客が来てなければ下げない
			IF 客数
				;耐久度が100(MAX)なら価値を半減させる
				IF MUSEUM:LCOUNT:耐久度 == "100"
					MUSEUM:LCOUNT:価値 = {TOINT(MUSEUM:LCOUNT:価値)/2}
				;それ以外なら10%ずつ下げていく
				ELSE
					価値計算用 = TOINT(MUSEUM:LCOUNT:価値)
					TIMES 価値計算用, 0.90
					MUSEUM:LCOUNT:価値 = {MAX(価値計算用, 1)}
				ENDIF
				MUSEUM:LCOUNT:耐久度 = {MAX(TOINT(MUSEUM:LCOUNT:耐久度)-3, 1)}
				;耐久度に応じて名前を変える
				MUSEUM:LCOUNT:名前 = %ONAHONAME(MUSEUM:LCOUNT:ベースリソース, CSVNAMEF(TOINT(MUSEUM:LCOUNT:キャラナンバー)), TOINT(MUSEUM:LCOUNT:耐久度))%
				;ついでに汚しておく
				MUSEUM:LCOUNT:汚れ = {TOINT(MUSEUM:LCOUNT:汚れ)+MAX(RAND:客数, 1)}
			ENDIF
		ELSE
			MUSEUM:LCOUNT:集客力 = {MAX(TOINT(MUSEUM:LCOUNT:集客力)-3, 1)}
		ENDIF
	;設置してないやつは10+加工技術合計上げる 最高150
	ELSE
		;ただし性具は耐久度を見るので回復しない
		SIF MUSEUM:LCOUNT:種類 == "性具"
			CONTINUE
		MUSEUM:LCOUNT:集客力 = {MIN(TOINT(MUSEUM:LCOUNT:集客力)+10+加工技術合計(), 150)}
	ENDIF
NEXT

;実用可能なら以下の処理
IF MU:実用
	;並びを適当にバラす
	FOR LCOUNT, 999, 1, -1
		SWAP 設置済み:LCOUNT, 設置済み:(RAND(1, LCOUNT))
	NEXT
	REPEAT 客数
		;ランダムで対象を決める
		対象 = RAND(1, 1000)
		WHILE !設置済み:対象
			対象++
			SIF 対象 == 1000
				対象 = 1
		WEND
		;0～2回汚される
		MUSEUM:(設置済み:対象):汚れ = {TOINT(MUSEUM:(設置済み:対象):汚れ)+RAND:2}
		SIF TOINT(MUSEUM:(設置済み:対象):汚れ) >= 1000
			CALL 実績解除, "作品には手を触れないでください"
		;DEBUGPRINTFORML %MUSEUM:(設置済み:対象):名前%+
	REND
ENDIF

$STAFF
;配属スタッフの処理
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "博物館"
		CONTINUE
	IF !CFLAG:LCOUNT:配属完了
		CFLAG:LCOUNT:配属完了 = 1
		CONTINUE
	ENDIF
	;狂気度が上がる ネクロマンサーor隷属なら上がらない
	IF !TALENT:LCOUNT:ネクロマンサー && !TALENT:LCOUNT:隷属 && !TALENT:LCOUNT:崩壊
		FOR LCOUNT2, 0, 1000
			SIF !設置済み:LCOUNT2
				CONTINUE
			CFLAG:LCOUNT:狂気度 += 3
			IF RELATION:LCOUNT:TOINT(MUSEUM:(設置済み:LCOUNT2):キャラナンバー) > 100 && MUSEUM:(設置済み:LCOUNT2):種類 != "胎児"
				CFLAG:LCOUNT:狂気度 += (RELATION:LCOUNT:TOINT(MUSEUM:(設置済み:LCOUNT2):キャラナンバー)-100)/20+1
				DEBUGPRINTFORML %CALLNAME:LCOUNT%が%MUSEUM:(設置済み:LCOUNT2):名前%を掃除
				IF CFLAG:LCOUNT:狂気度 >= 100 && !TALENT:LCOUNT:狂気
					PRINTFORML %CALLNAME:LCOUNT%は博物館に飾られた%MUSEUM:(設置済み:LCOUNT2):名前%の手入れ作業で気が狂ってしまったようだ……
					TALENT:LCOUNT:狂気 = 1
				ENDIF
			ENDIF
			IF CFLAG:LCOUNT:狂気度 >= 100 && !TALENT:LCOUNT:狂気
				PRINTFORML %CALLNAME:LCOUNT%は博物館の手入れ作業で気が狂ってしまったようだ……
				TALENT:LCOUNT:狂気 = 1
			ENDIF
		NEXT
	ENDIF
NEXT

