@STRATEGY_GC
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 定休日設定
#DIM DYNAMIC LRESULT

CALL TUTORIAL, "経営_GC_1"

CALL GC用DT作成

CALL GCバージョン管理
DRAWLINE
PRINTL □現在の規模
SIF GCS:店名 != "ゲームセンター"
	PRINTFORML 　店名：%GCS:店名%
PRINTFORM 　物件：%物件名(GC:物件)%
PRINTFORML 　名声：%GC名声グレード(GC:名声)%({GC:名声})

PRINTFORM 　評判：%GC評判グレード(GC:評判)%({GC:評判})
PRINTFORML 　メンテナンス：%GCメンテグレード(GC:メンテ)%

;CALL PASELIL, @"　前日の収入：%GC収入グレード(GC:前日の収入)%({GC:前日の収入}＄)"
CALL PASELIL, @"　前日の収入：{GC:前日の収入}＄"

PRINTFORML 　店長：%NAME:LIMIT(FINDCHARA(CFLAG:ゲーセン店長, 1), 0, CHARANUM)%　定休日：\@ GCS:定休日 == "" ? 無し # %GCS:定休日%曜日 \@
PRINTL 

HTML_PRINT SET_TOOLTIP("　[ 1] - 物件購入", "物件を購入します")
IF !GC:物件
	HTML_PRINT SET_TOOLTIP("　[--] - 物件増築", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[--] - 筐体購入", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[--] - 筐体設置", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[--] - 店名変更", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[--] - 通常営業", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[--] - 定休日設定", "物件を購入してください", 0, "gray")
ELSE
	HTML_PRINT SET_TOOLTIP("　[ 2] - 物件増築", "物件を増築、改装します")
	HTML_PRINT SET_TOOLTIP("　[ 3] - 筐体購入", "新しいアーケードゲーム筐体を購入します")
	HTML_PRINT SET_TOOLTIP("　[ 4] - 人員配置", "ゲームセンターに人員を配置します")
	HTML_PRINT SET_TOOLTIP("　[ 5] - 筐体設置", "ゲームセンターにアーケードゲーム筐体を設置します")
	HTML_PRINT SET_TOOLTIP("　[ 6] - 店長任命", "ゲームセンターの人員から店長を任命します")
	HTML_PRINT SET_TOOLTIP("　[ 7] - 店名変更", "ゲームセンターの店名を変更します")
	HTML_PRINT SET_TOOLTIP("　[ 8] - 定休日設定", "ゲームセンターの定休日を決めます")
	IF GC:営業中 > 0
		HTML_PRINT SET_TOOLTIP("　[ 9] - 一時休業", "ゲームセンターを一時休業し、収入と支出が行われないようにします")
	ELSE
		HTML_PRINT SET_TOOLTIP("　[ 9] - 通常営業", "ゲームセンターの営業を再開し、収入と支出が行われるようにします")
	ENDIF
	SIF FINDCHARA(CSTR:配属, "ゲーセン売春") >= 0
		HTML_PRINT SET_TOOLTIP("　[10] - 売春方針", "ゲームセンターで売春させるキャラに方針を命じます")
	HTML_PRINT SET_TOOLTIP("　[11] - 売上詳細", "日毎の筐体別売上を表示します")
ENDIF

DRAWLINE
PRINTPLAIN 　GAME CENTER　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

CALL TUTORIAL

BINPUT
SELECTCASE RESULT
	CASE 0
		CALL TUTORIAL, "経営_GC_6"
		RETURN 0
	CASE 1
		CALL GC_PROPERTY
	CASE 2
		CALL GC_RENOVATION
	CASE 3
		CALL GC_ARCADEGAME
	CASE 4
		PRINTL 配属する部署を選んでください
		PRINTL [0] - 経営部(通常のゲームセンター勤務)
		PRINTL [1] - 売春部(ゲームセンターで客を誘惑する)
		PRINTL [100] - 戻る
		BINPUT
		SELECTCASE RESULT
			CASE 0
				CALL STRATEGY_PERSONAL, "ゲームセンター"
			CASE 1
				CALL STRATEGY_PERSONAL, "ゲーセン売春"
			CASE 100
				RESTART
		ENDSELECT
	CASE 5
		CALL TUTORIAL, "経営_GC_4"
		CALL GC_MAP, 1
	CASE 6
		CALL GC_LEADER
	CASE 7
		PRINT □店名を入力してください
		SIF GCS:店名 != "ゲームセンター"
			PRINTFORM  現在の店名：%GCS:店名%
		PRINTL 
		PRINTBUTTON "[変更しない]", "変更しない"
		PRINTL 
		INPUTS
		SIF RESULTS == "変更しない"
			RESTART
		SIF RESULTS == ""
			RESULTS = ゲームセンター
		PRINTFORMW 店名を「%RESULTS%」に変更しました
		GCS:店名 = %RESULTS%
	CASE 8
		VARLINE = 0
		DO
			CALL LOOP_CLEARLINE
			定休日設定 = 
			DRAWLINE
			PRINTL □定休日を選んでください
			CALL PRINTCOLORL, @"　[1] - 日曜日（\@ GC定休日("日") ? 定休 # 営業 \@）", @"\@ GC定休日("日") ? gray #\@"
			CALL PRINTCOLORL, @"　[2] - 月曜日（\@ GC定休日("月") ? 定休 # 営業 \@）", @"\@ GC定休日("月") ? gray #\@"
			CALL PRINTCOLORL, @"　[3] - 火曜日（\@ GC定休日("火") ? 定休 # 営業 \@）", @"\@ GC定休日("火") ? gray #\@"
			CALL PRINTCOLORL, @"　[4] - 水曜日（\@ GC定休日("水") ? 定休 # 営業 \@）", @"\@ GC定休日("水") ? gray #\@"
			CALL PRINTCOLORL, @"　[5] - 木曜日（\@ GC定休日("木") ? 定休 # 営業 \@）", @"\@ GC定休日("木") ? gray #\@"
			CALL PRINTCOLORL, @"　[6] - 金曜日（\@ GC定休日("金") ? 定休 # 営業 \@）", @"\@ GC定休日("金") ? gray #\@"
			CALL PRINTCOLORL, @"　[0] - 土曜日（\@ GC定休日("土") ? 定休 # 営業 \@）", @"\@ GC定休日("土") ? gray #\@"
			PRINTL 　[100] - 設定終了
			BINPUT
			LRESULT = RESULT
			SIF RESULT == 100
				BREAK
			FOR LCOUNT, 0, 7
				IF LRESULT == LCOUNT
					IF !GC定休日(DOW(LRESULT))
						GCS:定休日 += @"・%DOW(LRESULT)%"
					ELSE
						CONTINUE
					ENDIF
				ENDIF
				SIF GC定休日(DOW(LCOUNT))
					定休日設定 += @"・%DOW(LCOUNT)%"
			NEXT
			GCS:定休日 = %SUBSTRINGU(定休日設定, 1)%
		LOOP 1
	CASE 9
		IF GC:営業中 > 0
			PRINTFORMW %GCS:店名%の経営を一時中断しました
			;定休日じゃない休業は-1
			GC:営業中 = -1
		ELSE
			PRINTFORMW %GCS:店名%の経営を開始しました
			GC:営業中 = DAY
		ENDIF
	CASE 10
		DO
			DRAWLINE
			PRINTL □方針を変更するキャラを選んでください
			FOR LCOUNT, 0, CHARANUM
				SIF CSTR:LCOUNT:配属 != "ゲーセン売春"
					CONTINUE
				PRINTFORM 　[{LCOUNT, 3}] - %NAME:LCOUNT% 現在の方針:
				SELECTCASE CFLAG:LCOUNT:ゲーセン売春方針
					CASE 0
						PRINTL ホール待機
					CASE 1
						PRINTL ホール待機(清潔)
					CASE 2
						PRINTL トイレ待機
					CASE 3
						PRINTL お持ち帰り待機
				ENDSELECT
			NEXT
			PRINTL 　[  0] - 戻る
			BINPUT
			SIF RESULT == 0
				RESTART
			PRINTFORML □%CALLNAME:RESULT%の売春方針を指定してください
			LRESULT = RESULT
			HTML_PRINT SET_TOOLTIP("　[0] - ホールで待機させる", "ホールで客引きをさせます")
			HTML_PRINT SET_TOOLTIP("　[1] - ホールで待機させる(清潔)", "ホールで客引きをさせますが、従業員や他の客にばれないように設備を汚させないようにします")
			IF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
				HTML_PRINT SET_TOOLTIP("　[-] - トイレで待機させる", "[淫乱][娼婦]、もしくは50人以上との性経験が必要です", 0, "gray")
			ELSE
				HTML_PRINT SET_TOOLTIP("　[2] - トイレで待機させる", "トイレで肉便器になるように命じます")
			ENDIF
			;IF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
			;	HTML_PRINT SET_TOOLTIP("　[-] - お持ち帰り待機", "[淫乱][娼婦]、もしくは50人以上との性経験が必要です", 0, "gray")
			;ELSEIF !TALENT:LRESULT:音ゲーマー
			;	HTML_PRINT SET_TOOLTIP("　[-] - お持ち帰り待機", "ある程度、客との共通の会話ができるスキル[音ゲーマー]が必要です", 0, "gray")
			;ELSE
			;	HTML_PRINT SET_TOOLTIP("　[3] - お持ち帰り待機", "客と一夜を共にさせます")
			;ENDIF

			BINPUT

			CFLAG:LRESULT:ゲーセン売春方針 = RESULT
		LOOP 1
	CASE 11
		CALL GC_売上詳細
	CASE 100
		CALL GC_TUTORIAL
	CASEELSE
		CONTINUE
ENDSELECT
RESTART

;----------------------------------------------------
;ゲームセンター経営モードチュートリアル
;----------------------------------------------------
@GC_TUTORIAL
DRAWLINE
PRINTL 当モードはゲームセンターを経営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [ 1] - 「物件購入」とは？
PRINTL [ 2] - 「物件増築」とは？
PRINTL [ 3] - 「筐体購入」とは？
PRINTL [ 4] - 「人員配置」とは？
PRINTL [ 5] - 「筐体設置」とは？
PRINTL [ 6] - 「店長任命」とは
PRINTL [ 7] - 「店名変更」とは
PRINTL [ 8] - 「定休日設定」とは？
PRINTL [ 9] - 「一時休業/通常営業」とは？
PRINTL [10] - 「売春方針」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		PRINTL 「物件購入」は筐体を置く物件を購入するコマンドです。
		PRINTL これが無いと話にならないので、まずはボロ屋でもいいので購入してみましょう。
		PRINTL 物件は大きく「所有物件」と「テナント」に分けられます。
		PRINTL 「所有物件」は物件購入の際の経費が高く広さや階層に応じた経費が掛かりますが、増築が可能です。
		PRINTW 「テナント」はその逆で、増築はできませんが物件購入の経費が安く建物に関する経費は一定です。また、所有物件よりも収入の安定性が高いのがメリットです。
	CASE 2
		PRINTL 「物件増築」は購入した物件を増築し、筐体を置けるスペースを広げたり、設備を向上させるコマンドです
		PRINTL 物件には「広さ」「分煙施設」「階層」「外装」「フードコート」「トイレ」の設備があり、それぞれ改築することにより様々な面での経済効果がアップします
		PRINTL 分煙施設とフードコートは一部の物件には初期から備えられており、有無で判断するので改築は一回で済みます
		PRINTL 他の施設はいくらでも増築できるので、その分経済効果も尻上がりになります。
		PRINTW しかし増築すればするほど、増築に必要な経費も上がり運営費も上昇するので、計画的な増築が必要になるでしょう
	CASE 3
		PRINTW 「筐体購入」はゲーセンに設置するための筐体を購入するコマンドです。
		;PRINTW 当バージョンでは少なく、これからも増やして行く予定……ですが予定は未定。
	CASE 4
		PRINTL 「人員配置」はゲーセン経営に人員を配置するコマンドです。
		PRINTL 性格や適性、能力に応じて名声や評判、メンテナンスレベルから収入まで様々な要素に影響があります。
		PRINTW 特に序盤は名声を上げることが大事なので、配属できるキャラは配属してみるのがいいでしょう。
	CASE 5
		PRINTL 「筐体設置」は所有する物件に筐体を設置するコマンドです。
		PRINTL 筐体は所持するだけなら経費はかかりませんが、設置した当日から経費がかかるので所持金とよく相談して決めましょう。
		PRINTW なお、筐体にはそれぞれ大きさがあり、経済効果の高い筐体ほど大きくなるのでスペースには気を使いましょう。
	CASE 6
		PRINTL 「店長任命」は配属してるキャラの中から店長を任命するコマンドです。
		PRINTL 店長に任命されたキャラはゲーセンの各ステータス・レベルへの影響が大きくなり、より多くの経験が得られるようになります。
		PRINTW 優秀な人材や、積極的に能力を伸ばしていきたいキャラを任命することにより、効率的な運営が可能になります。
	CASE 7
		PRINTL 「店名変更」はゲームセンターの店名を変更するコマンドです。
		PRINTW デフォルトのままでも変更しても収入や名声等への影響はありません。自分の好きな名前を付けましょう。
	CASE 8
		PRINTL 「定休日設定」はゲームセンターの定休曜日を設定するコマンドです。
		PRINTL 配属したキャラたちは能力にもよりますがストレスが溜まりやすくなります。
		PRINTW 適度に休みを設け、キャラたちのメンタルケアを行えばより効率的な運営ができるでしょう。
	CASE 9
		PRINTL 「一時休業/通常営業」はゲームセンターの営業を停止したり再開するコマンドです。
		PRINTW 配属したキャラクターの体調管理などで長期休暇が必要な場合、回復するまで営業を停止するのも良いでしょう。
	CASE 10
		PRINTL 「売春方針」は売春部に配属したキャラクターの売春方針を指定するコマンドです。
		PRINTL 激しく淫らな方針のほうが稼げる額も多くなります。いろいろ試してみましょう。
		PRINTW また、現在ではキャラと客間の妊娠については非対応です。
ENDSELECT

RESTART

;物件購入処理----------------------------------------------------
@GC_PROPERTY
#DIM DYNAMIC 総スコア
#DIM DYNAMIC LRESULT

総スコア = GC:名声+GC:評判
DRAWLINE
PRINTL 【物件購入】
;PRINTL 名声や評判によって買える物件も変わります
IF !ORIGIN
	PRINT 「物件名」(金額 - 日毎の基本維持費) 
	CALL PASELIL, @"所持金：{MONEY}＄"
ENDIF
PRINTL 
CALL PASELIL, @"　[1] - ボロ屋\@ !ORIGIN ? (300000＄ - 3000＄～)#\@"
CALL PASELIL, @"　[2] - 中小スーパーのテナント\@ !ORIGIN ? (50000＄ - 10000＄～)#\@"
CALL PASELIL, @"　[3] - 商店街の一角\@ !ORIGIN ? (1000000＄ - 12000＄～)#\@"
CALL PASELIL, @"　[4] - 駅中テナント\@ !ORIGIN ? (200000＄ - 30000＄～)#\@"
CALL PASELIL, @"　[5] - 街中物件\@ !ORIGIN ? (10000000＄ - 30000＄～)#\@"
CALL PASELIL, @"　[6] - 百貨店テナント\@ !ORIGIN ? (1000000＄ - 100000＄～)#\@"
CALL PASELIL, @"　[7] - アミューズメント施設\@ !ORIGIN ? (300000000＄ - 50000＄～)#\@"
DRAWLINE
PRINTPLAIN 　PROPERTY　
PRINTL [0] - 戻る　

BINPUT
LRESULT = RESULT
SELECTCASE LRESULT
	CASE 0
		RETURN 0
	CASE 1
		PRINTL 【ボロ屋】
		PRINTL 
		PRINTL その名の通りボロっちい建物。
		PRINTL 整備すればゲーセンとして使えるかもしれないが……
		PRINTL 狭いため筐体もあまり置けず、高い経済効果は期待できない。
	CASE 2
		PRINTL 【中小スーパーのテナント】
		PRINTL 
		PRINTL 時々スーパーの片隅に申し訳程度の筐体が置いてあるあのスペース。
		PRINTL ゲーム目的で来る人はほとんど居ないと思うが、お買い物に飽きた子どもたちがよく訪れる。
		PRINTL ボロ屋と比べると経済効果はだいぶマシではあるが、こちらはテナントなので維持費が高い。
		PRINTL 他にも改装ができないなど、使い勝手の悪い部分がある。
	CASE 3
		PRINTL 【商店街の一角】
		PRINTL 
		PRINTL 商店街といっても昔ながらの風情のあるような場所ではなく、ちょっとした飲食店やオシャレな店や並ぶいわゆる「アーケード街」
		PRINTL 改築の自由度もそこそこ高く、周囲にもいろいろな店が並ぶので、お客さんが多い。
		PRINTL 維持費も安く、初期設備も平均的なため、経営に慣れてきたらここを使ってみるのもいいだろう。
	CASE 4
		PRINTL 【駅中テナント】
		PRINTL 
		PRINTL 都会の駅中にあるスペースを間借りしたもの。
		PRINTL 最低限の設備は整っており、全体的に水準以上の環境でである。
		PRINTL イニシャルコストは安いが、増築もできずランニングコストも結構かかるため、計画的な経営ができないと破産する可能性も高い。注意しよう。
	CASE 5
		PRINTL 【街中物件】
		PRINTL 
		PRINTL 環境、土地共に優れていて、人通りも多い街中の物件。
		PRINTL 特徴としては分煙施設は無いが、ここは二階建てなので広さの倍の筐体が置ける。
		PRINTL 増築もできるので、買うことさえできればかなり使い勝手のいい物件である。
	CASE 6
		PRINTL 【百貨店テナント】
		PRINTL 
		PRINTL 百貨店内に間借りする物件。
		PRINTL 経済効果が非常に高いが、その分経費も馬鹿にならないため、店を構える際はよく考えよう。
		PRINTL こちらは街中物件とは違い、一階しか無く、増築も行えないが、フードコートと分煙施設は付いている。
	CASE 7
		PRINTL 【アミューズメント】
		PRINTL 
		PRINTL 当バージョンでは最高ランクの施設。
		PRINTL 初期コストが3億、基本経費が50000ととんでもないコストがかかるが、経済効果もコスト同等のものだと思っていいだろう。
		PRINTL 初期から二階建て、所有物件唯一の分煙設備、他高水準の設備もあり、フードコートもある。
		PRINTL 増築も自由にできるため、経営での物件は最終的にここに行き着くだろう。
ENDSELECT

IF MONEY < 物件価格(LRESULT) && !ORIGIN
	PRINTW 所持金が足りません
	RESTART
ELSEIF GC:物件 == LRESULT
	PRINTW 現在所持している物件です
	RESTART
ENDIF

IF !ORIGIN
	PRINTL 所持金変動
	CALL PASELIL, @"{MONEY}＄ > {MONEY - 物件価格(LRESULT)}＄"
	PRINTL 
ENDIF
PRINTL 購入しますか？
PRINTL [0] - 購入しない
PRINTL [1] - 購入する

BINPUT
SIF RESULT == 0
	RESTART

IF GC:物件
	PRINTFORML 現在%物件名(GC:物件)%を所持しています。本当に買い替えますか？
	PRINTFORML （編集した%物件名(GC:物件)%のマップは初期化されます）
	PRINTL [0] - それは困る
	PRINTL [1] - 大丈夫だ、問題ない
	BINPUT
	SIF RESULT == 0
		RETURN
	VARSET GCオブジェクト
	CALL 設置済み筐体計算
ELSE
	GCS:店名 = ゲームセンター
ENDIF

CALL TUTORIAL, "経営_GC_2"

PRINTFORMW ＜%物件名(LRESULT)%を購入しました＞
MONEY -= 物件価格(LRESULT)
CALL 現金出納帳記録, @"「%物件名(LRESULT)%」購入/-{物件価格(LRESULT)}"
IF パートナー != ""
	SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
		TRYCALLFORM BUY_OBJECT_%CSVNAMERPS(GETNO(パートナー))%, LRESULT
ENDIF
GC:営業中 = DAY

;初期値設定-------------------
;物件情報--------------
GC:物件 = LRESULT

;広さ初期値------------
GC:広さ = LRESULT*2

;分煙施設--------------
SELECTCASE LRESULT
	;テナントとアミューズメントには分煙施設が付く
	CASE 2, 4, 6, 7
		GC:分煙 = 1
ENDSELECT

;階層初期値------------
GC:階層 = 1
空きスペース:1 = GC:広さ
;街中とアミューズメントは2階建て
SELECTCASE LRESULT
	CASE 5, 7
		GC:階層 = 2
		空きスペース:2 = GC:広さ
ENDSELECT

;外装初期値------------
GC:外装 = LRESULT/2+1

;フードコート----------
SELECTCASE LRESULT
	;百貨店とアミューズメントにはフードコートが付く
	CASE 6, 7
		GC:フードコート = 1
ENDSELECT

;お手洗い--------------
GC:トイレ = LRESULT/2+1

RETURN

;物件増築処理----------------------------------------------------
@GC_RENOVATION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 費用, 7

DRAWLINE
PRINTL 【物件増築】
PRINTL 物件を増築できます。
PRINTL スペースを広げたり設備を強化すると、マップが広がったり評判が上がったりします。
DRAWLINE
PRINTFORMLC 　物件：%物件名(GC:物件)%
CALL PASELIL, @"所持金：{MONEY}＄"
PRINTFORMLC 　分煙施設：\@ GC:分煙 ? 有 # 無 \@
PRINTFORMLC 階層：{GC:階層}階建
PRINTFORMLC 広さ：{GC:広さ}
PRINTL 

PRINTFORMLC 　外装レベル：{GC:外装}
PRINTFORMLC フードコート：\@ GC:フードコート ? 有 # 無 \@
PRINTFORMLC お手洗いレベル：{GC:トイレ}
PRINTL 

DRAWLINE

;広さは最大で20まで
費用:1 = (GC:広さ*GC:広さ)*30000+50000
費用:2 = 100000
;階層は100まで
費用:3 = (GC:階層*GC:階層)*10000000
;外装も100まで
費用:4 = GC:外装*100000
費用:5 = 200000
;トイレは10まで
費用:6 = GC:トイレ*2000+5000

SELECTCASE GC:物件
	CASE 2, 4, 6
		PRINTL 
		PRINTL 【増築できません】
		PRINTL 
	CASEELSE
		IF GC:広さ >= 20
			CALL PRINTCOLORL, "　[-] - 広さは最大です", "gray"
		ELSE
			CALL PASELIL, @"　[1] - 広くする\@ !ORIGIN ? ({費用:1}＄)#\@"
		ENDIF
		SIF !GC:分煙
			CALL PASELIL, @"　[2] - 分煙施設を作る\@ !ORIGIN ? ({費用:2}＄)#\@"
		IF GC:階層 >= 100
			CALL PRINTCOLORL, "　[-] - 階層は最大です", "gray"
		ELSE
			CALL PASELIL, @"　[3] - 階層を増やす\@ !ORIGIN ? ({費用:3}＄)#\@"
		ENDIF
		IF GC:外装 >= 100
			CALL PRINTCOLORL, "　[-] - 外装は最大です", "gray"
		ELSE
			CALL PASELIL, @"　[4] - 外装強化\@ !ORIGIN ? ({費用:4}＄)#\@"
		ENDIF
		SIF !GC:フードコート
			CALL PASELIL, @"　[5] - フードコートを作る\@ !ORIGIN ? ({費用:5}＄)#\@"
		IF GC:トイレ >= 10
			CALL PRINTCOLORL, "　[-] - お手洗いレベルは最大です", "gray"
		ELSE
			CALL PASELIL, @"　[6] - お手洗い改装\@ !ORIGIN ? ({費用:6}＄)#\@"
		ENDIF
ENDSELECT
DRAWLINE
PRINTPLAIN 　RENOVATION　
PRINTL [0] 戻る
;PRINTL [100] マニュアル

BINPUT
SIF RESULT == 0
	RETURN

IF MONEY < 費用:RESULT && !ORIGIN
	PRINTW 所持金が足りません
	RESTART
ENDIF

SELECTCASE RESULT
	CASE 1
		PRINTFORMW %物件名(GC:物件)%を改築し、広くしました
		GC:広さ++
		FOR LCOUNT, 1, GC:階層+1
			空きスペース:LCOUNT++
		NEXT
	CASE 2
		PRINTFORMW 分煙施設を設置しました
		GC:分煙 = 1
	CASE 3
		PRINTFORMW {GC:階層+1}階建に増築しました
		GC:階層++
		空きスペース:(GC:階層) = GC:広さ
	CASE 4
		PRINTFORMW 外装を強化しました
		GC:外装++
	CASE 5
		PRINTFORMW フードコート施設を設置しました
		GC:フードコート = 1
	CASE 6
		PRINTFORMW トイレを改装しました
		GC:トイレ++
ENDSELECT
MONEY -= 費用:RESULT
RESULT = LRESULT
CALL 現金出納帳記録, @"ゲームセンター改築/-{費用:RESULT}"
IF パートナー != ""
	SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
		TRYCALLFORM GC_RENOVATION_%CSVNAMERPS(GETNO(パートナー))%, LRESULT
ENDIF

RESTART

;筐体購入処理----------------------------------------------------
@GC_ARCADEGAME
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ
#DIM DYNAMIC 購入筐体
#DIM DYNAMIC 購入数
#DIMS DYNAMIC 筐体名表示用
#DIM DYNAMIC 最長文字数
#DIM DYNAMIC 最高所持数
#DIM DYNAMIC 表示用桁

DRAWLINE
PRINTL 【筐体購入】
PRINTL アーケードゲームを購入できます
PRINTL これが無いとゲーセン経営は話になりません
CALL PRINT_AC_ITEM
PRINTL 　[0] - 音ゲー・アーケードゲーム
PRINTL 　[1] - メダルゲーム
;PRINTL [3] - プライズゲーム
PRINTL 　[3] - 両替機・券売機・その他
DRAWLINE
PRINTPLAIN 　ARCADE　
PRINTL [100] - 戻る

BINPUT

ページ = RESULT
DO
	SELECTCASE ページ
		CASE 100
			RETURN
		CASEELSE
			DRAWLINE
			SIF !ORIGIN
				CALL PASELIL, @"所持金:{MONEY}＄"
			最長文字数 = 0
			最高所持数 = 0
			表示用桁 = 0
			FOR LCOUNT, ページ*100, ページ*100+100
				IF ERDNAME(筐体, LCOUNT) != ""
					筐体名表示用 = %ERDNAME(筐体, LCOUNT)%
					SIF !ORIGIN
						筐体名表示用 '= 筐体名表示用 + "(" + @"{筐体価格(LCOUNT)}" + "＄)"
					CALL SETMAX, 最長文字数, STRLENS(筐体名表示用)
					CALL SETMAX, 最高所持数, 筐体:LCOUNT
				ENDIF
			NEXT
			DO
				表示用桁++
				最高所持数 /= 10
				SIF 最高所持数 == 0
					BREAK
			LOOP 1

			PRINTL □購入する筐体を選んでください
			CALL 設置済み筐体計算
			FOR LCOUNT, ページ*100, ページ*100+100
				IF ERDNAME(筐体, LCOUNT) != ""
					筐体名表示用 = %ERDNAME(筐体, LCOUNT)%
					SIF !ORIGIN
						筐体名表示用 '= 筐体名表示用 + "(" + @"{筐体価格(LCOUNT)}" + "＄)"
					CALL PASELIL, @"　[{LCOUNT, 3}] - %筐体名表示用, 最長文字数+1, LEFT% 設置/所持:{設置済み筐体:LCOUNT, 表示用桁}/{筐体:LCOUNT, 表示用桁}"
				ENDIF
			NEXT
			DRAWLINE
			PRINTPLAIN 　ARCADE　
			PRINTL [1000] 戻る

			BINPUT
			SELECTCASE RESULT
				CASE 1000
					RESTART
				CASEELSE
					IF MONEY < 筐体価格(RESULT) && !ORIGIN
						PRINTW 所持金が足りません
						CONTINUE
					ELSE
						購入筐体 = RESULT
						PRINTFORML %ERDNAME(筐体, 購入筐体)%の購入数を選択してください
						FOR LCOUNT, 1, 13
							IF MONEY < 筐体価格(購入筐体)*LCOUNT && !ORIGIN
								SETCOLORBYNAME gray
								PRINTPLAINFORM [{LCOUNT, 2}]
								RESETCOLOR
							ELSE
								PRINTFORM [{LCOUNT, 2}]
							ENDIF
							SIF LCOUNT%6 == 0
								PRINTL 
						NEXT
						PRINTL [-1] やめる
						BINPUT
						SELECTCASE RESULT
							CASE -1
								CONTINUE
							CASEELSE
								購入数 = RESULT
								CALL TUTORIAL, "経営_GC_3"
								PRINTFORMW ＜%ERDNAME(筐体, 購入筐体)%を{購入数}台購入しました＞
								筐体:購入筐体 += 購入数
								MONEY -= 筐体価格(購入筐体)*購入数
								CALL 現金出納帳記録, @"筐体「%ERDNAME(筐体, 購入筐体)%」×{購入数}購入/-{筐体価格(購入筐体)*購入数}"
								IF パートナー != ""
									SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
										TRYCALLFORM BUY_ARCADEGAME_%CSVNAMERPS(GETNO(パートナー))%, 購入筐体
								ENDIF
								CONTINUE
						ENDSELECT
					ENDIF
			ENDSELECT
	ENDSELECT
LOOP 1

;店長の任命----------------------------------------------------
@GC_LEADER
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 現店長

現店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
SIF 現店長 == -1
	現店長 = 0

DRAWLINE

PRINT □店長に任命するキャラを選んでください(現在の店長:
IF 現店長
	PRINTFORML %NAME:現店長%)
ELSE
	PRINTL 不在)
ENDIF
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		PRINTFORM 　[{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT%(家事技能:{ABL:LCOUNT:家事技能, 2, LEFT} 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} 料理技能:{ABL:LCOUNT:料理技能, 2, LEFT}
		SIF TALENT:LCOUNT:料理人
			PRINT  料理人
		SIF TALENT:LCOUNT:工学博士
			PRINT  工学博士
		SIF TALENT:LCOUNT:音ゲーマー
			PRINT  音ゲーマー
		PRINTL )
	ENDIF
NEXT

DRAWLINE
PRINTPLAIN 　LEADER　
PRINTL [0] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASEELSE
		PRINTFORMW %NAME:RESULT%を店長に任命しました
		CFLAG:現店長:ゲーセン店長 = 0
		CFLAG:RESULT:ゲーセン店長 = 1
		SIF OPTION:口上表示 && !CFLAG:RESULT:口上非表示
			TRYCALLFORM GC_ASSIGNLEADER_%CSVNAMERPS(NOF(RESULT))%, RESULT, 現店長
		RESTART
ENDSELECT

;----------------------------------------------------
;所持筐体表示処理
;----------------------------------------------------
@PRINT_AC_ITEM
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 筐体所持数
#DIM DYNAMIC その他所持数
#DIM DYNAMIC 表示可能文字数
#DIMS DYNAMIC 表示用

CALL CHECKDRAWLINE
表示可能文字数 = GETCONFIG("ウィンドウ幅")*2/(GETCONFIG("フォントサイズ")+1)

PRINTL □ゲーム筐体
PRINT 　
FOR LCOUNT, 0, 200
	SIF ERDNAME(筐体, LCOUNT) == ""
		CONTINUE
	SIF 筐体:LCOUNT == 0
		CONTINUE
	IF STRLENS(表示用)+STRLENS(@"[%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]") >= 表示可能文字数
		PRINTFORML %表示用%
		PRINT 　
		表示用 = [%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]
	ELSE
		表示用 += @"[%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]"
	ENDIF
	筐体所持数++
NEXT
	SIF 表示用 != ""
		PRINTFORML %表示用%
	SIF !筐体所持数
		PRINTFORML [所持筐体無し]

表示用 = 
PRINTL □両替機・自販機・椅子等
PRINT 　
FOR LCOUNT, 300, 400
	SIF ERDNAME(筐体, LCOUNT) == ""
		CONTINUE
	SIF 筐体:LCOUNT == 0
		CONTINUE
	IF STRLENS(表示用)+STRLENS(@"[%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]") >= 表示可能文字数
		PRINTFORML %表示用%
		PRINT 　
		表示用 = [%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]
	ELSE
		表示用 += @"[%ERDNAME(筐体, LCOUNT)%×{筐体:LCOUNT}]"
	ENDIF
	その他所持数++
NEXT
	SIF 表示用 != ""
		PRINTFORM %表示用%
	SIF !その他所持数
		PRINTFORM [所持筐体無し]

CALL NEWLINE
DRAWLINE

;----------------------------------------------------
;設置済み筐体の確認
;最大で9万回ループのため重くなりそうなら別の処理を考える
;----------------------------------------------------
@設置済み筐体計算, レイアウトロード:0, レイアウトロード:1
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 選択階層
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM DYNAMIC 筐体NO
#DIM DYNAMIC レイアウトロード, 2

VARSET 設置済み筐体
VARSET 階層別設置済み筐体

;各階層、各マップに設置してある筐体数を計算
;オブジェクトナンバーはそれぞれ上下左右の向きで4つ分ある
;それと1からカウントしてるので式は (オブジェクトナンバー-1)/4
FOR 選択階層, 1, GC:階層+1
	;レイアウトロードフラグが立ってる場合はそのレイアウトで計算する 階層0→1に置き換え
	IF レイアウトロード:0 == 選択階層
		FOR X座標, 0, 10+GC:広さ
			FOR Y座標, 0, 10+GC:広さ
				IF GCオブジェクト:(レイアウトロード:1):X座標:Y座標
					筐体NO = (GCオブジェクト:(レイアウトロード:1):X座標:Y座標-1)/4
					階層別設置済み筐体:選択階層:筐体NO++
				ENDIF
			NEXT
		NEXT
	ELSE
		FOR X座標, 0, 10+GC:広さ
			FOR Y座標, 0, 10+GC:広さ
				IF GCオブジェクト:選択階層:X座標:Y座標
					筐体NO = (GCオブジェクト:選択階層:X座標:Y座標-1)/4
					階層別設置済み筐体:選択階層:筐体NO++
				ENDIF
			NEXT
		NEXT
	ENDIF
NEXT

FOR LCOUNT, 1, GC:階層+1
	FOR LCOUNT2, 0, VARSIZE("設置済み筐体")
		設置済み筐体:LCOUNT2 += 階層別設置済み筐体:LCOUNT:LCOUNT2
	NEXT
NEXT

@GC_売上詳細
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ = -1
#DIMS DYNAMIC 列名, 200
#DIM DYNAMIC DTID

DTID = DT_ROW_LENGTH("GC_HISTORY")+ページ

DRAWLINE
IF DT_ROW_LENGTH("GC_HISTORY") <= 0
	PRINTL 
	PRINTL ---記録無し---
	PRINTL 
ELSE
	CALL 日数逆算, ページ
	PRINTFORML □%GC_売上日付(DTID)%の売上詳細
	FOR LCOUNT, 0, DT_COLUMN_NAMES("GC_HISTORY", 列名)
		SELECTCASE 列名:LCOUNT
			CASE "客数", "総売上", "支出", "日付", "id"
				CONTINUE
		ENDSELECT
		SIF GC_HISTORY(DTID, 列名:LCOUNT) == 0
			CONTINUE
		CALL PASELIL, @"　%列名:LCOUNT%:{GC_HISTORY(DTID, 列名:LCOUNT)}＄"
	NEXT
	CALL PASELIL, @"　来客数:{GC_HISTORY(DTID, "客数")} 総売上:{GC_HISTORY(DTID, "総売上")}＄ 支出:{GC_HISTORY(DTID, "支出")}＄"
ENDIF
DRAWLINE
PRINTPLAIN 　GC売上　
CALL PRINTSPACE, DT_ROW_LENGTH("GC_HISTORY") > ABS(ページ), "[1] - 前日の売上 "
PRINT [0] - 戻る 
CALL PRINTSPACE, ページ < -1, "[9] - 翌日の売上"
PRINTL 

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		ページ--
	CASE 9
		ページ++
ENDSELECT
RESTART

;----------------------------------------------------
;ゲームセンター経営での収入を計算。日付変更時に呼び出されます
;----------------------------------------------------
@INCOME_GC
#DIM DYNAMIC 収入
#DIM DYNAMIC 名声上昇値
#DIM DYNAMIC 評判上昇値
#DIM DYNAMIC 支出
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LTARGET
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC 設置済み筐体数
#DIM DYNAMIC 最高期待値
#DIM DYNAMIC 加算値
#DIM DYNAMIC メンテ加算値
#DIM DYNAMIC 来客数
#DIM DYNAMIC 来客確率
#DIM DYNAMIC バッファ終端
#DIMS DYNAMIC SWAP用バッファ
#DIMS DYNAMIC SPLITVAR, 2
#DIM DYNAMIC 注文率
#DIM DYNAMIC フードコート収入
#DIM DYNAMIC 給料

CALL 設置済み筐体計算

DT_ROW_ADD "GC_HISTORY"

SIF !GC:物件
	RETURN 0

;来客数を計算する
;良い物件ほど最低来客数保証
来客数 = 3*GC:物件
;テナントかそうでないかで計算式が違う
IF GCテナント()
	;休日は客が多くなる
	SELECTCASE DOW(FLAG:曜日)
		CASE "土"
			来客確率 = 50
		CASE "日"
			来客確率 = 60
		CASEELSE
			来客確率 = 40
	ENDSELECT
ELSE
	;休日は客が多くなる
	SELECTCASE DOW(FLAG:曜日)
		CASE "土"
			来客確率 = 20
		CASE "日"
			来客確率 = 30
		CASEELSE
			来客確率 = 10
	ENDSELECT
ENDIF

;ここに評判/100の補正 最大-30%～+30%
来客確率 += GC:評判/100
;普通に計算すると毎回同じになるので揺らぎを与える
FOR LCOUNT, 0, GC:名声/10
	SIF RANDPER(来客確率)
		来客数++
NEXT

;収入バッファを取得
VARSET 収入バッファ
FOR LCOUNT, 1, GC:階層+1
	CALL 階層収入バッファ, LCOUNT
NEXT

;来客数だけ収入バッファの抽選をする
[SKIPSTART]
FOR LCOUNT, 6, -1, -1
	FOR LCOUNT2, 0, VARSIZE("GC売上", 2)
		IF LCOUNT == 0
			GC売上:LCOUNT:LCOUNT2 = 0
		ELSE
			GC売上:LCOUNT:LCOUNT2 = GC売上:(LCOUNT-1):LCOUNT2
		ENDIF
	NEXT
NEXT
[SKIPEND]

;バッファの使用量を取得してSWAPかける処理は手動のほうが軽い
FOR LCOUNT, 0, VARSIZE("収入バッファ")
	IF 収入バッファ:LCOUNT == ""
		バッファ終端 = LCOUNT
		BREAK
	ENDIF
NEXT
FOR LCOUNT, 0, バッファ終端-1
	SWAP用バッファ = %収入バッファ:LCOUNT%
	収入バッファ:LCOUNT = %収入バッファ:RAND(LCOUNT+1, バッファ終端)%
	収入バッファ:RAND(LCOUNT+1, バッファ終端) = %SWAP用バッファ%
NEXT

;一人あたりのバッファ消費量はメンテと評判で決まる 粘着するほどメンテ良いか、長居するほど治安が良いかというイメージ 最大100
FOR LCOUNT, 0, 来客数*(GC:メンテ/3+1)*MAX(GC:評判/100, 1)
	SIF バッファ終端 == LCOUNT
		BREAK
	SPLIT 収入バッファ:LCOUNT, "/", SPLITVAR
	収入 += TOINT(SPLITVAR:0)
	CALL GC_CELL_PLUS, @"%SPLITVAR:1%売上", TOINT(SPLITVAR:0)
	;GC売上:0:(SPLITVAR:1) += TOINT(SPLITVAR:0)
NEXT

;フードコートレベルに応じて追加収入
IF フードコートレベル()
	;レベルに応じて注文率上がる 10～80
	注文率 = LIMIT(フードコートレベル()*4, 10, 80)
	FOR LCOUNT, 0, 来客数
		IF RANDPER(注文率)
			;(レベル*5±20)*10パセリの売上
			フードコート収入 += (フードコートレベル()*5+RAND(-20, 21))*10
		ENDIF
	NEXT
	;最低100保証
	CALL SETMAX(フードコート収入, 100)
	収入 += フードコート収入
	CALL GC_CELL_PLUS, "フードコート売上", フードコート収入
	;GC売上:0:フードコート = フードコート収入
ENDIF


[SKIPSTART]
;収入計算--------------------------------------------------------
;良い物件ほど最低額保証
収入 = 300 * GC:物件
FOR LCOUNT, 1, GC:階層+1
	;一番高い期待値を調べておく
	SIF 最高期待値 < 階層収入期待値(LCOUNT)
		最高期待値 = 階層収入期待値(LCOUNT)
	;期待値--------------------------------------
	SELECTCASE GC:物件
		;テナント店なら階層ごとの期待値をそのまま加算
		CASE 2, 4, 6
			収入 += 階層収入期待値(LCOUNT)
		;それ以外ならRAND:階層ごとの収入期待値を加算
		CASEELSE
			;RAND:期待値で出す 外装レベル%の収入が保証される
			加算値 = RAND:階層収入期待値(LCOUNT)
			CALL SETMAX, 加算値, 階層収入期待値(LCOUNT)*GC:外装/100
			収入 += 加算値
	ENDSELECT
NEXT
;分煙補正-------------------------------------
;収入には影響なし

;外装補正-------------------------------------
;上で計算済み

;フードコート補正-------------------------
;レベルに応じて支出を抑えられるのでフードコートによる収入は保留
;LOCAL:2 = 0
;IF GC:フードコート
;	;階層の数だけ期待値をロール 一番高い値を加算
;	REPEAT GC:階層
;		LOCAL:1 = RAND:フードコート収入期待値()
;		SIF LOCAL:2 < LOCAL:1
;			LOCAL:2 = LOCAL:1
;	REND
;ENDIF
;収入 += LOCAL:2

;人事補正---------------------------------
収入 += GC収入人事補正()

;プレミアムフライデー---------------------------------
SIF PREMIUM_FRIDAY(YEAR, MONTH, DAYS)
	TIMES 収入, 1.10

;収入処理--------------------------------------
[SKIPEND]
CALL PASELIL, @"%GCS:店名%の収入が{収入}＄入りました"
バンめし = 0
MONEY += 収入

DT_CELL_SET "GC_HISTORY", DT_ROW_LENGTH("GC_HISTORY")-1, "日付", @"{YEAR}年{MONTH}月{DAYS}日(%DOW(FLAG:曜日)%)"
CALL GC_CELL_PLUS, "客数", 来客数
CALL GC_CELL_PLUS, "総売上", 収入


;名声計算--------------------------------------------------------
;テナント施設だと名声が上がりやすい
SIF GCテナント()
	名声上昇値 += GC:物件

名声上昇値 += GC:メンテ/10

;筐体補正--------------------------------------
;単純に筐体数が多ければそれだけで名声が上がる
FOR LCOUNT, 0, 400
	IF 設置済み筐体:LCOUNT
		名声上昇値 += 名声筐体補正(LCOUNT)*設置済み筐体:LCOUNT
		設置済み筐体数++
	ENDIF
NEXT

;お手洗い補正---------------------------------
;レベルに応じて最大筐体設置数と同じまで上がる
;名声 += MIN(GC:トイレ, 設置済み筐体数)

;広さ補正-------------------------------------
;名声 += GC:広さ

;分煙補正-------------------------------------
;SIF GC:分煙
;	名声 += GC:物件*3

;階層・外装補正-------------------------------------
名声上昇値 += GC:外装

;フードコート補正-------------------------
;名声 += フードコートレベル()

;お手洗い補正---------------------------------
;名声 += GC:トイレ*GC:物件/10

;人事補正---------------------------------
名声上昇値 += 名声人事補正()

;名声処理--------------------------------------
PRINTFORML %GCS:店名%の名声が{名声上昇値}上がりました
GC:名声 += 名声上昇値
;上限は10000
GC:名声 = LIMIT(GC:名声, 0, 10000)


;評判計算--------------------------------------------------------
;評判 += GC:物件*3

評判上昇値 += 評判人事補正()

;飯がマズイと評判下がったりする
SIF GC:フードコート
	評判上昇値 += (フードコートレベル()-5)

;評判 += GC:メンテ/5

CALL 評判筐体補正
評判上昇値 += RESULT

;お手洗い補正---------------------------------
評判上昇値 += GC:トイレ*GC:物件

;分煙補正-------------------------------------
IF GC:分煙
	SELECTCASE 評判上昇値
		CASE IS >= 0
			TIMES 評判上昇値 , 1.20
		CASEELSE
			TIMES 評判上昇値 , 0.80
	ENDSELECT
ENDIF

;評判処理--------------------------------------
SELECTCASE 評判上昇値
	CASE IS > 0
		PRINTFORML %GCS:店名%の評判が{評判上昇値}上がりました
	CASE 0
		PRINTFORML %GCS:店名%の評判に変化はありませんでした
	CASE IS < 0
		PRINTFORML %GCS:店名%の評判が{ABS(評判上昇値)}下がりました
ENDSELECT
GC:評判 += 評判上昇値
;-3000～3000未満にはならない
GC:評判 = LIMIT(GC:評判, -3000, 3000)

;支出計算--------------------------------------------------------
;要調整
SELECTCASE GC:物件
	CASE 1
		支出 = 3000
	CASE 2
		支出 = 10000
	CASE 3
		支出 = 12000
	CASE 4
		支出 = 30000
	CASE 5
		支出 = 2000
	CASE 6
		支出 = 100000
	CASE 7
		支出 = 50000
ENDSELECT

;筐体補正--------------------------------------
FOR LCOUNT, 0, 400
	支出 += 筐体支出補正(LCOUNT)*設置済み筐体:LCOUNT
NEXT

;広さ補正-------------------------------------
SIF !GCテナント()
	支出 += GC:広さ*500

;階層補正-------------------------------------
SIF !GCテナント()
	支出 += GC:階層*GC:階層*10000

;フードコート補正-------------------------
;フードコートレベルに応じて支出を抑えられる
;SIF GC:フードコート
;	支出 = (支出/100)*(100-フードコートレベル())

;人件費---------------------------------
;奴隷にも給料はちゃんと払う
;サボり魔はサボっても給料はちゃんと貰う
FOR LCOUNT, 0, CHARANUM
	;全体の収入の1% 店長は2% 最低500パセリ保証
	給料 = MAX(収入/100, 500)
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		IF CFLAG:LCOUNT:ゲーセン店長
			支出 += 給料*2
			;浪費癖には倍額払う
			SIF TALENT:LCOUNT:浪費癖
				支出 += 給料*2
		ELSE
			支出 += 給料
			;浪費癖には倍額払う
			SIF TALENT:LCOUNT:浪費癖
				支出 += 給料
		ENDIF
	ENDIF
NEXT

;支出処理--------------------------------------
CALL PASELIL, @"%GCS:店名%の経費に{支出}＄かかりました"
MONEY -= 支出
GC:前日の収入 = 収入-支出
CALL GC_CELL_PLUS, "支出", 支出

CALL 現金出納帳記録, @"ゲームセンター収支/{GC:前日の収入}"

;人事補正計算後に配属中のキャラを配属完了にする
FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		IF OPTION:口上表示 && !CFLAG:LCOUNT:口上非表示
			LTARGET = TARGET
			TARGET = LCOUNT
			TRYCALLFORM GC_TURNEND_%CSVNAMERPS(NOF(LCOUNT))%, 収入, 支出
			TARGET = LTARGET
		ENDIF
		CFLAG:LCOUNT:配属完了 = 1
	ENDIF
NEXT

;モチベーションを計算する
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "ゲームセンター"
		CONTINUE
	モチベ計算用 = 0
	FOR LCOUNT2, 0, VARSIZE("TALENT")
		SIF !TALENT:LCOUNT:LCOUNT2
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT2
			CASE "恋慕", "服従"
				モチベ計算用++
			CASE "親愛", "隷属"
				モチベ計算用 += 2
			CASE "素直", "冷静", "強気", "好奇心", "目立ちたがり", "恥薄い", "献身的", "潔癖症", "音ゲーマー"
				モチベ計算用++
			CASE "臆病", "プライド高い", "大人しい", "無関心", "恥じらい", "汚臭敏感", "男嫌い", "女嫌い"
				モチベ計算用 -= 2
		ENDSELECT
	NEXT
	加算値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数("ゲームセンター")
	SIF TALENT:LCOUNT:世話好き
		加算値 -= 世話好き人数("ゲームセンター")
	;配属先のキャラの相性で計算
	加算値 += 相性モチベ補正(LCOUNT)
	CFLAG:LCOUNT:モチベーション = LIMIT(加算値, 1, 100)
NEXT

;最後に現状のメンテレベルを計算
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "ゲームセンター"
		CONTINUE
	IF CFLAG:LCOUNT:サボり
		PRINTFORML %CALLNAME:LCOUNT%はゲームセンターの仕事をサボりました
		CFLAG:LCOUNT:ストレス値 -= 5
		CFLAG:LCOUNT:サボり = 0
		CONTINUE
	ENDIF
	IF FINDCHARA(CFLAG:ゲーセン店長, 1) >= 0 && TALENT:FINDCHARA(CFLAG:ゲーセン店長, 1):統率 && !TALENT:LCOUNT:統率
		加算値 = ABL:LCOUNT:家事技能+2
		加算値 += ABL:LCOUNT:工作技能+2
	ELSEIF 統率者("ゲームセンター") && !TALENT:LCOUNT:統率
		加算値 = ABL:LCOUNT:家事技能+1
		加算値 += ABL:LCOUNT:工作技能+1
	ELSE
		加算値 = ABL:LCOUNT:家事技能
		加算値 += ABL:LCOUNT:工作技能
	ENDIF
	SIF TALENT:LCOUNT:音ゲーマー
		加算値 += 5
	SIF TALENT:LCOUNT:工学博士
		加算値 += 5
	IF CFLAG:LCOUNT:ゲーセン店長
		SIF 加算値 > 20
			加算値 = 20
	ELSE
		加算値 /= 2
		SIF 加算値 > 10
			加算値 = 10
	ENDIF
	メンテ加算値 += 加算値
	;ついでに配属中のキャラには実務経験を加算
	IF CFLAG:LCOUNT:ゲーセン店長
		EXP:LCOUNT:実務経験 += 5
		EXP:LCOUNT:工作経験 += 3
		EXP:LCOUNT:家事経験 += 3
		SIF GC:フードコート
			EXP:LCOUNT:料理経験 += 3
		;JUEL:LCOUNT:習得 += CFLAG:LCOUNT:モチベーション*20
	ELSE
		EXP:LCOUNT:実務経験++
		EXP:LCOUNT:工作経験++
		EXP:LCOUNT:家事経験++
		SIF GC:フードコート
			EXP:LCOUNT:料理経験++
		;JUEL:LCOUNT:習得 += CFLAG:LCOUNT:モチベーション*10
	ENDIF
	;人気度も加算
	人気度:(NOF(LCOUNT)) += 2
NEXT

;店長を2倍で計上して最高値110なので10/11に圧縮(最高値100)
GC:メンテ = メンテ加算値*10/11
GC:メンテ = LIMIT(GC:メンテ, 0, 100)

CALL TUTORIAL, "経営_GC_7"
CALL TUTORIAL

@GCバージョン管理
