@博物館
#DIM DYNAMIC 改装費用

SIF MU:広さ < 10
	MU:広さ = 10

改装費用 = POWER(MU:広さ, 3)*1000

DRAWLINE
PRINTL □博物館の運営をします
PRINTL 　博物館に展示できるオブジェは、魔法「ジュエリーストーム(レベル5)」やキャラを加工して作成できます
CALL PASELIL, @"　現在の名声:{MU:名声} 昨日の収入:{MU:収入}＄ 現在の所持金:{MONEY}＄"
PRINTL 
PRINTL 　[1] - 作品を展示する
PRINTL 　[2] - 作品を作る
IF MU:広さ >= 30
	PRINTL 　[-] - 博物館の広さは最大です
ELSE
	CALL PASELIL, @"　[3] - 博物館を改装する（現在の広さ:{MU:広さ} 改装費用:{改装費用}＄）"
ENDIF
PRINTL 　[9] - 博物館マニュアル
DRAWLINE
PRINTPLAIN 　MUSEUM　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL 博物館_MAP
	CASE 2
		CALL 博物館_CREATE
		;何もしないで戻ってきた場合
		SIF RESULT == 0
			RESTART
		;作品を作ったらターンエンド
		JUMP ターン終了
	CASE 3
		IF MONEY < 改装費用
			PRINTW 改装費用が足りません
			RESTART
		ENDIF
		MONEY -= 改装費用
		CALL 現金出納帳記録, @"博物館改築/-{改装費用}"
		MU:広さ++
	CASE 9
		CALL MANUAL_博物館
ENDSELECT
RESTART

@博物館_MAP
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 処理数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC XRES
#DIM DYNAMIC YRES
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIMS DYNAMIC HTML用

CALL LOOP_CLEARLINE

VARSET 設置済み
FOR LCOUNTY, 0, MU:広さ
	HTML用 = 
	FOR LCOUNTX, 0, MU:広さ
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			ID = 0
		ENDIF
		IF ID > 0
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", @"%MUSEUM:ID:名前% 価値:%MUSEUM:ID:価値% 集客力:%MUSEUM:ID:集客力%\% \@ MU:実用 ? 汚された回数:%MUSEUM:ID:汚れ% #\@", MUSEUM:ID:色)
			設置済み:ID = 1
		ELSE
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", "未設置")
		ENDIF
	NEXT
	HTML_PRINT HTML用
NEXT
DRAWLINE
PRINTPLAIN 　MUSEUM MAP　
PRINTFORML [-1] 戻る [-2] 全て片付ける [-3] 実用の許可/不許可(現在:\@ MU:実用 ? 許可 # 不許可 \@)

INPUT

SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		VARSET 博物館マップ
		RESTART
	CASE -3
		INVERTBIT MU:実用, 0
		RESTART
	CASEELSE
		XRES = RESULT/100
		YRES = RESULT%100
		SIF !INRANGE(XRES, 0, MU:広さ-1) || !INRANGE(YRES, 0, MU:広さ-1)
			RESTART
		ID = 博物館マップ:YRES:XRES
		IF ID
			PRINTFORML %MUSEUM:ID:名前%が設置されています。どうしますか？
			PRINTL [0] - 別の物に置き換える
			PRINTL [1] - 片付ける
			PRINTL [2] - 何もしない
			BINPUT
			SELECTCASE RESULT
				CASE 0
					GOTO SELECT_OBJECT
				CASE 1
					博物館マップ:YRES:XRES = 0
			ENDSELECT
			RESTART
		ENDIF
		$SELECT_OBJECT
		処理数 = 0
		PRINTL □何を設置しますか？
		FOR LCOUNT, 1, 1000
			IF MUSEUM:LCOUNT:種類 != "" && !設置済み:LCOUNT
				IF 処理数 >= ページ数*20+20
					次ページあり = 1
					BREAK
				ENDIF
				処理数++
				SIF ページ数*20 >= 処理数
					CONTINUE
				CALL PASELI, @"　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前% 価値:%MUSEUM:LCOUNT:価値%＄ 集客力:%MUSEUM:LCOUNT:集客力%\% "
				PRINT 色:
				CALL PRINTCOLORL, "■", MUSEUM:LCOUNT:色
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　OBJECT　
		CALL PRINTSPACE, ページ数, "[-1] 前ページ "
		PRINT [0] 戻る 
		CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "
		PRINTL 
		BINPUT
		SELECTCASE RESULT
			CASE 0
				RESTART
			CASE -1
				ページ数--
				GOTO SELECT_OBJECT
			CASE -9
				ページ数++
				次ページあり = 0
				GOTO SELECT_OBJECT
			CASEELSE
				ID = RESULT
				博物館マップ:YRES:XRES = ID
				RESTART
		ENDSELECT
ENDSELECT

@博物館_CREATE
#DIM DYNAMIC 対象, 2
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC 空き変数カウント
#DIM DYNAMIC 妊娠フラグ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 処理数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり

;GOTO CREATE_CORPSE

PRINTL □博物館用の作品を作ります 鉱物は魔法コマンドから作成してください
PRINT 　[1] - キャラを
CALL PRINTCOLOR, "加工", "red"
PRINTL する
;PRINTL 　[2] - 既存の作品を組み合わせる
PRINTL 　[3] - 既存の作品を解体する
PRINTL 　[0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		GOTO CREATE_CORPSE
	CASE 2
		GOTO JOIN_CORPSE
	CASE 3
		GOTO DESTRUCT_CORPSE
ENDSELECT


$CREATE_CORPSE
PRINT □加工するキャラを選んでください。
FONTBOLD
PRINTL 加工したキャラは死亡します
FONTREGULAR
FOR PAGING, 1, CHARANUM
	;DISABLEチェック
	DISABLE = 0
	IF OPTION:口上表示 && !CFLAG:PAGING:口上非表示
		TRYCALLFORM DISABLE_作品加工_%CSVNAMERPS(NOF(PAGING))%
		SIF RESULT
			DISABLE = 1
	ENDIF
	CALL PAGING, PAGING > 0 && !TALENT:PAGING:霊体 && !DISABLE
NEXT

SIF RESULT == -1
	RETURN 0

対象 = RESULT

PRINTFORML %NAME:対象%を加工します。どのように加工しますか？
PRINTL [0] - やっぱりやめる
SIF (!TALENT:対象:無機物 && CSTR:対象:種族 != "機械") && !ISSLIME(対象)
	PRINTL [1] - 剥製に加工する
SIF (!TALENT:対象:無機物 && CSTR:対象:種族 != "機械") && !ISSLIME(対象)
	PRINTL [2] - 白骨に加工する
SIF !TALENT:対象:無機物 && CSTR:対象:種族 != "機械"
	PRINTL [3] - ホルマリン漬けに加工する
SIF (!TALENT:対象:無機物 && CSTR:対象:種族 != "機械") && !ISSLIME(対象)
	PRINTL [4] - 透明標本に加工する
SIF TALENT:対象:無機物 || CSTR:対象:種族 == "機械"
	PRINTL [10] - 稼働を停止させる
BINPUT
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		SETCOLORBYNAME red
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL 剥製加工, 対象
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 剥製に加工された
		CALL CLEAR_CHARA, 対象, 0, 1
		RETURN 1
	CASE 2
		SETCOLORBYNAME ivory
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL 白骨加工, 対象
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 白骨に加工された
		CALL CLEAR_CHARA, 対象, 0, 1
		RETURN 1
	CASE 3
		SETCOLORBYNAME yellowgreen
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL ホルマリン加工, 対象
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = ホルマリン漬けに加工された
		CALL CLEAR_CHARA, 対象, 0, 1
		RETURN 1
	CASE 4
		SETCOLORBYNAME cyan
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL 透明標本加工, 対象
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 透明標本に加工された
		CALL CLEAR_CHARA, 対象, 0, 1
		RETURN 1
	CASE 10
		SETCOLORBYNAME gray
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL 稼働停止, 対象
		CSTR:対象:ロスト要因 = 死亡
		CSTR:対象:ロスト詳細 = 永久に稼働を停止した
		CALL CLEAR_CHARA, 対象, 0, 1
		RETURN 1
ENDSELECT

$JOIN_CORPSE
PRINTL □同じ種類の2つの作品を組み合わせます 何を組み合わせますか？
処理数 = 0
ページ数 = 0
FOR LCOUNT, 1, 1000
	SIF !GROUPMATCH(MUSEUM:LCOUNT:種類, "剥製", "白骨死体", "ホルマリン漬け", "透明標本")
		CONTINUE
	IF 処理数 < ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:種類%
NEXT
	
$DESTRUCT_CORPSE
PRINTL □作品を解体して展示物にできます 何を解体しますか？
処理数 = 0
FOR LCOUNT, 1, 1000
	;人形の解体は一旦まだ
	SIF !GROUPMATCH(MUSEUM:LCOUNT:種類, "剥製", "白骨死体", "ホルマリン漬け", "透明標本")
		CONTINUE
	IF 処理数 > ページ数*10+10
		次ページあり = 1
		BREAK
	ENDIF
	IF 処理数 < ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前%
	処理数++
NEXT
DRAWLINE
PRINTPLAINFORM 　解体　
CALL PRINTSPACE, ページ数, "[-1] 前ページ "
PRINT [0] 戻る
CALL PRINTSPACE, ページ数, "[-9] 次ページ "
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE -1
		ページ数--
		GOTO DESTRUCT_CORPSE
	CASE 0
		RESTART
	CASE -9
		ページ数++
		GOTO DESTRUCT_CORPSE
ENDSELECT

対象 = RESULT

PRINTFORML %MUSEUM:対象:名前%を解体して展示物にします。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

BINPUT
SIF RESULT == 1
	GOTO DESTRUCT_CORPSE

FOR LCOUNT2, 1, VARSIZE("MUSEUM", 1)
	IF MUSEUM:LCOUNT2:種類 == ""
		空き変数カウント++
		IF 空き変数カウント == 4
			空き変数 = LCOUNT2-3
			BREAK
		ENDIF
	ELSE
		空き変数カウント = 0
	ENDIF
NEXT

STRFIND MUSEUM:対象:名前, "妊娠した"
SIF RESULT != -1 && MUSEUM:対象:種類 != "白骨死体"
	妊娠フラグ = 1

FOR LCOUNT, 0, 4
	MUSEUM:空き変数:種類 = 部位
	MUSEUM:空き変数:名前 = %CSVNAMEF(TOINT(MUSEUM:対象:キャラナンバー))%
	SIF ISSLIME(GETCHARANAME(CSVNAMEF(TOINT(MUSEUM:対象:キャラナンバー)), 1))
		MUSEUM:空き変数:名前 += "スライム"
	MUSEUM:空き変数:名前 += "の"
	SELECTCASE LCOUNT
		CASE 0
			MUSEUM:空き変数:名前 += "頭"
		CASE 1
			IF 妊娠フラグ
				SELECTCASE MUSEUM:対象:種類
					CASE "透明標本"
						MUSEUM:空き変数:名前 += "胎児が入った"
					CASEELSE
						MUSEUM:空き変数:名前 += "お腹の膨らんだ"
				ENDSELECT
			ENDIF
			MUSEUM:空き変数:名前 += "胴体"
		CASE 2
			MUSEUM:空き変数:名前 += "腕"
		CASE 3
			MUSEUM:空き変数:名前 += "足"
	ENDSELECT
	SIF MUSEUM:対象:種類 == "白骨死体"
		MUSEUM:空き変数:名前 += "(白骨)"
	SIF MUSEUM:対象:種類 == "ホルマリン漬け"
		MUSEUM:空き変数:名前 += "(液浸標本)"
	SIF MUSEUM:対象:種類 == "透明標本"
		MUSEUM:空き変数:名前 += "(透明標本)"
	SIF MUSEUM:対象:種類 == "人形"
		MUSEUM:空き変数:名前 += "パーツ"

	IF 妊娠フラグ
		IF LCOUNT == 1
			MUSEUM:空き変数:価値 = {TOINT(MUSEUM:対象:価値)/2}
		ELSE
			;なんだこの変な計算
			MUSEUM:空き変数:価値 = {((TOINT(MUSEUM:対象:価値)/5*4)-TOINT(MUSEUM:対象:価値)/2)/3}
		ENDIF
	ELSE
		MUSEUM:空き変数:価値 = {TOINT(MUSEUM:対象:価値)/5}
	ENDIF
	MUSEUM:空き変数:キャラナンバー = %MUSEUM:対象:キャラナンバー%
	MUSEUM:空き変数:色 = %MUSEUM:対象:色%
	MUSEUM:空き変数:血縁コード = %MUSEUM:対象:血縁コード%
	MUSEUM:空き変数:オリジナルナンバー = 0
	MUSEUM:空き変数:集客力 = 100
	MUSEUM:空き変数:汚れ = 0
	MUSEUM:空き変数:価値減少 = %MUSEUM:対象:価値減少%
	空き変数++
NEXT

PRINTFORMW %MUSEUM:対象:1%を解体しました
FOR LCOUNT, 0, 10
	MUSEUM:対象:LCOUNT '= ""
NEXT

RETURN 1

@INCOME_博物館
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIM DYNAMIC 総価値
#DIM DYNAMIC 客数
#DIM DYNAMIC 名声上昇量
#DIM DYNAMIC 入場料
#DIM DYNAMIC 集客力平均
#DIM DYNAMIC 対象
#DIM DYNAMIC 総設置数

SIF !ITEM:博物館
	RETURN

FOR LCOUNTY, 0, 30
	FOR LCOUNTX, 0, 30
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			CONTINUE
		ENDIF
		IF ID
			設置済み:ID = ID
			総価値 += TOINT(MUSEUM:ID:価値)*TOINT(MUSEUM:ID:集客力)/100
			集客力平均 = (集客力平均+TOINT(MUSEUM:ID:集客力))/2
			総設置数++
		ENDIF
	NEXT
NEXT

;同じキャラ(クローン含む)を一緒に設置した場合は価値が半減する
FOR LCOUNT, 1, 1000
	IF 設置済み:LCOUNT
		FOR LCOUNT2, 1, 1000
			SIF !設置済み:LCOUNT2
				CONTINUE
			SIF LCOUNT == LCOUNT2
				CONTINUE
			;双方のクローンフラグが無ければスルー
			SIF MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == "0" && MUSEUM:(設置済み:LCOUNT2):オリジナルナンバー == "0"
				CONTINUE
			IF MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == MUSEUM:(設置済み:LCOUNT2):キャラナンバー || MUSEUM:(設置済み:LCOUNT):オリジナルナンバー == MUSEUM:(設置済み:LCOUNT2):オリジナルナンバー
				IF MUSEUM:(設置済み:LCOUNT):価値減少 == ""
					MUSEUM:(設置済み:LCOUNT):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT):価値)/2}
					MUSEUM:(設置済み:LCOUNT):価値減少 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT):名前%は贋作の発覚により価値が半減しました
				ENDIF
				IF MUSEUM:(設置済み:LCOUNT2):価値減少 == ""
					MUSEUM:(設置済み:LCOUNT2):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT2):価値)/2}
					MUSEUM:(設置済み:LCOUNT2):価値減少 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT2):名前%は贋作の発覚により価値が半減しました
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT

;総価値0=何も設置してなければ計算しない
SIF 総価値 == 0
	RETURN

;入場料は価値の平均+設置数*10 10未満切り上げ
入場料 = 総価値/総設置数
入場料 += 総設置数*10

入場料 = 入場料/10*10

;総価値/100or名声+広さ が一日の来客数上限
客数 = MAX(総価値/100, MU:名声+MU:広さ)
;さらに集客力平均で下げる
客数 = 客数*集客力平均/100
;そこからさらにRANDでふるいにかける
客数 = RAND:(客数+1)

;客数だけ名声も上がる 一日に上がる名声はMU:広さと同じ値まで
名声上昇量 = MIN(客数, MU:広さ)
;ただし客数以上の名声にはならない
SIF MU:名声+名声上昇量 > 客数
	名声上昇量 = MAX(客数-MU:名声, 0)

MU:名声 += 名声上昇量

MONEY += 入場料*客数
バンめし = 0
MU:収入 = 入場料*客数
CALL 現金出納帳記録, @"博物館収入/{MU:収入}"

IF 客数 == 0
	PRINTL 博物館には1人も客が来ませんでした
ELSE
	CALL PASELIL, @"博物館には{客数}人の客が訪れ、一人あたり入場料{入場料}＄で合計{入場料*客数}＄の収入がありました"
ENDIF
SIF 名声上昇量 > 0
	PRINTFORML 博物館の名声が{名声上昇量}上がりました

;集客力の変動
FOR LCOUNT, 1, 1000
	;設置中なら3下げる 最低1
	IF 設置済み:LCOUNT
		MUSEUM:LCOUNT:集客力 = {MAX(TOINT(MUSEUM:LCOUNT:集客力)-3, 1)}
	;設置してないやつは10上げる 最高150
	ELSE
		MUSEUM:LCOUNT:集客力 = {MIN(TOINT(MUSEUM:LCOUNT:集客力)+10, 150)}
	ENDIF
NEXT

;実用可能なら以下の処理
IF MU:実用
	;並びを適当にバラす
	FOR LCOUNT, 999, 1, -1
		SWAP 設置済み:LCOUNT, 設置済み:(RAND(1, LCOUNT))
	NEXT
	REPEAT 客数
		;ランダムで対象を決める
		対象 = RAND(1, 1000)
		WHILE !設置済み:対象
			対象++
			SIF 対象 == 1000
				対象 = 1
		WEND
		;0～2回汚される
		MUSEUM:(設置済み:対象):汚れ = {TOINT(MUSEUM:(設置済み:対象):汚れ)+RAND:2}
		;DEBUGPRINTFORML %MUSEUM:(設置済み:対象):名前%+
	REND
ENDIF

@MANUAL_博物館
PRINTL □博物館経営について
PRINTL 　博物館は作成したオブジェを展示して収益を得られる経営要素です
PRINTL 　レベル5魔法「ジュエリーストーム」や、キャラを物理的に加工して展示することが可能です
PRINTL 　上記の手段でオブジェ化したキャラは、売却時と同じくロスト扱いになるのでご注意ください
PRINTL 　オブジェにはそれぞれ集客力のパラメータがあります。長期間同じものを置いていると客が飽きて集客力が下がります
PRINTL 　展示していないオブジェは徐々に集客力が回復していきます。こまめに展示物を変えましょう
PRINTL 　オブジェ配置画面で、"実用"を許可することによって、来客数に応じてオブジェが汚されることがありますが、収入への影響はありません
PRINTL 　また、クローン個体をオブジェ化したものを、オリジナル含め複数体展示した場合、贋作として価値が半減します。注意しましょう
WAIT

@剥製加工, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%を剥製に加工しました
;価値は1000は保証
作品価値 = 1000

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "狐", "妖狐", "戦士", "ファーリー", "尻尾", "動物耳", "羽", "ふたなり"
			作品価値 += 500
		CASE "魅力", "魅惑", "足使用不可", "スライム", "溶ける快"
			作品価値 += 1000
		CASE "謎の魅力", "人気", "妊娠"
			作品価値 += 1500
		CASE "バストサイズ"
			SELECTCASE TALENT:対象:バストサイズ
				CASE IS >= 4
					作品価値 += (TALENT:対象:バストサイズ-3)*100
			ENDSELECT
	ENDSELECT
NEXT

SIF TALENT:対象:体格 == 1
	作品価値 += 500

;タトゥーでも微増
SIF CSTR:対象:右腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:左腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:右足の印 != ""
	作品価値 += 100
SIF CSTR:対象:左足の印 != ""
	作品価値 += 100
SIF CSTR:対象:背中の印 != ""
	作品価値 += 200
SIF CSTR:対象:胴体の印 != ""
	作品価値 += 300
;人気度を加算
作品価値 += 人気度:NOF(対象)

CALL 作品加工共通処理, "剥製", 対象, 作品価値

@白骨加工, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%を白骨に加工しました
;作品価値は1000は保証
作品価値 = 1000

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "尻尾", "羽"
			作品価値 += 500
		CASE "足使用不可"
			作品価値 += 1000
	ENDSELECT
NEXT
SIF TALENT:対象:体格 == 1
	作品価値 += 500
;人気度を加算
作品価値 += 人気度:NOF(対象)

CALL 作品加工共通処理, "白骨死体", 対象, 作品価値

@ホルマリン加工, 対象
;//一旦剥製からコピペ
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%をホルマリン漬けに加工しました
;価値は1000は保証
作品価値 = 1000

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "狐", "妖狐", "戦士", "ファーリー", "尻尾", "動物耳", "羽", "ふたなり"
			作品価値 += 500
		CASE "魅力", "魅惑", "足使用不可", "スライム", "溶ける快"
			作品価値 += 1000
		CASE "謎の魅力", "人気", "妊娠"
			作品価値 += 1500
		CASE "バストサイズ"
			SELECTCASE TALENT:対象:バストサイズ
				CASE IS >= 4
					作品価値 += (TALENT:対象:バストサイズ-3)*100
			ENDSELECT
	ENDSELECT
NEXT

SIF TALENT:対象:体格 == 1
	作品価値 += 500

;タトゥーでも微増
SIF CSTR:対象:右腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:左腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:右足の印 != ""
	作品価値 += 100
SIF CSTR:対象:左足の印 != ""
	作品価値 += 100
SIF CSTR:対象:背中の印 != ""
	作品価値 += 200
SIF CSTR:対象:胴体の印 != ""
	作品価値 += 300
;人気度を加算
作品価値 += 人気度:NOF(対象)

CALL 作品加工共通処理, "ホルマリン漬け", 対象, 作品価値

@透明標本加工, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%を透明標本に加工しました
;最低保証は1500（仮置き） 後々価値が高い分の手間がかかるようにする
作品価値 = 1500

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "尻尾", "動物耳", "羽"
			作品価値 += 500
		CASE "足使用不可"
			作品価値 += 1000
		CASE "妊娠"
			作品価値 += 1500
	ENDSELECT
NEXT
SIF TALENT:対象:体格 == 1
	作品価値 += 500
;人気度を加算
作品価値 += 人気度:NOF(対象)

CALL 作品加工共通処理, "透明標本", 対象, 作品価値

@稼働停止, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%を稼働停止させました
;博物館の客は人形ではなく死体を見に来ているので低め
作品価値 = 600

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "狐", "妖狐", "戦士", "ファーリー", "尻尾", "動物耳", "羽", "ふたなり"
			作品価値 += 500
		CASE "魅力", "魅惑", "足使用不可", "スライム", "溶ける快"
			作品価値 += 1000
		CASE "謎の魅力", "人気", "妊娠"
			作品価値 += 1500
		CASE "バストサイズ"
			SELECTCASE TALENT:対象:バストサイズ
				CASE IS >= 4
					作品価値 += (TALENT:対象:バストサイズ-3)*100
			ENDSELECT
	ENDSELECT
NEXT

SIF TALENT:対象:体格 == 1
	作品価値 += 500

;タトゥーでも微増
SIF CSTR:対象:右腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:左腕の印 != ""
	作品価値 += 100
SIF CSTR:対象:右足の印 != ""
	作品価値 += 100
SIF CSTR:対象:左足の印 != ""
	作品価値 += 100
SIF CSTR:対象:背中の印 != ""
	作品価値 += 200
SIF CSTR:対象:胴体の印 != ""
	作品価値 += 300
;人気度を加算
作品価値 += 人気度:NOF(対象)

;種別は便宜上
CALL 作品加工共通処理, "人形", 対象, 作品価値


@作品加工共通処理, 作品種類, 対象, 作品価値
#DIMS DYNAMIC 作品種類
#DIM DYNAMIC 対象
#DIM DYNAMIC 作品価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 1, VARSIZE("MUSEUM", 1)
	IF MUSEUM:LCOUNT:種類 == ""
		空き変数 = LCOUNT
		BREAK
	ENDIF
NEXT

;#;SIF 空き変数 == 0
;#;	THROW 変数「MUSEUM」に空きがありません

SIF TALENT:対象:妊娠 && 作品種類 != "白骨死体"
	MUSEUM:空き変数:名前 += "妊娠した"
SIF 作品種類 == "人形"
	MUSEUM:空き変数:名前 += "稼働を停止した"
MUSEUM:空き変数:名前 '= MUSEUM:空き変数:名前 + NAME:対象
SIF ISSLIME(対象) && !CSVTALENT(NO:対象, GETNUM(TALENT, "スライム")) && !CSVTALENT(NO:対象, GETNUM(TALENT, "溶ける快"))
	MUSEUM:空き変数:名前 += "(スライム)"
MUSEUM:空き変数:名前 '= MUSEUM:空き変数:名前
SIF 作品種類 != "人形"
	MUSEUM:空き変数:名前 += "の" + 作品種類

SELECTCASE 作品種類
	CASE "剥製"
		MUSEUM:空き変数:色 = {COLOR_FROMNAME("red")}
	CASE "白骨死体"
		MUSEUM:空き変数:色 = {COLOR_FROMNAME("ivory")}
	CASE "ホルマリン漬け"
		MUSEUM:空き変数:色 = {COLOR_FROMNAME("yellowgreen")}
	CASE "透明標本"
		MUSEUM:空き変数:色 = {COLOR_FROMNAME("cyan")}
	CASE "人形"
		MUSEUM:空き変数:色 = {COLOR_FROMNAME("gray")}
ENDSELECT

MUSEUM:空き変数:種類 = %作品種類%
MUSEUM:空き変数:キャラナンバー = {NOF(対象)}
MUSEUM:空き変数:価値 = {作品価値}
MUSEUM:空き変数:血縁コード = {CFLAG:対象:血縁コード}
MUSEUM:空き変数:オリジナルナンバー = {CFLAG:対象:複製}
MUSEUM:空き変数:集客力 = 100
MUSEUM:空き変数:汚れ = 0
