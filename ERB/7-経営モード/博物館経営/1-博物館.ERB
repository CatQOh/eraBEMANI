@博物館
#DIM DYNAMIC 改装費用

SIF 博物館広さ < 10
	博物館広さ = 10

改装費用 = POWER(博物館広さ, 3)*1000

PRINTL □博物館の運営をします
PRINTL 　博物館に展示できるオブジェは、魔法「ジュエリーストーム(レベル5)」やキャラを加工して作成できます
CALL PASELIL, @"　現在の名声:{博物館名声} 昨日の収入:{博物館収入}＄ 現在の所持金:{MONEY}＄"
PRINTL 
PRINTL 　[1] - 作品を展示する
PRINTL 　[2] - 作品を作る
IF 博物館広さ >= 30
	PRINTL 　[-] - 博物館の広さは最大です
ELSE
	CALL PASELIL, @"　[3] - 博物館を改装する（現在の広さ:{博物館広さ} 改装費用:{改装費用}＄）"
ENDIF
PRINTL 　[9] - 博物館マニュアル
DRAWLINE
PRINTPLAIN 　MUSEUM　
PRINTL [0] - 戻る

CALL INPUTF, 0, 1, 2, 3, 9

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL 博物館_MAP
		RESTART
	CASE 2
		CALL 博物館_CREATE
		;何もしないで戻ってきた場合
		SIF RESULT == 0
			RESTART
		;作品を作ったらターンエンド
		JUMP ターン終了
	CASE 3
		IF 博物館広さ >= 30
			PRINTW 博物館の広さは最大です
			RESTART
		ENDIF
		IF MONEY < 改装費用
			PRINTW 改装費用が足りません
			RESTART
		ENDIF
		MONEY -= 改装費用
		CALL 現金出納帳記録, @"博物館改築/-{改装費用}"
		博物館広さ++
		RESTART
	CASE 9
		CALL MANUAL_博物館
		RESTART
ENDSELECT

@博物館_MAP
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 処理数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC XRES
#DIM DYNAMIC YRES
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIMS DYNAMIC HTML用

#DIM CONST 種類 = 0
#DIM CONST 名前 = 1
#DIM CONST ナンバー = 2
#DIM CONST 価値 = 3
#DIM CONST 色 = 4
#DIM CONST 血縁 = 5
#DIM CONST クローン = 6
#DIM CONST 集客力 = 7
#DIM CONST 汚れ = 8

CALL LOOP_CLEARLINE

VARSET 設置済み
FOR LCOUNTY, 0, 博物館広さ
	HTML用 = 
	FOR LCOUNTX, 0, 博物館広さ
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			ID = 0
		ENDIF
		IF ID > 0
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", @"%MUSEUM:ID:名前% 価値:%MUSEUM:ID:価値% 集客力:%MUSEUM:ID:集客力%\% \@ 博物館射 ? 汚された回数:%MUSEUM:ID:汚れ% #\@", MUSEUM:ID:色)
			設置済み:ID = 1
		ELSE
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", "未設置")
		ENDIF
	NEXT
	HTML_PRINT HTML用
NEXT
DRAWLINE
PRINTPLAIN 　MUSEUM MAP　
PRINTFORML [-1] 戻る [-2] 全て片付ける [-3] 実用の許可/不許可(現在:\@ 博物館射 ? 許可 # 不許可 \@)

INPUT

SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		VARSET 博物館マップ
		RESTART
	CASE -3
		INVERTBIT 博物館射, 0
		RESTART
	CASEELSE
		XRES = RESULT/100
		YRES = RESULT%100
		SIF !INRANGE(XRES, 0, 博物館広さ-1) || !INRANGE(YRES, 0, 博物館広さ-1)
			RESTART
		ID = 博物館マップ:YRES:XRES
		IF ID
			PRINTFORML %MUSEUM:ID:名前%が設置されています。どうしますか？
			PRINTL [0] - 別の物に置き換える
			PRINTL [1] - 片付ける
			PRINTL [2] - 何もしない
			CALL INPUTF, 0, 1, 2
			SELECTCASE RESULT
				CASE 0
					GOTO SELECT_OBJECT
				CASE 1
					博物館マップ:YRES:XRES = 0
					RESTART
				CASE 2
					RESTART
			ENDSELECT
		ENDIF
		$SELECT_OBJECT
		処理数 = 0
		PRINTL □何を設置しますか？
		FOR LCOUNT, 1, 1000
			IF MUSEUM:LCOUNT:種類 != "" && !設置済み:LCOUNT
				IF 処理数 >= ページ数*20+20
					次ページあり = 1
					BREAK
				ENDIF
				処理数++
				SIF ページ数*20 >= 処理数
					CONTINUE
				CALL PASELI, @"　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前% 価値:%MUSEUM:LCOUNT:価値%＄ 集客力:%MUSEUM:LCOUNT:集客力%\% "
				PRINT 色:
				SETCOLOR TOINT(MUSEUM:LCOUNT:色)
				PRINTL ■
				RESETCOLOR
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　OBJECT　
		CALL PRINTSPACE, ページ数, "[-1] 前ページ "
		PRINT [0] 戻る 
		CALL PRINTSPACE, 次ページあり, "[-9] 次ページ "
		PRINTL 
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					RESTART
				CASE -1
					SIF ページ数
						ページ数--
					GOTO SELECT_OBJECT
				CASE -9
					SIF 次ページあり
						ページ数++
					次ページあり = 0
					GOTO SELECT_OBJECT
				CASEELSE
					ID = RESULT
					IF MUSEUM:ID:種類 != ""
						博物館マップ:YRES:XRES = ID
						RESTART
					ENDIF
					CONTINUE
			ENDSELECT
		LOOP 1
ENDSELECT

@博物館_CREATE
#DIM DYNAMIC 対象, 2
#DIM DYNAMIC 価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 処理数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり

;GOTO CREATE_CORPSE

PRINTL □博物館用の作品を作ります 鉱物は魔法コマンドから作成してください
PRINT 　[1] - キャラを
SETCOLORBYNAME red
PRINT 加工
RESETCOLOR
PRINTL する
;PRINTL 　[2] - 既存の作品を組み合わせる
PRINTL 　[3] - 既存の作品を解体する
PRINTL 　[0] - 戻る

CALL INPUTF, 0, 1, 2, 3

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		GOTO CREATE_CORPSE
	CASE 2
		GOTO JOIN_CORPSE
	CASE 3
		GOTO DESTRUCT_CORPSE
ENDSELECT


$CREATE_CORPSE
PRINT □加工するキャラを選んでください。
FONTBOLD
PRINTL 加工したキャラは死亡します
FONTREGULAR
FOR PAGING, 1, CHARANUM
	CALL PAGING, PAGING > 0 && !TALENT:PAGING:無機物 && CSTR:PAGING:種族 != "機械" && !ISSLIME(PAGING) && !TALENT:PAGING:霊体
NEXT

SIF RESULT == -1
	RETURN 0

対象 = RESULT

PRINTFORML %NAME:対象%を加工します。どのように加工しますか？
PRINTL [0] - やっぱりやめる
PRINTL [1] - 剥製に加工する
PRINTL [2] - 白骨に加工する
CALL INPUTF, 0, 1, 2
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		SETCOLORBYNAME red
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		CALL 剥製加工, 対象
		CALL CLEAR_CHARA, 対象
		RETURN 1
	CASE 2
		SETCOLORBYNAME red
		CUSTOMDRAWLINE ✂----------
		RESETCOLOR
		PRINTFORMW %NAME:対象%を白骨に加工しました
		;価値は1000は保証
		価値 = 1000

		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:対象:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "小柄体型", "尻尾", "羽"
					価値 += 500
				CASE "足使用不可"
					価値 += 1000
			ENDSELECT
		NEXT
		;人気度を加算
		価値 += 人気度:NOF(対象)

		FOR LCOUNT, 1, VARSIZE("MUSEUM", 0)
			IF MUSEUM:LCOUNT:0 == ""
				空き変数 = LCOUNT
				BREAK
			ENDIF
		NEXT

		;#;SIF 空き変数 == 0
		;#;	THROW 変数「MUSEUM」に空きがありません

		MUSEUM:空き変数:0 = 白骨死体
		MUSEUM:空き変数:1 = %NAME:対象%の白骨死体
		MUSEUM:空き変数:2 = {NOF(対象)}
		MUSEUM:空き変数:3 = {価値}
		MUSEUM:空き変数:4 = {COLOR_FROMNAME("whitesmoke")}
		MUSEUM:空き変数:5 = {CFLAG:対象:血縁コード}
		MUSEUM:空き変数:6 = {CFLAG:対象:複製}
		MUSEUM:空き変数:7 = 100
		MUSEUM:空き変数:8 = 0

		CALL CLEAR_CHARA, 対象
		RETURN 1
ENDSELECT

$JOIN_CORPSE
PRINTL □同じ種類の2つの作品を組み合わせます 何を組み合わせますか？
処理数 = 0
ページ数 = 0
FOR LCOUNT, 1, 1000
	SIF !GROUPMATCH(MUSEUM:LCOUNT:0, "剥製", "白骨死体")
		CONTINUE
	IF 処理数 < ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:0%
NEXT
	
$DESTRUCT_CORPSE
PRINTL □作品を解体して展示物にできます 何を解体しますか？
処理数 = 0
FOR LCOUNT, 1, 1000
	SIF !GROUPMATCH(MUSEUM:LCOUNT:0, "剥製", "白骨死体")
		CONTINUE
	IF 処理数 > ページ数*10+10
		次ページあり = 1
		BREAK
	ENDIF
	IF 処理数 < ページ数*10
		処理数++
		CONTINUE
	ENDIF
	PRINTFORML 　[{LCOUNT, 2}] %MUSEUM:LCOUNT:1%
	処理数++
NEXT
DRAWLINE
PRINTPLAINFORM 　解体　
CALL PRINTSPACE, ページ数, "[-1] 前ページ "
PRINT [0] 戻る
CALL PRINTSPACE, ページ数, "[-9] 次ページ "
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE -1
			SIF ページ数 > 0
				ページ数--
			GOTO DESTRUCT_CORPSE
		CASE 0
			RESTART
		CASE -9
			SIF 次ページあり
				ページ数++
			GOTO DESTRUCT_CORPSE
		CASEELSE
			対象 = RESULT
			SIF !GROUPMATCH(MUSEUM:対象:0, "剥製", "白骨死体")
				CONTINUE
			BREAK
	ENDSELECT
LOOP 1

PRINTFORML %MUSEUM:対象:1%を解体して展示物にします。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - いいえ

CALL INPUTF, 0, 1

SIF RESULT == 1
	GOTO DESTRUCT_CORPSE

FOR LCOUNT, 0, 4
	FOR LCOUNT2, 1, VARSIZE("MUSEUM", 0)
		IF MUSEUM:LCOUNT2:0 == ""
			空き変数 = LCOUNT2
			BREAK
		ENDIF
	NEXT
	MUSEUM:空き変数:0 = 部位
	MUSEUM:空き変数:1 = %CSVNAMEF(TOINT(MUSEUM:対象:2))%の
	SELECTCASE LCOUNT
		CASE 0
			MUSEUM:空き変数:1 += "頭"
		CASE 1
			MUSEUM:空き変数:1 += "胴体"
		CASE 2
			MUSEUM:空き変数:1 += "腕"
		CASE 3
			MUSEUM:空き変数:1 += "足"
	ENDSELECT
	SIF MUSEUM:対象:0 == "白骨"
		MUSEUM:空き変数:1 += "(白骨)"

	MUSEUM:空き変数:2 = %MUSEUM:対象:2%
	MUSEUM:空き変数:3 = {TOINT(MUSEUM:対象:3)/5}
	MUSEUM:空き変数:4 = %MUSEUM:対象:4%
	MUSEUM:空き変数:5 = %MUSEUM:対象:5%
	MUSEUM:空き変数:6 = 0
	MUSEUM:空き変数:7 = 100
	MUSEUM:空き変数:8 = 0
	MUSEUM:空き変数:9 = %MUSEUM:対象:9%
NEXT

PRINTFORMW %MUSEUM:対象:1%を解体しました
FOR LCOUNT, 0, 10
	MUSEUM:対象:LCOUNT '= ""
NEXT

RETURN 1

@INCOME_博物館
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIM DYNAMIC 総価値
#DIM DYNAMIC 収入
#DIM DYNAMIC 客数
#DIM DYNAMIC 名声上昇量
#DIM DYNAMIC 入場料
#DIM DYNAMIC 集客力平均
#DIM DYNAMIC 対象
#DIM DYNAMIC 総設置数

#DIM CONST 種類 = 0
#DIM CONST 名前 = 1
#DIM CONST ナンバー = 2
#DIM CONST 価値 = 3
#DIM CONST 色 = 4
#DIM CONST 血縁 = 5
#DIM CONST クローン = 6
#DIM CONST 集客力 = 7
#DIM CONST 汚れ = 8

SIF !ITEM:博物館
	RETURN

FOR LCOUNTY, 0, 30
	FOR LCOUNTX, 0, 30
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0 && MUSEUM:ID:種類 == ""
			博物館マップ:LCOUNTY:LCOUNTX = 0
			CONTINUE
		ENDIF
		IF ID
			設置済み:ID = ID
			総価値 += TOINT(MUSEUM:ID:価値)*TOINT(MUSEUM:ID:集客力)/100
			集客力平均 = (集客力平均+TOINT(MUSEUM:ID:集客力))/2
			総設置数++
		ENDIF
	NEXT
NEXT

;同じキャラ(クローン含む)を一緒に設置した場合は価値が半減する
FOR LCOUNT, 1, 1000
	IF 設置済み:LCOUNT
		FOR LCOUNT2, 1, 1000
			SIF !設置済み:LCOUNT2
				CONTINUE
			SIF LCOUNT == LCOUNT2
				CONTINUE
			;双方のクローンフラグが無ければスルー
			SIF MUSEUM:(設置済み:LCOUNT):クローン == "0" && MUSEUM:(設置済み:LCOUNT2):クローン == "0"
				CONTINUE
			IF MUSEUM:(設置済み:LCOUNT):クローン == MUSEUM:(設置済み:LCOUNT2):ナンバー || MUSEUM:(設置済み:LCOUNT):クローン == MUSEUM:(設置済み:LCOUNT2):クローン
				IF MUSEUM:(設置済み:LCOUNT):9 == ""
					MUSEUM:(設置済み:LCOUNT):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT):価値)/2}
					MUSEUM:(設置済み:LCOUNT):9 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT):名前%は贋作の発覚により価値が半減しました
				ENDIF
				IF MUSEUM:(設置済み:LCOUNT2):9 == ""
					MUSEUM:(設置済み:LCOUNT2):価値 = {TOINT(MUSEUM:(設置済み:LCOUNT2):価値)/2}
					MUSEUM:(設置済み:LCOUNT2):9 = 1
					PRINTFORML %MUSEUM:(設置済み:LCOUNT2):名前%は贋作の発覚により価値が半減しました
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT

;総価値0=何も設置してなければ計算しない
SIF 総価値 == 0
	RETURN

;入場料は価値の平均+設置数*10 10未満切り上げ
入場料 = 総価値/総設置数
入場料 += 総設置数*10

入場料 = 入場料/10*10

;総価値/100or名声+広さ が一日の来客数上限
客数 = MAX(総価値/100, 博物館名声+博物館広さ)
;さらに集客力平均で下げる
客数 = 客数*集客力平均/100
;そこからさらにRANDでふるいにかける
客数 = RAND:(客数+1)

;客数だけ名声も上がる 一日に上がる名声は博物館広さと同じ値まで
名声上昇量 = MIN(客数, 博物館広さ)
;ただし客数以上の名声にはならない
SIF 博物館名声+名声上昇量 > 客数
	名声上昇量 = MAX(客数-博物館名声, 0)

博物館名声 += 名声上昇量

MONEY += 入場料*客数
博物館収入 = 入場料*客数
CALL 現金出納帳記録, @"博物館収入/{博物館収入}"

IF 客数 == 0
	PRINTL 博物館には1人も客が来ませんでした
ELSE
	CALL PASELIL, @"博物館には{客数}人の客が訪れ、一人あたり入場料{入場料}＄で合計{入場料*客数}＄の収入がありました"
ENDIF
SIF 名声上昇量 > 0
	PRINTFORML 博物館の名声が{名声上昇量}上がりました

;集客力の変動
FOR LCOUNT, 1, 1000
	;設置中なら3下げる 最低1
	IF 設置済み:LCOUNT
		MUSEUM:LCOUNT:集客力 = {MAX(TOINT(MUSEUM:LCOUNT:集客力)-3, 1)}
	;設置してないやつは10上げる 最高150
	ELSE
		MUSEUM:LCOUNT:集客力 = {MIN(TOINT(MUSEUM:LCOUNT:集客力)+10, 150)}
	ENDIF
NEXT

;実用可能なら以下の処理
IF 博物館射
	;並びを適当にバラす
	FOR LCOUNT, 999, 1, -1
		SWAP 設置済み:LCOUNT, 設置済み:(RAND(1, LCOUNT))
	NEXT
	REPEAT 客数
		;ランダムで対象を決める
		対象 = RAND(1, 1000)
		WHILE !設置済み:対象
			対象++
			SIF 対象 == 1000
				対象 = 1
		WEND
		;0～2回汚される
		MUSEUM:(設置済み:対象):汚れ = {TOINT(MUSEUM:(設置済み:対象):汚れ)+RAND:2}
		;DEBUGPRINTFORML %MUSEUM:(設置済み:対象):名前%+
	REND
ENDIF

@MANUAL_博物館
PRINTL □博物館経営について
PRINTL 　博物館は作成したオブジェを展示して収益を得られる経営要素です
PRINTL 　レベル5魔法「ジュエリーストーム」や、キャラを物理的に加工して展示することが可能です
PRINTL 　上記の手段でオブジェ化したキャラは、売却時と同じくロスト扱いになるのでご注意ください
PRINTL 　オブジェにはそれぞれ集客力のパラメータがあります。長期間同じものを置いていると客が飽きて集客力が下がります
PRINTL 　展示していないオブジェは徐々に集客力が回復していきます。こまめに展示物を変えましょう
PRINTL 　オブジェ配置画面で、"実用"を許可することによって、来客数に応じてオブジェが汚されることがありますが、収入への影響はありません
PRINTL 　また、クローン個体をオブジェ化したものを、オリジナル含め複数体展示した場合、贋作として価値が半減します。注意しましょう
WAIT

@剥製加工, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 価値
#DIM DYNAMIC 空き変数
#DIM DYNAMIC LCOUNT

PRINTFORMW %NAME:対象%を剥製に加工しました
;価値は1000は保証
価値 = 1000

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "狐", "妖狐", "小柄体型", "戦士", "ファーリー", "尻尾", "動物耳", "羽", "ふたなり"
			価値 += 500
		CASE "魅力", "魅惑", "足使用不可", "スライム", "溶ける快"
			価値 += 1000
		CASE "謎の魅力", "人気", "妊娠"
			価値 += 1500
		CASE "バストサイズ"
			SELECTCASE TALENT:対象:バストサイズ
				CASE IS >= 4
					価値 += (TALENT:対象:バストサイズ-3)*100
			ENDSELECT
	ENDSELECT
NEXT

;タトゥーでも微増
SIF CSTR:対象:右腕の印 != ""
	価値 += 100
SIF CSTR:対象:左腕の印 != ""
	価値 += 100
SIF CSTR:対象:右足の印 != ""
	価値 += 100
SIF CSTR:対象:左足の印 != ""
	価値 += 100
SIF CSTR:対象:背中の印 != ""
	価値 += 200
SIF CSTR:対象:胴体の印 != ""
	価値 += 300
;人気度を加算
価値 += 人気度:NOF(対象)

FOR LCOUNT, 1, VARSIZE("MUSEUM", 0)
	IF MUSEUM:LCOUNT:0 == ""
		空き変数 = LCOUNT
		BREAK
	ENDIF
NEXT

;#;SIF 空き変数 == 0
;#;	THROW 変数「MUSEUM」に空きがありません

MUSEUM:空き変数:0 = 剥製
MUSEUM:空き変数:1 = %NAME:対象%の剥製
MUSEUM:空き変数:2 = {NOF(対象)}
MUSEUM:空き変数:3 = {価値}
MUSEUM:空き変数:4 = {COLOR_FROMNAME("red")}
MUSEUM:空き変数:5 = {CFLAG:対象:血縁コード}
MUSEUM:空き変数:6 = {CFLAG:対象:複製}
MUSEUM:空き変数:7 = 100
MUSEUM:空き変数:8 = 0

;CLEAR_CHARAは呼び出し元で行う
