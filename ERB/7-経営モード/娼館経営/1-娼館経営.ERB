@STRATEGY_BROTHEL
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIMS DYNAMIC 定休日設定
CALL BR_ITEM_POINT

SIF BR:広さ == 0
	BR:広さ = 1
SIF BR:部屋数 == 0
	BR:部屋数 = 1
SIF BR:方針 == 0
	BR:方針 = 1

DRAWLINE
PRINT □
SIF BRS:店名 != "娼館"
	PRINTFORM 娼館の店名：%BRS:店名% 
PRINTFORM 娼館レベル：{BR:レベル} 運営方針：%娼館方針名(BR:方針)% 
IF 娼館方針名(BR:方針) != "ソフト方針"
	PRINT 客の性別：
	SELECTCASE BR:性別固定
		CASE 0
			PRINT 固定無し
		CASE 1
			PRINT 男性のみ
		CASE 2
			PRINT 女性のみ
	ENDSELECT
ENDIF
PRINTL 

PRINTFORML 　定休日：\@ BRS:定休日 == "" ? 無し # %BRS:定休日%曜日 \@
IF BR:レベル >= 1000
	PRINTL 　娼館レベルは最大です
ELSE
	CALL PASELIL, @"　次のランクアップまであと{BR:レベルアップ必要額}＄の売上が必要"
ENDIF
PRINTL 

HTML_PRINT SET_TOOLTIP("　[1] - 道具配置", "娼館に道具を配置します")
HTML_PRINT SET_TOOLTIP("　[2] - 物件増築", "娼館用の物件を改装します")
HTML_PRINT SET_TOOLTIP("　[3] - 運営方針", "娼館の運営方針を決定します")
HTML_PRINT SET_TOOLTIP("　[4] - 人員配置", "娼館で働かせるキャラを配属します")
HTML_PRINT SET_TOOLTIP("　[5] - 店名変更", "娼館の店名を変更します")
HTML_PRINT SET_TOOLTIP("　[8] - 定休日設定", "娼館の定休日を決めます")
IF BR:営業中 > 0
	HTML_PRINT SET_TOOLTIP("　[9] - 一時休業", "娼館を一時休業し、収入と支出が行われないようにします")
ELSE
	HTML_PRINT SET_TOOLTIP("　[9] - 通常営業", "娼館の営業を再開し、収入と支出が行われるようにします")
ENDIF

DRAWLINE
PRINTPLAIN 　BROTHEL　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL BR_ITEM
	CASE 2
		CALL BR_RENOVATION
	CASE 3
		CALL BR_PLAN
	CASE 4
		CALL SELECT_ROOM_BR
	CASE 5
		PRINT □店名を入力してください
		SIF GCS:店名 != "娼館"
			PRINTFORM  現在の店名：%BRS:店名%
		PRINTL 
		PRINTBUTTON "[変更しない]", "変更しない"
		PRINTL 
		INPUTS "娼館"
		SIF RESULTS == "変更しない"
			RESTART
		PRINTFORMW 店名を「%RESULTS%」に変更しました
		BRS:店名 = %RESULTS%
	CASE 8
		VARLINE = 0
		DO
			CALL LOOP_CLEARLINE
			定休日設定 = 
			DRAWLINE
			PRINTL □定休日を選んでください
			CALL PRINTCOLORL, @"　[1] - 日曜日（\@ BR定休日("日") ? 定休 # 営業 \@）", @"\@ BR定休日("日") ? gray #\@"
			CALL PRINTCOLORL, @"　[2] - 月曜日（\@ BR定休日("月") ? 定休 # 営業 \@）", @"\@ BR定休日("月") ? gray #\@"
			CALL PRINTCOLORL, @"　[3] - 火曜日（\@ BR定休日("火") ? 定休 # 営業 \@）", @"\@ BR定休日("火") ? gray #\@"
			CALL PRINTCOLORL, @"　[4] - 水曜日（\@ BR定休日("水") ? 定休 # 営業 \@）", @"\@ BR定休日("水") ? gray #\@"
			CALL PRINTCOLORL, @"　[5] - 木曜日（\@ BR定休日("木") ? 定休 # 営業 \@）", @"\@ BR定休日("木") ? gray #\@"
			CALL PRINTCOLORL, @"　[6] - 金曜日（\@ BR定休日("金") ? 定休 # 営業 \@）", @"\@ BR定休日("金") ? gray #\@"
			CALL PRINTCOLORL, @"　[0] - 土曜日（\@ BR定休日("土") ? 定休 # 営業 \@）", @"\@ BR定休日("土") ? gray #\@"
			PRINTL 　[100] - 設定終了
			BINPUT
			LRESULT = RESULT
			SIF RESULT == 100
				BREAK
			FOR LCOUNT, 0, 7
				IF LRESULT == LCOUNT
					IF !BR定休日(DOW(LRESULT))
						BRS:定休日 += @"・%DOW(LRESULT)%"
					ELSE
						CONTINUE
					ENDIF
				ENDIF
				SIF BR定休日(DOW(LCOUNT))
					定休日設定 += @"・%DOW(LCOUNT)%"
			NEXT
			BRS:定休日 = %SUBSTRINGU(定休日設定, 1)%
		LOOP 1
	CASE 9
		IF BR:営業中 > 0
			PRINTW 娼館の経営を一時中断しました
			;定休日じゃない休業は-1
			BR:営業中 = -1
		ELSE
			PRINTW 娼館の経営を開始しました
			BR:営業中 = DAY
		ENDIF
	CASE 100
		CALL BR_TUTORIAL
	CASEELSE
		CONTINUE
ENDSELECT
RESTART

;----------------------------------------------------
;娼館経営モードチュートリアル
;----------------------------------------------------
@BR_TUTORIAL
DRAWLINE
PRINTL 当モードは娼館を経営して資金を稼ぐコマンドです。
PRINTL 現在はキャラと客間の妊娠については非対応です。
PRINTL 
PRINTL [1] - 「道具配置」とは？
PRINTL [2] - 「物件増築」とは？
PRINTL [3] - 「経営方針」とは？
PRINTL [4] - 「人員配置」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

BINPUT

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		PRINTL 道具を配置するコマンドです。
		PRINTL 調教用の道具とはまた別物で、娼館で使用するものは配置分だけ毎日コストを支払う必要があります。
		PRINTL 道具にはそれぞれ用途に合わせたポイントがあり、経営方針に見合った道具を配置することで収入にプラスの補正が入ります。
		PRINTW デフォルトでは1種類しか配置できませんが、増築することによって配置可能な数が増えます。
	CASE 2
		PRINTL 物件を増築し、より多くの道具や奴隷を配置できるようにするコマンドです。
		PRINTL ゲームセンター経営と違い、物件はデフォルトで所持しているので経営自体はすぐに始められます。
		SIF !ORIGIN
			PRINTW 増築には当然お金がかかりますが、序盤は小規模でも特に気になるものではないと思います。
	CASE 3
		PRINTL 運営方針を決めるコマンドです。
		;PRINTL 運営には方針が必要であり、これを決めないとほとんど経営放棄も同然です。
		PRINTW 奴隷の身体をよく案じて考えて、適切な方針を定めましょう。
	CASE 4
		PRINTL 奴隷から人員を選出するコマンドです。
		PRINTL 当然身体を売る奴隷がいないと収入はありません。売春を行ったキャラは、売春の回数に応じて売却額が下がっていきます。
		PRINTL 体力が減っている、もしくはストレス過多の奴隷は配置しても売春できないため、奴隷の身体をちゃんと考えてあげましょう。
		PRINTW (数値上は体力1000以下まで減っていると自動で休憩に入り、回復後に売春を行います)
ENDSELECT

RESTART

@BR_ITEM
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 娼館道具設置数
#DIM DYNAMIC 表示数

CALL BR_ITEM_POINT
PRINTL □道具を配置します
DRAWLINE
PRINTL 【現在のポイント】
CALL PRINTCOLORL, @"　ソフト - {ソフトポイント, 3}", @"\@ BR:方針 == 1 ?# gray \@"
CALL PRINTCOLORL, @"　ハード - {ハードポイント, 3}", @"\@ BR:方針 == 2 ?# gray \@"
CALL PRINTCOLORL, @"　SM     - {SMポイント, 3}", @"\@ BR:方針 == 3 ?# gray \@"
CALL PRINTCOLORL, @"　狂気   - {狂気ポイント, 3}", @"\@ BR:方針 == 4 ?# gray \@"
DRAWLINE

娼館道具設置数 = 0
FOR LCOUNT, GETNUM(BRITEM, "ローター"), GETNUM(BRITEM, "お風呂場")
	SIF BRITEM:LCOUNT
		娼館道具設置数++
NEXT
PRINT  *配置枠*［
表示数 = 0
REPEAT BR:広さ
	IF 表示数 < 娼館道具設置数
		CALL PRINTCOLOR, "■", "deepskyblue"
	ELSE
		CALL PRINTCOLOR, "■", "gray"
	ENDIF
	表示数++
REND
PRINTL ］
DRAWLINE
表示数 = 0
FOR LCOUNT, GETNUM(BRITEM, "ローター"), GETNUM(BRITEM, "お風呂場")
	SIF ERDNAME(BRITEM, LCOUNT) == ""
		CONTINUE
	SIF 表示数%3 == 0
		PRINT 　
	PRINTFORM [{LCOUNT, 2}] %ERDNAME(BRITEM, LCOUNT), 15, LEFT%
	SIF !ORIGIN
		CALL PASELI, @" 維持費:{BRアイテム価格(LCOUNT), 5}＄"
	IF BRITEM:LCOUNT == 0
		PRINT 【
		CALL PRINTCOLOR, "未配置", "gray"
		PRINT 】
	ELSE
		PRINT 【
		CALL PRINTCOLOR, "配置中", "deepskyblue"
		PRINT 】
	ENDIF
	表示数++
	SIF 表示数%3 == 0
		PRINTL 
NEXT
PRINTL 
DRAWLINE
PRINTPLAIN 　PERSONAL　
PRINTL [100] - 戻る

BINPUT
LRESULT = RESULT
SELECTCASE LRESULT
	CASE 100
		RETURN 0
	CASEELSE
		IF BRITEM:LRESULT == 0
			IF 娼館道具設置数 >= BR:広さ
				PRINTW 【これ以上配置できません】
			ELSE
				PRINTFORMW %ERDNAME(BRITEM, LRESULT)%を配置しました
				BRITEM:LRESULT = 1
				IF パートナー != ""
					SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
						TRYCALLFORM BR_ITEM_%CSVNAMERPS(GETNO(パートナー))%, LRESULT
				ENDIF
			ENDIF
		ELSE
			PRINTFORMW %ERDNAME(BRITEM, LRESULT)%を回収しました
			BRITEM:LRESULT = 0
		ENDIF
		RESTART
ENDSELECT

@PRINT_BR_ITEM
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 文字数
#DIM DYNAMIC 表示数

PRINTFORML 【設備】
FOR LCOUNT, GETNUM(BRITEM, "お風呂場"), GETNUM(BRITEM, "わけのわからないもの")+1
	SIF ERDNAME(BRITEM, LCOUNT) == ""
		CONTINUE
	SIF BRITEM:LCOUNT == 0
		CONTINUE
	SIF 文字数 == 0
		PRINT 　
	PRINTFORM [%ERDNAME(BRITEM, LCOUNT)%]
	文字数 += STRLENS(ERDNAME(BRITEM, LCOUNT))
	IF 文字数 > 80
		PRINTL 
		文字数 = 0
	ENDIF
	表示数++
NEXT
SIF !表示数
	PRINT 　[設備無し]
CALL NEWLINE

;娼館増設----------------------------------------------------
@BR_RENOVATION
#DIM DYNAMIC 費用, 2

DRAWLINE
費用 = POWER(BR:広さ, 2)*50000
費用:1 = POWER(2, BR:部屋数)*100000
PRINTL □物件を増設します
PRINTFORMLC 　広さ:{BR:広さ}
PRINTFORMLC 部屋数:{BR:部屋数}
PRINTL 
DRAWLINE
CALL PRINT_BR_ITEM
DRAWLINE

IF ORIGIN
	BR:広さ = 10
	BR:部屋数 = 20
ENDIF

IF BR:広さ >= 10
	CALL PRINTCOLORL, "　[-] - これ以上広く出来ません", "gray"
ELSE
	CALL PASELIL, @"　[1] - 広くする({費用}＄)"
ENDIF
IF BR:部屋数 >= 20
	CALL PRINTCOLORL, "　[-] - これ以上部屋を増やせません", "gray"
ELSE
	CALL PASELIL, @"　[2] - 部屋を増やす({費用:1}＄)"
ENDIF
PRINTFORML 　[3] - 設備を導入する
DRAWLINE
PRINTPLAIN 　RENOVATION　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		IF 費用 > MONEY
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		PRINTL 娼館を広くしますか？
		PRINTFORML 広さ {BR:広さ}→{BR:広さ+1}
		CALL PASELIL, @"増設後の所持金 {MONEY-費用}＄"
		PRINTL 
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SELECTCASE RESULT
			CASE 0
				PRINTW 娼館を広くしました
				MONEY -= 費用
				CALL 現金出納帳記録, @"娼館改築/-{費用}"
				BR:広さ++
				IF パートナー != ""
					SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
						TRYCALLFORM BR_RENOVATION_%CSVNAMERPS(GETNO(パートナー))%, 1
				ENDIF
		ENDSELECT
	CASE 2
		IF 費用:1 > MONEY
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		PRINTL 娼館の部屋を増やしますか？
		PRINTFORML 部屋数 {BR:部屋数}→{BR:部屋数+1}
		CALL PASELIL, @"増設後の所持金 {MONEY-費用:1}＄"
		PRINTL 
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		BINPUT
		SELECTCASE RESULT
			CASE 0
				PRINTW 娼館の部屋を増やしました
				MONEY -= 費用:1
				CALL 現金出納帳記録, @"娼館改築/-{費用:1}"
				BR:部屋数++
				IF パートナー != ""
					SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
						TRYCALLFORM BR_RENOVATION_%CSVNAMERPS(GETNO(パートナー))%, 2
				ENDIF
		ENDSELECT
	CASE 3
		CALL BR_ITEM_BUY
ENDSELECT
RESTART

;設備導入----------------------------------------------------
@BR_ITEM_BUY
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT, 2
#DIM DYNAMIC 単位文字数
#DIMS DYNAMIC 表示用
#DIM DYNAMIC LRESULT

表示数 = 0
DRAWLINE
PRINTL □設備を導入します
DRAWLINE
CALL PRINT_BR_ITEM
DRAWLINE

FOR LCOUNT, GETNUM(BRITEM, "お風呂場"), GETNUM(BRITEM, "わけのわからないもの")+1
	SIF ERDNAME(BRITEM, LCOUNT) == ""
		CONTINUE
	SIF 表示数 == 0
		PRINT 　
	IF BRITEM:LCOUNT
		SETCOLORBYNAME gray
		PRINTFORMLC [導入済み] %ERDNAME(BRITEM, LCOUNT)%
		RESETCOLOR
	ELSE
		表示用 = [{LCOUNT}] %ERDNAME(BRITEM, LCOUNT)%\@ !ORIGIN ? ({BRアイテム価格(LCOUNT)}＄)#\@
		IF ORIGIN
			PRINTFORMLC %表示用%
		ELSE
			CALL PASELI, 表示用
			;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
			IF OPTION:パセリ表示
				単位文字数 = 2
			ELSEIF 通貨単位 != ""
				単位文字数 = STRLENS(通貨単位)
			ELSE
				単位文字数 = 1
			ENDIF
			;＄の分2文字と通貨単位分
			FOR LCOUNT:1, 0, 28-(STRLENS(表示用)-2)-単位文字数
				PRINT  
			NEXT
		ENDIF
	ENDIF
	表示数++
	IF 表示数 == 4
		CALL NEWLINE
		表示数 = 0
	ENDIF
NEXT
CALL NEWLINE

DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASEELSE
		IF BRアイテム価格(RESULT) > MONEY && !ORIGIN
			PRINTW 所持金が足りません
			RESTART
		ENDIF
		DRAWLINE
		LRESULT = RESULT
		PRINTFORM %ERDNAME(BRITEM, RESULT)%:
		SELECTCASE ERDNAME(BRITEM, RESULT)
			CASE "お風呂場"
				PRINTL 性行為の前には体を洗いましょう
			CASE "高級ベッド"
				PRINTL 広々とした空間で新たなプレイを
			CASE "隠しカメラ"
				PRINTL マジックミラーの後ろに仕込む隠しカメラ
			CASE "換気扇"
				PRINTL 臭いと不快感を減らす
			CASE "ピンクライト"
				PRINTL 売春の雰囲気UP!
			CASE "防音壁"
				PRINTL 外に声が漏れないので遠慮なく楽しめます
			CASE "拷問器具"
				PRINTL 普通のSMプレイにも飽きた人へ
			CASE "わけのわからないもの"
				PRINTL わけのわからない人たちが使う
		ENDSELECT
		SIF !ORIGIN
			CALL PASELIL, @"導入後の所持金 {MONEY}＄→{MONEY-BRアイテム価格(RESULT)}＄"
		PRINTL [0] - 導入する
		PRINTL [1] - 導入しない
		BINPUT
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW %ERDNAME(BRITEM, LRESULT)%を導入しました
				BRITEM:LRESULT = 1
				MONEY -= BRアイテム価格(LRESULT)
				CALL 現金出納帳記録, @"「%ERDNAME(BRITEM, LRESULT)%」購入/-{BRアイテム価格(LRESULT)}"
				IF パートナー != ""
					SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
						TRYCALLFORM BR_ITEM_%CSVNAMERPS(GETNO(パートナー))%, LRESULT
				ENDIF
		ENDSELECT
		RESTART
ENDSELECT


;運営方針----------------------------------------------------
@BR_PLAN
#DIM DYNAMIC 現上級娼婦
#DIM DYNAMIC LVARLINE
;CALL BR_ITEM_POINT
SIF LVARLINE > 0
	CLEARLINE LINECOUNT-LVARLINE
LVARLINE = LINECOUNT

DRAWLINE
PRINTL 【現在のポイント】
CALL PRINTCOLORL, @"　ソフト - {ソフトポイント, 3}", @"\@ BR:方針 == 1 ?# gray \@"
CALL PRINTCOLORL, @"　ハード - {ハードポイント, 3}", @"\@ BR:方針 == 2 ?# gray \@"
CALL PRINTCOLORL, @"　SM     - {SMポイント, 3}", @"\@ BR:方針 == 3 ?# gray \@"
CALL PRINTCOLORL, @"　狂気   - {狂気ポイント, 3}", @"\@ BR:方針 == 4 ?# gray \@"
DRAWLINE

PRINTL □方針を変更する
PRINT 　
CALL PRINTCOLOR, "[1] - ソフト方針 ", @"\@ BR:方針 == 1 ? deepskyblue # \@"
CALL PRINTCOLOR, "[2] - ハード方針 ", @"\@ BR:方針 == 2 ? deepskyblue # \@"
CALL PRINTCOLOR, "[3] - SM方針 ", @"\@ BR:方針 == 3 ? deepskyblue # \@"
CALL PRINTCOLOR, "[4] - 狂気方針 ", @"\@ BR:方針 == 4 ? deepskyblue # \@"
PRINTL 

PRINTL □客の性別
PRINT 　
IF 娼館方針名(BR:方針) == "ソフト方針"
	CALL PRINTCOLOR, "[-] - ソフト方針では男性客のみの対応となります", "gray"
ELSE
	CALL PRINTCOLOR, "[5] - 固定しない ", @"\@ BR:性別固定 == 0 ? deepskyblue # \@"
	CALL PRINTCOLOR, "[6] - 男性客専門店にする ", @"\@ BR:性別固定 == 1 ? deepskyblue # \@"
	CALL PRINTCOLOR, "[7] - 女性客専門店にする ", @"\@ BR:性別固定 == 2 ? deepskyblue # \@"
ENDIF
PRINTL 

PRINTFORML □その他オプション
PRINTFORML 　[8] - プレイ内容の報告（現在:\@ FLAG:娼館報告 ? 報告する # 報告しない \@）
PRINTFORML 　[9] - 上級娼婦を任命する（現在の上級娼婦:\@ FINDCHARA(CFLAG:上級娼婦, 1) >= 0 ? %NAME:(FINDCHARA(CFLAG:上級娼婦, 1))% # 無し\@）
DRAWLINE
PRINTPLAIN 　PLAN　
PRINT [0] 戻る 
PRINTL [20] 各方針の説明

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1 TO 4
		BR:方針 = RESULT
		IF パートナー != ""
			SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
				TRYCALLFORM BR_PLAN_%CSVNAMERPS(GETNO(パートナー))%
		ENDIF
	CASE 5 TO 7
		BR:性別固定 = RESULT-5
	CASE 8
		INVERTBIT FLAG:娼館報告, 0
	CASE 9
		現上級娼婦 = FINDCHARA(CFLAG:上級娼婦, 1)
		SIF 現上級娼婦 >= 0
			CFLAG:現上級娼婦:上級娼婦 = 0
		DRAWLINE
		PRINTL □上級娼婦に任命したいキャラを選択してください
		FOR PAGING, 0, CHARANUM
			CALL PAGING, CSTR:PAGING:配属 == "娼館" && FALLTYPE(PAGING) == 2, "", "上級娼婦", "任命しない"
		NEXT
		SELECTCASE RESULT
			CASE -1
				PRINTW 上級娼婦を任命しません
			CASEELSE
				PRINTFORMW %CALLNAME:RESULT%を上級娼婦に任命します
				CFLAG:RESULT:上級娼婦 = 1
				SIF !CFLAG:RESULT:上級娼婦に任命された経験
					CFLAG:RESULT:名刺 = 5
				CFLAG:RESULT:上級娼婦に任命された経験++
		ENDSELECT
	CASE 20
		;PRINTL 0-方針無し
		;PRINTL ├特に方針を指定しません。初期設定でもあります。
		;PRINTL ├無計画ということなので方針としての効果は皆無。
		;PRINTL └何かしら方針は定めたほうが良いでしょう。
		;PRINTL 
		PRINTL □方針の変更
		PRINTL 【ソフト方針】
		PRINTL  ├本番無しなど店側の制約を多く設けた方針です。
		PRINTL  ├体力や気力の消費が軽く経験の浅い奴隷でもそれなりの効果があります。
		PRINTL  └しかし基本的に経験が上がりにくく、調教としての効果は薄いかもしれません。
		PRINTL 【ハード方針】
		PRINTL  ├経験豊富で淫乱な奴隷ほど効果が高まる方針です。
		PRINTL  ├各種経験に多めの数値が入り、収入も安定しています。
		PRINTL  └経験が浅く適正でない奴隷の場合、収入も低く体力消費も大きいため序盤にはおすすめできません。
		PRINTL 【SM方針】
		PRINTL  ├いろいろとマニアックなプレイを専門とする方針です。
		PRINTL  ├一部素質で強力なプラス補正がかかり、逆にこういったプレイに向かない奴隷の場合は
		PRINTL  └多大な体力消費など、マイナスの効果が大きいかもしれません。
		PRINTL 【狂気方針】
		PRINTL  ├ごく一部の稀有なクライアント向けの方針です。
		PRINTL  ├脱法的な経営なので、客は少なく、ランクが低いとまったく収入がありません。
		PRINTL  └崩壊した奴隷や特殊な素質を持った奴隷の効果が高く、玄人向けの方針といえるでしょう。
		PRINTL 
		PRINTL □客の性別
		PRINTL ├娼館に訪れる客の性別を男女どちらかに固定するかランダムにするか選ぶ事ができます。
		PRINTL └ソフト方針を選んでいる場合は自動的に男性客専門店の扱いになります。
		PRINTL 
		PRINTL □プレイ内容を報告させる
		PRINTL ├売春での奉仕内容を収入計算時に報告させます。
		PRINTL └オンオフで収入に違いは無いので奴隷の動向が気になるならオンにしてみましょう。
		PRINTL 
		PRINTL □上級娼婦を任命する
		PRINTL ├淫乱、もしくは娼婦の素質を持ったキャラを上級娼婦に任命できます。
		PRINTL ├上級娼婦に任命されたキャラは、売春中にプレイアブル可能になります。
		PRINTL ├運営方針に応じて可能な行為、効果的な行為が変わり、お客様の満足度によって得られる報酬も増減します。
		PRINTW └お客様とのより濃厚な売春を楽しみたいならぜひ任命してみましょう。
ENDSELECT

RESTART

;道具名----------------------------------------------------
@BR_ITEM_POINT
#DIM DYNAMIC LCOUNT
ソフトポイント = 0
ハードポイント = 0
SMポイント = 0
狂気ポイント = 0

FOR LCOUNT, GETNUM(BRITEM, "ローター"), GETNUM(BRITEM, "お風呂場")
	SIF ERDNAME(BRITEM, LCOUNT) == ""
		CONTINUE
	SIF !BRITEM:LCOUNT
		CONTINUE
	SELECTCASE ERDNAME(BRITEM, LCOUNT)
		CASE "ローター"
			ソフトポイント += 20
			ハードポイント += 10
			狂気ポイント += 5
		CASE "バイブ"
			ハードポイント += 10
			SMポイント += 20
			狂気ポイント += 15
		CASE "アナルバイブ"
			ハードポイント += 5
			SMポイント += 25
			狂気ポイント += 15
		CASE "鞭"
			SMポイント += 30
			狂気ポイント += 20
		CASE "縄"
			SMポイント += 25
			狂気ポイント += 35
		CASE "ペニスバンド"
			ハードポイント += 10
			SMポイント += 30
			狂気ポイント += 5
		CASE "コンドーム"
			ソフトポイント += 5
			ハードポイント += 20
			SMポイント += 15
			狂気ポイント += 5
		CASE "ローション"
			ソフトポイント += 15
			ハードポイント += 20
			SMポイント += 10
			狂気ポイント += 5
		CASE "媚薬"
			ソフトポイント += 20
			ハードポイント += 40
			SMポイント += 35
			狂気ポイント += 30
		CASE "睡眠薬"
			ハードポイント += 5
			SMポイント += 10
			狂気ポイント += 30
		CASE "ゴム手袋"
			ソフトポイント += 15
			狂気ポイント += 5
	ENDSELECT
NEXT

SIF BRITEM:ピンクライト
	TIMES ソフトポイント , 1.50
SIF BRITEM:防音壁
	TIMES ハードポイント , 1.50
SIF BRITEM:拷問器具
	TIMES SMポイント , 1.50
SIF BRITEM:わけのわからないもの
	TIMES 狂気ポイント , 1.50


;人員配置----------------------------------------------------
@SELECT_ROOM_BR
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 娼館部屋状態, 21, 2
#DIM DYNAMIC 選択部屋
#DIM DYNAMIC 部屋番号
DRAWLINE

VARSET 娼館部屋状態:0:0, 0

FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "娼館"
		部屋番号 = CFLAG:LCOUNT:娼館部屋番号
		IF 娼館部屋状態:部屋番号:0 == 0
			娼館部屋状態:部屋番号:0 = LCOUNT
		ELSE
			娼館部屋状態:部屋番号:1 = LCOUNT
		ENDIF
	ENDIF
NEXT

PRINTL □キャラを配属させる部屋を選んでください
FOR LCOUNT, 1, BR:部屋数+1
	PRINTFORM 　[{LCOUNT}] {LCOUNT}番ルーム
	IF 娼館部屋状態:LCOUNT:0
		PRINTFORM (%CALLNAME:(娼館部屋状態:LCOUNT:0)%
		SIF 娼館部屋状態:LCOUNT:1
			PRINTFORM と%CALLNAME:(娼館部屋状態:LCOUNT:1)%
		PRINTL が待機中)
	ELSE
		CALL PRINTCOLORL, "(空き部屋)", "gray"
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　娼館配属　
PRINTL [0] 戻る

BINPUT
選択部屋 = RESULT
SIF 選択部屋 == 0
	RETURN

IF 娼館部屋状態:選択部屋:0 && 娼館部屋状態:選択部屋:1
	PRINTFORML これ以上{選択部屋}番ルームには配属できません
	PRINTL [0] - 仕方ないな
	PRINTL [1] - その部屋に配属してるキャラを外してくれ
	BINPUT
	IF RESULT == 1
		CALL 配属解除, 娼館部屋状態:選択部屋:0
		CALL 配属解除, 娼館部屋状態:選択部屋:1
	ENDIF
	RESTART
ENDIF
CALL STRATEGY_PERSONAL, "娼館", RESULT
RESTART

;収入計算----------------------------------------------------
@INCOME_BR
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 収入合計
#DIM DYNAMIC 支出
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LMASTER
#DIM DYNAMIC LASSI
#DIM DYNAMIC 部屋配属人数
#DIM DYNAMIC 対象
#DIM DYNAMIC ノルマ

CVARSET 通常出勤

対象 = FINDCHARA(CFLAG:上級娼婦, 1)
IF 対象 >= 0
	;方針設定してないとダメ
	SIF BR:方針 == 0
		GOTO 通常営業
	;体力不足
	IF BASE:対象:体力 <= 1000
		PRINTFORML %CALLNAME:対象%は疲労で娼館を休みました
		CFLAG:対象:休憩 = 1
		GOTO 通常営業
	ENDIF
	IF CFLAG:対象:ストレス値 > 300 && !TALENT:対象:崩壊
		PRINTFORML %CALLNAME:対象%はストレス過多のため、娼館を休みました
		CFLAG:対象:休憩 = 1
		GOTO 通常営業
	ENDIF
	;サボり魔はモチベに応じてサボる
	IF サボり判定(LCOUNT)
		PRINTFORML %CALLNAME:対象%は売春をサボりました
		CFLAG:対象:ストレス値 -= 5
		CFLAG:対象:休憩 = 1
		GOTO 通常営業
	ENDIF
	LTARGET = TARGET
	LMASTER = MASTER
	LASSI = ASSI
	CALL 上級売春
	TARGET = LTARGET
	MASTER = LMASTER
	ASSI = LASSI
ENDIF

$通常営業

FOR LCOUNT, 1, BR:部屋数+1
	CALL CHECKDRAWLINE
	部屋配属人数 = 0
	FOR LCOUNT2, 1, CHARANUM
		;当然配属してないとダメ
		SIF CSTR:LCOUNT2:配属 != "娼館"
			CONTINUE
		;部屋番号違うならダメ
		SIF CFLAG:LCOUNT2:娼館部屋番号 != LCOUNT
			CONTINUE
		;配属完了してないとダメ 娼館なだけに召喚酔いってか
		SIF !CFLAG:LCOUNT2:配属完了
			GOTO 売春不可
		;休憩フラグ立ってるキャラの部屋は売春不可なので除外
		SIF CFLAG:LCOUNT2:休憩
			GOTO 売春不可
		;上級娼婦は上で処理してるので除外
		SIF CFLAG:LCOUNT2:上級娼婦 && BR:方針
			GOTO 売春不可
		;以下、売春できないキャラが居る部屋は収入の対象にならない
		;体力不足
		IF BASE:LCOUNT2:体力 <= 1000
			PRINTFORML %CALLNAME:LCOUNT2%は疲労で娼館を休みました
			GOTO 売春不可
		ENDIF
		;ストレス高
		IF CFLAG:LCOUNT2:ストレス値 > 300 && !TALENT:LCOUNT2:崩壊
			PRINTFORML %CALLNAME:LCOUNT2%はストレス過多のため、娼館を休みました
			GOTO 売春不可
		ENDIF
		;サボり魔はモチベに応じてサボる
		IF サボり判定(LCOUNT2)
			PRINTFORML %CALLNAME:LCOUNT2%は売春をサボりました
			CFLAG:LCOUNT2:休憩 = 1
			GOTO 売春不可
		ENDIF
		;上記の要素をすべてクリアしたキャラにフラグを立てる
		通常出勤:LCOUNT2:0 = 1
		部屋配属人数++
		;ついでにストレスも下げておく
		IF TALENT:LCOUNT2:淫乱
			CFLAG:LCOUNT2:ストレス値 -= 10
		ELSEIF TALENT:LCOUNT2:娼婦
			CFLAG:LCOUNT2:ストレス値 -= 15
		ENDIF
	NEXT

	;上記のループを抜けられたならこの部屋は売春可能 部屋の配属人数に応じて関数を呼ぶ
	SELECTCASE 部屋配属人数
		;ソロ
		CASE 1
			CALL ソロ売春, LCOUNT
			収入合計 += RESULT
			CONTINUE
		CASE 2
			CALL ペア売春, LCOUNT
			収入合計 += RESULT
			CONTINUE
	ENDSELECT
	
	$売春不可
	;ここに到達した場合はその部屋は売春不可能なので休憩フラグを立てる
	FOR LCOUNT2, 1, CHARANUM
		SIF CSTR:LCOUNT2:配属 != "娼館"
			CONTINUE
		IF CFLAG:LCOUNT2:娼館部屋番号 == LCOUNT
			CFLAG:LCOUNT2:休憩 = 1
			CFLAG:LCOUNT2:配属完了 = 1
			CONTINUE
		ENDIF
	NEXT
NEXT

CALL CHECKDRAWLINE

;方針とポイントに応じて上下する
SELECTCASE BR:方針
	;ソフト
	CASE 1
		収入合計 += 収入合計*ソフトポイント/100
	;ハード
	CASE 2
		収入合計 += 収入合計*ハードポイント/100
	;SM
	CASE 3
		収入合計 += 収入合計*SMポイント/100
	;狂気
	CASE 4
		収入合計 += 収入合計*狂気ポイント/100
ENDSELECT

;隠し撮りビデオを売却して追加報酬
SIF BRITEM:隠しカメラ
	収入合計 += 収入合計/10

IF 収入合計
	CALL PASELIL, @"%BRS:店名%の収入が{収入合計}＄入りました"
	MONEY += 収入合計
	BR:前日の収入 = 収入合計
	BR:総収入 += 収入合計
	バンめし = 0
	DO
		ノルマ = 0
		;高級ベッドがあるとレベルが上がりやすくなる
		IF BRITEM:高級ベッド
			ノルマ = 700*POWER(BR:レベル, 2)
		ELSE
			ノルマ = 1000*POWER(BR:レベル, 2)
		ENDIF
		SIF BR:総収入 >= ノルマ
			BR:レベル++
	LOOP BR:総収入 >= ノルマ

	BR:レベルアップ必要額 = ノルマ-BR:総収入
	SIF BR:レベル > 1000
		BR:レベル = 1000

	FOR LCOUNT, 0, GETNUM(BRITEM, "お風呂場")
		IF BRITEM:LCOUNT
			支出 += BRアイテム価格(LCOUNT)
			支出 += BRアイテム価格(LCOUNT)/5*浪費癖人数("娼館")
		ENDIF
	NEXT
	SIF 支出
		CALL PASELIL, @"%BRS:店名%の支出は{支出}＄でした"
	MONEY -= 支出
	CALL 現金出納帳記録, @"娼館収支/{収入合計-支出}"
ENDIF

@ソロ売春, 部屋番号
#DIM DYNAMIC 収入
#DIM DYNAMIC 部屋番号

BRREP:売春キャラ = FINDCHARA(CFLAG:娼館部屋番号, 部屋番号)

;1000パセリは保証
収入 = 1000
CALL 娼婦補正, BRREP:売春キャラ
収入 += RESULT
;プレミアムフライデー
SIF PREMIUM_FRIDAY(YEAR, MONTH, DAYS)
	TIMES 収入, 1.10

SELECTCASE BR:方針
	CASE 1
		BRREP:絶頂 = RAND:6
		;BRREP:射精 = RAND:6
		BRREP:自慰 = RAND:4
		SIF BRREP:自慰 && PENIS(BRREP:売春キャラ)
			BRREP:自慰で射精 = RAND:2
		BRREP:フェラ = RAND:8+3
		BRREP:フェラで射精 = RAND:3
		IF PENIS(BRREP:売春キャラ)
			BRREP:兜合わせ = RAND:3
			SIF BRREP:兜合わせ
				BRREP:兜合わせで射精 = RAND:2
			BRREP:射精 = RAND:3+BRREP:自慰で射精
		ELSE
			BRREP:素股 = RAND:3
			SIF BRREP:素股
				BRREP:素股で射精 = RAND:2
		ENDIF
		BRREP:足コキ = RAND:3
		SIF BRREP:足コキ == 2
			BRREP:足コキで射精 = RAND:2
		BRREP:手コキ = RAND:3
		SIF BRREP:手コキ == 2
			BRREP:手コキで射精 = RAND:2
		SIF SEX(BRREP:売春キャラ) == 1
			BRREP:ゲイ = RAND:4+1
		BRREP:客の射精 = BRREP:フェラで射精+BRREP:手コキで射精+BRREP:足コキで射精
	CASE 2
		;稀にふたなり女性客が来る
		SIF RANDPER(20)
			BRREP:客の性別 = 3
		;女性固定の場合はふたなりのみ
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = 3
		ENDSELECT
		IF VAGINA(BRREP:売春キャラ)
			BRREP:ヴァギナ = RAND(1, 11)
		ELSE
			BRREP:アナル = RAND(1, 11)
		ENDIF
		BRREP:絶頂 = RAND:6
		BRREP:フェラ = RAND:2
		BRREP:フェラで射精 = BRREP:フェラ
		SIF BRREP:アナル && BRREP:客の性別 != 2
			BRREP:アナル中出し = RAND:3
		SIF BRREP:ヴァギナ && BRREP:客の性別 != 2
			BRREP:中出し = RAND:3
		SIF PENIS(BRREP:売春キャラ)
			BRREP:射精 = RAND:6
		SELECTCASE SEX(BRREP:売春キャラ)
			CASE 1
				SIF BRREP:客の性別 <= 1
					BRREP:ゲイ = RAND:10+3
			CASE 2
				SIF BRREP:客の性別 >= 2
					BRREP:レズ = RAND:10+3
		ENDSELECT
		SIF !BRREP:ヴァギナ && !BRREP:アナル
			BRREP:アナル++
		BRREP:客の射精 = BRREP:中出し+BRREP:アナル中出し+BRREP:フェラで射精
	CASE 3
		BRREP:客の性別 = RAND:4
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = RAND(2, 4)
		ENDSELECT
		;たまにマゾが来る
		IF BRREP:客の性別 != 2 && RANDPER(20)
			BRREP:手コキで射精 = RAND:3+1
			BRREP:手コキ = BRREP:手コキで射精
			BRREP:足コキで射精 = RAND:3+1
			BRREP:足コキ = BRREP:足コキで射精
		ELSE
			BRREP:アナル = RAND:4
			SIF VAGINA(BRREP:売春キャラ)
				BRREP:ヴァギナ = RAND:4
			BRREP:絶頂 = RAND:4
			BRREP:自慰 = RAND:4+3
			SIF BRREP:自慰 && PENIS(BRREP:売春キャラ)
				BRREP:自慰で射精 = RAND:3
			SIF BRREP:アナル && RANDPER(25)
				BRREP:アナル中出し = 1
			SIF BRREP:ヴァギナ && BRREP:客の性別 != 2 && RANDPER(30)
				BRREP:中出し = 1
			SIF !BRREP:アナル && !BRREP:ヴァギナ
				BRREP:アナル++
			BRREP:客の射精 = RAND:2
		ENDIF
		SIF PENIS(BRREP:売春キャラ)
			BRREP:射精 = RAND:6+BRREP:自慰で射精
		SIF BRREP:客の性別 != 2
			BRREP:フェラ = RAND:4
		SIF BRREP:フェラ
			BRREP:フェラで射精 = RAND:(BRREP:フェラ)
		SELECTCASE SEX(BRREP:売春キャラ)
			CASE 1
				SIF BRREP:客の性別 <= 1
					BRREP:ゲイ = RAND:4+2
			CASE 2
				SIF BRREP:客の性別 >= 2
					BRREP:レズ = RAND:4+2
		ENDSELECT
		BRREP:客の射精 = BRREP:中出し+BRREP:アナル中出し+BRREP:手コキで射精+BRREP:足コキで射精+BRREP:フェラで射精
	CASE 4
		BRREP:客の性別 = RAND:4
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = RAND(2, 4)
		ENDSELECT
		SIF VAGINA(BRREP:売春キャラ)
			BRREP:ヴァギナ = RAND:26+5
		BRREP:アナル = RAND:26+5
		SIF BRREP:客の性別 != 2
			BRREP:客の射精 = RAND:2
		SIF PENIS(BRREP:売春キャラ)
			BRREP:射精 = RAND:11
		SELECTCASE SEX(BRREP:売春キャラ)
			CASE 1
				SIF BRREP:客の性別 <= 1
					BRREP:ゲイ = RAND:10+1
			CASE 2
				SIF BRREP:客の性別 >= 2
					BRREP:レズ = RAND:10+1
		ENDSELECT
ENDSELECT

SIF FLAG:娼館報告 && BR:方針
	CALL ソロ売春_地の文

SIF !FLAG:娼館報告
	SKIPDISP 1

SIF BRREP:ヴァギナ
	CALL EXPUP, "Ｖ経験", BRREP:ヴァギナ, BRREP:売春キャラ
SIF BRREP:アナル
	CALL EXPUP, "Ａ経験", BRREP:アナル, BRREP:売春キャラ
SIF BRREP:ヴァギナ || BRREP:アナル
	CALL EXPUP, "性交経験", BRREP:ヴァギナ+BRREP:アナル, BRREP:売春キャラ
SIF BRREP:絶頂 || BRREP:射精
	CALL EXPUP, "絶頂経験", BRREP:絶頂+BRREP:射精, BRREP:売春キャラ
SIF BRREP:射精
	CALL EXPUP, "射精経験", BRREP:射精, BRREP:売春キャラ
IF BRREP:中出し
	CALL EXPUP, "膣射経験", BRREP:中出し, BRREP:売春キャラ
	モブ中出され回数:(BRREP:売春キャラ):0 += BRREP:中出し
ENDIF
SIF BRREP:アナル中出し
	モブアナル中出され回数:(BRREP:売春キャラ):0 += BRREP:アナル中出し
SIF BRREP:客の射精
	CALL EXPUP, "精液経験", BRREP:客の射精, BRREP:売春キャラ
SIF BRREP:自慰
	CALL EXPUP, "自慰経験", BRREP:自慰, BRREP:売春キャラ
SIF BRREP:フェラ
	CALL EXPUP, "フェラ経験", BRREP:フェラ, BRREP:売春キャラ
SIF BRREP:レズ
	CALL EXPUP, "レズ経験", BRREP:レズ, BRREP:売春キャラ
SIF BRREP:ゲイ
	CALL EXPUP, "ゲイ経験", BRREP:ゲイ, BRREP:売春キャラ

SKIPDISP 0

IF BRREP:ヴァギナ && TALENT:(BRREP:売春キャラ):処女
	TALENT:(BRREP:売春キャラ):処女 = 0
	IF TALENT:(BRREP:売春キャラ):性転換済
		CSTR:(BRREP:売春キャラ):性転換処女 = 娼館に訪れた客
	ELSE
		CSTR:(BRREP:売春キャラ):処女 = 娼館に訪れた客
	ENDIF
ENDIF

TALENT:(BRREP:売春キャラ):傷物++
モブ経験人数:(BRREP:売春キャラ):0++
モブ経験回数:(BRREP:売春キャラ):0 += BRREP:ヴァギナ+BRREP:アナル
人気度:NOF((BRREP:売春キャラ)) += 10

CALL NEWLINE
VARSET BRREP
RETURN 収入

@ペア売春, 部屋番号
#DIM DYNAMIC 収入
#DIM DYNAMIC 相性
#DIM DYNAMIC 相性ボーナス
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 娼婦計算用
#DIM DYNAMIC 対象
#DIM DYNAMIC 表示済み
#DIM DYNAMIC 部屋番号

娼婦計算用 = 1
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:娼館部屋番号 == 部屋番号
		BRREP:@"売春キャラ{娼婦計算用++}" = LCOUNT
NEXT

;1000パセリは保証
収入 = 1000
FOR LCOUNT, 1, 3
	CALL 娼婦補正, BRREP:@"売春キャラ{LCOUNT}"
	収入 += RESULT
NEXT

;互いの相性値を乗算してその割合/10分上乗せ 互いに300%だったら900%（1.9倍）で、互いに50%だったら-75%（0.25倍）
相性 = 相性値(BRREP:売春キャラ1, BRREP:売春キャラ2)*相性値(BRREP:売春キャラ2, BRREP:売春キャラ1)/100

IF 相性 > 100
	収入 += 収入*相性/1000
ELSEIF 相性 < 100
	収入 -= 収入/100*(100-相性)
ENDIF

;血縁関係にあればさらに1.2倍
;近親相姦式中関数は血縁関係かどうかを判断してるのでここでも使う
SIF 近親相姦(BRREP:売春キャラ1, BRREP:売春キャラ2)
	TIMES 収入 , 1.20

;プレミアムフライデー
SIF PREMIUM_FRIDAY(YEAR, MONTH, DAYS)
	TIMES 収入, 1.10

SELECTCASE BR:方針
	CASE 1
		BRREP:絶頂1 = RAND:3
		BRREP:絶頂2 = RAND:3
		IF PENIS(BRREP:売春キャラ1) && PENIS(BRREP:売春キャラ2)
			;娼婦両方にペニスあるとたまに兜合わせを命じられる
			IF RANDPER(20)
				BRREP:売春キャラ兜合わせ = 1
				IF RANDPER(20)
					BRREP:売春キャラ兜合わせ射精1 = 1
					BRREP:射精1++
				ENDIF
				IF RANDPER(20)
					BRREP:売春キャラ兜合わせ射精2 = 1
					BRREP:射精2++
				ENDIF
				SIF SEX(BRREP:売春キャラ1) == 1
					BRREP:ゲイ1 += 2
				SIF SEX(BRREP:売春キャラ2) == 1
					BRREP:ゲイ2 += 2
			;同じくらいの確率で客が二本フェラしてくる
			ELSEIF RANDPER(20)
				二本フェラ = 1
				IF RANDPER(50)
					BRREP:売春キャラ二本フェラ射精1 = 1
					BRREP:射精1++
				ENDIF
				IF RANDPER(50)
					BRREP:売春キャラ二本フェラ射精2 = 1
					BRREP:射精2++
				ENDIF
			ENDIF
		ENDIF

		BRREP:ダブル自慰 = RAND:2
		IF BRREP:ダブル自慰
			IF PENIS(BRREP:売春キャラ1)
				BRREP:自慰で射精1 = RAND:2
				BRREP:射精1 += BRREP:自慰で射精1
			ELSE
				BRREP:自慰で絶頂1 = RAND:2
				BRREP:絶頂1 += BRREP:自慰で絶頂1
			ENDIF
			IF PENIS(BRREP:売春キャラ2)
				BRREP:自慰で射精2 = RAND:2
				BRREP:射精2 += BRREP:自慰で射精2
			ELSE
				BRREP:自慰で絶頂2 = RAND:2
				BRREP:絶頂2 += BRREP:自慰で絶頂2
			ENDIF
			BRREP:自慰1++
			BRREP:自慰2++
		ENDIF
		
		;相性が良いとそれに応じていろいろやる
		IF 相性 >= 200 && RANDPER(30)
			IF SEX(BRREP:売春キャラ1) == 2 && SEX(BRREP:売春キャラ2) == 2
				IF RANDPER(50)
					BRREP:ダブルパイズリ = 1
					BRREP:ダブルフェラ = 1
					BRREP:パイズリで射精 = RAND:3
					BRREP:フェラで射精 = RAND:3
					BRREP:客の射精1 += BRREP:パイズリで射精+BRREP:フェラで射精
					BRREP:客の射精2 += BRREP:パイズリで射精+BRREP:フェラで射精
					BRREP:フェラ1++
					BRREP:フェラ2++
				ELSE
					BRREP:ダブルパイズリ = 1
					BRREP:パイズリで射精 = RAND:4
					BRREP:客の射精1 += BRREP:パイズリで射精
					BRREP:客の射精2 += BRREP:パイズリで射精
					BRREP:フェラ1++
					BRREP:フェラ2++
				ENDIF
			ELSE
				BRREP:ダブルフェラ = 1
				BRREP:フェラで射精 = RAND:4
				BRREP:客の射精1 += BRREP:フェラで射精
				BRREP:客の射精2 += BRREP:フェラで射精
			ENDIF
		ELSE
			対象 = RAND(1, 3)
			BRREP:@"フェラ{対象}" = RAND(3, 5)
			BRREP:フェラで射精 = RAND:3
			BRREP:@"客の射精{対象}" += BRREP:フェラで射精
			対象 = RAND(1, 3)
			BRREP:@"手コキ{対象}" = RAND:3
			SIF BRREP:@"手コキ{対象}" == 2
				BRREP:手コキで射精 = RAND:2
			BRREP:@"客の射精{対象}" += BRREP:手コキで射精
		ENDIF

		IF 相性 >= 400 && VAGINA(BRREP:売春キャラ1) && VAGINA(BRREP:売春キャラ2) && RANDPER(30)
			BRREP:ダブル素股 = 1
			BRREP:素股で射精 = RAND(1, 2)
			BRREP:客の射精1 += 素股で射精
			BRREP:客の射精2 += 素股で射精
		ELSE
			対象 = RAND(1, 3)
			IF PENIS(BRREP:@"売春キャラ{対象}")
				BRREP:@"兜合わせ{対象}" = 1
				BRREP:兜合わせで射精 = RAND:2
				BRREP:@"射精{対象}" = RAND:3
				BRREP:@"客の射精{対象}" += BRREP:兜合わせで射精
			ELSE
				BRREP:@"素股{対象}" = 1
				BRREP:素股で射精 = RAND:2
				BRREP:@"客の射精{対象}" += BRREP:素股で射精
			ENDIF
		ENDIF

		SIF SEX(BRREP:売春キャラ1) == 1
			BRREP:ゲイ1 = RAND(1, 5)
		SIF SEX(BRREP:売春キャラ2) == 1
			BRREP:ゲイ2 = RAND(1, 5)
	CASE 2
		;稀にふたなり女性客が来る
		SIF RANDPER(20)
			BRREP:客の性別 = 3
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = 3
		ENDSELECT
		;相性が良いと交互に騎乗位
		IF 相性 >= 400 && RANDPER(30)
			BRREP:交互騎乗位 = 1
			IF VAGINA(BRREP:売春キャラ1)
				BRREP:ヴァギナ1 = RAND(3, 6)
				BRREP:中出し1 = RAND(2, 4)
			ELSE
				BRREP:アナル1 = RAND(3, 6)
				BRREP:アナル中出し1 = RAND(2, 4)
			ENDIF
			IF VAGINA(BRREP:売春キャラ2)
				BRREP:ヴァギナ2 = RAND(3, 6)
				BRREP:中出し2 = RAND(2, 4)
			ELSE
				BRREP:アナル2 = RAND(3, 6)
				BRREP:アナル中出し2 = RAND(2, 4)
			ENDIF
			BRREP:絶頂1 = RAND:6
			BRREP:絶頂2 = RAND:6
			SIF PENIS(BRREP:売春キャラ1)
				BRREP:射精1 = RAND:4
			SIF PENIS(BRREP:売春キャラ2)
				BRREP:射精2 = RAND:4
		ELSE
			;挿入パターンは3種類
			SELECTCASE RAND:3
				CASE 0
					IF VAGINA(BRREP:売春キャラ1)
						BRREP:ヴァギナ1 = RAND(5, 11)
						BRREP:中出し1 = RAND(1, 4)
					ELSE
						BRREP:アナル1 = RAND(5, 11)
						BRREP:アナル中出し1 = RAND(1, 4)
					ENDIF
					BRREP:絶頂1 = RAND:6
					SIF PENIS(BRREP:売春キャラ1)
						BRREP:射精1 = RAND:4
				CASE 1
					IF VAGINA(BRREP:売春キャラ2)
						BRREP:ヴァギナ2 = RAND(5, 11)
						BRREP:中出し2 = RAND(1, 4)
					ELSE
						BRREP:アナル2 = RAND(5, 11)
						BRREP:アナル中出し2 = RAND(1, 4)
					ENDIF
					BRREP:絶頂2 = RAND:6
					SIF PENIS(BRREP:売春キャラ2)
						BRREP:射精2 = RAND:4
				CASE 2
					IF VAGINA(BRREP:売春キャラ1)
						BRREP:ヴァギナ1 = RAND(3, 6)
						BRREP:中出し1 = RAND(1, 3)
					ELSE
						BRREP:アナル1 = RAND(3, 6)
						BRREP:アナル中出し1 = RAND(1, 3)
					ENDIF
					IF VAGINA(BRREP:売春キャラ2)
						BRREP:ヴァギナ2 = RAND(3, 6)
						BRREP:中出し2 = RAND(1, 3)
					ELSE
						BRREP:アナル2 = RAND(3, 6)
						BRREP:アナル中出し2 = RAND(1, 3)
					ENDIF
					BRREP:絶頂1 = RAND:6
					BRREP:絶頂2 = RAND:6
					SIF PENIS(BRREP:売春キャラ1)
						BRREP:射精1 = RAND:4
					SIF PENIS(BRREP:売春キャラ2)
						BRREP:射精2 = RAND:4
			ENDSELECT
		ENDIF
		BRREP:客の射精1 += BRREP:中出し1+BRREP:アナル中出し1
		BRREP:客の射精2 += BRREP:中出し2+BRREP:アナル中出し2

		;相性が良いとダブルフェラ
		IF 相性 >= 200
			BRREP:ダブルフェラ = 1
			BRREP:フェラで射精 = RAND:2
			BRREP:客の射精1 += フェラで射精
			BRREP:客の射精2 += フェラで射精
			BRREP:フェラ1++
			BRREP:フェラ2++
		ELSE
			対象 = RAND(1, 3)
			BRREP:@"フェラ{対象}" = RAND(1, 3)
			BRREP:フェラで射精 = RAND:2
			BRREP:@"客の射精{対象}" += BRREP:フェラで射精
		ENDIF
		SIF SEX(BRREP:売春キャラ1) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ1 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ2) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ2 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ1) == 2 && BRREP:客の性別 >= 2
			レズ:0 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ2) == 2 && BRREP:客の性別 >= 2
			BRREP:レズ2 = RAND(3, 10)
	CASE 3
		BRREP:客の性別 = RAND:4
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = RAND(2, 4)
		ENDSELECT
		;たまにマゾが来る
		IF BRREP:客の性別 <= 1 && RANDPER(20)
			対象 = RAND(1, 3)
			BRREP:@"手コキ{対象}" = 1
			BRREP:手コキで射精 = RAND(1, 4)
			BRREP:@"客の射精{対象}" += BRREP:手コキで射精
			IF 相性 >= 200
				BRREP:ダブル足コキで射精 = RAND(2, 5)
				BRREP:客の射精1 += BRREP:ダブル足コキで射精
				BRREP:客の射精2 += BRREP:ダブル足コキで射精
			ELSE
				BRREP:@"足コキ{(!BRREP:手コキ2)+1}" = 1
				BRREP:足コキで射精 = RAND(1, 4)
				BRREP:@"客の射精{(!BRREP:手コキ2)+1}" += BRREP:足コキで射精
			ENDIF
			;相性が良いと逆アナルコンビネーション
			IF 相性 >= 300
				BRREP:逆アナル責めで射精 = RAND(2, 5)
				BRREP:客の射精1 += BRREP:逆アナル責めで射精
				BRREP:客の射精2 += BRREP:逆アナル責めで射精
			ENDIF
		ELSE
			;どっちもペニスありなら我慢勝負を命じられる
			IF PENIS(BRREP:売春キャラ1) && PENIS(BRREP:売春キャラ2) && RANDPER(30)
				BRREP:射精我慢勝負 = 1
				BRREP:先に射精 = RAND(1, 3)
				BRREP:@"射精{BRREP:先に射精}"++
				;客が女性の場合、我慢できた方にご褒美
				IF BRREP:客の性別 == 2
					BRREP:@"客に中出し{(!BRREP:先に射精)+1}" = RAND(1, 3)
					BRREP:@"射精{(!BRREP:先に射精)+1}" += BRREP:@"客に中出し{(!BRREP:先に射精)+1}
				;客がペニスありの場合、先に射精したほうにお仕置きレイプ
				ELSE
					IF VAGINA(BRREP:@"売春キャラ{BRREP:先に射精}")
						BRREP:@"ヴァギナ{BRREP:先に射精}" = RAND(1, 4)
						BRREP:@"絶頂{BRREP:先に射精}" = RAND:4
						BRREP:@"中出し{BRREP:先に射精}" = RAND(1, 3)
						BRREP:@"客の射精{BRREP:先に射精}" += BRREP:@"中出し{BRREP:先に射精}"
					ELSE
						BRREP:@"アナル{BRREP:先に射精}" = RAND(1, 4)
						BRREP:@"絶頂{BRREP:先に射精}" = RAND:4
						BRREP:@"アナル中出し{BRREP:先に射精}" = RAND(1, 3)
						BRREP:@"客の射精{BRREP:先に射精}" += BRREP:@"アナル中出し{BRREP:先に射精}"
					ENDIF
				ENDIF
			;どっちも無しなら拘束プレイすることがある
			ELSEIF !PENIS(BRREP:売春キャラ1) && !PENIS(BRREP:売春キャラ2) && RANDPER(30) && BRREP:客の性別 != 2
				BRREP:拘束プレイ = 1
				;客が女性の場合、どっちも必ずイかされる
				IF BRREP:客の性別 == 2
					BRREP:絶頂1 = RAND(1, 4)
					BRREP:絶頂2 = RAND(1, 4)
				;竿ありだとどっちも必ず中出しされる
				ELSE
					BRREP:ヴァギナ1 = RAND(2, 5)
					BRREP:絶頂1 = RAND:4
					BRREP:中出し1 = RAND(2, 5)
					BRREP:客の射精1 += BRREP:中出し1
					BRREP:ヴァギナ2 = RAND(2, 5)
					BRREP:絶頂2 = RAND:4
					BRREP:中出し2 = RAND(2, 5)
					BRREP:客の射精2 += BRREP:中出し2
				ENDIF
			;上記の条件に満たなければダブル自慰
			ELSE
				BRREP:ダブル自慰 = 1
				IF PENIS(BRREP:売春キャラ1)
					BRREP:自慰で射精1 = RAND:2
					BRREP:射精1 += BRREP:自慰で射精1
				ELSE
					BRREP:自慰で絶頂1 = RAND:2
					BRREP:絶頂1 += BRREP:自慰で絶頂1
				ENDIF
				IF PENIS(BRREP:売春キャラ2)
					BRREP:自慰で射精2 = RAND:2
					BRREP:射精2 += BRREP:自慰で射精2
				ELSE
					BRREP:自慰で絶頂2 = RAND:2
					BRREP:絶頂2 += BRREP:自慰で絶頂2
				ENDIF
				BRREP:自慰1++
				BRREP:自慰2++
				;どちらかがイかされ地獄の餌食になる
				対象 = RAND(1, 3)
				IF PENIS(BRREP:@"売春キャラ{対象}")
					BRREP:@"射精地獄{対象}" = RAND(4, 8)
					BRREP:@"射精{対象}" += BRREP:@"射精地獄{対象}"
					BRREP:@"アナル{対象}" += RAND(2, 5)
					BRREP:@"絶頂{対象}" += RAND:2
					IF BRREP:客の性別 != 2
						BRREP:@"アナル中出し{対象}" += RAND(1, 4)
						BRREP:客の射精 += BRREP:@"アナル中出し{対象}"
					ENDIF
				ELSE
					BRREP:@"絶頂地獄{対象}" = RAND(4, 8)
					BRREP:@"絶頂{対象}" += BRREP:@"絶頂地獄{対象}"
					BRREP:@"ヴァギナ{対象}" = RAND(2, 5)
					BRREP:@"アナル{対象}" = RAND(2, 5)
					IF BRREP:客の性別 != 2
						BRREP:@"中出し{対象}" = RAND(1, 4)
						BRREP:@"アナル中出し{対象}" = RAND(1, 4)
						BRREP:@"客の射精{対象}" += BRREP:@"中出し{対象}"+BRREP:@"アナル中出し{対象}"
					ENDIF
				ENDIF
			ENDIF
		ENDIF

		;相性が良いとダブルフェラ
		IF BRREP:客の性別 != 2
			IF 相性 >= 200
				BRREP:ダブルフェラ = 1
				BRREP:フェラで射精 = RAND:2
				BRREP:客の射精1 += BRREP:フェラで射精
				BRREP:客の射精2 += BRREP:フェラで射精
				BRREP:フェラ1++
				BRREP:フェラ2++
			ELSE
				対象 = RAND(1, 3)
				BRREP:@"フェラ{対象}" = RAND(1, 3)
				BRREP:フェラで射精 = RAND:2
				BRREP:@"客の射精{対象}" += BRREP:フェラで射精
			ENDIF
		ENDIF
		SIF SEX(BRREP:売春キャラ1) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ1 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ2) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ2 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ1) == 2 && BRREP:客の性別 >= 2
			BRREP:レズ1 = RAND(3, 10)
		SIF SEX(BRREP:売春キャラ2) == 2 && BRREP:客の性別 >= 2
			BRREP:レズ2 = RAND(3, 10)
	CASE 4
		SELECTCASE BR:性別固定
			CASE 1
				BRREP:客の性別 = 0
			CASE 2
				BRREP:客の性別 = 3
		ENDSELECT
		BRREP:拘束プレイ = RAND:2
		IF VAGINA(BRREP:売春キャラ1)
			BRREP:ヴァギナ1 = RAND(10, 30)
			BRREP:中出し1 = RAND(15, 40)
			BRREP:客の射精1 += BRREP:中出し1
		ELSE
			BRREP:アナル1 = RAND(10, 30)
			BRREP:アナル中出し1 = RAND(15, 40)
			BRREP:客の射精1 += BRREP:アナル中出し1
		ENDIF
		IF VAGINA(BRREP:売春キャラ2)
			BRREP:ヴァギナ2 = RAND(10, 30)
			BRREP:中出し2 = RAND(15, 40)
			BRREP:客の射精2 += BRREP:中出し2
		ELSE
			BRREP:アナル2 = RAND(10, 30)
			BRREP:アナル中出し2 = RAND(15, 40)
			BRREP:客の射精2 += BRREP:アナル中出し2
		ENDIF
		BRREP:絶頂1 = RAND(5, 20)
		BRREP:絶頂2 = RAND(5, 20)
		SIF PENIS(BRREP:売春キャラ1)
			BRREP:射精1 = RAND(4, 10)
		SIF PENIS(BRREP:売春キャラ2)
			BRREP:射精2 = RAND(4, 10)

		SIF SEX(BRREP:売春キャラ1) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ1 = RAND(10, 20)
		SIF SEX(BRREP:売春キャラ2) == 1 && BRREP:客の性別 <= 1
			BRREP:ゲイ2 = RAND(10, 20)
		SIF SEX(BRREP:売春キャラ1) == 2 && BRREP:客の性別 >= 2
			BRREP:レズ1 = RAND(10, 20)
		SIF SEX(BRREP:売春キャラ2) == 2 && BRREP:客の性別 >= 2
			BRREP:レズ2 = RAND(10, 20)
ENDSELECT

SIF FLAG:娼館報告 && BR:方針
	CALL ペア売春_地の文

FOR LCOUNT, 1, 3
	SIF !FLAG:娼館報告
		SKIPDISP 1
	SIF BRREP:@"ヴァギナ{LCOUNT}"
		CALL EXPUP, "Ｖ経験", BRREP:@"ヴァギナ{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"アナル{LCOUNT}"
		CALL EXPUP, "Ａ経験", BRREP:@"アナル{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"ヴァギナ{LCOUNT}" || BRREP:@"アナル{LCOUNT}"
		CALL EXPUP, "性交経験", (BRREP:@"ヴァギナ{LCOUNT}")+(BRREP:@"アナル{LCOUNT}"), BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"絶頂{LCOUNT}" || BRREP:@"射精{LCOUNT}"
		CALL EXPUP, "絶頂経験", (BRREP:@"絶頂{LCOUNT}")+(BRREP:@"射精{LCOUNT}"), BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"射精{LCOUNT}"
		CALL EXPUP, "射精経験", BRREP:@"射精{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	IF BRREP:@"中出し{LCOUNT}"
		CALL EXPUP, "膣射経験", BRREP:@"中出し{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
		モブ中出され回数:(BRREP:@"売春キャラ{LCOUNT}"):0 += BRREP:@"中出し{LCOUNT}"
	ENDIF
	SIF BRREP:@"アナル中出し{LCOUNT}"
		モブアナル中出され回数:(BRREP:@"売春キャラ{LCOUNT}"):0 += BRREP:@"アナル中出し{LCOUNT}"
	SIF BRREP:@"客の射精{LCOUNT}"
		CALL EXPUP, "精液経験", BRREP:@"客の射精{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"自慰{LCOUNT}"
		CALL EXPUP, "自慰経験", BRREP:@"自慰{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"フェラ{LCOUNT}"
		CALL EXPUP, "フェラ経験", BRREP:@"フェラ{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	IF BRREP:ダブルパイズリ
		CALL EXPUP, "パイズリ経験", 1, BRREP:@"売春キャラ{LCOUNT}"
		モブパイズリ人数:(BRREP:@"売春キャラ{LCOUNT}"):0++
		モブパイズリ回数:(BRREP:@"売春キャラ{LCOUNT}"):0++
		モブ挟射回数:(BRREP:@"売春キャラ{LCOUNT}"):0++
	ENDIF
	SIF BRREP:@"レズ{LCOUNT}"
		CALL EXPUP, "レズ経験", BRREP:@"レズ{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SIF BRREP:@"ゲイ{LCOUNT}"
		CALL EXPUP, "ゲイ経験", BRREP:@"ゲイ{LCOUNT}", BRREP:@"売春キャラ{LCOUNT}"
	SKIPDISP 0
	IF BRREP:@"ヴァギナ{LCOUNT}" && TALENT:(BRREP:@"売春キャラ{LCOUNT}"):処女
		IF TALENT:(BRREP:@"売春キャラ{LCOUNT}"):性転換済
			CSTR:(BRREP:@"売春キャラ{LCOUNT}"):性転換処女 = 娼館に訪れた客
		ELSE
			CSTR:(BRREP:@"売春キャラ{LCOUNT}"):処女 = 娼館に訪れた客
		ENDIF
		TALENT:(BRREP:@"売春キャラ{LCOUNT}"):処女 = 0
		TALENT:(BRREP:@"売春キャラ{LCOUNT}"):再生処女 = 0
	ENDIF
	TALENT:(BRREP:@"売春キャラ{LCOUNT}"):傷物++
	人気度:(BRREP:@"売春キャラ{LCOUNT}") += 10
	モブ経験人数:(BRREP:@"売春キャラ{LCOUNT}"):0++
	モブ経験回数:(BRREP:@"売春キャラ{LCOUNT}"):0 += (BRREP:@"ヴァギナ{LCOUNT}")+(BRREP:@"アナル{LCOUNT}")
	SKIPDISP 0
NEXT

SIF !LINEISEMPTY()
	WAIT
VARSET BRREP
RETURN 収入

;ソロ売春とペア売春の補正が同じだけどゆくゆくは別の補正をかけるようにしたい
@娼婦補正, 売春キャラ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 売春キャラ
#DIM DYNAMIC 適性
#DIM DYNAMIC 不適性
#DIM DYNAMIC 収入
#DIM DYNAMIC 消耗量

;方針による補正
SELECTCASE BR:方針
	;ソフト
	CASE 1
		;ソフトでは適性-10
		不適性 = 10
		;淫乱系陥落は有利
		IF TALENT:売春キャラ:娼婦
			収入 += 500
			適性 += 3
		ELSEIF TALENT:売春キャラ:淫乱
			収入 += 300
			適性 += 2
		ENDIF
		;恋慕系陥落は不利
		IF TALENT:売春キャラ:親愛
			収入 -= 100
			不適性 += 2
		ELSEIF TALENT:売春キャラ:恋慕
			収入 -= 50
			不適性++
		ENDIF
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:売春キャラ:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				;適性の上がるプラス素質
				CASE "素直"
					収入 += 200
					適性 += 2
				CASE "献身的"
					収入 += 150
					適性 += 2
				CASE "男好き"
					収入 += 100
					適性 += 2
				CASE "痛みに強い"
					適性 += 2
				CASE "プライド低い", "好奇心", "淫核"
					収入 += 150
					適性++
				CASE "快感に素直", "人気"
					収入 += 100
					適性++
				CASE "大人しい", "自慰しやすい", "両刀"
					収入 += 50
					適性++
				CASE "恥薄い", "汚臭鈍感", "汚れ無視"
					適性++
				;収入へのプラス補正
				CASE "舌使い"
					収入 += 200
				CASE "臆病", "謎の魅力"
					収入 += 100
				CASE "ふたなり", "恥じらい", "魅力", "魅惑", "小悪魔"
					収入 += 50
				CASE "動物耳", "尻尾", "ファーリー"
					収入 += 30
				;マイナス素質
				CASE "男嫌い"
					収入 -= 50
					不適性 += 2
				CASE "抵抗", "痛みに弱い"
					不適性 += 2
				CASE "生意気", "反抗的", "同族嫌悪"
					収入 -= 50
					不適性++
				CASE "プライド高い"
					収入 -= 30
					不適性++
				CASE "保守的", "悲観的", "汚臭敏感", "潔癖症"
					不適性++
				CASE "ツンデレ", "無関心"
					収入 -= 30
				;体格、バストサイズ、小人の好みは人による
				CASE "体格"
					IF TALENT:売春キャラ:体格 <= 2
						IF RANDPER(50) 
							収入 += 100
						ELSE
							収入 -= 50
						ENDIF
						不適性++
					ENDIF
				CASE "バストサイズ"
						IF RANDPER(50) 
							収入 += MAX((7-TALENT:売春キャラ:バストサイズ)*10, 0)
						ELSE
							収入 += TALENT:売春キャラ:バストサイズ*10
						ENDIF
				CASE "小人体型"
					IF RANDPER(30)
						収入 += 200
					ELSE
						収入 -= 100
					ENDIF
					不適性 += 2
			ENDSELECT
		NEXT
		;ここから能力で倍率計算
		IF 統率者("娼館", CFLAG:売春キャラ:娼館部屋番号) && !TALENT:売春キャラ:統率
			収入 += (収入/10)*(ABL:売春キャラ:従順+1)
			適性 += (ABL:売春キャラ:従順+1)/3

			収入 += (収入/5)*(ABL:売春キャラ:欲望+1)
			適性 += (ABL:売春キャラ:欲望+1)/3

			収入 += (収入/2)*(ABL:売春キャラ:技巧+1)
			適性 += (ABL:売春キャラ:技巧+1)/2

			収入 += (収入/5)*(ABL:売春キャラ:奉仕精神+1)
			適性 += (ABL:売春キャラ:奉仕精神+1)/2
		ELSE
			収入 += (収入/10)*ABL:売春キャラ:従順
			適性 += ABL:売春キャラ:従順/3

			収入 += (収入/5)*ABL:売春キャラ:欲望
			適性 += ABL:売春キャラ:欲望/3

			収入 += (収入/2)*ABL:売春キャラ:技巧
			適性 += ABL:売春キャラ:技巧/2

			収入 += (収入/5)*ABL:売春キャラ:奉仕精神
			適性 += ABL:売春キャラ:奉仕精神/2
		ENDIF
	;ハード
	CASE 2
		;ハードは適性-15
		不適性 = 15
		;初物ボーナス
		SIF TALENT:売春キャラ:処女
			収入 += 2000
		;淫乱系陥落は有利
		IF TALENT:売春キャラ:娼婦
			収入 += 1000
			適性 += 2
		ELSEIF TALENT:売春キャラ:淫乱
			収入 += 500
			適性++
		ENDIF
		;恋慕系陥落は不利
		IF TALENT:売春キャラ:親愛
			収入 -= 300
			不適性 += 2
		ELSEIF TALENT:売春キャラ:恋慕
			収入 -= 150
			不適性++
		ENDIF
		;その他の素質
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:売春キャラ:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				;適性の上がるプラス素質
				CASE "献身的", "淫膣"
					収入 += 300
					適性 += 2
				CASE "快感に素直"
					収入 += 200
					適性 += 2
				CASE "貞操無頓着"
					収入 += 100
					適性 += 2
				CASE "痛みに強い"
					適性 += 2
				CASE "素直"
					収入 += 300
					適性++
				CASE "プライド低い", "好奇心", "人気"
					収入 += 200
					適性++
				CASE "大人しい", "男好き"
					収入 += 100
					適性++
				CASE "恥薄い", "汚れ無視", "Ｖ敏感", "Ａ敏感"
					適性++
				;収入へのプラス補正
				CASE "謎の魅力"
					収入 += 200
				CASE "恥じらい"
					収入 += 150
				CASE "ふたなり", "舌使い", "両刀", "魅力", "魅惑"
					収入 += 100
				CASE "動物耳", "尻尾", "ファーリー"
					収入 += 50
				;マイナス素質
				CASE "男嫌い"
					収入 -= 200
					不適性 += 3
				CASE "プライド高い"
					収入 -= 50
					不適性 += 2
				CASE "抵抗", "痛みに弱い", "潔癖症"
					不適性 += 2
				CASE "生意気", "反抗的", "同族嫌悪"
					収入 -= 100
					不適性++
				CASE "貞操観念"
					収入 -= 50
					不適性++
				CASE "保守的", "悲観的", "Ｖ鈍感", "Ａ鈍感"
					不適性++
				CASE "ツンデレ", "無関心"
					収入 -= 50
				;体格、バストサイズ、小人の好みは人による
				CASE "体格"
					IF TALENT:売春キャラ:体格 <= 2
						IF RANDPER(50)
							収入 += 300
						ELSE
							収入 -= 100
						ENDIF
						不適性++
					ENDIF
				CASE "バストサイズ"
					IF RANDPER(50)
						収入 += MAX((7-TALENT:売春キャラ:バストサイズ)*20, 0)
					ELSE
						収入 += TALENT:売春キャラ:バストサイズ*20
					ENDIF
				CASE "小人体型"
					IF RANDPER(30)
						収入 += 500
					ELSE
						収入 -= 300
					ENDIF
					不適性 += 2
			ENDSELECT
		NEXT
		;ここから能力で倍率計算
		IF 統率者("娼館", CFLAG:売春キャラ:娼館部屋番号) && !TALENT:売春キャラ:統率
			収入 += (収入/5)*(ABL:売春キャラ:従順+1)
			適性 += (ABL:売春キャラ:従順+1)/3

			収入 += (収入/5)*(ABL:売春キャラ:欲望+1)
			適性 += (ABL:売春キャラ:欲望+1)/2

			収入 += (収入/2)*(ABL:売春キャラ:技巧+1)
			適性 += (ABL:売春キャラ:技巧+1)/3

			IF VAGINA(売春キャラ)
				収入 += (収入/10)*(ABL:売春キャラ:Ｖ感覚+1)
				適性 += (ABL:売春キャラ:Ｖ感覚+1)/5
			ENDIF

			収入 += (収入/10)*(ABL:売春キャラ:Ａ感覚+1)
			適性 += (ABL:売春キャラ:Ａ感覚+1)/5

			収入 += (収入/5)*(ABL:売春キャラ:奉仕精神+1)
			適性 += (ABL:売春キャラ:奉仕精神+1)/3

			収入 += (収入/2)*(ABL:売春キャラ:性交中毒+1)
			適性 += (ABL:売春キャラ:性交中毒+1)/2
		ELSE
			収入 += (収入/5)*ABL:売春キャラ:従順
			適性 += ABL:売春キャラ:従順/3

			収入 += (収入/5)*ABL:売春キャラ:欲望
			適性 += ABL:売春キャラ:欲望/2

			収入 += (収入/2)*ABL:売春キャラ:技巧
			適性 += ABL:売春キャラ:技巧/3

			IF VAGINA(売春キャラ)
				収入 += (収入/10)*ABL:売春キャラ:Ｖ感覚
				適性 += ABL:売春キャラ:Ｖ感覚/5
			ENDIF

			収入 += (収入/10)*ABL:売春キャラ:Ａ感覚
			適性 += ABL:売春キャラ:Ａ感覚/5

			収入 += (収入/5)*ABL:売春キャラ:奉仕精神
			適性 += ABL:売春キャラ:奉仕精神/3

			収入 += (収入/2)*ABL:売春キャラ:性交中毒
			適性 += ABL:売春キャラ:性交中毒/2
		ENDIF
	;SM
	CASE 3
		;SMは適性がかなり上がりにくいので適性-5
		不適性 = 5
		;初物ボーナス
		SIF TALENT:売春キャラ:処女
			収入 += 1000
		;SMは我が強い子が結構稼げる 責めに回っても受けに回っても需要がある
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:売春キャラ:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				;適性の上がるプラス素質
				CASE "痛みに強い"
					収入 += 50
					適性 += 3
				CASE "強気"
					収入 += 150
					適性++
				CASE "戦士"
					収入 += 100
					適性++
				CASE "快感に素直"
					適性 += 2
				;収入へのプラス補正
				CASE "生意気", "反抗的", "倒錯的"
					収入 += 200
				CASE "ふたなり", "衝動的", "プライド高い", "抵抗", "貞操観念"
					収入 += 150
				CASE "気丈", "保守的", "一線越えない", "抑圧", "針さばき", "おもらし癖", "サド", "マゾ"
					収入 += 100
				CASE "快感の否定", "弱味", "両刀", "即落ち"
					収入 += 50
				;マイナス素質
				CASE "痛みに弱い"
					収入 -= 50
					不適性 += 5
				CASE "弱気"
					不適性 += 3
				CASE "恥じらい"
					収入 -= 50
					不適性 += 2
				CASE "無関心"
					収入 -= 150
					不適性++
				CASE "臆病", "大人しい", "感情乏しい", "寂しがり屋"
					収入 -= 100
					不適性++
				CASE "素直"
					収入 -= 50
					不適性++
			ENDSELECT
		NEXT
		;ここから能力で倍率計算
		IF 統率者("娼館", CFLAG:売春キャラ:娼館部屋番号) && !TALENT:売春キャラ:統率
			収入 += (収入/10)*(ABL:売春キャラ:従順+1)
			適性 += (ABL:売春キャラ:従順+1)/3+1

			収入 += (収入/10)*(ABL:売春キャラ:欲望+1)
			適性 += (ABL:売春キャラ:欲望+1)/5

			収入 += (収入/10)*(ABL:売春キャラ:技巧+1)
			適性 += (ABL:売春キャラ:技巧+1)/5

			IF VAGINA(売春キャラ)
				収入 += (収入/10)*(ABL:売春キャラ:Ｖ感覚+1)
				適性 += (ABL:売春キャラ:Ｖ感覚+1)/5
			ENDIF

			収入 += (収入/10)*(ABL:売春キャラ:Ａ感覚+1)
			適性 += (ABL:売春キャラ:Ａ感覚+1)/5

			収入 += (収入/5)*(ABL:売春キャラ:露出癖+1)
			適性 += (ABL:売春キャラ:露出癖+1)/5

			収入 += (収入/2)*(ABL:売春キャラ:マゾっ気+1)
			適性 += (ABL:売春キャラ:マゾっ気+1)/2

			収入 += (収入/2)*(ABL:売春キャラ:サドっ気+1)
			適性 += (ABL:売春キャラ:サドっ気+1)/2
		ELSE
			収入 += (収入/10)*ABL:売春キャラ:従順
			適性 += ABL:売春キャラ:従順/3+1

			収入 += (収入/10)*ABL:売春キャラ:欲望
			適性 += ABL:売春キャラ:欲望/5

			収入 += (収入/10)*ABL:売春キャラ:技巧
			適性 += ABL:売春キャラ:技巧/5

			IF VAGINA(売春キャラ)
				収入 += (収入/10)*ABL:売春キャラ:Ｖ感覚
				適性 += ABL:売春キャラ:Ｖ感覚/5
			ENDIF

			収入 += (収入/10)*ABL:売春キャラ:Ａ感覚
			適性 += ABL:売春キャラ:Ａ感覚/5

			収入 += (収入/5)*ABL:売春キャラ:露出癖
			適性 += ABL:売春キャラ:露出癖/5

			収入 += (収入/2)*ABL:売春キャラ:マゾっ気
			適性 += ABL:売春キャラ:マゾっ気/2

			収入 += (収入/2)*ABL:売春キャラ:サドっ気
			適性 += ABL:売春キャラ:サドっ気/2
		ENDIF
	;ルナティック
	CASE 4
		;マイナス
		不適性 = 20
		;崩壊が有利
		IF TALENT:売春キャラ:崩壊
			収入 += 2000
			適性++
		ENDIF
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:売春キャラ:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "狂気"
					収入 += 1000
					適性++
				CASE "小人体型"
					収入 += 1000
				CASE "倒錯的", "妄信", "無機物", "リミットバースト"
					収入 += 500
					適性++
				CASE "ふたなり", "弱味", "ネクロマンサー"
					収入 += 300
					適性++
				CASE "性器破損", "吸血鬼"
					収入 += 500
				CASE "狐", "幼稚"
					収入 += 300
				CASE "ファーリー", "夢喰い", "ドラゴン使い"
					収入 += 200
				CASE "バストサイズ"
					SIF TALENT:売春キャラ:バストサイズ >= 6
						収入 += 200
				CASE "羽"
					収入 += 100
				;CASE "幼児／幼児退行"
				;	収入 += 300
			ENDSELECT
		NEXT
		;娼館レベルに応じて変動
		収入 = 収入*BR:レベル/50
		;ここから能力で倍率計算
		IF 統率者("娼館", CFLAG:売春キャラ:娼館部屋番号) && !TALENT:売春キャラ:統率
			収入 += (収入/10)*(ABL:売春キャラ:従順+1)
			適性 += (ABL:売春キャラ:従順+1)/3

			収入 += (収入/10)*(ABL:売春キャラ:欲望+1)
			適性 += (ABL:売春キャラ:欲望+1)/3

			収入 += (収入/10)*(ABL:売春キャラ:技巧+1)
			適性 += (ABL:売春キャラ:技巧+1)/3
		ELSE
			収入 += (収入/10)*ABL:売春キャラ:従順
			適性 += ABL:売春キャラ:従順/3

			収入 += (収入/10)*ABL:売春キャラ:欲望
			適性 += ABL:売春キャラ:欲望/3

			収入 += (収入/10)*ABL:売春キャラ:技巧
			適性 += ABL:売春キャラ:技巧/3
		ENDIF
ENDSELECT
;体力気力消費計算
消耗量 = MAX(不適性-適性, 1)
SELECTCASE BR:方針
	;ソフト
	CASE 1
		BASE:売春キャラ:体力 -= 消耗量*50
		BASE:売春キャラ:気力 -= 消耗量*30
	;ハード
	CASE 2
		BASE:売春キャラ:体力 -= 消耗量*100
		BASE:売春キャラ:気力 -= 消耗量*50
	;SM
	CASE 3
		BASE:売春キャラ:体力 -= 消耗量*300
		BASE:売春キャラ:気力 -= 消耗量*70
	;狂気
	CASE 4
		BASE:売春キャラ:体力 -= 消耗量*200
		BASE:売春キャラ:気力 -= 消耗量*150
ENDSELECT
SIF BASE:売春キャラ:体力 <= 0
	BASE:売春キャラ:体力 = 1

;最低1000は保証
CALL SETMAX, 収入, 1000

収入 += 収入*BR:レベル/100

SELECTCASE BR:方針
	CASE 1
		収入 += ソフトポイント*10
	CASE 2
		収入 += ハードポイント*10
	CASE 3
		収入 += SMポイント*10
	CASE 4
		収入 += 狂気ポイント*10
ENDSELECT

;お風呂場があるとストレス溜まりづらくなる
IF BRITEM:お風呂場
	CFLAG:売春キャラ:ストレス値 += MAX(不適性-適性, 0)
ELSE
	CFLAG:売春キャラ:ストレス値 += MAX((不適性-適性)*2, 0)
ENDIF
;換気口あるとさらに軽減
SIF BRITEM:換気口
	CFLAG:売春キャラ:ストレス値 -= 2
;実務経験を加算
EXP:売春キャラ:実務経験++

;適性-不適性をモチベーションに加算
CFLAG:売春キャラ:モチベーション = LIMIT(CFLAG:売春キャラ:モチベーション+適性-不適性, 1, 100)

;適性*10が習得に入る
SIF 適性 > 0
	JUEL:売春キャラ:習得 += 適性*10

RETURN 収入

@ソロ売春_地の文
IF OPTION:口上表示 && !CFLAG:(BRREP:売春キャラ):口上非表示
	TRYCCALLFORM BR_PLAY_%CSVNAMERPS(NOF(BRREP:売春キャラ))%
	CATCH
		GOTO NOTKOJO
	ENDCATCH
ELSE
	$NOTKOJO
	PRINTFORML %CALLNAME:(BRREP:売春キャラ)%が今日の仕事を報告してきた。
	PRINTFORM %CALLNAME:(BRREP:売春キャラ)%は
	SIF BR:方針 == 3 && BRREP:手コキで射精
		PRINT マゾヒストの
	SELECTCASE BRREP:客の性別
		CASE IS <= 1
			PRINT 男性客を相手に
		CASE 2
			PRINT 女性客を相手に
		CASE 3
			PRINT ふたなり女性客を相手に
	ENDSELECT
	IF BRREP:客の性別 != 2
		IF BRREP:フェラ
			PRINT 口で奉仕
			IF BRREP:フェラで射精
				PRINTFORM して{BRREP:フェラで射精}回射精させた
			ELSE
				PRINT した
			ENDIF
			IF BRREP:ヴァギナ || BRREP:アナル || BRREP:手コキ || BRREP:足コキ || BRREP:素股 || BRREP:兜合わせ
				PRINT 後、
			ELSE
				PRINT 。
			ENDIF
		ENDIF
		IF BRREP:手コキ || BRREP:足コキ
			IF BRREP:手コキ && BRREP:足コキ
				IF BRREP:手コキで射精 && BRREP:足コキで射精
					PRINTFORM 手で{BRREP:手コキで射精}回、足で{BRREP:足コキで射精}回イかせ
				ELSEIF BRREP:手コキで射精 || BRREP:足コキで射精
					PRINTFORM 手と足で扱いて{BRREP:手コキで射精+BRREP:足コキで射精}回イかせ
				ELSE
					PRINTFORM 手と足で扱
				ENDIF
			ELSEIF BRREP:手コキ
				IF BRREP:手コキで射精
					PRINTFORM 手で{BRREP:手コキで射精}回イかせ
				ELSE
					PRINT 手で扱
				ENDIF
			ELSEIF BRREP:足コキ
				IF BRREP:足コキで射精
					PRINTFORM 足で{BRREP:足コキで射精}回イかせ
				ELSE
					PRINT 足で扱
				ENDIF
			ENDIF
			IF BRREP:手コキで射精 || BRREP:足コキで射精
				IF BRREP:ヴァギナ || BRREP:アナル || BRREP:素股 || BRREP:兜合わせ
					PRINT 、
				ELSE
					PRINT た。
				ENDIF
			ELSE
				IF BRREP:ヴァギナ || BRREP:アナル || BRREP:素股 || BRREP:兜合わせ
					PRINT き、
				ELSE
					PRINT いたが、射精させるには至らなかった。
				ENDIF
			ENDIF
		ENDIF
		IF BRREP:兜合わせ
			PRINTFORM 客と陰茎を擦り合わせ
			IF BRREP:兜合わせで射精
				PRINTFORM て{BRREP:兜合わせで射精}回射精させた。
			ELSE
				PRINT たが、射精させるには至らなかった。
			ENDIF
		ENDIF
		IF BRREP:素股
			PRINTFORM 性行為の代わりに秘部を擦り合わせ
			IF BRREP:素股で射精
				PRINTFORM て{BRREP:素股で射精}回射精させた。
			ELSE
				PRINT たが、射精させるには至らなかった。
			ENDIF
		ENDIF
		IF BRREP:ヴァギナ || BRREP:アナル
			SIF BRREP:ヴァギナ
				PRINT 膣
			SIF BRREP:ヴァギナ && BRREP:アナル
				PRINT と
			SIF BRREP:アナル
				PRINT アナル
			PRINT で性行為に
			IF BRREP:中出し || BRREP:アナル中出し
				PRINT 及び、
				IF BRREP:中出し && BRREP:アナル中出し
					PRINTFORM 膣に{BRREP:中出し}回、アナルに{BRREP:アナル中出し}回中出しされた。
				ELSEIF BRREP:中出し
					PRINTFORM {BRREP:中出し}回中出しされた。
				ELSEIF BRREP:アナル中出し
					PRINTFORM {BRREP:アナル中出し}回中出しされた。
				ENDIF
			ELSE
				PRINT 及んだが、射精させるには至らなかった。
			ENDIF
		ENDIF
		PRINTL 
	ENDIF
	IF (BRREP:アナル || BRREP:ヴァギナ) && (BRREP:絶頂 || BRREP:射精)
		SIF BRREP:客の性別 != 2
			PRINTFORM %CALLNAME:(BRREP:売春キャラ)%は
		SIF BRREP:アナル
			PRINT アナル
		SIF BRREP:アナル && BRREP:ヴァギナ
			PRINT や
		SIF BRREP:ヴァギナ
			PRINT 膣内
		PRINT を
		SIF BRREP:客の性別 == 2
			PRINT 指や道具で
		SIF BRREP:ヴァギナ > 7 || BRREP:アナル > 7
			PRINT 激しく
		PRINT 犯された
		IF !BRREP:射精 && !BRREP:絶頂
			PRINTL ようだ。
		ELSE
			PRINT 挙句、
			IF BRREP:中出し
				PRINT 膣に中出しされながら
			ELSEIF BRREP:アナル中出し
				PRINT アナルに中出しされながら
			ELSEIF BRREP:ヴァギナ > BRREP:アナル
				PRINT ヴァギナへの快感で
			ELSE
				PRINT アナルへの快感で
			ENDIF
			SIF BRREP:射精 >= 3 || BRREP:絶頂 >= 3
				PRINT 何回も
			IF BRREP:射精
				PRINTL 射精してしまったようだ。
			ELSE
				PRINTL 絶頂に達してしまったようだ。
			ENDIF
		ENDIF
	ENDIF
	IF BRREP:自慰
		PRINT その後、自慰を命じられ
		IF BRREP:自慰で射精
			PRINTFORML 、%CALLNAME:(BRREP:売春キャラ)%は上手に自分で射精できたようだ。
		ELSEIF BRREP:自慰で絶頂
			PRINTFORML 、%CALLNAME:(BRREP:売春キャラ)%は上手に自分で絶頂に達せたようだ。
		ELSE
			PRINTFORML たが、一人で絶頂を迎えることはできなかったらしい。
		ENDIF
	ENDIF
ENDIF

@ペア売春_地の文
#DIM DYNAMIC 対象
#DIM DYNAMIC 表示済み
#DIM DYNAMIC 口上対象, 2
#DIMS DYNAMIC 口上関数

IF OPTION:口上表示
	SIF EXISTFUNCTION(@"BR_PLAY_%CSVNAMERPS(NOF(BRREP:売春キャラ1))%_%CSVNAMERPS(NOF(BRREP:売春キャラ2))%") && !CFLAG:(BRREP:売春キャラ1):口上非表示
		口上対象:0 = 1
	SIF EXISTFUNCTION(@"BR_PLAY_%CSVNAMERPS(NOF(BRREP:売春キャラ2))%_%CSVNAMERPS(NOF(BRREP:売春キャラ1))%") && !CFLAG:(BRREP:売春キャラ2):口上非表示
		口上対象:1 = 1
	;口上があるほうを出す 二人ともあればランダム
	IF 口上対象:0 && 口上対象:1
		IF RAND:2
			$一人目
			口上関数 = BR_PLAY_%CSVNAMERPS(NOF(BRREP:売春キャラ1))%_%CSVNAMERPS(NOF(BRREP:売春キャラ2))%
		ELSE
			$二人目
			口上関数 = BR_PLAY_%CSVNAMERPS(NOF(BRREP:売春キャラ2))%_%CSVNAMERPS(NOF(BRREP:売春キャラ1))%
		ENDIF
	ELSEIF 口上対象:0
		GOTO 一人目
	ELSEIF 口上対象:1
		GOTO 二人目
	ELSE
		GOTO NOTKOJO
	ENDIF
	TRYCALLFORM %口上関数%
ELSE
	$NOTKOJO
	表示済み = 0
	IF BR:方針 == 4
		PRINTFORML 娼館で働いている%CALLNAME:(BRREP:売春キャラ1)%と%CALLNAME:(BRREP:売春キャラ2)%の様子を見に行くと、何度も犯されて身動きが取れないほど消耗したふたりが居た
	ELSE
		PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%と%CALLNAME:(BRREP:売春キャラ2)%が今日の仕事を報告してきた。
		PRINT 今日は
		SIF BR:方針 == 3 && BRREP:手コキで射精
			PRINT マゾヒストの
		SELECTCASE BRREP:客の性別
			CASE IS <= 1
				PRINT 男性客を相手に
			CASE 2
				PRINT 女性客を相手に
			CASE 3
				PRINT ふたなり女性客を相手に
		ENDSELECT
	ENDIF

	;マゾヒスト用
	IF BRREP:手コキで射精
		対象 = BRREP:手コキ1 ? 1 # 2
		PRINTFORM まずは%CALLNAME:(BRREP:@"売春キャラ{対象}")%が手で扱いて{BRREP:手コキで射精}回抜いた後、
		IF BRREP:ダブル足コキで射精
			PRINTFORML ふたりで言葉責めをしながら足で扱いて{BRREP:ダブル足コキで射精}回射精させたようだ。
		ELSE
			対象 = BRREP:足コキ1 ? 1 # 2
			IF BRREP:足コキで射精
				PRINTFORML %CALLNAME:(BRREP:@"売春キャラ{対象}")%が足で扱いて{BRREP:足コキで射精}回射精させたようだ。
			ELSE
				PRINTFORML %CALLNAME:(BRREP:@"売春キャラ{対象}")%が足で扱いたが、射精には至らなかったようだ。
			ENDIF
		ENDIF
		SIF BRREP:逆アナル責めで射精
			PRINTFORML 次にふたりは客を拘束し、ふたりでアナルや乳首、前立腺を責めながら{BRREP:逆アナル責めで射精}回射精させることに成功したらしい。
		表示済み = 1
	ENDIF

	IF BRREP:売春キャラ兜合わせ
		PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%と%CALLNAME:(BRREP:売春キャラ2)%で兜合わせをするように命じられ、
		IF BRREP:売春キャラ兜合わせ射精1 && BRREP:売春キャラ兜合わせ射精2
			PRINTL お互いにそれで射精してしまったようだ。
		ELSEIF BRREP:売春キャラ兜合わせ射精1
			PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%はそれで射精してしまったようだ。
		ELSEIF BRREP:売春キャラ兜合わせ射精2
			PRINTFORML %CALLNAME:(BRREP:売春キャラ2)%はそれで射精してしまったようだ。
		ELSE
			PRINTL 互いのペニスを擦り合わせて見せつけたようだ。
		ENDIF
		表示済み = 1
	ELSEIF BRREP:二本フェラ
		PRINT ペニスを交互にフェラされ、
		IF BRREP:売春キャラ二本フェラ射精1 && BRREP:売春キャラ二本フェラ射精2
			PRINTL ふたりとも客の舌使いでイかされてしまったようだ。
		ELSEIF BRREP:売春キャラ二本フェラ射精1
			PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は客の口の中に射精してしまったようだ。
		ELSEIF BRREP:売春キャラ二本フェラ射精2
			PRINTFORML %CALLNAME:(BRREP:売春キャラ2)%は客の口の中に射精してしまったようだ。
		ELSE
			PRINTL 客は満足そうに二本のペニスを弄んでいたようだ。
		ENDIF
		表示済み = 1
	ENDIF

	IF BRREP:交互騎乗位
		PRINT 交互に騎乗位をして、
		IF MAX(BRREP:中出し1, BRREP:アナル中出し1) == MAX(BRREP:中出し2, BRREP:アナル中出し2)
			PRINTFORML ふたりとも{MAX(BRREP:中出し1, BRREP:アナル中出し1)}回ずつ搾り取ったようだ。
		ELSE
			PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%が
			IF BRREP:中出し1
				PRINTFORM 膣で{BRREP:中出し1}回、
			ELSE
				PRINTFORM アナルで{BRREP:アナル中出し1}回、
			ENDIF
			PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%
			IF BRREP:中出し2
				PRINTFORM \@ BRREP:中出し1 ? も # が \@
				PRINTFORM 膣で{BRREP:中出し2}回
			ELSE
				PRINTFORM \@ BRREP:アナル中出し1 ? も # が \@
				PRINTFORM アナルで{BRREP:アナル中出し2}回
			ENDIF
			PRINTL 搾り取ったようだ。
		ENDIF
		表示済み = 1
	ELSEIF BRREP:射精我慢勝負
		PRINTFORML 「先に射精したほうが負け」というルールで同時にペニスを弄られ、我慢も虚しく%CALLNAME:(BRREP:@"売春キャラ{BRREP:先に射精}")%が先に射精してしまった。
		IF BRREP:客の性別 == 2
			PRINTFORM 我慢できた
			PRINTFORM \@ BRREP:先に射精 == 1 ? %CALLNAME:(BRREP:売春キャラ2)% # %CALLNAME:(BRREP:売春キャラ1)% \@
			PRINTFORML はご褒美に客の膣内に射精させてもらったようだ。
		ELSE
			PRINTFORM 我慢できなかった%CALLNAME:(BRREP:@"売春キャラ{BRREP:先に射精}")%はお仕置きに
			IF VAGINA(BRREP:@"売春キャラ{BRREP:先に射精}")
				PRINT 膣を激しく犯され、
			ELSE
				PRINT アナルを激しく犯され、
			ENDIF
			PRINTFORM {MAX(BRREP:@"中出し{BRREP:先に射精}", BRREP:@"アナル中出し{BRREP:先に射精}")}回中出しされた。
			SIF BRREP:@"絶頂{BRREP:先に射精}"
				PRINTFORM %CALLNAME:(BRREP:@"売春キャラ{BRREP:先に射精}")%も犯されながら{BRREP:@"絶頂{BRREP:先に射精}"}回イったようだ。
			PRINTL 
		ENDIF
		表示済み = 1
	ELSEIF BRREP:ダブル自慰
		PRINT ふたりで自慰をするように命じられ
		IF BRREP:自慰で射精1 && BRREP:自慰で射精2
			PRINTL 、客に見られながらふたり一緒に自慰で射精した。
		ELSEIF (BRREP:自慰で絶頂1 || BRREP:自慰で射精1) && (BRREP:自慰で絶頂2 || BRREP:自慰で射精2)
			PRINTL 、客に見られながらふたり一緒に自慰で絶頂に達した。
		ELSEIF BRREP:自慰で射精1 || BRREP:自慰で絶頂1
			PRINTFORM 、%CALLNAME:(BRREP:売春キャラ1)%は
			IF BRREP:自慰で射精1
				PRINT 自慰で射精したが、
			ELSE
				PRINT 絶頂に達したが、
			ENDIF
			IF PENIS(BRREP:売春キャラ2)
				PRINTFORML %CALLNAME:(BRREP:売春キャラ2)%は射精には達せなかった。
			ELSE
				PRINTFORML %CALLNAME:(BRREP:売春キャラ2)%は絶頂には達せなかった。
			ENDIF
		ELSEIF BRREP:自慰で射精2 || BRREP:自慰で絶頂2
			PRINTFORM 、%CALLNAME:(BRREP:売春キャラ2)%は
			IF BRREP:自慰で射精2
				PRINT 自慰で射精したが、
			ELSE
				PRINT 絶頂に達したが、
			ENDIF
			IF PENIS(BRREP:売春キャラ1)
				PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は射精には達せなかった。
			ELSE
				PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は絶頂には達せなかった。
			ENDIF
		ELSE
			PRINTFORML 客の前で自慰を披露した。
		ENDIF
		IF BR:方針 == 3
			IF BRREP:射精地獄1 || BRREP:絶頂地獄1
				対象 = 1
			ELSE
				対象 = 2
			ENDIF
			PRINTFORM 客は%CALLNAME:(BRREP:@"売春キャラ{対象}")%に目をつけて拘束すると、
			IF BRREP:@"射精地獄{対象}"
				PRINTL 激しくペニスを弄り始めた。
				PRINT 一度二度射精するだけでは手が止まらず、
				IF BRREP:客の性別 != 2
					PRINT アナルを犯しながら
				ELSE
					PRINT アナルを責めながら
				ENDIF
				PRINTFORML 精液を搾り取り、最終的に潮を噴いてしまうまで%CALLNAME:(BRREP:@"売春キャラ{対象}")%は{BRREP:@"射精地獄{対象}"}回連続で射精させられた。
			ELSEIF BRREP:@"絶頂地獄{対象}"
				IF BRREP:客の性別 != 2
					PRINTL 膣とアナルを交互に激しく犯し始めた。
				ELSE
					PRINTL 膣とアナルを指や道具で激しく責め始めた。
				ENDIF
				PRINTFORML %CALLNAME:(BRREP:@"売春キャラ{対象}")%が何度イっても終わることはなく、{BRREP:@"絶頂地獄{対象}"}回ほど連続で絶頂させられて気を失いかけたようだ。
			ENDIF
		ENDIF
		表示済み = 1
	ELSE
		IF MAX(BRREP:中出し1, BRREP:アナル中出し1) >= 1 && MAX(BRREP:中出し2, BRREP:アナル中出し2) >= 1
			IF BRREP:拘束プレイ
				PRINT ふたりは動けないように拘束されたまま
			ELSEIF BR:方針 == 4
				PRINT ふたりは
			ENDIF
			PRINT 交互に犯され、
			IF BRREP:中出し1 && BRREP:中出し2
				IF BRREP:中出し1 == BRREP:中出し2
					PRINTFORML 互いに{BRREP:中出し1}回ずつ膣内に中出しされたようだ。
				ELSE
					PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は{BRREP:中出し1}回、%CALLNAME:(BRREP:売春キャラ2)%は{BRREP:中出し2}回、膣内に中出しされたようだ。
				ENDIF
			ELSEIF BRREP:アナル中出し1 && BRREP:アナル中出し2
				IF BRREP:アナル中出し1 == BRREP:アナル中出し2
					PRINTFORML 互いに{BRREP:アナル中出し1}回ずつアナルに中出しされたようだ。
				ELSE
					PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は{BRREP:アナル中出し1}回、%CALLNAME:(BRREP:売春キャラ2)%は{BRREP:アナル中出し2}回、アナルに中出しされたようだ。
				ENDIF
			ELSE
				IF BRREP:中出し1 && BRREP:アナル中出し2
					IF BRREP:中出し1 == BRREP:アナル中出し2
						PRINTFORML {BRREP:中出し1}回ずつ、%CALLNAME:(BRREP:売春キャラ1)%は膣内に、%CALLNAME:(BRREP:売春キャラ2)%はアナルに中出しされたようだ。
					ELSE
						PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は{BRREP:中出し1}回膣内に、%CALLNAME:(BRREP:売春キャラ2)%は{BRREP:アナル中出し2}回アナルに中出しされたようだ。
					ENDIF
				ELSEIF BRREP:アナル中出し1 && BRREP:中出し2
					IF BRREP:アナル中出し1 == BRREP:中出し2
						PRINTFORML {BRREP:アナル中出し1}回ずつ、%CALLNAME:(BRREP:売春キャラ1)%はアナルに、%CALLNAME:(BRREP:売春キャラ2)%は膣内に中出しされたようだ。
					ELSE
						PRINTFORML %CALLNAME:(BRREP:売春キャラ1)%は{BRREP:アナル中出し1}回アナルに、%CALLNAME:(BRREP:売春キャラ2)%は{BRREP:中出し2}回膣内に中出しされたようだ。
					ENDIF
				ENDIF
			ENDIF
			表示済み = 1
		ELSEIF BRREP:中出し1 || BRREP:アナル中出し1
			PRINT 体中を触られた後、
			PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%の方を気に入った客は%CALLNAME:(BRREP:売春キャラ1)%と
			IF BRREP:中出し1
				PRINTFORM 性交に及び、{BRREP:中出し1}回
			ELSEIF BRREP:アナル中出し1
				PRINTFORM アナルセックスに及び、{BRREP:アナル中出し1}回
			ENDIF
			PRINTL 中出ししたようだ。
			表示済み = 1
		ELSEIF BRREP:中出し2 || BRREP:アナル中出し2
			PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%の方を気に入った客は%CALLNAME:(BRREP:売春キャラ2)%と
			IF BRREP:中出し2
				PRINTFORM 性交に及び、{BRREP:中出し2}回
			ELSEIF BRREP:アナル中出し2
				PRINTFORM アナルセックスに及び、{BRREP:アナル中出し2}回
			ENDIF
			PRINTL 中出ししたようだ。
			表示済み = 1
		ENDIF
	ENDIF

	IF BR:方針 == 4
		IF BRREP:拘束プレイ
			PRINTL 家畜のように縛られて放置されるふたりは並外れた回数の中出しに、妊娠したように腹を膨らませ、未だ収まりきらない激しい絶頂の余韻に足をがくがくと震わせている。
		ELSE
			PRINTL 散々犯されてぼろ雑巾のように打ち捨てられるふたりは、未だに体をびくびくと痙攣させている。
			対象 = RAND(1, 3)
			IF VAGINA(BRREP:@"売春キャラ{対象}")
				PRINTFORM %CALLNAME:(BRREP:@"売春キャラ{対象}")%は膣内から産み落とすように精液の塊を零し、
				PRINTDATAL
					DATA 絶頂に緩んだ尿道から勢い無く尿を漏らしている……
					DATA 膣内を這う濃厚な精液に小さい嬌声を上げた……
				ENDDATA
			ELSE
				PRINTFORM %CALLNAME:(BRREP:@"売春キャラ{対象}")%は下品な音を立てて排泄のようにアナルから白濁液を漏らし、
				PRINTDATAL
					DATA 犯される快感で出したのであろう精液が、拭うことも叶わずに先端から滲み出ている……
					DATA 周囲には当人や客が出したのであろう体液が散乱している……
				ENDDATA
			ENDIF
		ENDIF
		表示済み = 1
	ELSE
		IF BRREP:ダブルフェラ || BRREP:ダブルパイズリ
			SIF 表示済み
				PRINT その後、
			PRINT ふたり一緒に客のペニスを
			IF BRREP:ダブルフェラ && BRREP:ダブルパイズリ
				PRINT 胸で擦りながら舐め回し
			ELSEIF BRREP:ダブルフェラ
				PRINT 舐め回し
			ELSE
				PRINT 乳房で扱い
			ENDIF
			IF BRREP:パイズリで射精 || BRREP:フェラで射精
				SELECTCASE BR:方針
					CASE 2, 3
						PRINTFORML て{BRREP:パイズリで射精+BRREP:フェラで射精}回射精させた。
					CASEELSE
						PRINTFORM て{BRREP:パイズリで射精+BRREP:フェラで射精}回射精させた後、
				ENDSELECT
			ELSE
				SELECTCASE BR:方針
					CASE 2, 3
						PRINTL てお掃除したそうだ。
					CASEELSE
						PRINT た後、
				ENDSELECT
			ENDIF
		ELSEIF BRREP:客の性別 != 2
			SIF 表示済み
				PRINT その後、
			IF BR:方針 == 2 || BR:方針 == 3
				IF BRREP:フェラ1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%が口で奉仕
				ELSEIF BRREP:フェラ2
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%が口で奉仕
				ENDIF
				IF BRREP:フェラで射精
					PRINTFORML して{BRREP:フェラで射精}回射精させた。
				ELSE
					PRINTL して綺麗にお掃除したそうだ。
				ENDIF
			ELSE
				IF BRREP:フェラ1 && BRREP:手コキ1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%が口と手で奉仕
				ELSEIF BRREP:フェラ2 && BRREP:手コキ2
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%が口と手で奉仕
				ELSEIF BRREP:フェラ1 && BRREP:手コキ2
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%が口で、%CALLNAME:(BRREP:売春キャラ2)%が手で奉仕
				ELSEIF BRREP:フェラ2 && BRREP:手コキ1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%が口で、%CALLNAME:(BRREP:売春キャラ1)%が手で奉仕
				ELSEIF BRREP:フェラ1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%が口で奉仕
				ELSEIF BRREP:フェラ2
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%が口で奉仕
				ENDIF
				IF BRREP:フェラで射精 || BRREP:手コキで射精
					PRINTFORM して{BRREP:フェラで射精+BRREP:手コキで射精}回射精させた後、
				ELSE
					PRINT した後、
				ENDIF
			ENDIF
		ENDIF
		
		IF BRREP:ダブル素股
			PRINTFORML 互いの陰唇で客のペニスを挟み込み、{BRREP:素股で射精}回精液を搾り取ったらしい。
		ELSE
			IF BRREP:兜合わせ1 || BRREP:兜合わせ2
				IF BRREP:兜合わせ1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%
				ELSE
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%
				ENDIF
				PRINT が客と陰茎を擦り合わせ
				IF BRREP:兜合わせで射精
					PRINTFORML て{BRREP:兜合わせで射精}回射精させた。
				ELSE
					PRINTL たが、射精させるには至らなかった。
				ENDIF
			ENDIF
			IF BRREP:素股1 || BRREP:素股2
				IF BRREP:素股1
					PRINTFORM %CALLNAME:(BRREP:売春キャラ1)%
				ELSE
					PRINTFORM %CALLNAME:(BRREP:売春キャラ2)%
				ENDIF
				PRINTFORM が性行為の代わりに秘部を擦り合わせ
				IF BRREP:素股で射精
					PRINTFORML て{BRREP:素股で射精}回射精させた。
				ELSE
					PRINTL たが、射精させるには至らなかった。
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
