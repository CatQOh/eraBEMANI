@STRATEGY_IDOL
#DIM DYNAMIC LCOUNT

SIF IDOL:知名度 < 0
	IDOL:知名度 = 0

DRAWLINE
PRINTFORML □芸能プロダクション運営 現在の知名度:{IDOL:知名度}
PRINTL 

IF FINDCHARA(CSTR:配属, "芸能プロダクション") >= 0
	HTML_PRINT SET_TOOLTIP("　[1] - レッスン", "レッスンを受けさせる予定を入れます")
	HTML_PRINT SET_TOOLTIP("　[2] - イベント", "ライブなどのイベントを行います")
ELSE
	HTML_PRINT SET_TOOLTIP("　[-] - レッスン", "キャラを配属してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-] - イベント", "キャラを配属してください", 0, "gray")
ENDIF
IF ORIGIN
	HTML_PRINT SET_TOOLTIP("　[3] - 枕営業させる", "キャラに枕営業をさせて知名度を強引に上げます")
ELSE
	HTML_PRINT SET_TOOLTIP("　[3] - 賄賂する", "現金などを送ることにより知名度を強引に上げます")
ENDIF
HTML_PRINT SET_TOOLTIP("　[4] - 人員配置", "芸能プロダクションで働かせるキャラを配属します")
IF FINDCHARA(CSTR:配属, "芸能プロダクション") >= 0
	HTML_PRINT SET_TOOLTIP("　[5] - ユニット", "キャラ同士でユニットを組みます")
ELSE
	HTML_PRINT SET_TOOLTIP("　[-] - ユニット", "キャラを配属してください", 0, "gray")
ENDIF
HTML_PRINT SET_TOOLTIP("　[6] - 設定変更", "芸能プロダクション運営に関する設定を変更します")

DRAWLINE
PRINTPLAIN 　IDOL　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL IDOL_LESSON
	CASE 2
		CALL IDOL_EVENT
		;何もしないで戻ってきた場合
		SIF RESULT == 0
			RESTART
		;ライブを終えて戻ってきた場合はターン終了
		JUMP ターン終了
	CASE 3
		CALL IDOL_BRIBE
	CASE 4
		CALL STRATEGY_PERSONAL, "芸能プロダクション"
	CASE 5
		CALL IDOL_UNIT
	CASE 6
		CALL IDOL_OPTION
	CASE 100
		CALL IDOL_TUTORIAL
ENDSELECT
RESTART

;----------------------------------------------------
;芸能プロダクション運営モードチュートリアル
;----------------------------------------------------
@IDOL_TUTORIAL
DRAWLINE
PRINTL 当モードは芸能プロダクションを運営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「レッスン」とは？
PRINTL [2] - 「イベント」とは？
IF !ORIGIN
	PRINTL [3] - 「贈賄する」とは？
	PRINTL [4] - 「人員配置」とは？
ENDIF
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		PRINTL 「レッスン」はこれから受けるレッスンの予定を入れるコマンドです。
		PRINTL お金を払うことによりボーカル・ダンス・楽器演奏のいずれかのレッスンを受けることができます。
		PRINTL ここで予定を入れなくても全ての経験を少しずつ得ていきますが、
		PRINTL 能力をより強くしたり何かの技能に特化させたり際に必要となるでしょう。
		PRINTW 高い効果のレッスンは相応の知名度がないと受けることができません。
	CASE 2
		PRINTL 「イベント」はライブなどの催し物で利益を上げるコマンドです。
		PRINTW このコマンドを選択したときのみ、主人は半日間拘束されます。
	CASE 3
		PRINTL 「贈賄する」は賄賂を送ることなどの裏工作により知名度を上げるコマンドです。
		PRINTL 知名度が高いほど賄賂に必要な金額は増えていきます。
		PRINTW また、枕営業をさせることでも取引ができますが、その効果は奴隷の働き次第です。
	CASE 4
		PRINTL 「人員配置」は芸能プロダクションに人員を配置するコマンドです。
		PRINTW 配属された人数に応じてお金はかかってしまうので、誰を配属するかはよく考えましょう。
ENDSELECT

RESTART


;レッスン----------------------------------------------------
@IDOL_LESSON
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC レッスン対象
;「CFLAG:レッスン」について
;3で割った余りでレッスンの種類、3の倍数に切り上げたときの値でレッスンのレベルを判別
;Vocal:1, 4, 7, 10, 13
;Inst.:2, 5, 8, 11, 14
;Dance:3, 6, 9, 12, 15
;それぞれ左からLv1～Lv5となる

DRAWLINE
PRINTL □レッスンを受けさせるキャラを選んでください

FOR LCOUNT, 1, CHARANUM
	;体力不足だと無理
	SIF BASE:LCOUNT:体力 < 500
		CONTINUE
	IF CSTR:LCOUNT:配属 == "芸能プロダクション"
		IF CFLAG:LCOUNT:レッスン == 0
			PRINT 　　　　　
		ELSEIF CFLAG:LCOUNT:レッスン < 0
			PRINT 枕営業予定
		ELSEIF CFLAG:LCOUNT:レッスン%3 == 1
			PRINTFORM Vocal(Lv{(CFLAG:LCOUNT:レッスン+2)/3})
		ELSEIF CFLAG:LCOUNT:レッスン%3 == 2
			PRINTFORM Inst.(Lv{(CFLAG:LCOUNT:レッスン+1)/3})
		ELSE
			PRINTFORM Dance(Lv{CFLAG:LCOUNT:レッスン/3})
		ENDIF
		PRINTFORM  [{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT% 歌唱技能:{ABL:LCOUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:LCOUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:LCOUNT:舞踊技能, 2, LEFT}
		CALL ISTALENT, LCOUNT, "魅力"
		CALL ISTALENT, LCOUNT, "魅惑"
		CALL ISTALENT, LCOUNT, "謎の魅力"
		CALL ISTALENT, LCOUNT, "人気"
		CALL ISTALENT, LCOUNT, "絶対音感"
		PRINTL 
	ENDIF
NEXT

DRAWLINE
PRINTPLAIN 　LESSON　
PRINTL [0] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1 TO CHARANUM-1
		IF BASE:RESULT:体力 >= 500 && CSTR:RESULT:配属 == "芸能プロダクション"
			レッスン対象 = RESULT
			PRINTL
			PRINTL レッスンのレベルと種類を選んでください
			PRINTL
			PRINTL [0]レッスンの予定を入れない
			IF ORIGIN
				PRINTL 　　Vocal Inst. Dance
			ELSE
				PRINTL 　　　　　　 Vocal Inst. Dance
			ENDIF
			CALL PASELIL, @"Lv1\@ !ORIGIN ? (10000＄)#\@  [ 1]  [ 2]  [ 3]"
			IF IDOL:知名度 > 40
				CALL PASELIPLAIN, @"Lv2\@ !ORIGIN ? (30000＄)#\@ "
				SIF ORIGIN
					PRINT  
				PRINTL [ 4]  [ 5]  [ 6]
			ENDIF
			IF IDOL:知名度 > 160
				CALL PASELIPLAIN, @"Lv3\@ !ORIGIN ? (60000＄)#\@ "
				SIF ORIGIN
					PRINT  
				PRINTL [ 7]  [ 8]  [ 9]
			ENDIF
			IF IDOL:知名度 > 640
				CALL PASELIPLAIN, @"Lv4\@ !ORIGIN ? (100000＄)#\@ "
				PRINTL [10]  [11]  [12]
			ENDIF
			IF IDOL:知名度 > 2560
				CALL PASELIPLAIN, @"Lv5\@ !ORIGIN ? (150000＄)#\@ "
				PRINTL [13]  [14]  [15]
			ENDIF

			BINPUT
			SELECTCASE RESULT
				CASE 0 TO 3
					CFLAG:レッスン対象:レッスン = RESULT
				CASE 4 TO 6
					CFLAG:レッスン対象:レッスン = RESULT
				CASE 7 TO 9
					CFLAG:レッスン対象:レッスン = RESULT
				CASE 10 TO 12
					CFLAG:レッスン対象:レッスン = RESULT
				CASE 13 TO 15
					CFLAG:レッスン対象:レッスン = RESULT
			ENDSELECT
			RESTART
		ENDIF
ENDSELECT


;イベント----------------------------------------------------
@IDOL_EVENT
DRAWLINE
PRINTL □何のイベントを開催しますか？
PRINTL 　[1] ライブ
PRINTL 　[2] 出演依頼を見る
DRAWLINE
PRINTPLAIN 　EVENT　
PRINTL [0] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		CALL IDOL_LIVE
		RETURN RESULT
	CASE 2
		CALL IDOL_出演依頼
		RETURN RESULT
ENDSELECT

@IDOL_出演依頼
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 曲番号
#DIM DYNAMIC 総曲数
#DIM DYNAMIC 依頼番号

CALL REF_MUSIC
総曲数 = RESULT

DRAWLINE
IF MATCH(出演依頼, 0) == VARSIZE("出演依頼")
	PRINTL
	PRINTL -----出演依頼はありません-----
	PRINTL 
ELSE
	DUMPRAND
	FOR LCOUNT, 0, VARSIZE("出演依頼")
		SIF 出演依頼:LCOUNT == 0
			BREAK
		RANDOMIZE 出演依頼:LCOUNT
		曲番号 = RAND:総曲数
		PRINTFORML 　[{LCOUNT}] - 依頼曲:%楽曲情報:曲番号:0% 難易度:\@ 楽曲情報:曲番号:1 != "0" ? Vo%楽曲情報:曲番号:1+" "%#\@Inst%楽曲情報:曲番号:2% Dance%楽曲情報:曲番号:3% [{LCOUNT+VARSIZE("出演依頼")}] - 断る
		CALL 楽曲依頼文生成, 曲番号
		PRINTL 
	NEXT
	INITRAND
ENDIF

DRAWLINE
PRINTPLAIN 　出演依頼　
PRINT [100] - 戻る 
SIF MATCH(出演依頼, 0) != VARSIZE("出演依頼")
	PRINT [101] - 依頼を全部断る
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 101
		VARSET 出演依頼
		RESTART
	CASE IS >= VARSIZE("出演依頼")
		ARRAYREMOVE 出演依頼, RESULT-VARSIZE("出演依頼"), 1
		RESTART
	CASEELSE
		依頼番号 = RESULT
		DUMPRAND
		RANDOMIZE 出演依頼:依頼番号
		曲番号 = RAND:総曲数
		FOR LCOUNT, 0, 4
			IDOL選曲:LCOUNT = %楽曲情報:曲番号:LCOUNT%
		NEXT
		INITRAND
		CALL IDOL_LIVE, 1
		SIF !RESULT
			RESTART
		ARRAYREMOVE 出演依頼, 依頼番号, 1
		RETURN RESULT
ENDSELECT

@IDOL_LIVE, 依頼
#DIM DYNAMIC 分野
#DIM DYNAMIC 技量, 4
#DIM DYNAMIC 難度, 4
#DIM DYNAMIC 難度合計
#DIM DYNAMIC 人数, 4
#DIM DYNAMIC LMEMORY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 収入, 2
#DIM DYNAMIC ユニット数
#DIM DYNAMIC 選出可能, 11
#DIM DYNAMIC ユニット担当記録用, 1000
#DIMS DYNAMIC HTML用
#DIM DYNAMIC 依頼
#DIM DYNAMIC 超過ポイント

;ユニット数を数える
ユニット数 = 0
FOR LCOUNT, 1, 11
	SIF ユニット名:LCOUNT != ""
		ユニット数++
NEXT

$MUSIC_SELECT
SIF 依頼
	GOTO MEMBER_LOOP
CALL MUSIC_LIST
SIF IDOL選曲 == ""
	RETURN 0

$MEMBER_LOOP
;◆◆◆技量の計算◆◆◆
;	技量 = 技能LVの合計値 / √(担当人数)
;D&VやI&Vの場合は、それぞれで1人として数えるが技能LVを0.75倍で計算する
;技量は10000倍にして計算する(表記上は小数第4位まで計算する)
VARSET 技量
VARSET 人数
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "芸能プロダクション"
		SELECTCASE ユニット担当記録用:LCOUNT
			CASE 1
				;担当曲補正
				IF 担当曲判定(LCOUNT, IDOL選曲)
					技量:3 += 12000*ABL:LCOUNT:舞踊技能
				ELSE
					技量:3 += 10000*ABL:LCOUNT:舞踊技能
				ENDIF
				人数:3++
			CASE 2
				IF 担当曲判定(LCOUNT, IDOL選曲)
					技量:2 += 12000*ABL:LCOUNT:演奏技能
				ELSE
					技量:2 += 10000*ABL:LCOUNT:演奏技能
				ENDIF
				人数:2++
			CASE 3
				IF 担当曲判定(LCOUNT, IDOL選曲)
					技量:1 += 12000*ABL:LCOUNT:歌唱技能
				ELSE
					技量:1 += 10000*ABL:LCOUNT:歌唱技能
				ENDIF
				人数:1++
			CASE 4
				IF 担当曲判定(LCOUNT, IDOL選曲)
					技量:3 += 9000*ABL:LCOUNT:舞踊技能
					技量:1 += 9000*ABL:LCOUNT:歌唱技能
				ELSE
					技量:3 += 7500*ABL:LCOUNT:舞踊技能
					技量:1 += 7500*ABL:LCOUNT:歌唱技能
				ENDIF
				人数:3++
				人数:1++
			CASE 5
				IF 担当曲判定(LCOUNT, IDOL選曲)
					技量:2 += 9000*ABL:LCOUNT:演奏技能
					技量:1 += 9000*ABL:LCOUNT:歌唱技能
				ELSE
					技量:2 += 7500*ABL:LCOUNT:演奏技能
					技量:1 += 7500*ABL:LCOUNT:歌唱技能
				ENDIF
				人数:2++
				人数:1++
		ENDSELECT
	ENDIF
NEXT
FOR 分野, 1, 4
	SIF 人数:分野 <= 0
		CONTINUE
	技量:分野 *= 10000
	技量:分野 /= SQRT(人数:分野*10000*10000)
NEXT
DRAWLINE
PRINTL □ライブに出すメンバーと担当するパートを選んでください
PRINTL 　(ボーカル曲の場合、最低でも1人はボーカルを担当する必要があります)
PRINTL 
PRINTFORML 　選曲　　　　%IDOL選曲%
PRINT 　楽曲難易度　
FOR 分野, 1, 4
	SELECTCASE 分野
		CASE 1
			PRINT Vocal:
		CASE 2
			PRINT Inst.:
		CASE 3
			PRINT Dance:
	ENDSELECT
	SELECTCASE TOINT(IDOL選曲:分野)
		CASE 0
			PRINT ‐ 
		CASE IS < 10
			PRINTFORM %TOFULL(IDOL選曲:分野)% 
		CASEELSE
			PRINTFORM %IDOL選曲:分野% 
	ENDSELECT
NEXT
PRINTL 
PRINT 　現在の技量　
FOR 分野, 1, 4
	PRINTFORM {技量:分野/10000, 3, RIGHT}.
	FOR LCOUNT, 0, 4-STRLENS(TOSTR(技量:分野%10000))
		PRINT 0
	NEXT
	PRINTFORM {技量:分野%10000} 
NEXT
PRINTL 
PRINTL 
FOR LCOUNT, 0, CHARANUM
	;体力不足だと無理
	SIF BASE:LCOUNT:体力 < 500
		CONTINUE
	IF CSTR:LCOUNT:配属 == "芸能プロダクション"
		PRINT 　
		SELECTCASE ユニット担当記録用:LCOUNT
			CASE 0
				PRINT 　　
			CASE 1
				PRINT Da. 
			CASE 2
				PRINT In. 
			CASE 3
				PRINT Vo. 
			CASE 4
				PRINT D&V 
			CASE 5
				PRINT I&V 
		ENDSELECT
		PRINTFORM [{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT%(歌唱技能:{ABL:LCOUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:LCOUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:LCOUNT:舞踊技能, 2, LEFT}
		SIF TALENT:LCOUNT:魅力
			PRINT  魅力
		SIF TALENT:LCOUNT:魅惑
			PRINT  魅惑
		SIF TALENT:LCOUNT:謎の魅力
			PRINT  謎の魅力
		SIF TALENT:LCOUNT:人気
			PRINT  人気
		SIF TALENT:LCOUNT:絶対音感
			PRINT  絶対音感
		PRINTL )
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　LIVE　
PRINT [ 0] - ライブを始める　
IF 依頼
	PRINT [-1] - 出演依頼一覧に戻る　
ELSE
	PRINT [-1] - 選曲画面に戻る　
	;PRINT [-2] - 戻る
ENDIF
PRINTL 
SIF ユニット数
	PRINTL 　　　　[-2] - ユニットで選出する

BINPUT
IF RESULT == -2
	SIF !ユニット数
		CONTINUE
	DRAWLINE
	PRINTL □ユニットを選択してください
	FOR LCOUNT, 1, ユニット数+1
		;ユニット全部表示したらBREAK
		SIF ユニット名:LCOUNT == ""
			BREAK
		;選出可能フラグは予め立てておく
		選出可能:LCOUNT = 1
		FOR LCOUNT2, 1, CHARANUM
			;ユニットのメンバーが芸能プロダクションに配属されてるか確認する
			IF CFLAG:LCOUNT2:ユニット番号 == LCOUNT
				;配属されてないメンバーが居れば選出可能フラグを折って灰色表示してBREAK
				IF CSTR:LCOUNT2:配属 != "芸能プロダクション"
					HTML用 = <font color='gray'><nonbutton title='一部メンバーが不在です'>　[-] %ユニット名:LCOUNT%</nonbutton></font>
					HTML_PRINT HTML用
					選出可能:LCOUNT = 0
					BREAK
				ENDIF
			ENDIF
		NEXT
		;上記のチェックを抜けてきたら選出可能
		SIF 選出可能:LCOUNT
			PRINTFORML 　[{LCOUNT}] %ユニット名:LCOUNT%
	NEXT
	DRAWLINE
	PRINTPLAIN 　UNIT　
	PRINTL [0] - メンバー選定に戻る

	BINPUT
	SELECTCASE RESULT
		CASE 1 TO 10
			FOR LCOUNT, 0, CHARANUM
				IF CFLAG:LCOUNT:ユニット番号 == RESULT
					;ユニット結成時にあらかじめ決めておいた担当に分担する
					;ボーカル担当の場合内部数値は2桁 2桁目がメイン、1桁目がサブ
					SELECTCASE CFLAG:LCOUNT:ユニット担当
						CASE IS < 10
							;ダンス、楽器演奏はそのまま代入
							ユニット担当記録用:LCOUNT = CFLAG:LCOUNT:ユニット担当
						CASEELSE
							;ボーカル必要無い選曲の場合、予備の担当に切り替える(1桁目)
							IF IDOL選曲:1 == "0"
								ユニット担当記録用:LCOUNT = CFLAG:LCOUNT:ユニット担当%10
							;オンボーカルならメインの担当(2桁目)
							ELSE
								ユニット担当記録用:LCOUNT = CFLAG:LCOUNT:ユニット担当/10
							ENDIF
					ENDSELECT
				ENDIF
			NEXT
			GOTO MEMBER_LOOP
		CASE 0
			GOTO MEMBER_LOOP
	ENDSELECT
ENDIF

;SIF RESULT == -2
;	RESTART
IF RESULT == -1
	SIF 依頼
		RETURN 0
	GOTO MUSIC_SELECT
ELSEIF RESULT == 0
	IF 人数:1 == 0  && 人数:2 == 0 && 人数:3 == 0
		PRINTW メンバーを1人以上選び出してください
		GOTO MEMBER_LOOP
	ENDIF
	IF IDOL選曲:1 != "0" && 人数:1 == 0
		PRINTW ボーカルを1人以上選び出してください
		GOTO MEMBER_LOOP
	ENDIF
ELSE
	LMEMORY = RESULT
	PRINTL
	PRINTL どのパートを担当しますか？
	PRINTL [0] 担当しない
	PRINTL [1] ダンス
	PRINTL [2] 楽器演奏
	IF IDOL選曲:1 != "0"
		PRINTL [3] ボーカル
		PRINTL [4] ダンス&ボーカル
		PRINTL [5] 楽器演奏&ボーカル
	ENDIF

	BINPUT
	SELECTCASE RESULT
		CASE 0, 1, 2
			ユニット担当記録用:LMEMORY = RESULT
		CASE 3, 4, 5
			ユニット担当記録用:LMEMORY = RESULT
	ENDSELECT
	GOTO MEMBER_LOOP
ENDIF

;上記を超えるとライブ開始
難度:1 = 100*TOINT(IDOL選曲:1)
難度:2 = 100*TOINT(IDOL選曲:2)
;CHAOSはダンス難易度が4～9の間で変動する
SELECTCASE IDOL選曲
	CASE "CHAOS"
		IDOL選曲:3 = RAND(4, 10)
ENDSELECT
難度:3 = 100*TOINT(IDOL選曲:3)
難度合計 = TOINT(IDOL選曲:1) + TOINT(IDOL選曲:2) + TOINT(IDOL選曲:3)
;魅惑で2%、魅力で3%、謎の魅力で4%、人気で5%技量が上がる(乗算で重複する)
;ビジュアルによって難度が下がる(※かわいいは正義)ことを表現したいのだが
;難度を下げると収入も下がってしまうので相対的に技量を上げる
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "芸能プロダクション" && ユニット担当記録用:LCOUNT
		;サボり魔はモチベに応じてサボる
		IF サボり判定(LCOUNT)
			PRINTFORMW %CALLNAME:LCOUNT%はライブをサボりました
			CFLAG:LCOUNT:ストレス値 -= 5
			SELECTCASE ユニット担当記録用:LCOUNT
				CASE 1
					;担当曲補正
					IF 担当曲判定(LCOUNT, IDOL選曲)
						技量:3 -= 120*ABL:LCOUNT:舞踊技能
					ELSE
						技量:3 -= 100*ABL:LCOUNT:舞踊技能
					ENDIF
					人数:3--
				CASE 2
					IF 担当曲判定(LCOUNT, IDOL選曲)
						技量:2 -= 120*ABL:LCOUNT:演奏技能
					ELSE
						技量:2 -= 100*ABL:LCOUNT:演奏技能
					ENDIF
					人数:2--
				CASE 3
					IF 担当曲判定(LCOUNT, IDOL選曲)
						技量:1 -= 120*ABL:LCOUNT:歌唱技能
					ELSE
						技量:1 -= 100*ABL:LCOUNT:歌唱技能
					ENDIF
					人数:1--
				CASE 4
					IF 担当曲判定(LCOUNT, IDOL選曲)
						技量:3 -= 90*ABL:LCOUNT:舞踊技能
						技量:1 -= 90*ABL:LCOUNT:歌唱技能
					ELSE
						技量:3 -= 75*ABL:LCOUNT:舞踊技能
						技量:1 -= 75*ABL:LCOUNT:歌唱技能
					ENDIF
					人数:3--
					人数:1--
				CASE 5
					IF 担当曲判定(LCOUNT, IDOL選曲)
						技量:2 -= 90*ABL:LCOUNT:演奏技能
						技量:1 -= 90*ABL:LCOUNT:歌唱技能
					ELSE
						技量:2 -= 75*ABL:LCOUNT:演奏技能
						技量:1 -= 75*ABL:LCOUNT:歌唱技能
					ENDIF
					人数:2--
					人数:1--
			ENDSELECT
			ユニット担当記録用:LCOUNT = 0
			CONTINUE
		ENDIF
		CFLAG:LCOUNT:レッスン = 0
		BASE:LCOUNT:体力 -= 50*(難度合計)
		IF BASE:LCOUNT:体力 < 1
			BASE:LCOUNT:体力 = 1
			CFLAG:LCOUNT:回復量低下 += 2
		ENDIF
		IF TALENT:LCOUNT:魅惑
			TIMES 技量:1, 1.02
			TIMES 技量:2, 1.02
			TIMES 技量:3, 1.02
		ENDIF
		IF TALENT:LCOUNT:魅力
			TIMES 技量:1, 1.03
			TIMES 技量:2, 1.03
			TIMES 技量:3, 1.03
		ENDIF
		IF TALENT:LCOUNT:謎の魅力
			TIMES 技量:1, 1.04
			TIMES 技量:2, 1.04
			TIMES 技量:3, 1.04
		ENDIF
		IF TALENT:LCOUNT:人気
			TIMES 技量:1, 1.05
			TIMES 技量:2, 1.05
			TIMES 技量:3, 1.05
		ENDIF
		;去勢済みの男性はボーカルにボーナス
		SIF TALENT:LCOUNT:去勢済み && SEX(LCOUNT) == 1
			TIMES 技量:1, 1.05
	ENDIF
NEXT
;GAMBOL・nine seconds・SPICY PIECEの場合は判定が厳しくなる
SELECTCASE IDOL選曲
	CASE "GAMBOL", "nine seconds", "SPICY PIECE"
	TIMES 技量:1, 0.33
	TIMES 技量:2, 0.33
	TIMES 技量:3, 0.33
ENDSELECT
収入 = 0
超過ポイント = 0
;それぞれの分野についての基本収入を出す
;収入は(合計難易度*合計難易度)*1万円が目安 超過で最大5倍まで増える
収入 = 難度合計*難度合計*1000
FOR 分野, 1, 4
	;他の技能で補えるのは変なのでマイナス分は2倍にする
	IF 技量:分野-難度:分野 < 0
		超過ポイント += (技量:分野-難度:分野)*2
	ELSE
		超過ポイント += 技量:分野-難度:分野
	ENDIF
NEXT

;超過ポイントは100倍されたものが入ってる
SELECTCASE SIGN(超過ポイント)
	;+
	CASE 1
		収入 *= LIMIT((超過ポイント/難度合計), 100, 500)
		収入 /= 100
	;-
	CASE -1
		収入 *= -LIMIT((超過ポイント/難度合計), 100, 500)
		;可哀想なので損失は低めに
		収入 /= 1000
ENDSELECT

;出演依頼なら1.5倍
SIF 依頼
	TIMES 収入 , 1.5

収入:1 = 収入
;最後に知名度による補正
;マイナスも増幅するので、知名度が高いときにしくじると大惨事
;√知名度%の上昇に調整 最大300%
収入:1 += 収入*LIMIT(SQRT(IDOL:知名度), 1, 300)/100

MONEY += 収入:1
CALL 現金出納帳記録, @"ライブの収支/{収入:1}"
;最終的な収益の計算の後に知名度の増減を行う
SELECTCASE SIGN(収入)
	CASE 1
		IDOL:知名度 += SQRT(収入)/10
	CASE -1
		収入 *= -1
		IDOL:知名度 -= SQRT(収入)/10
		収入 *= -1
ENDSELECT
PRINTL
PRINTL ライブが終了しました
IF 収入:1 >= 0
	CALL PASELI, @"{収入:1}＄の利益を出し、"
ELSE
	CALL PASELI, @"{ABS(収入:1)}＄の損失を出し、"
ENDIF
PRINT 知名度が
IF SIGN(収入) == 1
	PRINTFORMW {SQRT(収入)/10}増えました
ELSE
	収入 *= -1
	PRINTFORMW {SQRT(収入)/10}減りました
	収入 *= -1
ENDIF

SIF IDOL:知名度 < 0
	IDOL:知名度 = 0
;収入がプラスなら楽曲難易度合計の人気度を加算する
IF 収入:1 > 0
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "芸能プロダクション" && ユニット担当記録用:LCOUNT
			人気度:(NOF(LCOUNT)) += TOINT(IDOL選曲:1)
			人気度:(NOF(LCOUNT)) += TOINT(IDOL選曲:2)
			人気度:(NOF(LCOUNT)) += TOINT(IDOL選曲:3)
			;人気度*10の習得増加
			;JUEL:LCOUNT:習得 += (TOINT(IDOL選曲:1)+TOINT(IDOL選曲:2)+TOINT(IDOL選曲:3))*10
			IF !TALENT:LCOUNT:人気 && 人気度:(NOF(LCOUNT)) >= 500
				PRINTFORML %CALLNAME:LCOUNT%は度重なるライブ経験の功を経て、たくさんのファンが付いたようだ
				PRINTFORMW %CALLNAME:LCOUNT%は[人気]を得た
				TRYCALLFORM IDOL_人気取得_%CSVNAMERPS(NOF(LCOUNT))%
				;実績:ライブでボーカリストが[人気]を取得する
				SELECTCASE ユニット担当記録用:LCOUNT
					CASE IS >= 30
						SIF 難度:1 > 0
							CALL 実績解除, "プロジェクト・ディーヴァ"
				ENDSELECT
				TALENT:LCOUNT:人気 = 1
			ENDIF
		ENDIF
	NEXT
ENDIF
IF 技量:1 >= 難度:1 && 技量:2 >= 難度:2 && 技量:3 >= 難度:3
	;実績：Sense 2007のライブを成功させる
	SELECTCASE IDOL選曲
		CASE "Sense 2007"
			CALL 実績解除, "You're pretty good."
		CASE "ビビットストリーム"
			MGR:0 = 1
		CASE "Voltage"
			MGR:1 = 1
		CASE "Freeze"
			MGR:2 = 1
		CASE "CHAOS"
			MGR:3 = 1
		CASE "Air", "air"
			MGR:4 = 1
	ENDSELECT
	;実績:マイグルーヴレーダーを完成させる
	SIF MINARRAY(MGR) == 1
		CALL 実績解除, "真・足神様"
ENDIF
;ライブを行った場合はRETURN 1
RETURN 1

@担当曲判定, キャラ, 曲名
#FUNCTION
#DIM DYNAMIC キャラ
#DIMS DYNAMIC 曲名
#DIMS DYNAMIC SPLITVAR, 100

SPLIT CSTR:キャラ:担当曲, "|", SPLITVAR
IF FINDELEMENTF(SPLITVAR, 曲名) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;賄賂・枕営業----------------------------------------------------
@IDOL_BRIBE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 費用
#DIM DYNAMIC 知名度計算用
;ORIGINでは枕営業のみ
SIF ORIGIN
	GOTO MAKURA_LOOP
DRAWLINE
PRINTL 何をしますか？
PRINTL [1] 賄賂を送る
PRINTL [2] 枕営業をさせる
DRAWLINE
PRINTPLAIN 　BRIBE　
PRINTL [0] 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		;知名度が50上昇したあと1%上昇する
		;贈賄に必要な金額は、知名度をX(ただしXが40未満のとき40にする)と置くと
		;1000*X^1.5
		費用 = IDOL:知名度
		知名度計算用 = IDOL:知名度
		SIF 費用 < 40
			費用 = 40
		費用 *= SQRT(費用*1000*1000)
		;1000の倍数に四捨五入
		費用 += 500
		費用 /= 1000
		費用 *= 1000
		CALL PASELIL, @"賄賂による裏工作をするには{費用}＄必要です"
		IF MONEY < 費用
			PRINTW お金が足りません
			RESTART
		ENDIF
		CALL PASELIL, @"支払いますか？(所持金:{MONEY}＄)"
		PRINTL [0] はい
		PRINTL [1] いいえ
		BINPUT
		IF RESULT == 0
			MONEY -= 費用
			CALL 現金出納帳記録, @"交際費/-{費用}"
			IDOL:知名度 += 50
			TIMES IDOL:知名度, 1.01
			CALL PASELIW, @"{費用}＄を支払い知名度を{IDOL:知名度-知名度計算用}上昇させました"
			IF パートナー != ""
				SIF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
					TRYCALLFORM IDOL_賄賂_%CSVNAMERPS(GETNO(パートナー))%
			ENDIF
		ENDIF
		RESTART
	CASE 2
		;枕営業とレッスンの切り替えについて
		;CFLAG:レッスンの変数を次のように変換する
		;0と-1、1と-2、2と-3、3と-4……
		;負の数ならば枕営業となる
		;	Y = -X-1
		;のような式になるうえに、この逆関数は
		;	X = -Y-1
		;となるので枕営業の設定・解除のどちらの場合も
		;「-1倍して1引く」という演算で済む
		$MAKURA_LOOP
		DRAWLINE
		PRINTL □枕営業をさせるキャラを選んでください

		FOR LCOUNT, 0, CHARANUM
			CALLFORM DISABLE, "枕営業", LCOUNT
			SIF RESULT
				CONTINUE
			;体力不足だと無理
			SIF BASE:LCOUNT:体力 < 500
				CONTINUE
			IF CSTR:LCOUNT:配属 == "芸能プロダクション"
				IF CFLAG:LCOUNT:レッスン == 0
					PRINT 　　　　　
				ELSEIF CFLAG:LCOUNT:レッスン < 0
					PRINT 枕営業予定
				ELSEIF CFLAG:LCOUNT:レッスン%3 == 1
					PRINTFORM Vocal(Lv{(CFLAG:LCOUNT:レッスン+2)/3})
				ELSEIF CFLAG:LCOUNT:レッスン%3 == 2
					PRINTFORM Inst.(Lv{(CFLAG:LCOUNT:レッスン+1)/3})
				ELSE
					PRINTFORM Dance(Lv{CFLAG:LCOUNT:レッスン/3})
				ENDIF
				PRINTFORML  [{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT%(従順:{ABL:LCOUNT:従順, 2, LEFT} 技巧:{ABL:LCOUNT:技巧, 2, LEFT} 奉仕精神:{ABL:LCOUNT:奉仕精神, 2, LEFT})
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　MAKURA　
		PRINTL [0] 戻る
		BINPUT
		SELECTCASE RESULT
			CASE 0
				IF ORIGIN
					RETURN 0
				ELSE
					RESTART
				ENDIF
			CASE 1 TO CHARANUM-1
				CFLAG:RESULT:レッスン *= -1
				CFLAG:RESULT:レッスン--
				GOTO MAKURA_LOOP
		ENDSELECT
ENDSELECT

;ユニット----------------------------------------------------
@IDOL_UNIT
#DIM DYNAMIC ユニット数
#DIM DYNAMIC 選出キャラ
#DIM DYNAMIC ユニット人数
#DIM DYNAMIC 特殊ユニット, 100
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ユニット担当記録用, 1000
#DIMS DYNAMIC ユニット名記録用
DRAWLINE

ユニット数 = 0
FOR LCOUNT, 0, 11
	SIF ユニット名:LCOUNT != ""
		ユニット数++
NEXT
;それぞれの担当を記録する
FOR LCOUNT, 0, CHARANUM
	ユニット担当記録用:LCOUNT = CFLAG:LCOUNT:ユニット担当
NEXT

$MEMBER_LOOP

;組めるユニットは10チームまで
IF ユニット数 < 10
	PRINTL □ユニットを組むキャラを選んでください
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "芸能プロダクション"
			IF CFLAG:LCOUNT:ユニット番号
				PRINT  盟 
			ELSE
				SELECTCASE ユニット担当記録用:LCOUNT
					CASE 0
						PRINT 　　
					CASE 1
						PRINT Da. 
					CASE 2
						PRINT In. 
					CASE IS > 10
						SELECTCASE ユニット担当記録用:LCOUNT/10
							CASE 3
								PRINT Vo. 
							CASE 4
								PRINT D&V 
							CASE 5
								PRINT I&V 
						ENDSELECT
				ENDSELECT
			ENDIF
			PRINTFORM [{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT%(歌唱技能:{ABL:LCOUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:LCOUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:LCOUNT:舞踊技能, 2, LEFT}
			SIF TALENT:LCOUNT:魅力
				PRINT  魅力
			SIF TALENT:LCOUNT:魅惑
				PRINT  魅惑
			SIF TALENT:LCOUNT:謎の魅力
				PRINT  謎の魅力
			SIF TALENT:LCOUNT:人気
				PRINT  人気
			SIF TALENT:LCOUNT:絶対音感
				PRINT  絶対音感
			PRINTL )
		ENDIF
	NEXT
	DRAWLINE
	PRINTPLAIN 　UNIT　
	PRINT [ 0] - 戻る　
	PRINT [-1] - 選んだキャラでユニットを組む　
	SIF ユニット数
		PRINT [-2] - 既存ユニットの解散
	PRINTL 
ELSE
	PRINTL 
	PRINTL これ以上ユニットを組めません
	PRINTL 
	PRINTPLAIN 　UNIT　
	PRINT [ 0] - 戻る　
	PRINTL [-2] - 既存ユニットの解散
ENDIF

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE -1
		ユニット人数 = 0
		FOR LCOUNT, 0, CHARANUM
			SIF ユニット担当記録用:LCOUNT
				ユニット人数++
		NEXT
		IF ユニット人数 == 0
			PRINTW メンバーを指定してください
			GOTO MEMBER_LOOP
		ENDIF
		PRINTL 選択したキャラでユニットを組みます
		VARSET 特殊ユニット, 0
		ユニット名記録用 = 
		;特定の組み合わせの場合は自動でユニット名が付く
		SELECTCASE ユニット人数
			CASE 2
				FOR LCOUNT, 0, CHARANUM
					SIF !ユニット担当記録用:LCOUNT
						CONTINUE
					SELECTCASE CHARANAMEF(LCOUNT)
						CASE "梅桐 彩葉", "雛月 理々奈"
							特殊ユニット:0++
						CASE "ミミ", "ニャミ"
							特殊ユニット:1++
						CASE "オリーブ", "ヤンクル"
							特殊ユニット:2++
						CASE "東雲 夏陽", "東雲 心菜"
							特殊ユニット:3++
						CASE "三田 希少", "朝霧 春子"
							特殊ユニット:4++
						CASE "桜井 春菜", "早乙女 彩華"
							特殊ユニット:5++
						CASE "井之原 小星", "柏木 咲姫"
							特殊ユニット:6++
					ENDSELECT
				NEXT
				SIF 特殊ユニット:0 == 2
					ユニット名記録用 = オカルト研究会
				SIF 特殊ユニット:1 == 2
					ユニット名記録用 = ポッパーズ
				SIF 特殊ユニット:2 == 2
					ユニット名記録用 = コレットオーダーショップ
				SIF 特殊ユニット:3 == 2
					ユニット名記録用 = ここなつ
				SIF 特殊ユニット:4 == 2
					ユニット名記録用 = Virtual Kiss
				SIF 特殊ユニット:5 == 2
					ユニット名記録用 = bitter flavor
				SIF 特殊ユニット:6 == 2
					ユニット名記録用 = 7EVENDAYS⇔HOLIDAYS
			CASE 3
				FOR LCOUNT, 0, CHARANUM
					SIF !ユニット担当記録用:LCOUNT
						CONTINUE
					SELECTCASE CHARANAMEF(LCOUNT)
						CASE "孔雀", "ジルチ", "刃雪 達磨"
							特殊ユニット:0++
						CASE "赤シグ", "黄シグ", "青シグ"
							特殊ユニット:1++
						CASE "レイシス", "嬬武器 雷刀", "嬬武器 烈風刀"
							特殊ユニット:2++
						CASE "蒼", "雛", "桃"
							特殊ユニット:3++
						CASE "虹野 ひかり", "夢野 みらい", "星野 そら"
							特殊ユニット:4++
						CASE "璃音", "響香", "弦"
							特殊ユニット:5++
						CASE "ユーリ", "アッシュ", "スマイル"
							特殊ユニット:6++
						CASE "結城 秋葉", "月島 美奈都", "田中 フランチェスカ"
							特殊ユニット:7++
						CASE "川口 夏海", "青山 つばさ", "真田 小幸村"
							特殊ユニット:8++
						CASE "片桐 奈々菜", "日比野 記子", "立川 美翠"
							特殊ユニット:9++
						CASE "ジルチ", "光伊 計名", "識"
							特殊ユニット:10++
						CASE "星咲 あかり", "藤沢 柚子", "三角 葵"
							特殊ユニット:11++
						CASE "高瀬 梨緒", "結城 莉久", "藍原 椿"
							特殊ユニット:12++
						CASE "九條 楓", "逢坂 茜", "珠洲島 有栖"
							特殊ユニット:13++
						CASE "柏木 美亜", "日向 千夏", "東雲 つむぎ"
							特殊ユニット:14++
						CASE "芒崎 奏", "藤堂 陽南袴", "桔梗 小夜曲"
							特殊ユニット:15++
					ENDSELECT
				NEXT
				SIF 特殊ユニット:0 == 3
					ユニット名記録用 = すけべえ同盟
				SIF 特殊ユニット:1 == 3
					ユニット名記録用 = SigSig
				SIF 特殊ユニット:2 == 3
					ユニット名記録用 = ハレルヤ組
				SIF 特殊ユニット:3 == 3
					ユニット名記録用 = バタフライキャッツ
				SIF 特殊ユニット:4 == 3
					ユニット名記録用 = にじっ娘
				SIF 特殊ユニット:5 == 3
					ユニット名記録用 = ひまわり＊パンチ
				SIF 特殊ユニット:6 == 3
					ユニット名記録用 = Deuil
				SIF 特殊ユニット:7 == 3
					ユニット名記録用 = Ｃ＃
				SIF 特殊ユニット:8 == 3
					ユニット名記録用 = Rhythmixxx
				SIF 特殊ユニット:9 == 3
					ユニット名記録用 = ソーファンタスティック
				SIF 特殊ユニット:10 == 3
					ユニット名記録用 = 胃ーあーる漢方
				SIF 特殊ユニット:11 == 3
					ユニット名記録用 = ASTERISM
				SIF 特殊ユニット:12 == 3
					ユニット名記録用 = ⊿TRiEDGE
				SIF 特殊ユニット:13 == 3
					ユニット名記録用 = R.B.P.
				SIF 特殊ユニット:14 == 3
					ユニット名記録用 = マーチングポケッツ
				SIF 特殊ユニット:15 == 3
					ユニット名記録用 = S.S.L.
			CASE 4
				FOR LCOUNT, 0, CHARANUM
					SIF !ユニット担当記録用:LCOUNT
						CONTINUE
					SELECTCASE CHARANAMEF(LCOUNT)
						CASE "ウーノ", "ツースト", "若さん", "フォース"
							特殊ユニット:0++
						CASE "アン", "ンビリ", "サン", "クアトロ"
							特殊ユニット:1++
						CASE "風雅", "氷海", "烈", "鈴花"
							特殊ユニット:2++
						CASE "立川 朱音", "草壁 野々香", "伊澄 いずみ", "日毬 みさき"
							特殊ユニット:3++
						CASE "雛月 聖奈", "大牙", "弦", "東雲 夏陽"
							特殊ユニット:4++
						CASE "栗花落 夜風", "吉廻 千代", "黒川 亜理紗", "百武 もなか"
							特殊ユニット:5++
						CASE "五十嵐 撫子", "萩原 七々瀬", "葛城 華", "小野 美苗"
							特殊ユニット:6++
					ENDSELECT
				NEXT
				SIF 特殊ユニット:0 == 4
					ユニット名記録用 = ミラクル☆4
				SIF 特殊ユニット:1 == 4
					ユニット名記録用 = マジカル☆4
				SIF 特殊ユニット:2 == 4
					ユニット名記録用 = つぎドカルテット
				SIF 特殊ユニット:3 == 4
					ユニット名記録用 = クッキーパラダイス
				SIF 特殊ユニット:4 == 4
					ユニット名記録用 = リトルシスター親衛隊
				SIF 特殊ユニット:5 == 4
					ユニット名記録用 = Blanc Bunny Bandit
				SIF 特殊ユニット:6 == 4
					ユニット名記録用 = HiNaMiNa
			CASE 5
				FOR LCOUNT, 0, CHARANUM
					SIF !ユニット担当記録用:LCOUNT
						CONTINUE
					SELECTCASE CHARANAMEF(LCOUNT)
						CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる"
							特殊ユニット:0++
						CASE "山形 まり花", "和泉 一舞", "春日 咲子", "霜月 凛", "芽兎 めう"
							特殊ユニット:1++
					ENDSELECT
				NEXT
				SIF 特殊ユニット:0 == 5
					ユニット名記録用 = イロドリミドリ
				SIF 特殊ユニット:1 == 5
					ユニット名記録用 = 日向美ビタースイーツ♪
		ENDSELECT
		SIF ユニット名記録用 == ""
			PRINTL ユニット名を入力してください(何も入力しない場合はメンバー名が連ねられます)
		IF ユニット名記録用 == ""
			INPUTS
			IF RESULTS == ""
				FOR LCOUNT, 0, CHARANUM
					IF ユニット担当記録用:LCOUNT
						SIF ユニット名記録用 != ""
							ユニット名記録用 += "、"
						ユニット名記録用 += NAME:LCOUNT
					ENDIF
				NEXT
			ELSE
				ユニット名記録用 = %RESULTS%
			ENDIF
		ENDIF
		;自動で前詰め処理をするのでユニット数+1のインデックスが空いている
		ユニット名:(ユニット数+1) = %ユニット名記録用%
		FOR LCOUNT, 0, CHARANUM
			IF ユニット担当記録用:LCOUNT
				;選んだキャラのCFLAGに、属するユニット番号と役割担当を記録する
				CFLAG:LCOUNT:ユニット番号 = ユニット数+1
				CFLAG:LCOUNT:ユニット担当 = ユニット担当記録用:LCOUNT
			ENDIF
		NEXT
		RESTART
	CASE -2
		SIF !ユニット数
			CONTINUE
		DRAWLINE
		PRINTL □解散するユニットを選んでください
		FOR LCOUNT, 1, ユニット数+1
			PRINTFORML 　[{LCOUNT}] %ユニット名:LCOUNT%
		NEXT
		DRAWLINE
		PRINTPLAIN 　BREAKUP　
		PRINTL [0] - 戻る

		BINPUT
		SELECTCASE RESULT
			CASE 1 TO 10
				;解散は別関数で行う
				CALL BREAKUP, RESULT
				RESTART
			CASE 0
				GOTO MEMBER_LOOP
		ENDSELECT
	CASEELSE
		SIF ユニット数 == 10
			CONTINUE
		;二つ以上のユニットには属せない(作れるけどコードの可読性が下がるので保留)
		IF CFLAG:RESULT:ユニット番号
			PRINTFORMW %CALLNAME:RESULT%は既に「%ユニット名:(CFLAG:RESULT:ユニット番号)%」に所属しています
			GOTO MEMBER_LOOP
		ENDIF
		;おおまかな流れはライブ時の選出と同じ
		IF CSTR:RESULT:配属 == "芸能プロダクション"
			選出キャラ = RESULT
			PRINTL 
			PRINTL どのパートを担当しますか？
			PRINTL [0] ユニットから外す
			PRINTL [1] ダンス
			PRINTL [2] 楽器演奏
			PRINTL [3] ボーカル
			PRINTL [4] ダンス&ボーカル
			PRINTL [5] 楽器演奏&ボーカル
			BINPUT
			SELECTCASE RESULT
				CASE 0, 1, 2
					ユニット担当記録用:選出キャラ = RESULT
				CASE 3, 4, 5
					ユニット担当記録用:選出キャラ = RESULT
					PRINTL ボーカル曲でない場合の予備の担当を指定してください
					PRINTL [1] ダンス
					PRINTL [2] 楽器演奏
					BINPUT
					;ハイブリッド担当は予備の情報を二桁で記録する。担当/10でメイン、担当%10で予備
					ユニット担当記録用:選出キャラ = ユニット担当記録用:選出キャラ*10+RESULT
			ENDSELECT
			GOTO MEMBER_LOOP
		ENDIF
ENDSELECT

@BREAKUP, 解散ユニット
#DIM DYNAMIC 解散ユニット
#DIM DYNAMIC LCOUNT

ユニット名:解散ユニット = 

;解散ユニットに属していたメンバーはフリーになる
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:ユニット番号 == 解散ユニット
		CFLAG:LCOUNT:ユニット番号 = 0
		CFLAG:LCOUNT:ユニット担当 = 0
	ENDIF
NEXT

;ユニット名の前詰め処理
FOR LCOUNT, 解散ユニット, 10
	SWAP ユニット名:LCOUNT, ユニット名:(LCOUNT+1)
NEXT

;キャラごとにユニット名に対応した番号が割り振られているので、解散したユニット以降の番号なら減算するだけでよい
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:ユニット番号 > 解散ユニット
		CFLAG:LCOUNT:ユニット番号--
NEXT

;オプション----------------------------------------------------
;「変数IDOL:報告」は4進数で管理されている
;収支の報告……0,1,2 (3は未使用)
;枕営業の報告…0,4,8,12
@IDOL_OPTION
DRAWLINE
PRINTL 設定する項目を選んでください
PRINTL [1] ターンごとの収支報告
PRINTL [2] 枕営業の報告
DRAWLINE
PRINTPLAIN 　IDOL　
PRINT [0] - 戻る

BINPUT
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		PRINTL
		PRINTL ターンごとの収支の報告をしますか？
		PRINTL [0] する(メッセージWAITあり)
		PRINTL [1] する(メッセージWAITなし)
		PRINTL [2] しない
		BINPUT
		IDOL:報告 = IDOL:報告/4*4 + RESULT
		PRINTW 設定を変更しました
	CASE 2
		PRINTL
		PRINTL 枕営業の報告をしますか？
		PRINTL [0] プレイ内容まで全て報告する
		PRINTL [1] 経験・知名度の上昇値のみ報告
		PRINTL [2] 知名度の上昇値のみ報告
		PRINTL [3] 一切しない
		BINPUT
		IDOL:報告 = IDOL:報告%4 + RESULT*4
		PRINTW 設定を変更しました
ENDSELECT
RESTART

;経験の増加・収支の計算など----------------------------------------------------
@TURNEND_IDOL
#DIM DYNAMIC レッスンレベル
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC ユニット担当記録用, 1000
#DIM DYNAMIC ローテーション

VARSET ユニット担当記録用

FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:配属 == "芸能プロダクション"
		;1人ごとにかかる基礎費用
		ユニット担当記録用:1 += 2000
		;レッスンの内容ごとに場合分け
		;0未満は枕営業中
		IF CFLAG:LCOUNT:レッスン < 0
			CALL IDOL_MAKURA, LCOUNT
		;0の場合はレッスンなしで、歌唱・演奏・ダンスの経験がどれか1つ増える
		;サボり魔はモチベに応じてサボる 費用はちゃんと持ってかれる
		IF サボり判定(LCOUNT)
			PRINTFORML %CALLNAME:LCOUNT%はレッスンをサボりました
			CFLAG:LCOUNT:サボり = 1
			CFLAG:LCOUNT:ストレス値 -= 5
			CONTINUE
		ENDIF
		;増えるものはローテーション
		ELSEIF CFLAG:LCOUNT:レッスン == 0
			ローテーション = 2*DAY%3
			SIF ターン == "夜"
				ローテーション++
			SELECTCASE ローテーション
				CASE 2
					EXP:LCOUNT:歌唱経験++
				CASE 0
					EXP:LCOUNT:演奏経験++
				CASE 1
					EXP:LCOUNT:ダンス経験++
			ENDSELECT
			;習得100増加
			;JUEL:LCOUNT:習得 += 100
		;1以上の場合は何らかのレッスンを受けており、5*Lvの経験が入る
		ELSE
			;3で割って1余るとき、ボーカルのレッスン
			IF CFLAG:LCOUNT:レッスン%3 == 1
				レッスンレベル = (CFLAG:LCOUNT:レッスン+2)/3
				EXP:LCOUNT:歌唱経験 += 5*レッスンレベル
			;3で割って2余るとき、楽器演奏のレッスン
			ELSEIF CFLAG:LCOUNT:レッスン%3 == 2
				レッスンレベル = (CFLAG:LCOUNT:レッスン+1)/3
				EXP:LCOUNT:演奏経験 += 5*レッスンレベル
			;3で割り切れるとき、ダンスのレッスン
			ELSE
				レッスンレベル = CFLAG:LCOUNT:レッスン/3
				EXP:LCOUNT:ダンス経験 += 5*レッスンレベル
			ENDIF
			;レベルNのレッスン費用は
			;	(1+2+3+…+N)*10000 ―①
			;で求まり
			;	 1+2+3+…+N = N(N+1)/2
			;であるから、①は
			;	5000N(N+1)
			;と変形できる
			ユニット担当記録用:2 += 5000*レッスンレベル*(レッスンレベル+1)
			;レッスンレベル*200の習得増加
			;JUEL:LCOUNT:習得 += レッスンレベル*200
		ENDIF
		モチベ計算用 = 0
		;モチベーションの計算
		FOR LCOUNT2, 0, VARSIZE("TALENT")
			SIF !TALENT:LCOUNT:LCOUNT2
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT2
				CASE "恋慕", "服従", "絶対音感"
					モチベ計算用 += 2
				CASE "親愛", "隷属"
					モチベ計算用 += 3
				CASE "淫乱", "娼婦"
					モチベ計算用 -= 3
				CASE "ハイテンション", "自制心", "好奇心", "楽観的", "目立ちたがり", "寂しがり屋", "ナルシスト"
					モチベ計算用++
				CASE "プライド高い", "反抗的", "大人しい", "弱気", "無関心", "悲観的", "恥じらい", "痛みに弱い"
					モチベ計算用 -= 2
			ENDSELECT
		NEXT
		ユニット担当記録用 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数("芸能プロダクション")
		SIF TALENT:LCOUNT:世話好き
			ユニット担当記録用 -= 世話好き人数("芸能プロダクション")
		;配属先のキャラの相性で計算
		ユニット担当記録用 += 相性モチベ補正(LCOUNT)
		CFLAG:LCOUNT:モチベーション = LIMIT(ユニット担当記録用, 1, 100)
	ENDIF
NEXT

ユニット担当記録用 = ユニット担当記録用:1 + ユニット担当記録用:2
MONEY -= ユニット担当記録用
CALL 現金出納帳記録, @"芸プロ維持費/-{ユニット担当記録用:1}"
CALL 現金出納帳記録, @"レッスン費用/-{ユニット担当記録用:2}"
IF ユニット担当記録用 && IDOL:報告%4 < 2
	CALL PASELI, @"事務所の維持費用に{ユニット担当記録用:1}＄"
	IF ユニット担当記録用:2
		CALL PASELIL, @"とレッスン費用に{ユニット担当記録用:2}＄かかったので、合計{ユニット担当記録用}＄を支払いました。"
	ELSE
		PRINTL かかりました。
	ENDIF
ENDIF

;出演依頼が入る 知名度1000ごとに一日の依頼件数が増加
IF ターン == "夜" && IDOL:知名度 > 0 && MATCH(出演依頼, 0)
	PRINTFORML 芸能プロダクションに出演依頼が{MIN(MATCH(出演依頼, 0), IDOL:知名度/1000+1)}件入りました
	FOR LCOUNT, 0, MIN(MATCH(出演依頼, 0), IDOL:知名度/1000+1)
		;RANDOMIZE用の乱数
		出演依頼:FINDELEMENT(出演依頼, 0) = RAND(1, __INT_MAX__)
	NEXT
ENDIF

CALL CHECKDRAWLINEW

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "芸能プロダクション" && !CFLAG:LCOUNT:口上非表示
		TRYCALLFORM TURNEND_芸プロ_%CSVNAMERPS(NOF(LCOUNT))%
NEXT


;枕営業の処理
;ARGはキャラ登録番号
@IDOL_MAKURA, ARG
#DIM DYNAMIC 満足度
#DIM DYNAMIC ヴァギナ
#DIM DYNAMIC 中出し
#DIM DYNAMIC アナル
#DIM DYNAMIC アナル中出し
#DIM DYNAMIC フェラ
#DIM DYNAMIC 奉仕
#DIM DYNAMIC 奉仕で射精
#DIM DYNAMIC 絶頂
#DIM DYNAMIC 射精
#DIM DYNAMIC モチベ計算用

;賄賂と違い、満足度に比例して増えるだけなので
;相対的に知名度が低いときの方が効果が高い
;基本的に言われたことを何でもするような従順な子の方が満足してもらえる

;サボり魔はモチベに応じてサボる
IF サボり判定(ARG)
	PRINTFORMW %CALLNAME:ARG%は枕営業をサボりました
	CFLAG:ARG:ストレス値 -= 5
	RETURN 0
ENDIF

;能力
満足度 = 3*ABL:ARG:従順 + ABL:ARG:技巧 + 3*ABL:ARG:奉仕精神

;陥落素質
IF TALENT:ARG:淫乱
	満足度 += 6
	モチベ計算用 += 2
ENDIF
IF TALENT:ARG:娼婦
	満足度 += 9
	モチベ計算用 += 3
ENDIF
SIF TALENT:ARG:服従
	満足度 += 20
SIF TALENT:ARG:隷属
	満足度 += 30

SIF TALENT:ARG:恋慕|| TALENT:ARG:親愛
	モチベ計算用 -= 5

;性格
IF TALENT:ARG:生意気
	満足度 -= 10
	モチベ計算用 -= 2
ENDIF
IF TALENT:ARG:臆病
	満足度 += 10
	モチベ計算用 -= 2
ENDIF
SIF TALENT:ARG:素直
	満足度 += 15
IF TALENT:ARG:反抗的
	満足度 -= 15
	モチベ計算用 -= 2
ENDIF
IF TALENT:ARG:プライド高い
	満足度 -= 10
	モチベ計算用 -= 2
ENDIF
SIF TALENT:ARG:プライド低い
	満足度 += 10
SIF TALENT:ARG:ツンデレ
	満足度 -= 5

;性への関心
IF TALENT:ARG:自制心
	満足度 -= 7
	モチベ計算用 -= 2
ENDIF
SIF TALENT:ARG:保守的
	満足度 -= 10
IF TALENT:ARG:一線越えない
	満足度 -= 10
	モチベ計算用 -= 2
ENDIF
IF TALENT:ARG:目立ちたがり
	満足度 += 2
	モチベ計算用++
ENDIF

;乙女心
IF TALENT:ARG:貞操観念
	満足度 -= 20
	モチベ計算用 -= 3
ENDIF
IF TALENT:ARG:貞操無頓着
	満足度 += 5
	モチベ計算用++
ENDIF
IF TALENT:ARG:恥じらい
	満足度 -= 2
	モチベ計算用 -= 2
ENDIF

;体質
SIF TALENT:ARG:濡れにくい
	満足度 -= 1

;技術
SIF TALENT:ARG:舌使い
	満足度 += 5
IF TALENT:ARG:献身的
	満足度 += 15
	モチベ計算用++
ENDIF
SIF TALENT:ARG:潔癖症
	満足度 -= 8

;正直度
IF TALENT:ARG:淫核 || TALENT:ARG:淫膣 || TALENT:ARG:淫尻 || TALENT:ARG:淫乳
	満足度 += 4
	モチベ計算用++
ENDIF
SIF TALENT:ARG:快感に素直
	モチベ計算用++
SIF TALENT:ARG:快感の否定
	モチベ計算用 -= 3

;先天的身体特徴
IF TALENT:ARG:体格 == 1 || TALENT:ARG:小人体型
	満足度 += 20
ELSEIF TALENT:ARG:体格 == 2
	満足度 += 13
ENDIF
SELECTCASE TALENT:ARG:バストサイズ
	CASE 0, 1
		満足度 += 1
	CASE 2
		満足度 += 2
	CASE 3
		満足度 += 3
	CASE 4
		満足度 += 5
	CASE 5
		満足度 += 8
	CASE IS >= 6
		満足度 += 11
ENDSELECT

;後天的身体特徴
SIF TALENT:ARG:母乳体質
	満足度 += 3
SIF TALENT:ARG:絶倫
	満足度 += 5

;満足度1を保証
SIF 満足度 < 1
	満足度 = 1

;特別ボーナス
SIF TALENT:ARG:処女
	TIMES 満足度, 1.80
SIF TALENT:ARG:謎の魅力
	TIMES 満足度, 1.10
SIF TALENT:ARG:魅力
	TIMES 満足度, 1.05

;回数のロール
IF VAGINA(ARG)
	ヴァギナ = 5+RAND:3
	中出し = RAND:(1+ヴァギナ)
ENDIF
アナル = 1+RAND:7
アナル中出し = RAND:(1+アナル)

絶頂 = RAND:(1+ヴァギナ+アナル)/3
射精 = RAND:(1+ヴァギナ+アナル)/3

フェラ += 1+RAND:4
SELECTCASE TALENT:ARG:バストサイズ
	CASE 1
		奉仕 += 1
	CASE 2
		奉仕 += 1
	CASE 3
		奉仕 += 2
	CASE 4
		奉仕 += 3
	CASE 5
		奉仕 += 5
	CASE IS >= 6
		奉仕 += 8
ENDSELECT
奉仕 += RAND:( 1+LIMIT(ABL:ARG:奉仕精神, 0, 10) )
奉仕で射精 = (フェラ+奉仕)/3

BASE:ARG:体力 -= 40*(ヴァギナ+アナル+絶頂+射精)
BASE:ARG:体力 -= 20*(フェラ+奉仕)
IF BASE:ARG:体力 < 1
	BASE:ARG:体力 = 1
	CFLAG:ARG:回復量低下 += 2
ENDIF

IF IDOL:報告/4 == 0
	PRINTL 今日は営業先の重役を相手した。
	PRINT フェラ
	SIF 奉仕
		PRINT や奉仕
	PRINT でペニスを刺激させ
	SIF 奉仕で射精
		PRINT て射精させ
	PRINT た後、
	SIF VAGINA(ARG)
		PRINTFORM 膣を{ヴァギナ}回
	PRINTFORM アナルを{アナル}回犯され
	SIF 中出し || アナル中出し
		PRINTL 、
	SIF 中出し
		PRINTFORM 膣に{中出し}回
	SIF アナル中出し
		PRINTFORM アナルに{アナル中出し}回
	SIF 中出し || アナル中出し
		PRINT 中出しされ
	PRINTW た……
ENDIF

IF VAGINA(ARG)
	SIF IDOL:報告/4 < 2
		PRINTFORML Ｖ経験+{ヴァギナ}
	EXP:ARG:Ｖ経験 += ヴァギナ
	IF TALENT:ARG:処女
		TALENT:ARG:処女 = 0
		IF TALENT:ARG:性転換済
			CSTR:ARG:性転換処女 = 営業先の重役
		ELSE
			CSTR:ARG:処女 = 営業先の重役
		ENDIF
	ENDIF
ENDIF

IF IDOL:報告/4 < 2
	PRINTFORML Ａ経験+{アナル}
	SIF 絶頂 || 射精
		PRINTFORML 絶頂経験+{絶頂+射精}
	SIF 射精
		PRINTFORML 射精経験+{射精}
	SIF 中出し
		PRINTFORML 膣射経験+{中出し}
	PRINTFORML フェラ経験+{フェラ}
	SIF 中出し+アナル中出し+奉仕で射精
		PRINTFORML 精液経験+{中出し+アナル中出し+奉仕で射精}
ENDIF

EXP:ARG:Ａ経験 += アナル
EXP:ARG:膣射経験 += 中出し
EXP:ARG:フェラ経験 += フェラ
EXP:ARG:絶頂経験 += 絶頂+射精
EXP:ARG:射精経験 += 射精
IF TALENT:ARG:性別 == 1
	SIF IDOL:報告/4 < 2
		PRINTFORML ゲイ経験+{2*(ヴァギナ+アナル+フェラ+奉仕)}
	EXP:ARG:ゲイ経験 += 2*(ヴァギナ+アナル+フェラ+奉仕)
ENDIF
CFLAG:ARG:モチベーション = LIMIT(CFLAG:ARG:モチベーション+モチベ計算用, 1, 100)
WAIT

SIF IDOL:報告 < 3
	PRINTFORML %CALLNAME:ARG%のおかげで知名度が{満足度}上がった
IDOL:知名度 += 満足度

;満足度*100の習得
JUEL:ARG:習得 += 満足度*100

CALL CHECKDRAWLINE

@楽曲依頼文生成, 曲番号
#DIM DYNAMIC 曲番号
#DIM DYNAMIC 語尾
;RANDOMIZEは関数CALL前に行われてるので好きにやっていい 必ず同じ結果になる
;「[X] - 」の分全角スペース3つ空ける

PRINT 　　　　

SELECTCASE RAND:3
	CASE 0
		PRINTDATA
			DATA 商店街
			DATA 駅
			DATA デパート
			DATA 会社
		ENDDATA
		PRINTFORM の創立{RAND(1, 15)*10}周年記念の祝賀会に%楽曲情報:曲番号:0%を演奏して
		語尾 = RAND:8
		SELECTCASE 語尾
			CASE 0
				PRINTL ほしいんだ。
			CASE 1
				PRINTL ほしいのよ。
			CASE 2
				PRINTL ください。
			CASE 3
				PRINTL くれないか？
			CASE 4
				PRINTL ちょうだい。
			CASE 5
				PRINTL くれぬか？
			CASE 6
				PRINTL ほしいッス。
			CASE 7
				PRINTL よ！
		ENDSELECT
		PRINT 　　　　
		SELECTCASE RAND:3
			CASE 0
				SELECTCASE 語尾
					CASE 0, 7
						PRINT 僕
					CASE 3, 6
						PRINT 俺
					CASE 1, 2
						PRINT 私
					CASE 4
						PRINT あたし
					CASE 5
						PRINT わし
				ENDSELECT
				PRINT の首がかかって
				SELECTCASE 語尾
					CASE 0, 3
						PRINT いるんだ。
					CASE 1, 4
						PRINT るの。
					CASE 2
						PRINT るんです。
					CASE 3
						PRINT くれないか？
					CASE 5
						PRINT おるのじゃ。
					CASE 6
						PRINT るッス。
					CASE 7
						PRINT るんだ！
				ENDSELECT
				PRINT 絶対に成功させて
				SELECTCASE 語尾
					CASE 0, 3
						PRINTL くれ。
					CASE 1, 4
						PRINTL ね。
					CASE 2
						PRINTL ください。
					CASE 5
						PRINTL ほしい。
					CASE 6
						PRINTL くれッス。
					CASE 7
						PRINTL ね！
				ENDSELECT
			CASE 1
				PRINT 報酬は
				SELECTCASE 語尾
					CASE 0
						PRINT 弾むよ。
					CASE 1, 4
						PRINT 弾むわ。
					CASE 2
						PRINT 弾みます。
					CASE 3, 5
						PRINT 十分用意した。
					CASE 6
						PRINT 弾むッス。
					CASE 7
						PRINT 弾むよ！
				ENDSELECT
				SELECTCASE 語尾
					CASE 0
						PRINTL 期待しているよ。
					CASE 1, 4
						PRINTL 期待してるわよ。
					CASE 2
						PRINTL 期待しています。
					CASE 3
						PRINTL 期待しているぞ。
					CASE 5
						PRINTL 期待しておるぞ。
					CASE 6
						PRINTL 期待してるッスよ。
					CASE 7
						PRINTL 期待してるからね！
				ENDSELECT
			CASE 2
				PRINT みんな楽しみに
				SELECTCASE 語尾
					CASE 0, 3
						PRINTL している。頼んだぞ。
					CASE 1, 4
						PRINTL しているわ。お願いね。
					CASE 2
						PRINTL しています。頼みますよ。
					CASE 5
						PRINTL しておる。どうか頼んだ。
					CASE 6
						PRINTL してるッス。よろしくお願いしまッス。
					CASE 7
						PRINTL してるんだ！お願いだよ！
				ENDSELECT
		ENDSELECT
	CASE 1
		PRINTDATA
			DATA 学校の学園祭
			DATA 幼稚園のお遊戯会
			DATA 病院のチャリティーイベント
			DATA 老人ホームのパーティ
			DATA 飲み会の余興
			DATA 結婚式の余興
			DATA 友人へのサプライズ
		ENDDATA
		PRINTFORM で%楽曲情報:曲番号:0%の演奏を
		語尾 = RAND:6
		SELECTCASE 語尾
			CASE 0
				PRINTL 頼みたい。
			CASE 1
				PRINTL 頼みたいの。
			CASE 2
				PRINTL 頼みたいです。
			CASE 3
				PRINTL 頼む。
			CASE 4
				PRINTL 頼むッス。
			CASE 5
				PRINTL 頼むよ！
		ENDSELECT
		PRINT 　　　　
		SELECTCASE RAND:3
			CASE 0
				PRINT 他のアーティストたちには全部断られて
				SELECTCASE 語尾
					CASE 0
						PRINT そちらが
					CASE 1, 2
						PRINT あなたたちが
					CASE 3
						PRINT そなたらが
					CASE 4
						PRINT みなさんが
					CASE 5
						PRINT 君たちが
				ENDSELECT
				PRINT 最後の希望
				SELECTCASE 語尾
					CASE 0
						PRINTL なんだ。よろしく頼む。
					CASE 1
						PRINTL なの。お願いよ。
					CASE 2
						PRINTL なんです。お願いします。
					CASE 3
						PRINTL なのじゃ。どうかこの通り。
					CASE 4
						PRINTL ッス。なんでもしますから！
					CASE 5
						PRINTL なんだ！よろしくお願いします！
				ENDSELECT
			CASE 1
				PRINT みんなが喜んでいる顔が
				SELECTCASE 語尾
					CASE 0
						PRINTL 目に浮かぶね。楽しみにしているよ。
					CASE 1
						PRINTL 目に浮かぶわ。楽しみにしてるわ。
					CASE 2
						PRINTL 目に浮かびます。楽しみにしていますよ。
					CASE 3
						PRINTL 目に浮かぶ。楽しみにしておるぞ。
					CASE 4
						PRINTL 目に浮かぶッスね～。楽しみッス。
					CASE 5
						PRINTL 目に浮かぶね！楽しみだよ！
				ENDSELECT
			CASE 2
				SELECTCASE 語尾
					CASE 0
						PRINT そちらの
					CASE 1, 2
						PRINT あなたたちの
					CASE 3
						PRINT そなたらの
					CASE 4
						PRINT みなさんの
					CASE 5
						PRINT 君たちの
				ENDSELECT
				PRINT 素晴らしい演奏なら大成功
				SELECTCASE 語尾
					CASE 0
						PRINTL 間違いなしだ。
					CASE 1
						PRINTL 間違いなしね。
					CASE 2
						PRINTL 間違いなしですね。
					CASE 3
						PRINTL は間違いないはず。
					CASE 4
						PRINTL 間違いなしッス。
					CASE 5
						PRINTL 間違いなしだね！
				ENDSELECT
		ENDSELECT
	CASE 2
		PRINTDATA
			DATA 友人
			DATA 親友
			DATA 恋人
			DATA 婚約者
			DATA 夫
			DATA 妻
			DATA 弟
			DATA 妹
			DATA 兄
			DATA 姉
			DATA 義弟
			DATA 義妹
			DATA 義兄
			DATA 義姉
			DATA 義父
			DATA 義母
			DATA 息子
			DATA 娘
			DATA 孫
			DATA 祖父
			DATA 祖母
		ENDDATA
		語尾 = RAND:6
		SELECTCASE RAND:2
			CASE 0
				PRINTFORM の誕生日パーティで、大好きでよく聞いている%楽曲情報:曲番号:0%の生演奏を聴かせてあげたい
				SELECTCASE 語尾
					CASE 0
						PRINTL んだ。
					CASE 1
						PRINTL のよ。
					CASE 2
						PRINTL んです。
					CASE 3
						PRINTL のじゃ。
					CASE 4
						PRINTL ッス。
					CASE 5
						PRINTL ！
				ENDSELECT
				PRINT 　　　　
				PRINT きっと喜んでくれるはず
				SELECTCASE 語尾
					CASE 0
						PRINTL だ。頼んだぞ。
					CASE 1
						PRINTL よ。お願いね。
					CASE 2
						PRINTL です。お願いしますね。
					CASE 3
						PRINTL じゃ。よろしゅう頼む。
					CASE 4
						PRINTL ッス。頼みますよ～。
					CASE 5
						PRINTL ！頼んだよ！
				ENDSELECT
			CASE 1
				PRINT が危篤でもう長くない
				SELECTCASE 語尾
					CASE 0, 3
						PRINT らしい。
					CASE 1
						PRINT らしいわ。
					CASE 2
						PRINT ようです。
					CASE 4
						PRINT らしいッス。
					CASE 5
						PRINT らしい！
				ENDSELECT
				PRINTFORM まだ意識があるうちに、せめて最期にあの人の大好きな%楽曲情報:曲番号:0%を聴かせてあげたい
				SELECTCASE 語尾
					CASE 0
						PRINTL んだ。
					CASE 1
						PRINTL の。
					CASE 2
						PRINTL んです。
					CASE 3
						PRINTL のじゃ。
					CASE 4
						PRINTL ッス。
					CASE 5
						PRINTL んだ！
				ENDSELECT
				PRINT 　　　　
				PRINT いつ容態が悪化するかも
				SELECTCASE 語尾
					CASE 0
						PRINTL 分からない。できるだけ早く依頼を受けてほしい。
					CASE 1
						PRINTL 分からないわ。できるだけ早く来てほしいわ。
					CASE 2
						PRINTL 分かりません。急を要する事態です。お願いします。
					CASE 3
						PRINTL 分からぬ。無理は承知じゃ。どうか頼む。
					CASE 4
						PRINTL 分からないッス。どうかこの通りッス。頼みを聞いてください！
					CASE 5
						PRINTL 分からない！この依頼のためなら金は惜しまないよ！
				ENDSELECT
		ENDSELECT
ENDSELECT
