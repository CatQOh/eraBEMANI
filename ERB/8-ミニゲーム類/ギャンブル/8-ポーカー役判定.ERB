;/*役を判定する式中関数*/

;5枚のカードがストレートであるとき、5枚のランクの最大値(J:11,Q:12,K:13)を返す
;ただし、(A,2,3,4,5)は5、(10,J,Q,K,A)は14
;ストレートでないなら0を返す
@STRAIGHT(LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
#DIM DYNAMIC LCOUNT

;引数を1-13に変更
FOR LCOUNT, 0, 5
	LCARD:LCOUNT %= 13
	SIF LCARD:LCOUNT == 0
		LCARD:LCOUNT = 13
NEXT
;ここまで引数の変換
ARRAYSORT LCARD
;LCARD:0が最小、LCARD:4が最大になった
;ここからストレート判定
;連番判定の良いコードが思い浮かばないbyEnter
IF LCARD:4-LCARD:3 == 1 && LCARD:4-LCARD:2 == 2 && LCARD:4-LCARD:1 == 3
	IF LCARD:4 == 13
		IF LCARD:0 == 9
			RETURNF 13
		ELSEIF LCARD:0 == 1;Aハイのストレート
			RETURNF 14
		ENDIF
	ELSEIF LCARD:4-LCARD:0 == 4
		RETURNF LCARD:4
	ENDIF
ENDIF
RETURNF 0

;5枚のカードがフラッシュであるとき、スートに応じた1-4の値を返す
;フラッシュでないなら0を返す
@FLUSH(LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
SIF ALLSAMES(SUIT(LCARD:0), SUIT(LCARD:1), SUIT(LCARD:2), SUIT(LCARD:3), SUIT(LCARD:4))
	RETURNF SUIT(LCARD:0)+1
RETURNF 0

;5枚のカードがフォーカードであるとき、(4枚のランクの値)*100+キッカーのランクを返す
;ただし、J:11,Q:12,K:13,A:14
;フォーカードでないなら0を返す
@FOUR_OF_A_KIND(LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC HIGH
#DIM DYNAMIC LOW = 14
;引数を2-14に変更
FOR LCOUNT, 0, 5
	LCARD:LCOUNT %= 13
	SIF LCARD:LCOUNT == 0
		LCARD:LCOUNT = 13
	SIF LCARD:LCOUNT == 1
		LCARD:LCOUNT = 14
	SIF LCARD:LCOUNT > HIGH
		HIGH = LCARD:LCOUNT
	SIF LCARD:LCOUNT < LOW
		LOW = LCARD:LCOUNT
NEXT
;ここまで引数の変換
SIF GROUPMATCH(HIGH, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 4
	RETURNF 100*HIGH+LOW
SIF GROUPMATCH(LOW, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 4
	RETURNF 100*LOW+HIGH
RETURNF 0

;フォーカードでない場合に使用
;5枚のカードがフルハウスまたはスリーカードであるとき
;(3枚のランクの値)*10000+(残りの2枚でハイカードのランク)*100+(残りの2枚でローカードのランク)を返す
;フルハウスのときはその値にさらに1000000を加える
;ただし、J:11,Q:12,K:13,A:14
;2役のどちらでもないなら0を返す
@FH_OR_THREE(LCARD:0, LCARD:1, LCARD:2, LCARD:3 ,LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC HIGH
#DIM DYNAMIC MIDDLE
#DIM DYNAMIC LOW = 14
;引数を2-14に変更
FOR LCOUNT, 0, 5
	LCARD:LCOUNT %= 13
	SIF LCARD:LCOUNT == 0
		LCARD:LCOUNT = 13
	SIF LCARD:LCOUNT == 1
		LCARD:LCOUNT = 14
	SIF LCARD:LCOUNT > HIGH
		HIGH = LCARD:LCOUNT
	SIF LCARD:LCOUNT > MIDDLE && LCARD:LCOUNT < HIGH
		MIDDLE = LCARD:LCOUNT
	SIF LCARD:LCOUNT < LOW
		LOW = LCARD:LCOUNT
NEXT
;ここまで引数の変換
IF GROUPMATCH(HIGH, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 3
	IF GROUPMATCH(LOW, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 2
		RETURNF HIGH*10000+LOW*100+10000000
	ELSE
		RETURNF HIGH*10000+MIDDLE*100
	ENDIF
ENDIF

IF GROUPMATCH(LOW, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 3
	IF GROUPMATCH(HIGH, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 2
		RETURNF LOW*10000+HIGH*100+10000000
	ELSE
		RETURNF LOW*10000+HIGH*100
	ENDIF
ENDIF

;MIDDLE対象はスリーカードのみ
SIF GROUPMATCH(MIDDLE, LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4) >= 3
	RETURNF MIDDLE*10000+HIGH*100
RETURNF 0

;フォーカード・フルハウスでない場合に使用
;5枚のカードがツーペアであるとき
;(ハイカードのペアのランク)*10000+(ローカードのペアのランク)*100+(キッカーのランク)を返す
;ただし、J:11,Q:12,K:13,A:14
;ツーペアでないなら0を返す
@TWO_PAIR(LCARD:0, LCARD:1, LCARD:2, LCARD:3, LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
#DIM DYNAMIC LCOUNT
;引数を2-14に変更
FOR LCOUNT, 0, 5
	LCARD:LCOUNT %= 13
	SIF LCARD:LCOUNT == 0
		LCARD:LCOUNT = 13
	SIF LCARD:LCOUNT == 1
		LCARD:LCOUNT = 14
NEXT
ARRAYSORT LCARD
;ここまで引数の変換
IF LCARD:0 == LCARD:1 && LCARD:2 == LCARD:3
	RETURNF 10000*LCARD:2+100*LCARD:0+LCARD:4
ELSEIF LCARD:0 == LCARD:1 && LCARD:3 == LCARD:4
	RETURNF 10000*LCARD:3+100*LCARD:0+LCARD:2
ELSEIF LCARD:1 == LCARD:2 && LCARD:3 == LCARD:4
	RETURNF 10000*LCARD:3+100*LCARD:1+LCARD:0
ENDIF
RETURNF 0

;フォーカード・フルハウス・スリーカード・ツーペアでない場合に使用
;5枚のカードがワンペアであるとき
;(ペアのランク)*1000000+(キッカー1のランク)*10000+(キッカー2のランク)*100+(キッカー3のランク)を返す
;ただし、J:11,Q:12,K:13,A:14
;ワンペアでないなら0を返す
@ONE_PAIR(LCARD:0,LCARD:1,LCARD:2,LCARD:3,LCARD:4)
#FUNCTION
#DIM DYNAMIC LCARD, 5
#DIM DYNAMIC LCOUNT

;引数を2-14に変更
FOR LCOUNT, 0, 5
	LCARD:LCOUNT %= 13
	SIF LCARD:LCOUNT == 0
		LCARD:LCOUNT = 13
	SIF LCARD:LCOUNT == 1
		LCARD:LCOUNT = 14
NEXT
;ここまで引数の変換
ARRAYSORT LCARD
;LCARD:0が最小、LCARD:4が最大になった
IF LCARD:0 == LCARD:1
	RETURNF 1000000*LCARD:0+10000*LCARD:4+100*LCARD:3+LCARD:2
ELSEIF LCARD:1 == LCARD:2
	RETURNF 1000000*LCARD:1+10000*LCARD:4+100*LCARD:3+LCARD:0
ELSEIF LCARD:2 == LCARD:3
	RETURNF 1000000*LCARD:2+10000*LCARD:4+100*LCARD:1+LCARD:0
ELSEIF LCARD:3 == LCARD:4
	RETURNF 1000000*LCARD:3+10000*LCARD:2+100*LCARD:1+LCARD:0
ENDIF
RETURNF 0

@SUIT, カード
#FUNCTION
#DIM DYNAMIC カード

RETURNF ((カード+51)%52)/13
