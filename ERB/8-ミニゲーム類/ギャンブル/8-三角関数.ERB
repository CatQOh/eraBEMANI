;テイラー展開を使ってサイン(正弦)・コサイン(余弦)を求める式中関数
;1000倍したラジアン「KRAD」の値に対して1000倍した三角比を返す
;例:KSIN(524)=501  (c.f.:SIN(0.524)≒0.50035 (π/6≒0.5236))
;360°を6.284ラジアン(この関数でいう6284)として計算
;ここで扱う数値の最高値は785^6(< 2^63)なのでエラーは出ないはず
;tan(x)=sin(x)/cos(x) より、タンジェント(正接)を求めることもできるが、ゼロ除算に注意
;タンジェントのテイラー展開は面倒なのでまだ割り算のほうがマシ
;KTAN(KRAD)=1000 * KSIN(KRAD) / KCOS(KRAD) (ただしKCOS(KRAD)≠0) で求められる

@KSIN(KRAD)
#FUNCTION
#DIM DYNAMIC KRAD
#DIM DYNAMIC 正負

正負 = 1;正負は後でつける

SIF KRAD < 0
	正負 *= -1
KRAD = ABS(KRAD) % 6284;2π以下にする

SIF KRAD > 3142
	正負 *= -1
KRAD %= 3142;π以下にする

IF KRAD > 1571
	KRAD = 3142 - KRAD;π/2以下にする
ENDIF

;π/4以下にして計算する
SIF KRAD > 785
	RETURNF 正負 * KCOS(1571-KRAD)
RETURNF 正負 * (KRAD - POWER(KRAD,3) / (6*POWER(1000,2)) + POWER(KRAD,5) / (120*POWER(1000,4)))


@KCOS(ARG)
#FUNCTION
#DIM DYNAMIC KRAD
#DIM DYNAMIC 正負

正負 = 1;正負は後でつける

KRAD = ABS(KRAD) % 6284;2π以下にする

SIF KRAD > 3142
	KRAD = 6284 - KRAD;π以下にする
	
IF KRAD > 1571
	正負 *= -1
	KRAD = 3142 - KRAD;π/2以下にする
ENDIF

;π/4以下にして計算する
SIF KRAD > 785
	RETURNF 正負 * KSIN(1571-KRAD)
RETURNF 正負 * (1000 - POWER(KRAD,2) / (2*1000) + POWER(KRAD,4) / (24*POWER(1000,3)) - POWER(KRAD,6) / (720*POWER(1000,5)))
