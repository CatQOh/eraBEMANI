@MAHJONG8
#DIM DYNAMIC NUM;次に取る牌が何枚目かを記録
#DIM DYNAMIC DBX, 8, 7;待ち牌の判定に使用する2次元変数
#DIM DYNAMIC LCOUNT;関数を跨いだFOR-NEXT構文でのカウンタ変数
#DIM 牌画像生成済み
#DIMS DYNAMIC HTML用, 10
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタ2

SIF !牌画像生成済み
	CALL 牌画像生成
牌画像生成済み = 1


KYOKU_MAX = 8

FOR LCOUNT, 0, 28
	CALL PRTPAI, LCOUNT, 1
NEXT
PRINTL 
PRINTL 
WAIT

PRINTL
PRINTL ◆八牌麻雀◆
PRINTL
PRINTL 萬子・筒子・字牌のみで2面子1雀頭を作ってアガるゲームです
PRINTL [0] 始める
PRINTL [1] 設定の確認・変更
PRINTL [2] 採用役の確認
PRINTL [3] ルールを見る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			BREAK
		CASE 1
			CALL PAI8OPTION
			RESTART
		CASE 2
			PRINTL
			PRINTL 　　　※役牌…三元牌(白・發・中)の他、東・西が共通で常に役牌
			PRINTFORM １翻：立直 一発 門前清自摸和 断么九 平和 役牌 一盃口 海底模月 河底撈魚
			SIF M8:抜きドラ嶺上
				PRINT  嶺上開花
			PRINTL
			PRINTFORML ２翻：ダブル立直 混老頭 対々和 混全帯么九
			PRINTFORML ３翻：混一色 純全帯么九
			PRINTL ６翻：清一色
			PRINT 役満：天和 地和 人和 清老頭 字一色
			SIF M8:抜きドラ小四喜
				PRINT  小四喜
			PRINTW
			RESTART
		CASE 3
			PRINTL
			PRINTL 八牌麻雀は次の25種類の牌を使って遊ぶゲームです
			PRINT 萬子:
			FOR LCOUNT, 0, 9
				CALL PRTPAI, LCOUNT
				IF LCOUNT == 8
					PRINTL
					BREAK
				ENDIF
				PRINT  
			NEXT
			CALL EMPTYLINE, 4

			PRINT 筒子:
			FOR LCOUNT, 9, 18
				CALL PRTPAI, LCOUNT
				IF LCOUNT == 17
					PRINTL
					BREAK
				ENDIF
				PRINT  
			NEXT
			CALL EMPTYLINE, 4

			PRINT 字牌:
			FOR LCOUNT, 18, 25
				CALL PRTPAI, LCOUNT
				IF LCOUNT == 24
					PRINTL
					BREAK
				ENDIF
				PRINT  
			NEXT
			CALL EMPTYLINE, 4

			PRINTL これらを組み合わせて2面子1雀頭で何らかの役を作ることでアガることができます
			PRINTL 　　面子…「順子」か「刻子」のこと
			PRINTL 　　　　順子（シュンツ）…「２３４」や「７８９」などを萬子か筒子で揃えたもの（※「８９１」「９１２」はダメ）
			PRINTL 　　　　刻子（コーツ）……「８８８」や「北北北」などの3つセットを揃えたもの
			PRINTL 　　雀頭…「２２」や「中中」などの2つセットを揃えたもの
			PRINTL 各プレイヤーは7枚の牌を所持していて、毎ターン1枚引いて1枚捨てることでゲームが進行します
			PRINTL 1枚引いたときにアガることができるとき、「ツモ」を宣言することでアガることができます
			PRINTL また、相手プレイヤーが捨てた牌を使ってアガることができるのなら「ロン」を宣言することでアガることができます
			PRINTL 誰かがアガるか、両プレイヤーが18枚ずつ牌を捨てるまでを1局として、
			PRINTFORML {KYOKU_MAX}局終わったときに、点数が多いほうが勝ちとなります
			PRINTL
			PRINTL 通常の麻雀とは違い、「ポン」「チー」「カン」ができません
			PRINTL フリテン立直や立直後の見逃しは認めています(ロン和了りはできなくなります)
			PRINTL 流局時のテンパイ料は場に3000点となります
			WAIT
			RESTART
	ENDSELECT
LOOP 1

VARSET PAI8SCORE, 50000
KYOKU = 0
VARLINE = LINECOUNT
REDRAW 0


$RESET
IF KYOKU >= KYOKU_MAX
	PRINTL
	PRINTL ゲーム終了
	PRINTFORML %NAME:MASTER%:{PAI8SCORE}点
	PRINTFORMW %NAME:TARGET%:{PAI8SCORE:1}点
	PRINTL
	RETURN PAI8SCORE, PAI8SCORE:1
ENDIF

VARSET KAWA
VARSET GARI
VARSET TURN
VARSET RICHI, -1
VARSET MINOGASHI
VARSET SOKU

;ヤマのシャッフル
IF M8:花牌
	CALL SHUFFLE_M8, 104
ELSE
	CALL SHUFFLE_M8, 100
ENDIF

;配牌と抜きドラの処理
NUM = 0
FOR LCOUNT, 0, 7
	$YAMA_LOOP0
	IF YAMA:NUM >= 100 || (YAMA:NUM %25 == 21 && M8:抜きドラ)
		GARI:0:0++
		GARI:0:(GARI:0:0) = YAMA:NUM
		NUM++
		GOTO YAMA_LOOP0
	ENDIF
	TEHAI:0:LCOUNT = YAMA:NUM
	NUM++
NEXT
FOR LCOUNT, 0, 7
	$YAMA_LOOP1
	IF YAMA:NUM >= 100 || (YAMA:NUM %25 == 21 && M8:抜きドラ)
		GARI:1:0++
		GARI:1:(GARI:1:0) = YAMA:NUM
		NUM++
		GOTO YAMA_LOOP1
	ENDIF
	TEHAI:1:LCOUNT = YAMA:NUM
	NUM++
NEXT

;1局目のみ親はランダム
KYOKU++
SIF KYOKU == 1
	TONCHA = RAND:2
PRINTL
IF TONCHA == 0
	PRINTFORMW %NAME:MASTER%が親です
ELSE
	PRINTFORMW %NAME:TARGET%が親です
	CALL LOOP_CLEARLINE
	GOTO AI_START
ENDIF

CALL LOOP_CLEARLINE

DO
	;ここからプレイヤーのターン
	SIF SOKU == 2
		SOKU = 1
	RINSHAN = 0
	$TSUMO_LOOP0
	TSUMO = YAMA:NUM
	CALL SORTPAI, 0
	DRAWLINE
	PRINTL
	FOR LCOUNT, 0, 7
		CALL PRTPAI, 200
		PRINT  
	NEXT
	IF GARI:1:0
		PRINT  　　　抜きドラ:
		FOR LCOUNT, 1, GARI:1:0 + 1
			CALL PRTPAI, GARI:1:LCOUNT
		NEXT
	ENDIF
	CALL EMPTYLINE, 4

	CALL RICHI_TURN, 1
	SIF TURN:1
		CALL 河表示, 1
	CALL EMPTYLINE, 4

	CALL PRT8PAI_SCORE
	SIF TURN
		CALL 河表示, 0
	CALL EMPTYLINE, 4

	CALL RICHI_TURN, 0
	PRINTL
	FOR LCOUNT, 0, 7
		CALL PRTPAI, TEHAI:0:LCOUNT
		PRINT  
	NEXT
	PRINT  
	CALL PRTPAI, TSUMO
	IF GARI:0:0
		PRINT 　　抜きドラ:
		FOR LCOUNT, 1, GARI:0:0 + 1
			CALL PRTPAI, GARI:0:LCOUNT
		NEXT
	ENDIF
	CALL EMPTYLINE, 4

	IF RICHI == -1
		PRINT [0]  [1]  [2]  [3]  [4]  [5]  [6]   
	ELSE
		PRINTPLAIN 　 　 　 　 　 　 　  
	ENDIF
	IF RICHI == -1 || SHANTEN(0, -1) == -1
		PRINT [7]
	ELSE
		PRINTPLAIN 　 
	ENDIF
	SIF SHANTEN(0, -1) <= 0 && RICHI == -1
		PRINT  [8]リーチ
	SIF SHANTEN(0, -1) == -1
		PRINT  [9]ツモ
	PRINTL
	IF TSUMO >= 100 || (YAMA:NUM %25 == 21 && M8:抜きドラ)
		GARI:0:0++
		GARI:0:(GARI:0:0) = TSUMO
		NUM++
		RINSHAN++
		IF M8:抜きドラ一発消し
			SOKU = 0
			SOKU:1 = 0
		ENDIF
		TWAIT 400, 0
		CALL LOOP_CLEARLINE
		GOTO TSUMO_LOOP0
	ENDIF
	IF RICHI != -1 && SHANTEN(0, -1) != -1
		TWAIT 400, 0
		GOTO TSUMOGIRI
	ENDIF
	$INPUTLOOP
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 6
			IF RICHI == -1
				KAWA:0:TURN = TEHAI:0:RESULT
				TEHAI:0:RESULT = TSUMO
			ELSE
				GOTO INPUTLOOP
			ENDIF
		CASE 7
			$TSUMOGIRI
			KAWA:0:TURN = TSUMO
		CASE 8
			IF SHANTEN(0, -1) <= 0
				CLEARLINE 7
				FOR LCOUNT, 0, 7
					CALL PRTPAI, TEHAI:0:LCOUNT
					PRINT  
				NEXT
				PRINT  
				CALL PRTPAI, TSUMO
				CALL EMPTYLINE, 4

				VARSET DBX
				FOR LCOUNT, 0, 8
					IF SHANTEN(0, LCOUNT) == 0
						カウンタ = 0
						カウンタ2 = 0
						DO
							IF カウンタ != LCOUNT && カウンタ < 7
								DBX:LCOUNT:カウンタ2 = TEHAI:0:カウンタ
								カウンタ2++
							ELSEIF  カウンタ != LCOUNT && カウンタ == 7
								DBX:LCOUNT:カウンタ2 = TSUMO
								カウンタ2++
							ENDIF
							カウンタ++
						LOOP カウンタ2 <= 6
						HTML用:LCOUNT = <button value='{LCOUNT}' title='待ちは「%MACHI(DBX:LCOUNT:0,DBX:LCOUNT:1,DBX:LCOUNT:2,DBX:LCOUNT:3,DBX:LCOUNT:4,DBX:LCOUNT:5,DBX:LCOUNT:6)%」です'>[{LCOUNT}]  </button>
					ELSE
						HTML用:LCOUNT = <font color='gray'><nonbutton title='この牌を切るとテンパイしません'>[-]  </nonbutton></font>
					ENDIF
				NEXT
				HTML_PRINT STRJOIN(HTML用, "")
				;HTML_PRINT LOCALS:0 + LOCALS:1 + LOCALS:2 + LOCALS:3 + LOCALS:4 + LOCALS:5 + LOCALS:6 + " " + LOCALS:7
				BINPUT
				IF RESULT == 7
					KAWA:0:TURN = TSUMO
				ELSE
					KAWA:0:TURN = TEHAI:0:RESULT
					TEHAI:0:RESULT = TSUMO
				ENDIF
				PAI8SCORE -= 1000
				DEPOSITION += 1000
				RICHI = TURN
				SOKU = 2
			ELSE
				GOTO INPUTLOOP
			ENDIF
		CASE 9
			IF SHANTEN(0, -1) == -1
				CALL PAI8SCORE(0, TSUMO, 1)
				PAI8SCORE += RESULT
				PAI8SCORE:1 -= RESULT
				PAI8SCORE += DEPOSITION
				DEPOSITION = 0
				TONCHA = 0
				GOTO RESET
			ELSE
				GOTO INPUTLOOP
			ENDIF
		CASEELSE
			GOTO INPUTLOOP
	ENDSELECT
	SIF SOKU == 1
		SOKU = 0
	;AIは見逃しをしない
	IF DEAGARI(1, KAWA:0:TURN)
		CALL LOOP_CLEARLINE
		DRAWLINE
		CALL SORTPAI, 1
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, TEHAI:1:LCOUNT
			PRINT  
		NEXT
		IF GARI:1:0
			PRINT  　　　抜きドラ:
			FOR LCOUNT, 1, GARI:1:0 + 1
				CALL PRTPAI, GARI:1:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4

		PRINTL 　 　 　 　 　 　 　 ロン
		CALL RICHI_TURN, 1
		SIF TURN:1
			CALL 河表示, 1
		PRINTL
		CALL EMPTYLINE, 5
		CALL PRT8PAI_SCORE
		SIF TURN+1
			CALL 河表示, 0, 1
		CALL EMPTYLINE, 4

		CALL RICHI_TURN, 0
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, TEHAI:0:LCOUNT
			PRINT  
		NEXT
		IF GARI:0:0
			PRINT  　　　抜きドラ:
			FOR LCOUNT, 1, GARI:0:0 + 1
				CALL PRTPAI, GARI:0:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4
		WAIT

		CALL PAI8SCORE(1, KAWA:0:TURN, 0)
		PAI8SCORE -= RESULT
		PAI8SCORE:1 += RESULT
		PAI8SCORE:1 += DEPOSITION
		DEPOSITION = 0
		;立直が成立してないので1000点戻す
		IF SOKU == 2
			PAI8SCORE += 1000
			PAI8SCORE -= 1000
		ENDIF
		TONCHA = 1
		GOTO RESET
	ENDIF
	NUM++
	TURN++
	SIF TURN == 18 && TURN:1 == 18
		BREAK
	CALL LINEPLUS
	
	$AI_START
	;ここから相手プレイヤーのターン
	SIF SOKU:1 == 2
		SOKU:1 = 1
	RINSHAN = 0
	$TSUMO_LOOP1
	TSUMO = YAMA:NUM
	CALL SORTPAI, 1
	DRAWLINE
	PRINTL
	FOR LCOUNT, 0, 7
		CALL PRTPAI, 200
		PRINT  
	NEXT
	IF TSUMO >= 100 || (YAMA:NUM %25 == 21 && M8:抜きドラ)
		CALL PRTPAI, TSUMO
	ELSE
		CALL PRTPAI, 200
	ENDIF
	IF GARI:1:0
		PRINT 　　抜きドラ:
		FOR LCOUNT, 1, GARI:1:0 + 1
			CALL PRTPAI, GARI:1:LCOUNT
		NEXT
	ENDIF
	CALL EMPTYLINE, 4

	CALL RICHI_TURN, 1
	SIF TURN:1
		CALL 河表示, 1
	CALL EMPTYLINE, 4
	CALL PRT8PAI_SCORE
	SIF TURN:0
		CALL 河表示, 0
	CALL EMPTYLINE, 4

	CALL RICHI_TURN, 0
	PRINTL
	FOR LCOUNT, 0, 7
		CALL PRTPAI, TEHAI:0:LCOUNT
		PRINT  
	NEXT
	IF GARI:0:0
		PRINT  　　　抜きドラ:
		FOR LCOUNT, 1, GARI:0:0 + 1
			CALL PRTPAI, GARI:0:LCOUNT
		NEXT
	ENDIF
	CALL EMPTYLINE, 4

	TWAIT 400, 0
	IF TSUMO >= 100 || (YAMA:NUM %25 == 21 && M8:抜きドラ)
		GARI:1:0++
		GARI:1:(GARI:1:0) = TSUMO
		NUM++
		RINSHAN++
		IF M8:抜きドラ一発消し
			SOKU = 0
			SOKU:1 = 0
		ENDIF
		TWAIT 400, 0
		CALL LOOP_CLEARLINE
		GOTO TSUMO_LOOP1
	ENDIF
	;ここからAIの思考
	;アガれるなら絶対アガる
	IF SHANTEN(1, -1) == -1
		CALL LINEPLUS
		DRAWLINE
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, TEHAI:1:LCOUNT
			PRINT  
		NEXT
		PRINT  
		CALL PRTPAI, TSUMO
		IF GARI:1:0
			PRINT 　　抜きドラ:
			FOR LCOUNT, 1, GARI:1:0 + 1
				CALL PRTPAI, GARI:1:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4

		PRINTL 　 　 　 　 　 　 　 ツモ
		CALL RICHI_TURN, 1
		SIF TURN:1
			CALL 河表示, 1
		PRINTL
		CALL EMPTYLINE, 5
		CALL PRT8PAI_SCORE
		SIF TURN:0
			CALL 河表示, 0
		CALL EMPTYLINE, 4

		CALL RICHI_TURN, 0
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, TEHAI:0:LCOUNT
			PRINT  
		NEXT
		IF GARI:0:0
			PRINT  　　　抜きドラ:
			FOR LCOUNT, 1, GARI:0:0 + 1
				CALL PRTPAI, GARI:0:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4
		WAIT

		CALL PAI8SCORE(1, TSUMO, 1)
		PAI8SCORE -= RESULT
		PAI8SCORE:1 += RESULT
		PAI8SCORE:1 += DEPOSITION
		DEPOSITION = 0
		TONCHA = 1
		GOTO RESET
	ENDIF
	;アガれないとき
	;立直をかけているならツモ切る
	IF RICHI:1 != -1
		KAWA:1:(TURN:1) = TSUMO
		GOTO DAHAI
	ENDIF
	;立直をかけていないとき
	;ランダムな位置からシャンテン数が少なくなる牌を探す
	LCOUNT = RAND:7
	FOR LCOUNT, 1, LCOUNT+7
		IF SHANTEN(1, LCOUNT%7) < SHANTEN(1, 7)
			KAWA:1:(TURN:1) = TEHAI:1:(LCOUNT%7)
			TEHAI:1:(LCOUNT%7) = TSUMO
			GOTO DAHAI
		ENDIF
	NEXT
	;ない場合、ランダムな位置からシャンテン数が変わらない牌を探す
	LCOUNT = RAND:8
	FOR LCOUNT, 1, LCOUNT+8
		IF LCOUNT%8 == 7
			KAWA:1:(TURN:1) = TSUMO
		ELSEIF SHANTEN(1, LCOUNT%8) <= SHANTEN(1, 7)
			KAWA:1:(TURN:1) = TEHAI:1:(LCOUNT%8)
			TEHAI:1:(LCOUNT%8) = TSUMO
			GOTO DAHAI
		ENDIF
	NEXT
	$DAHAI
	;聴牌即リー
	IF RICHI:1 == -1 && SHANTEN(1, 7) == 0
		PAI8SCORE:1 -= 1000
		DEPOSITION += 1000
		RICHI:1 = TURN:1
		SOKU:1 = 2
	ENDIF
	SIF SOKU:1 == 1
		SOKU:1 = 0
	;出和了りができるとき
	IF DEAGARI(0, KAWA:1:(TURN:1))
		CALL LOOP_CLEARLINE
		DRAWLINE
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, 200
			PRINT  
		NEXT
		IF GARI:1:0
			PRINT  　　　抜きドラ:
			FOR LCOUNT, 1, GARI:1:0 + 1
				CALL PRTPAI, GARI:1:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4

		CALL RICHI_TURN, 1
		SIF TURN:1+1
			CALL 河表示, 1, 1
		CALL EMPTYLINE, 4

		CALL PRT8PAI_SCORE
		SIF TURN
			CALL 河表示, 0
		PRINTL
		CALL EMPTYLINE, 4

		CALL RICHI_TURN, 0
		PRINTL
		FOR LCOUNT, 0, 7
			CALL PRTPAI, TEHAI:0:LCOUNT
			PRINT  
		NEXT
		IF GARI:0:0
			PRINT  　　　抜きドラ:
			FOR LCOUNT, 1, GARI:0:0 + 1
				CALL PRTPAI, GARI:0:LCOUNT
			NEXT
		ENDIF
		CALL EMPTYLINE, 4

		PRINTPLAIN 　 　 　 　 　 　 　  　  
		PRINTL [9]ロン [10]パス
		BINPUT
		SELECTCASE RESULT
			CASE 9
				CALL PAI8SCORE(0, KAWA:1:(TURN:1), 0)
				PAI8SCORE += RESULT
				PAI8SCORE:1 -= RESULT
				PAI8SCORE += DEPOSITION
				DEPOSITION = 0
				;立直が成立してないので1000点戻す
				IF SOKU:1 == 2
					PAI8SCORE -= 1000
					PAI8SCORE:1 += 1000
				ENDIF
				TONCHA = 0
				GOTO RESET
			CASE 10
				MINOGASHI = 1
		ENDSELECT
	ELSE
		TWAIT 400, 0
	ENDIF
	NUM++
	TURN:1++
	SIF TURN == 18 && TURN:1 == 18
		BREAK
	CALL LOOP_CLEARLINE
LOOP 1
;流局時の処理
CALL LOOP_CLEARLINE
CALL SORTPAI, 0
CALL SORTPAI, 1
DRAWLINE
PRINTL
FOR LCOUNT, 0, 7
	CALL PRTPAI, TEHAI:1:LCOUNT
	PRINT  
NEXT
IF GARI:1:0
	PRINT  　　　抜きドラ:
	FOR LCOUNT, 1, GARI:1:0 + 1
		CALL PRTPAI, GARI:1:LCOUNT
	NEXT
ENDIF
CALL EMPTYLINE, 4

IF SHANTEN(1, 7) == 0
	PRINTL 　 　 　 　 　 　 　 テンパイ
ELSE
	PRINTL 　 　 　 　 　 　 　 ノーテン
ENDIF
CALL RICHI_TURN, 1
SIF TURN:1
	CALL 河表示, 1
CALL EMPTYLINE, 4

CALL PRT8PAI_SCORE
SIF TURN:0
	CALL 河表示, 0
CALL EMPTYLINE, 4

CALL RICHI_TURN, 0
IF SHANTEN(0, 7) == 0
	PRINTL 　 　 　 　 　 　 　 テンパイ
ELSE
	PRINTL 　 　 　 　 　 　 　 ノーテン
ENDIF
FOR LCOUNT, 0, 7
	CALL PRTPAI, TEHAI:0:LCOUNT
	PRINT  
NEXT
IF GARI:0:0
	PRINT  　　　抜きドラ:
	FOR LCOUNT, 1, GARI:0:0 + 1
		CALL PRTPAI, GARI:0:LCOUNT
	NEXT
ENDIF
CALL EMPTYLINE, 4
WAIT

IF SHANTEN(0, 7) == 0 && SHANTEN(1, 7) != 0
	PAI8SCORE += 3000
	PAI8SCORE:1 -= 3000
ELSEIF SHANTEN(0, 7) != 0 && SHANTEN(1, 7) == 0
	PAI8SCORE -= 3000
	PAI8SCORE:1 += 3000
ENDIF
GOTO RESET


@PRT8PAI_SCORE
;スコア部分の表示
IF TONCHA == 1
	PRINT (親) 
ELSE
	PRINT (子) 
ENDIF
PRINTFORML %NAME:TARGET%:{PAI8SCORE:1}点
PRINTL
PRINTFORM {KYOKU}局目/全{KYOKU_MAX}局　ドラ表示牌:
CALL PRTPAI, YAMA:98
SIF DEPOSITION
	PRINTFORM 　供託:{DEPOSITION}点
CALL EMPTYLINE, 4

IF TONCHA == 0
	PRINT (親) 
ELSE
	PRINT (子) 
ENDIF
PRINTFORML %NAME:MASTER%:{PAI8SCORE}点
;CALL EMPTYLINE, 5


@RICHI_TURN, ループ回数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ループ回数
;リーチしているなら「ﾘｰﾁ」と出る
FOR LCOUNT, 0, TURN:ループ回数+1
	IF LCOUNT == RICHI:ループ回数
		FONTBOLD
		CALL PRINTCOLOR, "ﾘｰﾁ", "magenta"
		FONTREGULAR
		BREAK
	ELSE
		PRINT 　
	ENDIF
	SIF LCOUNT%6 == 5
		PRINT  
NEXT
PRINTL


@PAI8OPTION
PRINTL
PRINTL 　　　　　現在の設定
PRINT 　　赤牌　　赤五萬：
IF M8:赤五萬
	PRINTFORML %TOFULL(@"{M8:赤五萬}")%枚
ELSE
	PRINTL なし
ENDIF

PRINT 　　　　　　赤五筒：
IF M8:赤五筒
	PRINTFORM %TOFULL(@"{M8:赤五筒}")%枚
ELSE
	PRINT なし
ENDIF

IF M8:赤五筒役
	PRINTL (役と認める)
ELSE
	PRINTL (役ではない)
ENDIF

PRINT 　　　　　　赤三筒：
SELECTCASE M8:赤三筒
	CASE 0
		PRINTL なし
	CASE 1, 2
		PRINTFORML %TOFULL(@"{M8:赤三筒}")%枚
	CASE 3
		PRINTL 赤金
ENDSELECT

PRINT 　　　　　　　赤北：
IF M8:赤北
	PRINTL １枚
ELSE
	PRINTL なし
ENDIF

PRINT 抜きドラ　　　花牌：
IF M8:花牌
	PRINTL あり
ELSE
	PRINTL なし
ENDIF

PRINT 　　　　　北の扱い：
IF M8:抜きドラ
	PRINTL 抜きドラ
ELSE
	PRINTL 通常の牌
ENDIF

PRINT 　　　　　嶺上開花：
IF M8:抜きドラ嶺上
	PRINTL あり
ELSE
	PRINTL なし
ENDIF

PRINT 　　　　　一発消し：
IF M8:抜きドラ一発消し
	PRINTL あり
ELSE
	PRINTL なし
ENDIF

PRINT 　　　　　　小四喜：
IF M8:抜きドラ小四喜
	PRINTL あり
ELSE
	PRINTL なし
ENDIF

PRINTL
PRINTL [0] 赤牌の設定を変える
PRINTL [1] 抜きドラの設定を変える
PRINTL [9] 設定変更を終える
INPUT
IF RESULT == 0
	DO
		PRINTL
		PRINTL 　　　現在の設定　　　　 ＜設定したい項目を選んでください＞

		PRINTPLAIN 　　　　　赤五萬：
		IF M8:赤五萬
			PRINTPLAINFORM %TOFULL(@"{M8:赤五萬}")%枚
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [ 0]なし [ 1]１枚 [ 2]２枚 [ 4]４枚

		PRINTPLAIN 　　　　　赤五筒：
		IF M8:赤五筒
			PRINTPLAINFORM %TOFULL(@"{M8:赤五筒}")%枚
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [10]なし [11]１枚 [12]２枚 [14]４枚

		PRINTPLAIN 赤五筒を役とする：
		IF M8:赤五筒役
			PRINTPLAIN (役と認める)
		ELSE
			PRINTPLAIN (役ではない)
		ENDIF
		PRINTL [20]なし [21]あり

		PRINTPLAIN 　　　　　赤三筒：
		SELECTCASE M8:赤三筒
			CASE 0
				PRINTPLAIN なし
			CASE 1, 2
				PRINTPLAINFORM %TOFULL(@"{M8:赤三筒}")%枚
			CASE 3
				PRINTPLAIN 赤金
		ENDSELECT
		PRINTL [30]なし [31]１枚 [32]２枚 [33]赤金

		PRINTPLAIN 　　　　　　赤北：
		IF M8:赤北
			PRINTPLAIN 1枚
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [40]なし [41]１枚
		PRINTPLAIN 　　　　　　　　　　　　
		PRINTL [50]設定を終える
		BINPUT
		SELECTCASE RESULT
			CASE IS < 10
				M8:赤五萬 = RESULT
			CASE IS < 20
				M8:赤五筒 = RESULT%10
			CASE IS < 30
				M8:赤五筒役 = RESULT%10
			CASE IS < 40
				M8:赤三筒 = RESULT%10
			CASE IS < 50
				M8:赤北 = RESULT%10
			CASE 50
				BREAK
		ENDSELECT
	LOOP 1
	RESTART
ELSEIF RESULT == 1
	DO
		PRINTL
		PRINTL 　　　　現在の設定　　　　＜設定したい項目を選んでください＞

		PRINTPLAIN 　　　　　　　花牌：
		IF M8:花牌
			PRINTPLAIN あり
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [ 0]なし [ 1]あり

		PRINTPLAIN 北を抜きドラとする：
		IF M8:抜きドラ
			PRINTPLAIN 抜きドラ
		ELSE
			PRINTPLAIN 通常の牌
		ENDIF
		PRINTL [10]なし [11]あり

		PRINTPLAIN 抜きドラで嶺上開花：
		IF M8:抜きドラ嶺上
			PRINTPLAIN あり
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [20]なし [21]あり

		PRINTPLAIN 抜きドラで一発消し：
		IF M8:抜きドラ一発消し
			PRINTPLAIN あり
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [30]なし [31]あり

		PRINTPLAIN 北３枚抜きの小四喜：
		IF M8:抜きドラ小四喜
			PRINTPLAIN あり
		ELSE
			PRINTPLAIN なし
		ENDIF
		PRINTL [40]なし [41]あり
		PRINTPLAIN 　　　　　　　　　　　　　
		PRINTL [50]設定を終える
		BINPUT
		SIF RESULT == 50
			BREAK
		M8:(RESULT/10) = RESULT%10
	LOOP 1
	RESTART
ENDIF

@牌画像生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC CM, 5, 5

CM:0:0 = 0xFF, 0, 0, 0, 0
CM:1:0 = 0, 0xFF, 0, 0, 0
CM:2:0 = 0, 0, 0xFF, 0, 0
CM:3:0 = 0x8F, 0, 0, 0xFF, 0
CM:4:0 = 0, 0, 0, 0, 0xFF

GCREATEFROMFILE 10000, "麻雀牌.png"
GCREATE 10001, GWIDTH(10000), GHEIGHT(10000)

CM:0:0 = 0xFF, 0, 0, 0, 0
CM:1:0 = 0, 0xFF, 0, 0, 0
CM:2:0 = 0, 0, 0xFF, 0, 0
CM:3:0 = 0x8F, 0, 0, 0xFF, 0
CM:4:0 = 0, 0, 0, 0, 0xFF
GDRAWG 10001, 10000, 0, 0, GWIDTH(10001), GHEIGHT(10001), 0, 0, GWIDTH(10000), GHEIGHT(10000), CM:0:0

GCREATE 10002, GWIDTH(10000), GHEIGHT(10000)
CM:3:0 = 0x8F, 0x8F, 0, 0xFF, 0
GDRAWG 10002, 10000, 0, 0, GWIDTH(10002), GHEIGHT(10002), 0, 0, GWIDTH(10000), GHEIGHT(10000), CM:0:0

GCREATE 10003, GWIDTH(10000), GHEIGHT(10000)
CM:0:0 = 0x22, 0, 0, 0, 0
CM:1:0 = 0, 0x8B, 0, 0, 0
CM:2:0 = 0, 0, 0x22, 0, 0
CM:3:0 = 0, 0, 0, 0xFF, 0
GDRAWG 10003, 10000, 0, 0, GWIDTH(10003), GHEIGHT(10003), 0, 0, GWIDTH(10000), GHEIGHT(10000), CM:0:0

FOR LCOUNT, 0, 29
	SELECTCASE LCOUNT
		CASE 0 TO 8
			SPRITECREATE 牌名:LCOUNT, 10000, LCOUNT*84+4, 20, 84, 134
			SIF LCOUNT == 4
				SPRITECREATE "赤五萬", 10001, LCOUNT*84+4, 20, 84, 134
		CASE 9 TO 17
			SPRITECREATE 牌名:LCOUNT, 10000, (LCOUNT-9)*84+4, 200, 84, 134
			IF LCOUNT == 11
				SPRITECREATE "赤三筒", 10001, (LCOUNT-9)*84+4, 200, 84, 134
				SPRITECREATE "金三筒", 10002, (LCOUNT-9)*84+4, 200, 84, 134
			ENDIF
			SIF LCOUNT == 13
				SPRITECREATE "赤五筒", 10001, (LCOUNT-9)*84+4, 200, 84, 134
		CASEELSE
			SPRITECREATE 牌名:LCOUNT, 10000, (LCOUNT-18)*84+4, 560, 84, 134
			SIF 牌名:LCOUNT == "北"
				SPRITECREATE "赤北", 10001, (LCOUNT-18)*84+4, 560, 84, 134
			SIF 牌名:LCOUNT == "白"
				SPRITECREATE "牌背", 10003, (LCOUNT-18)*84+4, 560, 84, 134
	ENDSELECT
NEXT

GCREATE 10004, GHEIGHT(10000), GWIDTH(10000)
GCREATE 10005, GHEIGHT(10000), GWIDTH(10000)
GCREATE 10006, GHEIGHT(10000), GWIDTH(10000)
GCREATE 10007, GHEIGHT(10000), GWIDTH(10000)
GDRAWGWITHROTATE 10004, 10000, 90, 370, 375
GDRAWGWITHROTATE 10005, 10001, 90
GDRAWGWITHROTATE 10006, 10002, 90
GDRAWGWITHROTATE 10007, 10003, 90

GSAVE 10004, 1004

FOR LCOUNT, 0, 29
	SELECTCASE LCOUNT
		CASE 0 TO 8
			SPRITECREATE 牌名:LCOUNT+"リーチ", 10004, GHEIGHT(10000)-154, LCOUNT*84+4, 134, 84
			SIF LCOUNT == 4
				SPRITECREATE "赤五萬リーチ", 10005, GHEIGHT(10000)-154, LCOUNT*84+4, 134, 84
		CASE 9 TO 17
			SPRITECREATE 牌名:LCOUNT+"リーチ", 10004, GHEIGHT(10000)-334, (LCOUNT-9)*84+4, 134, 84
			IF LCOUNT == 11
				SPRITECREATE "赤三筒リーチ", 10005, GHEIGHT(10000)-334, (LCOUNT-9)*84+4, 134, 84
				SPRITECREATE "金三筒リーチ", 10006, GHEIGHT(10000)-334, (LCOUNT-9)*84+4, 134, 84
			ENDIF
			SIF LCOUNT == 13
				SPRITECREATE "赤五筒リーチ", 10005, GHEIGHT(10000)-334, (LCOUNT-9)*84+4, 134, 84
		CASEELSE
			SPRITECREATE 牌名:LCOUNT+"リーチ", 10004, GHEIGHT(10000)-694, (LCOUNT-18)*84+4, 134, 84
			SIF 牌名:LCOUNT == "北"
				SPRITECREATE "赤北リーチ", 10005, GHEIGHT(10000)-694, (LCOUNT-18)*84+4, 134, 84
			SIF 牌名:LCOUNT == "白"
				SPRITECREATE "牌背リーチ", 10007, GHEIGHT(10000)-694, (LCOUNT-18)*84+4, 134, 84
	ENDSELECT
NEXT
