; /* 八牌麻雀の役判定用の関数集 */
;同時成立の関係などから処理をまとめている場合があります
;
;清老頭
;字一色
;清一色+混一色
;純チャン+チャンタ
;混老頭+対々和
;タンヤオ 「ヤオ」はUnicodeでは「%UNICODE(0x4E48)%」
;平和
;役牌
;一盃口
;
;プレイヤー…和了したプレイヤー アガリ牌…アガリ牌 ツモ…ツモフラグ
@CHINROTO(プレイヤー, アガリ牌)
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT, 0, 8
	IF LCOUNT == 7
		役計算用 = アガリ牌 %25
	ELSE
		役計算用 = TEHAI:プレイヤー:LCOUNT %25
	ENDIF
	SIF 役計算用 > 17 || (1 <= 役計算用%9 && 役計算用%9 <= 7)
		RETURNF 0
NEXT

RETURNF 1


@SHOSUSHI(プレイヤー, アガリ牌)
#FUNCTION
#DIM DYNAMIC PEI;北の枚数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

IF GARI:プレイヤー:0 && M8:抜きドラ小四喜
	FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
		SIF GARI:プレイヤー:LCOUNT % 25 == 21
			PEI++
	NEXT
ELSE
	RETURNF 0
ENDIF

SIF PEI < 3
	RETURNF 0

FOR LCOUNT, 0, 8
	IF LCOUNT == 7
		役計算用 = アガリ牌 %25
	ELSE
		役計算用 = TEHAI:プレイヤー:LCOUNT %25
	ENDIF
	SIF 役計算用 < 18 || 20 < 役計算用
		RETURNF 0
NEXT

RETURNF 1


@TSUISO(プレイヤー, アガリ牌)
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT, 0, 8
	IF LCOUNT == 7
		役計算用 = アガリ牌 %25
	ELSE
		役計算用 = TEHAI:プレイヤー:LCOUNT %25
	ENDIF
	SIF 役計算用 < 18
		RETURNF 0
NEXT

RETURNF 1


@SOME(プレイヤー, アガリ牌)
;↑サムじゃなくて染め
;清一色に対して2、混一色に対して1、それ以外には0を返す
#FUNCTION
#DIM DYNAMIC PAI, 3;牌の色ごとの枚数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT, 0, 7
	IF TEHAI:プレイヤー:LCOUNT %25 /9 == 0
	;萬子
		PAI:0++
	ELSEIF TEHAI:プレイヤー:LCOUNT %25 /9 == 1
	;筒子
		PAI:1++
	ELSE
	;字牌
		PAI:2++
	ENDIF
NEXT
IF アガリ牌 %25 /9 == 0
	PAI:0++
ELSEIF アガリ牌 %25 /9 == 1
	PAI:1++
ELSE
	PAI:2++
ENDIF
 
IF PAI:0 && PAI:1
	RETURNF 0
ELSEIF PAI:0 || PAI:1
	IF PAI:2
		RETURNF 1
	ELSE
		RETURNF 2
	ENDIF
ELSE
;字一色
	RETURNF 0
ENDIF


@CHANTA(プレイヤー, アガリ牌)
;純チャンに対して2、チャンタに対して1、それ以外には0を返す
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC SET;面子と雀頭を数える
#DIM DYNAMIC DET;純チャンかチャンタかの判定
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

FOR LCOUNT, 0, 18
	IF LCOUNT%9 == 0 || LCOUNT%9 == 6
		IF PAI:LCOUNT >= 2 && PAI:(LCOUNT+1) >= 2 && PAI:(LCOUNT+2) >= 2
			PAI:LCOUNT -= 2
			PAI:(LCOUNT+1) -= 2
			PAI:(LCOUNT+2) -= 2
			SET += 2
		ELSEIF PAI:LCOUNT && PAI:(LCOUNT+1) && PAI:(LCOUNT+2)
			PAI:LCOUNT--
			PAI:(LCOUNT+1)--
			PAI:(LCOUNT+2)--
			SET++
		ENDIF
	ENDIF
NEXT

IF SET == 0
	;123or789の順子がなければ0を返す
	RETURNF 0
ELSEIF SET == 1
	IF PAI:0 == 3;一萬
		PAI:0 -= 3
		SET++
	ENDIF
	IF PAI:8 == 3;九萬
		PAI:8 -= 3
		SET++
	ENDIF
	IF PAI:9 == 3;一筒
		PAI:9 -= 3
		SET++
	ENDIF
	IF PAI:17 == 3;九筒
		PAI:9 -= 3
		SET++
	ENDIF
	FOR LCOUNT2, 18, 25;字牌
		IF PAI:LCOUNT2 == 3
			PAI:LCOUNT2 -= 3
			SET++
			DET++
			BREAK
		ENDIF
	NEXT
	SIF SET == 1
		RETURNF 0
ENDIF

;対子の確認 この時点で必ずSET==2
SIF PAI:0 == 2;一萬
	SET++
SIF PAI:8 == 2;九萬
	SET++
SIF PAI:9 == 2;一筒
	SET++
SIF PAI:17 == 2;九筒
	SET++
FOR LCOUNT2, 18, 25;字牌
	IF PAI:LCOUNT2 == 2
		SET++
		DET++
		BREAK
	ENDIF
NEXT

SIF SET != 3
	RETURNF 0
SIF DET
	RETURNF 1
RETURNF 2

@HONTOI(プレイヤー, アガリ牌)
;対々和なら1を返す混老頭も複合しているのなら2を返す
;清老頭でも2を返すので、この関数は役満ではないときに使う
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC KOTSU;刻子の数のカウント
#DIM DYNAMIC DET;混老頭かどうかの判定に使用(0なら成立,1以上なら不成立)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

FOR LCOUNT, 0, 25
	IF PAI:LCOUNT >= 3
		PAI:LCOUNT -= 3
		KOTSU++
		DET++
		SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
			DET--
	ENDIF
NEXT

SIF KOTSU != 2
	RETURNF 0

;刻子で2メンツがある状態でヘッドがあれば1を返す
FOR LCOUNT, 0, 25
	IF PAI:LCOUNT == 2
		DET++
		SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
			DET--
		SIF DET
			RETURNF 1
		RETURNF 2
	ENDIF
NEXT

RETURNF 0

@TANYAO(プレイヤー, アガリ牌)
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

SIF PAI:0;一萬
	RETURNF 0
SIF PAI:8;九萬
	RETURNF 0
SIF PAI:9;一筒
	RETURNF 0
FOR LCOUNT, 17, 25;九筒と字牌
	SIF PAI:LCOUNT
		RETURNF 0
NEXT
RETURNF 1


@PINFU(プレイヤー, アガリ牌)
;平和 判定が最強クラスに面倒
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数(最初に固定)
#DIM DYNAMIC PAI2, 25;種類別の牌の枚数(計算用 メンツ切り出しによって減っていく)
#DIM DYNAMIC DET;アガリ牌が面子に含まれているかどうかの判定
#DIM DYNAMIC SET;面子の数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LCOUNT3
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

FOR LCOUNT2, 0, 16
	IF LCOUNT2%9 < 7
		;面子構成は順子+順子+対子のみ
		VARSET DET
		VARSET SET
		ARRAYCOPY "PAI", "PAI2"
		FOR LCOUNT, LCOUNT2, LCOUNT2+18
			IF LCOUNT%9 < 7
				DO
					IF PAI2:(LCOUNT%18) && PAI2:(LCOUNT%18+1) && PAI2:(LCOUNT%18+2)
						IF DET == 0 && (LCOUNT%18 == アガリ牌 %25 || LCOUNT%18+2 == アガリ牌 %25)
							IF (LCOUNT%9 == 0 && LCOUNT%18+2 == アガリ牌 %25) || (LCOUNT%9 == 6 && LCOUNT%18 == アガリ牌 %25)
								BREAK
							ELSE
								PAI2:(LCOUNT%18)--
								PAI2:(LCOUNT%18+1)--
								PAI2:(LCOUNT%18+2)--
								SET++
								DET++
							ENDIF
						ELSEIF DET == 0 && LCOUNT%18+1 == アガリ牌 %25
							BREAK
						ELSE
							PAI2:(LCOUNT%18)--
							PAI2:(LCOUNT%18+1)--
							PAI2:(LCOUNT%18+2)--
							SET++
						ENDIF
					ENDIF
				LOOP PAI2:(LCOUNT%18) && PAI2:(LCOUNT%18+1) && PAI2:(LCOUNT%18+2)
			ENDIF
		NEXT
		FOR LCOUNT, 0, 22
			IF PAI2:LCOUNT >= 2 && LCOUNT != 18 && LCOUNT != 20
				SET++
				BREAK
			ENDIF
		NEXT
		SIF SET == 3 && DET
			RETURNF 1
	ENDIF
NEXT
RETURNF 0

@YAKUHAI(プレイヤー, アガリ牌)
#FUNCTION
;処理が同じため、ビットごとに処理
;字牌が暗刻の場合に特定のビットを1にする
;東:1 (南:2) 西:4 (北:8) 白:16 發:32 中:64
#DIM DYNAMIC YAKU
#DIM DYNAMIC TON
#DIM DYNAMIC SHA
#DIM DYNAMIC HAKU
#DIM DYNAMIC HATSU
#DIM DYNAMIC CHUN
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 7
	IF TEHAI:プレイヤー:LCOUNT2 %25 == 18
		TON++
	ELSEIF TEHAI:プレイヤー:LCOUNT2 %25 == 20
		SHA++
	ELSEIF TEHAI:プレイヤー:LCOUNT2 %25 == 22
		HAKU++
	ELSEIF TEHAI:プレイヤー:LCOUNT2 %25 == 23
		HATSU++
	ELSEIF TEHAI:プレイヤー:LCOUNT2 %25 == 24
		CHUN++
	ENDIF
NEXT

IF アガリ牌 %25 == 18
	TON++
ELSEIF アガリ牌 %25 == 20
	SHA++
ELSEIF アガリ牌 %25 == 22
	HAKU++
ELSEIF アガリ牌 %25 == 23
	HATSU++
ELSEIF アガリ牌 %25 == 24
	CHUN++
ENDIF

SIF TON == 3
	YAKU += 1
SIF SHA == 3
	YAKU += 4
SIF HAKU == 3
	YAKU += 16
SIF HATSU == 3
	YAKU += 32
SIF CHUN == 3
	YAKU += 64

RETURNF YAKU


@IPEKO(プレイヤー, アガリ牌)
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC DET;一盃口かどうかの判別
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

FOR LCOUNT, 0, 16
	IF LCOUNT%9 < 7
		IF PAI:LCOUNT >= 2 && PAI:(LCOUNT+1) >= 2 && PAI:(LCOUNT+2) >= 2
			PAI:LCOUNT -= 2
			PAI:(LCOUNT+1) -= 2
			PAI:(LCOUNT+2) -= 2
			DET++
			BREAK
		ENDIF
	ENDIF
NEXT

SIF DET == 0
	RETURNF 0

;一盃口の2メンツがある状態でヘッドがあれば1を返す
FOR LCOUNT, 0, 25
	SIF PAI:LCOUNT == 2
		RETURNF 1
NEXT

RETURNF 0


@PAI8SCORE(プレイヤー, アガリ牌, ツモ)
;プレイヤーはプレイヤー アガリ牌は和了り牌 ツモはツモフラグ
#DIM DYNAMIC FU;符を数えるための変数
#DIM DYNAMIC HAN;翻数を数えるための変数
#DIM DYNAMIC SCORE;最終的な得点計算のための変数
#DIM DYNAMIC YAKUMAN;役満を数えるための変数
#DIM DYNAMIC PINFU;平和かどうかの判定に使用
#DIM DYNAMIC DORA;ドラを数えるための変数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

;ここから役満
PRINTL
PRINT ドラ表示牌:
CALL PRTPAI, YAMA:98
IF RICHI:プレイヤー >= 0
	PRINT 　裏ドラ表示牌:
	CALL PRTPAI, YAMA:99
ENDIF
PRINTL
PRINTL
IF TURN:プレイヤー == 0
	IF ツモ
		IF TURN:((プレイヤー+1)%2) == 0
			PRINTL 天和　　　　　役満
			YAKUMAN++
		ELSE
			PRINTL 地和　　　　　役満
			YAKUMAN++
		ENDIF
	ELSE
		PRINTL 人和　　　　　役満
		YAKUMAN++
	ENDIF
ENDIF
IF CHINROTO(プレイヤー, アガリ牌)
	PRINTL 清老頭　　　　役満
	YAKUMAN++
ENDIF
IF SHOSUSHI(プレイヤー, アガリ牌)
	PRINTL 小四喜　　　　役満
	YAKUMAN++
ENDIF
IF TSUISO(プレイヤー, アガリ牌)
	PRINTL 字一色　　　　役満
	YAKUMAN++
ENDIF
;ここまで役満
IF YAKUMAN
	IF TONCHA == プレイヤー
		SCORE = YAKUMAN * 8000 * 6
	ELSE
		SCORE = YAKUMAN * 8000 * 4
	ENDIF
	SIF YAKUMAN == 2
		PRINT ダブル
	SIF YAKUMAN == 3
		PRINT トリプル
	PRINTFORMW 役満　{SCORE}点
	RETURN SCORE
ELSE
	SELECTCASE RICHI:プレイヤー
		CASE 0
			PRINTL ダブル立直　　2翻
			HAN += 2
		CASE IS > 0
			PRINTL 立直　　　　　1翻
			HAN += 1
	ENDSELECT
	IF SOKU:プレイヤー
		PRINTL 一発　　　　　1翻
		HAN += 1
	ENDIF
	SELECTCASE SOME(プレイヤー, アガリ牌)
		CASE 2
			PRINTL 清一色　　　　6翻
			HAN += 6
		CASE 1
			PRINTL 混一色　　　　3翻
			HAN += 3
	ENDSELECT
	SELECTCASE CHANTA(プレイヤー, アガリ牌)
		CASE 2
			PRINTFORML 純全帯%UNICODE(0x4E48)%九　　3翻
			HAN += 3
		CASE 1
			PRINTFORML 混全帯%UNICODE(0x4E48)%九　　2翻
			HAN += 2
	ENDSELECT
	SELECTCASE HONTOI(プレイヤー, アガリ牌)
		CASE 2
			PRINTL 混老頭　　　　2翻
			PRINTL 対々和　　　　2翻
			HAN += 4
		CASE 1
			PRINTL 対々和　　　　2翻
			HAN += 2
	ENDSELECT
	IF TANYAO(プレイヤー, アガリ牌)
		PRINTFORML 断%UNICODE(0x4E48)%九　　　　1翻
		HAN += 1
	ENDIF
	IF PINFU(プレイヤー, アガリ牌)
		PRINTL 平和　　　　　1翻
		PINFU++
		HAN += 1
	ENDIF
	IF ツモ
		PRINTL 門前清自摸和　1翻
		HAN += 1
	ENDIF
	IF ツモ && RINSHAN && M8:抜きドラ嶺上
		PRINTL 嶺上開花　　　1翻
		HAN += 1
	ENDIF
	役計算用 = YAKUHAI(プレイヤー, アガリ牌)
	IF 役計算用 & 1
		PRINTL 東　　　　　　1翻
		HAN += 1
	ELSEIF 役計算用 & 4
		PRINTL 西　　　　　　1翻
		HAN += 1
	ELSEIF 役計算用 & 16
		PRINTL 白　　　　　　1翻
		HAN += 1
	ELSEIF 役計算用 & 32
		PRINTL 發　　　　　　1翻
		HAN += 1
	ELSEIF 役計算用 & 64
		PRINTL 中　　　　　　1翻
		HAN += 1
	ENDIF
	IF IPEKO(プレイヤー, アガリ牌)
		PRINTL 一盃口　　　　1翻
		HAN += 1
	ENDIF
	IF TURN:プレイヤー == 17
		IF ツモ && TURN:((プレイヤー+1)%2) == 18
			PRINTL 海底摸月　　　1翻
			HAN += 1
		ENDIF
	ENDIF
	IF TURN:プレイヤー == 18
		PRINTL 河底撈魚　　　1翻
		HAN += 1
	ENDIF
	;ここからドラ計上
	;表ドラ
	;花牌がドラ表示牌のとき、花牌をドラとする
	IF YAMA:98 >= 100 && GARI:プレイヤー:0
		FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
			IF GARI:プレイヤー:LCOUNT >= 100
				DORA++
				HAN++
			ENDIF
		NEXT
	ELSEIF YAMA:98 < 100
		SELECTCASE YAMA:98 %25
			CASE 8
				役計算用 = 0
			CASE 17
				役計算用 = 9
			CASE 21
				役計算用 = 18
			CASE 24
				役計算用 = 22
			CASEELSE
				役計算用 = YAMA:98 %25 + 1
		ENDSELECT
		DORA = 0
		IF 役計算用 == 21 && M8:抜きドラ
			IF GARI:プレイヤー:0
				FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
					IF GARI:プレイヤー:LCOUNT %25 == 21
						DORA++
						HAN++
					ENDIF
				NEXT
			ENDIF
		ELSE
			FOR LCOUNT, 0, 7
				IF TEHAI:プレイヤー:LCOUNT %25 == 役計算用
					DORA++
					HAN++
				ENDIF
			NEXT
			IF アガリ牌 %25 == 役計算用
				DORA++
				HAN++
			ENDIF
		ENDIF
	ENDIF
	PRINTFORML 表ドラ　　　　{DORA}翻
	;裏ドラ
	IF RICHI:プレイヤー >= 0
		IF YAMA:99 >= 100 && GARI:プレイヤー:0
			FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
				IF GARI:プレイヤー:LCOUNT >= 100
					DORA++
					HAN++
				ENDIF
			NEXT
		ELSEIF YAMA:99 < 100
			SELECTCASE YAMA:99 %25
				CASE 8
					役計算用 = 0
				CASE 17
					役計算用 = 9
				CASE 21
					役計算用 = 18
				CASE 24
					役計算用 = 22
				CASEELSE
					役計算用 = YAMA:99 %25 + 1
			ENDSELECT
			DORA = 0
			IF 役計算用 == 21 && M8:抜きドラ
				IF GARI:プレイヤー:0
					FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
						IF GARI:プレイヤー:LCOUNT %25 == 21
							DORA++
							HAN++
						ENDIF
					NEXT
				ENDIF
			ELSE
				FOR LCOUNT, 0, 7
					IF TEHAI:プレイヤー:LCOUNT %25 == 役計算用
						DORA++
						HAN++
					ENDIF
				NEXT
				IF アガリ牌 %25 == 役計算用
					DORA++
					HAN++
				ENDIF
			ENDIF
		ENDIF
		PRINTFORML 裏ドラ　　　　{DORA}翻
	ENDIF
	DORA = 0
	FOR LCOUNT, 0, 8
		IF LCOUNT == 7
			役計算用:LCOUNT = アガリ牌
		ELSE
			役計算用:LCOUNT = TEHAI:プレイヤー:LCOUNT
		ENDIF
	NEXT
	FOR LCOUNT, 0, 8
		;赤五萬
		IF (役計算用:LCOUNT == 79 && M8:赤五萬) || (役計算用:LCOUNT == 54 && M8:赤五萬 >= 2) || (役計算用:LCOUNT %25 == 4 && M8:赤五萬 >= 3)
			DORA++
			HAN++
		;赤五筒
		ELSEIF (役計算用:LCOUNT == 88 && M8:赤五筒) || (役計算用:LCOUNT == 63 && M8:赤五筒 >= 2) || (役計算用:LCOUNT %25 == 13 && M8:赤五筒 >= 3)
			DORA++
			HAN++
		;赤三筒
		ELSEIF (役計算用:LCOUNT == 86 && M8:赤三筒) || (役計算用:LCOUNT == 61 && M8:赤三筒 == 2)
			DORA++
			HAN++
		;金三筒もといスーパー赤三筒
		ELSEIF (役計算用:LCOUNT == 61 && M8:赤三筒 >= 3)
			DORA++
			HAN++
		;赤北
		ELSEIF (役計算用:LCOUNT == 96 && M8:赤北)
			DORA++
			HAN++
		ENDIF
	NEXT
	IF GARI:プレイヤー:0 && M8:赤北
		FOR LCOUNT, 1, GARI:プレイヤー:0 + 1
			IF GARI:プレイヤー:LCOUNT == 96
				DORA++
				HAN++
			ENDIF
		NEXT
	ENDIF
	SIF DORA
		PRINTFORML 赤ドラ　　　　{DORA}翻
	;抜きドラ
	IF GARI:プレイヤー:0
		HAN += GARI:プレイヤー:0
		PRINTFORML 抜きドラ　　　{GARI:プレイヤー:0}翻
	ENDIF
	;ここまでドラ計上
	IF PINFU
		IF ツモ
			FU = 20
		ELSE
			FU = 30
		ENDIF
	ELSE
		IF ツモ
			FU = 30
		ELSE
			FU = 40
		ENDIF
		FU += FUHANE(プレイヤー, アガリ牌, ツモ)
	ENDIF
	SELECTCASE HAN
		CASE IS >= 13
			SCORE = 8000
		CASE 11, 12
			SCORE = 6000
		CASE 8, 9, 10
			SCORE = 4000
		CASE 6, 7
			SCORE = 3000
		CASE 5
			SCORE = 2000
		CASEELSE
			SCORE = FU * POWER(2, 2+HAN)
			SIF SCORE > 2000
				SCORE = 2000
	ENDSELECT
	IF TONCHA == プレイヤー
		SCORE *= 6
	ELSE
		SCORE *= 4
	ENDIF
	SIF SCORE%100 != 0
		SCORE += 100-SCORE%100
	PRINTFORMW {FU}符{HAN}翻　{SCORE}点
	RETURN SCORE
ENDIF


@FUHANE(プレイヤー, アガリ牌, ツモ)
;八牌麻雀においては、
;ロン和了り50符……8符の暗刻+(2符の雀頭+両面以外 or 4符の明刻) OR 4符の暗刻*2+2符の役牌単騎待ち
;ツモ和了り40符……8符の暗刻+(2符の雀頭or両面以外) OR 4符の暗刻*2+2符の雀頭
;対象となる和了りのとき10を返す そうではないときは0
;プレイヤー…プレーヤー アガリ牌…和了り牌 ツモ…ツモフラグ
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数(最初に固定)
#DIM DYNAMIC PAI2, 25;種類別の牌の枚数(計算用 メンツ切り出しによって減っていく)
#DIM DYNAMIC FU;符の計算に使用
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

FOR LCOUNT2, 0, 25
	FOR LCOUNT, 0, 7
		SIF TEHAI:プレイヤー:LCOUNT %25 == LCOUNT2
			PAI:LCOUNT2++
	NEXT
	SIF アガリ牌 %25 == LCOUNT2
		PAI:LCOUNT2++
NEXT

;ツモ和了りのとき
IF ツモ
	;「8符の暗刻+辺張嵌張単騎」以外すべてのケース
	ARRAYCOPY "PAI", "PAI2"
	FU = 2
	FOR LCOUNT, 0, 25
		IF PAI2:LCOUNT >= 3
			PAI2:LCOUNT -= 3
			FU += 4
			SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
				FU += 4
		ENDIF
	NEXT
	FOR LCOUNT, 18, 25
		;役牌対子
		IF LCOUNT != 19 && LCOUNT != 21
			SIF PAI2:LCOUNT == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU > 10
		RETURNF 10
	;「8符の暗刻+辺張嵌張単騎」のケースのみ
	ARRAYCOPY "PAI", "PAI2"
	FU = 0
	FOR LCOUNT, 0, 25
		IF PAI2:LCOUNT >= 3
			PAI2:LCOUNT -= 3
			FU += 4
			SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
				FU += 4
			BREAK
		ENDIF
	NEXT
	SIF FU != 8
		RETURNF 0
	FOR LCOUNT, 0, 25
		IF PAI2:LCOUNT >= 2
			PAI2:LCOUNT -= 2
			SIF LCOUNT == アガリ牌 %25
				RETURNF 10
			BREAK
		ENDIF
		SIF LCOUNT == 24
			RETURNF 0
	NEXT
	FOR LCOUNT, 0, 16
		IF LCOUNT%9 < 7
			IF PAI2:LCOUNT && PAI2:(LCOUNT+1) && PAI2:(LCOUNT+2)
				IF LCOUNT+1 == アガリ牌 %25
					RETURNF 10
				ELSEIF LCOUNT%9 == 0 && LCOUNT+2 == アガリ牌 %25
					RETURNF 10
				ELSEIF LCOUNT%9 == 6 && LCOUNT == アガリ牌 %25
					RETURNF 10
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
		ENDIF
	NEXT
	RETURNF 0
;ロン和了りの場合
ELSE
	;「8符の暗刻+2符の雀頭+辺張嵌張」以外すべてのケース
	ARRAYCOPY "PAI", "PAI2"
	FU = 0
	FOR LCOUNT, 0, 25
		IF PAI2:LCOUNT >= 3
			IF LCOUNT == アガリ牌 %25
				PAI2:LCOUNT -= 3
				FU += 2
				SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
					FU += 2
			ELSE
				PAI2:LCOUNT -= 3
				FU += 4
				SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
					FU += 4
			ENDIF
		ENDIF
	NEXT
	FOR LCOUNT, 18, 25
		;役牌対子
		IF LCOUNT != 19 && LCOUNT != 21
			SIF PAI2:LCOUNT == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU > 10
		RETURNF 10
	;「8符の暗刻+2符の雀頭+辺張嵌張」のケースのみ
	ARRAYCOPY "PAI", "PAI2"
	FU = 0
	FOR LCOUNT, 0, 25
		IF PAI2:LCOUNT >= 3
			IF LCOUNT == アガリ牌 %25
				PAI2:LCOUNT -= 3
				FU += 2
				SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
					FU += 2
			ELSE
				PAI2:LCOUNT -= 3
				FU += 4
				SIF LCOUNT%9 == 0 || LCOUNT%9 == 8 || LCOUNT >= 18
					FU += 4
			ENDIF
			BREAK
		ENDIF
	NEXT
	SIF FU != 8
		RETURNF 0
	FOR LCOUNT, 18, 25
		;役牌対子
		IF LCOUNT != 19 && LCOUNT != 21
			SIF PAI2:LCOUNT == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU != 10
		RETURNF 0
	FOR LCOUNT, 0, 16
		IF LCOUNT%9 < 7
			IF PAI2:LCOUNT && PAI2:(LCOUNT+1) && PAI2:(LCOUNT+2)
				IF LCOUNT+1 == アガリ牌 %25
					RETURNF 10
				ELSEIF LCOUNT%9 == 0 && LCOUNT+2 == アガリ牌 %25
					RETURNF 10
				ELSEIF LCOUNT%9 == 6 && LCOUNT == アガリ牌 %25
					RETURNF 10
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
		ENDIF
	NEXT
	RETURNF 0
ENDIF


@DEAGARI(プレイヤー, アガリ牌)
;出和了り可能かどうか判定する式中関数
;OKなら1,NGなら0
;プレイヤー…プレイヤー アガリ牌…和了り牌
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC プレイヤー
#DIM DYNAMIC アガリ牌
#DIM DYNAMIC 役計算用, 10
#DIM DYNAMIC ツモ

;和了形になっていないならNG
SIF SHANTEN8(TEHAI:プレイヤー:0, TEHAI:プレイヤー:1, TEHAI:プレイヤー:2, TEHAI:プレイヤー:3, TEHAI:プレイヤー:4, TEHAI:プレイヤー:5, TEHAI:プレイヤー:6, アガリ牌, -1) != -1
	RETURNF 0

;河に牌が1枚もないなら人和が成立するのでOK
SIF TURN:プレイヤー == 0
	RETURNF 1

;フリテンならNG
FOR 役計算用, 0, TURN:プレイヤー
	SIF SHANTEN8(TEHAI:プレイヤー:0, TEHAI:プレイヤー:1, TEHAI:プレイヤー:2, TEHAI:プレイヤー:3, TEHAI:プレイヤー:4, TEHAI:プレイヤー:5, TEHAI:プレイヤー:6, KAWA:プレイヤー:役計算用, -1) == -1
		RETURNF 0
NEXT
SIF MINOGASHI
	RETURNF 0

;あとは何かの役があればOK
SIF RICHI:プレイヤー >= 0
	RETURNF 1
SIF CHINROTO(プレイヤー, アガリ牌)
	RETURNF 1
SIF TSUISO(プレイヤー, アガリ牌)
	RETURNF 1
SIF SOME(プレイヤー, アガリ牌)
	RETURNF 1
SIF CHANTA(プレイヤー, アガリ牌)
	RETURNF 1
SIF HONTOI(プレイヤー, アガリ牌)
	RETURNF 1
SIF TANYAO(プレイヤー, アガリ牌)
	RETURNF 1
SIF PINFU(プレイヤー, アガリ牌)
	RETURNF 1
SIF YAKUHAI(プレイヤー, アガリ牌)
	RETURNF 1
SIF IPEKO(プレイヤー, アガリ牌)
	RETURNF 1
SIF TURN:プレイヤー == 18
	RETURNF 1

;赤五筒が役になるとき
IF M8:赤五筒役
	FOR LCOUNT, 0, 8
		IF LCOUNT == 7
			役計算用:LCOUNT = アガリ牌
		ELSE
			役計算用:LCOUNT = TEHAI:プレイヤー:LCOUNT
		ENDIF
	NEXT
	FOR LCOUNT, 0, 8
		SIF (役計算用:LCOUNT == 88 && M8:赤五筒) || (役計算用:LCOUNT == 63 && M8:赤五筒 >= 2) || (役計算用:LCOUNT %25 == 13 && M8:赤五筒 >= 3)
			RETURNF 1
	NEXT
ENDIF

RETURNF 0
