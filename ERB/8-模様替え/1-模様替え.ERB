@模様替え
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LRESULT
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM グリッド表示 = 1
#DIMS DYNAMIC 表示用変数, 100
#DIM DYNAMIC 部屋配置
#DIM DYNAMIC 幅, 2
#DIM DYNAMIC 始点, 2
#DIM DYNAMIC 始点保存用 = -1, -1
#DIMS DYNAMIC 部屋情報, 200, 3
#DIM DYNAMIC 設置済み, 200
#DIMS DYNAMIC 部屋名
#DIMS DYNAMIC 表示チップ
#DIM DYNAMIC マップ横幅
#DIM DYNAMIC マップ縦幅
#DIM DYNAMIC 壁無し
#DIM DYNAMIC タイル表示幅
#DIM DYNAMIC タイル始点, 200, 2
#DIM DYNAMIC 生け簀始点, 2
#DIMS DYNAMIC オブジェクト名
#DIM DYNAMIC オブジェクト幅, 2
#DIM DYNAMIC オブジェクト角度
#DIMS DYNAMIC オブジェクト配置
#DIMS DYNAMIC オブジェクト配置向き
#DIM DYNAMIC オブジェクト配置角度
#DIM DYNAMIC オブジェクト表示済み
#DIM DYNAMIC 選択座標, 2
#DIMS DYNAMIC 選択部屋
#DIMS DYNAMIC 選択オブジェクト
#DIM DYNAMIC 畑始点
#DIM DYNAMIC プランター始点
#DIM DYNAMIC 田んぼ始点
#DIM DYNAMIC 居住区設置数
#DIM DYNAMIC 居住区, 100
#DIMS DYNAMIC 住人, 2
#DIM DYNAMIC 部屋番号
#DIM DYNAMIC 居住LCOUNT
#DIM DYNAMIC 住人数
#DIM DYNAMIC ROOMCOUNT
#DIM DYNAMIC 使用グラフィック
#DIMS DYNAMIC HTML用
#DIM DYNAMIC L_Y
#DIM DYNAMIC ROOMID
#DIM DYNAMIC にんじん表示済み
#DIM DYNAMIC にんじん座標, 2

VARLINE = LINECOUNT
CALL マップバージョン管理
CALL オブジェクトデータ

ROOMCOUNT = 1
;部屋名, X座標の長さ, Y座標の長さ 1次元目の数値は部屋IDとしても扱う 0は欠番
部屋情報:(ROOMCOUNT++):0 '= "レッスン室", "7", "5"
部屋情報:(ROOMCOUNT++):0 '= "医務室", "7", "5"
部屋情報:(ROOMCOUNT++):0 '= "実験室", "7", "5"
部屋情報:(ROOMCOUNT++):0 '= "調理室", "8", "6"
部屋情報:(ROOMCOUNT++):0 '= "図書室", "12", "8"
;1から始まるので調整
プランター始点 = ROOMCOUNT-1
部屋情報:(ROOMCOUNT++):0 '= "プランター", "3", "3"
部屋情報:(ROOMCOUNT++):0 '= "プランター", "3", "3"
部屋情報:(ROOMCOUNT++):0 '= "プランター", "3", "3"
部屋情報:(ROOMCOUNT++):0 '= "プランター", "3", "3"
畑始点 = ROOMCOUNT-1
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
部屋情報:(ROOMCOUNT++):0 '= "畑", "4", "4"
田んぼ始点 = ROOMCOUNT-1
部屋情報:(ROOMCOUNT++):0 '= "田んぼ", "6", "6"
部屋情報:(ROOMCOUNT++):0 '= "田んぼ", "6", "6"
部屋情報:(ROOMCOUNT++):0 '= "牧場", "12", "12"
部屋情報:(ROOMCOUNT++):0 '= "畜舎", "8", "8"
部屋情報:(ROOMCOUNT++):0 '= "生け簀", "8", "8"
部屋情報:(ROOMCOUNT++):0 '= "養蜂場", "4", "4"

;100から居住用の部屋
FOR LCOUNT, 101, 200
	部屋情報:LCOUNT:0 '= "居住部屋", "4", "4"
NEXT

部屋配置 = 0

CALL 模様替え用スプライト

VARSET 始点

;SPRITEはここで作成しておく
GDISPOSE 0
GDISPOSE 1
GDISPOSE 2
GDISPOSE 3
GDISPOSE 4
GDISPOSE 5
GDISPOSE 6
GDISPOSE 7
GDISPOSE 8

GCREATEFROMFILE 0, "壁系.png"
SPRITECREATE "GRID", 0, 0, 0, 32, 32

GCREATEFROMFILE 1, "壁系.png"
;α値(不透明)+peru
GCLEAR 1, 0xFF000000+COLOR_FROMNAME("peru")
SPRITECREATE "GRID_OFF", 1, 0, 0, 32, 32

GCREATEFROMFILE 2, "壁系.png"
;α値(不透明)+yellow
GCLEAR 2, 0xFF000000+COLOR_FROMNAME("yellow")
SPRITECREATE "GRID_OVER", 2, 0, 0, 32, 32

GCREATEFROMFILE 3, "壁系.png"
;α値(不透明)+saddlebrow
GCLEAR 3, 0xFF000000+COLOR_FROMNAME("saddlebrown")
SPRITECREATE "GRID_WOOD", 3, 0, 0, 32, 32

GCREATEFROMFILE 4, "壁系.png"
;α値(不透明)+chocolate
GCLEAR 4, 0xFF000000+COLOR_FROMNAME("chocolate")
SPRITECREATE "GRID_GROUND", 4, 0, 0, 32, 32

GCREATEFROMFILE 5, "壁系.png"
;α値(不透明)+silver
GCLEAR 5, 0xFF000000+COLOR_FROMNAME("silver")
SPRITECREATE "GRID_SILVER", 5, 0, 0, 32, 32

GCREATEFROMFILE 8, "壁系.png"
;α値(不透明)+gray
GCLEAR 8, 0xFF000000+COLOR_FROMNAME("gray")
SPRITECREATE "GRID_GRAY", 8, 0, 0, 32, 32

;CALL BGM再生, 模様替えBGM

マップ横幅 = 60
マップ縦幅 = 30

DO
	$RESTART
	にんじん表示済み = 0
	CLEARLINE LINECOUNT-VARLINE
	VARSET 設置済み
	VARSET タイル始点, -1
	VARSET 居住区
	住人数 = 0
	;マップを表示する
	FOR Y座標, 0, マップ縦幅
		VARSET 表示用変数
		FOR X座標, 0, マップ横幅
			SIF X座標 == 0
				表示用変数:X座標 = <nobr>
			IF 部屋配置
				幅:0 = TOINT(部屋情報:部屋配置:1)
				幅:1 = TOINT(部屋情報:部屋配置:2)
			ELSEIF オブジェクト配置 != ""
				;幅が設定されていないならデフォルトの幅を返す
				IF 幅:0 == 0
					幅:0 = TOINT(オブジェクト情報(オブジェクト配置, "横幅"))
					幅:1 = TOINT(オブジェクト情報(オブジェクト配置, "縦幅"))
				ENDIF
				;向きも設定されてなかったら右向きデフォ
				SIF オブジェクト配置向き == ""
					オブジェクト配置向き = 右向き
			ENDIF
			部屋名 = 
			;重複対策に配置しようとしてる部屋は削除
			SIF 部屋配置 && 土地:Y座標:X座標 == 部屋配置
				土地:Y座標:X座標 = 0
			;始点の座標から計算して配置用の部屋を描画する
			IF (部屋配置 || オブジェクト配置 != "") && X座標 >= 始点:0 && Y座標 >= 始点:1 && INRANGE(X座標-始点:0, 0, 幅:0-1) && INRANGE(Y座標-始点:1, 0, 幅:1-1)
				表示用変数:X座標 += "<img src='GRID_OVER' srcb='' height='100' width='100'>"
			ELSE
				表示チップ =
				部屋名 = %部屋情報:(土地:Y座標:X座標):0%
				;居住部屋の場合は部屋番号と住んでいるキャラをツールチップに加える
				IF 部屋名 == "居住部屋"
					部屋番号 = 土地:Y座標:X座標-100
					住人 = 
					FOR 居住LCOUNT, 0, CHARANUM
						IF CFLAG:居住LCOUNT:居住 == 部屋番号
							IF 住人 == ""
								住人 = %CALLNAME:居住LCOUNT%
							ELSE
								住人 += @"と%CALLNAME:居住LCOUNT%"
							ENDIF
						ENDIF
					NEXT
					表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='居住部屋,{部屋番号} \@ 住人 == "" ? 空き部屋 # %住人%の部屋 \@' pos='{X座標*100}'>"
				ELSE
					表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
				ENDIF
				;まずオブジェクトを表示する
				;IF オブジェクト:Y座標:X座標 != ""
				;	;始点(左上)を記録しておく
				;	IF タイル始点:(オブジェクト:Y座標:X座標):0 == -1
				;		タイル始点:(オブジェクト:Y座標:X座標):0 = X座標
				;		タイル始点:(オブジェクト:Y座標:X座標):1 = Y座標
				;	ENDIF
				;その土地に部屋が設置してあったら対応した色で表示する
				IF 土地:Y座標:X座標 != 0
					;始点(左上)を記録しておく
					IF タイル始点:(土地:Y座標:X座標):0 == -1
						タイル始点:(土地:Y座標:X座標):0 = X座標
						タイル始点:(土地:Y座標:X座標):1 = Y座標
					ENDIF
					;設置済み判定に始点も入れておく
					設置済み:(土地:Y座標:X座標) = Y座標*100+X座標
					SELECTCASE 部屋名
						CASE "居住部屋"
							表示チップ = GRID_WOOD
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
							;部屋のIDから設置済み居住部屋を記録
							居住区:((土地:Y座標:X座標)-100) = 1

							;顔グラ表示の失敗作
							;表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'>"

							;VARSET 住人
							;FOR 居住LCOUNT, 1, CHARANUM
							;	IF CFLAG:居住LCOUNT:居住 == 部屋番号
							;		住人:FINDELEMENT(住人, "", 0, 2, 1) = %NAME:居住LCOUNT%
							;		GDISPOSE 住人数+100
							;		GCREATEFROMFILE 住人数+100, @"%NAME:居住LCOUNT%_FACE.png"
							;		SPRITECREATE @"%NAME:居住LCOUNT%_FACE", 住人数+100
							;		住人数++
							;	ENDIF
							;NEXT

							;IF 住人:0 != "" && 住人:1 != ""
							;	IF SPRITECREATED(@"%住人:0%_FACE") && SPRITECREATED(@"%住人:1%_FACE")
							;		表示用変数:X座標 += @"</button><button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
							;		表示用変数:X座標 += @"<img src='%住人:0%_FACE' srcb='' height='200' width='200' ypos='100'></button>"
							;		表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100+300}'>"
							;		表示用変数:X座標 += @"<img src='%住人:1%_FACE' srcb='' height='200' width='200' ypos='100'>"
							;	ENDIF
							;ELSEIF 住人:0 != ""
							;	IF SPRITECREATED(@"%住人:0%_FACE")
							;		表示用変数:X座標 += @"</button><button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100+100}'>"
							;		表示用変数:X座標 += @"<img src='%住人:0%_FACE' srcb='' height='300' width='300' ypos='100'>"
							;	ENDIF
							;ENDIF

						CASE "レッスン室", "調理室", "図書室"
							表示チップ = GRID_WOOD
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "医務室", "実験室"
							表示チップ = GRID_SILVER
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "畑"
							;畑 4*4
							;作物があれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%2 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%2 == 0
								表示チップ = GRID_GROUND
								IF 畑の作物:(土地:Y座標:X座標-畑始点) != ""
									;収穫可能かどうかで画像が変わる
									SELECTCASE 畑の作物:(土地:Y座標:X座標-畑始点)
										CASE "大根", "玉ねぎ", "さつまいも", "長ねぎ", "ごぼう", "ほうれん草", "スイカ", "メロン", "人参", "とうもろこし", "レタス", "白菜", "かぼちゃ", "キャベツ", "じゃがいも", "れんこん"
											表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='200' width='200'></button>"
											表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%(%畑の作物:(土地:Y座標:X座標-畑始点)%)' pos='{X座標*100}'>"
											表示用変数:X座標 += "<img src='"
											IF 畑収穫日:(土地:Y座標:X座標-畑始点)
												表示用変数:X座標 += "苗"
											ELSE
												IF 畑の作物:(土地:Y座標:X座標-畑始点) == "人参" && RANDPER(10) && !にんじん表示済み && !CCB:GETNO("にんじん")
													表示用変数:X座標 += @"にんじん"
													にんじん表示済み = 1
													にんじん座標:0 = Y座標
													にんじん座標:1 = X座標
												ELSE
													表示用変数:X座標 += @"%畑の作物:(土地:Y座標:X座標-畑始点)%"
												ENDIF
											ENDIF
											表示用変数:X座標 += "' srcb='' height='200' width='200'>"
										CASE "トマト", "きゅうり", "ナス", "ピーマン", "りんご", "みかん", "ぶどう", "カカオ豆"
											IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
												表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
												表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%(%畑の作物:(土地:Y座標:X座標-畑始点)%)' pos='{X座標*100}'>"
												表示用変数:X座標 += @"<img src='%畑の作物:(土地:Y座標:X座標-畑始点)%"
												IF 畑収穫日:(土地:Y座標:X座標-畑始点)
													表示用変数:X座標 += "1"
												ELSE
													表示用変数:X座標 += "2"
												ENDIF
												表示用変数:X座標 += "' srcb='' height='400' width='400'>"
											ENDIF
									ENDSELECT
								ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								ENDIF
							ENDIF
						CASE "田んぼ"
							表示チップ = GRID_GROUND
							;田んぼ 6*6
							;作物があれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%3 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%3 == 0
								表示チップ = GRID_GROUND
								IF 田んぼの作物:(土地:Y座標:X座標-田んぼ始点) != ""
									;収穫可能かどうかで画像が変わる
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%(%田んぼの作物:(土地:Y座標:X座標-田んぼ始点)%)' pos='{X座標*100}'>"
									表示用変数:X座標 += "<img src='稲"
									IF 田んぼ収穫日:(土地:Y座標:X座標-田んぼ始点)
										表示用変数:X座標 += "1"
									ELSE
										表示用変数:X座標 += "2"
									ENDIF
									表示用変数:X座標 += "' srcb='' height='300' width='300'>"
								ELSE
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								ENDIF
							ENDIF
						CASE "プランター"
							表示チップ = GRID_OFF
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								;収穫可能かどうかで画像が変わる
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF プランターの作物:(土地:Y座標:X座標-プランター始点) != ""
									IF プランター収穫日:(土地:Y座標:X座標-プランター始点)
										表示用変数:X座標 += "<img src='プランター苗"
									ELSE
										表示用変数:X座標 += @"<img src='%プランターの作物:(土地:Y座標:X座標-プランター始点)%"
									ENDIF
									表示用変数:X座標 += "' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%(%プランターの作物:(土地:Y座標:X座標-プランター始点)%)' pos='{X座標*100}'><img src='プランター' srcb='' height='300' width='300'>"
								ELSE
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'><img src='プランター' srcb='' height='300' width='300'>"
								ENDIF
							ENDIF
						CASE "牧場"
							;牧場 12*12
							;牛か羊か馬がいるならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 6 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 6
								IF 家畜:ユニコーン || 家畜:バイコーン
									使用グラフィック = 4
								ELSE
									使用グラフィック = 2
								ENDIF
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 6 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 6
								使用グラフィック = 3
							ELSE
								使用グラフィック = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%6 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%6 == 0
								表示チップ = GRID_GRASS
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='600' width='600'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF 使用グラフィック == 1
									SELECTCASE 家畜:牛
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='牛{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='牛' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='牛-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ELSEIF 使用グラフィック == 2
									SELECTCASE 家畜:羊
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='羊{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='羊' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='羊-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ELSEIF 使用グラフィック == 3
									SELECTCASE 家畜:馬
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='馬{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='馬' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='馬-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ELSEIF 使用グラフィック == 4
									IF 家畜:ユニコーン
										表示用変数:X座標 += "<img src='ユニコーン' srcb='' height='600' width='600'>"
									ELSEIF 家畜:バイコーン
										表示用変数:X座標 += "<img src='バイコーン' srcb='' height='600' width='600'>"
									ENDIF
								ENDIF
							ENDIF
						CASE "畜舎"
							;畜舎 8*8
							;豚か鶏がいるならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								使用グラフィック = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								使用グラフィック = 1
							ELSE
								使用グラフィック = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%4 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%4 == 0
								表示チップ = GRID_WOOD
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF 使用グラフィック == 1
									SELECTCASE 家畜:豚
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='豚{RAND(1, 4)}' srcb='' height='400' width='400'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='豚' srcb='' height='400' width='400'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='豚-{RAND(1, 4)}' srcb='' height='400' width='400'>"
									ENDSELECT
								ELSEIF 使用グラフィック == 2 && 家畜:鶏
									表示用変数:X座標 += "<img src='鶏' srcb='' height='400' width='400'>"
								ENDIF
							ENDIF
						CASE "生け簀"
							;生け簀は8*8
							;養殖中ならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								使用グラフィック = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								使用グラフィック = 3
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								使用グラフィック = 4
							ELSE
								使用グラフィック = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%4 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%4 == 0
								表示チップ = GRID_WATER
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF 生け簀の養殖:使用グラフィック != ""
									SELECTCASE 生け簀の養殖:使用グラフィック
										CASE "タコ"
											表示用変数:X座標 += "<img src='タコ'"
										CASE "イカ"
											表示用変数:X座標 += "<img src='イカ'"
										CASE "エビ"
											表示用変数:X座標 += "<img src='エビ'"
										CASE "ウナギ"
											表示用変数:X座標 += "<img src='ウナギ'"
										CASE "貝類"
											表示用変数:X座標 += "<img src='貝類'"
										CASE "海藻"
											表示用変数:X座標 += "<img src='海藻'"
										CASE "カニ"
											表示用変数:X座標 += "<img src='カニ'"
										CASEELSE
											表示用変数:X座標 += "<img src='魚'"
									ENDSELECT
									表示用変数:X座標 += " srcb='' height='400' width='400'>"
								ENDIF
							ENDIF
						CASE "養蜂場"
							;養蜂場 4*4
							;蜂がいれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%2 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%2 == 0
								表示チップ = GRID_BEEHIVE
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='200' width='200'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								SELECTCASE 家畜:蜂
									;元気
									CASE 80 TO 100
										表示用変数:X座標 += @"<img src='蜂{RAND(1, 4)}' srcb='' height='200' width='200'>"
									;普通
									CASE 30 TO 79
										表示用変数:X座標 += @"<img src='蜂' srcb='' height='200' width='200'>"
									;良くない
									CASE 1 TO 29
										表示用変数:X座標 += @"<img src='蜂-{RAND(1, 4)}' srcb='' height='200' width='200'>"
								ENDSELECT
							ENDIF
					ENDSELECT
				ELSEIF グリッド表示
					表示チップ = GRID
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ELSE
					表示チップ = GRID_OFF
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ENDIF
				表示用変数:X座標 += "</button>"
				;壁の確認 端でも壁表示
				IF !壁無し
					;左壁
					IF X座標 == 0 || 土地:Y座標:(X座標-1) != 土地:Y座標:X座標
						;上壁もあれば┌壁
						IF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┌壁'></nonbutton>"
						;下壁もあれば└壁
						ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='└壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='左壁'></nonbutton>"
						ENDIF
					;上壁
					ELSEIF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
						;右壁もあれば┐壁
						IF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┐壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='上壁'></nonbutton>"
						ENDIF
					;右壁
					ELSEIF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
						;下壁もあれば┘壁
						IF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┘壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='右壁'></nonbutton>"
						ENDIF
					;下壁
					ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='下壁'></nonbutton>"
					ENDIF
				ENDIF
			ENDIF
		NEXT
		HTML用 = %STRJOIN(表示用変数, "")%
		VARSET 表示用変数
		FOR X座標, 0, 60
			;配置しようとするオブジェクトも仮表示してみる
			SIF オブジェクト配置 != "" && Y座標 == 始点:1+幅:1-1 && X座標 == 始点:0
				表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト配置%' pos='{X座標*100}'><img src='%オブジェクト配置%_{オブジェクト配置角度}' srcb='' height='{幅:1*100}' width='\@ オブジェクト配置向き == "右向き" ? {幅:0*100} # -{幅:0*100} \@' ypos='{(幅:1-1)*(-100)}'></button>"
			IF OBJECTS:Y座標:X座標 != ""
				オブジェクト名 = %オブジェクト(OBJECTS:Y座標:X座標, "名前")%
				オブジェクト幅:0 = TOINT(オブジェクト(OBJECTS:Y座標:X座標, "横幅"))
				オブジェクト幅:1 = TOINT(オブジェクト(OBJECTS:Y座標:X座標, "縦幅"))
				オブジェクト角度 = TOINT(オブジェクト(OBJECTS:Y座標:X座標, "角度"))
				SELECTCASE オブジェクト情報(オブジェクト名, "オブジェクトタイプ")
					;小物は机等に置いて表示するように少しずらす
					CASE "小物"
						表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト名%' pos='{X座標*100}'><img src='%オブジェクト名%_{オブジェクト角度}' srcb='' height='{オブジェクト幅:1*100}' width='\@ オブジェクト(OBJECTS:Y座標:X座標, "向き") == "右向き" ? {オブジェクト幅:0*100} # -{オブジェクト幅:0*100} \@' ypos='{(オブジェクト幅:1-1)*(-100)+1}'></button>"
					CASEELSE
						表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト名%' pos='{X座標*100}'><img src='%オブジェクト名%_{オブジェクト角度}' srcb='' height='{オブジェクト幅:1*100}' width='\@ オブジェクト(OBJECTS:Y座標:X座標, "向き") == "右向き" ? {オブジェクト幅:0*100} # -{オブジェクト幅:0*100} \@' ypos='{(オブジェクト幅:1-1)*(-100)}'></button>"
				ENDSELECT
			ENDIF
		NEXT
		SIF X座標 == マップ横幅-1
			表示用変数:X座標 += "</nobr>"
		HTML用 += @"%STRJOIN(表示用変数, "")%"
		HTML_PRINT HTML用
	NEXT
	PRINTL 

	IF 部屋配置
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [7] 左回転　　　　 [9] 右回転
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUT

		SELECTCASE RESULT
			CASE 0
				IF 始点保存用:0 >= 0
					ARRAYCOPY "始点保存用", "始点"
					GOTO DECIDE_ROOM
				ENDIF
				部屋配置 = 0
			CASE 8
				SIF 始点:1 > 0
					始点:1--
			CASE 4
				SIF 始点:0 > 0
					始点:0--
			CASE 6
				SIF 始点:0+幅:0 < マップ横幅 
					始点:0++
			CASE 2
				SIF 始点:1+幅:1 < マップ縦幅
					始点:1++
			;四角形だから同義
			CASE 7, 9
				SWAP 部屋情報:部屋配置:1, 部屋情報:部屋配置:2
			CASE 5
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					SIF LCOUNT >= マップ縦幅
						GOTO RESTART
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF LCOUNT2 >= マップ横幅
							GOTO RESTART
						SIF 土地:LCOUNT:LCOUNT2 != 0
							GOTO RESTART
					NEXT
				NEXT
				$DECIDE_ROOM
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						土地:LCOUNT:LCOUNT2 = 部屋配置
					NEXT
				NEXT
				部屋配置 = 0
				VARSET 始点保存用, -1
				GOTO RESTART
			CASEELSE
				IF RESULT/100 > 0 && RESULT/100+幅:0 <= マップ横幅 && RESULT%100 > 0 && RESULT%100+幅:1 <= マップ縦幅
					始点:0 = RESULT/100
					始点:1 = RESULT%100
				ENDIF
		ENDSELECT

		CONTINUE
	ELSEIF オブジェクト配置 != ""
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [7] 横幅を縮小 [9] 横幅を拡大
		PRINTL [1] 縦幅を縮小 [3] 縦幅を拡大
		PRINTBUTTON "[R] 反転 ", "R"
		PRINTBUTTON "[J] 左回転 ", "J"
		PRINTBUTTON "[K] 右回転 ", "K"
		PRINTL 
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUTS

		SELECTCASE RESULTS
			CASE "0"
				IF 始点保存用:0 >= 0
					ARRAYCOPY "始点保存用", "始点"
					GOTO DECIDE_OBJ
				ENDIF
				オブジェクト配置 = 
			CASE "8"
				SIF 始点:1 > 0
					始点:1--
			CASE "4"
				SIF 始点:0 > 0
					始点:0--
			CASE "6"
				SIF 始点:0+幅:0 < マップ横幅 
					始点:0++
			CASE "2"
				SIF 始点:1+幅:1 < マップ縦幅
					始点:1++
			CASE "7"
				SIF 幅:0 > 1
					幅:0--
			CASE "9"
				SIF 幅:0 < 60
					幅:0++
			CASE "1"
				SIF 幅:1 > 1
					幅:1--
			CASE "3"
				SIF 幅:1 < 30
					幅:1++
			CASE "5"
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					SIF LCOUNT >= マップ縦幅
						GOTO RESTART
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF LCOUNT2 >= マップ横幅
							GOTO RESTART
					NEXT
				NEXT
				$DECIDE_OBJ
				OBJECTS:(始点:1+幅:1-1):(始点:0) = %オブジェクト配置%_%オブジェクト配置向き%_{幅:0}_{幅:1}_{オブジェクト配置角度}
				オブジェクト配置 = 
				VARSET 始点保存用, -1
				幅:0 = 0
				幅:1 = 0
				GOTO RESTART
			CASE "r", "R"
				IF オブジェクト配置向き == "右向き"
					オブジェクト配置向き = 左向き
				ELSE
					オブジェクト配置向き = 右向き
				ENDIF
				GOTO RESTART
			CASE "j", "J"
				オブジェクト配置角度 = (オブジェクト配置角度+270)%360
				SWAP 幅:0, 幅:1
				GOTO RESTART
			CASE "k", "K"
				オブジェクト配置角度 = (オブジェクト配置角度+90)%360
				SWAP 幅:0, 幅:1
				GOTO RESTART
			CASEELSE
				SIF !ISNUMERIC(RESULTS)
					CONTINUE
				RESULT = TOINT(RESULTS)
				IF RESULT/100 > 0 && RESULT/100+幅:0 <= マップ横幅 && RESULT%100 > 0 && RESULT%100+幅:1 <= マップ縦幅
					始点:0 = RESULT/100
					始点:1 = RESULT%100
				ENDIF
		ENDSELECT

		CONTINUE
	ELSE
		PRINTL [60] - オブジェクトを配置する [61] - 部屋を配置する [62] - オブジェクトを初期化する [63] - 部屋を初期化する
		PRINTFORML [65]  - グリッド表示\@ グリッド表示 ? しない # する \@ [70] 戻る [80] マップデータのセーブ/ロード
		INPUT
		SELECTCASE RESULT
			CASE 70
				RETURN
			CASE 80
				CALL マップ入出力
				GOTO RESTART
			CASE 60
				PRINTL □配置するオブジェクトを選んでください 名前(横幅×縦幅)
				;オブジェクト一覧関数でオブジェクト名入れるところまで処理する
				CALL オブジェクト一覧, オブジェクト配置
				VARSET 幅
				GOTO RESTART
			CASE 61
				PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
				FOR LCOUNT, 1, 102
					SIF 部屋情報:LCOUNT:0 == ""
						CONTINUE
					;現在は部屋名とアイテム名が一致してるからこれでいい
					部屋名 = %部屋情報:LCOUNT:0%
					SELECTCASE 部屋名
						CASE "レッスン室", "調理室", "居住部屋"
						CASEELSE
							SIF ITEM:部屋名 == 0
								CONTINUE
					ENDSELECT
					PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
				NEXT
				CALL NEWLINE
				DRAWLINE
				PRINTL [0] 戻る
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							GOTO RESTART
						CASE 1 TO VARSIZE("部屋情報", 1)
							;現在は部屋名とアイテム名が一致してるからこれでいい
							部屋名 = %部屋情報:RESULT:0%
							SELECTCASE 部屋名
								CASE "レッスン室", "調理室"
									部屋配置 = RESULT
									IF 設置済み:RESULT
										始点:0 = タイル始点:RESULT:0
										始点:1 = タイル始点:RESULT:1
									ENDIF
								;居住部屋は空いている番号を使う
								CASE "居住部屋"
									部屋配置 = FINDELEMENT(居住区, 0, 1, 100)+100
									IF 部屋配置 >= 200
										PRINTW これ以上居住部屋を置けません
										GOTO RESTART
									ENDIF
								CASEELSE
									SIF ITEM:部屋名 == 0
										CONTINUE
									部屋配置 = RESULT
									IF 設置済み:RESULT
										始点:0 = タイル始点:RESULT:0
										始点:1 = タイル始点:RESULT:1
									ENDIF
							ENDSELECT
							BREAK
					ENDSELECT
				LOOP 1
				CONTINUE
			CASE 62
				PRINTL オブジェクト情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET OBJECTS
				CONTINUE
			CASE 63
				PRINTL 部屋情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET 土地
				CONTINUE
			CASE 65
				INVERTBIT グリッド表示, 0
				CONTINUE
			CASEELSE
				;RESULTからX,Y座標を調べる
				;X
				選択座標:0 = RESULT/100
				;Y
				選択座標:1 = RESULT%100
				$部屋オプション
				選択部屋 = %部屋情報:(土地:(選択座標:1):(選択座標:0)):0%
				選択オブジェクト = %OBJECTS:(選択座標:1):(選択座標:0)%
				;座標の情報表示
				部屋番号 = 0
				住人 = 
				IF 選択部屋 == "居住部屋"
					部屋番号 = 土地:(選択座標:1):(選択座標:0)-100
					FOR 居住LCOUNT, 0, CHARANUM
						IF CFLAG:居住LCOUNT:居住 == 部屋番号
							IF 住人 == ""
								住人 = %CALLNAME:居住LCOUNT%
							ELSE
								住人 += @"と%CALLNAME:居住LCOUNT%"
							ENDIF
						ENDIF
					NEXT
				ENDIF
				IF 選択部屋 == "畑" && にんじん表示済み
					IF INRANGE(選択座標:1, にんじん座標:0, にんじん座標:0+1) && INRANGE(選択座標:0, にんじん座標:1, にんじん座標:1+1)
						CALL にんじん解禁
						CCB:(NO:TARGET) = 1
					ENDIF
				ENDIF
				PRINT □
				SIF 土地:(選択座標:1):(選択座標:0)
					PRINTFORM %選択部屋% 
				SIF 部屋番号
					PRINTFORM {部屋番号} \@ 住人 == "" ? 空き部屋 # %住人%の部屋 \@ 
				SIF 選択オブジェクト != ""
					PRINTFORM %オブジェクト(選択オブジェクト, "名前")%配置中 
				PRINTFORML {選択座標:0}×{選択座標:1}
				;オブジェクトの扱い
				IF 選択オブジェクト != ""
					PRINTFORML 　[1] - %オブジェクト(選択オブジェクト, "名前")%を動かす
					PRINTFORML 　[2] - %オブジェクト(選択オブジェクト, "名前")%を片付ける
				ELSE
					PRINTL 　[3] - オブジェクトを配置する
				ENDIF
				;配置してある部屋の機能にクイックアクセス
				IF 選択部屋 != ""
					PRINTFORML 　[4] - %選択部屋%\@ 部屋番号 ? ,{部屋番号} #\@を動かす
					PRINTFORML 　[5] - %選択部屋%\@ 部屋番号 ? ,{部屋番号} #\@を取り壊す
					SELECTCASE 選択部屋
						CASE "レッスン室", "医務室", "実験室", "調理室", "図書室"
							SIF GETNUM(ITEM, 選択部屋) == -1 || ITEM:選択部屋
								PRINTFORML 　[6] - %選択部屋%に行く
						CASE "居住部屋"
							SIF 住人 != ""
								PRINTFORML 　[6] - 居住部屋,{部屋番号}に行く
						CASE "プランター", "畑", "田んぼ"
							PRINTL 　[6] - 農作物を管理する
						CASE "牧場", "畜舎", "生け簀", "養蜂場"
							PRINTL 　[6] - 畜産物を管理する
					ENDSELECT
				ELSE
					PRINTL 　[7] - 部屋を配置する
				ENDIF
				SIF 選択部屋 == "居住部屋"
					PRINTL 　[8] - 住人を割り当てる
				DRAWLINE
				PRINTPLAINFORM 　{選択座標:0}×{選択座標:1}　
				PRINTL [0] - 戻る
				始点:0 = 選択座標:0
				始点:1 = 選択座標:1
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							VARSET 始点
							GOTO RESTART
						CASE 1
							SIF 選択オブジェクト == ""
								CONTINUE
							オブジェクト配置 = %オブジェクト(OBJECTS:(始点:1):(始点:0), "名前")%
							幅:0 = TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "横幅"))
							幅:1 = TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "縦幅"))
							;縦にずれてる分を調整
							L_Y = 始点:1
							始点:1 -= TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "縦幅"))-1
							OBJECTS:L_Y:(始点:0) = 
							ARRAYCOPY "始点", "始点保存用"
							GOTO RESTART
						CASE 2
							SIF 選択オブジェクト == ""
								CONTINUE
							OBJECTS:(始点:1):(始点:0) = 
							GOTO RESTART
						CASE 3
							PRINTL □配置するオブジェクトを選んでください 名前(横幅×縦幅)
							CALL オブジェクト一覧, オブジェクト配置
							VARSET 幅
							GOTO RESTART
						CASE 4
							SIF 選択部屋 == ""
								CONTINUE
							部屋配置 = 土地:(始点:1):(始点:0)
							IF 設置済み:部屋配置
								始点:0 = タイル始点:部屋配置:0
								始点:1 = タイル始点:部屋配置:1
							ENDIF
							ARRAYCOPY "始点", "始点保存用"
							GOTO RESTART
						CASE 5
							SIF 選択部屋 == ""
								CONTINUE
							IF 選択部屋 == "居住部屋"
								部屋番号 = 土地:(選択座標:1):(選択座標:0)-100
								PRINTFORML %選択部屋%,{部屋番号}を取り壊します。よろしいですか？(マップ上から消えるだけで機能は失われません)
							ELSE
								PRINTFORML %選択部屋%を取り壊します。よろしいですか？(マップ上から消えるだけで機能は失われません)
							ENDIF
							PRINTL [0] - はい
							PRINTL [1] - いいえ
							ROOMID = 土地:(選択座標:1):(選択座標:0)
							DO
								INPUT
								IF RESULT == 0
									FOR LCOUNT, 0, 30
										FOR LCOUNT2, 0, 60
											SIF 土地:LCOUNT:LCOUNT2 == ROOMID
												土地:LCOUNT:LCOUNT2 = 0
										NEXT
									NEXT
									VARSET 始点
								ENDIF
								IF 選択部屋 == "居住部屋"
									FOR 居住LCOUNT, 0, CHARANUM
										SIF CFLAG:居住LCOUNT:居住 == 部屋番号
											CFLAG:居住LCOUNT:居住 = 0
									NEXT
								ENDIF
								GOTO RESTART
							LOOP 1
						CASE 6
							SELECTCASE 選択部屋
								CASE "レッスン室"
									CALL STRATEGY_IDOL
									SIF RESULT
										RETURN
									GOTO RESTART
								CASE "図書室", "実験室", "医務室"
									IF !ITEM:選択部屋
										PRINTFORMW %選択部屋%を所持していません
										CONTINUE
									ENDIF
									CALLFORM %選択部屋%
									GOTO RESTART
								CASE "調理室"
									CALL 調理室
									GOTO RESTART
								CASE "プランター", "畑", "田んぼ"
									CALL 農産物
									GOTO RESTART
								CASE "牧場", "畜舎", "生け簀", "養蜂場"
									CALL 畜産
									GOTO RESTART
								CASE "居住部屋"
									IF 住人 != ""
										CALL 居住部屋, 部屋番号
										SIF FLAG:休憩フラグ
											RETURN
										GOTO RESTART
									ENDIF
							ENDSELECT
						CASE 7
							PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
							FOR LCOUNT, 1, 102
								SIF 部屋情報:LCOUNT:0 == ""
									CONTINUE
								;現在は部屋名とアイテム名が一致してるからこれでいい
								部屋名 = %部屋情報:LCOUNT:0%
								SELECTCASE 部屋名
									CASE "レッスン室", "調理室", "居住部屋"
									CASEELSE
										SIF ITEM:部屋名 == 0
											CONTINUE
								ENDSELECT
								PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
							NEXT
							CALL NEWLINE
							DRAWLINE
							PRINTL [0] 戻る
							DO
								INPUT
								SELECTCASE RESULT
									CASE 0
										GOTO RESTART
									CASE 1 TO VARSIZE("部屋情報", 1)
										;現在は部屋名とアイテム名が一致してるからこれでいい
										部屋名 = %部屋情報:RESULT:0%
										SELECTCASE 部屋名
											CASE "レッスン室", "調理室"
												部屋配置 = RESULT
											;居住部屋は空いている番号を使う
											CASE "居住部屋"
												部屋配置 = FINDELEMENT(居住区, 0, 1, 100)+100
												IF 部屋配置 >= 200
													PRINTW これ以上居住部屋を置けません
													GOTO RESTART
												ENDIF
											CASEELSE
												SIF ITEM:部屋名 == 0
													CONTINUE
												部屋配置 = RESULT
										ENDSELECT
										GOTO RESTART
								ENDSELECT
							LOOP 1
							CONTINUE
						CASE 8
							SIF 選択部屋 != "居住部屋"
								CONTINUE
							CALL 住人配置, 部屋番号
							GOTO 部屋オプション
					ENDSELECT
				LOOP 1
		ENDSELECT
	ENDIF
LOOP 1

;OBJECTS_DATAから目的の情報を抽出して返す
@オブジェクト情報, オブジェクト名, 指定タイプ
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 配列
#DIMS DYNAMIC オブジェクト名
#DIMS DYNAMIC 指定タイプ

FOR LCOUNT, 1, VARSIZE("OBJECTS_DATA", 1)
	IF OBJECTS_DATA:LCOUNT:0 == オブジェクト名
		配列 = LCOUNT
		BREAK
	ENDIF
NEXT

;#; SIF 配列 == 0
;#; 	THROW %オブジェクト名%のオブジェクト情報が存在しません

SELECTCASE 指定タイプ
	CASE "横幅"
		RETURNF OBJECTS_DATA:配列:1
	CASE "縦幅"
		RETURNF OBJECTS_DATA:配列:2
	CASE "オブジェクトタイプ"
		RETURNF OBJECTS_DATA:配列:3
ENDSELECT

@オブジェクト一覧, オブジェクト配置
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIMS REF オブジェクト配置

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;とりあえず上限は1000で設定しておく
FOR LCOUNT, 1, 1000
	SIF OBJECTS_DATA:LCOUNT:0 == ""
		BREAK
	PRINTFORMLC 　[{LCOUNT}] %OBJECTS_DATA:LCOUNT:0%(%OBJECTS_DATA:LCOUNT:1%×%OBJECTS_DATA:LCOUNT:2%)
NEXT
CALL NEWLINE

DRAWLINE
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			オブジェクト配置 = 
			RETURN
		CASEELSE
			LRESULT = RESULT
			IF OBJECTS_DATA:LRESULT:0 != ""
				オブジェクト配置 = %OBJECTS_DATA:LRESULT:0%
				RETURN
			ELSE
				CONTINUE
			ENDIF
	ENDSELECT
LOOP 1

@オブジェクト, オブジェクト名, タイプ指定
#FUNCTIONS
#DIMS DYNAMIC オブジェクト名
#DIMS DYNAMIC タイプ指定
#DIMS DYNAMIC 分割用, 5

;OBJECTSの中身は「%オブジェクト名%_%向き%_%横幅%_%縦幅%_%角度%」となっている
;SPLITで分解して目的の情報を返す

SPLIT オブジェクト名, "_", 分割用

SELECTCASE タイプ指定
	CASE "名前"
		RETURNF 分割用:0
	CASE "向き"
		RETURNF 分割用:1
	CASE "横幅"
		RETURNF 分割用:2
	CASE "縦幅"
		RETURNF 分割用:3
	CASE "角度"
		RETURNF 分割用:4
	CASEELSE
		;#; THROW タイプ指定が表記ゆれです
ENDSELECT

@マップ入出力
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC XCOUNT
#DIM DYNAMIC YCOUNT
#DIM DYNAMIC LRESULT
#DIMS DYNAMIC マップデータ, 4000
#DIMS DYNAMIC SPLITVAR, 4
#DIMS DYNAMIC マップ名
#DIMS DYNAMIC JOIN用

PRINTL マップデータの入出力を行います
PRINTL [0] - 出力(セーブ)する
PRINTL [1] - 入力(ロード)する
PRINTL [2] - 戻る
BINPUT
SELECTCASE RESULT
	CASE 0
		GOTO SAVE
	CASE 1
		GOTO LOAD
	CASE 2
		RETURN
ENDSELECT

$SAVE
PRINTL □模様替えのマップデータをファイル形式で出力します
PRINTL 　出力先のデータ番号を選んでください(savフォルダ内のtxt○○○.txtに相当)
FOR LCOUNT, 100, 130
	IF LOADTEXT(LCOUNT) != ""
		SPLIT LOADTEXT(LCOUNT), UNICODE(0xA), マップデータ
		PRINTFORML 　[{LCOUNT}] %マップデータ:0%
	ELSE
		PRINTFORML 　[{LCOUNT}] 空きスロット
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　MAP　
PRINTL [0] 戻る
DO
	INPUT
	LRESULT = RESULT
	SELECTCASE LRESULT
		CASE 0
			RETURN
		CASE 100 TO 129
			IF LOADTEXT(LRESULT) != ""
				PRINTFORML マップスロット {LRESULT}番には既にマップが登録されています。上書きしますか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				BINPUT
				SIF RESULT == 1
					GOTO SAVE
			ENDIF
			マップ名 = 模様替えマップ %GETTIMES()%
			PRINTFORML マップ名を入力してください(デフォルト名:%マップ名%)
			PRINTBUTTON "[デフォルト名を使う]", "デフォルト名を使う"
			PRINTL 
			INPUTS
			SIF RESULTS != "デフォルト名を使う" && RESULTS != ""
				マップ名 = %RESULTS%
			;上のスロット選択でLOADTEXTしてるので変数初期化
			VARSET マップデータ
			マップデータ:0 = マップ名/%マップ名%
			;HOGE:Y:Xだ いいね？
			FOR XCOUNT, 0, 60
				FOR YCOUNT, 0, 30
					SIF 土地:YCOUNT:XCOUNT
						マップデータ:FINDELEMENT(マップデータ, "", 0, VARSIZE("マップデータ"), 1) = 土地/{YCOUNT}/{XCOUNT}/{土地:YCOUNT:XCOUNT}
					SIF OBJECTS:YCOUNT:XCOUNT != ""
						マップデータ:FINDELEMENT(マップデータ, "", 0, VARSIZE("マップデータ"), 1) =オブジェクト/{YCOUNT}/{XCOUNT}/%OBJECTS:YCOUNT:XCOUNT%
				NEXT
			NEXT
			SAVETEXT STRJOIN(マップデータ, UNICODE(0xA), 0, FINDELEMENT(マップデータ, "", 0, VARSIZE("マップデータ"), 1)), LRESULT
			PRINTFORMW マップスロット{LRESULT}番に%マップ名%を保存しました
			RETURN
	ENDSELECT
	CONTINUE
LOOP 1

$LOAD
PRINTL □模様替えのマップデータをロードします
PRINTL 　ロードしたいデータ番号を選んでください(savフォルダ内のtxt○○○.txtに相当)
FOR LCOUNT, 100, 130
	IF LOADTEXT(LCOUNT) != ""
		SPLIT LOADTEXT(LCOUNT), UNICODE(0xA), マップデータ
		PRINTFORML 　[{LCOUNT}] %マップデータ:0%
	ELSE
		CALL PRINTCOLORL, @"　[{LCOUNT}] 空きスロット", "gray"
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　MAP　
PRINTL [0] 戻る
DO
	INPUT
	LRESULT = RESULT
	SELECTCASE LRESULT
		CASE 0
			RETURN
		CASE 100 TO 129
			IF LOADTEXT(LRESULT) == ""
				PRINTFORMW マップスロット {LRESULT}番にはマップが登録されていません
				GOTO LOAD
			ENDIF
			PRINTL マップデータをロードすると既存の模様替えマップは破棄されます。よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			BINPUT
			SIF RESULT == 1
				GOTO LOAD
			VARSET 土地
			VARSET OBJECTS
			CVARSET CFLAG, GETNUM(CFLAG, "居住")
			SPLIT LOADTEXT(LRESULT), UNICODE(0xA), マップデータ
			;オブジェクトor土地/Y座標/X座標/オブジェクト名or部屋番号
			FOR LCOUNT, 0, VARSIZE("マップデータ")
				SIF マップデータ:LCOUNT == ""
					BREAK
				SPLIT マップデータ:LCOUNT, "/", SPLITVAR
				SELECTCASE SPLITVAR:0
					CASE "土地"
						土地:TOINT(SPLITVAR:1):TOINT(SPLITVAR:2) = TOINT(SPLITVAR:3)
					CASE "オブジェクト"
						OBJECTS:TOINT(SPLITVAR:1):TOINT(SPLITVAR:2) = %SPLITVAR:3%
				ENDSELECT
			NEXT
			PRINTFORMW %マップデータ:0%をロードしました
			RETURN
	ENDSELECT
	CONTINUE
LOOP 1

@にんじん解禁
#DIM DYNAMIC LTARGET
PRINTW なんと、生きたカカシを発見した！
PRINTW にんじんが加わりました
CALL CHARA_ADD, GETNO("にんじん")
TRYCALLFORM %CSVNAMERPS(TARGET)%_にんじん解禁
TRYCALLFORM MASTER_%CSVNAMERPS(NOF(MASTER))%_にんじん解禁
IF パートナー != ""
	IF OPTION:口上表示 && !CFLAG:GETCHARANAME(パートナー):口上非表示
		LTARGET = TARGET
		TARGET = GETCHARANAME(パートナー)
		TRYCALLFORM PARTNER_%CSVNAMERPS(GETNO(パートナー))%_にんじん解禁
		TARGET = LTARGET
	ENDIF
ENDIF
SIF CFLAG:誕生日 == (MONTH*100)+DAYS
	CALL 実績解除, "アンハッピーバースデー"
CALL プレイログ記録, "にんじんが加入"

@マップバージョン管理
[SKIPSTART]
#DIMS DYNAMIC オブジェクト情報, 100, 4
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIMS DYNAMIC 分割用, 4
#DIM DYNAMIC OBJCOUNT

OBJCOUNT = 1
オブジェクト情報:(OBJCOUNT++):0 '= "ベッド1_右向き", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "ベッド1_左向き", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "ベッド2_右向き", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "ベッド2_左向き", "2", "1"

OBJCOUNT = 11
オブジェクト情報:(OBJCOUNT++):0 '= "椅子1_右向き", "1", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "椅子1_左向き", "1", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "椅子2_右向き", "1", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "椅子2_左向き", "1", "1"

OBJCOUNT = 21
オブジェクト情報:(OBJCOUNT++):0 '= "テーブル1", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "ランプ1", "1", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "地球儀", "1", "1", "小物"

OBJCOUNT = 31
オブジェクト情報:(OBJCOUNT++):0 '= "本棚1", "2", "2"
オブジェクト情報:(OBJCOUNT++):0 '= "本1", "1", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "カウンター_右向き", "3", "2"
オブジェクト情報:(OBJCOUNT++):0 '= "カウンター_左向き", "3", "2"

OBJCOUNT = 41
オブジェクト情報:(OBJCOUNT++):0 '= "救急箱", "2", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "心電図", "2", "1", "小物"

OBJCOUNT = 51
オブジェクト情報:(OBJCOUNT++):0 '= "シンク", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "コンロ", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "冷蔵庫", "2", "2"

OBJCOUNT = 61
オブジェクト情報:(OBJCOUNT++):0 '= "テレビ", "2", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "鏡", "1", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "タンス", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "クローゼット_右向き", "1", "2"
オブジェクト情報:(OBJCOUNT++):0 '= "クローゼット_左向き", "1", "2"
オブジェクト情報:(OBJCOUNT++):0 '= "ソファ", "2", "1"
オブジェクト情報:(OBJCOUNT++):0 '= "絨毯", "3", "2"

OBJCOUNT = 71
オブジェクト情報:(OBJCOUNT++):0 '= "三角マーク", "1", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "生体ディスプレイ", "2", "2", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "培養槽", "2", "3"
オブジェクト情報:(OBJCOUNT++):0 '= "フラスコセット", "2", "1", "小物"
オブジェクト情報:(OBJCOUNT++):0 '= "テーブル_白", "2", "1"

;Ver.2.14 2021/05/XX～導入
;変数「オブジェクト」にオブジェクト情報を数値で入れてたのを文字列変数「OBJECTS」に変更
IF マップバージョン == 0
	FOR Y座標, 0, 30
		FOR X座標, 0, 60
			IF オブジェクト:Y座標:X座標
				;テーブル_白だけハイフンが使われてるので別処理
				IF オブジェクト情報:(オブジェクト:Y座標:X座標):0 == "テーブル_白"
					OBJECTS:Y座標:X座標 = テーブル（白）_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
				ELSE
					SPLIT オブジェクト情報:(オブジェクト:Y座標:X座標):0, "_", 分割用
					IF RESULT >= 2
						OBJECTS:Y座標:X座標 = %オブジェクト情報:(オブジェクト:Y座標:X座標):0%_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
					ELSE
						OBJECTS:Y座標:X座標 = %オブジェクト情報:(オブジェクト:Y座標:X座標):0%_右向き_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
					ENDIF
				ENDIF
				DEBUGPRINTFORML X({X座標})×Y({Y座標})に%オブジェクト情報:(オブジェクト:Y座標:X座標):0% %オブジェクト情報:(オブジェクト:Y座標:X座標):1%×%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
			ENDIF
		NEXT
	NEXT
	;以降、上記の「オブジェクト情報」は枠が足りなくなるので使わない
	マップバージョン = 21400
ENDIF

;2022/03/10 オブジェクトに角度を付けられるようになったので情報を補完
IF マップバージョン <= 21400
	FOR Y座標, 0, 30
		FOR X座標, 0, 60
			SIF OBJECTS:Y座標:X座標 != ""
				OBJECTS:Y座標:X座標 = %OBJECTS:Y座標:X座標%_0
		NEXT
	NEXT
	マップバージョン = 21600
ENDIF
[SKIPEND]
