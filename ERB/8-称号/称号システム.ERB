@称号表示
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC CHARA_1
#DIM DYNAMIC CHARA_2
#DIM DYNAMIC CHARA_3
#DIM DYNAMIC 特殊称号計算用, 3
#DIM DYNAMIC 特殊称号候補, 3
#DIMS DYNAMIC 本文

VARSET WHO_P
VARSET DID_P

;将来的に色付けたりするかもしれないので式中関数ではなく普通の関数
;色変え等無いので式中関数化した

;特定の1人または2人の調教回数だけがやたら多い場合は特殊称号
IF MAXCARRAY(CFLAG:調教回数) >= 30
	FOR LCOUNT, 1, CHARANUM
		IF CFLAG:LCOUNT:調教回数 > 特殊称号計算用:0
			ARRAYSHIFT 特殊称号計算用, 1, CFLAG:LCOUNT:調教回数
			ARRAYSHIFT 特殊称号候補, 1, LCOUNT
		ELSEIF CFLAG:LCOUNT:調教回数 > 特殊称号計算用:1
			ARRAYSHIFT 特殊称号計算用, 1, CFLAG:LCOUNT:調教回数, 1
			ARRAYSHIFT 特殊称号候補, 1, LCOUNT, 1
		ELSEIF CFLAG:LCOUNT:調教回数 > 特殊称号計算用:2
			ARRAYSHIFT 特殊称号計算用, 1, CFLAG:LCOUNT:調教回数, 2
			ARRAYSHIFT 特殊称号候補, 1, LCOUNT, 2
		ENDIF
	NEXT
	CHARA_1 = 特殊称号候補:0
	CHARA_2 = 特殊称号候補:1
	CHARA_3 = 特殊称号候補:2
	;1人だけ30以上の差をつけてるとここ
	IF 特殊称号計算用:0-特殊称号計算用:1 >= 30
		DID_P:通常P = CFLAG:CHARA_1:通常調教
		DID_P:加虐P = CFLAG:CHARA_1:加虐調教
		DID_P:被虐P = CFLAG:CHARA_1:被虐調教
		;キャラに取って攻めだった場合、主人にとっては受け、とかそんな感じ
		DID_P:受けP = CFLAG:CHARA_1:攻め調教
		DID_P:攻めP = CFLAG:CHARA_1:受け調教

		本文 += @"%CALLNAME:CHARA_1%"
		SELECTCASE SEX(MASTER)
			CASE 1
				IF EXP:CHARA_1:助手調教経験 > EXP:CHARA_1:主人調教経験
					本文 += "を眺める会の会長"
				;SM寄り
				ELSEIF DID_P:加虐P > DID_P:通常P
					本文 += "のご主人様"
				ELSEIF DID_P:被虐P > DID_P:通常P
					本文 += "の犬"
				ELSE
					IF DID_P:受けP > DID_P:攻めP
						本文 += "の愛人"
					ELSE
						本文 += "の夫"
					ENDIF
				ENDIF
			CASE 2
				IF EXP:CHARA_1:助手調教経験 > EXP:CHARA_1:主人調教経験
					本文 += "を眺める会の会長"
				;SM寄り
				ELSEIF DID_P:加虐P > DID_P:通常P
					本文 += "の女王様"
				ELSEIF DID_P:被虐P > DID_P:通常P
					本文 += "のメス犬"
				ELSE
					IF DID_P:攻めP > DID_P:受けP
						本文 += "は俺の嫁"
					ELSE
						IF TALENT:MASTER:体格 <= 2
							本文 += "の幼妻"
						ELSE
							本文 += "の妻"
						ENDIF
					ENDIF
				ENDIF
		ENDSELECT
		RETURNF 本文
	;2人の差が30未満で3人目と30以上離れてる場合
	ELSEIF 特殊称号計算用:0-特殊称号計算用:1 < 30 && 特殊称号計算用:1-特殊称号計算用:2 >= 30
		;主人だけ男性&他女性
		IF SEX(MASTER) == 1 && SEX(CHARA_1) == 2 && SEX(CHARA_2) == 2
			本文 += @"%CALLNAME:CHARA_1%と%CALLNAME:CHARA_2%で両手に花"
		;主人だけ女性&他男性
		ELSEIF SEX(MASTER) == 2 && SEX(CHARA_1) == 1 && SEX(CHARA_2) == 1
			本文 += @"%CALLNAME:CHARA_1%と%CALLNAME:CHARA_2%の間で揺れ動く乙女"
		ELSE
			本文 += @"%CALLNAME:CHARA_1%と%CALLNAME:CHARA_2%との三角関係"
		ENDIF
		RETURNF 本文
	ENDIF
ENDIF

;ここから普通のプレイ称号・調教称号
IF MAXARRAY(プレイ称号) == 0
	本文 += "どこにでも居る"
ELSE
	本文 += プレイ称号計算("プレイ称号表示")
ENDIF

;「調教称号」変数は二次元配列
;調教称号:(誰に):(何をしたか)

VARSET WHO_P
FOR LCOUNT, 0, VARSIZE("WHO_P")
	FOR LCOUNT2, 0, VARSIZE("DID_P")
		WHO_P:LCOUNT += 調教称号:LCOUNT:LCOUNT2
	NEXT
	;DEBUGPRINTFORML %ERDNAME(WHO_P, LCOUNT)%{WHO_P:LCOUNT}pt
NEXT

IF MAXARRAY(WHO_P) == 0
	本文 += "調教師"
ELSE
	SELECTCASE MAXARRAY(WHO_P)
		CASE WHO_P:ロリコンP, WHO_P:ショタコンP
			;各ポイントが片方の2倍以上なら◯◯コン、差が小さければペドフィリア
			IF WHO_P:ロリコンP > WHO_P:ショタコンP*2
				FOR LCOUNT2, 0, VARSIZE("DID_P")
					DID_P:LCOUNT2 = 調教称号:ロリコンP:LCOUNT2
				NEXT
				IF DID_P:加虐P > DID_P:通常P
					本文 += "女児虐待常習者"
				ELSEIF DID_P:被虐P > DID_P:通常P || DID_P:受けP > DID_P:攻めP
					本文 += "女児に踏まれたい人"
				ELSE
					本文 += "ロリコン"
				ENDIF
			ELSEIF WHO_P:ショタコンP > WHO_P:ロリコンP*2
				FOR LCOUNT2, 0, VARSIZE("DID_P")
					DID_P:LCOUNT2 = 調教称号:ショタコンP:LCOUNT2
				NEXT
				IF DID_P:加虐P > DID_P:通常P
					本文 += "男児虐待の常習者"
				ELSEIF DID_P:攻めP > DID_P:受けP
					本文 += "ショタをメス堕ちさせたい人"
				ELSE
					本文 += "ショタコン"
				ENDIF
			ELSE
				FOR LCOUNT2, 0, VARSIZE("DID_P")
					IF WHO_P:ロリコンP > WHO_P:ショタコンP
						DID_P:LCOUNT2 = 調教称号:ロリコンP:LCOUNT2
					ELSE
						DID_P:LCOUNT2 = 調教称号:ショタコンP:LCOUNT2
					ENDIF
				NEXT
				IF DID_P:加虐P > DID_P:通常P
					本文 += "児童虐待常習者"
				ELSEIF DID_P:被虐P > DID_P:通常P || DID_P:受けP > DID_P:攻めP
					本文 += "児童に劣情を抱く者"
				ELSE
					本文 += "小児性愛者"
				ENDIF
			ENDIF
		CASE WHO_P:男性P
			FOR LCOUNT2, 0, VARSIZE("DID_P")
				DID_P:LCOUNT2 = 調教称号:男性P:LCOUNT2
			NEXT
			SELECTCASE SEX(MASTER)
				CASE 1
					IF DID_P:加虐P > DID_P:通常P || DID_P:被虐P > DID_P:通常P
						本文 += "SM愛好ゲイ"
					ELSEIF DID_P:攻めP > DID_P:受けP
						本文 += "タチ寄りゲイ"
					ELSEIF DID_P:受けP > DID_P:攻めP
						本文 += "ネコ寄りゲイ"
					ELSE
						本文 += "ゲイ"
					ENDIF
				CASE 2
					IF DID_P:加虐P > DID_P:通常P || DID_P:攻めP > DID_P:受けP
						IF TALENT:MASTER:体格 <= 2
							本文 += "メスガキ"
						ELSE
							本文 += "女王様気質"
						ENDIF
					ELSE
						本文 += "女調教師"
					ENDIF
			ENDSELECT
		CASE WHO_P:女性P
			FOR LCOUNT2, 0, VARSIZE("DID_P")
				DID_P:LCOUNT2 = 調教称号:女性P:LCOUNT2
			NEXT
			SELECTCASE SEX(MASTER)
				CASE 1
					IF DID_P:加虐P > DID_P:通常P
						本文 += "サディストな調教師"
					ELSEIF DID_P:被虐P > DID_P:通常P
						本文 += "マゾヒストな調教師"
					ELSE
						本文 += "調教師"
					ENDIF
				CASE 2
					IF DID_P:加虐P > DID_P:通常P || DID_P:攻めP > DID_P:受けP
						本文 += "小悪魔系レズ"
					ELSE
						本文 += "百合好き女調教師"
					ENDIF
			ENDSELECT
		CASE WHO_P:貧乳P
			本文 += "貧乳好き"
		CASE WHO_P:巨乳P
			本文 += "巨乳好き"
		CASE WHO_P:ふたなりP
			FOR LCOUNT2, 0, VARSIZE("DID_P")
				DID_P:LCOUNT2 = 調教称号:ふたなりP:LCOUNT2
			NEXT
			IF DID_P:受けP > DID_P:攻めP && SEX(MASTER) == 1
				本文 += "メス堕ちしがちの調教師"
			ELSE
				本文 += "ふたなり好き"
			ENDIF
		CASEELSE
			本文 += "調教師"
	ENDSELECT
ENDIF

RETURNF 本文


@プレイ称号計算, 状況
#FUNCTIONS
#DIM DYNAMIC 休憩値
#DIMS DYNAMIC 状況
#DIMS DYNAMIC 本文
{
#DIMS CONST プレイ称号一覧 = 
"休憩",
"スキル",
"能力表示",
"魔法詠唱",
"ギャンブル",
"マップ",
"アイテム",
"眷属",
"セーブ",
"テキスト設定",
"着衣設定",
"チュートリアル",
"ゲーセン経営",
"娼館経営",
"芸プロ運営",
"医務室",
"実験室",
"図書室",
"調理室",
"農業",
"畜産業",
"印",
"博物館",
"カニバリスト"
}

;休憩だけ一時的に減らす
;あまりにも疲れやすいためいっそ0にしてみる
休憩値 = プレイ称号:0
プレイ称号:0 = 0

IF 状況 == "プレイ称号表示"
	SELECTCASE プレイ称号一覧:FINDELEMENT(プレイ称号, MAXARRAY(プレイ称号))
		CASE "休憩"
			本文 += "やたら疲れやすい"
		CASE "スキル"
			本文 += "向上心がある"
		CASE "能力表示"
			本文 += "キャラの能力が気になる"
		CASE "魔法詠唱"
			本文 += "魔法を使いこなす"
		CASE "ギャンブル"
			本文 += "ギャンブラーな"
		CASE "マップ"
			本文 += "風水にこだわる"
		CASE "アイテム"
			本文 += "物欲の多い"
		CASE "眷属"
			本文 += "種族の垣根を越える"
		CASE "セーブ"
			本文 += "心配性な"
		CASE "テキスト設定"
			本文 += "いろいろ読みたい"
		CASE "着衣設定"
			本文 += "オシャレな"
		CASE "チュートリアル"
			本文 += "初心を忘れない"
		CASE "ゲーセン経営"
			本文 += "ゲーマーな"
		CASE "娼館経営"
			本文 += "娼婦を束ねる"
		CASE "芸プロ運営"
			本文 += "エンターテイナーな"
		CASE "医務室"
			本文 += "医学に明るい"
		CASE "実験室"
			本文 += "サイエンティストな"
		CASE "図書室"
			本文 += "読書家な"
		CASE "調理室"
			本文 += "グルメな"
		CASE "農業"
			本文 += "農業が趣味の"
		CASE "畜産業"
			本文 += "動物を愛する"
		CASE "印"
			本文 += "独占欲が強い"
		CASE "博物館"
			本文 += "美意識の高い"
		CASE "カニバリスト"
			本文 += "食欲旺盛な"
	ENDSELECT
ELSE
	プレイ称号:FINDELEMENT(プレイ称号一覧, 状況, 0, VARSIZE("プレイ称号一覧"))++
ENDIF

プレイ称号:0 = 休憩値

RETURNF ""

@調教称号計算
#DIM DYNAMIC 行動内容

CFLAG:調教回数++

IF PALAM:苦痛+PALAM:恐怖 > PALAM:恭順+PALAM:屈服
	CFLAG:加虐調教++
	行動内容 = GETNUM(調教称号, "加虐P", 2)
ELSEIF EXP:嗜虐快楽経験 > 嗜虐経験計算用
	CFLAG:被虐調教++
	行動内容 = GETNUM(調教称号, "被虐P", 2)
ELSEIF (EXP:MASTER:Ａ経験+EXP:MASTER:Ｖ経験)-主人ＡＶ経験計算用 > (EXP:Ａ経験+EXP:Ｖ経験)-ＡＶ経験計算用
	CFLAG:攻め調教++
	行動内容 = GETNUM(調教称号, "受けP", 2)
ELSEIF (EXP:MASTER:Ａ経験+EXP:MASTER:Ｖ経験)-主人ＡＶ経験計算用 < (EXP:Ａ経験+EXP:Ｖ経験)-ＡＶ経験計算用
	行動内容 = GETNUM(調教称号, "攻めP", 2)
	CFLAG:受け調教++
ELSE
	CFLAG:通常調教++
	行動内容 = GETNUM(調教称号, "通常P", 2)
ENDIF

SELECTCASE SEX(TARGET)
	CASE 1
		IF TALENT:体格 <= 2
			調教称号:ショタコンP:行動内容++
		ELSE
			調教称号:男性P:行動内容++
		ENDIF
	CASE 2
		IF TALENT:体格 <= 2
			調教称号:ロリコンP:行動内容++
		ELSE
			調教称号:女性P:行動内容++
		ENDIF
		SELECTCASE TALENT:バストサイズ
			CASE 1, 2
				調教称号:貧乳P:行動内容++
			CASE IS >= 4
				調教称号:巨乳P:行動内容++
		ENDSELECT
ENDSELECT

SIF TALENT:ふたなり
	調教称号:ふたなりP:行動内容++
