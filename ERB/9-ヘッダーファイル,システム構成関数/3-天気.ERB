@WEATHER
#DIM DYNAMIC 降雪確率補正
#DIM DYNAMIC 雷雨確率補正
#DIM DYNAMIC 気温差

;FLAG:天気 1=晴れ 2=曇り 3=雨 4=大雨 5=雷雨 6=雪 7=大雪

;梅雨入りと梅雨明けの処理
SELECTCASE MONTH
	CASE 6
		IF FLAG:梅雨 == 0 && RAND:101 < DAYS*10
			FLAG:梅雨 = 1
			PRINTW 梅雨に入りました
		ENDIF
	CASE 7
		IF FLAG:梅雨 && RAND:101 < DAYS*3
			FLAG:梅雨 = 0
			PRINTW 梅雨が明けました
		ENDIF
	CASE 8
		IF FLAG:梅雨 && RAND:101 < DAYS*3+93
			FLAG:梅雨 = 0
			PRINTW 梅雨が明けました
		ENDIF
ENDSELECT

SELECTCASE ターン
	;昼 前日の気温には左右されるが、前日の天気は引きずりにくい
	CASE "昼"
		;ここで雨量指数(FLAG:降水量)を設定する
		CALL PRECIPITATION
		;梅雨のときは降水量を増やす
		SIF FLAG:梅雨
			FLAG:降水量 *= 2
		SIF FLAG:梅雨 && FLAG:降水量
			FLAG:降水量 += RAND:20
		
		;1ミリでも降水量があるとき、雨とする
		IF FLAG:降水量
			FLAG:天気 = 天気TOINT("雨")
		;それ以外の場合、曇りか晴れかの判定
		ELSE
			;前日の昼との温度差を比較する
			;気温が上がっている場合、曇りになる確率は低い
			;気温が下がっている場合、曇りになる確率が若干高い
			;気温が高いと基本的に曇りにくい
			気温差 = FLAG:気温 + 2*(FLAG:気温-前の昼の気温)
			;前日が晴れなら曇りになりにくい
			SIF FLAG:天気 == 天気TOINT("晴れ")
				気温差 += 10
			;気温+50の乱数 前日が晴れでなく気温が同じなら、気温10℃で曇り33%、気温20℃で曇り20%、気温30℃で曇り14%、0℃なら当然100%曇る
			;気温-5℃以下は必ず曇りになる
			IF FLAG:気温 <= -50
				FLAG:天気 = 天気TOINT("曇り")
			ELSEIF RAND:(FLAG:気温+50) >= 気温差
				FLAG:天気 = 天気TOINT("曇り")
			ELSE
				FLAG:天気 = 天気TOINT("晴れ")
			ENDIF
		ENDIF
		
		;前の昼の気温の参照が完了したら、1,2ターン後のためにフラグ代入しておく
		前の昼の気温 = FLAG:気温
		
	;夜 昼の天気を引きずりやすいが、気温に左右されにくい
	;夜になって昼より気温が上がることはないが、デバッグモード使えば出来るから一応処理書いとく
	CASE "夜"
		;現在の天気を参照
		SELECTCASE 天気()
			CASE "晴れ"
				;気温が下がってない場合、晴れをキープ
				;ただし気温が下がらないというのは夏でも1割程度
				IF 前の昼の気温 <= FLAG:気温
					FLAG:天気 = 天気TOINT("晴れ")
				;気温が下がっている場合
				ELSE
					;昼との温度差を比較する
					;気温が下がっているほど、曇りになる確率が高くなる
					;これも気温が高いほど晴れやすい ほぼ間違いなく夜補正で気温は下がってるので、昼よりは曇りやすい、
					気温差 = FLAG:気温+(FLAG:気温-前の昼の気温)
					;気温+75の乱数 昼と気温が同じなら、気温10℃で曇り42%、気温20℃で曇り27%、気温30℃で曇り20%、0℃なら当然100%曇る
					;気温-7.5℃以下は必ず曇りになる
					IF FLAG:気温 <= -75
						FLAG:天気 = 天気TOINT("曇り")
					ELSEIF RAND:(FLAG:気温+75) >= 気温差
						FLAG:天気 = 天気TOINT("曇り")
					ELSE
						FLAG:天気 = 天気TOINT("晴れ")
					ENDIF
				ENDIF
			CASE "曇り"
				;気温が下がってない場合、晴れにする
				IF 前の昼の気温 <= FLAG:気温
					FLAG:天気 = 天気TOINT("晴れ")
				;気温が下がっている場合
				ELSE
					;雨量指数(FLAG:降水量)を再計算
					CALL PRECIPITATION
					;梅雨のときは降水量を増やす
					SIF FLAG:梅雨
						FLAG:降水量 *= 2
					SIF FLAG:梅雨 && FLAG:降水量
						FLAG:降水量 += RAND:20
					;1ミリでも降水量があるとき、雨とする
					IF FLAG:降水量
						FLAG:天気 = 天気TOINT("雨")
					;それ以外の場合、曇りか晴れかの判定
					ELSE
						;昼との温度差を比較する
						;気温が下がっているほど、曇りになる確率が高くなる
						;これも気温が高いほど晴れやすい ほぼ間違いなく夜補正で気温は下がってるので、昼よりは曇りやすい、
						気温差 = FLAG:気温+(FLAG:気温-前の昼の気温)
						;気温+75の乱数 昼と気温が同じなら、気温10℃で曇り42%、気温20℃で曇り27%、気温30℃で曇り20%、0℃なら当然100%曇る
						;気温-7.5℃以下は必ず曇りになる
						IF FLAG:気温 <= -75
							FLAG:天気 = 天気TOINT("曇り")
						ELSEIF RAND:(FLAG:気温+75) >= 気温差
							FLAG:天気 = 天気TOINT("曇り")
						ELSE
							FLAG:天気 = 天気TOINT("晴れ")
						ENDIF
					ENDIF
				ENDIF
			CASE "雨", "大雨", "雷雨", "雪", "大雪"
				;気温が下がってない場合、曇りにする
				IF 前の昼の気温 <= FLAG:気温 && !スウィートレイン && !スノープリズム
					FLAG:天気 = 天気TOINT("曇り")
					FLAG:降水量 = 0
				;気温が下がった場合、降水量を再計算し、現在の降水量/3+1を追加
				ELSE
					気温差 = FLAG:降水量
					;雨量指数(FLAG:降水量)を再計算
					CALL PRECIPITATION
					;梅雨のときは降水量を増やす
					SIF FLAG:梅雨
						FLAG:降水量 *= 2
					SIF FLAG:梅雨 && FLAG:降水量
						FLAG:降水量 += RAND:20

					FLAG:降水量 += 気温差/3+1
				ENDIF
		ENDSELECT
ENDSELECT

;ストームバスター発動中は問答無用で晴れる
SIF ストームバスター
	FLAG:天気 = 天気TOINT("晴れ")

;降水時に雨か雪かの処理
;一応5月～10月の設定はしてるけど、この条件式だと雪になることはまず無いと思われる
SELECTCASE MONTH
	CASE 1, 2, 12
		降雪確率補正 = 25
	CASE 3, 4, 11
		降雪確率補正 = 20
	CASE 5, 6, 10
		降雪確率補正 = 10
	CASE 7, 8, 9
		降雪確率補正 = 5
ENDSELECT

;雨,雪の分岐処理 一定以下の気温だと乱数次第でずっと雪が続く
IF GROUPMATCH(天気(), "雨", "大雨", "雷雨", "雪", "大雪")
	;魔法唱えてると必ずその天気になる
	IF スウィートレイン
		FLAG:天気 = 天気TOINT("雨")
	ELSEIF スノープリズム
		FLAG:天気 = 天気TOINT("雪")
	ELSE
		SELECTCASE FLAG:気温
			;気温0℃以下は雪
			CASE IS <= 0
				FLAG:天気 = 天気TOINT("雪")
			;0.1℃以上の場合
			CASEELSE
				;12月～2月は2.5℃以下で雪確定
				;3月、4月、11月は2.0℃以下で雪確定
				;5月、6月、10月は1.0℃以下で雪確定
				;7月～9月は0.5℃以下で雪確定
				SELECTCASE FLAG:気温 - 降雪確率補正
					CASE IS <= 0
						FLAG:天気 = 天気TOINT("雪")
					CASEELSE
						;降雪確率補正が低いほど雪になる確率は低い
						;雪が降る可能性のある最高気温
						;1月、2月、12月は12.9℃
						;3月、4月、11月は8.3℃
						;5月、6月、10月は2.1℃
						;7月～9月は0.5℃ つまり確定時以外降らない
						IF FLAG:気温*5/降雪確率補正 <= 降雪確率補正
							;上記の気温以下なら、気温が低ければ低いほど雪の確率アップ
							IF RAND:(FLAG:気温) < 降雪確率補正
								FLAG:天気 = 天気TOINT("雪")
							ELSE
								FLAG:天気 = 天気TOINT("雨")
							ENDIF
						ELSE
							FLAG:天気 = 天気TOINT("雨")
						ENDIF
				ENDSELECT
		ENDSELECT
	ENDIF
ENDIF


;降水時の天気派生処理
;下記は雷雨確率処理 雪とは違い、こっちは夏～秋のほうが雷雨の確率が高い
;ちなみに雷雨確率補正は低いほうが雷雨になりやすい
SELECTCASE MONTH
	CASE 1 TO 5, 8 TO 12
		雷雨確率補正 = 150
	CASE 9, 10, 11
		雷雨確率補正 = 100
	CASE 6, 7, 8
		雷雨確率補正 = 50
ENDSELECT

SELECTCASE 天気()
	CASE "雨", "大雨", "雷雨"
		;降水量15以上で大雨
		IF FLAG:降水量 >= 15
			FLAG:天気 = 天気TOINT("大雨")
		;14以下だと大雨解除
		ELSE
			FLAG:天気 = 天気TOINT("雨")
		ENDIF
		;大雨で気温が下がっている場合、確率で雷が伴う
		IF FLAG:天気 == 天気TOINT("大雨")
			;気温が下がってなければ終わり
			気温差 = 前の昼の気温-FLAG:気温
			SIF 気温差 <= 0
				RETURN 0
			;雷雨確率補正+気温差の乱数
			;6～8月で  気温差が1℃ある場合は16%、3℃ある場合は37%、5℃ある場合は50%
			;9～11月で 気温差が1℃ある場合は9%、 3℃ある場合は23%、5℃ある場合は33%
			;それ以外で気温差が1℃ある場合は6%、 3℃ある場合は16%、5℃ある場合は25%
			SIF RAND:(雷雨確率補正+気温差) < 気温差
				FLAG:天気 = 天気TOINT("雷雨")
		ENDIF
	CASE "雪", "大雪"
		;降水量10以上の雪で大雪
		;普通にプレイしてたら多分大雪にはめったにならない
		IF FLAG:降水量 >= 10
			FLAG:天気 = 天気TOINT("大雪")
		;9以下だと普通の雪
		ELSE
			FLAG:天気 = 天気TOINT("雪")
		ENDIF
ENDSELECT
