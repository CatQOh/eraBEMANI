;名前表示用関数
;あくまで表示用 内部処理は今まで通り日本語を使う
@NAMEDISP, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

IF LANGUAGE == "ENG" && CSTR:対象:英語名 != ""
	RETURNF CSTR:対象:英語名
ELSE
	RETURNF NAME:対象
ENDIF

;呼び名表示用関数 同上
@CALLNAMEDISP, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

IF LANGUAGE == "ENG" && CSTR:対象:英語呼び名 != ""
	RETURNF CSTR:対象:英語呼び名
ELSE
	RETURNF CALLNAME:対象
ENDIF

;CSVから対象の英語名を返す
@CSVENGNAME, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

;子供の名前はそのまま返す
SELECTCASE 対象
	CASE 0 TO 9999
		IF EXISTCSV(対象)
			IF CSVCSTR(対象, GETNUM(CSTR, "英語名")) != ""
				RETURNF CSVCSTR(対象, GETNUM(CSTR, "英語名"))
			ELSE
				RETURNF CSVNAME(対象)
			ENDIF
		ENDIF
	CASEELSE
		RETURNF CHILDNAME:(対象-10000)
ENDSELECT

;CSVから対象の英語呼び名を返す
@CSVENGCALLNAME, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

;子供の名前はそのまま返す
SELECTCASE 対象
	CASE 0 TO 9999
		IF EXISTCSV(対象)
			IF CSVCSTR(対象, GETNUM(CSTR, "英語呼び名")) != ""
				RETURNF CSVCSTR(対象, GETNUM(CSTR, "英語呼び名"))
			ELSE
				RETURNF CSVCALLNAME(対象)
			ENDIF
		ENDIF
	CASEELSE
		RETURNF CHILDNAME:(対象-10000)
ENDSELECT

;CSVNAMEFとCSVENGNAMEの統合版
@CSVNAMEDISP, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

IF LANGUAGE == "ENG"
	RETURNF CSVENGNAME(対象)
ELSE
	RETURNF CSVNAMEF(対象)
ENDIF


@CSVCALLNAMEDISP, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

IF LANGUAGE == "ENG"
	RETURNF CSVENGCALLNAME(対象)
ELSE
	RETURNF CSVCALLNAMEF(対象)
ENDIF

;曜日の英語関数
@DOWDISP(曜日)
#FUNCTIONS
#DIM DYNAMIC 曜日
;DOWはday of week(曜日)の略
;「1:日,2:月,3:火,4:水,5:木,6:金,0:土」を出力する
SELECTCASE 曜日
	CASE 1
		RETURNF "Sun"
	CASE 2
		RETURNF "Mon"
	CASE 3
		RETURNF "Tue"
	CASE 4
		RETURNF "Wed"
	CASE 5
		RETURNF "Thu"
	CASE 6
		RETURNF "Fri"
	CASE 0
		RETURNF "Sat"
ENDSELECT

;天気
[SKIPSTART]
@WEATHERDISP(ARG)
#FUNCTIONS

SIF ARG == 0
	ARG = FLAG:天気

SELECTCASE ARG
	CASE 1
		RETURNF "Sunny"
	CASE 2
		RETURNF "Cloudy"
	CASE 3
		RETURNF "Rain"
	CASE 4
		RETURNF "Heavy Rain"
	CASE 5
		RETURNF "Thunderstorm"
	CASE 6
		RETURNF "Snow"
	CASE 7
		RETURNF "Heavy Snow"
ENDSELECT
[SKIPEND]

;カジノゲーム名
@GAMBLEDISP, ゲーム名
#FUNCTIONS
#DIMS DYNAMIC ゲーム名

SELECTCASE ゲーム名
	CASE "ルーレット"
		RETURNF "Roulette"
	CASE "クラップス"
		RETURNF "Craps"
	CASE "ブラックジャック"
		RETURNF "Blackjack"
	CASE "ビンゴピンボール"
		RETURNF "Bingo Pinball"
	CASE "ビデオポーカー"
		RETURNF "Video Poker"
	CASE "ポーカーソリティア"
		RETURNF "Poker Square"
	CASE "レットイットライド"
		RETURNF "Let It Ride"
	CASE "バカラ"
		RETURNF "Baccarat"
	CASE "サーティー"
		RETURNF "Thirty"
	CASE "素数ダイス"
		RETURNF "Prime Dice"
	CASE "モンテカルロ"
		RETURNF "Monte Carlo"
	CASE ""
		RETURNF ""
	CASE ""
		RETURNF ""
ENDSELECT

@I18N, TEXT, TYPE, FORMAT:0, FORMAT:1, FORMAT:2
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC TEXT
#DIMS DYNAMIC TYPE
#DIMS DYNAMIC FORMAT, 3
#DIMS DYNAMIC TRANSLATED
#DIM DYNAMIC LANG_NUM
#DIMS DYNAMIC LIB_KEY
#DIMS DYNAMIC LDICT, 1000, 5

;SYSTXTDISPと関数分けるのが面倒になってきたのでこの関数で完結できるようにする
SIF TEXT != SYSTXTDISP_LIB(TEXT) || FORMAT:0 != ""
	RETURNF SYSTXTDISP(TEXT, TYPE, FORMAT:0, FORMAT:1, FORMAT:2)

TRANSLATED = %TEXT%

SELECTCASE LANGUAGE
	CASE "JP"
		RETURNF TRANSLATED
	CASE "ENG"
		LANG_NUM = 1
ENDSELECT

IF TYPE == ""
	LIB_KEY = I18N_COMMON
ELSE
	LIB_KEY '= "I18N_" + TYPE
ENDIF
ARRAYCOPY LIB_KEY, "LDICT"

FOR LCOUNT, 0, VARSIZE(LIB_KEY)
	IF TRANSLATED == LDICT:LCOUNT:0
		TRANSLATED = %LDICT:LCOUNT:LANG_NUM%
		RETURNF TRANSLATED
	ENDIF
NEXT

RETURNF TRANSLATED
