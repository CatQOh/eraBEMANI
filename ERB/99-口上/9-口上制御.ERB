;調教時キャラ別口上を一括管理

;-----------------------------------------------------------------------------------
;口上表示ファイル
;地の文、キャラ専用口上の表示をここで管理する
;OPTION:地の文表示 = 地の文表示するかどうか OPTION:口上表示 = 口上表示するかどうか
;-----------------------------------------------------------------------------------
;;;クローン、子供、それ以外に対応する口上関数もしくは主人の口上関数を決定する所までを行う
@FINDKOJOFUNC, 対象, 対象関数, 経営 = 0
#FUNCTIONS
#DIM DYNAMIC 対象
#DIMS DYNAMIC 対象関数
#DIMS DYNAMIC 口上関数
#DIM DYNAMIC 経営

SIF !OPTION:口上表示 || 対象 == -1
	RETURNF ""
;学園モードはロケーションごとに行為前と行為後を呼び出してるので不要
SIF ゲームモード() == "学園モード" && GROUPMATCH(対象関数, "START", "TURNEND")
	RETURNF ""
IF !CFLAG:対象:口上非表示
	IF NOF(対象) > 10000
		;クローン
		口上関数 = CLONE_%対象関数%_%CSVNAMERPS(CFLAG:対象:複製)%
		SIF EXISTFUNCTION(口上関数)
			RETURNF 口上関数
		;子供の場合はNO:父親×NO:母親の関数を呼び出す
		口上関数 = CHILD_%対象関数%_%CSVNAMERPS(CFLAG:対象:血縁コード/100000)%×%CSVNAMERPS(CFLAG:対象:血縁コード%100000)%
		SIF EXISTFUNCTION(口上関数)
			RETURNF 口上関数
	ELSE
		IF ゲームモード() == "学園モード"
			;学園モードでは別口上
			口上関数 = SCHOOL_KOJO_%対象関数%_%CSVNAMERPS(NOF(対象))%
			SIF EXISTFUNCTION(口上関数)
				RETURNF 口上関数
		ELSE
			;それ以外（魔法詠唱関数とゲーセン店長は特殊）
			IF GROUPMATCH(対象関数, "SPELLCAST", "RECIEVE_SPELLCAST", "ゲームセンター_COM") || 経営
				口上関数 = %対象関数%_%CSVNAMERPS(NOF(対象))%
			ELSE
				口上関数 = KOJO_%対象関数%_%CSVNAMERPS(NOF(対象))%
			ENDIF
			SIF EXISTFUNCTION(口上関数)
				RETURNF 口上関数
		ENDIF
	ENDIF
ENDIF
;主人口上を判定しないもの
SIF GROUPMATCH(対象関数, "SPELLCAST", "RECIEVE_SPELLCAST", "ゲームセンター_COM") || 経営
	RETURNF ""
;関数が見つからなかったら主人の口上
IF !CFLAG:MASTER:口上非表示
	口上関数 = MASTER_%対象関数%_%CSVNAMERPS(NOF(MASTER))%
	SIF EXISTFUNCTION(口上関数)
		RETURNF 口上関数
ENDIF
RETURNF ""

;--------------------------------------------------
;@KOJO_MESSAGE_COM
;コマンド実行時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_COM
#DIM DYNAMIC 店長

;口上の優先度 TARGETの口上→MASTERの口上→ゲームセンターコマンド口上→地の文
IF FINDKOJOFUNC(TARGET, "COM") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "COM"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_COM"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_COM"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "COM")%
ENDIF

;各口上に飛んでいない場合ゲームセンターコマンドなら店長の判定
店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
IF SELECTCOMS == "ゲームセンター" && 店長 > 0
	SIF FINDKOJOFUNC(店長, "ゲームセンター_COM") != ""
		JUMPFORM %FINDKOJOFUNC(店長, "ゲームセンター_COM")%
ENDIF

$WIDEDISABLE
SIF OPTION:口上表示
	CALL 汎用喘ぎ口上
SIF OPTION:地の文表示
	TRYCALLFORM MESSAGE_COM_%SELECTCOMS%

;--------------------------------------------------
;@KOJO_MESSAGE_USERCOM
;TRAIN外のコマンド実行時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_USERCOM, コマンド名
#DIMS DYNAMIC コマンド名

IF FINDKOJOFUNC(TARGET, "USERCOM") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "USERCOM"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_USERCOM"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_USERCOM"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "USERCOM")%, コマンド名
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	TRYCALLFORM MESSAGE_USERCOM_%コマンド名%

;-------------------------------------------------
;@KOJO_MESSAGE_SHOOT
;調教者の射精をトリガーにした口上
;-------------------------------------------------
@KOJO_MESSAGE_SHOOT
IF FINDKOJOFUNC(TARGET, "SHOOT") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "SHOOT"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_SHOOT"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_SHOOT"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "SHOOT")%
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	CALL SHOOT_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_EX
;奴隷の絶頂をトリガーにした口上
;-------------------------------------------------
@KOJO_MESSAGE_EX
IF FINDKOJOFUNC(TARGET, "EX") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "EX"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_EX"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_EX"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "EX")%
ENDIF

$WIDEDISABLE
IF OPTION:地の文表示
	CALL EX_MESSAGE
	;PLAYERの射精もまとめてここでウェイトする
	{
	SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:潮噴き || 
		NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精) || NOWEX:MASTER:触手射精
	}
		WAIT
ENDIF

;-------------------------------------------------
;@KOJO_MESSAGE_AFTER
;コマンド実行後に表示する口上（処女喪失時など）
;-------------------------------------------------
@KOJO_MESSAGE_AFTER
IF FINDKOJOFUNC(TARGET, "AFTER") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "AFTER"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_AFTER"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_AFTER"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "AFTER")%
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	CALL AFTER_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_PALAM
;パラメータ変動をトリガーにした口上
;-------------------------------------------------
@KOJO_MESSAGE_PALAM
IF FINDKOJOFUNC(TARGET, "PALAM") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "PALAM"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_PALAM"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_PALAM"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "PALAM")%
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	CALL PALAM_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_TSHOOT
;触手の射精をトリガーにした口上
;-------------------------------------------------
@KOJO_MESSAGE_TSHOOT
IF FINDKOJOFUNC(TARGET, "TSHOOT") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "TSHOOT"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_TSHOOT"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_TSHOOT"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "TSHOOT")%
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	CALL TSHOOT_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_MARK
;刻印変動をトリガーにした口上
;-------------------------------------------------
@KOJO_MESSAGE_MARK, 取得刻印
#DIMS DYNAMIC 取得刻印
IF FINDKOJOFUNC(TARGET, "MARK") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "MARK"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_MESSAGE_MARK"
		SIF RESULT
			GOTO WIDEDISABLE
		CALL WIDEDISABLE, "ボールギャグ時口上表示", TARGET, "KOJO_MESSAGE_MARK"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	JUMPFORM %FINDKOJOFUNC(TARGET, "MARK")%, 取得刻印
ENDIF

$WIDEDISABLE
SIF OPTION:地の文表示
	TRYCALLFORM MARK_MESSAGE_%取得刻印%

;-------------------------------------------------
;@KOJO_MESSAGE_EVENT
;イベント時の口上、イベント開始時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_EVENT, イベント名, 対象 = -1
#DIM DYNAMIC LTARGET
#DIMS DYNAMIC イベント名
#DIM DYNAMIC 対象
#DIMS DYNAMIC 口上関数
#DIMS DYNAMIC 地の文関数
#DIM DYNAMIC 調教対象口上フラグ = 1

SIF 対象 == -1
	対象 = TARGET
SIF 対象 == -1
	RETURN

;口上呼び出す時のみ、対象をTARGETにする
LTARGET = TARGET
TARGET = 対象

;上記の仕様上JUMPは使えないので工夫
IF FINDKOJOFUNC(TARGET, "EVENT") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "EVENT"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_EVENT"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	CALLFORM %FINDKOJOFUNC(TARGET, "EVENT")%, イベント名
ELSE
	$WIDEDISABLE
	SIF OPTION:地の文表示
		TRYCALLFORM EVENT_MESSAGE_%イベント名%
ENDIF

TARGET = LTARGET
RETURN RESULT

;-------------------------------------------------
;@KOJO_USE_ITEM
;アイテム使用時の口上
;-------------------------------------------------
@KOJO_USE_ITEM, アイテム名, 対象
#DIMS DYNAMIC 地の文関数
#DIM DYNAMIC LTARGET
#DIMS DYNAMIC アイテム名
#DIM DYNAMIC 対象
#DIMS DYNAMIC 口上関数
#DIM DYNAMIC 調教対象口上フラグ = 1

SIF 対象 == -1
	対象 = TARGET
SIF 対象 == -1
	RETURN

;口上呼び出す時のみ、対象をTARGETにする
LTARGET = TARGET
TARGET = 対象

;アイテム使用時の地の文は無い
IF FINDKOJOFUNC(TARGET, "USE_ITEM") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "USE_ITEM"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_USE_ITEM"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	CALLFORM %FINDKOJOFUNC(TARGET, "USE_ITEM")%, アイテム名
ENDIF

$WIDEDISABLE
TARGET = LTARGET

;-------------------------------------------------
;@KOJO_MESSAGE_SPELL
;魔法詠唱時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_SPELL, 詠唱者, 対象:0, 対象:1
#DIM DYNAMIC LTARGET
#DIM DYNAMIC 詠唱者
#DIM DYNAMIC 対象, 2
#DIMS DYNAMIC 詠唱者口上
#DIMS DYNAMIC 対象0口上
#DIMS DYNAMIC 対象1口上

LTARGET = TARGET
詠唱者口上 = %FINDKOJOFUNC(詠唱者, "SPELLCAST")%
対象0口上 = %FINDKOJOFUNC(対象:0, "RECIEVE_SPELLCAST")%
対象1口上 = %FINDKOJOFUNC(対象:1, "RECIEVE_SPELLCAST")%

;WIDEDISABLEの対象なら取得した関数を破棄
CALL WIDEDISABLE, "憑依時口上表示", 詠唱者, "SPELLCAST"
SIF RESULT
	詠唱者口上 = 
CALL WIDEDISABLE, "憑依時口上表示", 対象:0, "RECIEVE_SPELLCAST"
SIF RESULT
	対象0口上 = 
CALL WIDEDISABLE, "憑依時口上表示", 対象:1, "RECIEVE_SPELLCAST"
SIF RESULT
	対象1口上 = 

;優先度は詠唱者の口上→対象の口上→地の文
;ただし地の文は口上側で管理するようになる
TARGET = 詠唱者
TRYCCALLFORM %詠唱者口上%, 詠唱者, 対象:0, 対象:1
CATCH
	TARGET = 対象:0
	TRYCCALLFORM %対象0口上%, 詠唱者, 対象:0, 対象:1
	CATCH
		TARGET = 対象:1
		TRYCCALLFORM %対象1口上%, 詠唱者, 対象:0, 対象:1
		CATCH
			SIF OPTION:地の文表示
				CALL SPELLCAST_地の文, 詠唱者, 対象:0, 対象:1
		ENDCATCH
	ENDCATCH
ENDCATCH

TARGET = LTARGET

;-------------------------------------------------
;@KOJO_MESSAGE_SPELLEND
;魔法の効果が切れた時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_SPELLEND, 魔法名, 対象 = -1
#DIM DYNAMIC LTARGET
#DIMS DYNAMIC 魔法名
#DIM DYNAMIC 対象
#DIMS DYNAMIC 口上関数
#DIM DYNAMIC 調教対象口上フラグ = 1

SIF 対象 == -1
	対象 = TARGET
SIF 対象 == -1
	RETURN

;口上呼び出す時のみ、対象をTARGETにする
LTARGET = TARGET
TARGET = 対象

IF FINDKOJOFUNC(TARGET, @"SPELLEND_%魔法名%") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, @"SPELLEND_%魔法名%"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_SPELLEND_%魔法名%"
		SIF RESULT
			GOTO WIDEDISABLE
	ENDIF
	CALLFORM %FINDKOJOFUNC(TARGET, @"SPELLEND_%魔法名%")%
ENDIF

$WIDEDISABLE
TARGET = LTARGET
RETURN RESULT

;-------------------------------------------------
;@KOJO_MASTER_EVENT
;MASTER口上専用のイベント
;-------------------------------------------------
@KOJO_MASTER_EVENT, イベント名
#DIMS DYNAMIC イベント名

IF OPTION:口上表示 && !CFLAG:MASTER:口上非表示
	TRYCALLFORM KOJO_MASTER_EVENT_%CSVNAMERPS(NOF(MASTER))%, イベント名
ELSEIF OPTION:地の文表示
	TRYCALLFORM EVENT_MASTER_MESSAGE_%イベント名%
ENDIF

;--------------------------------------------------
;@KOJO_START
;調教開始時に呼び出す
;--------------------------------------------------
@KOJO_START
IF FINDKOJOFUNC(TARGET, "START") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "START"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_START"
		SIF RESULT
			RETURN
	ENDIF
	CALLFORM %FINDKOJOFUNC(TARGET, "START")%
ENDIF

;--------------------------------------------------
;@KOJO_TURNEND
;調教終了時に呼び出す
;--------------------------------------------------
@KOJO_TURNEND
IF FINDKOJOFUNC(TARGET, "TURNEND") != ""
	IF SUBSTRINGU(FINDKOJOFUNC(TARGET, "TURNEND"), 0, 6) != "MASTER"
		;WIDEDISABLEの判定
		CALL WIDEDISABLE, "憑依時口上表示", TARGET, "KOJO_TURNEND"
		SIF RESULT
			RETURN
	ENDIF
	CALLFORM %FINDKOJOFUNC(TARGET, "TURNEND")%
ENDIF

;-------------------------------------------------
;@KOJO_MESSAGE_ENDING
;エンディング口上
;-------------------------------------------------
@KOJO_MESSAGE_ENDING, エンディング名
#DIMS DYNAMIC エンディング名

SIF OPTION:口上表示 && !CFLAG:口上非表示
	TRYCALLFORM KOJO_MESSAGE_ENDING_%CSVNAMERPS(NOF(TARGET))%, エンディング名

;-------------------------------------------------
;@KOJO_STRATEGY
;経営用口上の管理用関数 パートナー限定口上やその他挙動が特殊なものはこの関数を通さず直接呼ぶ
;-------------------------------------------------
@KOJO_STRATEGY, 対象, 施設名, イベント名, 付帯情報:0, 付帯情報:1, 付帯情報:2
#DIM DYNAMIC 対象
#DIMS DYNAMIC 施設名
#DIMS DYNAMIC イベント名
#DIM DYNAMIC LTARGET
#DIM DYNAMIC 付帯情報, 3

SIF !OPTION:口上表示 || CFLAG:対象:口上非表示
	RETURN

LTARGET = TARGET
TARGET = 対象
RESULT = 0

;付帯情報が発生する口上を分ける
IF (GROUPMATCH(施設名, "ゲームセンター", "飲食店") && イベント名 == "ASSIGNLEADER") || (施設名 == "ゲームセンター" && イベント名 == "TURNEND")
	;ASSIGNLEADERは0に現店長の番号、1に元店長の番号 ゲーセンのTURNENDは収入と支出
	TRYCALLFORM %FINDKOJOFUNC(TARGET, @"%施設名%_%イベント名%", 1)%, 付帯情報:0, 付帯情報:1
ELSEIF 施設名 == "飲食店" && イベント名 == "TURNEND"
	;飲食店のTURNENDは収入支出に加えて来客数が入る
	TRYCALLFORM %FINDKOJOFUNC(TARGET, @"%施設名%_%イベント名%", 1)%, 付帯情報:0, 付帯情報:1, 付帯情報:2
ELSE
	TRYCALLFORM %FINDKOJOFUNC(TARGET, @"%施設名%_%イベント名%", 1)%
ENDIF

TARGET = LTARGET
RETURN RESULT

;-------------------------------------------------
;@DISABLE
;コマンドやイベントを弾く関数
;-------------------------------------------------
@DISABLE, 対象項目, 対象 = -1
#DIM DYNAMIC CSV番号フラグ = 0
#DIMS DYNAMIC 対象項目
#DIM DYNAMIC 対象

RESULT = 0
SIF GROUPMATCH(対象項目, "EVENT_クローン生成", "EVENT_体外受精保管精液")
	CSV番号フラグ = 1
SIF 対象 == -1 && !CSV番号フラグ
	RETURN 0

IF OPTION:口上表示
	IF CSV番号フラグ
		GETCHARA 対象
		IF RESULT >= 0
			対象 = RESULT
			RESULT = 0
			GOTO 登録番号判定
		ENDIF
		TRYCALLFORM DISABLE_%対象項目%_%CSVNAMERPS(対象)%
	ELSE
		$登録番号判定
		IF !CFLAG:対象:口上非表示
			IF NOF(対象) > 10000
				TRYCALLLIST
					FUNC DISABLE_%対象項目%_CLONE_%CSVNAMERPS(CFLAG:対象:複製)%
					FUNC DISABLE_%対象項目%_%CSVNAMERPS(CFLAG:対象:血縁コード/100000)%×%CSVNAMERPS(CFLAG:対象:血縁コード%100000)%
				ENDFUNC
			ELSE
				TRYCALLFORM DISABLE_%対象項目%_%CSVNAMERPS(NOF(対象))%
			ENDIF
		ENDIF
	ENDIF
ENDIF

RETURN RESULT

;-------------------------------------------------
;@DISABLECOM
;コマンド専用 返り値で判定可能
;-------------------------------------------------
@DISABLECOM, 対象 = -1, コマンド名
#DIM DYNAMIC 対象
#DIMS DYNAMIC コマンド名

RESULT = 0
SIF 対象 == -1
	RETURN 0

IF OPTION:口上表示
	IF !CFLAG:対象:口上非表示
		IF NOF(対象) > 10000
			TRYCALLLIST
				FUNC DISABLECOM_CLONE_%CSVNAMERPS(CFLAG:対象:複製)%, コマンド名
				FUNC DISABLECOM_%CSVNAMERPS(CFLAG:対象:血縁コード/100000)%×%CSVNAMERPS(CFLAG:対象:血縁コード%100000)%, コマンド名
			ENDFUNC
		ELSE
			TRYCALLFORM DISABLECOM_%CSVNAMERPS(NOF(対象))%, コマンド名
		ENDIF
	ENDIF
ENDIF

RETURN RESULT

;-------------------------------------------------
;@WIDEDISABLE
;影響範囲が大きいものはこの関数で処理
;-------------------------------------------------
@WIDEDISABLE, 対象項目, 対象 = -1, 対象関数
#DIMS DYNAMIC 対象項目
#DIM DYNAMIC 対象
#DIMS DYNAMIC 対象関数

RESULT = 0
SIF 対象 == -1
	RETURN 0

SELECTCASE 対象項目
	CASE "憑依時口上表示"
		SIF CFLAG:対象:憑依 == -1
			RETURN 0
	CASE "ボールギャグ時口上表示"
		SIF !TEQUIP:対象:ボールギャグ || SELECTCOMS == "ボールギャグ"
			RETURN 0
ENDSELECT

IF OPTION:口上表示
	IF !CFLAG:対象:口上非表示
		IF NOF(対象) > 10000
			TRYCALLLIST
				FUNC WIDEDISABLE_%対象項目%_CLONE_%CSVNAMERPS(CFLAG:対象:複製)%, 対象関数
				FUNC WIDEDISABLE_%対象項目%_%CSVNAMERPS(CFLAG:対象:血縁コード/100000)%×%CSVNAMERPS(CFLAG:対象:血縁コード%100000)%, 対象関数
			ENDFUNC
		ELSE
			TRYCALLFORM WIDEDISABLE_%対象項目%_%CSVNAMERPS(NOF(対象))%, 対象関数
		ENDIF
	ENDIF
ENDIF

RETURN RESULT

;;;CSVNAMERPS(RePlace Space) 名前の空白をハイフンに置換して返す
@CSVNAMERPS, 対象
#FUNCTIONS
#DIM DYNAMIC 対象

RETURNF REPLACE(CSVNAMEF(対象), (" |-|\\*"), "_")
