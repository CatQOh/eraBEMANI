;----------------------------------------------------------------------
;汎用喘ぎ別人版ERB
;ver0.02(10/11/28)

;>>118さん版または別人版0.01verでは「PANT_E |= 2とするとみさくら語が表示されなくなる」と説明していましたが、実際にはPANT_E |= 2とするとみさくら語が表示されるようになってました
;以前のバージョンを使っている方でみさくら語を禁止したい方はPANT_E |= 2を消してください。
;絶叫系喘ぎとみさくら語を禁止してた方はPANT_E |= 3をPANT_E |= 1にしてください。
;----------------------------------------------------------------------
;をeraBEMANI用に改造。マジックナンバーを減らし、加筆
;----------------------------------------------------------------------
;変数初期化
;----------------------------------------------------------------------
@汎用喘ぎ口上
#DIMS DYNAMIC 分岐先
#DIM DYNAMIC 感度計算用
#DIM DYNAMIC 頻度計算用

SIF !汎用喘ぎ:口調
	RETURN

汎用喘ぎ:頻度 = 0
汎用喘ぎ:感度 = 0
汎用喘ぎ:分岐 = 0
頻度計算用 = 0
感度計算用 = 0

;----------------------------------------------------------------------
;素質によって喘ぐ回数と感度にプラスマイナス。
;上がり下がりは自分の想像です。
;感度や苦痛に関わる素質は最後に。
;----------------------------------------------------------------------
;男である
SIF SEX(TARGET) == 1
	汎用喘ぎ:分岐 |= 喘ぎBIT:男

;反抗的
IF TALENT:反抗的
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度 --
ENDIF

;気丈
SIF TALENT:気丈
	汎用喘ぎ:頻度--

;素直
SIF TALENT:素直
	汎用喘ぎ:頻度++

;プライド高い
IF TALENT:プライド高い
	汎用喘ぎ:頻度 -= 2
	汎用喘ぎ:感度--
ENDIF

;プライド低い
SIF TALENT:プライド低い
	汎用喘ぎ:頻度++

;ツンデレは従順4以上なら汎用喘ぎ:頻度＋以下なら汎用喘ぎ:頻度－
IF TALENT:ツンデレ
	IF ABL:従順 >= 4
		汎用喘ぎ:頻度++
	ELSE
		汎用喘ぎ:頻度--
	ENDIF
ENDIF

;自制心
IF TALENT:自制心
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度 -= 2
ENDIF

;無関心
IF TALENT:無関心
	汎用喘ぎ:頻度 -= 2
	汎用喘ぎ:感度 -= 2
ENDIF

;感情乏しい
IF TALENT:感情乏しい
	汎用喘ぎ:頻度 -= 3
	汎用喘ぎ:感度--
ENDIF

;保守的
SIF TALENT:保守的
	汎用喘ぎ:頻度--

;一線越えないは陥落前なら汎用喘ぎ:頻度 汎用喘ぎ:感度-
IF TALENT:一線越えない && 陥落(TARGET)
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度 -= 2
ENDIF

;貞操観念
IF TALENT:貞操観念
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度 -= 2
ENDIF

;抑圧
SIF TALENT:抑圧
	汎用喘ぎ:感度--

;解放
IF TALENT:解放
	汎用喘ぎ:頻度 += 2
	汎用喘ぎ:感度++
ENDIF

;抵抗
SIF TALENT:抵抗
	汎用喘ぎ:感度--

;恥じらい
SIF TALENT:恥じらい
	汎用喘ぎ:頻度--

;恥薄い
SIF TALENT:恥薄い
	汎用喘ぎ:頻度++

;自慰しやすいまたは淫核は自慰系COMで
IF TALENT:自慰しやすい || TALENT:淫核
	SELECTCASE SELECTCOMS
		CASE "自慰", "胸自慰", "秘貝開帳", "オナフェラ", "騎乗乳房自慰"
			汎用喘ぎ:感度 += 1
	ENDSELECT
ENDIF

;快感に素直
IF TALENT:快感に素直
	汎用喘ぎ:頻度 += 2
	汎用喘ぎ:感度 += 2
ENDIF

;快感の否定
IF TALENT:快感の否定
	汎用喘ぎ:頻度 -= 2
	汎用喘ぎ:感度 -= 2
ENDIF

;回数異常阻止
;真面目で大人しい性格だと感度上がってもどうしようもないので。
SIF 汎用喘ぎ:頻度 < 1
	汎用喘ぎ:頻度 = 1
SIF 汎用喘ぎ:感度 < -2
	汎用喘ぎ:感度 = -2
;快感が入ってなくても喘ぎだすので素質補正は２まで
SIF 汎用喘ぎ:感度 > 2
	汎用喘ぎ:感度 = 2

;----------------------------------------------------------------------
;行為箇所＋感度計算
;----------------------------------------------------------------------
;感覚が高いと汎用喘ぎ:頻度 汎用喘ぎ:感度にプラス。
;またこの時点でどこを使った行為か汎用喘ぎ:分岐に付ける
;泡踊りってなんだろう…。
;----------------------------------------------------------------------
;B
IF コマンド属性:SELECTCOM & 喘ぎBIT:Ｂ
;SELECTCASE SELECTCOMS
;	{
;	CASE "愛撫", "胸揉み", "乳首責め", "胸愛撫", "乳首舐め", "ニプルキャップ", 
;		 "乳首ローター", "搾乳機", "乳房電極", "パイズリ", "パイズリフェラ", "縦パイズリ", 
;		 "乳ファック", "乳の揉み合い", "触手乳首責め", "触手搾乳", "授乳手コキ",
;	}
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｂ
	;感覚低い～高いで0～3
	SELECTCASE ABL:Ｂ感覚
		CASE IS >= 7
			頻度計算用 += 2
			感度計算用 += 3
		CASE IS >= 5
			頻度計算用++
			感度計算用 += 2
		CASE IS >= 3
			感度計算用++
		CASEELSE
			感度計算用--
	ENDSELECT
	;調教者の技巧により
	SELECTCASE ABL:PLAYER:技巧
		CASE IS >= 7
			感度計算用 += 2
		CASE IS >= 5
			感度計算用++
		CASE IS < 3
			頻度計算用--
	ENDSELECT
ENDIF
;アイテムがついているときは最大+1
IF Ｂ使用中(TARGET)
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｂ
	SELECTCASE ABL:Ｂ感覚
		CASE IS >= 5
			頻度計算用 += 2
		CASE IS >= 3
			頻度計算用++
	ENDSELECT
ENDIF

;C
IF コマンド属性:SELECTCOM & 喘ぎBIT:Ｃ
;SELECTCASE SELECTCOMS
;	{
;	CASE "愛撫", "クンニ", "フェラする", "足扱きする", "下着扱きする", "手淫する",
;		 "素股する", "パイズリする", "パイズリフェラする", "射精寸止め", "お掃除フェラする", 
;
;	CASE 0,3,4,20 TO 40,60 TO 62,76,145,163,182,240 TO 242,244 TO 246,260 TO 265,280,285,317,318,322,342,343,365,373,374,380 TO 382,400,401,422,430,431,503,504,505,515,519 TO 525
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｃ
	;感覚低い～高いで0～3
	SELECTCASE ABL:Ｃ感覚
		CASE IS >= 7
			頻度計算用 += 2
			感度計算用 += 3
		CASE IS >= 5
			頻度計算用++
			感度計算用 += 2
		CASE IS >= 3
			感度計算用++
		CASEELSE
			頻度計算用++
	ENDSELECT
	;調教者の技巧により
	SELECTCASE ABL:PLAYER:技巧
		CASE IS >= 7
			感度計算用 += 2
		CASE IS >= 5
			感度計算用++
		CASE IS < 3
			感度計算用--
	ENDSELECT
ENDIF
;アイテムがついているときは最大+1
IF Ｐ使用中(TARGET) || TEQUIP:クリキャップ
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｃ
	SELECTCASE ABL:Ｃ感覚
		CASE IS >= 5
			頻度計算用 += 2
		CASE IS >= 3
			感度計算用++
	ENDSELECT
ENDIF

;A
IF コマンド属性:SELECTCOM & 喘ぎBIT:Ａ || GROUPMATCH(2, TFLAG:主人挿入箇所, TFLAG:助手挿入箇所)
;SELECTCASE SELECTCOMS
;	{
;	CASE "アナル舐め", "アナル愛撫", "アナルバイブ", "アナルビーズ", "浣腸器", "空気浣腸", "拡張バルーン", "アナル電極",
;			"ローション注入", "正常位アナル", "後背位アナル", "騎乗位アナル", "対面座位アナル", "背面座位アナル", "下着ずらし挿入アナル",
;			"アナルフィスト", "両穴フィスト", "異物挿入", "アナル触手", "触手浣腸", "アナルを犯す（ドラゴン）", "ペットに犯させる",
;			"腸洗浄", "電動エネマグラ", "アナル双頭ディルド", 
;			"前立腺刺激", "騎乗乳房自慰",
;			"正常位アナルキス", "騎乗位アナルキス", "対面座位アナルキス", "後背位アナル手淫", "騎乗位アナル手淫",
;			"対面座位吸血", "背面座位吸血", "スライム姦",
;			"3P", "数珠繋ぎ", 
;			"正常位アナル首絞め", "後背位アナル首絞め", "アナル二輪挿し"
;	}
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ａ
	;感覚低い～高いで0～3
	SELECTCASE ABL:Ａ感覚
		CASE IS >= 7
			頻度計算用 += 2
			感度計算用 += 3
		CASE IS >= 5
			頻度計算用++
			感度計算用 += 2
		CASE IS >= 3
			感度計算用++
		CASEELSE
			頻度計算用 -= 1
			;潤滑により苦痛１or苦痛２
			IF PALAM:潤滑 < PALAMLV:2
				汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛2
			ELSE
				汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1
			ENDIF
			汎用喘ぎ:分岐 |= 喘ぎBIT:強引
	ENDSELECT
	;調教者の技巧により
	SELECTCASE ABL:PLAYER:技巧
		CASE IS >= 7
			感度計算用 += 2
		CASE IS >= 5
			感度計算用 += 1
		CASE IS < 3
			感度計算用 -= 1
	ENDSELECT
ENDIF
;アイテムがついているときは最大+1
IF Ａ使用中(TARGET)
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ａ
	SIF ABL:Ａ感覚 >= 5
		頻度計算用++
	IF ABL:Ａ感覚 >= 3
		感度計算用++
	;慣れてない＆濡れてないと痛い
	ELSEIF PALAM:潤滑 < PALAMLV:2
		汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1
		汎用喘ぎ:分岐 |= 喘ぎBIT:強引
	ENDIF
ENDIF

;V
IF コマンド属性:SELECTCOM & 喘ぎBIT:Ｖ || GROUPMATCH(1, TFLAG:主人挿入箇所, TFLAG:助手挿入箇所)
;		SELECTCASE SELECTCOMS
;			{
;			CASE "指挿入れ", "バイブ", "正常位", "後背位", "騎乗位", "対面座位", "背面座位", "下着ずらし挿入", "フィストファック",
;				 "両穴フィスト", "異物挿入", "触手挿入", "膣を犯す（ドラゴン）", "ペットに犯させる",
;				 "双頭ディルド",
;				 "Gスポット刺激", "乱れ牡丹", "騎乗乳房自慰", "挿入Gスポ責め", "挿入子宮口責め",
;				 "正常位キス", "騎乗位キス", "対面座位キス", "後背位手淫", "騎乗位手淫", 
;				 "対面座位吸血", "背面座位吸血", "スライム姦",
;				 "3P", "数珠繋ぎ",
;				 "正常位首絞め", "後背位首絞め", "二輪挿し"
;			}
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｖ
	;感覚低い～高いで0～3
	SELECTCASE ABL:Ｖ感覚
		CASE IS >= 7
			頻度計算用 += 2
			感度計算用 += 3
		CASE IS >= 5
			頻度計算用++
			感度計算用 += 2
		CASE IS >= 3
			感度計算用++
		CASEELSE
			;慣れてない＆濡れてなかったら苦痛
			頻度計算用 -= 1
			IF PALAM:潤滑 < PALAMLV:2
				汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1
				汎用喘ぎ:分岐 |= 喘ぎBIT:強引
			ENDIF
	ENDSELECT
	;調教者の技巧により
	SELECTCASE ABL:PLAYER:技巧
		CASE IS >= 7
			感度計算用 += 2
		CASE IS >= 5
			感度計算用 += 1
		CASE IS < 3
			感度計算用 -= 1
	ENDSELECT
ENDIF
IF Ｖ使用中(TARGET)
	汎用喘ぎ:分岐 |= 喘ぎBIT:Ｖ
	SIF ABL:Ｖ感覚 >= 5
		頻度計算用++
	IF ABL:Ｖ感覚 >= 3
		感度計算用++
	;慣れてない＆濡れてないと痛い
	ELSEIF PALAM:潤滑 < PALAMLV:2
		汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1
		汎用喘ぎ:分岐 |= 喘ぎBIT:強引
	ENDIF
ENDIF

;２か所以上責めるコマンドは、行為箇所による感度を半分にする
[SKIPSTART]
SELECTCASE SELECTCOM
	CASE 0,7,162,163,317,318,322,368,373,374,381,382,504,509,515,519
		;愛撫のとき、貞操帯がついてるとＢしか触らない
		IF SELECTCOM == 0 && TEQUIP:貞操帯
		ELSE
			頻度計算用 = 頻度計算用 / 2
			感度計算用 = 感度計算用 / 2
		ENDIF
ENDSELECT
[SKIPEND]

汎用喘ぎ:頻度 += 頻度計算用
汎用喘ぎ:感度 += 感度計算用

;口
SIF GROUPMATCH(3, TFLAG:主人挿入箇所, TFLAG:助手挿入箇所)
	GOTO 口
SELECTCASE SELECTCOMS
	{
	CASE "フェラチオ", "イラマチオ", "パイズリフェラ", "クンニさせる", "顔面騎乗する", "アナルを舐めさせる",
		 "ダブルフェラ", "フェラさせる（ドラゴン）", "シックスナイン", "オナフェラ", "二本フェラ"
	}
		$口
		汎用喘ぎ:分岐 |= 喘ぎBIT:口
ENDSELECT

;苦痛
SIF コマンド属性:SELECTCOM & 喘ぎBIT:苦痛1
	汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1

;ここまで来て分岐も属性も無ければRETURN
SIF !汎用喘ぎ:分岐 && !コマンド属性:SELECTCOM
	RETURN

[SKIPSTART]
;継続アイテムによる苦痛
;重度
IF TEQUIP:拡張バルーン || TEQUIP:尿道バルーン || BASE:尿意 >= 90 || BASE:便意 >= 90 || TEQUIP:三角木馬
	汎用喘ぎ:分岐 |= 23
;軽度
ELSEIF TEQUIP:縄 || TEQUIP:カテーテル || TEQUIP:触手膣内産卵 || TEQUIP:触手腸内産卵 || TEQUIP:触手乳腺挿入 || BASE:尿意 >= 60 || BASE:便意 >= 60 || BASE:吐き気 >= 60
	汎用喘ぎ:分岐 |= 22
ENDIF
[SKIPEND]

;----------------------------------------------------------------------
;陥落資質
;----------------------------------------------------------------------
;恋慕
IF TALENT:恋慕
	汎用喘ぎ:頻度++
	;汎用喘ぎ:分岐 |= 喘ぎBIT:恋慕
ENDIF
;親愛
IF TALENT:親愛
	汎用喘ぎ:頻度++
	;汎用喘ぎ:分岐 |= 喘ぎBIT:親愛
ENDIF
;淫乱
IF TALENT:淫乱
	汎用喘ぎ:頻度 += 2
	汎用喘ぎ:感度++
	;汎用喘ぎ:分岐 |= 喘ぎBIT:淫乱
ENDIF
;娼婦
IF TALENT:娼婦
	汎用喘ぎ:頻度 += 2
	汎用喘ぎ:感度 += 2
	;汎用喘ぎ:分岐 |= 喘ぎBIT:娼婦
ENDIF
;服従
IF TALENT:服従
	汎用喘ぎ:頻度--
	;汎用喘ぎ:分岐 |= 喘ぎBIT:服従
ENDIF
;隷属
IF TALENT:隷属
	汎用喘ぎ:頻度--
	;汎用喘ぎ:分岐 |= 喘ぎBIT:隷属
ENDIF
;反発
IF MARK:反発刻印 && !陥落(TARGET)
	;認めたくないってことで汎用喘ぎ:感度 - 
	汎用喘ぎ:頻度 -= 2
	汎用喘ぎ:感度 -= 2
	SELECTCASE MARK:反発刻印
		;反発Lv1
		CASE 1
			汎用喘ぎ:分岐 |= 喘ぎBIT:反発1
		;反発Lv2
		CASE 2
			汎用喘ぎ:分岐 |= 喘ぎBIT:反発2
		;反発Lv3
		CASE 3
			汎用喘ぎ:分岐 |= 喘ぎBIT:反発3
	ENDSELECT
ENDIF
;屈服
SELECTCASE MARK:屈服刻印
	;屈服Lv1
	CASE 1
		汎用喘ぎ:分岐 |= 4
	;屈服Lv2
	CASE 2
		汎用喘ぎ:分岐 |= 5
	;屈服Lv3
	CASE 3
		汎用喘ぎ:分岐 |= 6
ENDSELECT
;崩壊
SIF TALENT:崩壊
	汎用喘ぎ:分岐 |= 喘ぎBIT:崩壊

;----------------------------------------------------------------------
;素質その２
;----------------------------------------------------------------------
;淫核
IF TALENT:淫核 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
;淫膣
IF TALENT:淫膣 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
;淫尻
IF TALENT:淫尻 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
;淫乳
IF TALENT:淫乳 == 1 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF

;CVAB鈍感で-
IF TALENT:Ｃ鈍感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度--
ENDIF
IF TALENT:Ｖ鈍感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度--
ENDIF
IF TALENT:Ａ鈍感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度--
ENDIF
IF TALENT:Ｂ鈍感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
	汎用喘ぎ:頻度--
	汎用喘ぎ:感度--
ENDIF

;CVAB敏感で+
IF TALENT:Ｃ敏感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
IF TALENT:Ｖ敏感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
IF TALENT:Ａ敏感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF
IF TALENT:Ｂ敏感 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
	汎用喘ぎ:頻度++
	汎用喘ぎ:感度++
ENDIF

;胸分岐のときバストサイズで計算
IF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
	SELECTCASE TALENT:バストサイズ
		CASE 1
			汎用喘ぎ:感度 += 2
		CASE 2
			汎用喘ぎ:感度++
		CASE 4
			汎用喘ぎ:感度 -= 1
		CASE IS >= 5
			汎用喘ぎ:感度 -= 2
	ENDSELECT
	;母乳体質なら+
	SIF TALENT:母乳体質
		汎用喘ぎ:感度++
ENDIF

;----------------------------------------------------------------------
;行為軽度＋苦痛計算
;行為が激しいと汎用喘ぎ:頻度 PANT_Fにプラス
;----------------------------------------------------------------------
;乳ファック、泡踊りってどこだろう……。
;奉仕系は奴隷側がするだけのものは入ってません。
[SKIPSTART]
;軽い
SELECTCASE SELECTCOM
	CASE 0,1,3 TO 6,8,10,11,18,30,31,34,36,40,50,73 TO 75,87,222,224,240,241,243 TO 245,317,318,400 TO 402,502 TO 505,528
ENDSELECT

;普通
SELECTCASE SELECTCOM
	CASE 12 TO 15,20 TO 23,32,41,42,50,51,76,80,84,85,130,140,141,242,246,380,403,404,422,500,501
		汎用喘ぎ:頻度 += 1
		汎用喘ぎ:感度 += 1
		;苦痛なら
		SIF ((SELECTCOM == 14 || SELECTCOM == 23) && (TEQUIP:地下室 || TALENT:PLAYER:サド || ABL:PLAYER:サドっ気 >= 3)) || SELECTCOM == 15 || SELECTCOM == 24 || SELECTCOM == 130 || SELECTCOM == 140 || SELECTCOM == 141
			汎用喘ぎ:分岐 |= 22
ENDSELECT

;重い＋本番
SELECTCASE SELECTCOM
	CASE 7,17,33,43,53,54,60,72,100 TO 116,142 TO 145,155,157,158,322,341 TO 345,360 TO 365,405,406,506 TO 512,515,519 TO 521,526
		汎用喘ぎ:頻度 += 2
		汎用喘ぎ:感度 += 2
		;苦痛なら
		SIF SELECTCOM == 33 || SELECTCOM == 60 || SELECTCOM == 72 || SELECTCOM == 158 || SELECTCOM == 361 || SELECTCOM == 365 || SELECTCOM == 511 || SELECTCOM == 512
			汎用喘ぎ:分岐 |= 22
		;苦痛LV2
		SIF SELECTCOM == 142 || SELECTCOM == 143 || SELECTCOM == 144 || SELECTCOM == 145 || SELECTCOM == 157
			汎用喘ぎ:分岐 |= 23
		;本番なら汎用喘ぎ:頻度 + 1
		SELECTCASE SELECTCOM
			CASE 100 TO 116,405,406,506 TO 512,515,519 TO 521,526
				汎用喘ぎ:頻度 += 1
				汎用喘ぎ:分岐 |= 19
		ENDSELECT
ENDSELECT

;通常じゃやらないの
SELECTCASE SELECTCOM
	CASE 16,25,35,44,55,56,61,62,153,154,156,160 TO 163,180,181,352,366 TO 374,381 TO 383,390 TO 392,513,514,517,518,522 TO 525,527
		汎用喘ぎ:頻度 += 3
		汎用喘ぎ:分岐 |= 23
		;本番なら汎用喘ぎ:頻度 + 1
		IF SELECTCOM == 181 || SELECTCOM == 513 || SELECTCOM == 514 || SELECTCOM == 517 || SELECTCOM == 518 || SELECTCOM == 522 || SELECTCOM == 523 || SELECTCOM == 524 || SELECTCOM == 525
			汎用喘ぎ:頻度 = 汎用喘ぎ:頻度 + 1
			汎用喘ぎ:分岐 |= 19
		ENDIF
ENDSELECT

;奉仕系（イラマチオのような強制系は含まない）
SELECTCASE SELECTCOM
	CASE 70 TO 87,220 TO 225,243,317,420 TO 422,503,504,528
		汎用喘ぎ:分岐 |= 24
	;本番あり
	CASE 260 TO 265
		SETBIT 汎用喘ぎ:分岐,19
		SETBIT 汎用喘ぎ:分岐,24
ENDSELECT

;触手系
SELECTCASE SELECTCOM
	CASE 340 TO 374
		汎用喘ぎ:分岐 |= 20
ENDSELECT

;道具系
SELECTCASE SELECTCOM
	CASE 12,13,15 TO 17,20 TO 22,24,25,32,42 TO 44,52 TO 56,60 TO 62,130 TO 134,142 TO 145,180,300 TO 305
		汎用喘ぎ:分岐 |= 21
ENDSELECT
[SKIPEND]

;----------------------------------------------------------------------
;素質その３
;----------------------------------------------------------------------
;苦痛分岐
IF 汎用喘ぎ:分岐 & (喘ぎBIT:苦痛1 | 喘ぎBIT:苦痛2)
	;臆病は苦痛があるとき汎用喘ぎ:頻度 + 1（ぎゃあぎゃあ喚くイメージ）
	SIF TALENT:臆病
		汎用喘ぎ:頻度++
	;痛みに弱い
	IF TALENT:痛みに弱い
		汎用喘ぎ:頻度++
		汎用喘ぎ:感度--
	ENDIF
	;痛みに強い
	IF TALENT:痛みに強い
		汎用喘ぎ:頻度 -= 1
		IF 汎用喘ぎ:分岐 & 喘ぎBIT:苦痛2
			汎用喘ぎ:分岐 &= 喘ぎBIT:苦痛2
			汎用喘ぎ:分岐 |= 喘ぎBIT:苦痛1
		ENDIF
	ENDIF
	;サド
	SIF TALENT:サド
		汎用喘ぎ:感度--
	;マゾ
	IF TALENT:マゾ
		汎用喘ぎ:感度++
		SIF 汎用喘ぎ:分岐 & 喘ぎBIT:苦痛2
			汎用喘ぎ:感度++
		汎用喘ぎ:頻度++
	ENDIF
ENDIF

;----------------------------------------------------------------------
;喘ぐよ！
;----------------------------------------------------------------------
;喘ぐ前に回数に異常ないかチェック
;現状10回以上は繰り返さない。
汎用喘ぎ:頻度 = LIMIT(汎用喘ぎ:頻度, 1, 10)

;異常防止
SIF 汎用喘ぎ:感度 < 0
	汎用喘ぎ:感度 = 0


;苦痛LV2と快感LV3,4が喚きすぎるので-処理
SIF 汎用喘ぎ:感度 >= 4 || 汎用喘ぎ:分岐 & 喘ぎBIT:苦痛2
	汎用喘ぎ:頻度 -= 4

;頻度指定してればそれで
SIF 汎用喘ぎ:頻度指定
	汎用喘ぎ:頻度 = 汎用喘ぎ:頻度指定

;種類指定してればそれで
IF 汎用喘ぎ:種類指定
	分岐先 = %汎用喘ぎ_種類:(汎用喘ぎ:種類指定)%
ELSE
	;崩壊なら問答無用で快感LV1へ
	IF 汎用喘ぎ:分岐 & 喘ぎBIT:崩壊
		汎用喘ぎ:感度 = 0
		分岐先 = 快感1
	;口が塞がってる(ボールギャグ・触手口虐中以外)なら飛ぶ
	ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:口
		分岐先 = 口使用中
	;苦痛LV2
	ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:苦痛2
		;絶叫禁止でLV1へ
		IF 汎用喘ぎ:絶叫禁止
			分岐先 = 苦痛1
		ELSE
			;絶叫系は６回まで
			SIF 汎用喘ぎ:頻度 > 6
				汎用喘ぎ:頻度 = 6
			分岐先 = 苦痛2
		ENDIF
	;苦痛LV1
	ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:苦痛1
		分岐先 = 苦痛1
	ELSE
		;快感はLV1～LV4で分ける
		SELECTCASE 汎用喘ぎ:感度
			;快感LV4
			CASE IS >= 8
				;絶叫禁止でLV3へ
				IF 汎用喘ぎ:絶叫禁止
					分岐先 = 快感3
				ELSE
					;絶叫系は６回まで
					SIF 汎用喘ぎ:頻度 > 6
						汎用喘ぎ:頻度 = 6
					分岐先 = 快感4
				ENDIF
			;快感LV3
			CASE IS >= 5
				分岐先 = 快感3
			;快感LV2
			CASE IS >= 3
				分岐先 = 快感2
			;快感LV1
			CASEELSE
				分岐先 = 快感1
		ENDSELECT
	ENDIF
ENDIF

SIF OPTION:口上表示 && !EXISTFUNCTION(@"EXIST_KOJO_%CSVNAMERPS(NOF(TARGET))%")
	CALLFORM PANT_VOICE_%分岐先%

;連続禁止用処理
@喘ぎ乱数, ARG
#FUNCTION
#DIM DYNAMIC NOW
#DIM PREV, 5

NOW = RAND:ARG

SIF MATCH(PREV, NOW)
	RESTART

ARRAYSHIFT PREV, 1, 0
PREV:0 = NOW

RETURNF NOW

;----------------------------------------------------------------------
;快感1
;----------------------------------------------------------------------
@PANT_VOICE_快感1
PRINT 「
REPEAT 汎用喘ぎ:頻度
	SELECTCASE 喘ぎ乱数(10)
		CASE 0
			PRINT ……
		CASE 1
			PRINT …ん、
		CASE 2
			PRINT あ…
		CASE 3
			PRINT んっ、
		CASE 4
			PRINT ……ふぁ、
		CASE 5
			PRINT んん…、
		CASE 6
			PRINT んぁ…、
		CASE 7
			PRINT …っ……
		CASE 8
			PRINT ……ぁ…、
		CASE 9
			;ボールギャグか触手口虐中
			IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
				PRINT ん…
			;反発刻印１以上
			ELSEIF MARK:反発刻印 && !陥落(TARGET)
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "標準"
						PRINT やめ…っ、
					CASE "乱暴"
						PRINT やめろっ！
					CASE "柔らかめ"
						PRINT ヤダ…っ、
					CASE "敬語"
						PRINT 触らないでください…
					CASE "幼い"
						PRINT やぁだぁ…
					CASE "老人"
						PRINT 嫌じゃ…
					CASE "お嬢様"
						PRINT いけませんわ…
					CASE "レイシス語"
						PRINT だめデス…
					CASE "メスガキ"
						PRINT 触んないでよ！
					CASE "お姉さん"
						PRINT だめよ…
					CASE "土佐弁"
						PRINT 離しとぉせ…
					CASE "戦士"
						PRINT 離せ！
					CASE "関西弁"
						PRINT 触んなや…
					CASE "博多弁"
						PRINT やめてくれんね！
				ENDSELECT
			;淫乱か娼婦
			ELSEIF FALLTYPE(TARGET) == 2
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "標準"
						PRINT そこ…、じゃなくて…っ、
					CASE "柔らかめ", "敬語"
						PRINT もっと…ぁ、
					CASE "幼い"
						PRINT もっとしてぇ…
					CASE "老人"
						PRINT よいぞ…
					CASE "お嬢様"
						PRINT お上手ですわ…
					CASE "レイシス語"
						PRINT はうぅ…
					CASE "メスガキ"
						PRINT 下手くそ♡
					CASE "お姉さん"
						PRINT 上手ね…
					CASE "土佐弁"
						PRINT …そこや、ないきに……、
					CASE "戦士"
						PRINT ……違う…、
					CASE "関西弁"
						PRINT しょこや、なくて…っ
					CASE "博多弁"
						PRINT しょこつちごうとる…
				ENDSELECT
			ELSE
				PRINT …は……、
			ENDIF
	ENDSELECT
REND
;締め
SELECTCASE RAND:3
	CASE 0
		PRINT …っ
	CASE 1
		PRINT んっ
	CASE 2
		PRINT …ぁ
ENDSELECT
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;快感LV2
;----------------------------------------------------------------------
@PANT_VOICE_快感2
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;ボールギャグか触手口虐中
	IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT んんっ…
			CASE 1
				PRINT ふぅっ…
			CASE 2
				PRINT っ…、
			CASE 3
				PRINT ぅあ…
			CASE 4
				PRINT ふっ…ぅ、
			CASE 5
				PRINT んっ、
			CASE 6
				PRINT あ…っ、
			CASE 7
				PRINT くぅ…
			CASE 8
				PRINT ふっ…あっ、
			CASE 9
				PRINT ぁあ…
		ENDSELECT
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ふっ…
			CASE 1
				PRINT んあっ、
			CASE 2
				PRINT んんっ…
			CASE 3
				;淫語モード
				IF 汎用喘ぎ:淫語
					;胸弄られてる
					IF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
						PRINT 胸いい…っ
					;ペニス持ちでＣを弄られてる
					ELSEIF PENIS(TARGET) && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						PRINT おちんぽどくどくする…
					;ペニスなしでＣを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						PRINT もっとそこくりくりし
						SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
							CASE "乱暴", "戦士", "老人"
								PRINT てくれぇ…、
							CASE "敬語"
								PRINT てくださ…っ、
							CASE "お嬢様", "メスガキ"
								PRINT なさい…っ、
							CASE "レイシス語"
								PRINT てくだサイ…っ、
							CASEELSE
								PRINT てぇ…
						ENDSELECT
					;Ａを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
						PRINT けつまんこいい…っ
					;Ｖを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
						PRINT おまんこ汁出
						SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
							CASE "乱暴"
								PRINT るぅ…、
							CASE "敬語"
								PRINT ちゃいます…っ、
							CASE "戦士", "老人"
								PRINT てしまう…っ、
							CASE "幼い"
								PRINT ちゃうぅ…っ、
							CASE "お嬢様"
								PRINT ちゃいますわ…
							CASE "レイシス語"
								PRINT ちゃいマス…
							CASE "メスガキ"
								PRINT ちゃうって…
							CASEELSE
								PRINT るよぉ…
						ENDSELECT
					;一応保険
					ELSE
						PRINT あっ、あ…っ、
					ENDIF
				ELSE
					PRINT あっ、あ…っ、
				ENDIF
			CASE 4
				PRINT はぁ…んっ…
			CASE 5
				PRINT …ぁあっ、
			CASE 6
				PRINT ん…
			CASE 7
				PRINT …ん、ぅ、
			CASE 8
				;陥落前でAで感じるまたは男でBで感じる
				IF (!陥落(TARGET) && 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ) || (SEX(TARGET) == 1 && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ)
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴"
							PRINT うそだ…っ、
						CASE "標準", "柔らかめ"
							PRINT なん、で…っ？
						CASE "敬語", "戦士", "老人"
							PRINT …嘘……、
						CASE "土佐弁"
							PRINT なん、が…っ
						CASE "関西弁"
							PRINT 嘘や、ん…っ
						CASE "博多弁"
							PRINT なして、こぎゃんこつっ
						CASE "幼い"
							PRINT 変だよぉ…
						CASE "お嬢様"
							PRINT ありえませんわ…っ
						CASE "レイシス語"
							PRINT はわわ…
						CASE "メスガキ"
							PRINT 嘘でしょ…
						CASE "お姉さん"
							PRINT こんなことって…
					ENDSELECT
				ELSE
					PRINT いっ…
				ENDIF
			CASE 9
				;淫乱系陥落
				IF FALLTYPE(TARGET) == 2
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴", "標準", "柔らかめ", "敬語", "戦士"
							PRINT いい、っ…
						CASE "土佐弁"
							PRINT えい、よぉ…っ、
						CASE "関西弁"
							PRINT ええ…っ、
						CASE "博多弁"
							PRINT 気持ちよか…っ、
						CASE "幼い"
							PRINT すごいよぉ…
						CASE "老人"
							PRINT よい…
						CASE "お嬢様"
							PRINT 素敵ですわ…
						CASE "レイシス語"
							PRINT すごいデス…
						CASE "メスガキ"
							PRINT 最っ高♡
						CASE "お姉さん"
							PRINT いいわ…
					ENDSELECT
				ELSEIF MARK:反発刻印 && !陥落(TARGET)
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴"
							PRINT くそ…っ、
						CASE "標準"
							PRINT ヤダっ…
						CASE "柔らかめ"
							PRINT だめ、ぇっ…っ、
						CASE "敬語"
							PRINT やめ…てくださ…っ、
						CASE "土佐弁"
							PRINT やが…っ、
						CASE "戦士"
							PRINT 離せ…っ、
						CASE "関西弁"
							PRINT 嫌や…っ、
						CASE "博多弁"
							PRINT やめ…っ、
						CASE "幼い"
							PRINT やめて…っ、
						CASE "老人"
							PRINT ならぬ…っ、
						CASE "お嬢様"
							PRINT いけませんわ…っ、
						CASE "レイシス語"
							PRINT だめデス…っ、
						CASE "メスガキ"
							PRINT 離せっ、ザコ…っ、
						CASE "お姉さん"
							PRINT だめよ…っ、
					ENDSELECT
				ELSE
					PRINT っあ…！
				ENDIF
		ENDSELECT
	ENDIF
REND
;締め
SELECTCASE RAND:3
	CASE 0
		PRINT んっ
	CASE 1
		PRINT あ…っ！
	CASE 2
		PRINT あっ、ん
ENDSELECT
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;快感LV3
;----------------------------------------------------------------------
@PANT_VOICE_快感3
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;ボールギャグか触手口虐中
	IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ん―！
			CASE 1
				PRINT ふぅっ…！
			CASE 2
				PRINT んおっ…
			CASE 3
				PRINT ぅっ…
			CASE 4
				PRINT くぅっ、
			CASE 5
				PRINT んんっ！
			CASE 6
				PRINT あぅ…っ、
			CASE 7
				PRINT んく…っ、
			CASE 8
				PRINT ふっ…あっ、
			CASE 9
				PRINT あうぅ！
		ENDSELECT
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT や…っ、
			CASE 1
				PRINT あっ、あっ、
			CASE 2
				PRINT ふあ、ひ…っ、
			CASE 3
				PRINT あひ、ぃっ、
			CASE 4
				PRINT ひぁっ…
			CASE 5
				PRINT もっ…
			CASE 6
				PRINT やああっ…！
			CASE 7
				PRINT あーっ！
			CASE 8
				PRINT んっ、んーっ…
			CASE 9
				;反発刻印１以上
				IF MARK:反発刻印 && !陥落(TARGET)
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴"
							PRINT 嫌だぁっ、
						CASE "標準", "柔らかめ"
							PRINT だめっ、だめぇ…っ、
						CASE "敬語"
							PRINT もう無理ですっ、
						CASE "土佐弁"
							PRINT も、無理やき…っ
						CASE "戦士"
							PRINT やめろっ、
						CASE "関西弁"
							PRINT あかんよぉっ、
						CASE "博多弁"
							PRINT もう、いかんたいっ
						CASE "幼い"
							PRINT やだぁっ、
						CASE "老人"
							PRINT やめんかっ、
						CASE "お嬢様"
							PRINT 耐えられませんこと…っ
						CASE "レイシス語"
							PRINT もうだめデスぅ…っ
						CASE "メスガキ"
							PRINT こんなっ、ザコに…ぃ、
						CASE "お姉さん"
							PRINT だめよ…っ、もう…
					ENDSELECT
				;淫語モード
				ELSEIF 汎用喘ぎ:淫語
					;胸弄られてる
					IF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
						SELECTCASE RAND:6
							CASE 0
								PRINT おっぱいだめぇ……っ
							CASE 1
								PRINT おっぱいいじめられるぅ
							CASE 2
								PRINT おっぱいいいのぉっ
							CASE 3
								PRINT ちくびいいのぉっ
							CASE 4
								PRINT ちくび感じちゃうっ
							CASE 5
								PRINT ちくびコリコリいいっ、
						ENDSELECT
					;ペニス持ちでＣを弄られてる
					ELSEIF PENIS(TARGET) && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
							CASE "標準", "柔らかめ", "敬語", "土佐弁"
								PRINT お
						ENDSELECT
						PRINT ちんぽ汁出るぅっ、
					;ペニスなしでＣを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						PRINT クリきもちいいっ、
					;Ａを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
						SELECTCASE RAND:6
							CASE 0
								PRINT けつまんこがぁ…っ
							CASE 1
								PRINT おしりがぁ……っ
							CASE 2
								PRINT おしりやぁっ……
							CASE 3
								PRINT けつまんこやぁ……っ
							CASE 4
								PRINT けつまんこいぃのぉっ、
							CASE 5
								PRINT おしりいじられてるぅ……っ
						ENDSELECT
					;Ｖを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
						SELECTCASE RAND:6
							CASE 0
								PRINT おまんこいいっ
							CASE 1
								PRINT おまんこがぁ……っ
							CASE 2
								PRINT おまんこいいのぉっ
							CASE 3
								PRINT おまんこいじられて……っ
							CASE 4
								PRINT やぁ、おまんこ感じちゃうっ
							CASE 5
								;PRINT 男なのに、おまんこ、いいっ
								PRINT おまんこ、いいっ
						ENDSELECT
					;一応保険
					ELSE
						PRINT ひぃっ、
					ENDIF
				ELSE
					PRINT ひいっ、
				ENDIF
		ENDSELECT
	ENDIF
REND
;締め
;ボールギャグか触手口虐中
IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
	SELECTCASE RAND:3
		CASE 0
			PRINT んあぁ――！
		CASE 1
			PRINT む――！
		CASE 2
			PRINT ふあぁ――！
	ENDSELECT
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINT ぁあああ―っ！
		CASE 1
			PRINT …っく――！！
		CASE 2
			PRINT ああっ
	ENDSELECT
ENDIF
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;快感LV4
;----------------------------------------------------------------------
@PANT_VOICE_快感4
#DIM DYNAMIC 乱数
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;ボールギャグか触手口虐中
	IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT んん――――！
			CASE 1
				PRINT む―――！
			CASE 2
				PRINT あうっ…、
			CASE 3
				PRINT んうう！
			CASE 4
				PRINT ふうぅっ、
			CASE 5
				PRINT んおっ、
			CASE 6
				PRINT あぉ…っ！
			CASE 7
				PRINT んく…っ、
			CASE 8
				PRINT んんんっ、
			CASE 9
				PRINT あううぅ！
		ENDSELECT
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ひぃあああっ、
			CASE 1
				PRINT あああっ！
			CASE 2
				PRINT んっ…ああっ、
			CASE 3
				PRINT イ…くぅっ、
			CASE 4
				PRINT イくっ、イくぅ――…！！
			CASE 5
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "戦士"
						PRINT イ…くぅ…っ
					CASE "関西弁"
						PRINT イってまぅ…
					CASE "幼い"
						PRINT イっちゃううぅ…っ、
					CASE "レイシス語"
						PRINT イっちゃいマス…っ、
					CASE "老人"
						PRINT 達する…っ、
					CASE "お嬢様"
						PRINT イきますわ…っ、
					CASE "メスガキ"
						PRINT ザコにイかされる…っ、
					CASE "お姉さん"
						PRINT イきそうよ…っ、
					CASEELSE
						PRINT イ…くっ、
				ENDSELECT
			CASE 6
				;淫語モード
				IF 汎用喘ぎ:淫語
					SELECTCASE RAND:8
						CASE 0
							PRINT 気持ちいぃっ、気持ちいぃのぉっ♡
						CASE 1
							PRINT ぐちゃぐちゃにされて
						CASE 2
							PRINT 気持ちいいこといっぱいで
						CASE 3
							PRINT あちこちいじめられて
						CASE 4
							PRINT 全部きもちいぃっ♡
						CASE 5
							PRINT 全部、ぜんぶいいっ♡　ぜんぶいいのぉっ♡
						CASE 6
							PRINT 気持ちよすぎてバカになっちゃうぅぅ♡
						CASE 7
							PRINT ぜんぶいいっ♡　全身きもちいいっ♡
					ENDSELECT
					;胸弄られてる
					IF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
						SELECTCASE RAND:18
							CASE 0
								PRINT ちくびいじられて
							CASE 1
								PRINT おっぱいいじられて
							CASE 2
								PRINT ちくびいじめられて
							CASE 3
								PRINT おっぱいいじめられて
							CASE 4
								PRINT ちくび気持ちぃぃ♡
							CASE 5
								PRINT おっぱい気持ちいぃぃ♡
							CASE 6
								PRINT ちくび気持ちいぃのらめぇぇ♡
							CASE 7
								PRINT おっぱいもうらめぇぇ♡
							CASE 8
								PRINT おっぱい気持ちいぃのらめぇぇぇぇ♡
							CASE 9
								PRINT ちくびもうらめぇぇ♡
							CASE 10
								PRINT おっぱいいじめるのらめぇぇ♡
							CASE 11
								PRINT ちくびいじめるのらめぇぇ♡
							CASE 12
								PRINT ちくびでいくっ♡いっちゃうぅぅ♡
							CASE 13
								PRINT ちくびがおかしくなるぅぅぅっ♡
							CASE 14
								PRINT おっぱいおかしくなるぅぅぅっ♡
							CASE 15
								PRINT おっぱいでいくっ♡いっちゃうぅぅ♡
							CASE 16
								PRINT ちくび感じすぎるのぉぉ♡
							CASE 17
								PRINT おっぱい感じすぎるのぉぉぉ♡
						ENDSELECT
					;Ａを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
						SELECTCASE RAND:25
							CASE 0
								PRINT けつまんこいじられて
							CASE 1
								PRINT おしりいじられて
							CASE 2
								PRINT おしりほじほじされて
							CASE 3
								PRINT けつまんこぐちゅぐちゅされて
							CASE 4
								PRINT けつまんこいじめられて
							CASE 5
								PRINT オスまんこいじめられて
							CASE 6
								PRINT おしりなのに感じるぅ♡
							CASE 7
								PRINT けつまんこ気持ちいぃぃ♡
							CASE 8
								PRINT おしりぐちゃぐちゃだよぉ♡
							CASE 9
								PRINT けつまんこいっぱいして♡
							CASE 10
								PRINT おしりがおまんこになっちゃうぅぅ♡
							CASE 11
								PRINT おしりっ、おしりもうっ、おまんこにゃるのぉぉ♡
							CASE 12
								PRINT おしりぐちゅぐちゅしゅごいのぉぉ♡
							CASE 13
								PRINT けつまんこ感じすぎるのぉぉ♡
							CASE 14
								PRINT おしりっ♡おしりじゅぽじゅぽってぇ♡
							CASE 15
								PRINT けつまんこじゅぽじゅぽいいっ♡
							CASE 16
								PRINT けつまんこ♡まんこ♡もうおまんこなのぉぉ♡
							CASE 17
								PRINT おしりじゅぽじゅぽしゅごいいっ♡
							CASE 18
								PRINT おしりの奥まで入ってるぅっ♡
							CASE 19
								PRINT けつまんこ壊れちゃうぅぅぅ♡
							CASE 20
								PRINT けつまんこの奥まできてるぅぅ♡
							CASE 21
								PRINT おしりがっ♡おしりが壊れちゃうぅぅっ♡
							CASE 22
								PRINT おしりがっ♡おしりがぁぁっ♡
							CASE 23
								PRINT おしりっ♡女の子にされてるぅぅっ♡
							CASE 24
								PRINT けちゅまんこ♡女の子にされてるぅ♡
						ENDSELECT
					ENDIF
					;ペニスあり
					IF PENIS(TARGET) && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						IF ABL:メス度 >= 5
							乱数 = RAND:6
						ELSE
							乱数 = RAND:3
						ENDIF
						SELECTCASE 乱数
							CASE 0
								PRINT おちんぽ
							CASE 1
								PRINT はしたないおちんぽ
							CASE 2
								PRINT いやらしいおちんぽ
							CASE 3
								PRINT クリちんぽ
							CASE 4
								PRINT メスちんぽ
							CASE 5
								PRINT 役立たずのおちんぽ
						ENDSELECT
						;膣もある
						IF VAGINA(TARGET)
							SELECTCASE RAND:6
								CASE 0
									PRINT とおまんこからえっちなお汁でるうう♡♡♡
								CASE 1
									PRINT とおまんこからお汁でちゃうぅぅ♡♡♡
								CASE 2
									PRINT とおまんこがお潮ふいちゃうぅぅぅ♡♡♡
								CASE 3
									PRINT とおまんこからお汁もれちゃうぅぅ♡♡♡
								CASE 4
									PRINT とおまんこからぴゅっぴゅするぅぅぅう♡♡♡
								CASEELSE
									PRINT からみるくでちゃうぅぅぅぅ♡♡おまんこもイっちゃうぅぅぅ♡♡
							ENDSELECT
						ELSE
							SELECTCASE RAND:6
								CASE 0
									PRINT からえっちなお汁でるうう♡♡♡
								CASE 1
									PRINT からお汁でちゃうぅぅ♡♡♡
								CASE 2
									PRINT がお潮ふいちゃうぅぅぅ♡♡♡
								CASE 3
									PRINT 汁もれちゃうぅぅ♡♡♡
								CASE 4
									PRINT ぴゅっぴゅするぅぅぅう♡♡♡
								CASE 5
									PRINT からみるくでちゃうぅぅぅぅ♡♡♡
							ENDSELECT
						ENDIF
					;ペニスなし
					ELSE
						SELECTCASE RAND:2
							CASE 0
								PRINT おまんこからお汁でちゃうぅぅうう！
							CASE 1
								PRINT お潮でちゃうぅぅぅ♡♡
						ENDSELECT
					ENDIF
				;みさくら語モード
				ELSEIF 汎用喘ぎ:みさくら語
					SELECTCASE RAND:20
						CASE 0
							PRINT らめぇっ、
						CASE 1
							PRINT …んああああ！
						CASE 2
							PRINT んおぉぉぉぉっ♡♡
						CASE 3
							PRINT ひぃぃぃぃん！！
						CASE 4
							PRINT やらぁぁぁぁぁぁ♡
						CASE 5
							PRINT んほぉぉっ♡
						CASE 6
							PRINT きもぢいぃﾞーっ♡
						CASE 7
							PRINT ぴぎゃあぁ！！
						CASE 8
							PRINT あへぇぇぇっ♡
						CASE 9
							PRINT いいいいいっ♡♡
						CASE 10
							PRINT だめぇっ♡ダメえへええぇぇえぇっ♡♡
						CASE 11
							PRINT らめっ♡らめっ♡らめぇえぇぇぇっ♡♡
						CASE 12
							PRINT もおいぐぅ♡
						CASE 13
							PRINT いぐっ、いぐぅぅぅ♡♡
						CASE 14
							PRINT いってりゅ♡もぉいってゅぅぅうぅ♡
						CASE 15
							PRINT アクメぇ♡♡アクメしゅりゅぅぅぅう♡♡♡
						CASE 16
							PRINT イクぅぅっ♡イクイクイクイクイクっ♡♡♡
						CASE 17
							PRINT もおイクぅぅぅっ♡♡
						CASE 18
							PRINT くほぉぉぉぉっ♡
						CASE 19
							PRINT んほぉぉぉぉっ！！
					ENDSELECT
				ELSE
					PRINT …んああああ！
				ENDIF
			CASE 7
				PRINT ひぃっ、
			CASE 8
				;陥落前
				IF !陥落(TARGET)
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴", "標準", "柔らかめ", "敬語", "戦士"
							PRINT いやだあああっ！
						CASE "幼い"
							PRINT やだぁ、やだぁっ！
						CASE "お嬢様"
							PRINT やめてくださいまし！
						CASE "レイシス語"
							PRINT 嫌デスうぅ！
						CASE "メスガキ"
							PRINT やだぁ、やだぁっ♡
						CASE "お姉さん"
							PRINT 嫌よっ！
						CASE "土佐弁", "老人"
							PRINT 嫌じゃあああっ！
						CASE "関西弁"
							PRINT 嫌やあああっ！
						CASE "博多弁"
							PRINT 嫌ったいっ！
					ENDSELECT
				;淫語モード
				ELSEIF 汎用喘ぎ:淫語
					;胸弄られてる
					IF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｂ
						SELECTCASE RAND:8
							CASE 0
								PRINT ちくびへんになるぅぅ！！
							CASE 1
								PRINT おっぱいへんになるぅぅ！！
							CASE 2
								PRINT ちくびいいのぉぉ♡
							CASE 3
								PRINT おっぱいいいのぉぉ♡
							CASE 4
								PRINT おっぱいでいっちゃうぅぅ♡
							CASE 5
								PRINT ちくびでいっちゃうぅぅ♡
							CASE 6
								PRINT ちくびしゅごいよぉぉっ♡
							CASE 7
								PRINT おっぱいしゅごいよぉぉっ♡
						ENDSELECT
					;ペニス持ちでＣを弄られてる
					ELSEIF PENIS(TARGET) && 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						SELECTCASE RAND:8
							CASE 0
								PRINT おちんぽからせーえき出るぅっ！
							CASE 1
								PRINT おちんぽぴゅっぴゅってしちゃぅぅっ！！
							CASE 2
								PRINT おちんぽみるくでちゃうぅぅ♡♡
							CASE 3
								PRINT おちんぽからお汁もれちゃうぅぅ♡♡
							CASE 4
								PRINT くりちんぽ壊れるぅぅぅっ♡♡♡
							CASE 5
								PRINT おちんぽ壊れるぅぅぅっ♡♡♡
							CASE 6
								PRINT おちんぽしゅごいよぉぉぉっ♡♡♡
							CASE 7
								PRINT お潮でちゃうぅぅぅぅ♡
						ENDSELECT
					;ペニスなしでＣを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｃ
						SELECTCASE RAND:7
							CASE 0
								PRINT クリ感じすぎちゃっ…んああ！
							CASE 1
								PRINT えっちなお豆いじられていくぅぅ♡♡
							CASE 2
								PRINT クリいじっちゃやぁぁ♡♡
							CASE 3
								PRINT えっちなお豆くにゅくにゅしないでぇぇ♡♡
							CASE 4
								PRINT クリトリス壊れるぅぅぅっ♡♡♡
							CASE 5
								PRINT えっちなお豆さんいいのぉぉ♡♡
							CASE 6
								PRINT クリいじられたらお潮でちゃうぅぅぅぅ♡
						ENDSELECT
					;Ａを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ａ
						IF ABL:Ａ感覚 > 4
							SELECTCASE RAND:8
								CASE 0
									PRINT けつまんこぐちゃぐちゃにされてイくぅ！
								CASE 1
									PRINT けつまんこ感じすぎりゅからぁぁぁ♡♡
								CASE 2
									PRINT けつまんこすごいのぉぉぉ！！！
								CASE 3
									PRINT けつまんこ、いいっ！いいよぉぉっ♡♡
								CASE 4
									PRINT おしり壊れるぅ♡♡♡
								CASE 5
									PRINT おしりまんこ壊れるぅ♡♡♡
								CASE 6
									PRINT おしりぐちゃぐちゃなのぉ♡♡♡
								CASE 7
									PRINT けつまんこぐちゃぐちゃになるぅ♡
							ENDSELECT
						ENDIF
					;Ｖを弄られてる
					ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:Ｖ
						SELECTCASE RAND:6
							CASE 0
								PRINT おまんこいいぃいぃぃ！
							CASE 1
								PRINT おまんこっ♡おまんこぉぉ♡♡気持ちよすぎりゅぅぅぅ！！！
							CASE 2
								PRINT おまんこ、壊れるっ♡♡♡
							CASE 3
								PRINT おまんこがぁぁッ♡♡♡
							CASE 4
								PRINT 気持ちよすぎぃ♡おまんこ壊れるぅ♡♡♡
							CASE 5
								PRINT おまんこぐちゃぐちゃなのぉぉ♡
						ENDSELECT
					;一応保険
					ELSE
						PRINT あああ…っ、
					ENDIF
				ELSE
					PRINT あああ…っ、
				ENDIF
			CASE 9
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "標準"
						PRINT あたまがっ…変になるっ…
					CASE "柔らかめ", "敬語"
						PRINT 壊れちゃ…っ、
					CASE "土佐弁"
						PRINT わらくる…っ、
					CASE "戦士"
						PRINT こんな…嘘だろ…ッ、
					CASE "関西弁"
						PRINT あかん…っ、あかんてぇ…
					CASE "博多弁"
						PRINT 嫌ばい、えずかちゃー…
					CASE "幼い"
						PRINT あたまがヘンになっちゃったよぉ…
					CASE "老人"
						PRINT おかしゅうなる…
					CASE "お嬢様"
						PRINT はしたないですわ…
					CASE "レイシス語"
						PRINT はうぅーーーーっ！
					CASE "メスガキ"
						PRINT ザコにイかされちゃ…っ♡
					CASE "お姉さん"
						PRINT こんな、すごいの…
				ENDSELECT
		ENDSELECT
	ENDIF
REND
;締め
;ボールギャグか触手口虐中
IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
	SELECTCASE RAND:3
		CASE 0
			PRINT んう――――！
		CASE 1
			PRINT んあっぁあああ！
		CASE 2
			PRINT ああ――――！
	ENDSELECT
ELSE
	SELECTCASE RAND:3
		CASE 0
			IF 汎用喘ぎ:みさくら語
				PRINT らめぇぇええ！！
			ELSE
				PRINT …ッッッ！！！
			ENDIF
		CASE 1
			PRINT ひぃ―――っ！！！
		CASE 2
			PRINT あああぁっぁぁぁ！！！
	ENDSELECT
ENDIF
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;苦痛LV1
;----------------------------------------------------------------------
@PANT_VOICE_苦痛1
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;ボールギャグか触手口虐中
	IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT んぅぅ！
			CASE 1
				PRINT …くぅ…
			CASE 2
				PRINT ん…
			CASE 3
				PRINT あぅっ…
			CASE 4
				PRINT んむぅ…
			CASE 5
				PRINT …っ…！
			CASE 6
				PRINT あぉ…っ、
			CASE 7
				PRINT んく…っ、
			CASE 8
				PRINT ぐぅ…
			CASE 9
				PRINT むぅぅ…！
		ENDSELECT
	;マゾ持ちかマゾッ気3以上ありか、LV3以上の快感あり
	ELSEIF TALENT:マゾ || ABL:マゾっ気 > 2 || 汎用喘ぎ:感度 >= 5
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ああ！
			CASE 1
				PRINT ひいッ、
			CASE 2
				PRINT い…っ、
			CASE 3
				PRINT ぁっん…！
			CASE 4
				PRINT やめっ、
			CASE 5
				PRINT ぁああっ…
			CASE 6
				PRINT ひっ、
			CASE 7
				PRINT んあっ…、
			CASE 8
				PRINT あっ…ぁあ…っ、
			CASE 9
				;陥落前
				IF !陥落(TARGET)
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴", "標準", "柔らかめ", "敬語", "戦士"
							PRINT なんでっ、感じ…っ、
						CASE "土佐弁"
							PRINT なんで、えいなが…
						CASE "関西弁"
							PRINT なんでええのぉ…？
						CASE "博多弁"
							PRINT なしてよかん…っ、
						CASE "お嬢様"
							PRINT 感じてなんか…いませんわっ、
						CASEELSE
							PRINT く…ぁっ…、
					ENDSELECT
				ELSE
					PRINT ああ！
				ENDIF
		ENDSELECT
	;普通の人
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ああ！
			CASE 1
				PRINT ひいッ、
			CASE 2
				PRINT い…っ、
			CASE 3
				PRINT やぁっ…！
			CASE 4
				PRINT やめっ、
			CASE 5
				PRINT ぐ…
			CASE 6
				PRINT うぐ…っ、
			CASE 7
				PRINT くぅ…っ…
			CASE 8
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "標準"
						PRINT やめっ…！
					CASE "柔らかめ", "敬語"
						PRINT いや…
					CASE "土佐弁"
						PRINT も…っ、やめ…っ、
					CASE "戦士"
						PRINT くそッ…、
					CASE "関西弁"
						PRINT も…っ、せんで…っ、
					CASE "博多弁"
						PRINT ちゃしてとってくれ…
					CASE "幼い"
						PRINT やめてぇっ！
					CASE "老人"
						PRINT やめんか…っ、
					CASE "お嬢様", "お姉さん"
						PRINT やめなさい…っ、
					CASE "レイシス語"
						PRINT やめてくだサイ…っ、
					CASE "メスガキ"
						PRINT やめてよ…っ、
				ENDSELECT
			CASE 9
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "戦士"
						PRINT …っ…ふ…ざけ……、
					CASE "標準", "柔らかめ", "敬語"
						PRINT ダメ…っ、
					CASE "土佐弁"
						PRINT いかん…っ
					CASE "関西弁"
						PRINT 嫌やぁ―っ、
					CASE "博多弁"
						PRINT 嫌ばいっ
					CASEELSE
						PRINT ああ！
				ENDSELECT
		ENDSELECT
	ENDIF
REND
;締め
;ボールギャグか触手口虐中
IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
	SELECTCASE RAND:3
		CASE 0
			PRINT …んんっ！
		CASE 1
			PRINT …ぅ…
		CASE 2
			PRINT …ん…ぁぅ…
	ENDSELECT
;マゾ持ちかマゾッ気あり、LV3以上の快感あり
ELSEIF TALENT:マゾ || ABL:マゾっ気 > 2 || 汎用喘ぎ:感度 >= 5
	SELECTCASE RAND:3
		CASE 0
			PRINT やああっんっ
		CASE 1
			PRINT …ぅう、あ…
		CASE 2
			PRINT んっ、あぅ…
	ENDSELECT
;普通の人
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINT …ぅう、あ…
		CASE 1
			PRINT あ……あ………
		CASE 2
			PRINT ひいぃっ！
	ENDSELECT
ENDIF
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;苦痛LV2
;----------------------------------------------------------------------
@PANT_VOICE_苦痛2
;PANT_A = 口調（1=雑、2=標準、3=柔、4=敬語、5=関西）
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;ボールギャグか触手口虐中
	IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT んぐぅぅぅぅぅぅ！
			CASE 1
				PRINT あううぅ…！
			CASE 2
				PRINT ん――――！
			CASE 3
				PRINT う゛ぅっ…
			CASE 4
				PRINT ぐぅっ、
			CASE 5
				PRINT む―――っ！
			CASE 6
				PRINT あぉぉ…っ、
			CASE 7
				PRINT んんんっ、
			CASE 8
				PRINT んぐっ
			CASE 9
				PRINT むぅぅ…！
		ENDSELECT
	;マゾ持ち、LV3以上の快感あり
	ELSEIF TALENT:マゾ || 汎用喘ぎ:感度 >= 5
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT いあああああっ！
			CASE 1
				PRINT ひいいいっ…！
			CASE 2
				PRINT ひ、―――ッッッッ、
			CASE 3
				PRINT ひぁっ…！
			CASE 4
				PRINT ぎゃっ、
			CASE 5
				PRINT ひぎぃ――っ！
			CASE 6
				PRINT あ゛――…っ、
			CASE 7
				PRINT ひぃぃっ、
			CASE 8
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "乱暴", "戦士", "博多弁"
						PRINT きもち、い゛っ…
					CASE "柔らかめ"
						PRINT もっとしてぇっ！
					CASE "土佐弁"
						PRINT おかしゅうなるき、いかんちや…っ、
					CASE "関西弁"
						PRINT 狂ってまう…！
					CASE "博多弁"
						PRINT えらいぼっこしてぇ…っ、
					CASE "老人"
						PRINT いじめぬいてくれぃ…っ、
					CASE "お嬢様", "メスガキ", "お姉さん"
						PRINT もっといたぶりなさい…っ、
					CASE "レイシス語"
						PRINT 痛いのに、感じちゃいマス…っ、
					CASEELSE
						PRINT やだっ…やだ、気持ちぃよぉおっ、
				ENDSELECT
			CASE 9
				SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
					CASE "柔らかめ", "戦士"
						PRINT 気持ち、ぃ…
					CASE "敬語"
						PRINT …ヘン、になっちゃいま…っ、
					CASE "土佐弁"
						PRINT えい、えいがよ…っ
					CASE "関西弁"
						PRINT あかん、て…っ
					CASE "博多弁"
						PRINT 気持ちよか…っ
					CASEELSE
						PRINT あっ、あっ、
				ENDSELECT
		ENDSELECT
	;普通の人用
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ひぎぃ――っ、
			CASE 1
				PRINT いあああああっ！
			CASE 2
				PRINT ひいいいっ…！
			CASE 3
				PRINT ひ、―――ッッッッ、
			CASE 4
				PRINT ぎゃっ、
			CASE 5
				PRINT 死ぬ、ぅっ
			CASE 6
				PRINT はぁっ……はぁっ、はっ、
			CASE 7
				PRINT ぐあ…っ！
			CASE 8
				;プライド高いor生意気or気丈or抵抗or反抗的or痛みに強いor服従or反発
				IF TALENT:反抗的 || TALENT:気丈 || TALENT:プライド高い || TALENT:生意気 || TALENT:抵抗 || TALENT:痛みに弱い || TALENT:服従 || TALENT:隷属 || MARK:反発刻印
					PRINT ひ…っ、
				ELSE
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴", "標準", "博多弁"
							PRINT やめ…っ、
						CASE "柔らかめ"
							PRINT やめて…っ、
						CASE "敬語"
							PRINT お願っ…ですから…！
						CASE "土佐弁"
							PRINT もっ、やめとぉせ…
						CASE "戦士"
							PRINT む、り……だぁっ
						CASE "関西弁"
							PRINT もっ、堪忍して…やぁっ、
						CASE "幼い", "メスガキ"
							PRINT やめ…やめて…よぉ…
						CASE "老人"
							PRINT もう、これ以上は…後生じゃ…
						CASE "お嬢様"
							PRINT もう…無理、ですわ…
						CASE "レイシス語"
							PRINT やめてくだサイッ！
						CASE "お姉さん"
							PRINT だめよっ、もう…っ
					ENDSELECT
				ENDIF
			CASE 9
				;プライド高いor生意気or気丈or抵抗or反抗的or痛みに強いor服従or反発
				IF TALENT:反抗的 || TALENT:気丈 || TALENT:プライド高い || TALENT:生意気 || TALENT:抵抗 || TALENT:痛みに弱い || TALENT:服従 || TALENT:隷属 || MARK:反発刻印
					PRINT ぐ…っ、
				ELSE
					SELECTCASE 汎用喘ぎ_口調:(汎用喘ぎ:口調)
						CASE "乱暴", "戦士"
							PRINT やめろ、
						CASE "柔らかめ"
							PRINT やめてよぉ…！
						CASE "敬語"
							PRINT 許してくださっ…
						CASE "土佐弁"
							PRINT 許しとおせ、
						CASE "関西弁"
							PRINT イヤやあーーっっ！
						CASE "博多弁"
							PRINT も、せんで、
						CASE "老人"
							PRINT やめんか、
						CASE "レイシス語"
							PRINT 許してくだサイ…
						CASE "メスガキ"
							PRINT やめろっ、ザコ…
						CASEELSE
							PRINT やめて、
					ENDSELECT
				ENDIF
		ENDSELECT
	ENDIF
REND
;締め
;ボールギャグか触手口虐中
IF TEQUIP:ボールギャグ || TEQUIP:触手口辱
	SELECTCASE RAND:3
		CASE 0
			PRINT んぐううぅぅうう！
		CASE 1
			PRINT あおおおっ…！
		CASE 2
			PRINT ぐぅぅ…ふっ…
	ENDSELECT
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINT ああぁっぁぁっぁあぁ！！！
		CASE 1
			PRINT ひぃ―――っっ！！！
		CASE 2
			PRINT はぁん…っ、はぁ…
	ENDSELECT
ENDIF
PRINTW 」

RETURN 0

;----------------------------------------------------------------------
;口が塞がってる(ボールギャグ・触手口虐中以外)
;----------------------------------------------------------------------
@PANT_VOICE_口使用中
PRINT 「
REPEAT 汎用喘ぎ:頻度
	;苦痛がある
	IF 汎用喘ぎ:分岐 & (喘ぎBIT:苦痛1 | 喘ぎBIT:苦痛2)
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ……
			CASE 1
				PRINT んん、
			CASE 2
				PRINT んっ、
			CASE 3
				PRINT …んぅ、
			CASE 4
				PRINT ん、むっ…
			CASE 5
				PRINT ふぅ…
			CASE 6
				PRINT んぐっ！
			CASE 7
				PRINT ん゛ー…っ、
			CASE 8
				PRINT んーっんー！
			CASE 9
				PRINT うぐ…
		ENDSELECT
	;任意奉仕系
	ELSEIF 汎用喘ぎ:分岐 & 喘ぎBIT:奉仕系
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ……
			CASE 1
				PRINT んん、
			CASE 2
				PRINT んっ、
			CASE 3
				PRINT …んぅ、
			CASE 4
				PRINT ん、むっ…
			CASE 5
				PRINT ふぅ…
			CASE 6
				PRINT んく…っ、
			CASE 7
				PRINT あむっ…
			CASE 8
				PRINT …ちゅ、
			CASE 9
				;淫乱か娼婦
				IF FALLTYPE(TARGET) == 2
					PRINT おいひ…っ、
				ELSE
					PRINT は…、
				ENDIF
		ENDSELECT
	;その他
	ELSE
		SELECTCASE 喘ぎ乱数(10)
			CASE 0
				PRINT ……
			CASE 1
				PRINT んん、
			CASE 2
				PRINT んっ、
			CASE 3
				PRINT …んぅ、
			CASE 4
				PRINT ん、むっ…
			CASE 5
				PRINT ふぅ…
			CASE 6
				PRINT んく…っ、
			CASE 7
				PRINT あむっ…
			CASE 8
				PRINT …ちゅ、
			CASE 9
				PRINT は…、
		ENDSELECT
	ENDIF
REND
SELECTCASE RAND:3
	CASE 0
		PRINT んんっ
	CASE 1
		PRINT …ぷはっ
	;反発3または苦痛有でえづく
	CASE 2
		IF MARK:反発刻印 || 汎用喘ぎ:分岐 & (喘ぎBIT:苦痛1 |喘ぎBIT:苦痛2)
			PRINT ……ぅ、ぐぇ…
		ELSE
			PRINT …はぁ…っ
		ENDIF
ENDSELECT
PRINTW 」

RETURN 0
