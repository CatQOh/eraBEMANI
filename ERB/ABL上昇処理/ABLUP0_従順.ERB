;ABLUP:7 = 屈服の珠必要数
;ABLUPEXP:30 = 主人調教経験必要数
;ABLUPEXP:50 = 異常経験必要数
@ABLUP0
SIF ABL:従順 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL0
CALL CHECK_JUEL0
IF RESULT
	CALL NEED_JUEL0
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:従順++
ENDIF

@NEED_JUEL0
SELECTCASE ABL:従順
	CASE 0
		ABLUP:7 = 1
		ABLUPEXP:30 = 10
	CASE 1
		ABLUP:7 = 20
		ABLUPEXP:30 = 50
	CASE 2
		ABLUP:7 = 1000
		ABLUPEXP:30 = 100
	CASE 3
		ABLUP:7 = 8000
		ABLUPEXP:30 = 150
	CASE 4
		ABLUP:7 = 24000
		ABLUPEXP:30 = 200
	CASE 5
		ABLUP:7 = 30000
		ABLUPEXP:30 = 250
	CASE 6
		ABLUP:7 = 36000
		ABLUPEXP:30 = 300
	CASE 7
		ABLUP:7 = 45000
		ABLUPEXP:30 = 400
	CASE 8
		ABLUP:7 = 55000
		ABLUPEXP:30 = 600
	CASE 9
		ABLUP:7 = 70000
		ABLUPEXP:30 = 800
	CASE IS >= 10
		LOCAL = ABL:従順*ABL:従順
		ABLUP:7 = 800 * LOCAL
		ABLUPEXP:30 = 10 * LOCAL
ENDSELECT

;素質での補正
;恋慕
SIF TALENT:恋慕
	TIMES ABLUP:7 , 0.80
;親愛
SIF TALENT:親愛
	TIMES ABLUP:7 , 0.60
;淫乱
SIF TALENT:淫乱
	TIMES ABLUPEXP:30 , 1.10
;娼婦
SIF TALENT:娼婦
	TIMES ABLUPEXP:30 , 1.30
;服従
IF TALENT:服従
	TIMES ABLUP:7 , 0.50
	TIMES ABLUPEXP:30 , 0.50
ENDIF
;隷属
IF TALENT:隷属
	TIMES ABLUP:7 , 0.20
	TIMES ABLUPEXP:30 , 0.20
ENDIF

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:7 , 1.50
		TIMES ABLUPEXP:30 , 1.50
	ENDIF
ENDIF

;プライド高い、生意気
IF TALENT:プライド高い || TALENT:生意気
	TIMES ABLUPEXP:30 , 2.00
;大人しい、素直
ELSEIF TALENT:大人しい || TALENT:素直
	TIMES ABLUPEXP:30 , 0.80
ENDIF

;LV4からは異常経験必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
IF ABL:従順 >= 7 && !CFLAG:陥落
	ABLUPEXP:50 = 3
ELSEIF ABL:従順 >= 4 && !CFLAG:陥落
	ABLUPEXP:50 = 1
ENDIF

@CHECK_JUEL0
;異常経験が規定値(ABLUPEXP:50)に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:50
	RETURN 0

;屈服の珠(ABLUP:7)、主人調教経験(ABLUPEXP:30)が必要
SIF JUEL:屈服 >= ABLUP:7 && EXP:主人調教経験 >= ABLUPEXP:30
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE0
;LV4からは異常経験必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
SIF EXP:異常経験 < ABLUPEXP:50
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:50
	PRINTFORM [異常経験{ABLUPEXP:50}以上]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:7
	SETCOLOR 120, 120, 120
PRINTFORM [屈服の珠×{ABLUP:7}]
RESETCOLOR

SIF EXP:主人調教経験 < ABLUPEXP:30
	SETCOLOR 120, 120, 120
PRINTFORM [主人調教経験{ABLUPEXP:30}以上]
RESETCOLOR

