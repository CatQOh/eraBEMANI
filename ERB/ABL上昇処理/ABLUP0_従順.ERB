@ABLUP0
SIF ABL:従順 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL0
CALL CHECK_JUEL0
IF RESULT
	CALL NEED_JUEL0
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:従順++
ENDIF

@NEED_JUEL0
SELECTCASE ABL:従順
	CASE 0
		ABLUP:GETNUM(PALAM, "屈服") = 1
		ABLUPEXP:GETNUM(EXP, "調教経験") = 10
	CASE 1
		ABLUP:GETNUM(PALAM, "屈服") = 20
		ABLUPEXP:GETNUM(EXP, "調教経験") = 50
	CASE 2
		ABLUP:GETNUM(PALAM, "屈服") = 1000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 100
	CASE 3
		ABLUP:GETNUM(PALAM, "屈服") = 8000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 150
	CASE 4
		ABLUP:GETNUM(PALAM, "屈服") = 24000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 200
	CASE 5
		ABLUP:GETNUM(PALAM, "屈服") = 30000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 250
	CASE 6
		ABLUP:GETNUM(PALAM, "屈服") = 36000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 300
	CASE 7
		ABLUP:GETNUM(PALAM, "屈服") = 45000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 400
	CASE 8
		ABLUP:GETNUM(PALAM, "屈服") = 55000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 600
	CASE 9
		ABLUP:GETNUM(PALAM, "屈服") = 70000
		ABLUPEXP:GETNUM(EXP, "調教経験") = 800
	CASE IS >= 10
		LOCAL = ABL:従順*ABL:従順
		ABLUP:GETNUM(PALAM, "屈服") = 800 * LOCAL
		ABLUPEXP:GETNUM(EXP, "調教経験") = 10 * LOCAL
ENDSELECT

;素質での補正
;恋慕
SIF TALENT:恋慕
	TIMES ABLUP:GETNUM(PALAM, "屈服") , 0.80
;親愛
SIF TALENT:親愛
	TIMES ABLUP:GETNUM(PALAM, "屈服") , 0.60
;淫乱
SIF TALENT:淫乱
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 1.10
;娼婦
SIF TALENT:娼婦
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 1.30
;服従
IF TALENT:服従
	TIMES ABLUP:GETNUM(PALAM, "屈服") , 0.50
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 0.50
ENDIF
;隷属
IF TALENT:隷属
	TIMES ABLUP:GETNUM(PALAM, "屈服") , 0.20
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 0.20
ENDIF

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETNUM(PALAM, "屈服") , 1.50
		TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 1.50
	ENDIF
ENDIF

;プライド高い、生意気
IF TALENT:プライド高い || TALENT:生意気
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 2.00
;大人しい、素直
ELSEIF TALENT:大人しい || TALENT:素直
	TIMES ABLUPEXP:GETNUM(EXP, "調教経験") , 0.80
ENDIF

;LV4からは異常経験必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
IF ABL:従順 >= 7 && !CFLAG:陥落
	ABLUPEXP:GETNUM(EXP, "異常経験") = 3
ELSEIF ABL:従順 >= 4 && !CFLAG:陥落
	ABLUPEXP:GETNUM(EXP, "異常経験") = 1
ENDIF

@CHECK_JUEL0
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	RETURN 0

;屈服の珠、調教経験が必要
SIF JUEL:屈服 >= ABLUP:GETNUM(PALAM, "屈服") && EXP:主人調教経験 >= ABLUPEXP:GETNUM(EXP, "調教経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE0
;LV4からは異常経験必要 LV7からは異常経験3以上必要（陥落済みなら無視できる）
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "異常経験")
	PRINTFORM [異常経験{ABLUPEXP:GETNUM(EXP, "異常経験")}以上]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:GETNUM(PALAM, "屈服")
	SETCOLOR 120, 120, 120
PRINTFORM [屈服の珠×{ABLUP:GETNUM(PALAM, "屈服")}]
RESETCOLOR

SIF EXP:主人調教経験 < ABLUPEXP:GETNUM(EXP, "調教経験")
	SETCOLOR 120, 120, 120
PRINTFORM [主人調教経験{ABLUPEXP:GETNUM(EXP, "調教経験")}以上]
RESETCOLOR

