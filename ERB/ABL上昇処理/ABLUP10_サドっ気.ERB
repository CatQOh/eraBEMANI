;ABLUP:6 = 欲情の珠必要数
;ABLUPEXP:26 = 嗜虐快楽経験必要数
;ABLUPEXP:50 = 異常経験必要数
@ABLUP10
SIF ABL:サドっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL10
CALL CHECK_JUEL10
IF RESULT
	CALL NEED_JUEL10
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:サドっ気++
ENDIF

@NEED_JUEL10
SELECTCASE ABL:サドっ気
	CASE 0
		ABLUP:6 = 100
		ABLUPEXP:26 = 5
	CASE 1
		ABLUP:6 = 500
		ABLUPEXP:26 = 10
	CASE 2
		ABLUP:6 = 1500
		ABLUPEXP:26 = 20
	CASE 3
		ABLUP:6 = 3000
		ABLUPEXP:26 = 50
	CASE 4
		ABLUP:6 = 5000
		ABLUPEXP:26 = 80
	CASE 5
		ABLUP:6 = 8000
		ABLUPEXP:26 = 100
	CASE 6
		ABLUP:6 = 12000
		ABLUPEXP:26 = 150
	CASE 7
		ABLUP:6 = 15000
		ABLUPEXP:26 = 200
	CASE 8
		ABLUP:6 = 25000
		ABLUPEXP:26 = 250
	CASE 9
		ABLUP:6 = 30000
		ABLUPEXP:26 = 350
	CASE IS >= 10
		LOCAL = ABL:サドっ気*ABL:サドっ気
		ABLUP:6 = 400 * LOCAL
		ABLUPEXP:26 = 4 * LOCAL
ENDSELECT

;一線越えない及び未陥落
IF TALENT:一線越えない && !CFLAG:陥落
	TIMES ABLUP:6 , 2.00
	TIMES ABLUPEXP:26 , 2.00
ENDIF

;サド
IF TALENT:サド
	TIMES ABLUP:6 , 0.50
	TIMES ABLUP:32 , 0.50
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
IF !TALENT:サド
	IF ABL:サドっ気 >= 8
		ABLUPEXP:50 = 5
	ELSEIF ABL:サドっ気 >= 4
		ABLUPEXP:50 = 3
	ELSEIF ABL:サドっ気 >= 3
		ABLUPEXP:50 = 1
	ENDIF
ENDIF

@CHECK_JUEL10
;異常経験が規定値(ABLUPEXP:50)に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:50
	RETURN 0

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	RETURN 0

;欲情の珠(ABLUP:6)、嗜虐快楽経験(ABLUPEXP:26)が必要
SIF JUEL:欲情 >= ABLUP:6 && EXP:嗜虐快楽経験 >= ABLUPEXP:26
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE10
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:50
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:50
	PRINTFORM [異常経験{ABLUPEXP:50}以上]
RESETCOLOR

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	SETCOLOR 120, 120, 120
PRINTFORM [欲望LV{ABL:サドっ気+1}以上]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:6
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:6}]
RESETCOLOR

SIF EXP:嗜虐快楽経験 < ABLUPEXP:26
	SETCOLOR 120, 120, 120
PRINTFORM [嗜虐快楽経験{ABLUPEXP:26}以上]
RESETCOLOR

