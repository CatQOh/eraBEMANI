@ABLUP10
SIF ABL:サドっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL10
CALL CHECK_JUEL10
IF RESULT
	CALL NEED_JUEL10
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:サドっ気++
ENDIF

@NEED_JUEL10
SELECTCASE ABL:サドっ気
	CASE 0
		ABLUP:GETNUM(PALAM, "欲情") = 100
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 5
	CASE 1
		ABLUP:GETNUM(PALAM, "欲情") = 500
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 10
	CASE 2
		ABLUP:GETNUM(PALAM, "欲情") = 1500
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 20
	CASE 3
		ABLUP:GETNUM(PALAM, "欲情") = 3000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 50
	CASE 4
		ABLUP:GETNUM(PALAM, "欲情") = 5000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 80
	CASE 5
		ABLUP:GETNUM(PALAM, "欲情") = 8000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 100
	CASE 6
		ABLUP:GETNUM(PALAM, "欲情") = 12000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 150
	CASE 7
		ABLUP:GETNUM(PALAM, "欲情") = 15000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 200
	CASE 8
		ABLUP:GETNUM(PALAM, "欲情") = 25000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 250
	CASE 9
		ABLUP:GETNUM(PALAM, "欲情") = 30000
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 350
	CASE IS >= 10
		LOCAL = ABL:サドっ気*ABL:サドっ気
		ABLUP:GETNUM(PALAM, "欲情") = 400 * LOCAL
		ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") = 4 * LOCAL
ENDSELECT

;一線越えない及び未陥落
IF TALENT:一線越えない && !CFLAG:陥落
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 2.00
	TIMES ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") , 2.00
ENDIF

;サド
IF TALENT:サド
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.50
	TIMES ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験") , 0.50
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
IF !TALENT:サド
	IF ABL:サドっ気 >= 8
		ABLUPEXP:GETNUM(EXP, "異常経験") = 5
	ELSEIF ABL:サドっ気 >= 4
		ABLUPEXP:GETNUM(EXP, "異常経験") = 3
	ELSEIF ABL:サドっ気 >= 3
		ABLUPEXP:GETNUM(EXP, "異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL10
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	RETURN 0

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	RETURN 0

;欲情の珠、嗜虐快楽経験が必要
SIF JUEL:欲情 >= ABLUP:GETNUM(PALAM, "欲情") && EXP:嗜虐快楽経験 >= ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験")
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE10
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[サド]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "異常経験")
	PRINTFORM [異常経験{ABLUPEXP:GETNUM(EXP, "異常経験")}以上]
RESETCOLOR

;欲望がサドっ気+1レベルでないといけない
SIF ABL:欲望 <= ABL:サドっ気
	SETCOLOR 120, 120, 120
PRINTFORM [欲望LV{ABL:サドっ気+1}以上]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETNUM(PALAM, "欲情")
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:GETNUM(PALAM, "欲情")}]
RESETCOLOR

SIF EXP:嗜虐快楽経験 < ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験")
	SETCOLOR 120, 120, 120
PRINTFORM [嗜虐快楽経験{ABLUPEXP:GETNUM(EXP, "嗜虐快楽経験")}以上]
RESETCOLOR

