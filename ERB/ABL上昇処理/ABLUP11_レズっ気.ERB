@ABLUP11
SIF ABL:レズっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL11
CALL CHECK_JUEL11
IF RESULT
	CALL NEED_JUEL11
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:レズっ気++
ENDIF

@NEED_JUEL11
SELECTCASE ABL:レズっ気
	CASE 0
		ABLUP:GETNUM(PALAM, "快Ｃ") = 100
		ABLUP:GETNUM(PALAM, "欲情") = 200
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 50
	CASE 1
		ABLUP:GETNUM(PALAM, "快Ｃ") = 500
		ABLUP:GETNUM(PALAM, "欲情") = 1000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 200
	CASE 2
		ABLUP:GETNUM(PALAM, "快Ｃ") = 1000
		ABLUP:GETNUM(PALAM, "欲情") = 3000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 500
	CASE 3
		ABLUP:GETNUM(PALAM, "快Ｃ") = 2000
		ABLUP:GETNUM(PALAM, "欲情") = 8000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 1000
	CASE 4
		ABLUP:GETNUM(PALAM, "快Ｃ") = 5000
		ABLUP:GETNUM(PALAM, "欲情") = 20000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 2000
	CASE 5
		ABLUP:GETNUM(PALAM, "快Ｃ") = 6000
		ABLUP:GETNUM(PALAM, "欲情") = 25000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 2300
	CASE 6
		ABLUP:GETNUM(PALAM, "快Ｃ") = 8000
		ABLUP:GETNUM(PALAM, "欲情") = 30000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 2600
	CASE 7
		ABLUP:GETNUM(PALAM, "快Ｃ") = 10000
		ABLUP:GETNUM(PALAM, "欲情") = 35000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 3000
	CASE 8
		ABLUP:GETNUM(PALAM, "快Ｃ") = 15000
		ABLUP:GETNUM(PALAM, "欲情") = 40000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 3500
	CASE 9
		ABLUP:GETNUM(PALAM, "快Ｃ") = 20000
		ABLUP:GETNUM(PALAM, "欲情") = 45000
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 3500
	CASE IS >= 10
		LOCAL = ABL:レズっ気*ABL:レズっ気
		ABLUP:GETNUM(PALAM, "快Ｃ") = 250 * LOCAL
		ABLUP:GETNUM(PALAM, "欲情") = 500 * LOCAL
		ABLUPEXP:GETNUM(EXP, "レズ経験") = 50 * LOCAL
ENDSELECT

;恋慕かつMASTERが女
SIF TALENT:恋慕 && SEX(MASTER) == 2
	TIMES ABLUPEXP:GETNUM(EXP, "レズ経験") , 0.50
;親愛かつMASTERが女
SIF TALENT:親愛 && SEX(MASTER) == 2
	TIMES ABLUPEXP:GETNUM(EXP, "レズ経験") , 0.30
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:GETNUM(PALAM, "快Ｃ") , 0.80
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:GETNUM(PALAM, "快Ｃ") , 0.50
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.50
ENDIF
;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETNUM(PALAM, "快Ｃ") , 2.00
		TIMES ABLUP:GETNUM(PALAM, "欲情") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETNUM(PALAM, "快Ｃ") , 0.70
		TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.70
	ENDIF
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀]なら無視できる）
IF !TALENT:両刀
	IF ABL:レズっ気 >= 8
		ABLUPEXP:GETNUM(EXP, "異常経験") = 5
	ELSEIF ABL:レズっ気 >= 4
		ABLUPEXP:GETNUM(EXP, "異常経験") = 3
	ELSEIF ABL:レズっ気 >= 3
		ABLUPEXP:GETNUM(EXP, "異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL11
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	RETURN 0

;快Ｃの珠、欲情の珠、レズ経験が必要
SIF JUEL:快Ｃ >= ABLUP:GETNUM(PALAM, "快Ｃ") && JUEL:欲情 >= ABLUP:GETNUM(PALAM, "欲情") && EXP:レズ経験 >= ABLUPEXP:GETNUM(EXP, "レズ経験")
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE11
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "異常経験")
	PRINTFORM [異常経験{ABLUPEXP:GETNUM(EXP, "異常経験")}以上]
RESETCOLOR

SIF JUEL:快Ｃ < ABLUP:GETNUM(PALAM, "快Ｃ")
	SETCOLOR 120, 120, 120
PRINTFORM [快Ｃの珠×{ABLUP:GETNUM(PALAM, "快Ｃ")}]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETNUM(PALAM, "欲情")
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:GETNUM(PALAM, "欲情")}]
RESETCOLOR

SIF EXP:レズ経験 < ABLUPEXP:GETNUM(EXP, "レズ経験")
	SETCOLOR 120, 120, 120
PRINTFORM [レズ経験{ABLUPEXP:GETNUM(EXP, "レズ経験")}以上]
RESETCOLOR

