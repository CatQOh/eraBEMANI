;ABLUP:0 = 快Ｃの珠必要数
;ABLUP:6 = 欲情の珠必要数
;ABLUPEXP:40 = レズ経験必要数
;ABLUPEXP:50 = 異常経験必要数
@ABLUP11
SIF ABL:レズっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL11
CALL CHECK_JUEL11
IF RESULT
	CALL NEED_JUEL11
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:レズっ気++
ENDIF

@NEED_JUEL11
SELECTCASE ABL:レズっ気
	CASE 0
		ABLUP:0 = 100
		ABLUP:6 = 200
		ABLUPEXP:40 = 50
	CASE 1
		ABLUP:0 = 500
		ABLUP:6 = 1000
		ABLUPEXP:40 = 200
	CASE 2
		ABLUP:0 = 1000
		ABLUP:6 = 3000
		ABLUPEXP:40 = 500
	CASE 3
		ABLUP:0 = 2000
		ABLUP:6 = 8000
		ABLUPEXP:40 = 1000
	CASE 4
		ABLUP:0 = 5000
		ABLUP:6 = 20000
		ABLUPEXP:40 = 2000
	CASE 5
		ABLUP:0 = 6000
		ABLUP:6 = 25000
		ABLUPEXP:40 = 2300
	CASE 6
		ABLUP:0 = 8000
		ABLUP:6 = 30000
		ABLUPEXP:40 = 2600
	CASE 7
		ABLUP:0 = 10000
		ABLUP:6 = 35000
		ABLUPEXP:40 = 3000
	CASE 8
		ABLUP:0 = 15000
		ABLUP:6 = 40000
		ABLUPEXP:40 = 3500
	CASE 9
		ABLUP:0 = 20000
		ABLUP:6 = 45000
		ABLUPEXP:40 = 3500
	CASE IS >= 10
		LOCAL = ABL:レズっ気*ABL:レズっ気
		ABLUP:0 = 250 * LOCAL
		ABLUP:6 = 500 * LOCAL
		ABLUPEXP:40 = 50 * LOCAL
ENDSELECT

;恋慕かつMASTERが女
SIF TALENT:恋慕 && SEX(MASTER) == 2
	TIMES ABLUPEXP:40 , 0.50
;親愛かつMASTERが女
SIF TALENT:親愛 && SEX(MASTER) == 2
	TIMES ABLUPEXP:40 , 0.30
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:0 , 0.80
	TIMES ABLUP:6 , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:0 , 0.50
	TIMES ABLUP:6 , 0.50
ENDIF
;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:0 , 2.00
		TIMES ABLUP:6 , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:0 , 0.70
		TIMES ABLUP:6 , 0.70
	ENDIF
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀]なら無視できる）
IF !TALENT:両刀
	IF ABL:レズっ気 >= 8
		ABLUPEXP:50 = 5
	ELSEIF ABL:レズっ気 >= 4
		ABLUPEXP:50 = 3
	ELSEIF ABL:レズっ気 >= 3
		ABLUPEXP:50 = 1
	ENDIF
ENDIF

@CHECK_JUEL11
;異常経験が規定値(ABLUPEXP:50)に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:50
	RETURN 0

;快Ｃの珠(ABLUP:0)、欲情の珠(ABLUP:6)、レズ経験(ABLUPEXP:40)が必要
SIF JUEL:快Ｃ >= ABLUP:0 && JUEL:欲情 >= ABLUP:6 && EXP:レズ経験 >= ABLUPEXP:40
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE11
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:50
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:50
	PRINTFORM [異常経験{ABLUPEXP:50}以上]
RESETCOLOR

SIF JUEL:快Ｃ < ABLUP:0
	SETCOLOR 120, 120, 120
PRINTFORM [快Ｃの珠×{ABLUP:0}]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:6
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:6}]
RESETCOLOR

SIF EXP:レズ経験 < ABLUPEXP:40
	SETCOLOR 120, 120, 120
PRINTFORM [レズ経験{ABLUPEXP:40}以上]
RESETCOLOR

