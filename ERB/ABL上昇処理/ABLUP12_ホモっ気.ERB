@ABLUP12
#DIM DYNAMIC LCOUNT
SIF ABL:ホモっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:ホモっ気 < 0
	RETURN 0
CALL NEED_JUEL_ホモっ気
CALL CHECK_JUEL_ホモっ気
IF RESULT
	CALL NEED_JUEL_ホモっ気
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:ホモっ気++
ENDIF

@NEED_JUEL_ホモっ気
SELECTCASE ABL:ホモっ気
	CASE 0
		ABLUP:GETPALAM("快Ａ") = 100
		ABLUP:GETPALAM("欲情") = 200
		ABLUPEXP:GETEXP("ゲイ経験") = 50
	CASE 1
		ABLUP:GETPALAM("快Ａ") = 500
		ABLUP:GETPALAM("欲情") = 1000
		ABLUPEXP:GETEXP("ゲイ経験") = 200
	CASE 2
		ABLUP:GETPALAM("快Ａ") = 1000
		ABLUP:GETPALAM("欲情") = 3000
		ABLUPEXP:GETEXP("ゲイ経験") = 500
	CASE 3
		ABLUP:GETPALAM("快Ａ") = 2000
		ABLUP:GETPALAM("欲情") = 8000
		ABLUPEXP:GETEXP("ゲイ経験") = 1000
	CASE 4
		ABLUP:GETPALAM("快Ａ") = 5000
		ABLUP:GETPALAM("欲情") = 20000
		ABLUPEXP:GETEXP("ゲイ経験") = 2000
	CASE 5
		ABLUP:GETPALAM("快Ａ") = 6000
		ABLUP:GETPALAM("欲情") = 25000
		ABLUPEXP:GETEXP("ゲイ経験") = 2300
	CASE 6
		ABLUP:GETPALAM("快Ａ") = 8000
		ABLUP:GETPALAM("欲情") = 30000
		ABLUPEXP:GETEXP("ゲイ経験") = 2600
	CASE 7
		ABLUP:GETPALAM("快Ａ") = 10000
		ABLUP:GETPALAM("欲情") = 35000
		ABLUPEXP:GETEXP("ゲイ経験") = 3000
	CASE 8
		ABLUP:GETPALAM("快Ａ") = 15000
		ABLUP:GETPALAM("欲情") = 40000
		ABLUPEXP:GETEXP("ゲイ経験") = 3500
	CASE 9
		ABLUP:GETPALAM("快Ａ") = 20000
		ABLUP:GETPALAM("欲情") = 45000
		ABLUPEXP:GETEXP("ゲイ経験") = 4000
	CASE IS >= 10
		LOCAL = ABL:ホモっ気*ABL:ホモっ気
		ABLUP:GETPALAM("快Ａ") = 250 * LOCAL
		ABLUP:GETPALAM("欲情") = 500 * LOCAL
		ABLUPEXP:GETEXP("ゲイ経験") = 50 * LOCAL
ENDSELECT

;恋慕かつMASTERが男
SIF TALENT:恋慕 && SEX(MASTER) == 1
	TIMES ABLUPEXP:GETEXP("ゲイ経験") , 0.50
;親愛かつMASTERが男
SIF TALENT:親愛 && SEX(MASTER) == 1
	TIMES ABLUPEXP:GETEXP("ゲイ経験") , 0.30
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:GETPALAM("快Ａ") , 0.80
	TIMES ABLUP:GETPALAM("欲情") , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:GETPALAM("快Ａ") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
ENDIF
;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ａ") , 2.00
		TIMES ABLUP:GETPALAM("欲情") , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("快Ａ") , 0.70
		TIMES ABLUP:GETPALAM("欲情") , 0.70
	ENDIF
ENDIF
;男嫌い
IF TALENT:男嫌い
	TIMES ABLUPEXP:GETEXP("ゲイ経験") , 1.50
	TIMES ABLUP:GETPALAM("欲情") , 1.50
ENDIF
;男好き
IF TALENT:男好き
	TIMES ABLUPEXP:GETEXP("ゲイ経験") , 0.50
	TIMES ABLUP:GETPALAM("欲情") , 0.50
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀][男好き]なら無視できる）
IF !TALENT:両刀 && !TALENT:男好き
	IF ABL:ホモっ気 >= 8
		ABLUPEXP:GETEXP("異常経験") = 5
	ELSEIF ABL:ホモっ気 >= 4
		ABLUPEXP:GETEXP("異常経験") = 3
	ELSEIF ABL:ホモっ気 >= 3
		ABLUPEXP:GETEXP("異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL_ホモっ気
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0

;快Ａの珠、欲情の珠、ゲイ経験が必要
SIF JUEL:快Ａ >= ABLUP:GETPALAM("快Ａ") && JUEL:欲情 >= ABLUP:GETPALAM("欲情") && EXP:ゲイ経験 >= ABLUPEXP:GETEXP("ゲイ経験")
	RETURN 1

;上記の条件を満たしていないと上げられない
RETURN 0

@ABLUP_MESSAGE_ホモっ気
SELECTCASE ABL:ホモっ気
	CASE 3, 4, 8
		;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[両刀]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLORBYNAME gray
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF JUEL:快Ａ < ABLUP:GETPALAM("快Ａ")
	SETCOLORBYNAME gray
PRINTFORM [快Ａの珠×{ABLUP:GETPALAM("快Ａ")}]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETPALAM("欲情")
	SETCOLORBYNAME gray
PRINTFORM [欲情の珠×{ABLUP:GETPALAM("欲情")}]
RESETCOLOR

SIF EXP:ゲイ経験 < ABLUPEXP:GETEXP("ゲイ経験")
	SETCOLORBYNAME gray
PRINTFORM [ゲイ経験{ABLUPEXP:GETEXP("ゲイ経験")}以上]
RESETCOLOR

