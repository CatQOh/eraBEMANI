@ABLUP20
SIF ABL:料理技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL20
CALL CHECK_JUEL20
IF RESULT
	CALL NEED_JUEL20
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:料理技能++
ENDIF

@NEED_JUEL20
SELECTCASE ABL:料理技能
	CASE 0
		ABLUP:GETNUM(PALAM, "習得") = 75
		ABLUPEXP:GETNUM(EXP, "料理経験") = 5
	CASE 1
		ABLUP:GETNUM(PALAM, "習得") = 500
		ABLUPEXP:GETNUM(EXP, "料理経験") = 30
	CASE 2
		ABLUP:GETNUM(PALAM, "習得") = 3000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 100
	CASE 3
		ABLUP:GETNUM(PALAM, "習得") = 9000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 300
	CASE 4
		ABLUP:GETNUM(PALAM, "習得") = 24000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 500
	CASE 5
		ABLUP:GETNUM(PALAM, "習得") = 30000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 1000
	CASE 6
		ABLUP:GETNUM(PALAM, "習得") = 35000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 2000
	CASE 7
		ABLUP:GETNUM(PALAM, "習得") = 40000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 4000
	CASE 8
		ABLUP:GETNUM(PALAM, "習得") = 45000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 7000
	CASE 9
		ABLUP:GETNUM(PALAM, "習得") = 50000
		ABLUPEXP:GETNUM(EXP, "料理経験") = 10000
	CASE IS >= 10
		LOCAL = ABL:料理技能*ABL:料理技能
		ABLUP:GETNUM(PALAM, "習得") = 600 * LOCAL
		ABLUPEXP:GETNUM(EXP, "料理経験") = 100 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:GETNUM(PALAM, "習得") , 0.80
	TIMES ABLUPEXP:GETNUM(EXP, "料理経験") , 0.80
ENDIF

;習得遅いもしくは無関心
IF TALENT:習得遅い || TALENT:無関心
	TIMES ABLUP:GETNUM(PALAM, "習得") , 1.30
	TIMES ABLUPEXP:GETNUM(EXP, "料理経験") , 1.30
ENDIF

@CHECK_JUEL20
;習得の珠、料理経験で上げる
SIF JUEL:習得 >= ABLUP:GETNUM(PALAM, "習得") && EXP:料理経験 >= ABLUPEXP:GETNUM(EXP, "料理経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE20
SIF JUEL:習得 < ABLUP:GETNUM(PALAM, "習得")
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:GETNUM(PALAM, "習得")}]
RESETCOLOR

SIF EXP:料理経験 < ABLUPEXP:GETNUM(EXP, "料理経験")
	SETCOLOR 120, 120, 120
PRINTFORM [料理経験{ABLUPEXP:GETNUM(EXP, "料理経験")}以上]
RESETCOLOR

