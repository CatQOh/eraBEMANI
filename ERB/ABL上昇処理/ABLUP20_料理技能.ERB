@ABLUP20
#DIM DYNAMIC LCOUNT
SIF ABL:料理技能 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:料理技能 < 0
	RETURN 0
CALL NEED_JUEL_料理技能
CALL CHECK_JUEL_料理技能
IF RESULT
	CALL NEED_JUEL_料理技能
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:料理技能++
ENDIF

@NEED_JUEL_料理技能
SELECTCASE ABL:料理技能
	CASE 0
		ABLUP:GETPALAM("習得") = 75
		ABLUPEXP:GETEXP("料理経験") = 5
	CASE 1
		ABLUP:GETPALAM("習得") = 500
		ABLUPEXP:GETEXP("料理経験") = 30
	CASE 2
		ABLUP:GETPALAM("習得") = 3000
		ABLUPEXP:GETEXP("料理経験") = 100
	CASE 3
		ABLUP:GETPALAM("習得") = 9000
		ABLUPEXP:GETEXP("料理経験") = 300
	CASE 4
		ABLUP:GETPALAM("習得") = 24000
		ABLUPEXP:GETEXP("料理経験") = 500
	CASE 5
		ABLUP:GETPALAM("習得") = 30000
		ABLUPEXP:GETEXP("料理経験") = 1000
	CASE 6
		ABLUP:GETPALAM("習得") = 35000
		ABLUPEXP:GETEXP("料理経験") = 2000
	CASE 7
		ABLUP:GETPALAM("習得") = 40000
		ABLUPEXP:GETEXP("料理経験") = 4000
	CASE 8
		ABLUP:GETPALAM("習得") = 45000
		ABLUPEXP:GETEXP("料理経験") = 7000
	CASE 9
		ABLUP:GETPALAM("習得") = 50000
		ABLUPEXP:GETEXP("料理経験") = 10000
	CASE IS >= 10
		ABLUP:GETPALAM("習得") = 600 * POWER(ABL:料理技能, 2)
ENDSELECT

;習得早い、好奇心、調合知識、食欲皆伝
IF TALENT:習得早い || TALENT:好奇心 || TALENT:調合知識 || TALENT:食欲皆伝
	TIMES ABLUP:GETPALAM("習得") , 0.80
	TIMES ABLUPEXP:GETEXP("料理経験") , 0.80
ENDIF

;習得遅いもしくは無関心
IF TALENT:習得遅い || TALENT:無関心
	TIMES ABLUP:GETPALAM("習得") , 1.30
	TIMES ABLUPEXP:GETEXP("料理経験") , 1.30
ENDIF

@CHECK_JUEL_料理技能
;習得の珠、料理経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:料理経験 >= ABLUPEXP:GETEXP("料理経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_料理技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

IF ABLUPEXP:GETEXP("料理経験")
	SIF EXP:料理経験 < ABLUPEXP:GETEXP("料理経験")
		SETCOLORBYNAME gray
	PRINTFORM [料理経験{ABLUPEXP:GETEXP("料理経験")}以上]
	RESETCOLOR
ENDIF
