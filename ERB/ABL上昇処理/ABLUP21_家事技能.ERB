@ABLUP21
#DIM DYNAMIC LCOUNT
SIF ABL:家事技能 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:家事技能 < 0
	RETURN 0
CALL NEED_JUEL_家事技能
CALL CHECK_JUEL_家事技能
IF RESULT
	CALL NEED_JUEL_家事技能
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:家事技能++
ENDIF

@NEED_JUEL_家事技能
SELECTCASE ABL:家事技能
	CASE 0
		ABLUP:GETPALAM("習得") = 100
		ABLUPEXP:GETEXP("家事経験") = 5
	CASE 1
		ABLUP:GETPALAM("習得") = 500
		ABLUPEXP:GETEXP("家事経験") = 20
	CASE 2
		ABLUP:GETPALAM("習得") = 1000
		ABLUPEXP:GETEXP("家事経験") = 50
	CASE 3
		ABLUP:GETPALAM("習得") = 5000
		ABLUPEXP:GETEXP("家事経験") = 100
	CASE 4
		ABLUP:GETPALAM("習得") = 10000
		ABLUPEXP:GETEXP("家事経験") = 200
	CASE 5
		ABLUP:GETPALAM("習得") = 20000
		ABLUPEXP:GETEXP("家事経験") = 300
	CASE 6
		ABLUP:GETPALAM("習得") = 40000
		ABLUPEXP:GETEXP("家事経験") = 500
	CASE 7
		ABLUP:GETPALAM("習得") = 70000
		ABLUPEXP:GETEXP("家事経験") = 1000
	CASE 8
		ABLUP:GETPALAM("習得") = 150000
		ABLUPEXP:GETEXP("家事経験") = 1500
	CASE 9
		ABLUP:GETPALAM("習得") = 300000
		ABLUPEXP:GETEXP("家事経験") = 2000
	CASE IS >= 10
		ABLUP:GETPALAM("習得") = 3500 * POWER(ABL:家事技能, 2)
ENDSELECT

;習得早い、好奇心、潔癖症
IF TALENT:習得早い || TALENT:好奇心 || TALENT:潔癖症
	TIMES ABLUP:GETPALAM("習得") , 0.80
	TIMES ABLUPEXP:GETEXP("家事経験") , 0.80
ENDIF
;習得遅い、無関心、汚れ無視
IF TALENT:習得遅い || TALENT:無関心 || TALENT:汚れ無視
	TIMES ABLUP:GETPALAM("習得") , 1.30
	TIMES ABLUPEXP:GETEXP("家事経験") , 1.30
ENDIF
;母性もしくは恋慕、親愛
IF TALENT:母性 || TALENT:恋慕 || TALENT:親愛
	TIMES ABLUP:GETPALAM("習得") , 0.50
	TIMES ABLUPEXP:GETEXP("家事経験") , 0.50
ENDIF

@CHECK_JUEL_家事技能
;習得の珠、家事経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:家事経験 >= ABLUPEXP:GETEXP("家事経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_家事技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

IF ABLUPEXP:GETEXP("家事経験")
	SIF EXP:家事経験 < ABLUPEXP:GETEXP("家事経験")
		SETCOLORBYNAME gray
	PRINTFORM [家事経験{ABLUPEXP:GETEXP("家事経験")}以上]
	RESETCOLOR
ENDIF
