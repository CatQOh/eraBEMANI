;ABLUP:8 = 習得の珠必要数
;ABLUPEXP:101 = 家事経験必要数
@ABLUP21
SIF ABL:家事技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL21
CALL CHECK_JUEL21
IF RESULT
	CALL NEED_JUEL21
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:家事技能++
ENDIF

@NEED_JUEL21
SELECTCASE ABL:家事技能
	CASE 0
		ABLUP:8 = 100
		ABLUPEXP:101 = 5
	CASE 1
		ABLUP:8 = 500
		ABLUPEXP:101 = 20
	CASE 2
		ABLUP:8 = 1000
		ABLUPEXP:101 = 50
	CASE 3
		ABLUP:8 = 5000
		ABLUPEXP:101 = 100
	CASE 4
		ABLUP:8 = 10000
		ABLUPEXP:101 = 200
	CASE 5
		ABLUP:8 = 20000
		ABLUPEXP:101 = 300
	CASE 6
		ABLUP:8 = 40000
		ABLUPEXP:101 = 500
	CASE 7
		ABLUP:8 = 70000
		ABLUPEXP:101 = 1000
	CASE 8
		ABLUP:8 = 150000
		ABLUPEXP:101 = 1500
	CASE 9
		ABLUP:8 = 300000
		ABLUPEXP:101 = 2000
	CASE IS >= 10
		LOCAL = ABL:家事技能*ABL:家事技能
		ABLUP:8 = 3500 * LOCAL
		ABLUPEXP:101 = 30 * LOCAL
ENDSELECT

;習得早い、好奇心、潔癖症
IF TALENT:習得早い || TALENT:好奇心 || TALENT:潔癖症
	TIMES ABLUP:8 , 0.80
	TIMES ABLUPEXP:101 , 0.80
ENDIF

;習得遅い、無関心、汚れ無視
IF TALENT:習得遅い || TALENT:無関心 || TALENT:汚れ無視
	TIMES ABLUP:8 , 1.30
	TIMES ABLUPEXP:101 , 1.30
ENDIF

;母性もしくは恋慕、親愛
IF TALENT:母性 || TALENT:恋慕 || TALENT:親愛
	TIMES ABLUP:8 , 0.50
	TIMES ABLUPEXP:101 , 0.50
ENDIF

@CHECK_JUEL21
;習得の珠(ABLUP:8)、家事経験(ABLUPEXP:101)で上げる
SIF JUEL:習得 >= ABLUP:8 && EXP:家事経験 >= ABLUPEXP:101
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE21
SIF JUEL:習得 < ABLUP:8
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:8}]
RESETCOLOR

SIF EXP:家事経験 < ABLUPEXP:101
	SETCOLOR 120, 120, 120
PRINTFORM [家事経験{ABLUPEXP:101}以上]
RESETCOLOR

