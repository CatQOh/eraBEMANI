;ABLUP:8 = 習得の珠必要数
;ABLUPEXP:61 = 家事経験必要数
@ABLUP21
SIF ABL:料理技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL21
CALL CHECK_JUEL21
IF RESULT
	CALL NEED_JUEL21
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:料理技能++
ENDIF

@NEED_JUEL21
SELECTCASE ABL:料理技能
	CASE 0
		ABLUP:8 = 75
		ABLUPEXP:61 = 5
	CASE 1
		ABLUP:8 = 500
		ABLUPEXP:61 = 10
	CASE 2
		ABLUP:8 = 3000
		ABLUPEXP:61 = 30
	CASE 3
		ABLUP:8 = 9000
		ABLUPEXP:61 = 50
	CASE 4
		ABLUP:8 = 24000
		ABLUPEXP:61 = 100
	CASE 5
		ABLUP:8 = 30000
		ABLUPEXP:61 = 200
	CASE 6
		ABLUP:8 = 35000
		ABLUPEXP:61 = 300
	CASE 7
		ABLUP:8 = 40000
		ABLUPEXP:61 = 400
	CASE 8
		ABLUP:8 = 45000
		ABLUPEXP:61 = 500
	CASE 9
		ABLUP:8 = 50000
		ABLUPEXP:61 = 600
	CASE IS >= 10
		LOCAL = ABL:料理技能*ABL:料理技能
		ABLUP:8 = 600 * LOCAL
		ABLUPEXP:61 = 7 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:8 , 0.80
	TIMES ABLUPEXP:61 , 0.80
ENDIF

;習得遅いもしくは無関心
IF TALENT:習得遅い || TALENT:無関心
	TIMES ABLUP:8 , 1.30
	TIMES ABLUPEXP:61 , 1.30
ENDIF

@CHECK_JUEL21
;習得の珠(ABLUP:8)、家事経験(ABLUPEXP:61)で上げる
SIF JUEL:習得 >= ABLUP:8 && EXP:家事経験 >= ABLUPEXP:61
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE21
SIF JUEL:習得 < ABLUP:8
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:8}]
RESETCOLOR

SIF EXP:家事経験 < ABLUPEXP:61
	SETCOLOR 120, 120, 120
PRINTFORM [家事経験{ABLUPEXP:61}以上]
RESETCOLOR

