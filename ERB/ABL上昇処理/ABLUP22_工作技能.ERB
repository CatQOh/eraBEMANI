;ABLUP:8 = 習得の珠必要数
;ABLUPEXP:102 = 工作経験必要数
@ABLUP22
SIF ABL:工作技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL22
CALL CHECK_JUEL22
IF RESULT
	CALL NEED_JUEL22
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:工作技能++
ENDIF

@NEED_JUEL22
SELECTCASE ABL:工作技能
	CASE 0
		ABLUP:8 = 100
		ABLUPEXP:102 = 5
	CASE 1
		ABLUP:8 = 500
		ABLUPEXP:102 = 30
	CASE 2
		ABLUP:8 = 1000
		ABLUPEXP:102 = 100
	CASE 3
		ABLUP:8 = 5000
		ABLUPEXP:102 = 300
	CASE 4
		ABLUP:8 = 10000
		ABLUPEXP:102 = 500
	CASE 5
		ABLUP:8 = 20000
		ABLUPEXP:102 = 1000
	CASE 6
		ABLUP:8 = 40000
		ABLUPEXP:102 = 2000
	CASE 7
		ABLUP:8 = 70000
		ABLUPEXP:102 = 4000
	CASE 8
		ABLUP:8 = 150000
		ABLUPEXP:102 = 7000
	CASE 9
		ABLUP:8 = 300000
		ABLUPEXP:102 = 10000
	CASE IS >= 10
		LOCAL = ABL:工作技能*ABL:工作技能
		ABLUP:8 = 3500 * LOCAL
		ABLUPEXP:102 = 100 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:8 , 0.80
	TIMES ABLUPEXP:102 , 0.80
ENDIF

;習得遅いもしくは無関心
IF TALENT:習得遅い || TALENT:無関心
	TIMES ABLUP:8 , 1.30
	TIMES ABLUPEXP:102 , 1.30
ENDIF

;種族:機械、TALENT:無機物はめちゃくちゃ強い
IF CSTR:種族 == "機械" || TALENT:無機物
	TIMES ABLUP:8 , 0.20
	TIMES ABLUPEXP:102 , 0.20
ENDIF

@CHECK_JUEL22
;習得の珠(ABLUP:8)、工作経験(ABLUPEXP:102)で上げる
SIF JUEL:習得 >= ABLUP:8 && EXP:工作経験 >= ABLUPEXP:102
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE22
SIF JUEL:習得 < ABLUP:8
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:8}]
RESETCOLOR

SIF EXP:工作経験 < ABLUPEXP:102
	SETCOLOR 120, 120, 120
PRINTFORM [工作経験{ABLUPEXP:102}以上]
RESETCOLOR

