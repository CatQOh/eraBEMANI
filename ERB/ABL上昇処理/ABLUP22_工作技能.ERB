@ABLUP22
SIF ABL:工作技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL22
CALL CHECK_JUEL22
IF RESULT
	CALL NEED_JUEL22
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:工作技能++
ENDIF

@NEED_JUEL22
SELECTCASE ABL:工作技能
	CASE 0
		ABLUP:GETNUM(PALAM, "習得") = 100
		ABLUPEXP:GETNUM(EXP, "工作経験") = 5
	CASE 1
		ABLUP:GETNUM(PALAM, "習得") = 500
		ABLUPEXP:GETNUM(EXP, "工作経験") = 30
	CASE 2
		ABLUP:GETNUM(PALAM, "習得") = 1000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 100
	CASE 3
		ABLUP:GETNUM(PALAM, "習得") = 5000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 300
	CASE 4
		ABLUP:GETNUM(PALAM, "習得") = 10000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 500
	CASE 5
		ABLUP:GETNUM(PALAM, "習得") = 20000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 1000
	CASE 6
		ABLUP:GETNUM(PALAM, "習得") = 40000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 2000
	CASE 7
		ABLUP:GETNUM(PALAM, "習得") = 70000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 4000
	CASE 8
		ABLUP:GETNUM(PALAM, "習得") = 150000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 7000
	CASE 9
		ABLUP:GETNUM(PALAM, "習得") = 300000
		ABLUPEXP:GETNUM(EXP, "工作経験") = 10000
	CASE IS >= 10
		LOCAL = ABL:工作技能*ABL:工作技能
		ABLUP:GETNUM(PALAM, "習得") = 3500 * LOCAL
		ABLUPEXP:GETNUM(EXP, "工作経験") = 100 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:GETNUM(PALAM, "習得") , 0.80
	TIMES ABLUPEXP:GETNUM(EXP, "工作経験") , 0.80
ENDIF

;習得遅いもしくは無関心
IF TALENT:習得遅い || TALENT:無関心
	TIMES ABLUP:GETNUM(PALAM, "習得") , 1.30
	TIMES ABLUPEXP:GETNUM(EXP, "工作経験") , 1.30
ENDIF

;種族:機械、TALENT:無機物はめちゃくちゃ強い
IF CSTR:種族 == "機械" || TALENT:無機物
	TIMES ABLUP:GETNUM(PALAM, "習得") , 0.20
	TIMES ABLUPEXP:GETNUM(EXP, "工作経験") , 0.20
ENDIF

@CHECK_JUEL22
;習得の珠、工作経験で上げる
SIF JUEL:習得 >= ABLUP:GETNUM(PALAM, "習得") && EXP:工作経験 >= ABLUPEXP:GETNUM(EXP, "工作経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE22
SIF JUEL:習得 < ABLUP:GETNUM(PALAM, "習得")
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:GETNUM(PALAM, "習得")}]
RESETCOLOR

SIF EXP:工作経験 < ABLUPEXP:GETNUM(EXP, "工作経験")
	SETCOLOR 120, 120, 120
PRINTFORM [工作経験{ABLUPEXP:GETNUM(EXP, "工作経験")}以上]
RESETCOLOR

