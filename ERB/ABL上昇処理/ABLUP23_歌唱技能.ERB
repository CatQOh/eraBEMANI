;ABLUP:8 = 習得の珠必要数
;ABLUPEXP:103 = 歌唱経験必要数
@ABLUP23
SIF ABL:歌唱技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL23
CALL CHECK_JUEL23
IF RESULT
	CALL NEED_JUEL23
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:歌唱技能++
ENDIF

@NEED_JUEL23
SELECTCASE ABL:歌唱技能
	CASE 0
		ABLUP:8 = 75
		ABLUPEXP:103 = 5
	CASE 1
		ABLUP:8 = 500
		ABLUPEXP:103 = 20
	CASE 2
		ABLUP:8 = 3000
		ABLUPEXP:103 = 50
	CASE 3
		ABLUP:8 = 9000
		ABLUPEXP:103 = 100
	CASE 4
		ABLUP:8 = 24000
		ABLUPEXP:103 = 300
	CASE 5
		ABLUP:8 = 30000
		ABLUPEXP:103 = 700
	CASE 6
		ABLUP:8 = 35000
		ABLUPEXP:103 = 1000
	CASE 7
		ABLUP:8 = 40000
		ABLUPEXP:103 = 1500
	CASE 8
		ABLUP:8 = 45000
		ABLUPEXP:103 = 2000
	CASE 9
		ABLUP:8 = 50000
		ABLUPEXP:103 = 3000
	CASE IS >= 10
		LOCAL = ABL:歌唱技能*ABL:歌唱技能
		ABLUP:8 = 600 * LOCAL
		ABLUPEXP:103 = 50 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:8 , 0.80
	TIMES ABLUPEXP:103 , 0.80
ENDIF

;習得遅い、無関心、恥じらい
IF TALENT:習得遅い || TALENT:無関心 || TALENT:恥じらい
	TIMES ABLUP:8 , 1.30
	TIMES ABLUPEXP:103 , 1.30
ENDIF

;絶対音感なら半分
IF TALENT:絶対音感
	TIMES ABLUP:8 , 0.50
	TIMES ABLUPEXP:103 , 0.50
ENDIF

@CHECK_JUEL23
;習得の珠(ABLUP:8)、歌唱経験(ABLUPEXP:103)で上げる
SIF JUEL:習得 >= ABLUP:8 && EXP:歌唱経験 >= ABLUPEXP:103
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE23
SIF JUEL:習得 < ABLUP:8
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:8}]
RESETCOLOR

SIF EXP:歌唱経験 < ABLUPEXP:103
	SETCOLOR 120, 120, 120
PRINTFORM [歌唱経験{ABLUPEXP:103}以上]
RESETCOLOR

