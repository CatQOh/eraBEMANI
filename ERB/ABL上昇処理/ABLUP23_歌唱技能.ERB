@ABLUP23
SIF ABL:歌唱技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL_歌唱技能
CALL CHECK_JUEL_歌唱技能
IF RESULT
	CALL NEED_JUEL_歌唱技能
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:歌唱技能++
ENDIF

@NEED_JUEL_歌唱技能
SELECTCASE ABL:歌唱技能
	CASE 0
		ABLUP:GETPALAM("習得") = 75
		ABLUPEXP:GETEXP("歌唱経験") = 5
	CASE 1
		ABLUP:GETPALAM("習得") = 500
		ABLUPEXP:GETEXP("歌唱経験") = 20
	CASE 2
		ABLUP:GETPALAM("習得") = 3000
		ABLUPEXP:GETEXP("歌唱経験") = 50
	CASE 3
		ABLUP:GETPALAM("習得") = 9000
		ABLUPEXP:GETEXP("歌唱経験") = 100
	CASE 4
		ABLUP:GETPALAM("習得") = 24000
		ABLUPEXP:GETEXP("歌唱経験") = 300
	CASE 5
		ABLUP:GETPALAM("習得") = 30000
		ABLUPEXP:GETEXP("歌唱経験") = 700
	CASE 6
		ABLUP:GETPALAM("習得") = 35000
		ABLUPEXP:GETEXP("歌唱経験") = 1000
	CASE 7
		ABLUP:GETPALAM("習得") = 40000
		ABLUPEXP:GETEXP("歌唱経験") = 1500
	CASE 8
		ABLUP:GETPALAM("習得") = 45000
		ABLUPEXP:GETEXP("歌唱経験") = 2000
	CASE 9
		ABLUP:GETPALAM("習得") = 50000
		ABLUPEXP:GETEXP("歌唱経験") = 3000
	CASE IS >= 10
		LOCAL = ABL:歌唱技能*ABL:歌唱技能
		ABLUP:GETPALAM("習得") = 600 * LOCAL
		ABLUPEXP:GETEXP("歌唱経験") = 50 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:GETPALAM("習得") , 0.80
	TIMES ABLUPEXP:GETEXP("歌唱経験") , 0.80
ENDIF

;習得遅い、無関心、恥じらい
IF TALENT:習得遅い || TALENT:無関心 || TALENT:恥じらい
	TIMES ABLUP:GETPALAM("習得") , 1.30
	TIMES ABLUPEXP:GETEXP("歌唱経験") , 1.30
ENDIF

;絶対音感なら半分
IF TALENT:絶対音感
	TIMES ABLUP:GETPALAM("習得") , 0.50
	TIMES ABLUPEXP:GETEXP("歌唱経験") , 0.50
ENDIF

@CHECK_JUEL_歌唱技能
;習得の珠、歌唱経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:歌唱経験 >= ABLUPEXP:GETEXP("歌唱経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_歌唱技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

SIF EXP:歌唱経験 < ABLUPEXP:GETEXP("歌唱経験")
	SETCOLORBYNAME gray
PRINTFORM [歌唱経験{ABLUPEXP:GETEXP("歌唱経験")}以上]
RESETCOLOR

