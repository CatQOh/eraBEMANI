@ABLUP26
#DIM DYNAMIC LCOUNT
SIF ABL:舞踊技能 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:舞踊技能 < 0
	RETURN 0
CALL NEED_JUEL_舞踊技能
CALL CHECK_JUEL_舞踊技能
IF RESULT
	CALL NEED_JUEL_舞踊技能
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:舞踊技能++
ENDIF

@NEED_JUEL_舞踊技能
SELECTCASE ABL:舞踊技能
	CASE 0
		ABLUP:GETPALAM("習得") = 75
		ABLUPEXP:GETEXP("ダンス経験") = 5
	CASE 1
		ABLUP:GETPALAM("習得") = 500
		ABLUPEXP:GETEXP("ダンス経験") = 20
	CASE 2
		ABLUP:GETPALAM("習得") = 3000
		ABLUPEXP:GETEXP("ダンス経験") = 50
	CASE 3
		ABLUP:GETPALAM("習得") = 9000
		ABLUPEXP:GETEXP("ダンス経験") = 100
	CASE 4
		ABLUP:GETPALAM("習得") = 24000
		ABLUPEXP:GETEXP("ダンス経験") = 300
	CASE 5
		ABLUP:GETPALAM("習得") = 30000
		ABLUPEXP:GETEXP("ダンス経験") = 700
	CASE 6
		ABLUP:GETPALAM("習得") = 35000
		ABLUPEXP:GETEXP("ダンス経験") = 1000
	CASE 7
		ABLUP:GETPALAM("習得") = 40000
		ABLUPEXP:GETEXP("ダンス経験") = 1500
	CASE 8
		ABLUP:GETPALAM("習得") = 45000
		ABLUPEXP:GETEXP("ダンス経験") = 2000
	CASE 9
		ABLUP:GETPALAM("習得") = 50000
		ABLUPEXP:GETEXP("ダンス経験") = 3000
	CASE IS >= 10
		ABLUP:GETPALAM("習得") = 600 * POWER(ABL:舞踊技能, 2)
ENDSELECT

;習得早い、好奇心、目立ちたがり
IF TALENT:習得早い || TALENT:好奇心 || TALENT:目立ちたがり
	TIMES ABLUP:GETPALAM("習得") , 0.80
	TIMES ABLUPEXP:GETEXP("ダンス経験") , 0.80
ENDIF
;ハイテンション
IF TALENT:ハイテンション
	TIMES ABLUP:GETPALAM("習得") , 0.95
	TIMES ABLUPEXP:GETEXP("ダンス経験") , 0.95
ENDIF
;大人しい
IF TALENT:大人しい
	TIMES ABLUP:GETPALAM("習得") , 1.05
	TIMES ABLUPEXP:GETEXP("ダンス経験") , 1.05
ENDIF
;習得遅い、無関心、恥じらい
IF TALENT:習得遅い || TALENT:無関心 || TALENT:恥じらい
	TIMES ABLUP:GETPALAM("習得") , 1.30
	TIMES ABLUPEXP:GETEXP("ダンス経験") , 1.30
ENDIF

@CHECK_JUEL_舞踊技能
;習得の珠、ダンス経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:ダンス経験 >= ABLUPEXP:GETEXP("ダンス経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_舞踊技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

IF ABLUPEXP:GETEXP("ダンス経験")
	SIF EXP:ダンス経験 < ABLUPEXP:GETEXP("ダンス経験")
		SETCOLORBYNAME gray
	PRINTFORM [ダンス経験{ABLUPEXP:GETEXP("ダンス経験")}以上]
	RESETCOLOR
ENDIF
