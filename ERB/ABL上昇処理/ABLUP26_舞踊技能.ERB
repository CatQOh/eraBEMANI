;ABLUP:8 = 習得の珠必要数
;ABLUPEXP:107 = ダンス経験必要数
@ABLUP26
SIF ABL:舞踊技能 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL26
CALL CHECK_JUEL26
IF RESULT
	CALL NEED_JUEL26
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:舞踊技能++
ENDIF

@NEED_JUEL26
SELECTCASE ABL:舞踊技能
	CASE 0
		ABLUP:8 = 75
		ABLUPEXP:107 = 5
	CASE 1
		ABLUP:8 = 500
		ABLUPEXP:107 = 20
	CASE 2
		ABLUP:8 = 3000
		ABLUPEXP:107 = 50
	CASE 3
		ABLUP:8 = 9000
		ABLUPEXP:107 = 100
	CASE 4
		ABLUP:8 = 24000
		ABLUPEXP:107 = 300
	CASE 5
		ABLUP:8 = 30000
		ABLUPEXP:107 = 700
	CASE 6
		ABLUP:8 = 35000
		ABLUPEXP:107 = 1000
	CASE 7
		ABLUP:8 = 40000
		ABLUPEXP:107 = 1500
	CASE 8
		ABLUP:8 = 45000
		ABLUPEXP:107 = 2000
	CASE 9
		ABLUP:8 = 50000
		ABLUPEXP:107 = 3000
	CASE IS >= 10
		LOCAL = ABL:舞踊技能*ABL:舞踊技能
		ABLUP:8 = 600 * LOCAL
		ABLUPEXP:107 = 50 * LOCAL
ENDSELECT

;習得早いもしくは好奇心
IF TALENT:習得早い || TALENT:好奇心
	TIMES ABLUP:8 , 0.80
	TIMES ABLUPEXP:107 , 0.80
ENDIF

;ハイテンション
IF TALENT:ハイテンション
	TIMES ABLUP:8 , 0.95
	TIMES ABLUPEXP:107 , 0.95
ENDIF
;大人しい
IF TALENT:大人しい
	TIMES ABLUP:8 , 1.05
	TIMES ABLUPEXP:107 , 1.05
ENDIF

;習得遅い、無関心、恥じらい
IF TALENT:習得遅い || TALENT:無関心 || TALENT:恥じらい
	TIMES ABLUP:8 , 1.30
	TIMES ABLUPEXP:107 , 1.30
ENDIF

@CHECK_JUEL26
;習得の珠(ABLUP:8)、ダンス経験(ABLUPEXP:107)で上げる
SIF JUEL:習得 >= ABLUP:8 && EXP:ダンス経験 >= ABLUPEXP:107
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE26
SIF JUEL:習得 < ABLUP:8
	SETCOLOR 120, 120, 120
PRINTFORM [習得の珠×{ABLUP:8}]
RESETCOLOR

SIF EXP:ダンス経験 < ABLUPEXP:107
	SETCOLOR 120, 120, 120
PRINTFORM [ダンス経験{ABLUPEXP:107}以上]
RESETCOLOR

