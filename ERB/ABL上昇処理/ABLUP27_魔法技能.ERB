@ABLUP27
#DIM DYNAMIC LCOUNT
;魔法技能はリミットバースト不可
SIF ABL:魔法技能 == 10
	RETURN 0
SIF ABL:魔法技能 < 0
	RETURN 0
CALL NEED_JUEL_魔法技能
CALL CHECK_JUEL_魔法技能
IF RESULT
	CALL NEED_JUEL_魔法技能
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:魔法技能++
	MAXBASE:魔力 = (ABL:魔法技能*1000)+(特質("魔力強化")*100)
ENDIF

@NEED_JUEL_魔法技能
SELECTCASE ABL:魔法技能
	CASE 1
		ABLUP:GETPALAM("習得") = 500
		ABLUPEXP:GETEXP("詠唱経験") = 20
	CASE 2
		ABLUP:GETPALAM("習得") = 3000
		ABLUPEXP:GETEXP("詠唱経験") = 50
	CASE 3
		ABLUP:GETPALAM("習得") = 9000
		ABLUPEXP:GETEXP("詠唱経験") = 100
	CASE 4
		ABLUP:GETPALAM("習得") = 24000
		ABLUPEXP:GETEXP("詠唱経験") = 300
	CASE 5
		ABLUP:GETPALAM("習得") = 30000
		ABLUPEXP:GETEXP("詠唱経験") = 500
	CASE 6
		ABLUP:GETPALAM("習得") = 35000
		ABLUPEXP:GETEXP("詠唱経験") = 700
	CASE 7
		ABLUP:GETPALAM("習得") = 40000
		ABLUPEXP:GETEXP("詠唱経験") = 800
	CASE 8
		ABLUP:GETPALAM("習得") = 45000
		ABLUPEXP:GETEXP("詠唱経験") = 900
	CASE 9
		ABLUP:GETPALAM("習得") = 50000
		ABLUPEXP:GETEXP("詠唱経験") = 1000
ENDSELECT

;習得早い、禁断の知識
IF TALENT:習得早い || TALENT:禁断の知識
	TIMES ABLUP:GETPALAM("習得") , 0.90
	TIMES ABLUPEXP:GETEXP("詠唱経験") , 0.90
ENDIF
;習得遅い、工学博士
IF TALENT:習得遅い || TALENT:工学博士
	TIMES ABLUP:GETPALAM("習得") , 1.20
	TIMES ABLUPEXP:GETEXP("詠唱経験") , 1.20
ENDIF

;種族適性
SELECTCASE CSTR:種族
	CASE "神", "悪魔", "魔術師"
		TIMES ABLUP:GETPALAM("習得") , 0.80
		TIMES ABLUPEXP:GETEXP("詠唱経験") , 0.80
	CASE "精霊", "天使", "妖精", "ヴァンパイア"
		TIMES ABLUP:GETPALAM("習得") , 0.90
		TIMES ABLUPEXP:GETEXP("詠唱経験") , 0.90
ENDSELECT

@CHECK_JUEL_魔法技能
;習得の珠、詠唱経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:詠唱経験 >= ABLUPEXP:GETEXP("詠唱経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_魔法技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

SIF EXP:詠唱経験 < ABLUPEXP:GETEXP("詠唱経験")
	SETCOLORBYNAME gray
PRINTFORM [詠唱経験{ABLUPEXP:GETEXP("詠唱経験")}以上]
RESETCOLOR

