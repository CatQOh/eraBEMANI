@ABLUP28
#DIM DYNAMIC LCOUNT
;飼育技能はリミットバースト不可
SIF ABL:飼育技能 == 10
	RETURN 0
SIF ABL:飼育技能 < 0
	RETURN 0
CALL NEED_JUEL_飼育技能
CALL CHECK_JUEL_飼育技能
IF RESULT
	CALL NEED_JUEL_飼育技能
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:飼育技能++
ENDIF

@NEED_JUEL_飼育技能
SELECTCASE ABL:飼育技能
	CASE 0
		ABLUPEXP:GETEXP("飼育経験") = 5
	CASE 1
		ABLUPEXP:GETEXP("飼育経験") = 20
	CASE 2
		ABLUPEXP:GETEXP("飼育経験") = 50
	CASE 3
		ABLUPEXP:GETEXP("飼育経験") = 100
	CASE 4
		ABLUPEXP:GETEXP("飼育経験") = 150
	CASE 5
		ABLUPEXP:GETEXP("飼育経験") = 200
	CASE 6
		ABLUPEXP:GETEXP("飼育経験") = 250
	CASE 7
		ABLUPEXP:GETEXP("飼育経験") = 300
	CASE 8
		ABLUPEXP:GETEXP("飼育経験") = 400
	CASE 9
		ABLUPEXP:GETEXP("飼育経験") = 500
ENDSELECT

;栽培技能と飼育技能の習得の珠はゆるい
ABLUP:GETPALAM("習得") = ABL:飼育技能*100

;習得早い
IF TALENT:習得早い
	TIMES ABLUP:GETPALAM("習得") , 0.90
	TIMES ABLUPEXP:GETEXP("飼育経験") , 0.90
ENDIF
;習得遅い、サボり魔
IF TALENT:習得遅い || TALENT:サボり魔
	TIMES ABLUP:GETPALAM("習得") , 1.20
	TIMES ABLUPEXP:GETEXP("飼育経験") , 1.20
ENDIF

;種族適性
SELECTCASE CSTR:種族
	CASE "獣人", "獣", "人魚"
		TIMES ABLUP:GETPALAM("習得") , 0.90
		TIMES ABLUPEXP:GETEXP("飼育経験") , 0.90
ENDSELECT

@CHECK_JUEL_飼育技能
;習得の珠、飼育経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:飼育経験 >= ABLUPEXP:GETEXP("飼育経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_飼育技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

SIF EXP:飼育経験 < ABLUPEXP:GETEXP("飼育経験")
	SETCOLORBYNAME gray
PRINTFORM [飼育経験{ABLUPEXP:GETEXP("飼育経験")}以上]
RESETCOLOR

