@ABLUP29
;栽培技能はリミットバースト不可
SIF ABL:栽培技能 == 10
	RETURN 0
CALL NEED_JUEL_栽培技能
CALL CHECK_JUEL_栽培技能
IF RESULT
	CALL NEED_JUEL_栽培技能
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:栽培技能++
ENDIF

@NEED_JUEL_栽培技能
SELECTCASE ABL:栽培技能
	CASE 0
		ABLUPEXP:GETEXP("栽培経験") = 5
	CASE 1
		ABLUPEXP:GETEXP("栽培経験") = 20
	CASE 2
		ABLUPEXP:GETEXP("栽培経験") = 50
	CASE 3
		ABLUPEXP:GETEXP("栽培経験") = 100
	CASE 4
		ABLUPEXP:GETEXP("栽培経験") = 150
	CASE 5
		ABLUPEXP:GETEXP("栽培経験") = 200
	CASE 6
		ABLUPEXP:GETEXP("栽培経験") = 250
	CASE 7
		ABLUPEXP:GETEXP("栽培経験") = 300
	CASE 8
		ABLUPEXP:GETEXP("栽培経験") = 400
	CASE 9
		ABLUPEXP:GETEXP("栽培経験") = 500
ENDSELECT

;栽培技能と飼育技能の習得の珠はゆるい
ABLUP:GETPALAM("習得") = ABL:栽培技能*100

;習得早い、調合知識、専業主婦
IF TALENT:習得早い || TALENT:調合知識 || TALENT:専業主婦
	TIMES ABLUP:GETPALAM("習得") , 0.90
	TIMES ABLUPEXP:GETEXP("栽培経験") , 0.90
ENDIF
;習得遅い、サボり魔
IF TALENT:習得遅い || TALENT:サボり魔
	TIMES ABLUP:GETPALAM("習得") , 1.20
	TIMES ABLUPEXP:GETEXP("栽培経験") , 1.20
ENDIF

;種族適性
SELECTCASE CSTR:種族
	CASE "精霊", "妖精", "獣人", "獣"
		TIMES ABLUP:GETPALAM("習得") , 0.90
		TIMES ABLUPEXP:GETEXP("栽培経験") , 0.90
ENDSELECT

;主に昼の仕事なのでヴァンパイアと吸血鬼はかなり伸ばしづらい
IF CSTR:種族 == "ヴァンパイア" || TALENT:吸血鬼
	TIMES ABLUP:GETPALAM("習得") , 2.00
	TIMES ABLUPEXP:GETEXP("栽培経験") , 2.00
ENDIF

@CHECK_JUEL_栽培技能
;習得の珠、栽培経験で上げる
SIF JUEL:習得 >= ABLUP:GETPALAM("習得") && EXP:栽培経験 >= ABLUPEXP:GETEXP("栽培経験")
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE_栽培技能
SIF JUEL:習得 < ABLUP:GETPALAM("習得")
	SETCOLORBYNAME gray
PRINTFORM [習得の珠×{ABLUP:GETPALAM("習得")}]
RESETCOLOR

SIF EXP:栽培経験 < ABLUPEXP:GETEXP("栽培経験")
	SETCOLORBYNAME gray
PRINTFORM [栽培経験{ABLUPEXP:GETEXP("栽培経験")}以上]
RESETCOLOR

