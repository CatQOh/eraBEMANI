;ABLUP:1 = 快Ｖの珠必要数
;ABLUPEXP:0 = Ｖ経験必要数
@ABLUP4
SIF ABL:Ｖ感覚 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL4
CALL CHECK_JUEL4
IF RESULT
	CALL NEED_JUEL4
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:Ｖ感覚++
ENDIF

@NEED_JUEL4
SELECTCASE ABL:Ｖ感覚
	CASE 0
		ABLUP:1 = 1
		ABLUPEXP:0 = 2
	CASE 1
		ABLUP:1 = 20
		ABLUPEXP:0 = 10
	CASE 2
		ABLUP:1 = 400
		ABLUPEXP:0 = 30
	CASE 3
		ABLUP:1 = 5000
		ABLUPEXP:0 = 75
	CASE 4
		ABLUP:1 = 15000
		ABLUPEXP:0 = 150
	CASE 5
		ABLUP:1 = 30000
		ABLUPEXP:0 = 230
	CASE 6
		ABLUP:1 = 40000
		ABLUPEXP:0 = 270
	CASE 7
		ABLUP:1 = 50000
		ABLUPEXP:0 = 360
	CASE 8
		ABLUP:1 = 60000
		ABLUPEXP:0 = 420
	CASE 9
		ABLUP:1 = 70000
		ABLUPEXP:0 = 500
	CASE IS >= 10
		LOCAL = ABL:Ｖ感覚*ABL:Ｖ感覚
		ABLUP:1 = 800 * LOCAL
		ABLUPEXP:0 = 6 * LOCAL
ENDSELECT

;陥落素質での補正
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:1 , 0.80
	TIMES ABLUPEXP:0 , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:1 , 0.50
	TIMES ABLUPEXP:0 , 0.50
ENDIF

@CHECK_JUEL4
;快Ｖの珠(ABLUP:1)、Ｖ経験(ABLUPEXP:0)が必要
SIF JUEL:快Ｖ >= ABLUP:1 && EXP:Ｖ経験 >= ABLUPEXP:0
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE4
SIF JUEL:快Ｖ < ABLUP:1
	SETCOLOR 120, 120, 120
PRINTFORM [快Ｖの珠×{ABLUP:1}]
RESETCOLOR

SIF EXP:Ｖ経験 < ABLUPEXP:0
	SETCOLOR 120, 120, 120
PRINTFORM [Ｖ経験{ABLUPEXP:0}以上]
RESETCOLOR

