;ABLUP:5 恭順の珠必要数
;ABLUP:7 屈服の珠必要数
;ABLUPEXP:2  = 絶頂経験必要数
;ABLUPEXP:24 = 奉仕快楽経験必要数
;ABLUPEXP:50 = 異常経験必要数
@ABLUP7
SIF ABL:奉仕精神 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL7
CALL CHECK_JUEL7
IF RESULT
	CALL NEED_JUEL7
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:奉仕精神++
ENDIF

@NEED_JUEL7
SELECTCASE ABL:奉仕精神
	CASE 0
		ABLUP:5 = 10
		ABLUP:7 = 10
		ABLUPEXP:2 = 3
		ABLUPEXP:24 = 5
	CASE 1
		ABLUP:5 = 100
		ABLUP:7 = 300
		ABLUPEXP:2 = 10
		ABLUPEXP:24 = 20
	CASE 2
		ABLUP:5 = 500
		ABLUP:7 = 2000
		ABLUPEXP:2 = 30
		ABLUPEXP:24 = 50
	CASE 3
		ABLUP:5 = 1000
		ABLUP:7 = 5000
		ABLUPEXP:2 = 50
		ABLUPEXP:24 = 100
	CASE 4
		ABLUP:5 = 3000
		ABLUP:7 = 10000
		ABLUPEXP:2 = 100
		ABLUPEXP:24 = 150
	CASE 5
		ABLUP:5 = 5000
		ABLUP:7 = 20000
		ABLUPEXP:2 = 150
		ABLUPEXP:24 = 200
	CASE 6
		ABLUP:5 = 10000
		ABLUP:7 = 30000
		ABLUPEXP:2 = 200
		ABLUPEXP:24 = 300
	CASE 7
		ABLUP:5 = 15000
		ABLUP:7 = 45000
		ABLUPEXP:2 = 250
		ABLUPEXP:24 = 400
	CASE 8
		ABLUP:5 = 30000
		ABLUP:7 = 60000
		ABLUPEXP:2 = 300
		ABLUPEXP:24 = 500
	CASE 9
		ABLUP:5 = 40000
		ABLUP:7 = 80000
		ABLUPEXP:2 = 400
		ABLUPEXP:24 = 600
	CASE IS >= 10
		LOCAL = ABL:奉仕精神*ABL:奉仕精神
		ABLUP:5 = 500 * LOCAL
		ABLUP:7 = 1000 * LOCAL
		ABLUPEXP:2 = 5 * LOCAL
		ABLUPEXP:24 = 7 * LOCAL
ENDSELECT

;素質での補正
;恋慕
SIF TALENT:恋慕
	TIMES ABLUP:5 , 0.80
;親愛
SIF TALENT:親愛
	TIMES ABLUP:5 , 0.60
;淫乱
IF TALENT:淫乱
	TIMES ABLUP:5 , 0.90
	TIMES ABLUP:7 , 0.90
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES ABLUP:5 , 0.70
	TIMES ABLUP:7 , 0.70
ENDIF
;服従
SIF TALENT:服従
	TIMES ABLUP:7 , 0.70
;隷属
SIF TALENT:隷属
	TIMES ABLUP:7 , 0.40

;小柄体型、小人体型、幼稚
SIF TALENT:小柄体型 || TALENT:小人体型 || TALENT:幼稚
	TIMES ABLUPEXP:2 , 0.75

;陥落、妄信、献身的
IF CFLAG:陥落 || TALENT:妄信 || TALENT:献身的
	TIMES ABLUPEXP:24 , 0.50
;反抗的、もしくは生意気
ELSEIF TALENT:反抗的 || TALENT:生意気
	TIMES ABLUPEXP:24 , 1.50
ENDIF

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:5 , 2.00
		TIMES ABLUP:7 , 2.00
		TIMES ABLUPEXP:2 , 2.00
		TIMES ABLUPEXP:24 , 2.00
	;陥落
	ELSEIF CFLAG:陥落
		TIMES ABLUP:5 , 0.50
		TIMES ABLUP:7 , 0.50
		TIMES ABLUPEXP:2 , 0.50
		TIMES ABLUPEXP:24 , 0.50
	ENDIF
ENDIF

;LV4からは異常経験必要 LV8からは異常経験5以上必要（陥落済みなら無視できる）
IF !CFLAG:陥落
	IF ABL:奉仕精神 >= 8
		ABLUPEXP:50 = 5
	ELSEIF ABL:奉仕精神 >= 4
		ABLUPEXP:50 = 1
	ENDIF
ENDIF

@CHECK_JUEL7
;従順が奉仕精神レベル以上でないといけない
SIF ABL:従順 < ABL:奉仕精神
	RETURN 0
;異常経験が規定値(ABLUPEXP:50)に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:50
	RETURN 0

;恭順の珠(ABLUP:5)、屈服の珠(ABLUP:7)、絶頂経験(ABLUPEXP:2)、奉仕快楽経験(ABLUPEXP:24)が必要
SIF JUEL:恭順 >= ABLUP:5 && JUEL:屈服 >= ABLUP:7 && EXP:絶頂経験 >= ABLUPEXP:2 && EXP:奉仕快楽経験 >= ABLUPEXP:24
	RETURN 1

;上記の条件を満たせなかったら上げられない
RETURN 0

@ABLUP_MESSAGE7
;LV4からは異常経験必要 LV8からは異常経験5以上必要（陥落済みなら無視できる）
SIF EXP:異常経験 < ABLUPEXP:50
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:50
	PRINTFORM [異常経験{ABLUPEXP:50}以上]
RESETCOLOR

SIF ABL:奉仕精神 > ABL:従順
	SETCOLOR 120, 120, 120
SIF ABL:奉仕精神
	PRINTFORM [従順LV{ABL:奉仕精神}以上]
RESETCOLOR

SIF JUEL:恭順 < ABLUP:5
	SETCOLOR 120, 120, 120
PRINTFORM [恭順の珠×{ABLUP:5}]
RESETCOLOR

SIF JUEL:屈服 < ABLUP:7
	SETCOLOR 120, 120, 120
PRINTFORM [屈服の珠×{ABLUP:7}]
RESETCOLOR

SIF EXP:絶頂経験 < ABLUPEXP:2
	SETCOLOR 120, 120, 120
PRINTFORM [絶頂経験{ABLUPEXP:2}以上]
RESETCOLOR

SIF EXP:奉仕快楽経験 < ABLUPEXP:24
	SETCOLOR 120, 120, 120
PRINTFORM [奉快経験{ABLUPEXP:24}以上]
RESETCOLOR

