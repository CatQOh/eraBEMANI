;ABLUP:9 = 恥情の珠必要数
;ABLUP:11 = 恐怖の珠必要数
;ABLUPEXP:50 = 異常経験必要数
@ABLUP8
SIF ABL:露出癖 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL8
CALL CHECK_JUEL8
IF RESULT
	CALL NEED_JUEL8
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:露出癖++
ENDIF

@NEED_JUEL8
SELECTCASE ABL:露出癖
	CASE 0
		ABLUP:9 = 100
		ABLUP:11 = 50
	CASE 1
		ABLUP:9 = 1000
		ABLUP:11 = 500
	CASE 2
		ABLUP:9 = 5000
		ABLUP:11 = 2500
	CASE 3
		ABLUP:9 = 15000
		ABLUP:11 = 7500
	CASE 4
		ABLUP:9 = 35000
		ABLUP:11 = 17500
	CASE 5
		ABLUP:9 = 50000
		ABLUP:11 = 25000
	CASE 6
		ABLUP:9 = 70000
		ABLUP:11 = 35000
	CASE 7
		ABLUP:9 = 100000
		ABLUP:11 = 50000
	CASE 8
		ABLUP:9 = 150000
		ABLUP:11 = 75000
	CASE 9
		ABLUP:9 = 200000
		ABLUP:11 = 100000
	CASE IS >= 10
		LOCAL = ABL:露出癖*ABL:露出癖
		ABLUP:9 = 2500 * LOCAL
		ABLUP:11 = 1000 * LOCAL
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:9 , 2.00
		TIMES ABLUP:11 , 2.00
	ELSEIF CFLAG:陥落
		TIMES ABLUP:9 , 0.70
		TIMES ABLUP:11 , 0.70
	ENDIF
ENDIF

;恥薄い 目立ちたがり
SIF TALENT:恥薄い || TALENT:目立ちたがり
	TIMES ABLUP:9 , 0.80

;臆病
SIF TALENT:臆病
	TIMES ABLUP:11 , 0.70

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
IF !TALENT:目立ちたがり
	IF ABL:露出癖 >= 8
		ABLUPEXP:50 = 5
	ELSEIF ABL:露出癖 >= 4
		ABLUPEXP:50 = 3
	ELSEIF ABL:露出癖 >= 3
		ABLUPEXP:50 = 1
	ENDIF
ENDIF

@CHECK_JUEL8
;欲望が露出癖+1レベルでないといけない
SIF ABL:欲望 <= ABL:露出癖
	RETURN 0
;異常経験が規定値(ABLUPEXP:50)に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:50
	RETURN 0
;LV3から絶頂経験、自慰経験が必要
SIF ABL:露出癖 >= 3 && (!EXP:絶頂経験 || !EXP:自慰経験)
	RETURN 0

;恥情の珠(ABLUP:9)、恐怖の珠(ABLUP:11)が必要
SIF JUEL:恥情 >= ABLUP:9 && JUEL:恐怖 >= ABLUP:11
	RETURN 1

;上記の条件を満たせなかったら上げられない
	RETURN 0

@ABLUP_MESSAGE8
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:50
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:50
	PRINTFORM [異常経験{ABLUPEXP:50}以上]
RESETCOLOR

SIF ABL:欲望 <= ABL:露出癖
	SETCOLOR 120, 120, 120
PRINTFORM [欲望LV{ABL:露出癖+1}以上]
RESETCOLOR

SIF JUEL:恥情 < ABLUP:9
	SETCOLOR 120, 120, 120
PRINTFORM [恥情の珠×{ABLUP:9}]
RESETCOLOR

SIF JUEL:恐怖 < ABLUP:11
	SETCOLOR 120, 120, 120
PRINTFORM [恐怖の珠×{ABLUP:11}]
RESETCOLOR

IF ABL:露出癖 >= 3
	SIF !EXP:絶頂経験
		SETCOLOR 120, 120, 120
	PRINT [絶頂経験あり]
	RESETCOLOR
	SIF !EXP:自慰経験
		SETCOLOR 120, 120, 120
	PRINT [自慰経験あり]
	RESETCOLOR
ENDIF

