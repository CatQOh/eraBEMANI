@ABLUP8
#DIM DYNAMIC LCOUNT
SIF ABL:露出癖 == 10 && !TALENT:リミットバースト
	RETURN 0
SIF ABL:露出癖 < 0
	RETURN 0
CALL NEED_JUEL_露出癖
CALL CHECK_JUEL_露出癖
IF RESULT
	CALL NEED_JUEL_露出癖
	FOR LCOUNT, 0, 15
		JUEL:LCOUNT -= ABLUP:LCOUNT
	NEXT
	ABL:露出癖++
ENDIF

@NEED_JUEL_露出癖
SELECTCASE ABL:露出癖
	CASE 0
		ABLUP:GETPALAM("恥情") = 100
		ABLUP:GETPALAM("恐怖") = 50
	CASE 1
		ABLUP:GETPALAM("恥情") = 1000
		ABLUP:GETPALAM("恐怖") = 500
	CASE 2
		ABLUP:GETPALAM("恥情") = 5000
		ABLUP:GETPALAM("恐怖") = 2500
	CASE 3
		ABLUP:GETPALAM("恥情") = 15000
		ABLUP:GETPALAM("恐怖") = 7500
	CASE 4
		ABLUP:GETPALAM("恥情") = 35000
		ABLUP:GETPALAM("恐怖") = 17500
	CASE 5
		ABLUP:GETPALAM("恥情") = 50000
		ABLUP:GETPALAM("恐怖") = 25000
	CASE 6
		ABLUP:GETPALAM("恥情") = 70000
		ABLUP:GETPALAM("恐怖") = 35000
	CASE 7
		ABLUP:GETPALAM("恥情") = 100000
		ABLUP:GETPALAM("恐怖") = 50000
	CASE 8
		ABLUP:GETPALAM("恥情") = 150000
		ABLUP:GETPALAM("恐怖") = 75000
	CASE 9
		ABLUP:GETPALAM("恥情") = 200000
		ABLUP:GETPALAM("恐怖") = 100000
	CASE IS >= 10
		LOCAL = ABL:露出癖*ABL:露出癖
		ABLUP:GETPALAM("恥情") = 2500 * LOCAL
		ABLUP:GETPALAM("恐怖") = 1000 * LOCAL
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETPALAM("恥情") , 2.00
		TIMES ABLUP:GETPALAM("恐怖") , 2.00
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETPALAM("恥情") , 0.70
		TIMES ABLUP:GETPALAM("恐怖") , 0.70
	ENDIF
ENDIF

;恥薄い 目立ちたがり、マゾ、解放
SIF TALENT:恥薄い || TALENT:目立ちたがり || TALENT:マゾ || TALENT:解放
	TIMES ABLUP:GETPALAM("恥情") , 0.80

;臆病
SIF TALENT:臆病
	TIMES ABLUP:GETPALAM("恐怖") , 0.70

;恥じらい
SIF TALENT:恥じらい
	TIMES ABLUP:GETPALAM("恥情") , 1.20

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
IF !TALENT:目立ちたがり
	IF ABL:露出癖 >= 8
		ABLUPEXP:GETEXP("異常経験") = 5
	ELSEIF ABL:露出癖 >= 4
		ABLUPEXP:GETEXP("異常経験") = 3
	ELSEIF ABL:露出癖 >= 3
		ABLUPEXP:GETEXP("異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL_露出癖
;欲望が露出癖+1レベルでないといけない
SIF ABL:欲望 <= ABL:露出癖
	RETURN 0
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
	RETURN 0
;LV3から絶頂経験、自慰経験が必要
SIF ABL:露出癖 >= 3 && (!EXP:絶頂経験 || !EXP:自慰経験)
	RETURN 0

;恥情の珠、恐怖の珠が必要
SIF JUEL:恥情 >= ABLUP:GETPALAM("恥情") && JUEL:恐怖 >= ABLUP:GETPALAM("恐怖")
	RETURN 1

;上記の条件を満たせなかったら上げられない
	RETURN 0

@ABLUP_MESSAGE_露出癖
SELECTCASE ABL:露出癖
	CASE 3, 4, 8
		;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
		SIF EXP:異常経験 < ABLUPEXP:GETEXP("異常経験")
			SETCOLORBYNAME gray
		SIF ABLUPEXP:GETEXP("異常経験")
			PRINTFORM [異常経験{ABLUPEXP:GETEXP("異常経験")}以上]
		RESETCOLOR
ENDSELECT

SIF ABL:欲望 <= ABL:露出癖
	SETCOLORBYNAME gray
PRINTFORM [欲望LV{ABL:露出癖+1}以上]
RESETCOLOR

SIF JUEL:恥情 < ABLUP:GETPALAM("恥情")
	SETCOLORBYNAME gray
PRINTFORM [恥情の珠×{ABLUP:GETPALAM("恥情")}]
RESETCOLOR

SIF JUEL:恐怖 < ABLUP:GETPALAM("恐怖")
	SETCOLORBYNAME gray
PRINTFORM [恐怖の珠×{ABLUP:GETPALAM("恐怖")}]
RESETCOLOR

IF ABL:露出癖 >= 3
	SIF !EXP:絶頂経験
		SETCOLORBYNAME gray
	PRINT [絶頂経験あり]
	RESETCOLOR
	SIF !EXP:自慰経験
		SETCOLORBYNAME gray
	PRINT [自慰経験あり]
	RESETCOLOR
ENDIF

