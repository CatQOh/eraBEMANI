@ABLUP8
SIF ABL:露出癖 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL8
CALL CHECK_JUEL8
IF RESULT
	CALL NEED_JUEL8
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:露出癖++
ENDIF

@NEED_JUEL8
SELECTCASE ABL:露出癖
	CASE 0
		ABLUP:GETNUM(PALAM, "恥情") = 100
		ABLUP:GETNUM(PALAM, "恐怖") = 50
	CASE 1
		ABLUP:GETNUM(PALAM, "恥情") = 1000
		ABLUP:GETNUM(PALAM, "恐怖") = 500
	CASE 2
		ABLUP:GETNUM(PALAM, "恥情") = 5000
		ABLUP:GETNUM(PALAM, "恐怖") = 2500
	CASE 3
		ABLUP:GETNUM(PALAM, "恥情") = 15000
		ABLUP:GETNUM(PALAM, "恐怖") = 7500
	CASE 4
		ABLUP:GETNUM(PALAM, "恥情") = 35000
		ABLUP:GETNUM(PALAM, "恐怖") = 17500
	CASE 5
		ABLUP:GETNUM(PALAM, "恥情") = 50000
		ABLUP:GETNUM(PALAM, "恐怖") = 25000
	CASE 6
		ABLUP:GETNUM(PALAM, "恥情") = 70000
		ABLUP:GETNUM(PALAM, "恐怖") = 35000
	CASE 7
		ABLUP:GETNUM(PALAM, "恥情") = 100000
		ABLUP:GETNUM(PALAM, "恐怖") = 50000
	CASE 8
		ABLUP:GETNUM(PALAM, "恥情") = 150000
		ABLUP:GETNUM(PALAM, "恐怖") = 75000
	CASE 9
		ABLUP:GETNUM(PALAM, "恥情") = 200000
		ABLUP:GETNUM(PALAM, "恐怖") = 100000
	CASE IS >= 10
		LOCAL = ABL:露出癖*ABL:露出癖
		ABLUP:GETNUM(PALAM, "恥情") = 2500 * LOCAL
		ABLUP:GETNUM(PALAM, "恐怖") = 1000 * LOCAL
ENDSELECT

;一線越えない
IF TALENT:一線越えない
	;未陥落
	IF !CFLAG:陥落
		TIMES ABLUP:GETNUM(PALAM, "恥情") , 2.00
		TIMES ABLUP:GETNUM(PALAM, "恐怖") , 2.00
	ELSEIF CFLAG:陥落
		TIMES ABLUP:GETNUM(PALAM, "恥情") , 0.70
		TIMES ABLUP:GETNUM(PALAM, "恐怖") , 0.70
	ENDIF
ENDIF

;恥薄い 目立ちたがり
SIF TALENT:恥薄い || TALENT:目立ちたがり
	TIMES ABLUP:GETNUM(PALAM, "恥情") , 0.80

;臆病
SIF TALENT:臆病
	TIMES ABLUP:GETNUM(PALAM, "恐怖") , 0.70

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
IF !TALENT:目立ちたがり
	IF ABL:露出癖 >= 8
		ABLUPEXP:GETNUM(EXP, "異常経験") = 5
	ELSEIF ABL:露出癖 >= 4
		ABLUPEXP:GETNUM(EXP, "異常経験") = 3
	ELSEIF ABL:露出癖 >= 3
		ABLUPEXP:GETNUM(EXP, "異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL8
;欲望が露出癖+1レベルでないといけない
SIF ABL:欲望 <= ABL:露出癖
	RETURN 0
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	RETURN 0
;LV3から絶頂経験、自慰経験が必要
SIF ABL:露出癖 >= 3 && (!EXP:絶頂経験 || !EXP:自慰経験)
	RETURN 0

;恥情の珠、恐怖の珠が必要
SIF JUEL:恥情 >= ABLUP:GETNUM(PALAM, "恥情") && JUEL:恐怖 >= ABLUP:GETNUM(PALAM, "恐怖")
	RETURN 1

;上記の条件を満たせなかったら上げられない
	RETURN 0

@ABLUP_MESSAGE8
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "異常経験")
	PRINTFORM [異常経験{ABLUPEXP:GETNUM(EXP, "異常経験")}以上]
RESETCOLOR

SIF ABL:欲望 <= ABL:露出癖
	SETCOLOR 120, 120, 120
PRINTFORM [欲望LV{ABL:露出癖+1}以上]
RESETCOLOR

SIF JUEL:恥情 < ABLUP:GETNUM(PALAM, "恥情")
	SETCOLOR 120, 120, 120
PRINTFORM [恥情の珠×{ABLUP:GETNUM(PALAM, "恥情")}]
RESETCOLOR

SIF JUEL:恐怖 < ABLUP:GETNUM(PALAM, "恐怖")
	SETCOLOR 120, 120, 120
PRINTFORM [恐怖の珠×{ABLUP:GETNUM(PALAM, "恐怖")}]
RESETCOLOR

IF ABL:露出癖 >= 3
	SIF !EXP:絶頂経験
		SETCOLOR 120, 120, 120
	PRINT [絶頂経験あり]
	RESETCOLOR
	SIF !EXP:自慰経験
		SETCOLOR 120, 120, 120
	PRINT [自慰経験あり]
	RESETCOLOR
ENDIF

