@ABLUP9
SIF ABL:マゾっ気 == 10 && !TALENT:リミットバースト
	RETURN 0
CALL NEED_JUEL9
CALL CHECK_JUEL9
IF RESULT
	CALL NEED_JUEL9
	REPEAT 15
		JUEL:COUNT -= ABLUP:COUNT
	REND
	ABL:マゾっ気++
ENDIF

@NEED_JUEL9
SELECTCASE ABL:マゾっ気
	CASE 0
		ABLUP:GETNUM(PALAM, "欲情") = 100
		ABLUP:GETNUM(PALAM, "苦痛") = 100
	CASE 1
		ABLUP:GETNUM(PALAM, "欲情") = 500
		ABLUP:GETNUM(PALAM, "苦痛") = 500
	CASE 2
		ABLUP:GETNUM(PALAM, "欲情") = 1000
		ABLUP:GETNUM(PALAM, "苦痛") = 1200
	CASE 3
		ABLUP:GETNUM(PALAM, "欲情") = 6000
		ABLUP:GETNUM(PALAM, "苦痛") = 3000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 10
	CASE 4
		ABLUP:GETNUM(PALAM, "欲情") = 12000
		ABLUP:GETNUM(PALAM, "苦痛") = 5000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 50
	CASE 5
		ABLUP:GETNUM(PALAM, "欲情") = 15000
		ABLUP:GETNUM(PALAM, "苦痛") = 8000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 100
	CASE 6
		ABLUP:GETNUM(PALAM, "欲情") = 20000
		ABLUP:GETNUM(PALAM, "苦痛") = 12000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 150
	CASE 7
		ABLUP:GETNUM(PALAM, "欲情") = 25000
		ABLUP:GETNUM(PALAM, "苦痛") = 15000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 200
	CASE 8
		ABLUP:GETNUM(PALAM, "欲情") = 35000
		ABLUP:GETNUM(PALAM, "苦痛") = 25000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 250
	CASE 9
		ABLUP:GETNUM(PALAM, "欲情") = 40000
		ABLUP:GETNUM(PALAM, "苦痛") = 30000
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 350
	CASE IS >= 10
		LOCAL = ABL:マゾっ気*ABL:マゾっ気
		ABLUP:GETNUM(PALAM, "欲情") = 500 * LOCAL
		ABLUP:GETNUM(PALAM, "苦痛") = 400 * LOCAL
		ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") = 4 * LOCAL
ENDSELECT

;一線越えない及び未陥落
IF TALENT:一線越えない && CFLAG:陥落
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 1.50
	TIMES ABLUP:GETNUM(PALAM, "苦痛") , 1.50
	TIMES ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") , 1.50
ENDIF
;マゾ
IF TALENT:マゾ
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.50
	TIMES ABLUP:GETNUM(PALAM, "苦痛") , 0.50
	TIMES ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") , 0.50
ENDIF
;痛みに強い
IF TALENT:痛みに強い
	TIMES ABLUP:GETNUM(PALAM, "欲情") , 0.70
	TIMES ABLUP:GETNUM(PALAM, "苦痛") , 0.70
	TIMES ABLUPEXP:GETNUM(EXP, "苦痛快楽経験") , 0.70
ENDIF

;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[解放]なら無視できる）
IF TALENT:解放 == 0
	IF ABL:マゾっ気 >= 8
		ABLUPEXP:GETNUM(EXP, "異常経験") = 5
	ELSEIF ABL:マゾっ気 >= 4
		ABLUPEXP:GETNUM(EXP, "異常経験") = 3
	ELSEIF ABL:マゾっ気 >= 3
		ABLUPEXP:GETNUM(EXP, "異常経験") = 1
	ENDIF
ENDIF

@CHECK_JUEL9
;異常経験が規定値に満たないと上昇できない
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	RETURN 0

;苦痛、欲情の珠、苦痛快楽経験が必要
SIF JUEL:苦痛 >= ABLUP:GETNUM(PALAM, "苦痛") && JUEL:欲情 >= ABLUP:GETNUM(PALAM, "欲情") && EXP:苦痛快楽経験 >= ABLUPEXP:GETNUM(EXP, "苦痛快楽経験")
	RETURN 1

;上記の条件を満たせないと上げられない
RETURN 0

@ABLUP_MESSAGE9
;LV3からは異常経験必要 LV4からは異常経験3以上必要 LV8からは異常経験5以上必要（素質：[解放]なら無視できる）
SIF EXP:異常経験 < ABLUPEXP:GETNUM(EXP, "異常経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "異常経験")
	PRINTFORM [異常経験{ABLUPEXP:GETNUM(EXP, "異常経験")}以上]
RESETCOLOR

SIF JUEL:欲情 < ABLUP:GETNUM(PALAM, "欲情")
	SETCOLOR 120, 120, 120
PRINTFORM [欲情の珠×{ABLUP:GETNUM(PALAM, "欲情")}]
RESETCOLOR

SIF JUEL:苦痛 < ABLUP:GETNUM(PALAM, "苦痛")
	SETCOLOR 120, 120, 120
PRINTFORM [苦痛の珠×{ABLUP:GETNUM(PALAM, "苦痛")}]
RESETCOLOR

SIF EXP:苦痛快楽経験 < ABLUPEXP:GETNUM(EXP, "苦痛快楽経験")
	SETCOLOR 120, 120, 120
SIF ABLUPEXP:GETNUM(EXP, "苦痛快楽経験")
	PRINTFORM [苦痛快楽経験{ABLUPEXP:GETNUM(EXP, "苦痛快楽経験")}以上]
RESETCOLOR

