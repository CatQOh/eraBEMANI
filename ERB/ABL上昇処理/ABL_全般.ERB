@SHOW_JUEL
;見た目を整えるためのCLEARLINE処理
CALL LOOP_CLEARLINE

DRAWLINE
;経験と珠の表示はそれぞれ別関数だったりする
PRINTL □経験
CALL SHOW_INFO_EXP, TARGET
DRAWLINE
PRINTL □珠
CALL SHOW_INFO_JUEL, TARGET
DRAWLINE

@SHOW_ABLUP_SELECT
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 能力名

PRINTFORML □%NAME:TARGET%の能力
FOR LCOUNT, 0, VARSIZE("ABL")
	VARSET ABLUP, 0
	VARSET ABLUPEXP, 0

	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE ABLNAME:LCOUNT
		;膣が無いとＶ感覚は上げられない
		CASE "Ｖ感覚"
			SIF !VAGINA(TARGET)
				CONTINUE
		;男ならレズっ気とレズ中毒と噴乳中毒は上げられない
		CASE "レズっ気", "レズ中毒", "噴乳中毒"
			SIF SEX(TARGET) == 1
				CONTINUE
		;女ならホモっ気とゲイ中毒とメス度は上げられない
		CASE "ホモっ気", "ゲイ中毒", "メス度"
			SIF SEX(TARGET) == 2
				CONTINUE
		;グリモワールを使ってない、魔法技能初期値が設定されてないキャラは魔法技能を伸ばせない
		CASE "魔法技能"
			SIF !ABL:魔法技能
				CONTINUE
	ENDSELECT
	;マイナスの場合は特殊表示
	IF ABL:LCOUNT < 0
		SETCOLOR 条件成否(0)
		PRINTFORML 　[--] %ABLNAME:LCOUNT, 8, LEFT% - LVマイナス 特殊訓練が必要です
	ELSE
		;要求される珠と経験を取得
		CALLFORM NEED_JUEL_%ABLNAME:LCOUNT%
		;条件を満たしているか確認
		CALLFORM CHECK_JUEL_%ABLNAME:LCOUNT%
		;満たしていれば青色表示、満たしてなければ灰色表示
		SETCOLOR 条件成否(RESULT)
		;リミットバースト無しでABLがMAX(10)なら白文字
		IF ABL:LCOUNT >= 10 && TALENT:リミットバースト == 0
			$LIMITELSE
			RESETCOLOR
			PRINTFORML 　[{LCOUNT, 2}] %ABLNAME:LCOUNT, 8, LEFT% - LV{ABL:LCOUNT} *MAX*
			CONTINUE
		ELSE
			;魔法技能、飼育技能、栽培技能はリミットバースト対象外
			SELECTCASE ABLNAME:LCOUNT
				CASE "魔法技能", "飼育技能", "栽培技能"
					SIF ABL:LCOUNT >= 10
						GOTO LIMITELSE
			ENDSELECT
			PRINTFORM 　[{LCOUNT, 2}] %ABLNAME:LCOUNT, 8, LEFT% - LV{ABL:LCOUNT}
		ENDIF
		PRINT  
		RESETCOLOR
		CALLFORM ABLUP_MESSAGE_%ABLNAME:LCOUNT%
	ENDIF
	PRINTL 
NEXT

FOR LCOUNT, 0, 2
	SELECTCASE LCOUNT
		CASE 0
			能力名 = 苦痛刻印
		CASE 1
			能力名 = 反発刻印
	ENDSELECT

	CALLFORM NEED_JUEL_%能力名%
	CALLFORM CHECK_JUEL_%能力名%
	SETCOLOR 条件成否(RESULT)

	PRINTFORM 　[{90+LCOUNT}] %能力名% - LV{MARK:能力名}

	IF MARK:能力名 > 0
		PRINT  
		RESETCOLOR
		CALLFORM ABLUP_MESSAGE_%能力名%
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT
DRAWLINE
PRINTPLAIN 　ABLUP　
PRINT [100] 能力値アップの終了 
IF FLAG:能力上昇 != 0
	CALL PRINTSPACE, TARGET > 1, "[101] 前のキャラ "
	CALL PRINTSPACE, CHARANUM > TARGET+1, "[109] 次のキャラ "
ENDIF
PRINTL 

VARSET ABLUP, 0
VARSET ABLUPEXP, 0

;上昇不可なら灰色、上昇可能なら青色の色コードを返す
@条件成否, ARG
#FUNCTION
IF ARG == 0
	RETURNF COLOR_FROMNAME("gray")
ELSE
	RETURNF COLOR_FROMRGB(100, 150, 200)
ENDIF

@USERABLUP
;SHOP画面から入ってきたときはターン終わらないようにBEGIN SHOPする
SELECTCASE RESULT
	CASE 100
		CALL ERASE_TALENT
		VARLINE = 0
		IF FLAG:能力上昇 != 0
			TARGET = FLAG:能力上昇
			FLAG:能力上昇 = 0
			BEGIN SHOP
		ELSE
			BEGIN TURNEND
		ENDIF
	CASE 101
		SIF TARGET > 1
			TARGET--
		BEGIN ABLUP
	CASE 109
		SIF CHARANUM > TARGET+1
			TARGET++
		BEGIN ABLUP
ENDSELECT

@SHOP_ABLUP
#DIM DYNAMIC 次ページあり
#DIM 表示上限 = 10

DRAWLINE
PRINTL 能力を上昇させたいキャラを選んでください

;見た目を整えるためのCLEARLINE処理
CALL LOOP_CLEARLINE

CALL LIFE_LIST, "通常表示", 0, 表示上限
;RESULTにはまだ表示されてないキャラが居るかどうか
次ページあり = RESULT
DRAWLINE
PRINTPLAIN 　ABLUP　

CALL PRINTSPACE, TFLAG:ページ数 > 0, "[999] 前ページ "
PRINTFORM [1000] 戻る 
CALL PRINTSPACE, 次ページあり, "[1001] 次ページ "
PRINTL 

INPUT
SELECTCASE RESULT
	CASE -1
		SIF 表示上限 > 10
			表示上限 -= 10
		RESTART
	CASE -9
		表示上限 += 10
		RESTART
	CASE 0
		PRINTW 主人の能力は上げられません
	CASE 999
		SIF TFLAG:ページ数 > 0
			TFLAG:ページ数--
	CASE 1000
		RETURN 0
	CASE 1001
		SIF 次ページあり
			TFLAG:ページ数++
	CASE 0 TO CHARANUM-1
		FLAG:能力上昇 = TARGET
		TARGET = RESULT
		BEGIN ABLUP
ENDSELECT

RESTART

;-------------------------------------------------
;能力の上昇による素質の消滅をチェック
;-------------------------------------------------
@ERASE_TALENT
IF CFLAG:陥落 && TALENT:抵抗
	PRINTFORML %CALLNAME:TARGET%は[抵抗]を失った
	TALENT:抵抗 = 0
	PRINTFORML %CALLNAME:TARGET%は[素直]を得た
	TALENT:素直 = 1
ENDIF

IF CFLAG:陥落 && TALENT:抑圧
	PRINTFORML %CALLNAME:TARGET%は[抑圧]を失った
	TALENT:抑圧 = 0
	PRINTFORMW %CALLNAME:TARGET%は[解放]を得た
	TALENT:解放 = 1
	CALL 実績解除, "escape"
ENDIF

@ABLマイナス打ち消し, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABL:対象:LCOUNT < 0
		ABL:対象:LCOUNT = 0
NEXT

