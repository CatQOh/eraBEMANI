@SHOW_JUEL
SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT
DRAWLINE
PRINTL □経験
CALL SHOW_INFO_EXP, TARGET
DRAWLINE
PRINTL □珠
CALL SHOW_INFO_JUEL, TARGET
DRAWLINE

@SHOW_ABLUP_SELECT
#DIM LCOUNT
FOR LCOUNT, 0, 27
	VARSET ABLUP, 0
	VARSET ABLUPEXP, 0

	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE ABLNAME:LCOUNT
		;男ならＶ感覚は上げられない ふたなりを除く
		CASE "Ｖ感覚"
			SIF SEX(TARGET) == 1 && !TALENT:ふたなり
				CONTINUE
		;男ならレズっ気とレズ中毒と噴乳中毒は上げられない
		CASE "レズっ気", "レズ中毒", "噴乳中毒"
			SIF SEX(TARGET) == 1
				CONTINUE
		;女ならホモっ気とゲイ中毒は上げられない
		CASE "ホモっ気", "ゲイ中毒"
			SIF SEX(TARGET) == 2
				CONTINUE
				
	ENDSELECT
	;要求される珠と経験を取得
	CALLFORM NEED_JUEL{LCOUNT}
	;条件を満たしているか確認
	CALLFORM CHECK_JUEL{LCOUNT}
	LOCAL = RESULT
	;満たしていれば青色表示、満たしてなければ灰色表示
	IF RESULT == 0
		SETCOLOR 120, 120, 120
	ELSE
		SETCOLOR 100, 150, 200
	ENDIF
	;リミットバースト無しでABLがMAX(10)なら白文字
	SIF ABL:LCOUNT >= 10 && TALENT:リミットバースト == 0
		RESETCOLOR
	PRINTFORM 　[{LCOUNT, 2}] %ABLNAME:LCOUNT, 8, LEFT% - LV{ABL:LCOUNT}

	IF ABL:LCOUNT >= 10 && TALENT:リミットバースト == 0
		PRINTL  *MAX*
		CONTINUE
	ENDIF

	PRINT  
	RESETCOLOR
	CALLFORM ABLUP_MESSAGE{LCOUNT}
	PRINTL 
NEXT

CALL NEED_JUEL98
CALL CHECK_JUEL98
IF RESULT == 0
	SETCOLOR 120, 120, 120
ELSE
	SETCOLOR 100, 150, 200
ENDIF

PRINTFORM 　[98] 苦痛刻印 - LV{MARK:苦痛刻印}

IF MARK:苦痛刻印 > 0
	PRINT  
	RESETCOLOR
	CALL ABLUP_MESSAGE98
ENDIF

RESETCOLOR
PRINTL 

CALL NEED_JUEL99
CALL CHECK_JUEL99
IF RESULT == 0
	SETCOLOR 120, 120, 120
ELSE
	SETCOLOR 100, 150, 200
ENDIF

PRINTFORM 　[99] 反発刻印 - LV{MARK:反発刻印}

IF MARK:反発刻印 > 0
	PRINT  
	RESETCOLOR
	CALL ABLUP_MESSAGE99
ENDIF

RESETCOLOR
PRINTL 
DRAWLINE
PRINTPLAIN 　ABLUP　
PRINTL [100] 能力値アップの終了

VARSET ABLUP, 0
VARSET ABLUPEXP, 0

@USERABLUP
;SHOP画面から入ってきたときはターン終わらないようにBEGIN SHOPする
IF RESULT == 100
	CALL ERASE_TALENT
	VARLINE = 0
	IF FLAG:能力上昇 != 0
		TARGET = FLAG:能力上昇
		FLAG:能力上昇 = 0
		BEGIN SHOP
	ENDIF
	BEGIN TURNEND
ENDIF

@SHOP_ABLUP
DRAWLINE
PRINTL 能力を上昇させたいキャラを選んでください

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

CALL LIFE_LIST
;RESULT:0には選んだキャラ、RESULT:1にはこのリストでどのキャラまで表示したか
DRAWLINE
PRINTPLAIN 　ABLUP　
SIF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
PRINTFORM [1000] 戻る 
SIF RESULT:1 < CHARANUM-1
	PRINTFORM [1001] 次ページ 
PRINTL 
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			PRINTW 主人の能力は上げられません
			RESTART
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1 < CHARANUM-1
				TFLAG:ページ数++
		CASE 0 TO CHARANUM-1
			FLAG:能力上昇 = TARGET
			TARGET = RESULT
			BEGIN ABLUP
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

VARLINE = 0

RESTART

;-------------------------------------------------
;能力の上昇による素質の消滅をチェック
;-------------------------------------------------
@ERASE_TALENT
IF ABL:従順 >= 3 && TALENT:抵抗
	PRINTFORML %CALLNAME:TARGET%は[抵抗]を失った
	TALENT:抵抗 = 0
	PRINTL 否定の珠が半分になった
	JUEL:否定 /= 2
ENDIF

IF ABL:欲望 >= 3 && TALENT:抑圧
	PRINTFORML %CALLNAME:TARGET%は[抑圧]を失った
	TALENT:抑圧 = 0
	PRINTL 否定の珠が半分になった
	JUEL:否定 /= 2
ENDIF

