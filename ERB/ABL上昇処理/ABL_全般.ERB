@SHOW_JUEL
;見た目を整えるためのCLEARLINE処理
SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT
DRAWLINE
;経験と珠の表示はそれぞれ別関数だったりする
PRINTL □経験
CALL SHOW_INFO_EXP, TARGET
DRAWLINE
PRINTL □珠
CALL SHOW_INFO_JUEL, TARGET
DRAWLINE

@SHOW_ABLUP_SELECT
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 能力名
FOR LCOUNT, 0, VARSIZE("ABL")
	VARSET ABLUP, 0
	VARSET ABLUPEXP, 0

	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE ABLNAME:LCOUNT
		;膣が無いとＶ感覚は上げられない
		CASE "Ｖ感覚"
			SIF !VAGINA(TARGET)
				CONTINUE
		;男ならレズっ気とレズ中毒と噴乳中毒は上げられない
		CASE "レズっ気", "レズ中毒", "噴乳中毒"
			SIF SEX(TARGET) == 1
				CONTINUE
		;女ならホモっ気とゲイ中毒は上げられない
		CASE "ホモっ気", "ゲイ中毒"
			SIF SEX(TARGET) == 2
				CONTINUE
		;グリモワールを使ってない、魔法技能初期値が設定されてないキャラは魔法技能を伸ばせない
		CASE "魔法技能"
			SIF !ABL:魔法技能
				CONTINUE
	ENDSELECT
	;要求される珠と経験を取得
	CALLFORM NEED_JUEL_%ABLNAME:LCOUNT%
	;条件を満たしているか確認
	CALLFORM CHECK_JUEL_%ABLNAME:LCOUNT%
	LOCAL = RESULT
	;満たしていれば青色表示、満たしてなければ灰色表示
	SETCOLOR 条件成否(RESULT)
	;リミットバースト無しでABLがMAX(10)なら白文字 魔法技能だけはリミットバースト対象外
	IF ABL:LCOUNT >= 10 && (TALENT:リミットバースト == 0 || ABLNAME:LCOUNT == "魔法技能")
		RESETCOLOR
		PRINTFORML 　[{LCOUNT, 2}] %ABLNAME:LCOUNT, 8, LEFT% - LV{ABL:LCOUNT} *MAX*
		CONTINUE
	ELSE
		PRINTFORM 　[{LCOUNT, 2}] %ABLNAME:LCOUNT, 8, LEFT% - LV{ABL:LCOUNT}
	ENDIF

	PRINT  
	RESETCOLOR
	CALLFORM ABLUP_MESSAGE_%ABLNAME:LCOUNT%
	PRINTL 
NEXT

FOR LCOUNT, 0, 2
	SELECTCASE LCOUNT
		CASE 0
			能力名 = 苦痛刻印
		CASE 1
			能力名 = 反発刻印
	ENDSELECT

	CALLFORM NEED_JUEL_%能力名%
	CALLFORM CHECK_JUEL_%能力名%
	SETCOLOR 条件成否(RESULT)

	PRINTFORM 　[{90+LCOUNT}] %能力名% - LV{MARK:能力名}

	IF MARK:能力名 > 0
		PRINT  
		RESETCOLOR
		CALLFORM ABLUP_MESSAGE_%能力名%
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT
DRAWLINE
PRINTPLAIN 　ABLUP　
PRINTL [100] 能力値アップの終了

VARSET ABLUP, 0
VARSET ABLUPEXP, 0

;上昇不可なら灰色、上昇可能なら青色の色コードを返す
@条件成否, ARG
#FUNCTION
IF ARG == 0
	RETURNF COLOR_FROMNAME("gray")
ELSE
	RETURNF COLOR_FROMRGB(100, 150, 200)
ENDIF

@USERABLUP
;SHOP画面から入ってきたときはターン終わらないようにBEGIN SHOPする
IF RESULT == 100
	CALL ERASE_TALENT
	VARLINE = 0
	IF FLAG:能力上昇 != 0
		TARGET = FLAG:能力上昇
		FLAG:能力上昇 = 0
		BEGIN SHOP
	ENDIF
	BEGIN TURNEND
ENDIF

@SHOP_ABLUP
DRAWLINE
PRINTL 能力を上昇させたいキャラを選んでください

;見た目を整えるためのCLEARLINE処理
SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

CALL LIFE_LIST
;RESULT:0には選んだキャラ、RESULT:1にはまだ表示されてないキャラが居るかどうか
DRAWLINE
PRINTPLAIN 　ABLUP　
LOCALS = 
SIF TFLAG:ページ数 > 0
	LOCALS = [999] 前ページ 

PRINTFORM %LOCALS, 15, LEFT%

PRINTFORM [1000] 戻る 
SIF RESULT:1
	PRINTFORM [1001] 次ページ 
PRINTL 
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			PRINTW 主人の能力は上げられません
			RESTART
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
		CASE 0 TO CHARANUM-1
			FLAG:能力上昇 = TARGET
			TARGET = RESULT
			BEGIN ABLUP
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

RESTART

;-------------------------------------------------
;能力の上昇による素質の消滅をチェック
;-------------------------------------------------
@ERASE_TALENT
IF CFLAG:陥落 && TALENT:抵抗
	PRINTFORML %CALLNAME:TARGET%は[抵抗]を失った
	TALENT:抵抗 = 0
	PRINTFORML %CALLNAME:TARGET%は[素直]を得た
	TALENT:素直 = 1
ENDIF

IF CFLAG:陥落 && TALENT:抑圧
	PRINTFORML %CALLNAME:TARGET%は[抑圧]を失った
	TALENT:抑圧 = 0
	PRINTFORMW %CALLNAME:TARGET%は[解放]を得た
	TALENT:解放 = 1
	CALL 実績解除, "escape"
ENDIF

