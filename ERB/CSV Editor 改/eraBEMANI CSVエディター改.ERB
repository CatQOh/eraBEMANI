@CSV_EDITOR
#DIMS DYNAMIC 経験名
#DIMS DYNAMIC 素質名

ADDVOIDCHARA
ADDVOIDCHARA
NAME:0 = 主人
CALLNAME:0 = 主人
TARGET = 1

PRINTL 作成するキャラの性別を選んでください
PRINTL [1] - 男
PRINTL [2] - 女
PRINTL [0] - キャンセル
INPUT
DO
	IF RESULT == 0
		DELALLCHARA
		RETURN 0
	ENDIF
LOOP LOOPRES(1, 2)

TALENT:性別 = RESULT

$RESTART
DRAWLINE
PRINTL □ 基本情報
PRINTBUTTON @"[キャラ番号] {NO:TARGET} ", "キャラ番号"
PRINTBUTTON @"[価格] {CFLAG:価格} ", "価格"
PRINTBUTTON @"[誕生日] \@ CFLAG:誕生日 ? {CFLAG:誕生日/100}月{CFLAG:誕生日%100}日 # 未設定 \@", "誕生日"
PRINTL 
PRINTBUTTON @"[名前] %NAME:TARGET% ", "名前"
PRINTBUTTON @"[呼び名] %CALLNAME:TARGET%", "呼び名"
PRINTL 
PRINTBUTTON @"[性別] \@ SEX(TARGET) == 1 ? 男 # 女 \@ ", "性別"
PRINTBUTTON @"[種族] %CSTR:種族%", "種族"
PRINTL 
PRINTBUTTON @"[体力] {MAXBASE:体力} ", "体力"
PRINTBUTTON @"[気力] {MAXBASE:気力}", "気力"
PRINTL 
DRAWLINE
PRINTL □ 経験

VARSET LOCALS, 
LOCAL = 0
REPEAT VARSIZE("EXP")
	IF COUNT == VARSIZE("EXP")-1
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		BREAK
	ELSEIF EXPNAME:COUNT == ""
		CONTINUE
	ENDIF

	;改行するのか？ 続けて出力するのか？
	IF LOCAL%6 == 0 && LOCAL != 0
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		VARSET LOCALS, 
	ENDIF

	RESETCOLOR
	SIF EXP:COUNT == 0
		SETCOLORBYNAME gray

	SELECTCASE EXPNAME:COUNT
		CASE "奉仕快楽経験"
			経験名 = 奉快経験
		CASE "フェラ経験"
			経験名 = 口淫経験
		CASE "苦痛快楽経験"
			経験名 = 痛楽経験
		CASE "嗜虐快楽経験"
			経験名 = 虐快経験
		CASE "主人調教経験"
			経験名 = 主調経験
		CASE "助手調教経験"
			経験名 = 助調経験
		CASE "Ｖ拡張経験"
			経験名 = Ｖ拡経験
		CASE "Ａ拡張経験"
			経験名 = Ａ拡経験
		CASE "ダンス経験"
			経験名 = 舞踊経験
		CASE "音ゲー経験"
			経験名 = 音ゲ経験
		CASEELSE
			経験名 = %EXPNAME:COUNT%
	ENDSELECT

	LOCALS:(LOCAL%6) = %SET_TOOLTIP(@"　[%経験名, 8%]:{EXP:COUNT, 4, LEFT}", EXP_COMMENT(EXPNAME:COUNT, TARGET))%
	LOCAL++
REND
SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTL □ 能力
LOCAL = 0
VARSET LOCALS, 
REPEAT VARSIZE("ABL")
	IF COUNT == VARSIZE("ABL")-1
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		BREAK
	ELSEIF ABLNAME:COUNT == ""
		CONTINUE
	ENDIF

	;改行するのか？ 続けて出力するのか？
	IF LOCAL%6 == 0 && LOCAL != 0
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		VARSET LOCALS, 
	ENDIF

	RESETCOLOR
	SIF ABL:COUNT == 0
		SETCOLORBYNAME gray

	LOCALS:(LOCAL%6) = %SET_TOOLTIP(@"　[%ABLNAME:COUNT, 8%]:LV{ABL:COUNT, 4, LEFT}", ABL_COMMENT(ABLNAME:COUNT, TARGET))%
	LOCAL++
REND

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN □ 素質 
PRINTBUTTON "[変更する]", "素質を変更する"
PRINTL 
LOCAL = 0
VARSET LOCALS, 
REPEAT VARSIZE("TALENT")
	SELECTCASE TALENTNAME:COUNT
		CASE "性別"
			CONTINUE
	ENDSELECT
	IF COUNT == VARSIZE("TALENT")-1
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		BREAK
	ELSEIF TALENT:COUNT == 0
		CONTINUE
	ENDIF

	IF TALENTNAME:COUNT == "バストサイズ"
		SELECTCASE TALENT:バストサイズ
			CASE 0
				SIF SEX(TARGET) == 2
					素質名 = [胸の大きさを設定してください]
			CASE 1
				素質名 = [絶壁]
			CASE 2
				素質名 = [貧乳]
			CASE 3
				素質名 = [並乳]
			CASE 4
				素質名 = [巨乳]
			CASE 5
				素質名 = [爆乳]
		ENDSELECT
	ELSE
		素質名 = [%TALENTNAME:COUNT%]
	ENDIF

	SIF LOCAL%6 == 0
		LOCALS = 　

	LOCALS:(LOCAL%6) += @"%SET_TOOLTIP(素質名, TALENT_COMMENT(TALENTNAME:COUNT, TARGET))%"

	LOCAL++

	;改行するのか？ 続けて出力するのか？
	IF LOCAL%6 == 0 && LOCAL != 0
		HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5
		VARSET LOCALS, 
	ENDIF
REND

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTL □ 服装
REPEAT VARSIZE("CSTR")
	SELECTCASE CSTRNAME:COUNT
		CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
			IF CFLAG:(CSTRNAME:COUNT) == 100
				LOCALS = 着衣不可
			ELSE
				LOCALS = %CSTR:COUNT%
			ENDIF
			PRINTBUTTON @"%@"　[%CSTRNAME:COUNT%]", 14, LEFT%:%LOCALS% ", CSTRNAME:COUNT
			IF LOCALS != "着衣不可"
				SELECTCASE CSTRNAME:COUNT
					CASE "下半身上着"
						PRINTBUTTON @"[挿入可フラグ]:上着を着たまま挿入\@ CFLAG:挿入可 == 1 ? 可能 # 不可能 \@", "挿入可"
					CASE "全身上着"
						SELECTCASE CFLAG:着衣併用
							CASE 0
								LOCALS = フラグ無し
							CASE 1
								LOCALS = 通常上着との併用可能
							CASE 2
								LOCALS = 下着との併用不可能
						ENDSELECT
						PRINTBUTTON @"[着衣併用フラグ]:%LOCALS%", "着衣併用"
				ENDSELECT
			ENDIF
			PRINTL 
	ENDSELECT
REND

DRAWLINE
PRINTPLAIN □ 相性 
PRINTBUTTON "[変更する]", "相性を変更する"
PRINTL 
LOCAL = 0
REPEAT 10000
	IF EXISTCSV(COUNT)
		LOCALS = 
		IF RELATION:COUNT != 100
			LOCALS = 　%CSVNAMEF(COUNT)%×{RELATION:COUNT}\%
		ENDIF
		IF LOCALS != ""
			PRINTFORMLC %LOCALS%
			LOCAL++
		ENDIF
		IF LOCAL == 4 && !LINEISEMPTY()
			PRINTL 
			LOCAL = 0
		ENDIF
	ENDIF
REND

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN □ キャラマニュアル 
PRINTBUTTON "[変更する]", "マニュアルを変更する"
PRINTL 
REPEAT 100
	SIF CSTR:COUNT == ""
		BREAK
	PRINTFORML 　%CSTR:COUNT%
REND

DRAWLINE
PRINTBUTTON "　[0] CSVを出力する ", "0"
PRINTBUTTON "[1] CSV作成をやめる ", "1"
PRINTBUTTON "[2] 既存のCSVを呼び出す", "2"

$LOOP
INPUTS
CLEARLINE 1
SELECTCASE RESULTS
	CASE "0"
		PRINTL 現在の作業状況のCSVをログに出力します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		INPUT
		DO
			SELECTCASE RESULT
				CASE 0
					CLEARLINE LINECOUNT
					PRINTFORML 番号,{NO:TARGET}
					PRINTFORML 名前,%NAME:TARGET%
					IF CALLNAME:TARGET == ""
						PRINTFORML 呼び名,%NAME:TARGET%
					ELSE
						PRINTFORML 呼び名,%CALLNAME:TARGET%
					ENDIF
					PRINTL 
					PRINTFORML フラグ,価格,{CFLAG:価格}
					SIF CFLAG:誕生日
						PRINTFORML フラグ,誕生日,{CFLAG:誕生日}
					SIF CFLAG:血縁コード
						PRINTFORML フラグ,血縁コード,{CFLAG:血縁コード}
					SIF CFLAG:続柄
						PRINTFORML フラグ,続柄,{CFLAG:続柄}
					SIF CFLAG:兄弟
						PRINTFORML フラグ,兄弟,{CFLAG:兄弟}
					SIF CFLAG:姉妹
						PRINTFORML フラグ,姉妹,{CFLAG:姉妹}
						
					PRINTL 
					PRINTFORML 基礎,体力,{MAXBASE:体力}
					PRINTFORML 基礎,気力,{MAXBASE:気力}
					REPEAT VARSIZE("EXP")
						SIF EXP:COUNT != 0
							PRINTFORML 経験,%EXPNAME:COUNT%,{EXP:COUNT}
					REND
					REPEAT VARSIZE("ABL")
						SIF ABL:COUNT != 0
							PRINTFORML 能力,%ABLNAME:COUNT%,{ABL:COUNT}
					REND
					PRINTFORML 素質,性別,{TALENT:性別}
					SIF SEX(TARGET) == 2
						PRINTFORML 素質,バストサイズ,{TALENT:バストサイズ}
					SIF TALENT:恋心 != 0
						PRINTFORML 素質,恋心,{TALENT:恋心},;%CSVCALLNAME(TALENT:恋心)%
					SIF TALENT:既婚者 != 0
						PRINTFORML 素質,既婚者,{TALENT:既婚者},;%CSVCALLNAME(TALENT:既婚者)%
					REPEAT VARSIZE("TALENT")
						SELECTCASE TALENTNAME:COUNT
							CASE "性別", "バストサイズ", "恋心", "既婚者"
							CASEELSE
								SIF TALENT:COUNT != 0
									PRINTFORML 素質,%TALENTNAME:COUNT%
						ENDSELECT
					REND
					REPEAT 10000
						SIF RELATION:COUNT != 100
							PRINTFORML 相性,{COUNT},{RELATION:COUNT},;%CSVCALLNAME(COUNT)%
					REND
					
					PRINTL 
					
					REPEAT 100
						IF CSTR:COUNT != ""
							PRINTFORML CSTR,{COUNT},%CSTR:COUNT%
						ELSE
							BREAK
						ENDIF
					REND
					
					PRINTL 
					
					PRINTL ;服装
					REPEAT VARSIZE("CSTR")
						SELECTCASE CSTRNAME:COUNT
							CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
								IF CFLAG:(CSTRNAME:COUNT) == 100
									PRINTFORML フラグ,%CSTRNAME:COUNT%,100
								ELSEIF CSTR:COUNT != ""
									PRINTFORML CSTR,%CSTRNAME:COUNT%,%CSTR:COUNT%
								ENDIF
						ENDSELECT
					REND
					SIF CFLAG:挿入可
						PRINTL フラグ,挿入可,1
					SIF CFLAG:着衣併用 != 0
						PRINTFORML フラグ,着衣併用,{CFLAG:着衣併用}
					SIF CFLAG:上着パイズリ != 0
						PRINTFORML フラグ,上着パイズリ,{CFLAG:上着パイズリ}
					SIF CFLAG:下着パイズリ != 0
						PRINTFORML フラグ,下着パイズリ,{CFLAG:下着パイズリ}

					PRINTL 
					PRINTFORML CSTR,種族,%CSTR:種族%
					PRINTL 
					SIF CFLAG:グリム
						PRINTL フラグ,グリム,1
					OUTPUTLOG
					PRINTW ログに出力しました。CSVとして利用する方法は資料フォルダ内のテキストをご覧ください
			ENDSELECT
		LOOP LOOPRES(0, 1)
		GOTO RESTART
	CASE "1"
		PRINTL 現在の作業状況を破棄してタイトル画面に戻ります。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		INPUT
		DO
			SELECTCASE RESULT
				CASE 0
					DELALLCHARA
					RETURN 0
				CASE 1
					GOTO RESTART
			ENDSELECT
		LOOP 1
	CASE "2"
		PRINTL 現在の作業状況を破棄して既存のCSVを呼び出します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		INPUT
		DO
			SELECTCASE RESULT
				CASE 0
					CALL CHARA_LIST, 1
					DELALLCHARA
					ADDVOIDCHARA
					NAME:0 = 主人
					CALLNAME:0 = 主人
					ADDCHARA RESULT
					TARGET = 1
					GOTO RESTART
				CASE 1
					GOTO RESTART
			ENDSELECT
		LOOP 1
	CASE "キャラ番号"
		PRINTL キャラ番号を入力してください
		INPUT
		NO:TARGET = RESULT
		GOTO RESTART
	CASE "価格"
		PRINTL 価格を入力してください
		INPUT
		CFLAG:価格 = RESULT
		GOTO RESTART
	CASE "誕生日"
		PRINTL 誕生日を4桁で入力してください(例:6月24日→0624)
		DO
			INPUT
		LOOP !INRANGE(RESULT/100, 1, 12) || !INRANGE(RESULT%100, 1, 31)
		CFLAG:誕生日 = RESULT
		GOTO RESTART
	CASE "名前"
		PRINTL フルネームを入力してください
		INPUTS
		NAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "呼び名"
		PRINTL 呼び名を入力してください
		INPUTS
		CALLNAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "性別"
		PRINTL 性別を選んでください
		PRINTL [1] - 男
		PRINTL [2] - 女
		INPUT
		DO
		LOOP LOOPRES(1, 2)
		TALENT:性別 = RESULT
	CASE "体力"
		PRINTL 体力の数値を入力してください
		INPUT
		MAXBASE:体力 = RESULT
		GOTO RESTART
	CASE "気力"
		PRINTL 気力の数値を入力してください
		INPUT
		MAXBASE:気力 = RESULT
		GOTO RESTART
	CASE "種族"
		PRINTL 種族名を入力してください
		INPUTS
		CSTR:TARGET:種族 = %RESULTS%
		GOTO RESTART
	CASE "素質を変更する"
		CALL TALENT_CHANGE
		GOTO RESTART
	CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
		LOCALS = %RESULTS%
		PRINTFORML %LOCALS%の名前を入力してください
		PRINTBUTTON "[着衣不可にする]", "着衣不可にする"
		PRINTL 
		INPUTS
		SELECTCASE RESULTS
			CASE "着衣不可にする"
				CFLAG:LOCALS = 100
				CSTR:LOCALS = 
			CASEELSE
				CSTR:LOCALS = %RESULTS%
		ENDSELECT
		GOTO RESTART
	CASE "挿入可"
		PRINTL 挿入可能フラグを設定してください
		PRINTL [0] - フラグ無し
		PRINTL [1] - 下半身上着、もしくは全身上着を着たまま性交可能
		INPUT
		DO
		LOOP LOOPRES(0, 1)
		CFLAG:挿入可 = RESULT
		GOTO RESTART
	CASE "着衣併用"
		PRINTL 全身上着の着衣併用フラグを設定してください
		PRINTL [0] - フラグ無し
		PRINTL [1] - 上半身上着及び下半身上着との併用可能
		PRINTL [2] - 上半身下着及び下半身下着との併用不可能
		INPUT
		DO
		LOOP LOOPRES(0, 1, 2)
		CFLAG:着衣併用 = RESULT
		GOTO RESTART
	CASE "相性を変更する"
		PRINTL 相手の番号を入力してください
		PRINT [10000] 一覧表示 
		PRINTL [10001] 全部既定値に戻す
		INPUT
		IF RESULT == 10000
			PRINTL □ 一覧表示
			CALL CHARA_LIST, 1
		ELSEIF RESULT == 10001
			VARSET RELATION:TARGET:0, 100
			GOTO RESTART
		ELSEIF !EXISTCSV(RESULT)
			PRINTFORMW {RESULT}にはキャラが登録されていません
			GOTO RESTART
		ENDIF
		LOCAL = RESULT
		PRINTFORML %CSVNAME(LOCAL)%との相性値を入力してください
		INPUT
		RELATION:LOCAL = RESULT
		GOTO RESTART
	CASE "マニュアルを変更する"
		$MANUAL
		PRINTL 変更する行を選んでください
		REPEAT 100
			IF CSTR:COUNT == ""
				LOCAL = COUNT
				BREAK
			ENDIF
			PRINTFORML [{COUNT}]%CSTR:COUNT%
		REND
		DRAWLINE
		PRINT  [100] 完了 
		PRINTL [101] 行を追加
		INPUT
		DO
			SELECTCASE RESULT
				CASE 100
					GOTO RESTART
				CASE 101
					PRINTL 追加する行のマニュアルを入力してください
					INPUTS
					CSTR:LOCAL = %RESULTS%
					GOTO MANUAL
				CASEELSE
					IF CSTR:RESULT != ""
						PRINTFORML {RESULT}行目のマニュアルを入力してください
						PRINTBUTTON "[この行を削除]", "この行を削除"
						PRINTL 
						INPUTS %CSTR:RESULT%
						IF RESULTS == "この行を削除"
							FOR COUNT, RESULT, LOCAL
								CSTR:COUNT = %CSTR:(COUNT+1)%
							NEXT
							GOTO MANUAL
						ENDIF
						CSTR:RESULT = %RESULTS%
						GOTO MANUAL
					ENDIF
			ENDSELECT
		LOOP 1
	CASEELSE
		RESULTS = %REPLACE(RESULTS, " ", "")%
		SIF RESULTS == "舞踊経験"
			RESULTS = ダンス経験
		SIF RESULTS == "音ゲ経験"
			RESULTS = 音ゲー経験
		SIF RESULTS == "Ｖ拡経験"
			RESULTS = Ｖ拡張経験
		SIF RESULTS == "Ａ拡経験"
			RESULTS = Ａ拡張経験
		SIF RESULTS == "口淫経験"
			RESULTS = フェラ経験
		REPEAT VARSIZE("EXP")
			IF EXPNAME:COUNT == RESULTS
				PRINTFORML %EXPNAME:COUNT%の数値を入力してください
				INPUT
				EXP:COUNT = RESULT
			ENDIF
		REND
		REPEAT VARSIZE("ABL")
			IF ABLNAME:COUNT == RESULTS
				PRINTFORML %ABLNAME:COUNT%の数値を入力してください
				INPUT
				ABL:COUNT = RESULT
			ENDIF
		REND
		GOTO RESTART
ENDSELECT

GOTO RESTART

