@CSV_EDITOR
#DIMS DYNAMIC 経験名
#DIMS DYNAMIC 素質名
#DIM DYNAMIC LCOUNT
#DIM 素質数値指定
#DIM DYNAMIC 表示数
#DIMS DYNAMIC 表示用変数, 6
#DIM DYNAMIC LRESULT

ADDVOIDCHARA
ADDVOIDCHARA
NAME:0 = 主人
CALLNAME:0 = 主人
TARGET = 1

PRINTL 作成するキャラの性別を選んでください
PRINTL [1] - 男
PRINTL [2] - 女
PRINTL [0] - キャンセル
CALL INPUTF, 0, 1, 2
IF RESULT == 0
	DELALLCHARA
	RETURN
ENDIF

TALENT:性別 = RESULT

$RESTART
DRAWLINE
PRINTL □ 基本情報
PRINTBUTTON @"[キャラ番号] {NO:TARGET} ", "キャラ番号"
PRINTBUTTON @"[価格] {CFLAG:価格} ", "価格"
PRINTBUTTON @"[誕生日] \@ CFLAG:誕生日 ? {CFLAG:誕生日/100}月{CFLAG:誕生日%100}日 # 未設定 \@", "誕生日"
PRINTL 
PRINTBUTTON @"[名前] %NAME:TARGET% ", "名前"
PRINTBUTTON @"[呼び名] %CALLNAME:TARGET%", "呼び名"
PRINTL 
PRINTBUTTON @"[性別] \@ SEX(TARGET) == 1 ? 男 # 女 \@ ", "性別"
PRINTBUTTON @"[種族] %CSTR:種族%", "種族"
PRINTL 
PRINTBUTTON @"[体力] {MAXBASE:体力} ", "体力"
PRINTBUTTON @"[気力] {MAXBASE:気力}", "気力"
PRINTL 
DRAWLINE
PRINTL □ 経験

PRINTBUTTON "　[経験無し]", "経験無し"
PRINTBUTTON "[経験ランダム]", "経験ランダム"
PRINTBUTTON "[経験ランダム(すごい)]", "経験ランダム(すごい)"
PRINTL 

VARSET 表示用変数
表示数 = 0
FOR LCOUNT, 0, VARSIZE("EXP")
	IF LCOUNT == VARSIZE("EXP")-1
		HTML_PRINT STRJOIN(表示用変数, "")
		BREAK
	ELSEIF EXPNAME:LCOUNT == ""
		CONTINUE
	ENDIF

	;改行するのか？ 続けて出力するのか？
	IF 表示数%6 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用変数, "")
		VARSET 表示用変数
	ENDIF

	RESETCOLOR
	SIF EXP:LCOUNT < 0
		EXP:LCOUNT = 0
	SIF EXP:LCOUNT == 0
		SETCOLORBYNAME gray

	SELECTCASE EXPNAME:LCOUNT
		CASE "奉仕快楽経験"
			経験名 = 奉快経験
		CASE "フェラ経験"
			経験名 = 口淫経験
		CASE "パイズリ経験"
			経験名 = 乳擦経験
		CASE "射精管理経験"
			経験名 = 射精管理
		CASE "苦痛快楽経験"
			経験名 = 痛楽経験
		CASE "嗜虐快楽経験"
			経験名 = 虐快経験
		CASE "主人調教経験"
			経験名 = 主調経験
		CASE "助手調教経験"
			経験名 = 助調経験
		CASE "Ｖ拡張経験"
			経験名 = Ｖ拡経験
		CASE "Ａ拡張経験"
			経験名 = Ａ拡経験
		CASE "ダンス経験"
			経験名 = 舞踊経験
		CASE "音ゲー経験"
			経験名 = 音ゲ経験
		CASEELSE
			経験名 = %EXPNAME:LCOUNT%
	ENDSELECT

	表示用変数:(表示数%6) = %SET_TOOLTIP(@"　[%経験名, 8%]:{EXP:LCOUNT, 4, LEFT}", EXP_COMMENT(EXPNAME:LCOUNT, TARGET))%
	RESETCOLOR
	表示数++
NEXT
SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTL □ 能力
表示数 = 0
VARSET 表示用変数 
FOR LCOUNT, 0, VARSIZE("ABL")
	IF LCOUNT == VARSIZE("ABL")-1
		HTML_PRINT STRJOIN(表示用変数, "")
		BREAK
	ELSEIF ABLNAME:LCOUNT == ""
		CONTINUE
	ENDIF

	;改行するのか？ 続けて出力するのか？
	IF 表示数%6 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用変数, "")
		VARSET 表示用変数
	ENDIF

	RESETCOLOR
	SIF ABL:LCOUNT < 0
		ABL:LCOUNT = 0
	SIF ABL:LCOUNT == 0
		SETCOLORBYNAME gray

	表示用変数:(表示数%6) = %SET_TOOLTIP(@"　[%ABLNAME:LCOUNT, 8%]:LV{ABL:LCOUNT, 4, LEFT}", ABL_COMMENT(ABLNAME:LCOUNT, TARGET))%
	RESETCOLOR
	表示数++
NEXT

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN □ 素質 
SIF 素質数値指定
	PRINT (数値指定モード) 
PRINTBUTTON "[変更する] ", "素質を変更する"
PRINTBUTTON "[素質を数値指定する]", "素質を数値指定する"
PRINTL 
表示数 = 0
VARSET 表示用変数 
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別"
			CONTINUE
	ENDSELECT
	SIF TALENT:LCOUNT < 0
		TALENT:LCOUNT = 0
	IF LCOUNT == VARSIZE("TALENT")-1
		HTML_PRINT STRJOIN(表示用変数, "")
		BREAK
	ELSEIF SEX(TARGET) == 2 && GETNUM(TALENT, "バストサイズ") == LCOUNT
	ELSEIF TALENT:LCOUNT == 0
		CONTINUE
	ENDIF

	IF TALENTNAME:LCOUNT == "バストサイズ"
		SELECTCASE TALENT:バストサイズ
			CASE 0
				SIF SEX(TARGET) == 2
					素質名 = [胸の大きさを設定してください]
			CASE 1
				素質名 = [絶壁]
			CASE 2
				素質名 = [貧乳]
			CASE 3
				素質名 = [並乳]
			CASE 4
				素質名 = [巨乳]
			CASE 5
				素質名 = [豊乳]
			CASE 6
				素質名 = [爆乳]
		ENDSELECT
	ELSE
		素質名 = [%TALENTNAME:LCOUNT%]
	ENDIF

	SIF 表示数%6 == 0
		表示用変数 = 　

	IF 素質数値指定
		表示用変数:(表示数%6) += @"%SET_TOOLTIP(@"%素質名%={TALENT:LCOUNT} ", TALENT_COMMENT(TALENTNAME:LCOUNT, TARGET))%"
	ELSE
		表示用変数:(表示数%6) += @"%SET_TOOLTIP(素質名, TALENT_COMMENT(TALENTNAME:LCOUNT, TARGET))%"
	ENDIF

	表示数++

	;改行するのか？ 続けて出力するのか？
	IF 表示数%6 == 0 && 表示数 != 0
		HTML_PRINT STRJOIN(表示用変数, "")
		VARSET 表示用変数
	ENDIF
NEXT

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTL □ 服装
FOR LCOUNT, 0, VARSIZE("CSTR")
	SELECTCASE CSTRNAME:LCOUNT
		CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
			IF CFLAG:(CSTRNAME:LCOUNT) == 100
				表示用変数 = 着衣不可
			ELSE
				表示用変数 = %CSTR:LCOUNT%
			ENDIF
			PRINTBUTTON @"%@"　[%CSTRNAME:LCOUNT%]", 14, LEFT%:%表示用変数% ", CSTRNAME:LCOUNT
			IF 表示用変数 != "着衣不可"
				SELECTCASE CSTRNAME:LCOUNT
					CASE "下半身上着"
						PRINTBUTTON @"[挿入可フラグ]:上着を着たまま挿入\@ CFLAG:挿入可 == 1 ? 可能 # 不可能 \@", "挿入可"
					CASE "全身上着"
						SELECTCASE CFLAG:着衣併用
							CASE 0
								表示用変数 = フラグ無し
							CASE 1
								表示用変数 = 通常上着との併用可能
							CASE 2
								表示用変数 = 下着との併用不可能
						ENDSELECT
						PRINTBUTTON @"[着衣併用フラグ]:%表示用変数%", "着衣併用"
				ENDSELECT
			ENDIF
			PRINTL 
	ENDSELECT
NEXT

DRAWLINE
PRINTPLAIN □ 相性 
PRINTBUTTON "[変更する]", "相性を変更する"
PRINTL 
表示数 = 0
FOR LCOUNT, 0, 10000
	IF EXISTCSV(LCOUNT)
		表示用変数 = 
		IF RELATION:LCOUNT != 100
			表示用変数 = 　%CSVNAMEF(LCOUNT)%×{RELATION:LCOUNT}\%
		ENDIF
		IF 表示用変数 != ""
			PRINTFORMLC %表示用変数%
			表示数++
		ENDIF
		IF 表示数 == 4 && !LINEISEMPTY()
			PRINTL 
			表示数 = 0
		ENDIF
	ENDIF
NEXT

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN □ キャラマニュアル 
PRINTBUTTON "[変更する]", "マニュアルを変更する"
PRINTL 
FOR LCOUNT, 0, 100
	SIF CSTR:LCOUNT == ""
		BREAK
	PRINTFORML 　%CSTR:LCOUNT%
NEXT

DRAWLINE
PRINTL □ 登場作品、キャラデザイナー
PRINT 　登場作品 
PRINTBUTTON @"[1] %CSTR:登場作品% ", "登場作品"
PRINTBUTTON @"[2] %CSTR:登場作品２% ", "登場作品２"
PRINTBUTTON @"[3] %CSTR:登場作品３%", "登場作品３"
PRINTL 
PRINT 　キャラデザ 
PRINTBUTTON @"[1] %CSTR:キャラデザ% ", "キャラデザ"
PRINTBUTTON @"[2] %CSTR:キャラデザ２%", "キャラデザ２"
PRINTL 
DRAWLINE
PRINTBUTTON @"　[詳細な性経験を設定する]", "性経験設定"
PRINTL

DRAWLINE
PRINTBUTTON "　[0] CSVを出力する ", "0"
PRINTBUTTON "[1] CSVを全て数値で出力する", "1"
PRINTBUTTON "[2] CSV作成をやめる ", "2"
PRINTBUTTON "[3] 既存のCSVを呼び出す ", "3"
PRINTBUTTON "[4] プロジェクトを初期化する", "4"

$LOOP
INPUTS
CLEARLINE 1
SELECTCASE RESULTS
	CASE "0"
		PRINTL 現在の作業状況のCSVをログに出力します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1

		IF RESULT == 0
			CLEARLINE LINECOUNT
			PRINTFORML 番号,{NO:TARGET}
			PRINTFORML 名前,%NAME:TARGET%
			IF CALLNAME:TARGET == ""
				PRINTFORML 呼び名,%NAME:TARGET%
			ELSE
				PRINTFORML 呼び名,%CALLNAME:TARGET%
			ENDIF
			PRINTL 
			PRINTFORML フラグ,価格,{CFLAG:価格}
			SIF CFLAG:誕生日
				PRINTFORML フラグ,誕生日,{CFLAG:誕生日}
			SIF CFLAG:血縁コード
				PRINTFORML フラグ,血縁コード,{CFLAG:血縁コード}
			SIF CFLAG:続柄
				PRINTFORML フラグ,続柄,{CFLAG:続柄}
			SIF CFLAG:兄弟
				PRINTFORML フラグ,兄弟,{CFLAG:兄弟}
			SIF CFLAG:姉妹
				PRINTFORML フラグ,姉妹,{CFLAG:姉妹}
				
			PRINTL 
			PRINTFORML 基礎,体力,{MAXBASE:体力}
			PRINTFORML 基礎,気力,{MAXBASE:気力}
			FOR LCOUNT, 0, VARSIZE("EXP")
				SIF EXP:LCOUNT != 0
					PRINTFORML 経験,%EXPNAME:LCOUNT%,{EXP:LCOUNT}
			NEXT
			FOR LCOUNT, 0, VARSIZE("ABL")
				SIF ABL:LCOUNT != 0
					PRINTFORML 能力,%ABLNAME:LCOUNT%,{ABL:LCOUNT}
			NEXT
			IF TALENT:恋心 != 0
				IF TALENT:恋心 == 10000
					PRINTFORML CSTR,恋心,不明
				ELSE
					PRINTFORML CSTR,恋心,%CSVNAME(TALENT:恋心)%
				ENDIF
			ENDIF
			IF TALENT:既婚者 != 0
				IF TALENT:既婚者 == 10000
					PRINTFORML CSTR,既婚者,不明
				ELSE
					PRINTFORML CSTR,既婚者,%CSVNAME(TALENT:既婚者)%
				ENDIF
			ENDIF
			PRINTFORML 素質,性別,{TALENT:性別}
			SIF SEX(TARGET) == 2
				PRINTFORML 素質,バストサイズ,{TALENT:バストサイズ}
			FOR LCOUNT, 0, VARSIZE("TALENT")
				SELECTCASE TALENTNAME:LCOUNT
					CASE "性別", "バストサイズ", "恋心", "既婚者"
					CASEELSE
						IF TALENT:LCOUNT > 0
							IF 素質数値指定
								PRINTFORML 素質,%TALENTNAME:LCOUNT%,{TALENT:LCOUNT}
							ELSE
								PRINTFORML 素質,%TALENTNAME:LCOUNT%
							ENDIF
						ENDIF
				ENDSELECT
			NEXT
			IF MAXARRAY(RELATION) != 100 || MINARRAY(RELATION) != 100
				PRINTL {
				PRINTL CSTR,相性,
				FOR LCOUNT, 0, 10000
					SIF RELATION:LCOUNT != 100
						PRINTFORML %CSVNAME(LCOUNT)%*{RELATION:LCOUNT}/
				NEXT
				PRINTL }
			ENDIF
			IF MAX(モブ経験人数:TARGET:0, MAXARRAY(経験回数)) > 0
				PRINTL {
				PRINTL CSTR,性経験,
				SIF モブ経験人数:0
					PRINTFORML モブ経験人数/{モブ経験人数}/
				SIF モブ経験回数:0
					PRINTFORML モブ経験回数/{モブ経験回数}/
				SIF モブ中出し回数:0
					PRINTFORML モブ中出し回数/{モブ中出し回数}/
				SIF モブアナル中出し回数:0
					PRINTFORML モブ経験アナル中出し回数/{モブアナル中出し回数}/
				SIF モブ中出され回数:0
					PRINTFORML モブ中出され回数/{モブ中出され回数}/
				SIF モブアナル中出され回数:0
					PRINTFORML モブアナル中出され回数/{モブアナル中出され回数}/
				SIF モブ孕み回数:0
					PRINTFORML モブ孕み回数/{モブ孕み回数}/
				SIF モブ孕ませ回数:0
					PRINTFORML モブ孕ませ回数/{モブ孕ませ回数}/
				FOR LCOUNT, 0, 10000
					SIF 経験回数:LCOUNT
						PRINTFORML 経験回数/%CSVNAME(LCOUNT)%*{経験回数:LCOUNT}/
					SIF 中出し回数:LCOUNT
						PRINTFORML 中出し回数/%CSVNAME(LCOUNT)%*{中出し回数:LCOUNT}/
					SIF アナル中出し回数:LCOUNT
						PRINTFORML アナル中出し回数/%CSVNAME(LCOUNT)%*{アナル中出し回数:LCOUNT}/
					SIF 中出され回数:LCOUNT
						PRINTFORML 中出され回数/%CSVNAME(LCOUNT)%*{中出され回数:LCOUNT}/
					SIF アナル中出され回数:LCOUNT
						PRINTFORML アナル中出され回数/%CSVNAME(LCOUNT)%*{アナル中出され回数:LCOUNT}/
					SIF 孕み回数:LCOUNT
						PRINTFORML 孕み回数/%CSVNAME(LCOUNT)%*{孕み回数:LCOUNT}/
					SIF 孕ませ回数:LCOUNT
						PRINTFORML 孕ませ回数/%CSVNAME(LCOUNT)%*{孕ませ回数:LCOUNT}/
				NEXT
				PRINTL }
			ENDIF
			SIF CSTR:童貞 != ""
				PRINTFORML CSTR,童貞,%CSTR:童貞%
			SIF CSTR:処女 != ""
				PRINTFORML CSTR,処女,%CSTR:処女%
			PRINTL 
			
			FOR LCOUNT, 0, 100
				IF CSTR:LCOUNT != ""
					PRINTFORML CSTR,{LCOUNT},%CSTR:LCOUNT%
				ELSE
					BREAK
				ENDIF
			NEXT
			
			PRINTL 
			
			PRINTL ;服装
			FOR LCOUNT, 0, VARSIZE("CSTR")
				SELECTCASE CSTRNAME:LCOUNT
					CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
						IF CFLAG:(CSTRNAME:LCOUNT) == 100
							PRINTFORML フラグ,%CSTRNAME:LCOUNT%,100
						ELSEIF CSTR:LCOUNT != ""
							PRINTFORML CSTR,%CSTRNAME:LCOUNT%,%CSTR:LCOUNT%
						ENDIF
				ENDSELECT
			NEXT
			SIF CFLAG:挿入可
				PRINTL フラグ,挿入可,1
			SIF CFLAG:着衣併用 != 0
				PRINTFORML フラグ,着衣併用,{CFLAG:着衣併用}
			SIF CFLAG:上着パイズリ != 0
				PRINTFORML フラグ,上着パイズリ,{CFLAG:上着パイズリ}
			SIF CFLAG:下着パイズリ != 0
				PRINTFORML フラグ,下着パイズリ,{CFLAG:下着パイズリ}

			PRINTL 
			PRINTFORML CSTR,種族,%CSTR:種族%
			IF CFLAG:グリム
				PRINTL 
				PRINTL フラグ,グリム,1
			ENDIF
			
			SIF CSTR:登場作品 != ""
				PRINTFORML CSTR,登場作品,%CSTR:登場作品%
			SIF CSTR:登場作品２ != ""
				PRINTFORML CSTR,登場作品２,%CSTR:登場作品２%
			SIF CSTR:登場作品３ != ""
				PRINTFORML CSTR,登場作品３,%CSTR:登場作品３%
			SIF CSTR:キャラデザ != ""
				PRINTFORML CSTR,キャラデザ,%CSTR:キャラデザ%
			SIF CSTR:キャラデザ２ != ""
				PRINTFORML CSTR,キャラデザ２,%CSTR:キャラデザ２%
			PRINTL 
			OUTPUTLOG
			PRINTW ログに出力しました。CSVとして利用する方法は資料フォルダ内のテキストをご覧ください
		ENDIF
		GOTO RESTART
	CASE "1"
		PRINTL 現在の作業状況のCSVを数値でログに出力します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		IF RESULT == 0
			CLEARLINE LINECOUNT
			PRINTFORML 番号,{NO:TARGET}
			PRINTFORML 名前,%NAME:TARGET%
			IF CALLNAME:TARGET == ""
				PRINTFORML 呼び名,%NAME:TARGET%
			ELSE
				PRINTFORML 呼び名,%CALLNAME:TARGET%
			ENDIF
			PRINTL 
			PRINTFORML フラグ,{GETNUM(CFLAG, "価格")},{CFLAG:価格}
			SIF CFLAG:誕生日
				PRINTFORML フラグ,{GETNUM(CFLAG, "誕生日")},{CFLAG:誕生日}
			SIF CFLAG:血縁コード
				PRINTFORML フラグ,{GETNUM(CFLAG, "血縁コード")},{CFLAG:血縁コード}
			SIF CFLAG:続柄
				PRINTFORML フラグ,{GETNUM(CFLAG, "続柄")},{CFLAG:続柄}
			SIF CFLAG:兄弟
				PRINTFORML フラグ,{GETNUM(CFLAG, "兄弟")},{CFLAG:兄弟}
			SIF CFLAG:姉妹
				PRINTFORML フラグ,{GETNUM(CFLAG, "姉妹")},{CFLAG:姉妹}
				
			PRINTL 
			PRINTFORML 基礎,0,{MAXBASE:体力}
			PRINTFORML 基礎,1,{MAXBASE:気力}
			FOR LCOUNT, 0, VARSIZE("EXP")
				SIF EXP:LCOUNT != 0
					PRINTFORML 経験,{LCOUNT},{EXP:LCOUNT}
			NEXT
			FOR LCOUNT, 0, VARSIZE("ABL")
				SIF ABL:LCOUNT != 0
					PRINTFORML 能力,{LCOUNT},{ABL:LCOUNT}
			NEXT
			SIF TALENT:恋心 != 0
				PRINTFORML CSTR,恋心,%CSVNAME(TALENT:恋心)%
			SIF TALENT:既婚者 != 0
				PRINTFORML CSTR,既婚者,%CSVNAME(TALENT:既婚者)%
			PRINTFORML 素質,{GETNUM(TALENT, "性別")},{TALENT:性別}
			SIF SEX(TARGET) == 2
				PRINTFORML 素質,{GETNUM(TALENT, "バストサイズ")},{TALENT:バストサイズ}
			FOR LCOUNT, 0, VARSIZE("TALENT")
				SELECTCASE TALENTNAME:LCOUNT
					CASE "性別", "バストサイズ", "恋心", "既婚者"
					CASEELSE
						IF TALENT:LCOUNT != 0
							IF 素質数値指定
								PRINTFORML 素質,{LCOUNT},{TALENT:LCOUNT}
							ELSE
								PRINTFORML 素質,{LCOUNT}
							ENDIF
						ENDIF
				ENDSELECT
			NEXT
			IF MAXARRAY(RELATION) != 100 || MINARRAY(RELATION) != 100
				PRINTL {
				PRINTL CSTR,相性,
				FOR LCOUNT, 0, 10000
					SIF RELATION:LCOUNT != 100
						PRINTFORML %CSVNAME(LCOUNT)%*{RELATION:LCOUNT}/
				NEXT
				PRINTL }
			ENDIF
			IF MAX(モブ経験人数:TARGET:0, MAXARRAY(経験回数)) > 0
				PRINTL {
				PRINTL CSTR,性経験,
				SIF モブ経験人数:0
					PRINTFORML モブ経験人数/{モブ経験人数}/
				SIF モブ経験回数:0
					PRINTFORML モブ経験回数/{モブ経験回数}/
				SIF モブ中出し回数:0
					PRINTFORML モブ中出し回数/{モブ中出し回数}/
				SIF モブアナル中出し回数:0
					PRINTFORML モブ経験アナル中出し回数/{モブアナル中出し回数}/
				SIF モブ中出され回数:0
					PRINTFORML モブ中出され回数/{モブ中出され回数}/
				SIF モブアナル中出され回数:0
					PRINTFORML モブアナル中出され回数/{モブアナル中出され回数}/
				SIF モブ孕み回数:0
					PRINTFORML モブ孕み回数/{モブ孕み回数}/
				SIF モブ孕ませ回数:0
					PRINTFORML モブ孕ませ回数/{モブ孕ませ回数}/
				FOR LCOUNT, 0, 10000
					SIF 経験回数:LCOUNT
						PRINTFORML 経験回数/%CSVNAME(LCOUNT)%*{経験回数:LCOUNT}/
					SIF 中出し回数:LCOUNT
						PRINTFORML 中出し回数/%CSVNAME(LCOUNT)%*{中出し回数:LCOUNT}/
					SIF アナル中出し回数:LCOUNT
						PRINTFORML アナル中出し回数/%CSVNAME(LCOUNT)%*{アナル中出し回数:LCOUNT}/
					SIF 中出され回数:LCOUNT
						PRINTFORML 中出され回数/%CSVNAME(LCOUNT)%*{中出され回数:LCOUNT}/
					SIF アナル中出され回数:LCOUNT
						PRINTFORML アナル中出され回数/%CSVNAME(LCOUNT)%*{アナル中出され回数:LCOUNT}/
					SIF 孕み回数:LCOUNT
						PRINTFORML 孕み回数/%CSVNAME(LCOUNT)%*{孕み回数:LCOUNT}/
					SIF 孕ませ回数:LCOUNT
						PRINTFORML 孕ませ回数/%CSVNAME(LCOUNT)%*{孕ませ回数:LCOUNT}/
				NEXT
				PRINTL }
			ENDIF
			SIF CSTR:童貞 != ""
				PRINTFORML CSTR,童貞,%CSTR:童貞%
			SIF CSTR:処女 != ""
				PRINTFORML CSTR,処女,%CSTR:処女%
			PRINTL 
			FOR LCOUNT, 0, 100
				IF CSTR:LCOUNT != ""
					PRINTFORML CSTR,{LCOUNT},%CSTR:LCOUNT%
				ELSE
					BREAK
				ENDIF
			NEXT
			
			PRINTL 
			
			PRINTL ;服装
			FOR LCOUNT, 0, VARSIZE("CSTR")
				SELECTCASE CSTRNAME:LCOUNT
					CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
						IF CFLAG:(CSTRNAME:LCOUNT) == 100
							PRINTFORML フラグ,{LCOUNT},100
						ELSEIF CSTR:LCOUNT != ""
							PRINTFORML CSTR,{LCOUNT},%CSTR:LCOUNT%
						ENDIF
				ENDSELECT
			NEXT
			SIF CFLAG:挿入可
				PRINTFORML フラグ,{GETNUM(CFLAG, "挿入可")},1
			SIF CFLAG:着衣併用 != 0
				PRINTFORML フラグ,{GETNUM(CFLAG, "着衣併用")},{CFLAG:着衣併用}
			SIF CFLAG:上着パイズリ != 0
				PRINTFORML フラグ,{GETNUM(CFLAG, "上着パイズリ")},{CFLAG:上着パイズリ}
			SIF CFLAG:下着パイズリ != 0
				PRINTFORML フラグ,{GETNUM(CFLAG, "下着パイズリ")},{CFLAG:下着パイズリ}

			PRINTL 
			PRINTFORML CSTR,{GETNUM(CSTR, "種族")},%CSTR:種族%
			IF CFLAG:グリム
				PRINTL 
				PRINTL フラグ,グリム,1
			ENDIF
			
			SIF CSTR:登場作品 != ""
				PRINTFORML CSTR,{GETNUM(CSTR, "登場作品")},%CSTR:登場作品%
			SIF CSTR:登場作品２ != ""
				PRINTFORML CSTR,{GETNUM(CSTR, "登場作品２")},%CSTR:登場作品２%
			SIF CSTR:登場作品３ != ""
				PRINTFORML CSTR,{GETNUM(CSTR, "登場作品３")},%CSTR:登場作品３%
			SIF CSTR:キャラデザ != ""
				PRINTFORML CSTR,{GETNUM(CSTR, "キャラデザ")},%CSTR:キャラデザ%
			SIF CSTR:キャラデザ２ != ""
				PRINTFORML CSTR,{GETNUM(CSTR, "キャラデザ２")},%CSTR:キャラデザ２%
			PRINTL 
			OUTPUTLOG
			PRINTW ログに出力しました。CSVとして利用する方法は資料フォルダ内のテキストをご覧ください
		ENDIF
		GOTO RESTART
	CASE "2"
		PRINTL 現在の作業状況を破棄してタイトル画面に戻ります。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		IF RESULT == 0
			DELALLCHARA
			RETURN
		ENDIF
		GOTO RESTART
	CASE "3"
		PRINTL 現在の作業状況を破棄して既存のCSVを呼び出します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		IF RESULT == 0
			CALL CHARA_LIST, 1
			DELALLCHARA
			ADDVOIDCHARA
			NAME:0 = 主人
			CALLNAME:0 = 主人
			LRESULT = RESULT
			SKIPDISP 1
			CALL CHARA_ADD, LRESULT
			SKIPDISP 0
			TARGET = 1
		ENDIF
		GOTO RESTART
	CASE "4"
		PRINTL 現在の作業状況を破棄してCSVを作り直します。よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		IF RESULT == 0
			RESETDATA
			RESTART
		ENDIF
		GOTO RESTART
	CASE "キャラ番号"
		PRINTL キャラ番号を入力してください
		INPUT
		NO:TARGET = RESULT
		GOTO RESTART
	CASE "価格"
		PRINTL 価格を入力してください
		INPUT
		CFLAG:価格 = RESULT
		GOTO RESTART
	CASE "誕生日"
		PRINTL 誕生日を4桁で入力してください(例:6月24日→0624)
		DO
			INPUT
		LOOP !INRANGE(RESULT/100, 1, 12) || !INRANGE(RESULT%100, 1, 31)
		CFLAG:誕生日 = RESULT
		GOTO RESTART
	CASE "名前"
		PRINTL フルネームを入力してください
		INPUTS
		NAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "呼び名"
		PRINTL 呼び名を入力してください
		INPUTS
		CALLNAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "性別"
		PRINTL 性別を選んでください
		PRINTL [1] - 男
		PRINTL [2] - 女
		CALL INPUTF, 1, 2
		TALENT:性別 = RESULT
	CASE "体力"
		PRINTL 体力の数値を入力してください
		INPUT
		MAXBASE:体力 = RESULT
		GOTO RESTART
	CASE "気力"
		PRINTL 気力の数値を入力してください
		INPUT
		MAXBASE:気力 = RESULT
		GOTO RESTART
	CASE "種族"
		PRINTL 種族名を入力してください
		INPUTS
		CSTR:TARGET:種族 = %RESULTS%
		GOTO RESTART
	CASE "素質を変更する"
		CALL TALENT_CHANGE, 素質数値指定
		GOTO RESTART
	CASE "素質を数値指定する"
		INVERTBIT 素質数値指定, 0
		GOTO RESTART
	CASE "特殊", "上半身上着", "下半身上着", "全身上着", "上半身下着", "下半身下着", "足元"
		表示用変数 = %RESULTS%
		PRINTFORML %表示用変数%の名前を入力してください
		PRINTBUTTON "[着衣不可にする]", "着衣不可にする"
		PRINTL 
		INPUTS
		SELECTCASE RESULTS
			CASE "着衣不可にする"
				CFLAG:表示用変数 = 100
				CSTR:表示用変数 = 
			CASEELSE
				CSTR:表示用変数 = %RESULTS%
		ENDSELECT
		GOTO RESTART
	CASE "挿入可"
		PRINTL 挿入可能フラグを設定してください
		PRINTL [0] - フラグ無し
		PRINTL [1] - 下半身上着、もしくは全身上着を着たまま性交可能
		CALL INPUTF, 0, 1
		CFLAG:挿入可 = RESULT
		GOTO RESTART
	CASE "着衣併用"
		PRINTL 全身上着の着衣併用フラグを設定してください
		PRINTL [0] - フラグ無し
		PRINTL [1] - 上半身上着及び下半身上着との併用可能
		PRINTL [2] - 上半身下着及び下半身下着との併用不可能
		CALL INPUTF, 0, 1, 2
		CFLAG:着衣併用 = RESULT
		GOTO RESTART
	CASE "相性を変更する"
		PRINTL 相手の番号を入力してください
		PRINT [10000] 一覧表示 
		PRINTL [10001] 全部既定値に戻す
		INPUT
		IF RESULT == 10000
			PRINTL □ 一覧表示
			CALL CHARA_LIST, 1
		ELSEIF RESULT == 10001
			VARSET RELATION:TARGET:0, 100
			GOTO RESTART
		ELSEIF !EXISTCSV(RESULT)
			PRINTFORMW {RESULT}にはキャラが登録されていません
			GOTO RESTART
		ENDIF
		LRESULT = RESULT
		PRINTFORML %CSVNAME(LRESULT)%との相性値を入力してください
		INPUT
		RELATION:LRESULT = lRESULT
		GOTO RESTART
	CASE "マニュアルを変更する"
		$MANUAL
		PRINTL 変更する行を選んでください
		FOR LCOUNT, 0, 100
			IF CSTR:LCOUNT == ""
				表示数 = LCOUNT
				BREAK
			ENDIF
			PRINTFORML [{LCOUNT}]%CSTR:LCOUNT%
		NEXT
		DRAWLINE
		PRINT  [100] 完了 
		PRINTL [101] 行を追加
		DO
			INPUT
			SELECTCASE RESULT
				CASE 100
					GOTO RESTART
				CASE 101
					PRINTL 追加する行のマニュアルを入力してください
					INPUTS
					CSTR:表示数 = %RESULTS%
					GOTO MANUAL
				CASEELSE
					IF CSTR:RESULT != ""
						PRINTFORML {RESULT}行目のマニュアルを入力してください
						PRINTBUTTON "[この行を削除]", "この行を削除"
						PRINTL 
						INPUTS %CSTR:RESULT%
						IF RESULTS == "この行を削除"
							FOR LCOUNT, RESULT, 表示数
								CSTR:LCOUNT = %CSTR:(LCOUNT+1)%
							NEXT
							GOTO MANUAL
						ENDIF
						CSTR:RESULT = %RESULTS%
						GOTO MANUAL
					ENDIF
			ENDSELECT
		LOOP 1
	CASE "登場作品"
		PRINTL 登場作品名を入力してください
		INPUTS
		CSTR:登場作品 = %RESULTS%
		GOTO RESTART
	CASE "登場作品２"
		PRINTL 登場作品名を入力してください
		INPUTS
		CSTR:登場作品２ = %RESULTS%
		GOTO RESTART
	CASE "登場作品３"
		PRINTL 登場作品名を入力してください
		INPUTS
		CSTR:登場作品３ = %RESULTS%
		GOTO RESTART
	CASE "キャラデザ"
		PRINTL キャラデザイナー名を入力してください
		INPUTS
		CSTR:キャラデザ = %RESULTS%
		GOTO RESTART
	CASE "キャラデザ２"
		PRINTL キャラデザイナー名を入力してください
		INPUTS
		CSTR:キャラデザ２ = %RESULTS%
		GOTO RESTART
	CASE "性経験設定"
		CALL STATUS_SEX_EXP, 1
		GOTO RESTART
	CASE "経験無し"
		SIF PENIS(TARGET)
			TALENT:童貞 = 1
		SIF VAGINA(TARGET)
			TALENT:処女 = 1
		VARSET EXP, 0
	CASE "経験ランダム"
		TALENT:童貞 = 0
		TALENT:処女 = 0
		VARSET EXP, 0
		IF VAGINA(TARGET)
			EXP:Ｖ経験 = RAND(10, 31)
			EXP:膣射経験 = RAND(0, 6)
			EXP:絶頂経験 += RAND(10, 21)
			EXP:精液経験 = RAND(20, 51)+EXP:膣射経験
		ENDIF
		IF PENIS(TARGET)
			EXP:射精経験 = RAND(50, 101)
			EXP:絶頂経験 += EXP:射精経験
		ENDIF
		IF SEX(TARGET) == 2
			EXP:パイズリ経験 = RAND(3, 11)
			EXP:フェラ経験 = RAND(5, 21)
		ENDIF
		EXP:性交経験 = RAND(5, 11)
		EXP:自慰経験 = RAND(10, 31)
	CASE "経験ランダム(すごい)"
		TALENT:童貞 = 0
		TALENT:処女 = 0
		VARSET EXP, 0
		IF VAGINA(TARGET)
			EXP:Ｖ経験 = RAND(500, 1001)
			EXP:膣射経験 = RAND(300, 501)
			EXP:出産経験 = RAND(1, 4)
			SIF RAND:5 == 0
				EXP:流産経験 = 1
		ENDIF
		SIF PENIS(TARGET)
			EXP:射精経験 = RAND(2000, 5001)
		SELECTCASE SEX(TARGET)
			CASE 1
				EXP:ゲイ経験 = RAND(500, 1001)
			CASE 2
				EXP:レズ経験 = RAND(500, 1001)
				EXP:パイズリ経験 = RAND(200, 301)
		ENDSELECT
		EXP:Ａ経験 = RAND(500, 1001)
		EXP:性交経験 = RAND(300, 501)
		EXP:自慰経験 = RAND(1000, 2001)
		EXP:フェラ経験 = RAND(500, 1001)
		EXP:絶頂経験 = RAND(500, 1001)+EXP:射精経験
		EXP:精液経験 = RAND(2000, 5001)+EXP:膣射経験
	CASEELSE
		RESULTS = %REPLACE(RESULTS, " ", "")%
		SIF RESULTS == ""
			GOTO RESTART
		SIF RESULTS == "舞踊経験"
			RESULTS = ダンス経験
		SIF RESULTS == "音ゲ経験"
			RESULTS = 音ゲー経験
		SIF RESULTS == "Ｖ拡経験"
			RESULTS = Ｖ拡張経験
		SIF RESULTS == "Ａ拡経験"
			RESULTS = Ａ拡張経験
		SIF RESULTS == "口淫経験"
			RESULTS = フェラ経験
		SIF RESULTS == "虐快経験"
			RESULTS = 嗜虐快楽経験
		SIF RESULTS == "奉快経験"
			RESULTS = 奉仕快楽経験
		SIF RESULTS == "痛楽経験"
			RESULTS = 苦痛快楽経験
		SIF RESULTS == "射精管理"
			RESULTS = 射精管理経験
		SIF RESULTS == "乳擦経験"
			RESULTS = パイズリ経験
		
		FOR LCOUNT, 0, VARSIZE("EXP")
			IF EXPNAME:LCOUNT == RESULTS
				PRINTFORML %EXPNAME:LCOUNT%の数値を入力してください
				INPUT
				EXP:LCOUNT = RESULT
			ENDIF
		NEXT
		FOR LCOUNT, 0, VARSIZE("ABL")
			IF ABLNAME:LCOUNT == RESULTS
				PRINTFORML %ABLNAME:LCOUNT%の数値を入力してください
				INPUT
				ABL:LCOUNT = RESULT
			ENDIF
		NEXT
		GOTO RESTART
ENDSELECT

GOTO RESTART

