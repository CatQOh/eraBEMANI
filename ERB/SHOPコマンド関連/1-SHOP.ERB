@SHOW_SHOP
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIM CSV読み込み

CALL GIDDEL

;学園モードでは別途SHOPを使うためこの関数は使わない
SIF ゲームモード() == "学園モード"
	JUMP SHOP_SCHOOL

;SLGモードも同じく
SIF ゲームモード() == "SLGモード"
	JUMP SHOP_SLG

;ORIGINならCSV実装済みの全キャラ解禁する
IF CSV読み込み == 0 && ORIGIN
	LTARGET = TARGET
	FOR LCOUNT, 0, 10000
		SIF EXISTCSV(LCOUNT) && GETCHARA(LCOUNT) == -1 && NO:MASTER != LCOUNT
			CALL CHARA_ADD, LCOUNT
	NEXT
	TARGET = LTARGET
	CSV読み込み = 1
ENDIF

;ORIGINには金の概念は無いものの内部処理的には多いほうが都合が良いのである程度の額で固定する
SIF ORIGIN
	MONEY = 1000000

;モードに応じてBGMを流す
SELECTCASE ゲームモード()
	CASE "新婚モード"
		CALL 新婚モード_BGM
	CASE "段位認定"
		CALL 段位認定_BGM
	CASEELSE
		CALL 通常モード_BGM
ENDSELECT

VARLINE = LINECOUNT

SIF TARGET == 0
	TARGET = -1

;STRATEGYモードクリアデータを周回可能にする
SIF ゲームモード() == "フリーモード" && FLAG:パートナー
	FLAG:周回可能 = 1

CALL セーブデータアップデート

FOR LCOUNT, 0, CHARANUM
	;CSVを確認する
	SIF !EXISTCSV(NO:LCOUNT) && NO:LCOUNT < 10000
		PRINTFORMW %NAME:LCOUNT%のCSVが存在しません。ゲームを続行するとエラーが起こる場合があります
	;ジェントルストレスの発動中はストレスが上がらない
	IF ジェントルストレス:LCOUNT:0
		ジェントルストレス:LCOUNT:1 = MIN(CFLAG:LCOUNT:ストレス値, ジェントルストレス:LCOUNT:1)
		CFLAG:LCOUNT:ストレス値 = ジェントルストレス:LCOUNT:1
	ENDIF

	IF CFLAG:LCOUNT:グリム
		SELECTCASE FLAG:グリムコンフィグ
			CASE 0
				CALLNAME:LCOUNT = %CSVCALLNAME(NO:LCOUNT)%
				CSTR:LCOUNT:英語呼び名 = %CSVCSTR(NO:LCOUNT, GETNUM(CSTR, "英語呼び名"))%
			CASE 1
				CALLNAME:LCOUNT = グリム・%CSVCALLNAME(NO:LCOUNT)%
				CSTR:LCOUNT:英語呼び名 = Grim %CSVCSTR(NO:LCOUNT, GETNUM(CSTR, "英語呼び名"))%
		ENDSELECT
	ENDIF
	IF TALENT:LCOUNT:魔術師
		SIF ABL:LCOUNT:魔法技能 < 3
			ABL:LCOUNT:魔法技能 = 3
		MAXBASE:LCOUNT:魔力 = ABL:LCOUNT:魔法技能*1000
		BASE:LCOUNT:魔力 = LIMIT(BASE:LCOUNT:魔力, 0, MAXBASE:LCOUNT:魔力)
	ENDIF
	IF CSTR:LCOUNT:種族 != "鳳凰" && MAXBASE:LCOUNT:鳳凰基礎体力
		MAXBASE:LCOUNT:体力 = MAXBASE:LCOUNT:鳳凰基礎体力
		MAXBASE:LCOUNT:気力 = MAXBASE:LCOUNT:鳳凰基礎気力
		MAXBASE:LCOUNT:鳳凰基礎体力 = 0
		MAXBASE:LCOUNT:鳳凰基礎気力 = 0
	ENDIF
NEXT

CALL CVAR_RESET

IF GC店名 == ""
	IF LANGUAGE == "ENG"
		GC店名 = Game Center
	ELSE
		GC店名 = ゲームセンター
	ENDIF
ENDIF

IF 娼館店名 == ""
	IF LANGUAGE == "ENG"
		娼館店名 = Brothel
	ELSE
		娼館店名 = 娼館
	ENDIF
ENDIF

;GUMINDAYSキャラ5人以上陥落でユリア天使化イベント
SIF GUMINDAYS >= 5
	CALL ユリア天使化

;実績:STRATEGYモードで期限内に100億円稼ぐ
SIF ゲームモード() == "経営モード" && MONEY >= TONUM("100億")
	CALL 実績解除, "10，000，000，000"

;実績:5000兆円稼ぐ
SIF MONEY >= TONUM("5000兆")
	CALL 実績解除, "5000兆円欲しい！"

;陥落済みなら陥落済みフラグをオン
IF TARGET > 0
	IF FALLTYPE(TARGET) != 0
		CFLAG:陥落 = 1
	ELSE
		CFLAG:陥落 = 0
	ENDIF
ENDIF

SIF 受験段位名 == "魅惑のサバト"
	ITEM:媚薬 = 99

;新婚モードで経験値0なら53万補充される
SIF ゲームモード() == "新婚モード" && FLAG:経験値 == 0
	FLAG:経験値 = TONUM("53万")

;曜日計算
FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

;祝日・記念日の設定
CALL HOLIDAY

;対象か助手が使用不可の場合は外す
SIF TARGET >= 0 && CFLAG:使用不可
	TARGET = -1
SIF (ASSI >= 0 && CFLAG:ASSI:使用不可) || ASSI == 0
	ASSI = -1

VARLINE = LINECOUNT

MAX_LINE = 40

;段位認定では施設経営を外す
IF ゲームモード() == "段位認定"
	CALL カラム生成, "SHOP", "メイン", "キャラクター", "キャラ・アイテム購入", "その他", "データベース", "セーブ", "ロード", "ショートカット", "オプション"
ELSE
	CALL カラム生成, "SHOP", "メイン", "キャラクター", "施設・経営", "キャラ・アイテム購入", "その他", "データベース", "セーブ", "ロード", "ショートカット", "オプション"
ENDIF

@SHOP_メイン
#DIMS DYNAMIC 能力表示用, 4
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 店長
#DIM DYNAMIC 誕生日キャラ
#DIMS DYNAMIC LCALL
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 売却可能
#DIM DYNAMIC 調教可能
;ゲーム内で表示されるショップ画面
;□XXXX年XX月XX日(曜日) X日目(昼夜) X周目 MODE:XXXXX
;　天気:XX 気温:XX.X℃ XXX(月齢.XXX) 
;　所持金:$XXXX 昨日の所持金:$XXXX XXXX 変動価格
;　難易度:XXXX
;　総奴隷数:X/陥落済み:X
;
;□調教中 [750]　前のキャラ [751] 次のキャラ
;　No.X XXXXXX(♂♀)[ふたなり]【陥落素質等】依存度:XX ストレス値:XXX/999
;　従順:LVXX 欲望:LVXX 技巧:LVXX
;　Ｃ感:LVXX Ｖ感:LVXX Ａ感:LVXX Ｂ感:LVXX
;　苦痛刻印:LVX [XXX]　快楽刻印:LVX [XXX]　屈服刻印:LVX [XXX]　反発刻印:LVX [XXX]
;　体力[********************************](XXXX/XXXX)[排卵誘発中]
;　気力[********************************](XXXX/XXXX)
;
;　保管済み母乳:Xml　保管済み精液:Xml　保管済み血液:Xml　保管済み粘液:Xml
;　主人:XXX
;　助手:XXX
;-------------------------------------------------------------------------------------
;□ゲームセンターの状況
;　営業中or休業中 [700]営業を開始するor休業する
;　前日の収入:($XXX) 店長:XXXX
;-------------------------------------------------------------------------------------
;□娼館の状況
;　営業中or休業中 [701]営業を開始するor休業する
;　前日の収入:($XXX)
;-------------------------------------------------------------------------------------
;□飲食店の状況
;　営業中or休業中 [702]営業を開始するor休業する
;　前日の収入:($XXX)
;-------------------------------------------------------------------------------------
;□適用中のMOD(あれば表示)
;　[XXX][XXX]
;

;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 1, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
	;売却可能
	SIF CFLAG:LCOUNT:状態 >= 1 && !CFLAG:LCOUNT:売却不可
		売却可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;対象も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT

;日数表示
;CALL CHECKDRAWLINE
CALL COLUMNPRINT, 0, @"□%日付表示()%"

;ORIGINでは表示しない
IF !ORIGIN
	SIF FLAG:周回数 > 1
		CALL COLUMNPRINT, 0, @" {FLAG:周回数}周目 "
	IF ゲームモード() == "段位認定"
		CALL GRADE, FLAG:選択段位, 1
	ELSE
		CALL COLUMNPRINT, 0, @"%ゲームモード表示()% "
	ENDIF
	CALL COLUMNPRINT, 0, ""
;ただしeraBEMANIのORIGINモードでは表示
ELSEIF ORIGIN && GAMEBASE_TITLE == "eraBEMANI+α"
	CALL COLUMNPRINT, 0, "MODE:Origin "
ENDIF
CALL COLUMNPRINTL, 0, ""

IF LANGUAGE == "ENG"
	CALL COLUMNPRINTL, 0, @"　Weather:%WEATHERDISP()% Temperature:%DIV10(FLAG:気温)%℃"
ELSE
	CALL COLUMNPRINT, 0, @"　天気:"
	IF GUI
		;夜で晴れなら月アイコン
		IF TIME == 1 && 天気() == "晴れ"
			CALL COLUMNIMG, 0, 月齢画像(), 100, 100
		ELSE
			CALL COLUMNIMG, 0, 天気(), 100, 100
		ENDIF
	ELSE
		CALL COLUMNPRINT, 0, @"%天気()%"
	ENDIF
	;GUIモードでは情報量を減らしていく
	SIF !GUI
		CALL COLUMNPRINTL, 0, @" 気温:%DIV10(FLAG:気温)%℃ %月齢()%(月齢:%DIV100(月齢)%)"
ENDIF

;段位認定のときのみ表示を変える
;ORIGINでは所持金、難易度は表示しない
IF !ORIGIN
	IF ゲームモード() != "段位認定"
		IF LANGUAGE == "ENG"
			;所持金表示
			CALL COLUMNPRINT, 0, @"　Money:{MONEY}＄"
			SIF 借金 > 0
				CALL COLUMNPRINT, 0, @" Debts:{借金}＄"
			CALL COLUMNPRINT, 0, @" Money(Yesterday):{LASTMONEY}＄"
			IF MONEY == LASTMONEY
				CALL COLUMNPRINTL, 0, "→0 KEEP"
			ELSEIF MONEY > LASTMONEY
				CALL COLUMNPRINTL, 0, @"↑{MONEY-LASTMONEY} UP!"
			ELSEIF MONEY < LASTMONEY
				CALL COLUMNPRINTL, 0, @"↓{LASTMONEY-MONEY} DOWN..."
			ENDIF

			;新婚生活中は非表示
			IF ゲームモード() != "新婚モード"
				;難易度表示
				CALL COLUMNPRINT, 0, @"　Difficulty:%難易度()%"
				;奴隷人数表示
				CALL COLUMNPRINTL, 0, @"　Characters:{CHARANUM-1}\/Fell:{CMATCH(CFLAG:陥落, 1)}"
			ENDIF
		ELSE
			;所持金表示
			CALL COLUMNPRINT, 0, @"　所持金:%TOOPTIONINT(MONEY)%＄"
			SIF 借金 > 0
				CALL COLUMNPRINT, 0, @" 借金:%TOOPTIONINT(借金)%＄"
			CALL COLUMNPRINT, 0, @" 昨日の所持金:%TOOPTIONINT(LASTMONEY)%＄"
			IF MONEY == LASTMONEY
				CALL COLUMNPRINT, 0, "→0 KEEP "
			ELSEIF MONEY > LASTMONEY
				CALL COLUMNPRINT, 0, @"↑%TOOPTIONINT(MONEY-LASTMONEY)% UP!"
			ELSEIF MONEY < LASTMONEY
				CALL COLUMNPRINT, 0, @"↓%TOOPTIONINT(LASTMONEY-MONEY)% DOWN..."
			ENDIF
			IF GUI
				;ここらへんでカレンダーを表示してみる
				CALL カレンダー生成
				;そうそうサイズは変わらないと思うのでサイズ直指定してみる
				CALL COLUMNPRINT, 0, "　　　　　　　"
				CALL COLUMNIMG, 0, "カレンダー", 1095, 1162
			ENDIF
			CALL COLUMNPRINTL, 0, ""

			;新婚生活中は非表示
			IF ゲームモード() != "新婚モード"
				;難易度表示
				CALL COLUMNPRINT, 0, @"　難易度:%難易度()%"
				;奴隷人数表示
				CALL COLUMNPRINTL, 0, @"　総奴隷数:{CHARANUM-1}\/陥落済み:{CMATCH(CFLAG:陥落, 1)}"
			ENDIF
		ENDIF
	ELSE
		IF LANGUAGE == "ENG"
			;所持金表示
			CALL COLUMNPRINT, 0, @"　Money:{MONEY}＄"
			SIF ストック > 0
				CALL COLUMNPRINT, 0, @" (Stock:{ストック}＄)"
			PRINTL

			;難易度表示
			CALL COLUMNPRINT, 0, @"　Difficulty:%難易度()%"
			;売却可能人数表示
			CALL COLUMNPRINTL, 0, @"Characters:{CHARANUM-1}\/Can sell:{CMATCH(CFLAG:状態, 1)+CMATCH(CFLAG:状態, 2)}"
		ELSE
			;所持金表示
			CALL COLUMNPRINT, 0, @"　所持金:%TOOPTIONINT(MONEY)%＄"
			SIF ストック > 0
				CALL COLUMNPRINT, 0, @" (ストック:%TOOPTIONINT(ストック)%＄)"
			PRINTL

			;難易度表示
			CALL COLUMNPRINT, 0, @"　難易度:%難易度()%"
			;売却可能人数表示
			CALL COLUMNPRINTL, 0, @"総奴隷数:{CHARANUM-1}\/売却可能:{CMATCH(CFLAG:状態, 1)+CMATCH(CFLAG:状態, 2)}"
		ENDIF
		CALL COLUMNPRINT, 0, "　"
		CALL 段位ゲージ表示
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ENDIF

;奴隷の状態------------------------------------------------------------
IF TARGET > 0
	CALL COLUMNPRINTL, 0, ""
	IF ゲームモード() == "新婚モード"
		IF LANGUAGE == "ENG"
			CALL COLUMNPRINTL, 0, "□Partner"
		ELSE
			CALL COLUMNPRINTL, 0, "□同棲中"
		ENDIF
		CALL COLUMNPRINT, 0, @"　%NAMEDISP(TARGET)%"
	ELSE
		IF LANGUAGE == "ENG"
			CALL COLUMNPRINT, 0, "□Training "
			SIF TARGET > 1
				CALL COLUMNPRINT, 0, "[10] Prev character "
			SIF TARGET < CHARANUM-1
				CALL COLUMNPRINT, 0, "[19] Next character"
			CALL COLUMNPRINTL, 0, ""
		ELSE
			CALL COLUMNPRINT, 0, "□調教中 "
			SIF TARGET > 1
				CALL COLUMNPRINT, 0, "[10] 前のキャラ "
			SIF TARGET < CHARANUM-1
				CALL COLUMNPRINT, 0, "[19] 次のキャラ"
			CALL COLUMNPRINTL, 0, ""
		ENDIF
		CALL COLUMNPRINT, 0, @"　No.{TARGET} %NAMEDISP(TARGET)%"
	ENDIF
	CALL COLUMNPRINT, 0, @"%性別表示(TARGET)% "
	IF LANGUAGE == "ENG"
		SIF TALENT:ふたなり
			CALL COLUMNPRINT, 0, "[Futanari]"
		SIF TALENT:アニマル
			CALL COLUMNPRINT, 0, "[Animal]"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "隷属", "崩壊", "淫核", "淫膣", "淫尻", "淫乳"
					CALL COLUMNPRINT, 0, @"【%TALENTDISP(LCOUNT)%】"
				CASE "妊娠"
					CALL COLUMNPRINT, 0, "【Prengnant】"
				CASE "リミットバースト"
					CALL COLUMNPRINT, 0, "【BURST】"
			ENDSELECT
		NEXT
		SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
			CALL COLUMNPRINT, 0, @"Depend:{CFLAG:依存度} "
		CALL COLUMNPRINT, 0, "Stress:"

		SELECTCASE CFLAG:ストレス値
			CASE 0
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "gray"
			CASE 1 TO 99
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "lime"
			CASE 100 TO 299
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "yellow"
			CASE 300 TO 499
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "red"
			CASE IS >= 500
				CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "darkred"
		ENDSELECT

		CALL COLUMNPRINTL, 0, ""
		
		IF GUI
			CALL COLUMNTOOLTIP, 0, "　Obedience:", ABL_COMMENT("従順")
			CALL COLUMNIMG, 0, GBAR(ABL:従順, 10, 3, BARCOLOR:能力, "gray", @"{ABL:従順}", "black")
			CALL COLUMNTOOLTIP, 0, " Lust:", ABL_COMMENT("欲望")
			CALL COLUMNIMG, 0, GBAR(ABL:欲望, 10, 3, BARCOLOR:能力, "gray", @"{ABL:欲望}", "black")
			CALL COLUMNTOOLTIP, 0, " Technique:", ABL_COMMENT("技巧")
			CALL COLUMNIMG, 0, GBAR(ABL:技巧, 10, 3, BARCOLOR:能力, "gray", @"{ABL:技巧}", "black")
			CALL COLUMNPRINTL, 0, ""

			CALL COLUMNTOOLTIP, 0, "　CSence:", ABL_COMMENT("Ｃ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ｃ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｃ感覚}", "black")
			IF VAGINA(TARGET)
				CALL COLUMNTOOLTIP, 0, " VSence:", ABL_COMMENT("Ｖ感覚")
				CALL COLUMNIMG, 0, GBAR(ABL:Ｖ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｖ感覚}", "black")
			ENDIF
			CALL COLUMNTOOLTIP, 0, " ASence:", ABL_COMMENT("Ａ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ａ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ａ感覚}", "black")
			CALL COLUMNTOOLTIP, 0, @" BSence:", ABL_COMMENT("Ｂ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ｂ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｂ感覚}", "black")
			CALL COLUMNPRINTL, 0, ""
		ELSE
			CALL COLUMNTOOLTIP, 0, @"　Obedience:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順")
			CALL COLUMNTOOLTIP, 0, @" Lust:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望")
			CALL COLUMNTOOLTIPL, 0, @" Technique:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧")

			CALL COLUMNTOOLTIP, 0, @"　CSence:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚")
			SIF VAGINA(TARGET)
				CALL COLUMNTOOLTIP, 0, @" VSence:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚")
			CALL COLUMNTOOLTIP, 0, @" ASence:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚")
			CALL COLUMNTOOLTIPL, 0, @" BSence:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚")
		ENDIF

		CALL COLUMNTOOLTIP, 0, @" PainMark:LV{MARK:苦痛刻印, 2, LEFT}", MARK_COMMENT("苦痛刻印")
		CALL COLUMNTOOLTIP, 0, @" PlesureMark:LV{MARK:快楽刻印, 2, LEFT}", MARK_COMMENT("快楽刻印")
		CALL COLUMNTOOLTIPL, 0, @" YieldMark:LV{MARK:屈服刻印, 2, LEFT}", MARK_COMMENT("屈服刻印")

		CALL COLUMNPRINTL, 0, @"　Health%BARBASE("体力")%"
		CALL COLUMNPRINTL, 0, @"　Energy%BARBASE("気力")%"
		SIF TALENT:魔術師
			CALL COLUMNPRINTL, 0, @"　MagicP%BARBASE("魔力")%"
	ELSE
		SIF TALENT:ふたなり
			CALL COLUMNPRINT, 0, "[ふたなり]"
		SIF TALENT:アニマル
			CALL COLUMNPRINT, 0, "[獣]"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF !TALENT:LCOUNT
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT
				CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "隷属", "崩壊", "淫核", "淫膣", "淫尻", "淫乳"
					CALL COLUMNPRINT, 0, @"【%TALENTDISP(LCOUNT)%】"
				CASE "妊娠"
					IF LANGUAGE == "ENG"
						CALL COLUMNPRINT, 0, "【Prengnant】"
					ELSE
						CALL COLUMNPRINT, 0, "【妊娠中】"
					ENDIF
				CASE "リミットバースト"
					CALL COLUMNPRINT, 0, "【BURST】"
			ENDSELECT
		NEXT
		SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
			CALL COLUMNPRINT, 0, @"依存度:{CFLAG:依存度} "
		IF GUI
			CALL COLUMNPRINT, 0, @"ストレス値:%IMGTOTAG(BASEBAR("ストレス値", TARGET, 4))%"
		ELSE
			CALL COLUMNPRINT, 0, "ストレス値:"
			SELECTCASE CFLAG:ストレス値
				CASE 0
					CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "gray"
				CASE 1 TO 99
					CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "lime"
				CASE 100 TO 299
					CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "yellow"
				CASE 300 TO 499
					CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "red"
				CASE IS >= 500
					CALL COLUMNPRINT, 0, @"{CFLAG:ストレス値}/999", "darkred"
			ENDSELECT
		ENDIF
		CALL COLUMNPRINTL, 0, ""
		
		IF GUI
			CALL COLUMNTOOLTIP, 0, "　従順:", ABL_COMMENT("従順")
			CALL COLUMNIMG, 0, GBAR(ABL:従順, 10, 3, BARCOLOR:能力, "gray", @"{ABL:従順}", "black")
			CALL COLUMNTOOLTIP, 0, "欲望:", ABL_COMMENT("欲望")
			CALL COLUMNIMG, 0, GBAR(ABL:欲望, 10, 3, BARCOLOR:能力, "gray", @"{ABL:欲望}", "black")
			CALL COLUMNTOOLTIP, 0, "技巧:", ABL_COMMENT("技巧")
			CALL COLUMNIMG, 0, GBAR(ABL:技巧, 10, 3, BARCOLOR:能力, "gray", @"{ABL:技巧}", "black")
			CALL COLUMNPRINTL, 0, ""

			CALL COLUMNTOOLTIP, 0, "　Ｃ感:", ABL_COMMENT("Ｃ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ｃ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｃ感覚}", "black")
			IF VAGINA(TARGET)
				CALL COLUMNTOOLTIP, 0, "Ｖ感:", ABL_COMMENT("Ｖ感覚")
				CALL COLUMNIMG, 0, GBAR(ABL:Ｖ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｖ感覚}", "black")
			ENDIF
			CALL COLUMNTOOLTIP, 0, "Ａ感:", ABL_COMMENT("Ａ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ａ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ａ感覚}", "black")
			CALL COLUMNTOOLTIP, 0, @"Ｂ感:", ABL_COMMENT("Ｂ感覚")
			CALL COLUMNIMG, 0, GBAR(ABL:Ｂ感覚, 10, 3, BARCOLOR:能力, "gray", @"{ABL:Ｂ感覚}", "black")
			CALL COLUMNPRINTL, 0, ""
		ELSE
			CALL COLUMNTOOLTIP, 0, @"　従順:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順")
			CALL COLUMNTOOLTIP, 0, @"欲望:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望")
			CALL COLUMNTOOLTIPL, 0, @"技巧:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧")

			CALL COLUMNTOOLTIP, 0, @"　Ｃ感:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚")
			SIF VAGINA(TARGET)
				CALL COLUMNTOOLTIP, 0, @"Ｖ感:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚")
			CALL COLUMNTOOLTIP, 0, @"Ａ感:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚")
			CALL COLUMNTOOLTIPL, 0, @"Ｂ感:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚")
		ENDIF
		CALL COLUMNTOOLTIP, 0, @"　苦痛刻印:LV{MARK:苦痛刻印, 2, LEFT}", MARK_COMMENT("苦痛刻印")
		CALL COLUMNTOOLTIP, 0, @" 快楽刻印:LV{MARK:快楽刻印, 2, LEFT}", MARK_COMMENT("快楽刻印")
		CALL COLUMNTOOLTIPL, 0, @" 屈服刻印:LV{MARK:屈服刻印, 2, LEFT}", MARK_COMMENT("屈服刻印")

		IF GUI
			CALL COLUMNPRINT, 0, "　体力:"
			CALL COLUMNIMG, 0, BASEBAR("体力")
			CALL COLUMNPRINTL, 0, ""

			CALL COLUMNPRINT, 0, "　気力:"
			CALL COLUMNIMG, 0, BASEBAR("気力")
			CALL COLUMNPRINTL, 0, ""

			IF TALENT:魔術師
				CALL COLUMNPRINT, 0, "　魔力:"
			CALL COLUMNIMG, 0, BASEBAR("魔力")
				CALL COLUMNPRINTL, 0, ""
			ENDIF
		ELSE
			CALL COLUMNPRINTL, 0, @"　体力%BARBASE("体力")%"
			CALL COLUMNPRINTL, 0, @"　気力%BARBASE("気力")%"
			SIF TALENT:魔術師
				CALL COLUMNPRINTL, 0, @"　魔力%BARBASE("魔力")%"
		ENDIF
	ENDIF
ENDIF
CALL COLUMNPRINTL, 0, ""

;主人・助手の状態------------------------------------------------------------
IF LANGUAGE == "ENG"
	SIF FLAG:保管済み母乳
		CALL COLUMNPRINT, 0, @"　Saved Milk:{FLAG:保管済み母乳}ml"
	SIF FLAG:保管済み精液
		CALL COLUMNPRINT, 0, @"　Saved Samen:{FLAG:保管済み精液}ml"
	SIF FLAG:保管済み血液
		CALL COLUMNPRINT, 0, @"　Saved Blood:{FLAG:保管済み血液}ml"
	SIF FLAG:保管済み粘液
		CALL COLUMNPRINT, 0, @"　Saved Mucus:{FLAG:保管済み粘液}ml"
	SIF COLUMNLOG:0:(COLUMN:0:LINE) != ""
		CALL COLUMNPRINTL, 0, ""

	CALL COLUMNPRINT, 0, @"　Master:%NAMEDISP(MASTER)% %性別表示(MASTER)% "
	SIF TALENT:MASTER:ふたなり
		CALL COLUMNPRINT, 0, "[Futanari]"
	CALL COLUMNPRINTL, 0, ""
	SIF TALENT:MASTER:魔術師
		CALL COLUMNPRINTL, 0, @"　MagicP%BARBASE("魔力", MASTER)%"

	IF ASSI >= 0
		IF TALENT:ASSI:アニマル
			CALL COLUMNPRINT, 0, @"　Pet:%NAMEDISP(ASSI)%"
		ELSE
			CALL COLUMNPRINT, 0, @"　Assistant:%NAMEDISP(ASSI)%"
		ENDIF
		CALL COLUMNPRINT, 0, @"%性別表示(ASSI)% "
		SIF TALENT:ASSI:ふたなり
			CALL COLUMNPRINT, 0, "[Futanari]"
		SIF TALENT:ASSI:アニマル
			CALL COLUMNPRINT, 0, "[Animal]"
		CALL COLUMNPRINTL, 0, ""
		SIF TALENT:ASSI:魔術師
			CALL COLUMNPRINTL, 0, @"　MagicP%BARBASE("魔力", ASSI)%"
	ENDIF
ELSE
	SIF FLAG:保管済み母乳
		CALL COLUMNPRINT, 0, @"　保管済み母乳:{FLAG:保管済み母乳}ml"
	SIF FLAG:保管済み精液
		CALL COLUMNPRINT, 0, @"　保管済み精液:{FLAG:保管済み精液}ml"
	SIF FLAG:保管済み血液
		CALL COLUMNPRINT, 0, @"　保管済み血液:{FLAG:保管済み血液}ml"
	SIF FLAG:保管済み粘液
		CALL COLUMNPRINT, 0, @"　保管済み粘液:{FLAG:保管済み粘液}ml"
	SIF COLUMNLOG:0:(COLUMN:0:LINE) != ""
		CALL COLUMNPRINTL, 0, ""

	CALL COLUMNPRINT, 0, @"　主人:%NAMEDISP(MASTER)% %性別表示(MASTER)% "
	SIF TALENT:MASTER:ふたなり
		CALL COLUMNPRINT, 0, "[ふたなり]"
	CALL COLUMNPRINTL, 0, ""
	SIF TALENT:MASTER:魔術師
		CALL COLUMNPRINTL, 0, @"　魔力%BARBASE("魔力", MASTER)%"

	IF ASSI >= 0
		IF TALENT:ASSI:アニマル
			CALL COLUMNPRINT, 0, @"　ペット:%NAMEDISP(ASSI)%"
		ELSE
			CALL COLUMNPRINT, 0, @"　助手:%NAMEDISP(ASSI)%"
		ENDIF
		CALL COLUMNPRINT, 0, @"%性別表示(ASSI)% "
		SIF TALENT:ASSI:ふたなり
			CALL COLUMNPRINT, 0, "[ふたなり]"
		SIF TALENT:ASSI:アニマル
			CALL COLUMNPRINT, 0, "[獣]"
		CALL COLUMNPRINTL, 0, ""
		SIF TALENT:ASSI:魔術師
			CALL COLUMNPRINTL, 0, @"　魔力%BARBASE("魔力", ASSI)%"
	ENDIF
ENDIF

CALL COLUMNDRAWLINE, 0
IF GC物件 && ゲームモード() != "通常モード"
	IF LANGUAGE == "ENG"
		CALL COLUMNPRINTL, 0, @"□%GC店名%"
		IF FLAG:ゲーセン営業中 > 0
			CALL COLUMNPRINT, 0, "　OPEN "
			CALL COLUMNPRINTL, 0, "[20] - Take a leave"
			店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
			SIF 店長 == -1
				店長 = 0
			CALL COLUMNPRINTL, 0, @"　Previous bussines day's income:{基本収入}＄ Store manager:%NAMEDISP(店長)%"
		ELSE
			CALL COLUMNPRINTL, 0, "　CLOSED "
			CALL COLUMNPRINTL, 0, "[20] - Take a open"
		ENDIF
	ELSE
		CALL COLUMNPRINTL, 0, @"□%GC店名%の状況"
		IF FLAG:ゲーセン営業中 > 0
			CALL COLUMNPRINT, 0, "　営業中 "
			CALL COLUMNPRINTL, 0, "[20] - 一時休業する"
			店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
			SIF 店長 == -1
				店長 = 0
			CALL COLUMNPRINTL, 0, @"　前営業日の収入:%TOOPTIONINT(基本収入)%＄ 店長:%NAMEDISP(店長)%"
		ELSE
			CALL COLUMNPRINT, 0, "　休業中 "
			CALL COLUMNPRINTL, 0, "[20] - 営業を開始する"
		ENDIF
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

IF ITEM:風俗店営業許可証 && ゲームモード() != "通常モード"
	IF LANGUAGE == "ENG"
		CALL COLUMNPRINTL, 0, @"□%娼館店名%"
		IF FLAG:娼館営業中 > 0
			CALL COLUMNPRINT, 0, "　OPEN "
			CALL COLUMNPRINTL, 0, "[21] - Take a leave"
			CALL COLUMNPRINTL, 0, @"　Previous bussines day's income:{娼館前日の収入}＄"
		ELSE
			CALL COLUMNPRINT, 0, "　CLOSED "
			CALL COLUMNPRINTL, 0, "[21] - Take a open"
		ENDIF
	ELSE
		CALL COLUMNPRINTL, 0, @"□%娼館店名%の状況"
		IF FLAG:娼館営業中 > 0
			CALL COLUMNPRINT, 0, "　営業中 "
			CALL COLUMNPRINTL, 0, "[21] - 一時休業する"
			CALL COLUMNPRINTL, 0, @"　前営業日の収入:%TOOPTIONINT(娼館前日の収入)%＄"
		ELSE
			CALL COLUMNPRINT, 0, "　休業中 "
			CALL COLUMNPRINTL, 0, "[21] - 営業を開始する"
		ENDIF
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

IF 業種 != "" && ゲームモード() != "通常モード"
	IF LANGUAGE == "ENG"
		CALL COLUMNPRINTL, 0, @"□%飲食店名%"
		IF FLAG:飲食店営業中 > 0
			CALL COLUMNPRINT, 0, "　OPEN "
			CALL COLUMNPRINTL, 0, "[22] - Take a leave"
			CALL COLUMNPRINTL, 0, @"　Previous bussines day's income:{飲食店売上}＄"
		ELSE
			CALL COLUMNPRINT, 0, "　CLOSE "
			CALL COLUMNPRINTL, 0, "[22] - Take a open"
		ENDIF
	ELSE
		CALL COLUMNPRINTL, 0, @"□%飲食店名%の状況"
		IF FLAG:飲食店営業中 > 0
			CALL COLUMNPRINT, 0, "　営業中 "
			CALL COLUMNPRINTL, 0, "[22] - 一時休業する"
			CALL COLUMNPRINTL, 0, @"　前営業日の収入:%TOOPTIONINT(飲食店売上)%＄"
		ELSE
			CALL COLUMNPRINT, 0, "　休業中 "
			CALL COLUMNPRINTL, 0, "[22] - 営業を開始する"
		ENDIF
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

;CALL SHOW_MODS

誕生日キャラ = 0
FOR LCOUNT, 0, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	IF (CSVCFLAG(LCOUNT, GETNUM(CFLAG, "誕生日"))/100) == MONTH && (CSVCFLAG(LCOUNT, 0)%100) == DAYS
		IF LANGUAGE == "ENG"
			CALL COLUMNPRINTL, 0, @"　Today is %CSVNAMEDISP(LCOUNT)%'s birthday."
		ELSE
			CALL COLUMNPRINTL, 0, @"　今日は%CSVNAMEF(LCOUNT)%の誕生日です"
		ENDIF
		誕生日キャラ++
	ENDIF
NEXT
IF 誕生日キャラ
	IF LANGUAGE == "ENG"
		CALL COLUMNPRINTL, 0, "　Happy Birthday!"
	ELSE
		CALL COLUMNPRINTL, 0, "　ハッピーバースデー！"
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF
;IF LANGUAGE == "ENG"
;	CALL COLUMNPRINTL, 0, @"□ITEM [710] \@ アイテム非表示 ? Show # Hide \@"
;ELSE
;	CALL COLUMNPRINTL, 0, @"□所持アイテム [710] \@ アイテム非表示 ? 表示する # 隠す \@"
;ENDIF
;SIF !アイテム非表示
;	CALL ITEMPRINT
;CALL COLUMNDRAWLINE, 0

SELECTCASE SEX(TARGET)
	CASE 1
		LCALL = him
	CASE 2
		LCALL = her
ENDSELECT

CALL COLUMNPRINT, 0, "　"

IF LANGUAGE == "ENG"
	;TARGETがいれば表示
	;九尾狐夜行では夜だけ調教可能
	IF 受験段位名 == "九尾狐夜行" && TIME == 0
		CALL COLUMNTOOLTIP, 0, "[-] Start Training ", "You can training at night only.", "gray"
	ELSEIF TARGET > 0
		SELECTCASE FALLTYPE(TARGET)
			CASE 1
				CALL COLUMNTOOLTIP, 0, "[0] Foster Love ", @"Make love to %CALLNAMEDISP(TARGET)%."
			CASE 2
				CALL COLUMNTOOLTIP, 0, @"[0] Lewd %LCALL% ", @"Get laid by %CALLNAMEDISP(TARGET)%"
			CASE 3
				CALL COLUMNTOOLTIP, 0, "[0] Dominate ", @"Dominate %LCALL%"
			CASEELSE
				CALL COLUMNTOOLTIP, 0, "[0] Start Training ", @"Training with %CALLNAMEDISP(TARGET)%"
		ENDSELECT
	ELSE
		SETCOLORBYNAME gray
		IF CHARANUM == 1 && ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "[-] Start Training ", "Characters are can't train.", "gray"
		ELSEIF ゲームモード() == "新婚モード"
			CALL COLUMNTOOLTIP, 0, "[-] Foster Love ", "Please select a character from Character→Change Target", "gray"
		ELSEIF 調教可能
			CALL COLUMNTOOLTIP, 0, "[-] Start Training ", "Please select a character from Character→Change Target", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, "[-] Start Training ", "Please buy character from Store→Buy Slaves", "gray"
		ENDIF
		RESETCOLOR
	ENDIF
	;無条件で表示
	CALL COLUMNTOOLTIP, 0, "[1] Rest ", "Take a half day break"
	;STRATEGYモード、PFREEモードなら表示
	SELECTCASE ゲームモード()
		CASE "経営モード", "フリーモード"
			;パートナーに話しかける口上が実装されていれば表示
			TRYCCALLFORM EXIST_STRATEGY_KOJO_%CSVNAMERPS(FLAG:パートナー)%
				SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
					CALL COLUMNTOOLTIP, 0, "[2] Talk with Manager ", @"%CSVCALLNAMEDISP(FLAG:パートナー)%に経営の相談をします"
			CATCH
			ENDCATCH
	ENDSELECT

	;新婚モードの一転攻勢
	IF ゲームモード() == "新婚モード"
		IF TALENT:FINDCHARA(CFLAG:嫁, 1):妊娠 || TALENT:FINDCHARA(CFLAG:嫁, 1):育児中
			CALL COLUMNTOOLTIP, 0, "[-] Replace a Master ", @"%CALLNAMEDISP(FINDCHARA(CFLAG:嫁, 1))%が身篭っているため、できません", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, "[3] Replace a Master ", @"%CALLNAMEDISP(MASTER)%と%CALLNAMEDISP(FINDCHARA(CFLAG:嫁, 1))%の受け攻めを変えます"
		ENDIF
	ENDIF

	;主人のスキルアップ
	CALL COLUMNTOOLTIP, 0, "[4] SKILL UP ", @"%CALLNAMEDISP(MASTER)%のスキルツリーを表示します"
	CALL COLUMNPRINTL, 0, ""


	;所持金が条件に達していて、ゲームモードがSTANDARDもしくはSTRATEGYなら表示
	SELECTCASE ゲームモード()
		;STANDARD
		CASE "通常モード"
			IF MONEY < DIG_SEP("1,000,000")
				CALL COLUMNTOOLTIP, 0, "　[-] Payment ", "1,000,000の所持金が必要です", "gray"
			ELSE
				CALL COLUMNTOOLTIP, 0, "　[5] Payment ", "1,000,000の所持金を納め、ゲームをクリアします"
			ENDIF
		;STRATEGY
		CASE "経営モード"
			IF MONEY < DIG_SEP("100,000,000")
				CALL COLUMNTOOLTIP, 0, "　[-] Payment ", "100,000,000の所持金が必要です", "gray"
			ELSE
				CALL COLUMNTOOLTIP, 0, "　[5] Payment ", "100,000,000の所持金を納め、ゲームをクリアします"
			ENDIF
		;新婚
		CASE "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[-] Payment ", "新婚モードでは納金の必要はありません", "gray"
		;段位認定
		CASE "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[-] Payment ", "段位認定では納金の必要はありません", "gray"
		;PFREE
		CASE "フリーモード"
			CALL COLUMNTOOLTIP, 0, "　[-] Payment ", "PREMIUM FREE MODEでは納金の必要はありません", "gray"
	ENDSELECT
	;周回可能なら表示
	IF FLAG:周回可能
		CALL COLUMNTOOLTIP, 0, "[6] New Game+ ", "この周を終了し、引き継いでニューゲームを開始します"
	ELSE
		SELECTCASE ゲームモード()
			CASE "新婚モード"
				CALL COLUMNTOOLTIP, 0, "[-] New Game+ ", "新婚モードでは周回出来ません", "gray"
			CASE "段位認定"
				CALL COLUMNTOOLTIP, 0, "[-] New Game+ ", "段位認定では周回出来ません", "gray"
			CASE "フリーモード"
				CALL COLUMNTOOLTIP, 0, "[-] New Game+ ", "PREMIUM FREE MODEでは周回出来ません", "gray"
			CASEELSE
				CALL COLUMNTOOLTIP, 0, "[-] New Game+ ", "ゲームをクリアしないと周回は出来ません", "gray"
		ENDSELECT
	ENDIF
ELSE
	;TARGETがいれば表示
	;九尾狐夜行では夜だけ調教可能
	IF 受験段位名 == "九尾狐夜行" && TIME == 0
		CALL COLUMNTOOLTIP, 0, "[-] 調教開始 ", "九尾狐夜行に挑戦中は夜のみ調教可能です", "gray"
	ELSEIF TARGET > 0
		SELECTCASE FALLTYPE(TARGET)
			CASE 1
				CALL COLUMNTOOLTIP, 0, "[0] 愛を育む ", @"%CALLNAME:TARGET%と幸せな時間を過ごします"
			CASE 2
				CALL COLUMNTOOLTIP, 0, "[0] 乱れ合う ", @"%CALLNAME:TARGET%と互いの欲望を満たし合います"
			CASE 3
				CALL COLUMNTOOLTIP, 0, "[0] 躾をする ", @"%CALLNAME:TARGET%に権威を示します"
			CASEELSE
				CALL COLUMNTOOLTIP, 0, "[0] 調教開始 ", @"%CALLNAME:TARGET%を調教します"
		ENDSELECT
	ELSE
		IF CHARANUM == 1 && ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "[-] 調教開始 ", "調教可能なキャラが居ません", "gray"
		ELSEIF ゲームモード() == "新婚モード"
			CALL COLUMNTOOLTIP, 0, "[-] 愛を育む ", "キャラクター→対象変更 から対象を選択してください", "gray"
		ELSEIF 調教可能
			CALL COLUMNTOOLTIP, 0, "[-] 調教開始 ", "キャラクター→奴隷変更 から調教対象を選択してください", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, "[-] 調教開始 ", "キャラ・アイテム購入→奴隷購入 からキャラを購入してください", "gray"
		ENDIF
	ENDIF
	;無条件で表示
	CALL COLUMNTOOLTIP, 0, "[1] 休憩 ", @"\@ TIME == 0 ? 夜まで休憩します # 翌日まで休憩します \@。体力、気力、ストレスが回復します"

	;STRATEGYモード、PFREEモードなら表示
	SELECTCASE ゲームモード()
		CASE "経営モード", "フリーモード"
			;パートナーに話しかける口上が実装されていれば表示
			TRYCCALLFORM EXIST_STRATEGY_KOJO_%CSVNAMERPS(FLAG:パートナー)%
				SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
					CALL COLUMNTOOLTIP, 0, "[2] 経営相談 ", @"%CSVCALLNAMEF(FLAG:パートナー)%に経営の相談をします"
			CATCH
			ENDCATCH
	ENDSELECT

	;新婚モードの一転攻勢
	IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
		IF TALENT:FINDCHARA(CFLAG:嫁, 1):妊娠 || TALENT:FINDCHARA(CFLAG:嫁, 1):育児中
			CALL COLUMNTOOLTIP, 0, "[-] 一転攻勢 ", @"%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%が身篭っているため、できません", "gray"
		ELSE
			CALL COLUMNTOOLTIP, 0, "[3] 一転攻勢 ", @"%CALLNAME:MASTER%と%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%の受け攻めを変えます"
		ENDIF
	ENDIF

	;主人のスキルアップ
	CALL COLUMNTOOLTIP, 0, "[4] SKILL UP ", @"%CALLNAME:MASTER%のスキルツリーを表示します"
	CALL COLUMNPRINTL, 0

	;所持金が条件に達していて、ゲームモードがSTANDARDもしくはSTRATEGYなら表示
	SELECTCASE ゲームモード()
		;STANDARD
		CASE "通常モード"
			IF MONEY < DIG_SEP("1,000,000")
				CALL COLUMNTOOLTIP, 0, "　[-] 納金 ", "1,000,000の所持金が必要です", "gray"
			ELSE
				CALL COLUMNTOOLTIP, 0, "　[5] 納金 ", "1,000,000の所持金を納め、ゲームをクリアします"
			ENDIF
		;STRATEGY
		CASE "経営モード"
			IF MONEY < DIG_SEP("100,000,000")
				CALL COLUMNTOOLTIP, 0, "　[-] 納金 ", "100,000,000の所持金が必要です", "gray"
			ELSE
				CALL COLUMNTOOLTIP, 0, "　[5] 納金 ", "100,000,000の所持金を納め、ゲームをクリアします"
			ENDIF
		;新婚
		CASE "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[-] 納金 ", "新婚モードでは納金の必要はありません", "gray"
		;段位認定
		CASE "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[-] 納金 ", "段位認定では納金の必要はありません", "gray"
		;PFREE
		CASE "フリーモード"
			CALL COLUMNTOOLTIP, 0, "　[-] 納金 ", "PREMIUM FREE MODEでは納金の必要はありません", "gray"
	ENDSELECT
	;周回可能なら表示
	IF FLAG:周回可能
		CALL COLUMNTOOLTIP, 0, "[6] 周回 ", "この周を終了し、引き継いでニューゲームを開始します"
	ELSE
		SELECTCASE ゲームモード()
			CASE "新婚モード"
				CALL COLUMNTOOLTIP, 0, "[-] 周回 ", "新婚モードでは周回出来ません", "gray"
			CASE "段位認定"
				CALL COLUMNTOOLTIP, 0, "[-] 周回 ", "段位認定では周回出来ません", "gray"
			CASE "フリーモード"
				CALL COLUMNTOOLTIP, 0, "[-] 周回 ", "PREMIUM FREE MODEでは周回出来ません", "gray"
			CASEELSE
				CALL COLUMNTOOLTIP, 0, "[-] 周回 ", "ゲームをクリアしないと周回は出来ません", "gray"
		ENDSELECT
	ENDIF
	CALL COLUMNPRINTL, 0, ""
ENDIF

;PALAMのオーバーフロー警告をする
VARLINE = LINECOUNT
FOR LCOUNT, 0, 100
	SIF PALAMOVERFLOW:LCOUNT
		PRINTFORML 　前回の調教で%PALAMNAME:LCOUNT%がオーバーフローしました
	SIF LCOUNT == 99 && VARLINE != LINECOUNT
		PRINTW 　正常に続行するには、前回の調教対象にアイテム「リミッター制御」を使用してください
NEXT
VARSET PALAMOVERFLOW

@SHOP_メイン_INPUT
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 伴侶
SELECTCASE CRESULT:1
	;調教開始
	CASE 0
		IF TARGET > 0
			LOOP_BREAK = 1
			BEGIN TRAIN
		ENDIF
	;休憩
	CASE 1
		;休憩フラグを立てる
		FLAG:休憩フラグ = 1
		CALL プレイ称号計算, "休憩"
		LOOP_BREAK = 1
		BEGIN TURNEND
	;経営相談
	CASE 2
		LTARGET = TARGET
		TARGET = GETCHARA(FLAG:パートナー)
		SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
			TRYCALLFORM TALK_PARTNER_%CSVNAMERPS(FLAG:パートナー)%
		TARGET = LTARGET
	;一転攻勢
	CASE 3
		IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
			伴侶 = FINDCHARA(CFLAG:嫁, 1)
			IF !TALENT:伴侶:妊娠 && !TALENT:伴侶:育児中
				TALENT:MASTER:親愛 = 1
				TALENT:伴侶:親愛 = 0
				CFLAG:MASTER:嫁 = 1
				CFLAG:伴侶:嫁 = 0
				SWAPCHARA 伴侶, MASTER
			ENDIF
		ENDIF
	;スキルツリー
	CASE 4
		CALL プレイ称号計算, "スキル"
		CALL スキルツリー
	;納金
	CASE 5
		SIF ORIGIN
			RETURN 0
		SELECTCASE ゲームモード()
			CASE "通常モード"
				;100万
				IF MONEY >= TONUM("100万")
					;実績:1周目のゲームを20日以内にクリアする
					SIF FLAG:周回数 == 1 && DAY <= 20
						CALL 実績解除, "SPEED DEMON"
					CALL ENDING_CHECK
				ENDIF
			CASE "経営モード"
				;1億
				IF MONEY >= TONUM("1億")
					;実績:1周目のゲームを20日以内にクリアする
					SIF FLAG:周回数 == 1 && DAY <= 20
						CALL 実績解除, "SPEED DEMON"
					;実績:キャラを一人も購入せずにパートナーだけでSTRATEGYモードをクリアする
					SIF キャラ未購入
						CALL 実績解除, "もう全部あいつ一人でいいんじゃないかな"
					;実績:芸能プロダクションの収入のみでSTRATEGYモードをクリアする
					SIF バンめし
						CALL 実績解除, "他人がバンドで稼いだ金でめしが食いたい"
					CALL ENDING_CHECK
				ENDIF
		ENDSELECT
	;周回
	CASE 6
		SIF FLAG:周回可能 && !ORIGIN
			CALL SUCCESSION
	;キャラ変更
	CASE 10, 19
		LTARGET = TARGET
		LRESULT = CRESULT:1
		DO
			SELECTCASE LRESULT
				CASE 10
					LTARGET--
				CASE 19
					LTARGET++
			ENDSELECT
			;調教可能かどうか確認してからTARGETにアサインする 不可能だった場合は次のキャラに飛ぶ
			IF !CHECK_CHILD_CARE(LTARGET) && !CFLAG:LTARGET:使用不可 && LTARGET != MASTER
				CALL ASSIGN_TARGET, LTARGET
				BREAK
			ENDIF
		LOOP INRANGE(LTARGET, 1, CHARANUM-2)
	;ゲーセン営業変更
	CASE 20
		IF FLAG:ゲーセン営業中 > 0
			;PRINTFORMW %GC店名%の経営を一時中断しました
			FLAG:ゲーセン営業中 = -1
		ELSE
			;PRINTFORMW %GC店名%の経営を開始しました
			FLAG:ゲーセン営業中 = DAY
		ENDIF
	;娼館営業変更
	CASE 21
		IF FLAG:娼館営業中 > 0
			;PRINTFORMW %娼館店名%の経営を一時中断しました
			FLAG:娼館営業中 = -1
		ELSE
			;PRINTFORMW %娼館店名%の経営を開始しました
			FLAG:娼館営業中 = DAY
		ENDIF
	;飲食店営業変更
	CASE 22
		IF FLAG:飲食店営業中 > 0
			;PRINTFORMW %飲食店名%の経営を一時中断しました
			FLAG:飲食店営業中 = -1
		ELSE
			;PRINTFORMW %飲食店名%の経営を開始しました
			FLAG:飲食店営業中 = DAY
		ENDIF
	;コナミコマンド
	CASE 573
		PRINTW このコマンドはもう意味無いよ
		CALL 実績解除, "いつのバージョンだよ"
	;アイテム表示の切り替え
	;CASE 710
	;	INVERTBIT アイテム非表示, 0
ENDSELECT

;旧@LIFE_LIST
@SHOP_キャラクター
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC カウント数
;#DIM DYNAMIC SHOP_未表示キャラ
#DIM DYNAMIC LTARGET
#DIM 表示切り替え
#DIM DYNAMIC 表示上限

VARLINE = LINECOUNT

IF ゲームモード() == "新婚モード"
	CALL COLUMNPRINT, 0, "□キャラ一覧 "
ELSE
	SELECTCASE SHOP_キャラ表示情報
		CASE "調教対象選択"
			CALL COLUMNPRINT, 0, "□調教対象変更 "
		CASE "助手選択"
			CALL COLUMNPRINT, 0, "□助手変更     "
		CASEELSE
			CALL COLUMNPRINT, 0, "□キャラ一覧   "
	ENDSELECT
ENDIF

IF SHOP_キャラ表示情報 == "調教対象選択"
	CALL COLUMNPRINT, 0, "[--] 対象選択 ", "deepskyblue"
ELSE
	CALL COLUMNPRINT, 0, "[-1] 対象選択 "
ENDIF
IF SHOP_キャラ表示情報 == "助手選択"
	CALL COLUMNPRINT, 0, "[--] 助手選択 ", "deepskyblue"
ELSE
	CALL COLUMNPRINT, 0, "[-2] 助手選択 "
ENDIF
IF SHOP_キャラ表示情報 == "能力表示"
	CALL COLUMNPRINT, 0, "[--] 能力表示 ", "deepskyblue"
ELSE
	CALL COLUMNPRINT, 0, "[-3] 能力表示 "
ENDIF
IF SHOP_キャラ表示情報 == "ABL上昇"
	CALL COLUMNPRINT, 0, "[--] ABL上昇 ", "deepskyblue"
ELSE
	CALL COLUMNPRINT, 0, "[-4] ABL上昇 "
ENDIF
CALL COLUMNPRINTL, 0, ""
;PRINTL [-1] 表示上限を減らす [-9] 表示上限を増やす

SHOP_未表示キャラ = 0

FOR LCOUNT, 0, CHARANUM
	;1巡目はMASTERとTARGET及びASSIの能力表示
	IF LCOUNT == 0
		IF SHOP_キャラ表示情報 == "助手選択"
			CALL COLUMNPRINTL, 0, "　[  0] 助手無し"
		ELSE
			CALL COLUMNPRINTL, 0, @"　[  0] %CALLNAME:MASTER%(主人) \@ SEX(MASTER) == 1 ? ♂ # ♀ \@ \@ TALENT:MASTER:ふたなり ? 双 #\@"
		ENDIF
		;TARGETとASSIはMASTERの下に表示する
		IF TARGET > 0
			;助手の指定なら助手不可能なキャラは除外
			SIF SHOP_キャラ表示情報 == "助手選択" && CFLAG:状態 < 2 && !TALENT:アニマル
				CONTINUE
			CALL STATUS_BAR, TARGET
		ENDIF
		SIF ASSI > 0
			CALL STATUS_BAR, ASSI
		CALL COLUMNDRAWLINE, 0
		CONTINUE
	ENDIF

	;2巡目以降は通常のキャラ表示なので条件を指定
	;助手の指定なら助手不可能なキャラは除外
	SIF SHOP_キャラ表示情報 == "助手選択" && CFLAG:LCOUNT:状態 < 2 && !TALENT:LCOUNT:アニマル
		CONTINUE
	;登場作品フィルタが設定してあるならそれ以外のキャラは除外
	SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３) == 0 && ORIGIN
		CONTINUE
	;キャラを表示上限数表示したら終了
	IF 表示数 >= 20
		SHOP_未表示キャラ = 1
		BREAK
	ENDIF
	;対象と助手は主人の下に表示されるので除外
	SIF GROUPMATCH(LCOUNT, TARGET, ASSI)
		CONTINUE
	カウント数++
	;ページ数×表示上限まで表示を飛ばす
	SIF SHOP_キャラページ*20 >= カウント数
		CONTINUE
	CALL STATUS_BAR, LCOUNT
	表示数++
NEXT
;表示数が足りない場合は改行入れる
IF 表示数 < 20
	DO
		CALL COLUMNPRINTL, 0, ""
		表示数++
	LOOP 表示数 < 20
ENDIF

CALL COLUMNDRAWLINE, 0
CALL COLUMNPRINTL, 0, "　☆：調教中　★：助手　○：助手可　●：元助手 ■：タグ色"
CALL COLUMNPRINTL, 0, "　KA：経営モードに配属中　不：使用不可　妊：妊娠育児中　獣：ペット候補　を表しています"
CALL COLUMNDRAWLINE, 0

;条件を満たしているにも関わらずまだリスト表示されてないキャラが居ればSHOP_未表示キャラフラグは1になる
;RETURN SHOP_未表示キャラ

;DRAWLINE
;CALL LIFE_LIST, "調教対象選択", 0, 表示上限
;RESULTにはSHOP_未表示キャラが居るかどうか
;次ページあり = RESULT
;DRAWLINE
;PRINTPLAIN 　CHANGE TARGET　
CALL COLUMNPRINT, 0, RETURNSPACE(SHOP_キャラページ, "　[999] 前ページ ")
CALL COLUMNPRINT, 0, RETURNSPACE(SHOP_未表示キャラ, "[1001] 次ページ ")
CALL COLUMNPRINT, 0, "[1010] キャラソート "
IF SHOP_表示切り替え
	CALL COLUMNPRINT, 0, "[1011] 通常表示に切り替え "
ELSE
	CALL COLUMNPRINT, 0, "[1011] 経営用表示に切り替え "
ENDIF
SIF ORIGIN
	CALL COLUMNPRINT, 0, "[1012] 登場作品で絞る"
CALL COLUMNPRINTL, 0, ""

@SHOP_キャラクター_INPUT
#DIM DYNAMIC LTARGET
SELECTCASE CRESULT:1
	CASE MASTER
		SELECTCASE SHOP_キャラ表示情報
			CASE "調教対象選択"
				PRINTW 自分を調教することはできません
			CASE "助手選択"
				ASSI = 0
			CASE "ABL上昇"
				PRINTW 主人の能力上昇はスキルツリーから行ってください
			CASE "能力表示"
				GOTO 能力表示
		ENDSELECT
	CASE -1
		SHOP_キャラ表示情報 = 調教対象選択
	CASE -2
		SHOP_キャラ表示情報 = 助手選択
		SHOP_キャラページ = 0
	CASE -3
		SHOP_キャラ表示情報 = 能力表示
	CASE -4
		SHOP_キャラ表示情報 = ABL上昇
	;CASE -1
	;	SIF 表示上限 > 10
	;		表示上限 -= 10
	;CASE -9
	;	表示上限 += 10
	CASE 999
		SIF SHOP_キャラページ
			SHOP_キャラページ--
	CASE 1000
		RETURN
	CASE 1001
		SIF SHOP_未表示キャラ
			SHOP_キャラページ++
	CASE 1010
		CALL CHARA_SORT
	CASE 1011
		INVERTBIT SHOP_表示切り替え, 0
	CASE 1012
		CALL 登場作品フィルタ設定
		TFLAG:ページ数 = 0
	CASE 0 TO CHARANUM-1
		SELECTCASE SHOP_キャラ表示情報
			CASE "調教対象選択"
				SIF CHECK_CHILD_CARE(CRESULT:1)
					RETURN
				SIF CFLAG:(CRESULT:1):使用不可
					RETURN
				SIF CRESULT:1 == 0
					RETURN
				CALL ASSIGN_TARGET, CRESULT:1
			CASE "助手選択"
				SIF CFLAG:(CRESULT:1):状態 < 2 && !TALENT:(CRESULT:1):アニマル
					RESTART
				SIF CHECK_CHILD_CARE(CRESULT:1)
					RESTART
				SIF CFLAG:(CRESULT:1):使用不可
					RESTART
				CALL ASSIGN_ASSI, CRESULT:1
			CASE "能力表示"
				$能力表示
				LTARGET = TARGET
				TARGET = CRESULT:1
				;TARGETの能力表示中はあっちで「調教対象に指定」を表示させないためのフラグ
				IF TARGET == LTARGET
					SHOW_TARGET = LTARGET
				ELSE
					SHOW_TARGET = 0
				ENDIF
				CALL STATUS
				;TARGET変更がされてる場合は戻さない
				SIF !RESULT && ASSI != LTARGET
					TARGET = LTARGET
				;TARGET変更無しかつ元々TARGETが居なかった場合も戻す
				SIF !RESULT && LTARGET == -1
					TARGET = -1
			CASE "ABL上昇"
				FLAG:能力上昇 = TARGET
				TARGET = CRESULT:1
				CALL USER_ABLUP
		ENDSELECT
		RETURN
ENDSELECT

@STATUS_BAR, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 文字色
#DIMS DYNAMIC 文字列

;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラは灰色表示
SIF SHOP_キャラ表示情報 != "通常表示" && CHECK_CHILD_CARE(対象)
	SETCOLORBYNAME gray
;調教対象の指定、もしくは助手の指定なら、使用不可のキャラは灰色表示
SIF SHOP_キャラ表示情報 != "通常表示" && CFLAG:対象:使用不可
	SETCOLORBYNAME gray

IF CFLAG:対象:タグ色 > 0
	文字色 = CFLAG:対象:タグ色
ELSE
	文字色 = -1
ENDIF

IF CFLAG:対象:使用不可
	文字列 += COLUMNPRINTF("不", 文字色)
ELSEIF TALENT:対象:アニマル
	文字列 += COLUMNPRINTF("獣", 文字色)
ELSEIF CHECK_CHILD_CARE(対象)
	文字列 += COLUMNPRINTF("妊", 文字色)
ELSEIF 対象 == TARGET
	文字列 += COLUMNPRINTF("☆", 文字色)
ELSEIF 対象 == ASSI
	文字列 += COLUMNPRINTF("★", 文字色)
ELSEIF CSTR:対象:配属 != ""
	文字列 += COLUMNPRINTF("KA", 文字色)
;助手経験あり
ELSEIF ISASSI:対象
	文字列 += COLUMNPRINTF("●", 文字色)
;助手可能
ELSEIF CFLAG:対象:状態 >= 2
	文字列 += COLUMNPRINTF("○", 文字色)
ELSEIF CFLAG:対象:タグ色
	文字列 += COLUMNPRINTF("■", 文字色)
ELSE
	文字列 += COLUMNPRINTF("　")
ENDIF

;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラと使用不可キャラは選べない
IF SHOP_キャラ表示情報 != "通常表示" && (CHECK_CHILD_CARE(対象) || CFLAG:対象:使用不可)
	文字列 += COLUMNPRINTF(@"[---] %CALLNAMEDISP(対象), 20, LEFT%")
ELSE
	文字列 += COLUMNPRINTF(@"[{対象, 3}] %CALLNAMEDISP(対象), 20, LEFT%")
ENDIF

;上記のキャラを表示したら詳細情報も表示
;助手選択画面では表示しない
IF SHOP_キャラ表示情報 != "助手選択"
	;表示切り替え中は経営用の情報表示
	IF SHOP_表示切り替え
		文字列 += COLUMNPRINTF("配属先:")
		IF CSTR:対象:配属 != ""
			文字列 += COLUMNPRINTF(@"%CSTR:対象:配属, 19, LEFT% ")
		ELSE
			文字列 += COLUMNPRINTF(@"%"無し", 19, LEFT% ")
		ENDIF
		文字列 += COLUMNPRINTF("モチベーション:")
		SELECTCASE CFLAG:対象:モチベーション
			CASE 1 TO 9
				文字列 += COLUMNPRINTF("皆無 ", "gray")
			CASE 10 TO 39
				文字列 += COLUMNPRINTF("微妙 ", "yellow")
			CASE 40 TO 60
				文字列 += COLUMNPRINTF("普通 ", "silver")
			CASE 61 TO 90
				文字列 += COLUMNPRINTF("良好 ", "cyan")
			CASE 91 TO 100
				文字列 += COLUMNPRINTF("最高 ", "lime")
		ENDSELECT
		IF GUI
			文字列 += @"%IMGTOTAG(BASEBAR("モチベ", 対象, 4))% "
		ELSE
			文字列 += COLUMNPRINTF(@"{CFLAG:対象:モチベーション, 3}/100 ")
		ENDIF
	ELSE
		IF GUI
			文字列 += @"体力:%IMGTOTAG(BASEBAR("体力", 対象, 11))% "
			文字列 += @"気力:%IMGTOTAG(BASEBAR("気力", 対象, 11))% "
		ELSE
			文字列 += COLUMNPRINTF(@"体力%BARBASE("体力", 対象, 10), 23, LEFT%") 
			文字列 += COLUMNPRINTF(@" 気力%BARBASE("気力", 対象, 10), 23, LEFT% ")
		ENDIF
	ENDIF
ENDIF
SELECTCASE SEX(対象)
	CASE 1
		文字列 += COLUMNPRINTF("♂ ")
	CASE 2
		文字列 += COLUMNPRINTF("♀ ")
ENDSELECT
SELECTCASE SHOP_キャラ表示情報
	CASE "調教対象選択"
		SIF TALENT:対象:処女
			文字列 += COLUMNPRINTF("処女 ")
		SIF TALENT:対象:再生処女
			文字列 += COLUMNPRINTF("再生処女 ")
		SIF TALENT:対象:童貞
			文字列 += COLUMNPRINTF("童貞 ")
		SIF TALENT:対象:恋慕
			文字列 += COLUMNPRINTF("恋慕 ")
		SIF TALENT:対象:淫乱
			文字列 += COLUMNPRINTF("淫乱 ")
		SIF TALENT:対象:服従
			文字列 += COLUMNPRINTF("服従 ")
		SIF TALENT:対象:親愛
			文字列 += COLUMNPRINTF("親愛 ")
		SIF TALENT:対象:娼婦
			文字列 += COLUMNPRINTF("娼婦 ")
		SIF TALENT:対象:隷属
			文字列 += COLUMNPRINTF("隷属 ")
		SIF TALENT:対象:崩壊
			文字列 += COLUMNPRINTF("崩壊 ")
		SIF TALENT:対象:ふたなり
			文字列 += COLUMNPRINTF("双 ")
		SIF TALENT:対象:リミットバースト
			文字列 += COLUMNPRINTF("超 ")
		SIF TALENT:対象:スライム || TALENT:対象:溶ける快
			文字列 += COLUMNPRINTF("粘 ")
	CASE "助手選択"
		IF GUI
			文字列 += COLUMNPRINTF(@"技巧:")
			文字列 += COLUMNPRINTF(IMGTOTAG(GBAR(ABL:対象:技巧, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:技巧}", "black"))) 
			SELECTCASE SEX(対象)
				CASE 1
					文字列 += COLUMNPRINTF(@"ホモっ気:")
					文字列 += COLUMNPRINTF(IMGTOTAG(GBAR(ABL:対象:ホモっ気, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:ホモっ気}", "black"))) 
					文字列 += COLUMNPRINTF(@"ゲイ中毒:")
					文字列 += COLUMNPRINTF(IMGTOTAG(GBAR(ABL:対象:ゲイ中毒, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:ゲイ中毒}", "black"))) 
				CASE 2
					文字列 += COLUMNPRINTF(@"レズっ気:")
					文字列 += COLUMNPRINTF(IMGTOTAG(GBAR(ABL:対象:レズっ気, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:レズっ気}", "black"))) 
					文字列 += COLUMNPRINTF(@"レズ中毒:")
					文字列 += COLUMNPRINTF(IMGTOTAG(GBAR(ABL:対象:レズ中毒, 10, 3, BARCOLOR:能力, "gray", @"{ABL:対象:レズ中毒}", "black"))) 
			ENDSELECT
			文字列 += " "
			SIF TALENT:対象:両刀
				文字列 += COLUMNPRINTF("両刀 ")
			SIF TALENT:対象:ふたなり
				文字列 += COLUMNPRINTF("ふたなり")
		ELSE
			SELECTCASE SEX(対象)
				CASE 1
					文字列 += COLUMNPRINTF(@"技巧Lv{ABL:対象:技巧} ホモっ気Lv{ABL:対象:ホモっ気} ゲイ中毒Lv{ABL:対象:ゲイ中毒} ")
				CASE 2
					文字列 += COLUMNPRINTF(@"技巧Lv{ABL:対象:技巧} レズっ気Lv{ABL:対象:レズっ気} レズ中毒Lv{ABL:対象:レズ中毒} ")
			ENDSELECT
			SIF TALENT:対象:両刀
				文字列 += COLUMNPRINTF("両刀 ")
			SIF TALENT:対象:ふたなり
				文字列 += COLUMNPRINTF("ふたなり")
		ENDIF
	CASEELSE
		IF GUI
			文字列 += COLUMNPRINTF(@"ストレス値:%IMGTOTAG(BASEBAR("ストレス値", 対象, 4))%")
		ELSE
			文字列 += COLUMNPRINTF("ストレス値:")
			SELECTCASE CFLAG:対象:ストレス値
				CASE 0
					文字列 += COLUMNPRINTF(@"{CFLAG:対象:ストレス値}/999", "gray")
				CASE 1 TO 99
					文字列 += COLUMNPRINTF(@"{CFLAG:対象:ストレス値}/999", "lime")
				CASE 100 TO 299
					文字列 += COLUMNPRINTF(@"{CFLAG:対象:ストレス値}/999", "yellow")
				CASE 300 TO 499
					文字列 += COLUMNPRINTF(@"{CFLAG:対象:ストレス値}/999", "red")
				CASE IS >= 500
					文字列 += COLUMNPRINTF(@"{CFLAG:対象:ストレス値}/999", "darkred")
			ENDSELECT
		ENDIF
ENDSELECT
RESETCOLOR
CALL COLUMNPRINTL, 0, 文字列

@SHOP_施設・経営

IF !GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
	CALL COLUMNPRINTL, 0, "□経営"
	IF ITEM:風俗店営業許可証
		CALL COLUMNPRINTL, 0, "　[ 0] - ゲームセンター経営"
		CALL COLUMNPRINTL, 0, "　[ 1] - 娼館経営"
	ELSE
		CALL COLUMNPRINTL, 0, "　[--] - ゲームセンター経営（風俗店営業許可証が必要です）", "gray"
		CALL COLUMNPRINTL, 0, "　[--] - 娼館経営（風俗店営業許可証が必要です）", "gray"
		RESETCOLOR
	ENDIF
	;PRINTPLAIN 　[3] - ゲーム開発（工事中）
	;PRINTL 
	CALL COLUMNPRINTL, 0, "　[ 2] - 芸能プロダクション運営"
	CALL COLUMNPRINTL, 0, "　[ 3] - 飲食店・ホテル経営"
	IF ITEM:事業開発オフィス
		CALL COLUMNPRINTL, 0, "　[ 4] - 出版社経営"
	ELSE
		CALL COLUMNPRINTL, 0, "　[--] - 出版社経営（アイテム[事業開発オフィス]が必要です）", "gray"
	ENDIF
	IF STSMOD
		IF ITEM:博物館
			CALL COLUMNPRINTL, 0, "　[ 5] - 博物館経営"
		ELSE
			CALL COLUMNPRINTL, 0, "　[--] - 博物館経営（アイテム[博物館]が必要です）", "gray"
		ENDIF
	ENDIF
	CALL COLUMNDRAWLINE, 0
ENDIF

CALL COLUMNPRINTL, 0, "□施設・設備"
IF ITEM:医務室
	CALL COLUMNPRINTL, 0, "　[10] - 医務室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 医務室（アイテム[医務室]が必要です）", "gray"
ENDIF
IF ITEM:実験室
	CALL COLUMNPRINTL, 0, "　[11] - 実験室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 実験室（アイテム[実験室]が必要です）", "gray"
ENDIF
IF ITEM:図書室
	CALL COLUMNPRINTL, 0, "　[12] - 図書室"
ELSE
	CALL COLUMNPRINTL, 0, "　[--] - 図書室（アイテム[図書室]が必要です）", "gray"
ENDIF
;IF ITEM:音楽室
;	CALL COLUMNPRINTL, 0, "[4] - 音楽室
;ELSE
;	CALL COLUMNPRINTL, 0, "[-] - 音楽室
;ENDIF

CALL COLUMNPRINTL, 0, "　[13] - 調理室"
CALL COLUMNPRINTL, 0, "　[14] - 農産物の管理"
CALL COLUMNPRINTL, 0, "　[15] - 畜産関連の管理"
CALL COLUMNPRINTL, 0, "　[16] - 監禁室"

@SHOP_施設・経営_INPUT
SELECTCASE CRESULT:1
	CASE 0
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		CALL プレイ称号計算, "ゲーセン経営"
		CALL STRATEGY_GC
	CASE 1
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		CALL プレイ称号計算, "娼館経営"
		CALL STRATEGY_BROTHEL
	CASE 2
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		CALL プレイ称号計算, "芸プロ運営"
		CALL STRATEGY_IDOL
		;返り値が1ならTURNENDする
		IF RESULT == 1
			LOOP_BREAK = 1
			BEGIN TURNEND
			RETURN
		ENDIF
	CASE 3
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		CALL 飲食店経営
	CASE 4
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		SIF ITEM:事業開発オフィス
			CALL 出版社経営
	CASE 5
		SIF GROUPMATCH(ゲームモード(), "段位認定", "通常モード")
			RETURN
		IF ITEM:博物館 && STSMOD
			CALL プレイ称号計算, "博物館"
			CALL 博物館
			;返り値が1ならTURNENDする
			IF RESULT == 1
				LOOP_BREAK = 1
				BEGIN TURNEND
				RETURN
			ENDIF
		ENDIF
	CASE 10
		IF ITEM:医務室
			CALL プレイ称号計算, "医務室"
			CALL 医務室
		ENDIF
	CASE 11
		IF ITEM:実験室
			CALL プレイ称号計算, "実験室"
			CALL 実験室
		ENDIF
	CASE 12
		IF ITEM:図書室
			CALL プレイ称号計算, "図書室"
			CALL 図書室
		ENDIF
	CASE 13
		CALL プレイ称号計算, "調理室"
		CALL 調理室
	CASE 14
		CALL プレイ称号計算, "農業"
		CALL 農産物
		CALL 献立条件確認
	CASE 15
		CALL プレイ称号計算, "畜産業"
		CALL 畜産
		CALL 献立条件確認
	CASE 16
		CALL 監禁室
ENDSELECT

@SHOP_キャラ・アイテム購入
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 売却可能

;売却可能キャラ数の確認
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:状態 >= 1 && !CFLAG:LCOUNT:売却不可
		売却可能++
NEXT

CALL COLUMNPRINTL, 0, "□アイテム一覧"
CALL ITEMPRINT, 1
CALL COLUMNDRAWLINE, 0
CALL COLUMNPRINT, 0, "□アイテム購入 "
IF ゲームモード() == "段位認定"
	CALL COLUMNPRINTL, 0, "　[0] アイテムを購入する"
ELSE
	CALL COLUMNPRINT, 0, "[-1] 調教道具 ", @"\@ SHOP_アイテム分類 == "調教道具" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-2] 消耗品 ", @"\@ SHOP_アイテム分類 == "消耗品" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-3] 素質アイテム ", @"\@ SHOP_アイテム分類 == "素質アイテム" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-4] 薬品類 ", @"\@ SHOP_アイテム分類 == "薬品類" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-5] 施設 ", @"\@ SHOP_アイテム分類 == "施設" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-6] 本 ", @"\@ SHOP_アイテム分類 == "本" ? deepskyblue#\@"
	CALL COLUMNPRINT, 0, "[-7] すべて ", @"\@ SHOP_アイテム分類 == "すべて" ? deepskyblue#\@"
	CALL COLUMNPRINTL, 0, ""

	CALL SHOPITEM_LIST, SHOP_アイテム分類, 0, 1
ENDIF

;段位認定、新婚モード以外で表示
;ORIGINでは全キャラ無条件解禁なので購入もできない
IF LANGUAGE == "ENG"
	;段位認定、新婚モード以外で表示
	IF !ORIGIN
		CALL COLUMNDRAWLINE, 0
		IF ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[----] - Buy Slaves ", "段位認定では奴隷を購入することができません", "gray"
		ELSEIF ゲームモード() != "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[2000] - Buy Slaves ", "調教するための奴隷を購入します"
		ENDIF
		;売却できるキャラがいれば表示
		IF !売却禁止
			IF 売却可能
				CALL COLUMNTOOLTIP, 0, "　[2001] - Sell Slaves ", "調教で仕上がった奴隷を売却します"
			ELSEIF ゲームモード() != "新婚モード"
				CALL COLUMNTOOLTIP, 0, "　[----] - Sell Slaves ", "売却可能な奴隷が居ません", "gray"
			ENDIF
		ENDIF
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ELSE
	IF !ORIGIN
		CALL COLUMNDRAWLINE, 0
		IF ゲームモード() == "段位認定"
			CALL COLUMNTOOLTIP, 0, "　[----] - 奴隷購入 ", "段位認定では奴隷を購入することができません", "gray"
		ELSEIF ゲームモード() != "新婚モード"
			CALL COLUMNTOOLTIP, 0, "　[2000] - 奴隷購入 ", "調教するための奴隷を購入します"
		ENDIF
		;売却できるキャラがいれば表示
		IF 受験段位名 != "ヤンデレの音ゲーキャラに死ぬほど愛されて眠れないコース"
			IF 売却可能
				CALL COLUMNTOOLTIP, 0, "　[2001] - 売却 ", "調教で仕上がった奴隷を売却します"
			ELSEIF ゲームモード() != "新婚モード"
				CALL COLUMNTOOLTIP, 0, "　[----] - 売却 ", "売却可能な奴隷が居ません", "gray"
			ENDIF
		ENDIF
		CALL COLUMNPRINTL, 0, ""
	ENDIF
ENDIF


@SHOP_キャラ・アイテム購入_INPUT
#DIMS DYNAMIC 購入アイテム分類
SELECTCASE CRESULT:1
	CASE -1
		SHOP_アイテム分類 = 調教道具
	CASE -2
		SHOP_アイテム分類 = 消耗品
	CASE -3
		SHOP_アイテム分類 = 素質アイテム
	CASE -4
		SHOP_アイテム分類 = 薬品類
	CASE -5
		SHOP_アイテム分類 = 施設
	CASE -6
		SHOP_アイテム分類 = 本
	CASE -7
		SHOP_アイテム分類 = すべて
	CASE 2000
		IF !GROUPMATCH(ゲームモード(), "新婚モード", "段位認定") && !ORIGIN
			CALL TUTORIAL, "奴隷購入"
			CALL CHARA_BUY
		ENDIF
	CASE 2001
		SIF ゲームモード() != "新婚モード" && 受験段位名 != "ヤンデレの音ゲーキャラに死ぬほど愛されて眠れないコース" && !ORIGIN
			CALL CHARA_SELL
	CASE 0 TO VARSIZE("ITEM")-1
		IF ゲームモード() == "段位認定" && CRESULT:1 == 0
			CALL GORILLA_ITEM
			RETURN
		ENDIF
		RESULT = CRESULT:1
		IF ITEMSALES:RESULT && ITEMNAME:RESULT != ""
			IF ITEMPRICE:RESULT > MONEY && !ORIGIN
				PRINTW 所持金が足りません
			ELSE
				;アイテム分類を名前で渡す
				SELECTCASE RESULT
					CASE 0 TO 99, 500 TO 599, 1000 TO 1049
						購入アイテム分類 = 買い切り
					CASE 100 TO 199
						購入アイテム分類 = 複数購入可
					CASE 200 TO 249
						購入アイテム分類 = 素質アイテム
					CASE 250 TO 399, 1050 TO 1099
						購入アイテム分類 = 薬品等
				ENDSELECT
				;以下特定のアイテムの分類を変えるときに使う
				SELECTCASE ITEMNAME:RESULT
					CASE "ロジカルダッシュ"
						購入アイテム分類 = 買い切り
				ENDSELECT
				CALL ITEMBUY_DECIDE, ITEMNAME:RESULT, 購入アイテム分類
			ENDIF
		ENDIF
ENDSELECT

@SHOP_データベース

IF LANGUAGE == "ENG"
	;ビデオ所持していれば表示
	SIF FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1) > 0
		CALL COLUMNTOOLTIPL, 0, "　[0] - VIDEO", "所持しているビデオの一覧を表示します"
	CALL COLUMNTOOLTIPL, 0, "　[4] - MANUAL", "ゲーム内マニュアルを表示します"
ELSE
	;ビデオ所持していれば表示
	SIF FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1) > 0
		CALL COLUMNTOOLTIPL, 0, "　[0] - 所持ビデオ一覧", "所持しているビデオの一覧を表示します"
	;アルバム
	CALL COLUMNTOOLTIPL, 0, "　[1] - アルバム", "撮影、保管した写真を表示します"
	;部屋ログあれば表示
	SIF 部屋ログ != ""
		CALL COLUMNTOOLTIPL, 0, "　[2] - 部屋ログ", "居住部屋で起きた出来事を確認します"
	CALL COLUMNTOOLTIPL, 0, "　[3] - プレイログ", "プレイログ及び現金出納帳を確認します"
	CALL COLUMNTOOLTIPL, 0, "　[4] - ゲームマニュアル", "ゲーム内マニュアルを表示します"
ENDIF

@SHOP_データベース_INPUT
SELECTCASE CRESULT:1
	;ビデオ一覧
	CASE 0
		SIF FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1) > 0
			CALL 所持ビデオ一覧
	;アルバム
	CASE 1
		CALL 所持写真一覧
	;部屋ログ
	CASE 2
		SIF 部屋ログ != ""
			CALL 部屋ログ表示
	;プレイログ
	CASE 3
		CALL プレイログ閲覧
	;マニュアル
	CASE 4
		CALL GAME_MANUAL
ENDSELECT

@SHOP_その他

IF LANGUAGE == "ENG"

	;魔法
	IF TALENT:MASTER:魔術師 || (TARGET > 0 && TALENT:魔術師) || (ASSI > 0 && TALENT:ASSI:魔術師)
		CALL COLUMNTOOLTIPL, 0, "　[0] - Magic Chant", "魔法を使用します"
	ELSE
		CALL COLUMNTOOLTIPL, 0, "　[-] - Magic Chant", "魔法を使用するには主人、助手、もしくは調教対象が魔術師である必要があります", "gray"
	ENDIF
	;今月号のパズル雑誌持ってれば表示
	SIF ITEM:ロジカルダッシュ == YEAR*100+MONTH
		CALL COLUMNTOOLTIPL, 0, "　[1] - Puzzle", "今月号のパズル雑誌で遊びます"
	;経営、PFREEなら表示
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[2] - Casino", "カジノに行ってギャンブルをします"
	;裏カジノ行ければ表示
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && FLAG:裏カジノ && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[3] - Darkness Casino", "高レートの裏カジノに行ってギャンブルをします"
	;無条件表示（暫定）
	CALL COLUMNTOOLTIPL, 0, "[4] - Redesign", "家の模様替えを行います"

	;変化できるキャラが居れば表示
	SIF CMATCH(TALENT:変化, 1)
		CALL COLUMNTOOLTIPL, 0, "　[5] - Transform", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます"

	;新婚モード以外で表示
	IF ゲームモード() != "新婚モード"
		;奴隷が一人でもいれば表示
		IF CHARANUM > 1
			CALL COLUMNTOOLTIPL, 0, "　[-] - Ritual", "眷属の義、もしくは印の義を行います"
			CALL COLUMNPRINTL, 0, "　　[10] - 眷属の儀"
			CALL COLUMNPRINTL, 0, "　　[11] - 印の儀"
			CALL COLUMNPRINTL, 0, "　　[12] - 幽体離脱・憑依の儀"
			SIF STSMOD
				CALL COLUMNPRINTL, 0, "　　[13] - カニバリズム"
		ELSE
			CALL COLUMNTOOLTIPL, 0, "　[-] - Ritual", "キャラ・アイテム購入→奴隷購入 からキャラを購入してください", "gray"
		ENDIF
	ENDIF
ELSE
	;魔法
	IF TALENT:MASTER:魔術師 || (TARGET > 0 && TALENT:魔術師) || (ASSI > 0 && TALENT:ASSI:魔術師)
		CALL COLUMNTOOLTIPL, 0, "　[0] - 魔法詠唱", "魔法を使用します"
	ELSE
		CALL COLUMNTOOLTIPL, 0, "　[-] - 魔法詠唱", "魔法を使用するには主人、助手、もしくは調教対象が魔術師である必要があります",  "gray"
	ENDIF
	;今月号のパズル雑誌持ってれば表示
	SIF ITEM:ロジカルダッシュ == YEAR*100+MONTH
		CALL COLUMNTOOLTIPL, 0, "　[1] - パズル", "今月号のパズル雑誌で遊びます"
	;経営、PFREEなら表示
	;ORIGINでは金の概念が無いので使えない
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[2] - カジノ", "カジノに行ってギャンブルをします"

	;裏カジノ行ければ表示 同じくORIGINではry
	SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && FLAG:裏カジノ && !ORIGIN
		CALL COLUMNTOOLTIPL, 0, "　[3] - 裏カジノ", "高レートの裏カジノに行ってギャンブルをします"
	;スマホ版では使えない
	SIF !SP
		CALL COLUMNTOOLTIPL, 0, "　[4] - 模様替え", "家の模様替えを行います"
	;変化できるキャラが居れば表示
	SIF CMATCH(TALENT:変化, 1)
		CALL COLUMNTOOLTIPL, 0, "　[5] - 変化", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます"

	;新婚モードと段位認定以外で表示
	IF ゲームモード() != "新婚モード" && ゲームモード() != "段位認定"
		;奴隷が一人でもいれば表示
		IF CHARANUM > 1
			CALL COLUMNTOOLTIPL, 0, "　[-] - 儀式", "眷属の義、もしくは印の義を行います"
			CALL COLUMNPRINTL, 0, "　　[10] - 眷属の儀"
			CALL COLUMNPRINTL, 0, "　　[11] - 印の儀"
			CALL COLUMNPRINTL, 0, "　　[12] - 幽体離脱・憑依の儀"
			SIF STSMOD
				CALL COLUMNPRINTL, 0, "　　[13] - カニバリズム"
		ELSE
			CALL COLUMNTOOLTIPL, 0, "　[-] - 儀式", "アイテム・キャラ購入→奴隷購入 からキャラを購入してください", "gray"
		ENDIF
	ENDIF
ENDIF

;デバッグモードなら表示
;#;CALL COLUMNPRINTL, 0, "　[997] - 乱キャラ"
;#;CALL COLUMNPRINTL, 0, "　[998] - デバッグ"

@SHOP_その他_INPUT

SELECTCASE CRESULT:1
	;魔法詠唱
	CASE 0
		SIF TALENT:MASTER:魔術師 || (TARGET > 0 && TALENT:魔術師) || (ASSI > 0 && TALENT:ASSI:魔術師)
			CALL SPELLCAST
		;魔法を詠唱したならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "魔法詠唱"
			FLAG:休憩フラグ = 1
			BEGIN TURNEND
		ENDIF
	;パズル
	CASE 1
		SIF ITEM:ロジカルダッシュ == YEAR*100+MONTH
			CALL PUZZLE
	;カジノ
	CASE 2
		SIF GROUPMATCH(ゲームモード(), "経営モード", "フリーモード") && !ORIGIN
			CALL GAMBLE, 1
		;ギャンブルしたならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "ギャンブル"
			FLAG:休憩フラグ = 1
			LOOP_BREAK = 1
			BEGIN TURNEND
		ENDIF
	;裏カジノ
	CASE 3
		SIF FLAG:裏カジノ && !ORIGIN
			CALL GAMBLE, 2
		;ギャンブルしたならターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "ギャンブル"
			FLAG:休憩フラグ = 1
			LOOP_BREAK = 1
			BEGIN TURNEND
		ENDIF
	;模様替え
	CASE 4
		SIF SP
			RETURN 0
		CALL プレイ称号計算, "マップ"
		CALL 模様替え
	;変化
	CASE 5
		SIF CMATCH(TALENT:変化, 1)
			CALL 変化
	;儀式
	CASE 10
		SIF CHARANUM < 2
			RETURN
		CALL FAMILIAR
		;儀を行った場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "眷属"
			FLAG:休憩フラグ = 1
			LOOP_BREAK = 1
			BEGIN TURNEND
		ENDIF
	CASE 11
		SIF CHARANUM < 2
			RETURN
		CALL RITUAL_OF_SIGN
		;儀を行った場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "印"
			FLAG:休憩フラグ = 1
			LOOP_BREAK = 1
			BEGIN TURNEND
		ENDIF
	CASE 12
		SIF CHARANUM < 2
			RETURN
		CALL SPECTRAL
	CASE 13
		SIF CHARANUM < 2
			RETURN
		SIF !STSMOD
			RESTART
		CALL CANNIBALISM
		;食事をした場合はターン終了
		IF RESULT == 1
			CALL プレイ称号計算, "カニバリスト"
			FLAG:休憩フラグ = 1
			LOOP_BREAK = 1
			BEGIN TURNEND
		ENDIF
	;デバッグ用
	;#;CASE 997
	;#;	CALL ADD_ランダムキャラ, 2, -1
	;#;CASE 998
	;#;	CALL DEBUG
ENDSELECT


;USERSAVEの流用
@SHOP_セーブ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC データ番号
#DIM DYNAMIC データ数
CALL COLUMNPRINTL, 0, "□何番にセーブしますか？"
CALL COLUMNPRINTL, 0, "　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間"

FOR LCOUNT, TFLAG:ページ数*20, TFLAG:ページ数*20+20
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] %RESULTS%"
		CASE 1
			;表示テンプレ
			;PRINTFORML 　[ 5] 2016/01/10(曜) 00:10:15 | 2016/3/28(曜) 197日目夜 難易度:(N) ∞ 
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] ----　　　　　　　　　　|", "gray"
		CASE 2
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] 別バリアントのセーブデータです 上書きは可能です", "gray"
		CASE 3
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] 互換性の無いセーブデータです 上書きは可能です", "gray"
		CASE 4
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT, 2}] ロードできないセーブデータです 上書きは可能です", "gray"
	ENDSELECT
NEXT
CALL COLUMNDRAWLINE, 0
;PRINTPLAINFORM 　SAVE.{ページ数+1}　
CALL COLUMNPRINT, 0, @"%RETURNSPACE(TFLAG:ページ数 > 0, "[101] 前ページ ")%"
CALL COLUMNPRINT, 0, @"%RETURNSPACE(TFLAG:ページ数 < 3, "[109] 次ページ")%"
CALL COLUMNPRINTL, 0, ""

IF SHOP_セーブ確認 >= 0
	CALL COLUMNPRINTL, 0, @"{SHOP_セーブ確認}番には既にセーブデータが存在します。上書きしますか？"
	CALL COLUMNPRINT, 0, "[0] はい "
	CALL COLUMNPRINTL, 0, "[1] いいえ"
ENDIF


@SHOP_セーブ_INPUT
#DIM DYNAMIC データ番号
#DIM DYNAMIC データ数
#DIM DYNAMIC LCOUNT

IF SHOP_セーブ確認 >= 0
	SELECTCASE CRESULT:1
		CASE 0
			データ番号 = SHOP_セーブ確認
		CASE 1
			SHOP_セーブ確認 = -1
			RETURN
	ENDSELECT
ELSE
	SELECTCASE CRESULT:1
		CASE 0 TO 79, 94 TO 99
			SELECTCASE CHKDATA(CRESULT:1)
				CASE 0, 2, 3, 4
					SHOP_セーブ確認 = CRESULT:1
					RETURN
				CASE 1
					データ番号 = CRESULT:1
			ENDSELECT
		CASE 101
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RETURN
		CASE 109
			SIF TFLAG:ページ数 < 3
				TFLAG:ページ数++
			RETURN
	ENDSELECT
ENDIF

CALL プレイ称号計算, "セーブ"
SAVEDATA データ番号, セーブデータ情報()

SHOP_セーブ確認 = -1

データ数 = 0
FOR LCOUNT, 0, 80
	SIF CHKDATA(LCOUNT) == 0
		データ数++
NEXT

SIF データ数 == 80
	CALL 実績解除, "メモリーカードの容量が足りません"

SAVEGLOBAL

@SHOP_ロード
CALL タイトル_セーブデータのロード

@SHOP_ロード_INPUT
CALL タイトル_セーブデータのロード_INPUT

@SHOP_オプション
LOADGLOBAL
CALL COLUMNPRINTL, 0, "□設定項目を選択してください"
CALL COLUMNPRINTL, 0, "　[0] - テキスト設定"
CALL COLUMNPRINTL, 0, "　[1] - 着衣関連設定"
CALL COLUMNPRINTL, 0, "　[2] - イベント関連の設定"
SIF TALENT:MASTER:ドラゴン使い
	CALL COLUMNPRINTL, 0, "　[3] - ドラゴンの名前変更"
SIF !SP
	CALL COLUMNPRINTL, 0, "　[4] - サウンドオプション"
CALL COLUMNPRINTL, 0, @"　[5] - 能力表示画面の顔グラ表示(現在:\@ 顔グラ表示 == 1 ? オン # オフ \@)"
CALL COLUMNPRINTL, 0, @"　[6] - GUIを有効にする(現在:\@ GUI ? オン # オフ \@)"
CALL COLUMNPRINTL, 0, @"　　　　[7] - GUIの色変更"

@SHOP_オプション_INPUT
#DIM DYNAMIC LRESULT

SELECTCASE CRESULT:1
	CASE 0
		CALL プレイ称号計算, "テキスト設定"
		CALL TEXT_SETTING
	CASE 1
		CALL プレイ称号計算, "着衣設定"
		CALL CLOTHES_SETTING
	CASE 2
		CALL EVENT_SETTING
	CASE 3
		IF TALENT:MASTER:ドラゴン使い
			DO
				PRINTL ドラゴンの名前を入力してください(何も入力しない場合、「ドラゴン」になります)
				INPUTS
				SIF RESULTS == ""
					RESULTS = ドラゴン
				PRINTFORML %RESULTS%でよろしいですか？(後からオプション画面で変更できます)
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				CALL INPUTF, 0, 1
				IF RESULT == 0
					CSTR:MASTER:ドラゴン = %RESULTS%
					BREAK
				ENDIF
				CONTINUE
			LOOP 1
		ENDIF
	CASE 4
		SIF SP
			CONTINUE
		DRAWLINE
		VARLINE = LINECOUNT
		$SOUNDOPTION
		CLEARLINE LINECOUNT-VARLINE
		PRINTFORML □現在の設定 サウンド再生機能:\@ サウンド == 1 ? オン # オフ \@ BGM音量:{BGM音量} SE音量:{SOUND音量}
		PRINTFORML 　[0] - サウンド再生機能(現在:\@ サウンド == 1 ? オン # オフ \@)
		PRINTL 　[1] - BGMの音量を上げる
		PRINTL 　[2] - BGMの音量を下げる
		PRINTL 　[3] - SEの音量を上げる
		PRINTL 　[4] - SEの音量を下げる
		DRAWLINE
		PRINTPLAIN 　SOUND　
		PRINTL [100] - 戻る
		DO
			INPUT
			LRESULT = RESULT
			SELECTCASE RESULT
				CASE 0
					INVERTBIT サウンド, 0
					SELECTCASE サウンド
						CASE 0
							SETBGMVOLUME 0
							SETSOUNDVOLUME 0
						CASE 1
							SETSOUNDVOLUME SOUND音量
							SETBGMVOLUME BGM音量
					ENDSELECT
					GOTO SOUNDOPTION
				CASE 1
					BGM音量 += 10
					CALL SETMIN(BGM音量, 100)
				CASE 2
					BGM音量 -= 10
					CALL SETMAX(BGM音量, 0)
				CASE 3
					SOUND音量 += 10
					CALL SETMIN(SOUND音量, 100)
				CASE 4
					SOUND音量 -= 10
					CALL SETMAX(SOUND音量, 0)
				CASE 100
					BREAK
				CASEELSE
					CONTINUE
			ENDSELECT

			SETSOUNDVOLUME 0
			SETBGMVOLUME 0
			SETSOUNDVOLUME SOUND音量
			SETBGMVOLUME BGM音量
			SIF GROUPMATCH(LRESULT, 3, 4)
				CALL SE再生, トランプドロー
			GOTO SOUNDOPTION
		LOOP 1
		SAVEGLOBAL
	CASE 5
		INVERTBIT 顔グラ表示, 0
		SAVEGLOBAL
	CASE 6
		INVERTBIT GUI, 0
		SAVEGLOBAL
	CASE 7
		CALL GUI_COLOR
ENDSELECT

@SHOP_ショートカット
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT
;ショートカット有効にしていれば表示
;カラム機能を作ったので常時表示
CALL COLUMNPRINTL, 0, "□ショートカット一覧"
FOR LCOUNT, 0, VARSIZE("LNK")
	IF LNK:LCOUNT != ""
		CALL COLUMNPRINT, 0, @"　[{LCOUNT, 2}] - %LNKCOM(LNK:LCOUNT), 40, LEFT%"
		表示数++
		SIF 表示数 != 0 && 表示数%3 == 0
			CALL COLUMNPRINTL, 0, ""
	ENDIF
NEXT
SIF 表示数%3 != 0
	CALL COLUMNPRINTL, 0, ""

CALL COLUMNDRAWLINE, 0

SIF FINDELEMENT(LNK, "", 0, VARSIZE("LNK"), 1) >= 0
	CALL COLUMNPRINT, 0, "　[100] - ショートカットコマンドの登録 "
CALL COLUMNPRINTL, 0, "[101] - ショートカットキーの削除"

@SHOP_ショートカット_INPUT
SELECTCASE CRESULT:1
	CASE 0 TO VARSIZE("LNK")
		CALL ショートカットコマンド, RESULT
	CASE 100
		CALL ショートカット登録
	CASE 101
		CALL ショートカット削除
ENDSELECT

[SKIPSTART]
@HOGE
;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 1, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
	;売却可能
	SIF CFLAG:LCOUNT:状態 >= 1 && !CFLAG:LCOUNT:売却不可
		売却可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;対象も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT
[SKIPEND]

@変化
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
DRAWLINE
PRINTL □変化させるキャラを選んでください
FOR LCOUNT, 1, CHARANUM
	IF TALENT:LCOUNT:変化
		IF CFLAG:LCOUNT:陥落
			PRINTFORML [{LCOUNT, 2}] %NAME:LCOUNT% 現在：\@ TALENT:LCOUNT:アニマル ? 獣の姿 # 人間の姿 \@
		ELSE
			SETCOLORBYNAME gray
			PRINTFORML [--] %NAME:LCOUNT% 未陥落
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　変化　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASEELSE
			;TRANCE変数を使うのは変化後に付いた素質を維持するため
			LTARGET = TARGET
			TARGET = RESULT
			IF CFLAG:陥落 && TALENT:変化
				IF TALENT:アニマル
					;着衣フラグのリセット
					FOR LCOUNT, GETNUM(CFLAG, "特殊"), GETNUM(CFLAG, "足元")+1
						CFLAG:LCOUNT = CSVCFLAG(NO:TARGET, LCOUNT)
					NEXT
					FOR LCOUNT, GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
						CSTR:LCOUNT = %CSVCSTR(NO:TARGET, LCOUNT)%
					NEXT
					PRINTFORMW %CALLNAME:TARGET%は人間の姿に変化した
					TALENT:RESULT:アニマル = 0
					IF !TRANCE:TALENTF("動物耳")
						TALENT:動物耳 = 0
						TRANCE:TALENTF("動物耳") = 0
					ENDIF
					IF !TRANCE:TALENTF("尻尾")
						TALENT:尻尾 = 0
						TRANCE:TALENTF("尻尾") = 0
					ENDIF
					;茶倉だけ特殊で機械処理なので人間に戻す
					IF CHARANAMEF(TARGET) == "戌"
						IF TRANCE:TALENTF("無機物")
							TALENT:無機物 = 0
							CSTR:種族 = %TRANCERACE:TARGET:0%
							NAME:TARGET = 大伴 茶倉
							CALLNAME:TARGET = 茶倉
							TRANCE:TALENTF("無機物") = 0
						ENDIF
					ELSE
						IF TRANCE:TALENTF("ファーリー")
							TALENT:ファーリー = 0
							TRANCE:TALENTF("ファーリー") = 0
						ENDIF
					ENDIF
					TALENT:バストサイズ = TRANCE:TALENTF("バストサイズ")
					TRANCE:TALENTF("バストサイズ") = 0
				ELSE
					PRINTFORMW %CALLNAME:TARGET%は獣の姿に変化した
					;着衣フラグの変更
					VARSET CFLAG, 100, GETNUM(CFLAG, "特殊"), GETNUM(CFLAG, "足元")+1
					VARSET CSTR, "", GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
					TALENT:アニマル = 1
					IF !TALENT:動物耳
						TALENT:動物耳 = 1
						TRANCE:TALENTF("動物耳") = 1
					ENDIF
					IF !TALENT:尻尾
						TALENT:尻尾 = 1
						TRANCE:TALENTF("尻尾") = 1
					ENDIF
					;茶倉だけ特殊で機械（戌）になる
					IF CHARANAMEF(TARGET) == "大伴 茶倉"
						IF !TALENT:無機物
							TALENT:無機物 = 1
							TRANCERACE:TARGET:0 = %CSTR:種族%
							CSTR:種族 = 機械
							NAME:TARGET = 戌
							CALLNAME:TARGET = 戌
							TRANCE:TALENTF("無機物") = 1
						ENDIF
					ELSE
						IF !TALENT:ファーリー
							TALENT:ファーリー = 1
							TRANCE:TALENTF("ファーリー") = 1
						ENDIF
					ENDIF
					TRANCE:TALENTF("バストサイズ") = TALENT:バストサイズ
					TALENT:バストサイズ = 0
				ENDIF
			ENDIF
			TARGET = LTARGET
			RESTART
	ENDSELECT
LOOP 1

@所持ビデオ一覧
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 総ページ数
#DIM DYNAMIC ページ数
#DIM DYNAMIC 総本数

総本数 = FINDELEMENT(所持ビデオ, "", 0, VARSIZE("所持ビデオ"), 1)

総ページ数 = 総本数/10+1
$TOP
DRAWLINE
PRINTFORML □所持ビデオ一覧 総本数:{総本数}
FOR LCOUNT, ページ数*10, ページ数*10+10
	SIF LCOUNT >= 総本数
		BREAK
	PRINTFORML 　{LCOUNT+1}.%所持ビデオ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAINFORM 　VIDEO.{ページ数+1}　
PRINT [0] - 戻る 
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
CALL PRINTSPACE, ページ数+1 < 総ページ数, "[9] - 次ページ "
PRINTL 

CALL INPUTF, 0, 1, 9
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		SIF ページ数 > 1
			ページ数--
	CASE 9
		SIF ページ数 < 総ページ数
			ページ数++
ENDSELECT

GOTO TOP

@プレイログ閲覧
#DIM DYNAMIC ページ数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 最大文字数, 4
#DIMS DYNAMIC SPLITVAR, 3
#DIMS DYNAMIC DETAILVAR, 4
#DIMS DYNAMIC 同上, 3

ページ数 = 0

DRAWLINE
PRINTL [0] - プレイログを見る
PRINTL [1] - 現金出納帳を見る
PRINTL [2] - 戻る

CALL INPUTF, 0, 1, 2
SELECTCASE RESULT
	CASE 0
		GOTO プレイログ
	CASE 1
		GOTO 現金出納帳
	CASE 2
		RETURN
ENDSELECT

$プレイログ
DRAWLINE
FOR LCOUNT, ページ数*30, ページ数*30+30
	SIF プレイログ:LCOUNT == ""
		BREAK
	PRINTFORML %プレイログ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAINFORM 　プレイログ,{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, プレイログ:(ページ数*30+31) != "", "[9] - 次ページ"
PRINTL 

CALL INPUTF, 0, 1, 9
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		SIF ページ数 > 0
			ページ数--
	CASE 9
		SIF プレイログ:(ページ数*30+31) != ""
			ページ数++
ENDSELECT

GOTO プレイログ

$現金出納帳
FOR LCOUNT, ページ数*30, ページ数*30+30
	SIF 現金出納帳:LCOUNT == ""
		BREAK
	SPLIT 現金出納帳:LCOUNT, "/", SPLITVAR
	SPLIT SPLITVAR:0, "-", DETAILVAR
	SIF STRLENS(DETAILVAR:0) > 最大文字数:0
		最大文字数:0 = STRLENS(DETAILVAR:0)
	SIF STRLENS(DETAILVAR:3) > 最大文字数:1
		最大文字数:1 = STRLENS(DETAILVAR:3)
	;邪悪すぎる式
	SIF STRLENS(TODIGSEP(ABS(TOINT(SPLITVAR:1)))) > 最大文字数:2
		最大文字数:2 = STRLENS(TODIGSEP(ABS(TOINT(SPLITVAR:1))))
	SIF STRLENS(TODIGSEP(TOINT(SPLITVAR:2))) > 最大文字数:3
		最大文字数:3 = STRLENS(TODIGSEP(TOINT(SPLITVAR:2)))
NEXT

DRAWLINE
VARSET 同上
PRINTFORML │%"年", 最大文字数:0, LEFT%│月│日│%"概要", 最大文字数:1, LEFT%│%"入金", 最大文字数:2, LEFT%│%"出金", 最大文字数:2, LEFT%│%"残高", 最大文字数:3, LEFT%│
;時系列順に上から下に並べる
FOR LCOUNT, ページ数*30+29, ページ数*30-1, -1
	SIF 現金出納帳:LCOUNT == ""
		CONTINUE
	SPLIT 現金出納帳:LCOUNT, "/", SPLITVAR
	SPLIT SPLITVAR:0, "-", DETAILVAR
	;あまりにも長いので数行に分割する
	PRINTFORM │\@ 同上:0 != DETAILVAR:0 ? %DETAILVAR:0, 最大文字数:0% # %"", 最大文字数:0% \@
	PRINTFORM │\@ 同上:1 != DETAILVAR:1 ? %DETAILVAR:1, 2% # %"  "% \@
	PRINTFORM │\@ 同上:2 != DETAILVAR:2 ? %DETAILVAR:2, 2% # %"  "% \@
	PRINTFORM │%DETAILVAR:3, 最大文字数:1, LEFT%
	PRINTFORM │\@ SIGN(TOINT(SPLITVAR:1)) > 0 ? %TODIGSEP(TOINT(SPLITVAR:1)), 最大文字数:2% # %"", 最大文字数:2% \@│\@ SIGN(TOINT(SPLITVAR:1)) < 0 ? %TODIGSEP(ABS(TOINT(SPLITVAR:1))), 最大文字数:2% # %"", 最大文字数:2% \@
	PRINTFORM │%TODIGSEP(TOINT(SPLITVAR:2)), 最大文字数:3%
	PRINTL │
	同上:0 = %DETAILVAR:0%
	同上:1 = %DETAILVAR:1%
	同上:2 = %DETAILVAR:2%
NEXT
DRAWLINE
PRINTPLAINFORM 　現金出納帳,{ページ数+1}　
CALL PRINTSPACE, ページ数 > 0, "[1] - 前ページ "
PRINT [0] - 戻る 
CALL PRINTSPACE, 現金出納帳:(ページ数*30+31) != "", "[9] - 次ページ"
PRINTL 

CALL INPUTF, 0, 1, 9
SELECTCASE RESULT
	CASE 0
		RESTART
	CASE 1
		SIF ページ数 > 0
			ページ数--
	CASE 9
		SIF 現金出納帳:(ページ数*30+31) != ""
			ページ数++
ENDSELECT

GOTO 現金出納帳

@通常モード_BGM
;季節で変わる
SELECTCASE MONTH
	CASE 3 TO 5
		CALL BGM再生, 通常_春
	CASE 6 TO 8
		CALL BGM再生, 通常_夏
	CASE 9 TO 11
		CALL BGM再生, 通常_秋
	CASE 12, 1, 2
		CALL BGM再生, 通常_冬
ENDSELECT

@新婚モード_BGM
;季節で変わる
SELECTCASE MONTH
	CASE 3 TO 5
		CALL BGM再生, 新婚_春
	CASE 6 TO 8
		CALL BGM再生, 新婚_夏
	CASE 9 TO 11
		CALL BGM再生, 新婚_秋
	CASE 12, 1, 2
		CALL BGM再生, 新婚_冬
ENDSELECT
