@SPELLCAST
DRAWLINE
PRINTL □魔法を詠唱するキャラを選んでください
SIF TARGET > 0 && TALENT:魔術師
	PRINTFORML [0] %NAME:TARGET% 魔力%BARSTR(BASE:魔力, MAXBASE:魔力, 32)% 魔法技能LV{ABL:魔法技能}
SIF TALENT:MASTER:魔術師
	PRINTFORML [1] %NAME:MASTER% 魔力%BARSTR(BASE:MASTER:魔力, MAXBASE:MASTER:魔力, 32)% 魔法技能LV{ABL:MASTER:魔法技能}
SIF ASSI > 0 && TALENT:ASSI:魔術師
	PRINTFORML [2] %NAME:ASSI% 魔力%BARSTR(BASE:ASSI:魔力, MAXBASE:ASSI:魔力, 32)% 魔法技能LV{ABL:ASSI:魔法技能}
PRINTL [10] 戻る

CALL INPUTF, 0, 1, 2, 10

SELECTCASE RESULT
	CASE 0
		IF CFLAG:状態 < 2
			PRINTL 助手可能でないと魔法の詠唱を許可できません
			RESTART
		ENDIF
		SIF TALENT:魔術師
			CALL SELECT_SPELL, TARGET
		SIF RESULT
			RETURN 1
	CASE 1
		SIF TALENT:MASTER:魔術師
			CALL SELECT_SPELL, MASTER
		SIF RESULT
			RETURN 1
	CASE 2
		SIF ASSI > 0 && TALENT:ASSI:魔術師
			CALL SELECT_SPELL, ASSI
		SIF RESULT
			RETURN 1
	CASE 10
		RETURN
ENDSELECT

RESTART

@SELECT_SPELL, 詠唱者
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC スキル
#DIM DYNAMIC 対象, 2
#DIMS DYNAMIC 使用スペル名
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日
#DIM DYNAMIC コスト
#DIM DYNAMIC 詠唱者
#DIM DYNAMIC 表示
#DIM DYNAMIC 必要レベル
#DIM DYNAMIC 詠唱可能, 50
;魔法名, 必要レベル
{
#DIMS DYNAMIC 魔法一覧 = 
"0は欠番", "",
"フリージングアトモスフィア", "3" ,
"バーニンザフロア", "3",
"ストームバスター", "4",
"スウィートレイン", "4",
"スノープリズム", "4",
"ビーストモード", "5",
"ビーストモードtype2", "5",
"ジュエリーストーム", "5",
"リ：ジェネレーション", "6",
"ジェントルストレス", "6",
"ハニーバニー", "6",
"クロノダイブ", "7",
"ヒプノティッククライシス", "7",
"ティル・ナ・ノーグ", "7",
"エナジードライブ", "8",
"ポゼッション", "9",
"ミラクル・スイート・スイーツ・マジック！！", "10",
"バック・トゥ・クロノス", "10",
"スウィートイリュージョン", "10",
}

スキル = ABL:詠唱者:魔法技能
DRAWLINE
PRINTFORML □魔法一覧 詠唱者:%NAME:詠唱者% 魔法技能LV{スキル}
PRINTFORML 　%NAME:詠唱者%の魔力%BARSTR(BASE:詠唱者:魔力, MAXBASE:詠唱者:魔力, 32)%({BASE:詠唱者:魔力}/{MAXBASE:詠唱者:魔力})
;魔力消費量は単純に詠唱可能レベル*1000
;詠唱可能レベルから1上昇する度に魔力消費量は10%減
VARSET 詠唱可能
FOR LCOUNT, 1, VARSIZE("魔法一覧")/2
	使用スペル名 = %魔法一覧:(LCOUNT*2)%
	必要レベル = TOINT(魔法一覧:(LCOUNT*2+1))
	SELECTCASE 使用スペル名
		;天土と緋浮美は作中で使ってるので魔法技能無しで詠唱可能
		CASE "ビーストモード", "ビーストモードtype2"
			SIF GROUPMATCH(CHARANAMEF(詠唱者), "梅桐 天土", "梅桐 緋浮美")
				GOTO OK
		;クロノダイブのみクロノスとカイロスは無条件で使える
		CASE "クロノダイブ"
			SIF GROUPMATCH(CHARANAMEF(詠唱者), "クロノス", "カイロス")
				GOTO OK
		;ヒプノティッククライシスは主人が女性じゃないと実行できない
		CASE "ヒプノティッククライシス"
			SIF SEX(MASTER) != 2
				CONTINUE
		;まり花のみレベル8から使用可能
		CASE "ミラクル・スイート・スイーツ・マジック！！"
			SIF CHARANAMEF(詠唱者) == "山形 まり花" && ABL:詠唱者:魔法技能 >= 8
				GOTO OK
		;ロザリー、塵、アサギス、キャスは魔法技能無しで使える
		CASE "ジュエリーストーム"
			;要STS
			SIF !STSMOD
				CONTINUE
			SIF GROUPMATCH(CHARANAMEF(詠唱者), "ロザリー", "塵", "アサギス", "キャスメラ")
				GOTO OK
		;要STS
		CASE "スウィートイリュージョン"
			SIF !STSMOD
				CONTINUE
	ENDSELECT
	SIF ABL:詠唱者:魔法技能 < 必要レベル
		CONTINUE
	$OK
	PRINTFORML 　[{LCOUNT, 2}] - %使用スペル名%（魔力消費量{魔力消費量(必要レベル, スキル)}）
	詠唱可能:LCOUNT = 1
	SELECTCASE 魔法一覧:(LCOUNT*2)
		;Freezing atomosphere
		CASE "フリージングアトモスフィア"
			PRINTL 　　　　 気温の低下 魔法技能に応じて効果持続
		;BURNIN' THE FLOOR
		CASE "バーニンザフロア"
			PRINTL 　　　　 気温の上昇 魔法技能に応じて効果持続
		;Storm Buster
		CASE "ストームバスター"
			PRINTL 　　　　 天気を晴れにする 魔法技能に応じて効果持続
		;Sweet Rain
		CASE "スウィートレイン"
			PRINTL 　　　　 天気を雨にする 魔法技能に応じて効果持続
		;snow prism
		CASE "スノープリズム"
			PRINTL 　　　　 天気を雪にする 魔法技能に応じて効果持続
		;Beast Mode
		CASE "ビーストモード"
			PRINTL 　　　　 任意のキャラに対して一時的に素質[動物耳][尻尾]を与え、種族を獣人にする 魔法技能に応じて効果持続
		CASE "ビーストモードtype2"
			PRINTL 　　　　 ビーストモードに加え素質[ファーリー]を与える
		;Re:GENERATION
		CASE "リ：ジェネレーション"
			PRINTL 　　　　 キャラ全員の回復量上昇 魔法技能に応じて効果持続
		;gentle stress
		CASE "ジェントルストレス"
			PRINTFORML 　　　　 任意のキャラのストレスが上昇しなくなる。魔法技能に応じて効果持続
		;CHRONO DIVER
		CASE "クロノダイブ"
			PRINTL 　　　　 任意の年月日にタイムリープする
		;HYPNΦTIC CRISIS 主人が女性じゃないとダメ
		CASE "ヒプノティッククライシス"
			PRINTL 　　　　 女性主人による色仕掛けの成功率が上がる 魔法技能に応じて効果持続
		;Tir na n'Og
		CASE "ティル・ナ・ノーグ"
			PRINTL 　　　　 素質[小人体型]を一時的に消滅させる 魔法技能に応じて効果持続
		CASE "エナジードライブ"
			PRINTFORML 　　　　 主人の全ABLを一時的に{スキル-7}上昇させ、精力も上昇する 魔法技能に応じて効果持続
		;POSESSION
		CASE "ポゼッション"
			PRINTL 　　　　 任意のキャラのABLを一時的に詠唱者と同じにする 魔法技能に応じて効果持続 主人には使用不可
		;ミラクル・スイート・スイーツ・マジック！！
		CASE "ミラクル・スイート・スイーツ・マジック！！"
			PRINTL 　　　　 任意の2キャラ間の相性を上昇させる 詠唱者は対象にできない 最大500%
		;Back to chronos
		CASE "バック・トゥ・クロノス"
			PRINTL 　　　　 現在の状態を維持したまま期限・日にちを1日分戻す
		;JEWELLERY STORM
		CASE "ジュエリーストーム"
			PRINTL 　　　　 対象を鉱物に変える キャラロスト扱いで元に戻すことはできない
		;Sweet Illusion
		CASE "スウィートイリュージョン"
			PRINTL 　　　　 対象をお菓子に変えて他キャラに食べさせる 食べられたキャラはロストする
		;Hunny Bunny
		CASE "ハニーバニー"
			PRINTL 　　　　 調教中、キャラの持つ[恋心][既婚者]の対象を一時的に主人に変える 魔法技能に応じて効果持続
	ENDSELECT
NEXT

DRAWLINE
PRINTPLAIN 　SPELL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASEELSE
			SIF !詠唱可能:RESULT
				CONTINUE
			使用スペル名 = %魔法一覧:(RESULT*2)%
			必要レベル = TOINT(魔法一覧:(RESULT*2+1))
			コスト = BASE:詠唱者:魔力 - 魔力消費量(必要レベル, スキル)
			IF コスト < 0
				IF 詠唱者 == MASTER
					PRINTFORMW 主人は魔力の限界を越えた詠唱はできません
					RESTART
				ELSE
					PRINTFORML 魔力が不足しています。このまま詠唱すると反動で{ABS(コスト/100)+1}ターン使用不可状態になります
				ENDIF
			ENDIF
			対象 = -1
			対象:1 = -1
			年 = 0
			月 = 0
			日 = 0
			;独自の入力が必要なもの
			SELECTCASE 使用スペル名
				CASE "ビーストモード", "ビーストモードtype2"
					PRINTFORML □%使用スペル名%で獣人化する対象を選んでください
					FOR PAGING, 0, CHARANUM
						表示 = 0
						;既に付与素質全部あるキャラは無理
						IF 使用スペル名 == "ビーストモードtype2"
							SIF !TALENT:PAGING:尻尾 || !TALENT:PAGING:動物耳 || !TALENT:PAGING:ファーリー
								表示 = 1
						ELSE
							SIF !TALENT:PAGING:尻尾 || !TALENT:PAGING:動物耳
								表示 = 1
						ENDIF
						;ビーストモード発動中のキャラは無理
						SIF ビーストモード:PAGING:0
							表示 = 0
						;詠唱者も無理
						SIF PAGING == 詠唱者
							表示 = 0
						;霊体も無理
						SIF TALENT:PAGING:霊体
							表示 = 0
						CALL PAGING, 表示, @"%CSTR:PAGING:種族%"
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
				CASE "ジェントルストレス"
					PRINTL □ジェントルストレスでストレスを抑える対象を選んでください
					表示 = 0
					SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳) || TALENT:詠唱者:孔雀の瞳
						表示 = 1
					FOR PAGING, 0, CHARANUM
						CALL PAGING, 詠唱者 != PAGING, @"\@ 表示 ? ストレス値:{CFLAG:PAGING:ストレス値} #  \@"
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
				CASE "クロノダイブ"
					$BTTF
					PRINTL □移動先の日付を入力してください
					PRINTFORM 　[0] - {年}年 
					PRINTFORM [1] - {月}月 
					PRINTFORML [2] - {日}日
					PRINT 　[3] - 確定 
					PRINTL [4] - キャンセル
					CALL INPUTF, 0, 1, 2, 3, 4
					SELECTCASE RESULT
						CASE 0
							PRINT 移動先の西暦を入力してください
							INPUT
							年 = RESULT
							GOTO BTTF
						CASE 1
							CALL TWO_DIGIT, 12, "月", 1
							月 = RESULT
							GOTO BTTF
						CASE 2
							CALL TWO_DIGIT, 31, "日", 1
							日 = RESULT
							GOTO BTTF
						CASE 3
							IF !EXIST_DAY(年, 月, 日)
								PRINTFORMW {年}年{月}月{日}日は存在しません
								GOTO BTTF
							ENDIF
						CASE 4
							RESTART
					ENDSELECT
				CASE "ティル・ナ・ノーグ"
					PRINTL □ティル・ナ・ノーグで体を大きくする対象を選んでください
					FOR PAGING, 0, CHARANUM
						CALL PAGING, TALENT:PAGING:小人体型
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
				CASE "エナジードライブ"
					IF エナジードライブ > 0
						PRINTW エナジードライブは既に発動中です
						RESTART
					ENDIF
				CASE "ポゼッション"
					PRINTL □憑依する相手を選んでください
					FOR PAGING, 0, CHARANUM
						CALL PAGING, PAGING != 詠唱者 && PAGING != MASTER
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
				CASE "ミラクル・スイート・スイーツ・マジック！！"
					$MSSM
					PRINTL □仲良しにする2人を選んでください
					IF 対象 == -1
						PRINTFORM 　[0] 選択する
					ELSE
						PRINTFORM 　[0] %NAME:対象%
					ENDIF
					PRINTPLAIN ×
					IF 対象:1 == -1
						PRINTFORML [1] 選択する
					ELSE
						PRINTFORML [1] %NAME:(対象:1)%
					ENDIF
					PRINT 　
					SIF 対象 >= 0 && 対象:1 >= 0 && 対象 != 対象:1
						PRINT [2] 確定 
					PRINTL [3] キャンセル
					DO
						INPUT
						SELECTCASE RESULT
							CASE 0
								FOR PAGING, 0, CHARANUM
									CALL PAGING, PAGING != 詠唱者 && PAGING != 対象:1
								NEXT
								SIF RESULT == -1
									GOTO MSSM
								対象 = RESULT
								GOTO MSSM
							CASE 1
								FOR PAGING, 0, CHARANUM
									CALL PAGING, PAGING != 詠唱者 && PAGING != 対象
								NEXT
								SIF RESULT == -1
									GOTO MSSM
								対象:1 = RESULT
								GOTO MSSM
							CASE 2
								SIF 対象 >= 0 && 対象:1 >= 0 && 対象 != 対象:1
									BREAK
							CASE 3
								RESTART
						ENDSELECT
					LOOP 1
				CASE "ジュエリーストーム"
					PRINTFORML □ジュエリーストームの対象に選んでください
					FOR PAGING, 1, CHARANUM
						CALL PAGING, PAGING != MASTER && PAGING != 詠唱者 && CSTR:PAGING:種族 != "機械" && !TALENT:PAGING:無機物 && !TALENT:PAGING:霊体 && NO:PAGING != FLAG:パートナー
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
				;これだけここで完結する
				CASE "スウィートイリュージョン"
					VARSET 対象, -1
					$SWEET
					PRINTFORML %CALLNAME:詠唱者%にスウィートイリュージョンを詠唱させます。誰を対象にしますか？
					PRINTFORML [0] - お菓子に変えるキャラ(\@ 対象 >= 0 ? %NAME:対象% # 無し \@)
					PRINTFORML [1] - それを食べるキャラ(\@ 対象:1 >= 0 ? %NAME:(対象:1)% # 無し \@)
					SIF 対象 >= 0 && 対象:1 >= 0
						PRINTL [2] - これで決定
					PRINTL [3] - やめる
					CALL INPUTF, 0, 1, 2, 3
					SELECTCASE RESULT
						CASE 3
							RESTART
						CASE 0
							FOR PAGING, 1, CHARANUM
								CALL PAGING, PAGING != MASTER && PAGING != 詠唱者 && CSTR:PAGING:種族 != "機械" && !TALENT:PAGING:無機物 && !TALENT:PAGING:霊体 && PAGING != 対象:1 && NO:PAGING != FLAG:パートナー
							NEXT
							対象 = RESULT
							GOTO SWEET
						CASE 1
							FOR PAGING, 0, CHARANUM
								CALL PAGING, PAGING != 対象
							NEXT
							対象:1 = RESULT
							GOTO SWEET
						CASE 2
							TFLAG:キャスター = 詠唱者
							;被捕食者のID<詠唱者のIDの場合はキャラロストで1ずれるので戻す
							SIF 対象 < 詠唱者
								詠唱者--
							CALL 捕食, 対象:1, 対象, 1
							PRINTFORML %CALLNAME:詠唱者%の詠唱経験+10
							EXP:詠唱者:詠唱経験 += 10
							BREAK
					ENDSELECT
				CASE "ハニーバニー"
					PRINTL □ハニーバニーで気を引く対象を選んでください
					FOR PAGING, 1, CHARANUM
						CALL PAGING, (TALENT:PAGING:恋心 || TALENT:PAGING:既婚者) && PAGING != 詠唱者
					NEXT
					SIF RESULT == -1
						RESTART
					対象 = RESULT
			ENDSELECT
			IF 年
				PRINTFORML %CALLNAME:詠唱者%に、{年}年{月}月{日}日を目標した%使用スペル名%を詠唱させます。よろしいですか？
			ELSEIF 対象 >= 0 && 対象:1 >= 0
				PRINTFORML %CALLNAME:詠唱者%に、%CALLNAME:対象%と%CALLNAME:(対象:1)%を対象にした%使用スペル名%を詠唱させます。よろしいですか？
			ELSEIF 対象 >= 0
				PRINTFORML %CALLNAME:詠唱者%に、%CALLNAME:対象%を対象にした%使用スペル名%を詠唱させます。よろしいですか？
			ELSE
				PRINTFORML %CALLNAME:詠唱者%に%使用スペル名%を詠唱させます。よろしいですか？
			ENDIF
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			CALL INPUTF, 0, 1
			SIF RESULT == 1
				RESTART
			詠唱魔法 = %使用スペル名%

			;優先度は詠唱者の口上→対象の口上→地の文
			;ただし地の文は口上側で管理するようになる
			IF FLAG:口上表示
				TRYCCALLFORM SPELLCAST_%CSVNAMERPS(詠唱者)%, 詠唱者, 対象:0, 対象:1
				CATCH
					TRYCCALLFORM RECIEVE_SPELLCAST_%CSVNAMERPS(対象:0)%, 詠唱者, 対象:0, 対象:1
					CATCH
						SIF 対象:1 < 0
							GOTO NOTTARGET1
						TRYCCALLFORM RECIEVE_SPELLCAST_%CSVNAMERPS(対象:1)%, 詠唱者, 対象:0, 対象:1
						CATCH
							$NOTTARGET1
							SIF FLAG:地の文表示
								CALL SPELLCAST_地の文, 詠唱者, 対象:0, 対象:1
						ENDCATCH
					ENDCATCH
				ENDCATCH
			ELSEIF FLAG:地の文表示
				CALL SPELLCAST_地の文, 詠唱者, 対象:0, 対象:1
			ENDIF
			CALL EXPUP, "詠唱経験", 必要レベル, 詠唱者
			;魔法の効果処理
			SELECTCASE 使用スペル名
				CASE "フリージングアトモスフィア"
					フリージングアトモスフィア = スキル
					SIF TIME == 0
						FLAG:気温 -= フリージングアトモスフィア*30
				CASE "バーニンザフロア"
					バーニンザフロア = スキル
					SIF TIME == 0
						FLAG:気温 += バーニンザフロア*30
				CASE "ストームバスター"
					ストームバスター = スキル
					スウィートレイン = 0
					スノープリズム = 0
					FLAG:天気 = 1
				CASE "スウィートレイン"
					ストームバスター = 0
					スウィートレイン = スキル
					スノープリズム = 0
					FLAG:天気 = 3
				CASE "スノープリズム"
					ストームバスター = 0
					スウィートレイン = 0
					スノープリズム = スキル
					FLAG:天気 = 6
				CASE "ビーストモード", "ビーストモードtype2"
					;ビーストモード:0=効果時間 1=耳の有無 2=尻尾の有無 3=ファーリーの有無
					;ビーストモードS=元の種族名
					ビーストモード:対象:0 = スキル
					SIF !TALENT:対象:動物耳
						ビーストモード:対象:1 = 1
					SIF !TALENT:対象:尻尾
						ビーストモード:対象:2 = 1
					ビーストモードS:対象:0 = %CSTR:対象:種族%
					TALENT:対象:動物耳 = 1
					TALENT:対象:尻尾 = 1
					CSTR:対象:種族 = 獣人
					IF 使用スペル名 == "ビーストモードtype2"
						SIF !TALENT:対象:ファーリー
							ビーストモード:対象:3 = 1
						TALENT:対象:ファーリー = 1
					ENDIF
				CASE "リ：ジェネレーション"
					リジェネレーション = スキル
				CASE "ジェントルストレス"
					ジェントルストレス:対象:0 = スキル
					ジェントルストレス:対象:1 = CFLAG:対象:ストレス値
				CASE "クロノダイブ"
					CALL プレイログ記録, @"クロノダイブで{年}年{月}月{日}日にタイムリープ"
					YEAR = 年
					MONTH = 月
					DAYS = 日
				CASE "ヒプノティッククライシス"
					ヒプノティッククライシス = スキル
				CASE "ティル・ナ・ノーグ"
					ティルナノーグ:対象:0 = スキル
					TALENT:対象:小人体型 = 0
					;TALENT:対象:小柄体型 = 1
				CASE "エナジードライブ"
					エナジードライブ = スキル
					エナジードライブ:1 = スキル-7
					FOR LCOUNT, 0, VARSIZE("ABL")
						SIF ABLNAME:LCOUNT != ""
							ABL:MASTER:LCOUNT += スキル-7
					NEXT
				CASE "ポゼッション"
					FOR LCOUNT, 0, 30
						SIF ABLNAME:LCOUNT == "魔法技能"
							CONTINUE
						ポゼッション:対象:LCOUNT = ABL:対象:LCOUNT
						ABL:対象:LCOUNT = ABL:詠唱者:LCOUNT
					NEXT
					CFLAG:対象:ポゼ = スキル
				CASE "ミラクル・スイート・スイーツ・マジック！！"
					CALL 相性加算, 対象, 対象:1, 20
					CALL 相性加算, 対象:1, 対象, 20
				CASE "バック・トゥ・クロノス"
					CALL プレイログ記録, @"バック・トゥ・クロノス詠唱"
					DO
						DAYS--
						IF DAYS <= 0
							MONTH--
							IF MONTH <= 0
								YEAR--
								MONTH = 12
							ENDIF
							DAYS = 31
						ENDIF
					LOOP !EXIST_DAY(YEAR, MONTH, DAYS)
					DAY--
				CASE "ジュエリーストーム"
					CALL ジュエリーストーム, 対象, 詠唱者
					SIF RESULT == 0
						RESTART
					;被詠唱者のID<詠唱者のIDの場合はキャラロストで1ずれるので戻す
					SIF 対象 < 詠唱者
						詠唱者--
				CASE "ハニーバニー"
					ハニーバニー:対象:0 = スキル
				CASEELSE
					CONTINUE
			ENDSELECT
			BREAK
	ENDSELECT
LOOP 1
WAIT
BASE:詠唱者:魔力 = コスト
IF コスト < 0
	PRINTFORMW %CALLNAME:詠唱者%は魔力の反動でひどく疲れてしまったようだ。少し休ませてあげよう……
	CFLAG:詠唱者:使用不可 = ABS(コスト/100+1)
	CALL 配属解除, 詠唱者
ENDIF
RETURN 1

@魔力消費量, ARG, ARG:1
#FUNCTION

RETURNF ARG*1000-(ARG*100*(ARG:1-ARG))

@ジュエリーストーム, 対象, 詠唱者
#DIM DYNAMIC 対象
#DIM DYNAMIC 詠唱者
#DIM DYNAMIC 価値
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 空き変数
#DIM DYNAMIC 色保存用

PRINTFORML □%CALLNAME:対象%を鉱物化する際の色を設定してください
CALL RGBBAR, CFLAG:対象:タグ色/0x10000, CFLAG:対象:タグ色%0x10000/0x100, CFLAG:対象:タグ色%0x100

色保存用 = RESULT

SIF RESULT == 0
	RETURN 0

詠唱魔法 = ジュエリーストーム

;優先度は詠唱者の口上→対象の口上→地の文
;ただし地の文は口上側で管理するようになる
IF FLAG:口上表示
	TRYCCALLFORM SPELLCAST_%CSVNAMERPS(詠唱者)%, 詠唱者, 対象
	CATCH
		TRYCCALLFORM RECIEVE_SPELLCAST_%CSVNAMERPS(対象)%, 詠唱者, 対象
		CATCH
			SIF FLAG:地の文表示
				CALL SPELLCAST_地の文, 詠唱者, 対象
		ENDCATCH
	ENDCATCH
ELSEIF FLAG:地の文表示
	CALL SPELLCAST_地の文, 詠唱者, 対象
ENDIF
CALL EXPUP, "詠唱経験", 5, 詠唱者

;価値は1000は保証
価値 = 1000

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:対象:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "狐", "妖狐", "小柄体型", "戦士", "ファーリー", "尻尾", "動物耳", "羽", "ふたなり", "恋慕", "淫乱", "服従"
			価値 += 500
		CASE "魅力", "魅惑", "足使用不可", "スライム", "溶ける快", "親愛", "娼婦", "隷属"
			価値 += 1000
		CASE "謎の魅力", "人気", "妊娠"
			価値 += 1500
		CASE "バストサイズ"
			SELECTCASE TALENT:対象:バストサイズ
				CASE IS >= 4
					価値 += (TALENT:対象:バストサイズ-3)*100
			ENDSELECT
	ENDSELECT
NEXT

;タトゥーでも微増
SIF CSTR:対象:右腕の印 != ""
	価値 += 100
SIF CSTR:対象:左腕の印 != ""
	価値 += 100
SIF CSTR:対象:右足の印 != ""
	価値 += 100
SIF CSTR:対象:左足の印 != ""
	価値 += 100
SIF CSTR:対象:背中の印 != ""
	価値 += 200
SIF CSTR:対象:胴体の印 != ""
	価値 += 300
;人気度を加算
価値 += 人気度:(NO:対象)

;崩壊が付いてると激減
SIF TALENT:対象:崩壊
	価値 /= 10

FOR LCOUNT, 1, VARSIZE("MUSEUM", 0)
	IF MUSEUM:LCOUNT:0 == ""
		空き変数 = LCOUNT
		BREAK
	ENDIF
NEXT

;#;SIF 空き変数 == 0
;#;	THROW 変数「MUSEUM」に空きがありません

MUSEUM:空き変数:0 = 鉱物
MUSEUM:空き変数:1 = 鉱物化した%NAME:対象%

;陥落の種類で鉱物の名称変化
SIF TALENT:対象:恋慕
	MUSEUM:空き変数:1 = 穏やかに微笑む%NAME:対象%の彫像
SIF TALENT:対象:親愛
	MUSEUM:空き変数:1 = 幸福に満ちた%NAME:対象%の彫像
SIF TALENT:対象:淫乱
	MUSEUM:空き変数:1 = 淫らな%NAME:対象%の裸婦像
SIF TALENT:対象:娼婦
	MUSEUM:空き変数:1 = 色欲に溺れた%NAME:対象%の裸婦像
SIF TALENT:対象:服従
	MUSEUM:空き変数:1 = 調度品として身を捧げた%NAME:対象%
SIF TALENT:対象:隷属
	MUSEUM:空き変数:1 = 永遠の晒し物となった%NAME:対象%
SIF TALENT:対象:崩壊
	MUSEUM:空き変数:1 = 今にも朽ちそうな%NAME:対象%の像

MUSEUM:空き変数:2 = {NO:対象}
MUSEUM:空き変数:3 = {価値}
MUSEUM:空き変数:4 = {色保存用}
MUSEUM:空き変数:5 = {CFLAG:対象:血縁コード}
MUSEUM:空き変数:6 = {CFLAG:対象:複製}
MUSEUM:空き変数:7 = 100
MUSEUM:空き変数:8 = 0

CALL プレイログ記録, @"%NAME:対象%が鉱物化する"
CALL CLEAR_CHARA, 対象

RETURN 1

@RGBBAR, INT_R, INT_G, INT_B
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC INT_R
#DIM DYNAMIC INT_G
#DIM DYNAMIC INT_B
{
#DIMS DYNAMIC 色プリセット = 
"ゴールド",
"ルビー",
"サファイア",
"エメラルド",
"アメジスト",
"トパーズ",
"クリスタル",
"石",
}

GDISPOSE 0
GDISPOSE 1
GDISPOSE 2
GDISPOSE 3

CALL LOOP_CLEARLINE

GCREATE 0, 256, 17
FOR LCOUNT, 0, 256
	FOR LCOUNT2, 0, 17
		IF INT_R == LCOUNT
			GSETCOLOR 0, 0xFF000000+COLOR_FROMRGB(255, 255, 255), LCOUNT, LCOUNT2
		ELSE
			GSETCOLOR 0, 0xFF000000+COLOR_FROMRGB(LCOUNT, 0, 0), LCOUNT, LCOUNT2
		ENDIF
	NEXT
NEXT
SPRITECREATE "RBAR", 0

GCREATE 1, 256, 17
FOR LCOUNT, 0, 256
	FOR LCOUNT2, 0, 17
		IF INT_G == LCOUNT
			GSETCOLOR 1, 0xFF000000+COLOR_FROMRGB(255, 255, 255), LCOUNT, LCOUNT2
		ELSE
			GSETCOLOR 1, 0xFF000000+COLOR_FROMRGB(0, LCOUNT, 0), LCOUNT, LCOUNT2
		ENDIF
	NEXT
NEXT
SPRITECREATE "GBAR", 1

GCREATE 2, 256, 17
FOR LCOUNT, 0, 256
	FOR LCOUNT2, 0, 17
		IF INT_B == LCOUNT
			GSETCOLOR 2, 0xFF000000+COLOR_FROMRGB(255, 255, 255), LCOUNT, LCOUNT2
		ELSE
			GSETCOLOR 2, 0xFF000000+COLOR_FROMRGB(0, 0, LCOUNT), LCOUNT, LCOUNT2
		ENDIF
	NEXT
NEXT
SPRITECREATE "BBAR", 2

PRINTBUTTON "[E]", "E"
PRINT_IMG "RBAR"
PRINTBUTTON "[R]", "R"
PRINTBUTTON @" R値:{INT_R} [変更]", "SET_R"
PRINTL 

PRINTBUTTON "[F]", "F"
PRINT_IMG "GBAR"
PRINTBUTTON "[G]", "G"
PRINTBUTTON @" G値:{INT_G} [変更]", "SET_G"
PRINTL 

PRINTBUTTON "[V]", "V"
PRINT_IMG "BBAR"
PRINTBUTTON "[B]", "B"
PRINTBUTTON @" B値:{INT_B} [変更]", "SET_B"
PRINTL 

PRINTL □サンプル色
GCREATE 3, 256, 17
GCLEAR 3, 0xFF000000+COLOR_FROMRGB(INT_R, INT_G, INT_B)
SPRITECREATE "SAMPLEBAR", 3

PRINT 　[
PRINT_IMG "SAMPLEBAR"
PRINTL ]
PRINTL □プリセットから選ぶ
PRINT 　
FOR LCOUNT, 0, VARSIZE("色プリセット")
	PRINTBUTTON @"[%色プリセット:LCOUNT%]", 色プリセット:LCOUNT
NEXT
PRINTL 

DRAWLINE
PRINTPLAIN 　RGB　
PRINTBUTTON "[0] - 戻る ", "0"
PRINTBUTTON "[1] - 決定", "1"
PRINTL 

ONEINPUTS

SELECTCASE RESULTS
	CASE "0"
		GDISPOSE 0
		GDISPOSE 1
		GDISPOSE 2
		GDISPOSE 3
		RETURN 0
	CASE "1"
		GDISPOSE 0
		GDISPOSE 1
		GDISPOSE 2
		GDISPOSE 3
		RETURN COLOR_FROMRGB(INT_R, INT_G, INT_B)
	CASE "E", "e"
		INT_R--
	CASE "R", "r"
		INT_R++
	CASE "F", "f"
		INT_G--
	CASE "G", "g"
		INT_G++
	CASE "V", "v"
		INT_B--
	CASE "B", "b"
		INT_B++
	CASE "SET_R", "SET_G", "SET_B"
		PRINTL 数値を入力してください（0～255）
		INPUT
		SELECTCASE RESULTS
			CASE "SET_R"
				INT_R = RESULT
			CASE "SET_G"
				INT_G = RESULT
			CASE "SET_B"
				INT_B = RESULT
		ENDSELECT
	CASE "ゴールド"
		INT_R = 0xFF
		INT_G = 0xD7
		INT_B = 0x00
	CASE "ルビー"
		INT_R = 0xC7
		INT_G = 0x00
		INT_B = 0x67
	CASE "サファイア"
		INT_R = 0x00
		INT_G = 0x54
		INT_B = 0xA6
	CASE "エメラルド"
		INT_R = 0x00
		INT_G = 0xA9
		INT_B = 0x68
	CASE "アメジスト"
		INT_R = 0x9E
		INT_G = 0x76
		INT_B = 0xB4
	CASE "トパーズ"
		INT_R = 0xE9
		INT_G = 0xBC
		INT_B = 0x00
	CASE "クリスタル"
		INT_R = 0x00
		INT_G = 0xFF
		INT_B = 0xFF
	CASE "石"
		INT_R = 0xA9
		INT_G = 0xA9
		INT_B = 0xA9
ENDSELECT

INT_R = LIMIT(INT_R, 0, 255)
INT_G = LIMIT(INT_G, 0, 255)
INT_B = LIMIT(INT_B, 0, 255)

RESTART

@ISENCHANT, 対象
#FUNCTION
#DIM DYNAMIC 対象
;対象に魔法強化がかかっているかの判定
SIF 対象 < 0
	RETURNF 0

SIF ビーストモード:対象:0
	RETURNF 1
SIF ジェントルストレス:対象:0
	RETURNF 1
SIF ティルナノーグ:対象:0
	RETURNF 1
SIF CFLAG:対象:ポゼ
	RETURNF 1
SIF ハニーバニー:対象:0
	RETURNF 1

RETURNF 0

@TURNEND_魔法効果
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2

IF フリージングアトモスフィア > 0
	SIF --フリージングアトモスフィア == 0
		PRINTW [フリージングアトモスフィア]の効果が切れました
ENDIF
IF バーニンザフロア > 0
	SIF --バーニンザフロア == 0
		PRINTW [バーニンザフロア]の効果が切れました
ENDIF
IF ストームバスター > 0
	SIF --ストームバスター == 0
		PRINTW [ストームバスター]の効果が切れました
ENDIF
IF スウィートレイン > 0
	SIF --スウィートレイン == 0
		PRINTW [スウィートレイン]の効果が切れました
ENDIF
IF スノープリズム > 0
	SIF --スノープリズム == 0
		PRINTW [スノープリズム]の効果が切れました
ENDIF
IF リジェネレーション > 0
	SIF --リジェネレーション == 0
		PRINTW [リ：ジェネレーション]の効果が切れました
ENDIF
IF ヒプノティッククライシス > 0
	SIF --ヒプノティッククライシス == 0
		PRINTW [ヒプノティッククライシス]の効果が切れました
ENDIF
IF エナジードライブ > 0
	IF --エナジードライブ == 0
		PRINTFORMW %CALLNAME:MASTER%にかけられた[エナジードライブ]の効果が切れました
		FOR LCOUNT, 0, VARSIZE("ABL")
			SIF ABLNAME:LCOUNT != ""
				ABL:MASTER:LCOUNT -= エナジードライブ:1
		NEXT
		VARSET エナジードライブ
	ENDIF
ENDIF
FOR LCOUNT, 0, CHARANUM
	IF ビーストモード:LCOUNT:0 > 0
		IF --ビーストモード:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ビーストモード]の効果が切れました
			SIF ビーストモード:LCOUNT:1
				TALENT:LCOUNT:動物耳 = 0
			SIF ビーストモード:LCOUNT:2
				TALENT:LCOUNT:尻尾 = 0
			SIF ビーストモード:LCOUNT:3
				TALENT:LCOUNT:ファーリー = 0
			CSTR:LCOUNT:種族 = %ビーストモードS:LCOUNT:0%
			VARSET ビーストモード:LCOUNT:0
			VARSET ビーストモードS:LCOUNT:0
		ENDIF
	ENDIF
	IF ジェントルストレス:LCOUNT:0 > 0
		IF --ジェントルストレス:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられたジェントルストレスの効果が切れました
			ジェントルストレス:LCOUNT:1 = 0
		ENDIF
	ENDIF
	IF ティルナノーグ:LCOUNT:0 > 0
		IF --ティルナノーグ:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ティル・ナ・ノーグ]の効果が切れました
			TALENT:LCOUNT:小人体型 = 1
		ENDIF
	ENDIF
	IF CFLAG:LCOUNT:ポゼ
		IF --CFLAG:LCOUNT:ポゼ == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ポゼッション]の効果が切れました
			FOR LCOUNT2, 0, 30
				SIF ABLNAME:LCOUNT2 == "魔法技能"
					CONTINUE
				ABL:LCOUNT:LCOUNT2 = ポゼッション:LCOUNT:LCOUNT2
			NEXT
			VARSET ポゼッション:LCOUNT:0
		ENDIF
	ENDIF
	IF ハニーバニー:LCOUNT:0 > 0
		SIF -- ハニーバニー:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ハニーバニー]の効果が切れました
	ENDIF
NEXT

@SPELLCAST_地の文, 詠唱者, 対象:0, 対象:1
#DIM DYNAMIC 詠唱者
#DIM DYNAMIC 対象, 2

SELECTCASE 詠唱魔法
	CASE "フリージングアトモスフィア"
		PRINTFORML %CALLNAME:詠唱者%は%詠唱魔法%を唱えた
		PRINTFORML 突然、外が寒くなった気がする……
	CASE "バーニンザフロア"
		PRINTFORML %CALLNAME:詠唱者%は%詠唱魔法%を唱えた
		PRINTFORML 突然、外が暖かくなった気がする……
	CASE "ストームバスター"
		PRINTFORML %CALLNAME:詠唱者%は%詠唱魔法%を唱えた
		SELECTCASE FLAG:天気
			CASE 1
				IF TIME == 0
					PRINTFORML 日差しが強くなり、雲ひとつない青空が天に広がった……
				ELSE
					PRINTFORML 月明かりと星明かりがよりいっそう強くなった気がする……
				ENDIF
			CASEELSE
				IF TIME == 0
					PRINTFORML 数刻が過ぎると、空は快晴となった……
				ELSE
					PRINTFORML 数刻が過ぎると、空には綺麗な星空が広がっていた……
				ENDIF
		ENDSELECT
	CASE "スウィートレイン"
		PRINTFORML %CALLNAME:詠唱者%は%詠唱魔法%を唱えた
		SELECTCASE FLAG:天気
			CASE 3, 4, 5
				;全然スウィートじゃない
				PRINTFORML 雨はさらに強くなり、風がうなり始めた……
			CASE 6, 7
				PRINTFORML 雪は突然、雨と変わり、ざぁざぁと音を立てて降り始めた……
			CASEELSE
				PRINTFORML ぽつぽつと雨が降り始めた……
		ENDSELECT
	CASE "スノープリズム"
		PRINTFORML %CALLNAME:詠唱者%は%詠唱魔法%を唱えた
		SELECTCASE FLAG:天気
			CASE 3, 4, 5
				PRINTFORML 雨は突如、雹のつぶてになり、やがて白く柔らかい雪へと変わった……
			CASE 6, 7
				PRINTFORML 降雪はさらに勢いを増し、外の風景を真っ白に染め上げた……
			CASEELSE
				PRINTFORML 空模様は変わらないまま雪が降ってきた……
		ENDSELECT
	CASE "ビーストモード", "ビーストモードtype2"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%に対してビーストモードを唱えた
		PRINTFORM %CALLNAME:対象%に
		SIF !TALENT:対象:動物耳
			PRINT 動物耳
		SIF !TALENT:対象:動物耳 && !TALENT:対象:尻尾
			PRINT と
		SIF !TALENT:対象:尻尾
			PRINT 尻尾
		PRINT が生え
		SIF 詠唱魔法 == "ビーストモードtype2"
			PRINT 、全身が体毛に覆われ
		PRINTL た……
	CASE "リ：ジェネレーション"
		PRINTFORML %CALLNAME:詠唱者%はリ：ジェネレーションを唱えた
		PRINTFORML 館の周囲にぼんやりと魔法陣が浮かび上がり、少しだけ心身が安らかになった……
	CASE "ジェントルストレス"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%に対してジェントルストレスを唱えた
		PRINTFORML %CALLNAME:対象%の感情はとても穏やかになった……
	CASE "クロノダイブ"
		PRINTFORML %CALLNAME:詠唱者%はクロノダイブを唱えた
		PRINTFORML 激しい地鳴りと地震が響き渡り、館を出ると今までとは違う風景が外に広がっていた……
	CASE "ヒプノティッククライシス"
		PRINTFORML %CALLNAME:詠唱者%はヒプノティッククライシスを唱えた
		PRINTFORML %CALLNAME:MASTER%の瞳は見るものを虜にする魔力を得て、よりいっそう魅力的になった……
	CASE "ティル・ナ・ノーグ"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%に対してティル・ナ・ノーグを唱えた
		PRINTFORML %CALLNAME:対象%はまばゆい光に包まれ、人間と比べても遜色の無い大きさの身体になった……
	CASE "エナジードライブ"
		PRINTFORML %CALLNAME:詠唱者%はエナジードライブを唱えた
		PRINTFORML %CALLNAME:MASTER%の身体はさらなる活力を得た……
	CASE "ポゼッション"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%に対してポゼッションを唱えた
		PRINTFORML %CALLNAME:対象%は意識を失い、再び目が覚めると自身の体の違和感に困惑している……
	CASE "ミラクル・スイート・スイーツ・マジック！！"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%と%CALLNAME:(対象:1)%にミラクル・スイート・スイーツ・マジック！！を唱えた
		;無関心
		IF 相性値(対象, 対象:1) == 100 && 相性値(対象:1, 対象) == 100
			PRINTFORML %CALLNAME:対象%と%CALLNAME:(対象:1)%は少しお互いのことを気にし始めたようだ……
		;敵対
		ELSEIF 相性値(対象, 対象:1) < 100 && 相性値(対象:1, 対象) < 100
			PRINTFORML %CALLNAME:対象%と%CALLNAME:(対象:1)%の間柄が少し改善したようだ……
		;親密
		ELSEIF 相性値(対象, 対象:1) >= 100 && 相性値(対象:1, 対象) >= 100
			PRINTFORML %CALLNAME:対象%と%CALLNAME:(対象:1)%の絆がさらに深まったようだ……
		;片方だけ悪い
		ELSEIF 相性値(対象, 対象:1) < 100
			PRINTFORML %CALLNAME:対象%は%CALLNAME:(対象:1)%に心を少し開いた…… 
		ELSEIF 相性値(対象:1, 対象) < 100
			PRINTFORML %CALLNAME:(対象:1)%は%CALLNAME:対象%に心を少し開いた……
		ENDIF
	CASE "バック・トゥ・クロノス"
		PRINTFORML %CALLNAME:詠唱者%はバック・トゥ・クロノスを唱えた
		PRINTFORML 何も起こっていないように思えるが、1日前の世界に遡ったようだ……
	CASE "ジュエリーストーム"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%にジュエリーストームを唱えた
		SIF CFLAG:対象:タグ色
			SETCOLOR CFLAG:対象:タグ色
		PRINTFORML %CALLNAME:対象%は無機物となり、瞳の輝きも失われた。もう元に戻ることはないだろう……
		RESETCOLOR
	CASE "ハニーバニー"
		PRINTFORML %CALLNAME:詠唱者%は%CALLNAME:対象%に対してハニーバニーを唱えた
		IF CFLAG:対象:陥落
			PRINTFORML しかし%CALLNAME:対象%は既に陥落してるので意味が無かった……
		ELSE
			PRINTFORML 一瞬視界が眩んだ%CALLNAME:対象%は、気がつくと明らかに%CALLNAME:MASTER%への視線が変わった……
		ENDIF
ENDSELECT
