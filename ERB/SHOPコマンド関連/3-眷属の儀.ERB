@FAMILIAR
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 主 = -1
#DIM DYNAMIC 対象 = -1
#DIM DYNAMIC 費用
#DIM DYNAMIC ネクロ兼用
#DIM DYNAMIC 眷属可能, 1000
#DIM DYNAMIC 被眷属可能, 1000
#DIM DYNAMIC 儀式実行可能

ネクロ兼用 = 0
儀式実行可能 = 0
費用 = 100000

IF 対象 >= 0
	SIF TALENT:対象:小柄体型
		費用 += 30000
ENDIF

IF 主 >= 0
	SELECTCASE CSTR:主:種族
		CASE "神", "天使", "悪魔"
			費用 += 100000
		CASE "精霊", "鳳凰", "魔術師"
			費用 += 50000
		CASE "ヴァンパイア", "スライム"
			費用 = 0
	ENDSELECT
ENDIF

IF 主 >= 0 && 対象 >= 0
	;相性が良いと割安、悪いと割高
	費用 = 費用*(10000/相性値(主, 対象))/100
	費用 = 費用*(10000/相性値(対象, 主))/100
ENDIF

DRAWLINE
PRINTL □眷属の儀
IF 主 >= 0 && 対象 >= 0
	SELECTCASE CSTR:主:種族
		CASE "人間", "人間(？)", "改造人間", "宇宙人", "不明"
			PRINTFORML 　%NAME:主%により%NAME:対象%を屍鬼の一族に迎え入れる儀式
		CASEELSE
			PRINTFORML 　%NAME:主%により%NAME:対象%を%CSTR:主:種族%\@ TALENT:主:ネクロマンサー ? 、もしくは屍鬼 #  \@の一族に迎え入れる儀式
			SIF TALENT:主:ネクロマンサー
				ネクロ兼用 = 1
	ENDSELECT
ENDIF
IF ORIGIN
	費用 = 0
ELSE
	PRINTL □材料費
	CALL PASELIL, @"　{費用}＄"
ENDIF
PRINTL □効果
PRINTL 　眷属主は陥落済みの必要がある。眷属対象は陥落済み、もしくは眷属主に対する相性が一定以上でなければならない(主人は例外)
PRINTL 　眷属の儀を執り行うには錬金術による薬の調合等に材料費が必要
PRINTL 　眷属主と眷属対象は儀式の後、3日ほどの安静が必要
IF !ORIGIN
	PRINTL 　眷属主と眷属対象の売却額は下がる。眷属主は眷属の儀を行うたびに売却額が下がっていく
	PRINTL 　ただしヴァンパイアとしての眷属の儀は上記の限りではない
ENDIF
PRINTL 
PRINTLC 　[1] - 眷属の主を選ぶ
PRINTLC [2] - 眷属化の対象を選ぶ
PRINTL 

IF 主 == -1
	SETCOLORBYNAME gray
	PRINTFORMLC 　主:----
ELSE
	IF !CFLAG:主:陥落 && 主 != 0
		SETCOLORBYNAME gray
		PRINTFORMLC 　主:%NAME:主% 未陥落
	ELSE
		PRINTFORMLC 　主:%NAME:主%
	ENDIF
ENDIF
RESETCOLOR
IF 対象 == -1
	SETCOLORBYNAME gray
	PRINTFORMLC 対象:----
ELSE
	IF !CFLAG:対象:陥落 && 対象 != 0 && 相性値(対象, 主) < 120
		SETCOLORBYNAME gray
		PRINTFORMLC 対象:%NAME:対象% 未陥落
	ELSE
		PRINTFORMLC 対象:%NAME:対象%
	ENDIF
ENDIF
RESETCOLOR
PRINTL 
DRAWLINE
PRINTPLAIN 　眷属の儀　
PRINTLC [0] - 戻る 
IF 主 >= 0 && 対象 >= 0 && (CFLAG:主:陥落 || 主 == 0) && (CFLAG:対象:陥落 || 対象 == 0 || 相性値(対象, 主) >= 120)
	PRINT [3] - 儀式を執り行う
	儀式実行可能 = 1
ENDIF
PRINTL 

FOR LCOUNT, 0, CHARANUM
	SELECTCASE CSTR:LCOUNT:種族
		CASE "人間", "宇宙人", "人間(？)", "改造人間", "不明"
			被眷属可能:LCOUNT = 1
		;幽霊は眷属の儀はできない
		CASE "幽霊"
		CASEELSE
			眷属可能:LCOUNT = 1
	ENDSELECT
	SIF TALENT:LCOUNT:ネクロマンサー
		眷属可能:LCOUNT = 1
NEXT

CALL INPUTF, 0, 1, 2, 3

SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASE 1
		DRAWLINE
		PRINTL □眷属主を選んでください
		;人間系か不明以外の種族、もしくはネクロマンサー
		FOR PAGING, 0, CHARANUM
			CALL PAGING, 眷属可能:PAGING, @"%CSTR:PAGING:種族%", "眷属の儀", "やめる"
		NEXT
		SIF RESULT >= 0
			主 = RESULT
		RESTART
		
	CASE 2
		DRAWLINE
		PRINTL □眷属対象を選んでください
		;人間系か不明の種族
		FOR PAGING, 0, CHARANUM
			CALL PAGING, 被眷属可能:PAGING, @"%CSTR:PAGING:種族%", "眷属の儀", "やめる"
		NEXT
		SIF RESULT >= 0
			対象 = RESULT
		RESTART
	CASE 3
		SIF !儀式実行可能
			RESTART

		IF 費用 > MONEY && !ORIGIN
			PRINTW 費用が足りません
			RESTART
		ENDIF
		IF ネクロ兼用
			IF CSTR:主:種族 == "幽霊"
				ネクロ兼用 = 1
			ELSE
				PRINTFORML %CALLNAME:主%は%CSTR:主:種族%と屍鬼の眷属化が可能です。どちらにしますか？
				PRINTFORML [0] - %CSTR:主:種族%
				PRINTL [1] - 屍鬼
				CALL INPUTF, 0, 1
				ネクロ兼用 = RESULT
			ENDIF
		ENDIF
		SIF !ORIGIN
			CALL PASELI, @"{費用}＄"
		PRINTFORML を使い、%NAME:主%と%NAME:対象%の眷属の儀を執り行います。本当によろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		CALL INPUTF, 0, 1
		SIF RESULT
			RESTART
		MONEY -= 費用
		CALL プレイログ記録, @"%NAME:対象%が%NAME:主%によって%CSTR:主:種族%の眷属に迎え入れられる"
		CALL 現金出納帳記録, @"眷属の儀/-{費用}"
		TFLAG:眷属主 = 主
		TFLAG:眷属対象 = 対象
		CALL KOJO_MESSAGE_EVENT, "眷属化", 主
		CALL KOJO_MESSAGE_EVENT, "眷属の儀", 対象
		PRINTW ………………
		PRINTW …………
		PRINTW ……
		IF ネクロ兼用
			PRINTFORMW 儀式は無事に終了し、%CALLNAME:対象%は屍鬼の眷属となりました
			PRINTW 二人とも体力を消耗したようです。しばらく安静にしましょう
			CSTR:対象:種族 = ゾンビ
			;痛みに弱いがあれば消える
			TALENT:対象:痛みに弱い = 0
		ELSE
			PRINTFORMW 儀式は無事に終了し、%CALLNAME:対象%は%CSTR:主:種族%の眷属となりました
			PRINTW 二人とも体力を消耗したようです。しばらく安静にしましょう
			CSTR:対象:種族 = %CSTR:主:種族%
			;種族次第で素質が付いたりする
			SELECTCASE CSTR:対象:種族
				CASE "天使"
					TALENT:対象:羽 = 1
				CASE "悪魔"
					;一段階頑丈になる
					IF TALENT:対象:痛みに弱い
						TALENT:対象:痛みに弱い = 0
					ELSE
						TALENT:対象:痛みに強い = 1
					ENDIF
				CASE "神"
					TALENT:対象:謎の魅力 = 1
				CASE "獣人", "獣"
					TALENT:対象:動物耳 = 1
					TALENT:対象:尻尾 = 1
					;エロくなる
					IF TALENT:対象:無関心
						TALENT:対象:無関心 = 0
					ELSE
						TALENT:対象:好奇心 = 1
					ENDIF
				CASE "魔術師"
					TALENT:対象:調合知識 = 1
				CASE "人魚"
					;一段階濡れやすくなる
					IF TALENT:対象:濡れにくい
						TALENT:対象:濡れにくい = 0
					ELSE
						TALENT:対象:濡れやすい = 1
					ENDIF
				CASE "鳳凰"
					TALENT:対象:羽 = 1
					MAXBASE:対象:鳳凰基礎体力 = MAXBASE:対象:体力
					MAXBASE:対象:鳳凰基礎気力 = MAXBASE:対象:気力
				CASE "ヴァンパイア"
					TALENT:対象:吸血鬼 = 1
					TALENT:対象:月光浴 = 1
				CASE "スライム"
					TALENT:対象:スライム = 1
					CFLAG:対象:スライム度 = CFLAG:主:スライム度+30
					CFLAG:主:スライム度 += 100
					CFLAG:対象:スライム治療不可 = 1
					CFLAG:主:スライム治療不可 = 1
			ENDSELECT
		ENDIF
		CFLAG:主:使用不可 = 7
		CFLAG:対象:使用不可 = 7

		SIF TALENT:主:回復遅い
			CFLAG:主:使用不可 += 4
		SIF TALENT:対象:回復遅い
			CFLAG:対象:使用不可 += 4

		SIF TALENT:主:回復早い
			CFLAG:主:使用不可 -= 2
		SIF TALENT:対象:回復早い
			CFLAG:対象:使用不可 -= 2
		
		CSTR:対象:眷属主 = %NAME:主%

		CFLAG:主:眷属化した人数++
		
		SIF 相性値(主, 対象) < 500
			CALL 相性加算, 主, 対象, 20, 500
		SIF 相性値(対象, 主) < 500
			CALL 相性加算, 対象, 主, 20, 500

		RETURN 1
ENDSELECT

