;ステータス表示
@SHOW_CHARADATA
#DIM 表示切り替え
#DIM DYNAMIC LTARGET
#DIM 表示上限 = 10
#DIM DYNAMIC 次ページあり
DRAWLINE

CALL LOOP_CLEARLINE

CALL LIFE_LIST, "通常表示", 表示切り替え, 表示上限
;RESULTにはまだ表示されてないキャラが居るかどうか
次ページあり = RESULT
DRAWLINE

PRINTPLAIN 　CHARADATA　
CALL PRINTSPACE, TFLAG:ページ数, "[999] 前ページ "
PRINTFORM [1000] 戻る 
CALL PRINTSPACE, 次ページあり, "[1001] 次ページ "

PRINT [1010] キャラソート 
IF 表示切り替え
	PRINT [1011] 通常表示に切り替え 
ELSE
	PRINT [1011] 経営用表示に切り替え 
ENDIF
SIF ORIGIN
	PRINT [1012] 登場作品で絞る
PRINTL

DO
	INPUT
	SELECTCASE RESULT
		CASE -1
			SIF 表示上限 > 10
				表示上限 -= 10
			RESTART
		CASE -9
			表示上限 += 10
			RESTART
		CASE 999
			SIF TFLAG:ページ数
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN
		CASE 1001
			SIF 次ページあり
				TFLAG:ページ数++
			RESTART
		CASE 1010
			CALL CHARA_SORT
			RESTART
		CASE 1011
			INVERTBIT 表示切り替え, 0
			RESTART
		CASE 1012
			CALL 登場作品フィルタ設定
			RESTART
		CASE 0 TO CHARANUM-1
			LTARGET = TARGET
			TARGET = RESULT
			;TARGETの能力表示中はあっちで「調教対象に指定」を表示させないためのフラグ
			IF TARGET == LTARGET
				SHOW_TARGET = LTARGET
			ELSE
				SHOW_TARGET = 0
			ENDIF
			CALL STATUS
			;TARGET変更がされてる場合は戻さない
			SIF !RESULT && ASSI != LTARGET
				TARGET = LTARGET
			;TARGET変更無しかつ元々TARGETが居なかった場合も戻す
			SIF !RESULT && LTARGET == -1
				TARGET = -1
			RESTART
	ENDSELECT
LOOP 1

;調教対象選択
@CHANGE_TARGET
#DIM DYNAMIC LCOUNT
#DIM 表示上限 = 10
#DIM DYNAMIC 次ページあり

CALL LOOP_CLEARLINE

DRAWLINE
CALL LIFE_LIST, "調教対象選択", 0, 表示上限
;RESULTには未表示キャラが居るかどうか
次ページあり = RESULT
DRAWLINE
PRINTPLAIN 　CHANGE TARGET　
CALL PRINTSPACE, TFLAG:ページ数, "[999] 前ページ "
PRINTFORM [1000] 戻る 
CALL PRINTSPACE, 次ページあり, "[1001] 次ページ "
PRINT [1010] キャラソート 
SIF ORIGIN
	PRINT [1011] 登場作品で絞る
PRINTL 

INPUT
SELECTCASE RESULT
	CASE MASTER
		PRINTW 自分を調教することはできません
	CASE -1
		SIF 表示上限 > 10
			表示上限 -= 10
	CASE -9
		表示上限 += 10
	CASE 999
		SIF TFLAG:ページ数
			TFLAG:ページ数--
	CASE 1000
		RETURN
	CASE 1001
		SIF 次ページあり
			TFLAG:ページ数++
	CASE 1010
		CALL CHARA_SORT
	CASE 1011
		CALL 登場作品フィルタ設定
		TFLAG:ページ数 = 0
	CASE 0 TO CHARANUM-1
		SIF CHECK_CHILD_CARE(RESULT)
			RESTART
		SIF CFLAG:RESULT:使用不可
			RESTART
		SIF RESULT == 0
			RESTART
		CALL ASSIGN_TARGET, RESULT
		RETURN
ENDSELECT

RESTART

;対象指定用関数
@ASSIGN_TARGET, 対象
#DIM DYNAMIC 対象
TARGET = 対象
SIF TARGET == ASSI
	ASSI = -1
CALL KOJO_MESSAGE_EVENT, "調教対象指定"
;配属してたら自動的に外す
CALL 配属解除, TARGET
;監禁も解除
SIF CFLAG:対象:監禁
	CALL 監禁解除, 対象

@SELECT_ASSI, ARG
#DIM DYNAMIC LTARGET
#DIM 表示上限 = 10
#DIM DYNAMIC 次ページあり

CALL LOOP_CLEARLINE

DRAWLINE
CALL LIFE_LIST, "助手選択", 0, 表示上限
;RESULTにはまだ表示されてないキャラが居るかどうか
次ページあり = RESULT
DRAWLINE
PRINTPLAIN 　SELECT ASSI　
CALL PRINTSPACE, TFLAG:ページ数, "[999] 前ページ "
PRINTFORM [1000] 戻る 
CALL PRINTSPACE, 次ページあり, "[1001] 次ページ "
PRINT [1010] キャラソート 
SIF ORIGIN
	PRINT [1011] 登場作品で絞る
PRINTL 

INPUT
SELECTCASE RESULT
	CASE -1
		SIF 表示上限 > 10
			表示上限 -= 10
	CASE -9
		表示上限 += 10
	CASE 999
		SIF TFLAG:ページ数
			TFLAG:ページ数--
	CASE 1000
		RETURN
	CASE 1001
		SIF 次ページあり
			TFLAG:ページ数++
	CASE 1010
		CALL CHARA_SORT
	CASE 1011
		CALL 登場作品フィルタ設定
		TFLAG:ページ数 = 0
	CASE 0
		ASSI = -1
		RETURN 0
	CASE 0 TO CHARANUM-1
		SIF CFLAG:RESULT:状態 < 2 && !TALENT:RESULT:アニマル
			RESTART
		SIF CHECK_CHILD_CARE(RESULT)
			RESTART
		SIF CFLAG:RESULT:使用不可
			RESTART
		CALL ASSIGN_ASSI, RESULT
		RETURN
ENDSELECT

RESTART

;助手指定用関数
@ASSIGN_ASSI, 助手
#DIM DYNAMIC 助手
ASSI = 助手
CALL KOJO_MESSAGE_EVENT, "助手指定", 助手
;配属してたら自動的に外す
CALL 配属解除, ASSI
;監禁も解除
SIF CFLAG:助手:監禁
	CALL 監禁解除, 助手
;助手にした際の素質変化用関数
CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1
;実績:マタン主人で陥落済みのノクスを助手にする
SIF CHARANAMEF(MASTER) == "マタン・カトルセ" && CHARANAMEF(ASSI) == "ノクス・カトルセ" && CFLAG:ASSI:陥落
	CALL 実績解除, "「姉さんは僕が守る」"

@LIFE_LIST, 表示情報, 表示切り替え, 表示上限 = 10
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC カウント数
#DIM DYNAMIC 未表示キャラ
#DIM DYNAMIC LTARGET
#DIM 表示切り替え
#DIMS DYNAMIC 表示情報
#DIM DYNAMIC 表示上限

DRAWLINE
IF ゲームモード() == "新婚モード"
	PRINTPLAIN □キャラ一覧 
ELSE
	SELECTCASE 表示情報
		CASE "調教対象選択"
			PRINTPLAIN □調教対象変更 
		CASE "助手選択"
			PRINTPLAIN □助手変更 
		CASEELSE
			PRINTPLAIN □キャラ一覧 
	ENDSELECT
ENDIF
PRINTL [-1] 表示上限を減らす [-9] 表示上限を増やす

FOR LCOUNT, 0, CHARANUM
	RESETCOLOR
	;1巡目はMASTERとTARGET及びASSIの能力表示
	IF LCOUNT == 0
		IF 表示情報 == "助手選択"
			PRINTL 　[ 0] 助手無し
		ELSE
			PRINTFORM 　[ 0] %CALLNAME:MASTER%(主人) \@ SEX(MASTER) == 1 ? ♂ # ♀ \@ 
			SIF TALENT:MASTER:ふたなり
				PRINT 双
			PRINTL 
		ENDIF
		;TARGETとASSIはMASTERの下に表示する
		IF TARGET > 0
			;助手の指定なら助手不可能なキャラは除外
			SIF 表示情報 == "助手選択" && CFLAG:状態 < 2 && !TALENT:アニマル
				CONTINUE
			CALL STATUS_BAR, TARGET, 表示情報, 表示切り替え
		ENDIF
		SIF ASSI > 0
			CALL STATUS_BAR, ASSI, 表示情報, 表示切り替え
		DRAWLINE
		CONTINUE
	ENDIF

	;2巡目以降は通常のキャラ表示なので条件を指定
	;助手の指定なら助手不可能なキャラは除外
	SIF 表示情報 == "助手選択" && CFLAG:LCOUNT:状態 < 2 && !TALENT:LCOUNT:アニマル
		CONTINUE
	;登場作品フィルタが設定してあるならそれ以外のキャラは除外
	SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSTR:LCOUNT:登場作品, CSTR:LCOUNT:登場作品２, CSTR:LCOUNT:登場作品３) == 0 && ORIGIN
		CONTINUE
	;キャラを表示上限数表示したら終了
	IF 表示数 >= 表示上限
		未表示キャラ = 1
		BREAK
	ENDIF
	;対象と助手は主人の下に表示されるので除外
	SIF GROUPMATCH(LCOUNT, TARGET, ASSI)
		CONTINUE
	カウント数++
	;ページ数×表示上限まで表示を飛ばす
	SIF TFLAG:ページ数*表示上限 >= カウント数
		CONTINUE
	CALL STATUS_BAR, LCOUNT, 表示情報, 表示切り替え
	表示数++
NEXT
DRAWLINE
PRINTL 　☆：調教中　★：助手　○：助手可　●：元助手 ■：タグ色
PRINTL 　KA：経営モードに配属中　不：使用不可　妊：妊娠育児中　獣：ペット候補　を表しています

;条件を満たしているにも関わらずまだリスト表示されてないキャラが居れば未表示キャラフラグは1になる
RETURN 未表示キャラ

;一行で表示できる内容
@STATUS_BAR, 対象, 表示情報, 表示切り替え
#DIM DYNAMIC 対象
#DIMS DYNAMIC 表示情報
#DIM DYNAMIC 表示切り替え
#DIM DYNAMIC 文字色

;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラは灰色表示
SIF 表示情報 != "通常表示" && CHECK_CHILD_CARE(対象)
	SETCOLORBYNAME gray
;調教対象の指定、もしくは助手の指定なら、使用不可のキャラは灰色表示
SIF 表示情報 != "通常表示" && CFLAG:対象:使用不可
	SETCOLORBYNAME gray

文字色 = GETCOLOR()

SIF CFLAG:対象:タグ色
	SETCOLOR CFLAG:対象:タグ色

IF CFLAG:対象:使用不可
	PRINT 不
ELSEIF TALENT:対象:アニマル
	PRINT 獣
ELSEIF CHECK_CHILD_CARE(対象)
	PRINT 妊
ELSEIF 対象 == TARGET
	PRINT ☆
ELSEIF 対象 == ASSI
	PRINT ★
ELSEIF CSTR:対象:配属 != ""
	PRINT KA
;助手経験あり
ELSEIF ISASSI:対象
	PRINT ●
;助手可能
ELSEIF CFLAG:対象:状態 >= 2
	PRINT ○
ELSEIF CFLAG:対象:タグ色
	PRINT ■
ELSE
	PRINT 　
ENDIF

SETCOLOR 文字色

;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラと使用不可キャラは選べない
IF 表示情報 != "通常表示" && (CHECK_CHILD_CARE(対象) || CFLAG:対象:使用不可)
	PRINTFORM [--] %CALLNAME:対象, 20, LEFT%
ELSE
	PRINTFORM [{対象, 2}] %CALLNAME:対象, 20, LEFT%
ENDIF

;上記のキャラを表示したら詳細情報も表示
;助手選択画面では表示しない
IF 表示情報 != "助手選択"
	;表示切り替え中は経営用の情報表示
	IF 表示切り替え
		PRINT 配属先:
		IF CSTR:対象:配属 != ""
			PRINTFORM %CSTR:対象:配属, 20, LEFT% 
		ELSE
			PRINTFORM %"無し", 20, LEFT% 
		ENDIF
		PRINT モチベーション:
		SELECTCASE CFLAG:対象:モチベーション
			CASE 1 TO 9
				SETCOLORBYNAME gray
				PRINT 皆無
			CASE 10 TO 39
				SETCOLORBYNAME yellow
				PRINT 微妙
			CASE 40 TO 60
				PRINT 普通
			CASE 61 TO 90
				SETCOLORBYNAME cyan
				PRINT 良好
			CASE 91 TO 100
				SETCOLORBYNAME lime
				PRINT 最高
		ENDSELECT
		PRINTFORM {CFLAG:対象:モチベーション, 3}/100 
		RESETCOLOR
	ELSE
		PRINTFORM 体力%BARBASE("体力", 対象, 10), 23, LEFT% 
		PRINTFORM 気力%BARBASE("気力", 対象, 10), 23, LEFT% 
	ENDIF
ENDIF
SELECTCASE SEX(対象)
	CASE 1
		PRINT ♂ 
	CASE 2
		PRINT ♀ 
ENDSELECT
SELECTCASE 表示情報
	CASE "調教対象選択"
		SIF TALENT:対象:処女
			PRINT 処女 
		SIF TALENT:対象:再生処女
			PRINT 再生処女 
		SIF TALENT:対象:童貞
			PRINT 童貞 
		SIF TALENT:対象:恋慕
			PRINT 恋慕 
		SIF TALENT:対象:淫乱
			PRINT 淫乱 
		SIF TALENT:対象:服従
			PRINT 服従 
		SIF TALENT:対象:親愛
			PRINT 親愛 
		SIF TALENT:対象:娼婦
			PRINT 娼婦 
		SIF TALENT:対象:隷属
			PRINT 隷属 
		SIF TALENT:対象:崩壊
			PRINT 崩壊 
		SIF TALENT:対象:ふたなり
			PRINT 双 
		SIF TALENT:対象:リミットバースト
			PRINT 超 
		SIF TALENT:対象:スライム || TALENT:対象:溶ける快
			PRINT 粘 
	CASE "助手選択"
		SELECTCASE SEX(対象)
			CASE 1
				PRINTFORM 技巧Lv{ABL:対象:技巧} ホモっ気Lv{ABL:対象:ホモっ気} ゲイ中毒Lv{ABL:対象:ゲイ中毒} 
			CASE 2
				PRINTFORM 技巧Lv{ABL:対象:技巧} レズっ気Lv{ABL:対象:レズっ気} レズ中毒Lv{ABL:対象:レズ中毒} 
		ENDSELECT
		SIF TALENT:対象:両刀
			PRINT 両刀 
		SIF TALENT:対象:ふたなり
			PRINT ふたなり
	CASEELSE
		PRINT ストレス値:
		CALL ストレス表示, 対象
ENDSELECT
RESETCOLOR
PRINTL 

@CHARA_SORT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 番号
#DIM DYNAMIC 表示数
DRAWLINE
PRINTL □どの要素でソートしますか？
PRINTL 　[1] - 刻印
PRINTL 　[2] - 能力
PRINTL 　[3] - 経験
PRINTL 　[4] - 名前
PRINTL 　[5] - キャラ番号
PRINTL 　[6] - 体力
PRINTL 　[7] - 気力
PRINTL 　[8] - 素質
PRINTL 　[9] - 配属先
SIF !ORIGIN
	PRINTL 　[10] - 解禁日
DRAWLINE
PRINTPLAIN 　CHARA SORT　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			DRAWLINE
			PRINTL □刻印
			FOR LCOUNT, 0, 4
				SIF MARKNAME:LCOUNT != ""
					PRINTFORML 　[{LCOUNT}] - %MARKNAME:LCOUNT%
			NEXT
			DRAWLINE
			PRINTPLAIN 　MARK　
			PRINTL [100] - 戻る
			CALL INPUTF, 0, 1, 2, 3, 100
			番号 = RESULT
			SIF RESULT == 100
				RETURN 0
			PRINTL □並べ替え
			PRINTL 　[0] - 昇順(低い順)
			PRINTL 　[1] - 降順(高い順)
			DRAWLINE
			PRINTPLAIN 　MARK　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTFORMW %MARKNAME:番号%で昇順にソートしました
					SORTCHARA MARK:番号
				CASE 1
					PRINTFORMW %MARKNAME:番号%で降順にソートしました
					SORTCHARA MARK:番号, BACK
			ENDSELECT
			RETURN
		CASE 2
			DRAWLINE
			PRINTL □能力
			FOR LCOUNT, 0, 50
				SIF ABLNAME:LCOUNT == ""
					CONTINUE
				PRINTFORMLC 　[{LCOUNT, 2}] - %ABLNAME:LCOUNT%
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　ABL　
			PRINTL [100] - 戻る
			DO
				INPUT
				番号 = RESULT
				SELECTCASE RESULT
					CASE 0 TO 50
						SIF ABLNAME:RESULT == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順(低い順)
						PRINTL 　[1] - 降順(高い順)
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						CALL INPUTF, 0, 1, 10
						SELECTCASE RESULT
							CASE 0
								PRINTFORMW %ABLNAME:番号%で昇順にソートしました
								SORTCHARA ABL:番号
							CASE 1
								PRINTFORMW %ABLNAME:番号%で降順にソートしました
								SORTCHARA ABL:番号, BACK
						ENDSELECT
						RETURN
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN
		CASE 3
			DRAWLINE
			PRINTL □経験
			FOR LCOUNT, 0, VARSIZE("EXP")
				SIF EXPNAME:LCOUNT == ""
					CONTINUE
				PRINTFORMLC 　[{LCOUNT, 2}] - %EXPNAME:LCOUNT%
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　EXP　
			PRINTFORML [{VARSIZE("EXP")}] - 戻る
			DO
				INPUT
				番号 = RESULT
				SELECTCASE RESULT
					CASE VARSIZE("EXP")
						RETURN
					CASE 0 TO VARSIZE("EXP")-1
						SIF EXPNAME:番号 == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順(低い順)
						PRINTL 　[1] - 降順(高い順)
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						CALL INPUTF, 0, 1, 10
						SELECTCASE RESULT
							CASE 0
								PRINTFORMW %EXPNAME:番号%で昇順にソートしました
								SORTCHARA EXP:番号
							CASE 1
								PRINTFORMW %EXPNAME:番号%で降順にソートしました
								SORTCHARA EXP:番号, BACK
						ENDSELECT
						RETURN
				ENDSELECT
			LOOP 1
		CASE 4
			DRAWLINE
			PRINTL □名前
			PRINTL 　[0] - 昇順(フルネーム)
			PRINTL 　[1] - 降順(フルネーム)
			PRINTL 　[2] - 昇順(呼び名)
			PRINTL 　[3] - 降順(呼び名)
			PRINTL 　[4] - 昇順(アルファベット)
			PRINTL 　[5] - 降順(アルファベット)
			DRAWLINE
			PRINTPLAIN 　NAME　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 2, 3, 4, 5, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW 名前(フルネーム)で昇順にソートしました
					SORTCHARA NAME
				CASE 1
					PRINTW 名前(フルネーム)で降順にソートしました
					SORTCHARA NAME, BACK
				CASE 2
					PRINTW 名前(呼び名)で昇順にソートしました
					SORTCHARA CALLNAME
				CASE 3
					PRINTW 名前(呼び名)で降順にソートしました
					SORTCHARA CALLNAME, BACK
				CASE 4
					PRINTW 名前(アルファベット)で昇順にソートしました
					SORTCHARA CSTR:英語名
				CASE 5
					PRINTW 名前(アルファベット)で昇順にソートしました
					SORTCHARA CSTR:英語名, BACK
			ENDSELECT
			RETURN
		CASE 5
			DRAWLINE
			PRINTL □番号
			PRINTL 　[0] - 昇順(0～)
			PRINTL 　[1] - 降順(～0)
			DRAWLINE
			PRINTPLAIN 　NUMBER　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW キャラ番号で昇順にソートしました
					SORTCHARA
				CASE 1
					PRINTW キャラ番号で降順にソートしました
					SORTCHARA BACK
			ENDSELECT
			RETURN
		CASE 6
			DRAWLINE
			PRINTL □体力
			PRINTL 　[0] - 昇順(低い順)
			PRINTL 　[1] - 降順(高い順)
			DRAWLINE
			PRINTPLAIN 　HEALTH　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW 体力で昇順にソートしました
					SORTCHARA BASE:体力
				CASE 1
					PRINTW 体力で降順にソートしました
					SORTCHARA BASE:体力, BACK
			ENDSELECT
			RETURN
		CASE 7
			DRAWLINE
			PRINTL □気力
			PRINTL 　[0] - 昇順(低い順)
			PRINTL 　[1] - 降順(高い順)
			DRAWLINE
			PRINTPLAIN 　VITAL　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW 気力で昇順にソートしました
					SORTCHARA BASE:気力
				CASE 1
					PRINTW 気力で降順にソートしました
					SORTCHARA BASE:気力, BACK
			ENDSELECT
			RETURN
		CASE 8
			DRAWLINE
			PRINTL □素質
			FOR LCOUNT, 0, VARSIZE("TALENT")
				SIF TALENTNAME:LCOUNT == ""
					CONTINUE
				PRINTFORMLC 　[{LCOUNT, 2}] - %TALENTNAME:LCOUNT%
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　TALENT　
			PRINTFORML [{VARSIZE("TALENT")}] - 戻る
			DO
				INPUT
				番号 = RESULT
				SELECTCASE RESULT
					CASE VARSIZE("TALENT")
						RETURN
					CASE 0 TO VARSIZE("TALENT")-1
						SIF TALENTNAME:番号 == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順(素質の無いキャラ優先)
						PRINTL 　[1] - 降順(素質のあるキャラ優先)
						DRAWLINE
						PRINTPLAIN 　TALENT　
						PRINTL [10] - 戻る
						CALL INPUTF, 0, 1, 10
						SELECTCASE RESULT
							CASE 0
								PRINTFORMW %TALENTNAME:番号%で昇順にソートしました
								SORTCHARA TALENT:番号
							CASE 1
								PRINTFORMW %TALENTNAME:番号%で降順にソートしました
								SORTCHARA TALENT:番号, BACK
						ENDSELECT
						RETURN
				ENDSELECT
			LOOP 1
		CASE 9
			DRAWLINE
			PRINTL □配属先
			PRINTL 　[0] - 昇順(配属されてないキャラ優先)
			PRINTL 　[1] - 降順(配属されてるキャラ優先)
			DRAWLINE
			PRINTPLAIN 　ASSIGNED　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW 配属先で昇順にソートしました
					SORTCHARA CSTR:配属
				CASE 1
					PRINTW 配属先で降順にソートしました
					SORTCHARA CSTR:配属, BACK
			ENDSELECT
			RETURN
		CASE 10
			SIF ORIGIN
				CONTINUE
			FOR LCOUNT, 0, CHARANUM
				ソート用購入日:LCOUNT:0 = BUYY:LCOUNT:0*10000+BUYM:LCOUNT:0*100+BUYD:LCOUNT:0
			NEXT
			PRINTL □解禁日
			PRINTL 　[0] - 昇順(早い順)
			PRINTL 　[1] - 降順(遅い順)
			DRAWLINE
			PRINTPLAIN 　UNLOCK　
			PRINTL [10] - 戻る
			CALL INPUTF, 0, 1, 10
			SELECTCASE RESULT
				CASE 0
					PRINTW 解禁日で昇順にソートしました
					SORTCHARA ソート用購入日:0
				CASE 1
					PRINTW 解禁日で降順にソートしました
					SORTCHARA ソート用購入日:0, BACK
			ENDSELECT
			RETURN
	ENDSELECT
LOOP 1

