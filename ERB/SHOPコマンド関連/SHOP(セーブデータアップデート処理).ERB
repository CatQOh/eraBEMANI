@セーブデータアップデート
#DIM DYNAMIC LCOUNT
#DIM UPDATE
#DIM 互換性警告

;セーブデータIDを入れる
SIF データID == 0
	データID = GETSECOND()
;バージョンを入れる
IF バージョン == 0
	IF LASTLOAD_VERSION == -1
		バージョン = GAMEBASE_VERSION
	ELSE
		バージョン = LASTLOAD_VERSION
	ENDIF
ENDIF

;2021/01/02以前のセーブデータに警告を出す
;起動のたびに出す
SIF 互換性警告 != データID
	互換性警告 = 0
IF !互換性警告 && バージョン <= 2130
	FONTBOLD
	PRINTL ※警告※
	PRINTL このセーブデータは互換性の損なわれているセーブデータです
	PRINTL CSVのチェック、相性や経験人数などのステータス、及び口上表示がおかしいことになっています
	PRINTL ゲームは続行できますが、新規にセーブデータを作成することを強く推奨します
	PRINTW この警告が出るセーブデータは2021年3月以降の更新で互換性が切られる予定です
	FONTREGULAR
	互換性警告 = データID
ENDIF

FOR LCOUNT, 0, CHARANUM
	;以下経営に関するセーブデータのアップデート用
	SIF UPDATE
		BREAK
	;CFLAG:配属をCSTR:配属に移動
	SELECTCASE CFLAG:LCOUNT:配属
		CASE 1
			CSTR:LCOUNT:配属 = ゲームセンター
			CFLAG:LCOUNT:配属 = 0
		CASE 2
			CSTR:LCOUNT:配属 = 娼館
			CFLAG:LCOUNT:配属 = 0
		CASE 4
			CSTR:LCOUNT:配属 = 芸能プロダクション
			CFLAG:LCOUNT:配属 = 0
	ENDSELECT
	;娼館に配属してるキャラで、部屋番号実装前に配属してたキャラの配属を外す
	SIF CSTR:LCOUNT:配属 == "娼館" && CFLAG:LCOUNT:娼館部屋番号 == 0
		CALL 配属解除, LCOUNT
	;モチベーションが設定されてなければ初期値(50)にする
	SIF CFLAG:LCOUNT:モチベーション == 0
		CFLAG:LCOUNT:モチベーション = 50
	;各SAVESTRにアイテム名をセットする
	FOR LCOUNT, 0, 50
		SIF SAVESTR:LCOUNT == ""
			SAVESTR:LCOUNT = %SAVESTRNAME:LCOUNT%
	NEXT
NEXT
UPDATE = 1
