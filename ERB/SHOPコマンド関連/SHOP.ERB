@SHOW_SHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 売却可能
#DIM DYNAMIC 調教可能
VARSET LOCAL, 0

VARLINE = 0

SIF データID == 0
	データID = GETSECOND()

REPEAT CHARANUM
	IF CFLAG:COUNT:グリム
		SELECTCASE FLAG:グリムコンフィグ
			CASE 0
				CALLNAME:COUNT = %CSVCALLNAME(NO:COUNT)%
			CASE 1
				CALLNAME:COUNT = グリム・%CSVCALLNAME(NO:COUNT)%
		ENDSELECT
	ENDIF
	IF TALENT:COUNT:魔術師
		MAXBASE:COUNT:魔力 = ABL:COUNT:魔法技能*1000
		BASE:COUNT:魔力 = LIMIT(BASE:COUNT:魔力, 0, MAXBASE:COUNT:魔力)
	ENDIF
	;CFLAG:配属をCSTR:配属に移動
	SELECTCASE CFLAG:COUNT:配属
		CASE 1
			CSTR:COUNT:配属 = ゲームセンター
			CFLAG:COUNT:配属 = 0
		CASE 2
			CSTR:COUNT:配属 = 娼館
			CFLAG:COUNT:配属 = 0
		CASE 4
			CSTR:COUNT:配属 = 芸能プロダクション
			CFLAG:COUNT:配属 = 0
	ENDSELECT
	;娼館に配属してるキャラで、部屋番号実装前に配属してたキャラの配属を外す
	SIF CSTR:COUNT:配属 == "娼館" && CFLAG:COUNT:娼館部屋番号 == 0
		CALL 配属解除, COUNT
REND

CALL CVAR_RESET

SIF GC店名 == ""
	GC店名 = ゲームセンター

SIF 娼館店名 == ""
	娼館店名 = 娼館

;実績:STRATEGYモードで期限内に100億円稼ぐ
SIF FLAG:ゲームモード == 1 && MONEY >= 10000000000
	CALL 実績解除, "10，000，000，000"

;実績:5000兆円稼ぐ
SIF MONEY >= 5000000000000000
	CALL 実績解除, "5000兆円欲しい！"

;陥落済みなら陥落済みフラグをオン
IF TARGET > 0
	IF FALLTYPE(TARGET) != 0
		CFLAG:陥落 = 1
	ELSE
		CFLAG:陥落 = 0
	ENDIF
ENDIF

SIF 受験段位名 == "魅惑のサバト"
	ITEM:媚薬 = 99

;曜日計算
FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

;祝日・記念日の設定
CALL HOLIDAY

;対象か助手が使用不可の場合は外す
SIF TARGET >= 0 && CFLAG:使用不可
	TARGET = -1
SIF ASSI >= 0 && CFLAG:ASSI:使用不可
	ASSI = -1

;コマンド以外の情報表示は別関数で行う
CALL SHOP_UI

;現助手＋元助手のキャラ数の確認
REPEAT CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:COUNT:状態 == 2
		助手可能++
	;売却可能
	SIF CFLAG:COUNT:状態 >= 1 && !CFLAG:COUNT:売却不可
		売却可能++
REND

;TARGETの他に調教できるキャラがいるかどうか
REPEAT CHARANUM
	;主人は除外
	SIF COUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF COUNT == TARGET
		CONTINUE
	;使用不可は除外
	SIF CFLAG:COUNT:使用不可
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(COUNT)
		CONTINUE
	調教可能++
REND

VARSET LOCALS

;コマンド名は現在全角4文字の範囲で統一
;100番台は行動のコマンド
LOCALS = 　
;TARGETがいれば表示
IF TARGET > 0
	SELECTCASE FALLTYPE(TARGET)
		CASE 1
			LOCALS:1 = %SET_TOOLTIP("[100] - 愛を育む ", @"%CALLNAME:TARGET%と幸せな時間を過ごします")%
		CASE 2
			LOCALS:1 = %SET_TOOLTIP("[100] - 乱れ合う ", @"%CALLNAME:TARGET%と互いの欲望を満たし合います")%
		CASE 3
			LOCALS:1 = %SET_TOOLTIP("[100] - 躾をする ", @"%CALLNAME:TARGET%に権威を示します")%
		CASEELSE
			LOCALS:1 = %SET_TOOLTIP("[100] - 調教開始 ", @"%CALLNAME:TARGET%を調教します")%
	ENDSELECT
ELSE
	SETCOLORBYNAME gray
	IF CHARANUM == 1 && FLAG:ゲームモード == 8
		LOCALS:1 = %SET_TOOLTIP("[---] - 調教開始 ", "調教可能なキャラが居ません", 0)%
	ELSEIF FLAG:ゲームモード == 7
		LOCALS:1 = %SET_TOOLTIP("[---] - 愛を育む ", "[202] - 対象変更 から対象を選択してください", 0)%
	ELSEIF 調教可能
		LOCALS:1 = %SET_TOOLTIP("[---] - 調教開始 ", "[202] - 奴隷変更 から調教対象を選択してください", 0)%
	ELSE
		LOCALS:1 = %SET_TOOLTIP("[---] - 調教開始 ", "[302] - 奴隷購入 からキャラを購入してください", 0)%
	ENDIF
	RESETCOLOR
ENDIF
;無条件で表示
LOCALS:2 = %SET_TOOLTIP("[101] - 休憩     ", @"\@ TIME == 0 ? 夜まで休憩します # 翌日まで休憩します \@")%

;無条件で表示
LOCALS:3 = %SET_TOOLTIP("[102] - 眷属の儀 ", "奴隷の種族を変更するための儀式を行います")%

;無条件で表示
LOCALS:3 = %SET_TOOLTIP("[103] - 施設管理 ", "屋敷の施設・設備の管理をします")%
;LOCALS:3 = %SET_TOOLTIP("[---] - 施設管理 ", "調整中", 0, "gray")%

;STRATEGYモード、PFREEモードなら表示
SELECTCASE FLAG:ゲームモード
	CASE 1, 9
		LOCALS:5 = %SET_TOOLTIP("[104] - 経営     ", "収益要素を管理します")%
		;パートナーに話しかける口上が実装されていれば表示
		TRYCCALLFORM EXIST_STRATEGY_KOJO_K{FLAG:パートナー}
			SIF FLAG:口上表示
				LOCALS:6 = %SET_TOOLTIP("[105] - 経営相談 ", @"%CSVCALLNAMEF(FLAG:パートナー)%に経営の相談をします")%
		CATCH
		ENDCATCH
ENDSELECT

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5+LOCALS:6+LOCALS:7

;200番台は奴隷関連のコマンド
VARSET LOCALS
LOCALS = 　
;調教者含め誰かがいれば表示 現在は必ず表示される
LOCALS:1 = %SET_TOOLTIP("[200] - 能力表示 ", @"キャラの様々な情報を表示します")%
;奴隷が一人でもいれば表示
IF CHARANUM > 1
	LOCALS:2 = %SET_TOOLTIP("[201] - 能力上昇 ", @"キャラの能力を上昇させます")%
ELSEIF FLAG:ゲームモード != 8
	LOCALS:2 = %SET_TOOLTIP("[---] - 能力上昇 ", "[302] - 奴隷購入 からキャラを購入してください", 0, "gray")%
ELSE
	LOCALS:2 = %SET_TOOLTIP("[---] - 能力上昇 ", "調教可能なキャラが居ません", 0, "gray")%
ENDIF
IF 調教可能
	IF FLAG:ゲームモード == 7
		LOCALS:3 = %SET_TOOLTIP("[202] - 対象変更 ", "お相手を変更します")%
	ELSE
		LOCALS:3 = %SET_TOOLTIP("[202] - 奴隷変更 ", "調教対象を変更します")%
	ENDIF
ELSE
	IF FLAG:ゲームモード == 7
		LOCALS:3 = %SET_TOOLTIP("[---] - 対象変更 ", @"%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%以外にキャラが居ません", 0, "gray")%
	ELSEIF CHARANUM == 1 && FLAG:ゲームモード == 8
		LOCALS:3 = %SET_TOOLTIP("[---] - 奴隷変更 ", "調教可能なキャラが居ません", 0, "gray")%
	ELSEIF CHARANUM != 1 && TARGET >= 0
		LOCALS:3 = %SET_TOOLTIP("[---] - 奴隷変更 ", @"%CALLNAME:TARGET%以外に調教可能なキャラが居ません", 0, "gray")%
	ELSE
		LOCALS:3 = %SET_TOOLTIP("[---] - 奴隷変更 ", "[302] - 奴隷購入 からキャラを購入してください", 0, "gray")%
	ENDIF
ENDIF
;助手可能キャラが一人でもいれば表示
IF 助手可能
	IF ASSI > 0
		LOCALS:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を変更します")%
	ELSE
		LOCALS:4 = %SET_TOOLTIP("[203] - 助手変更 ", "助手を選出します")%
	ENDIF
ELSE
	LOCALS:4 = %SET_TOOLTIP("[---] - 助手変更 ", "助手に選べるキャラは居ません", 0, "gray")%
ENDIF
;魔法
IF TALENT:MASTER:魔術師 || (TARGET > 0 && TALENT:魔術師) || (ASSI > 0 && TALENT:ASSI:魔術師)
	LOCALS:5 = %SET_TOOLTIP("[204] - 魔法詠唱 ", "魔法を使用します")%
ELSE
	LOCALS:5 = %SET_TOOLTIP("[---] - 魔法詠唱 ", "魔法を使用するには主人、助手、もしくは調教対象が魔術師である必要があります", 0, "gray")%
ENDIF

;主人が禁断の知識持ってる場合
IF TALENT:MASTER:禁断の知識
	IF CHARANUM > 1
		LOCALS:6 = %SET_TOOLTIP("[205] - 身体改造 ", "キャラの体に改造を施します")%
	ELSE
		LOCALS:6 = %SET_TOOLTIP("[---] - 身体改造 ", "改造可能なキャラが居ません", 0, "gray")%
	ENDIF
ENDIF

;新婚モードの一転攻勢
IF FLAG:ゲームモード == 7
	IF TALENT:FINDCHARA(CFLAG:嫁, 1):妊娠 || TALENT:FINDCHARA(CFLAG:嫁, 1):育児中
		LOCALS:7 = %SET_TOOLTIP("[---] - 一転攻勢 ", @"%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%が身篭っているため、できません", 0, "gray")%
	ELSE
		LOCALS:7 = %SET_TOOLTIP("[206] - 一転攻勢 ", @"%CALLNAME:MASTER%と%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%の受け攻めを変えます")%
	ENDIF
ENDIF

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4+LOCALS:5+LOCALS:6+LOCALS:7

VARSET LOCALS
;300番台は購入・売却のコマンド
LOCALS = 　
;無条件で表示
LOCALS:1 = %SET_TOOLTIP("[300] - アイテム ", "淫具や素質追加などのアイテムを購入します")%


;変化できるキャラが居れば表示
SIF CMATCH(TALENT:変化, 1)
	LOCALS:2 = %SET_TOOLTIP("[301] - 変化     ", "素質[変化]を持った陥落済みのキャラクターを動物の姿や人間の姿に変化させます")%

;段位認定でなければ表示
IF FLAG:ゲームモード == 8
	LOCALS:3 = %SET_TOOLTIP("[---] - 奴隷購入 ", "段位認定では奴隷を購入することができません")%
ELSE
	LOCALS:3 = %SET_TOOLTIP("[302] - 奴隷購入 ", "調教するための奴隷を購入します")%
ENDIF
;売却できるキャラがいれば表示
IF 売却可能
	LOCALS:4 = %SET_TOOLTIP("[303] - 売却     ", "調教で仕上がった奴隷を売却します")%
ELSE
	LOCALS:4 = %SET_TOOLTIP("[---] - 売却     ", "売却可能な奴隷が居ません", 0, "gray")%
ENDIF

;新婚モードではアイテム購入のみ表示
IF FLAG:ゲームモード == 7
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2
ELSE
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4
ENDIF

VARSET LOCALS
;400番台はセーブ・ロード・オプションのシステムコマンド
;セーブ・ロード・オプションは無条件で表示
LOCALS = 　
LOCALS:2 = %SET_TOOLTIP("[401] - LOAD     ", "セーブデータをロードします。保存していない進行状況は失われます")%
LOCALS:3 = %SET_TOOLTIP("[402] - OPTION   ", "服装やテキスト表示など、様々な設定を変更します")%

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3

;500番台は周回コマンド
;所持金が条件に達していて、ゲームモードがSTANDARDもしくはSTRATEGYなら表示
LOCALS = 　
SELECTCASE FLAG:ゲームモード
	;STANDARD
	CASE 0
		IF MONEY < 1000000
			LOCALS:1 = %SET_TOOLTIP("[---] - 納金     ", "1,000,000の所持金が必要です", 0, "gray")%
		ELSE
			LOCALS:1 = %SET_TOOLTIP("[500] - 納金     ", "1,000,000の所持金を納め、ゲームをクリアします")%
		ENDIF
	;STRATEGY
	CASE 1
		IF MONEY < 100000000
			LOCALS:1 = %SET_TOOLTIP("[---] - 納金     ", "100,000,000の所持金が必要です", 0, "gray")%
		ELSE
			LOCALS:1 = %SET_TOOLTIP("[500] - 納金     ", "100,000,000の所持金を納め、ゲームをクリアします")%
		ENDIF
	;新婚
	CASE 7
		LOCALS:1 = %SET_TOOLTIP("[---] - 納金     ", "新婚モードでは納金の必要はありません", 0, "gray")%
	;段位認定
	CASE 8
		LOCALS:1 = %SET_TOOLTIP("[---] - 納金     ", "段位認定では納金の必要はありません", 0, "gray")%
	;PFREE
	CASE 9
		LOCALS:1 = %SET_TOOLTIP("[---] - 納金     ", "PREMIUM FREE MODEでは納金の必要はありません", 0, "gray")%
ENDSELECT
;周回可能なら表示
IF FLAG:周回可能
	LOCALS:2 = %SET_TOOLTIP("[501] - 周回     ", "この周を終了し、引き継いでニューゲームを開始します")%
ELSEIF FLAG:ゲームモード == 7
	LOCALS:2 = %SET_TOOLTIP("[---] - 周回     ", "新婚モードでは周回出来ません", 0, "gray")%
ELSEIF FLAG:ゲームモード == 8
	LOCALS:2 = %SET_TOOLTIP("[---] - 周回     ", "段位認定では周回出来ません", 0, "gray")%
ELSEIF FLAG:ゲームモード == 9
	LOCALS:2 = %SET_TOOLTIP("[---] - 周回     ", "PREMIUM FREE MODEでは周回出来ません", 0, "gray")%
ELSE
	LOCALS:2 = %SET_TOOLTIP("[---] - 周回     ", "ゲームをクリアしないと周回は出来ません", 0, "gray")%
ENDIF

HTML_PRINT LOCALS+LOCALS:1+LOCALS:2

;デバッグモードなら表示
;#;PRINTPLAIN 　
;#;PRINTL [998] - デバッグ

DRAWLINE
PRINTPLAIN 　SHOP　

@USERSHOP
#DIM DYNAMIC 助手可能
#DIM DYNAMIC 売却可能
#DIM DYNAMIC 調教可能
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
VARSET LOCAL, 0
;現助手＋元助手のキャラ数の確認
FOR LCOUNT, 0, CHARANUM
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;助手可能
	SIF CFLAG:LCOUNT:状態 == 2
		助手可能++
	;売却可能
	SIF CFLAG:LCOUNT:状態 >= 1
		売却可能++
NEXT

;TARGETの他に調教できるキャラがいるかどうか
FOR LCOUNT, 0, CHARANUM
	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;奴隷も除外
	SIF LCOUNT == TARGET
		CONTINUE
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	調教可能++
NEXT

TFLAG:ページ数 = 0

SELECTCASE RESULT
	;調教開始
	CASE 100
		SIF TARGET > 0
			BEGIN TRAIN
	;休憩
	CASE 101
		;休憩フラグを立てる
		FLAG:休憩フラグ = 1
		BEGIN TURNEND
	;眷属の儀
	CASE 102
		CALL FAMILIAR
		;儀を行った場合はターン終了
		IF RESULT == 1
			FLAG:休憩フラグ = 1
			BEGIN TURNEND
		ENDIF
	;施設管理
	CASE 103
		CALL 施設
	;経営
	CASE 104
		SELECTCASE FLAG:ゲームモード
			CASE 1, 9
				CALL STRATEGY
		ENDSELECT
	;経営相談
	CASE 105
		LTARGET = TARGET
		TARGET = GETCHARA(FLAG:パートナー)
		SIF FLAG:口上表示
			TRYCALLFORM TALK_PARTNER_K{FLAG:パートナー}
		TARGET = LTARGET
	;能力表示
	CASE 200
		CALL SHOW_CHARADATA
	;能力上昇
	CASE 201
		SIF CHARANUM > 1
			CALL SHOP_ABLUP
	;奴隷変更
	CASE 202
		SIF 調教可能
			CALL CHANGE_TARGET
	;助手変更
	CASE 203
		SIF 助手可能
			CALL SELECT_ASSI
	;魔法詠唱
	CASE 204
		SIF TALENT:MASTER:魔術師 || (TARGET > 0 && TALENT:魔術師) || (ASSI > 0 && TALENT:ASSI:魔術師)
			CALL SPELLCAST
		;魔法を詠唱したならターン終了
		IF RESULT == 1
			FLAG:休憩フラグ = 1
			BEGIN TURNEND
		ENDIF
	;身体改造
	CASE 205
		SIF TALENT:MASTER:禁断の知識 && CHARANUM > 1
			CALL BODY_MOD
	;一転攻勢
	CASE 206
		IF FLAG:ゲームモード == 7
			LOCAL = FINDCHARA(CFLAG:嫁, 1)
			IF !TALENT:LOCAL:妊娠 && !TALENT:LOCAL:育児中
				TALENT:MASTER:親愛 = 1
				TALENT:LOCAL:親愛 = 0
				CFLAG:MASTER:嫁 = 1
				CFLAG:LOCAL:嫁 = 0
				SWAPCHARA LOCAL, MASTER
			ENDIF
		ENDIF
	;アイテム
	CASE 300
		IF FLAG:ゲームモード == 8
			CALL GORILLA_ITEM
		ELSE
			CALL SHOPITEM_BUY
		ENDIF
	;変化
	CASE 301
		SIF CMATCH(TALENT:変化, 1)
			CALL 変化
	;奴隷購入
	CASE 302
		SELECTCASE FLAG:ゲームモード
			CASE IS != 7, 8
				CALL CHARA_BUY
		ENDSELECT
	;売却
	CASE 303
		SELECTCASE FLAG:ゲームモード
			CASE IS != 7
				SIF 売却可能
					CALL CHARA_SELL
		ENDSELECT
	;セーブ
	CASE 400
		CALL USERSAVE
	;ロード
	CASE 401
		CALL USERLOAD
	;オプション
	CASE 402
		CALL OPTION
	;納金
	CASE 500
		SELECTCASE FLAG:ゲームモード
			CASE 0
				SIF MONEY >= 1000000
					CALL ENDING_CHECK
			CASE 1
				IF MONEY >= 100000000
					;実績:キャラを一人も購入せずにパートナーだけでSTRATEGYモードをクリアする
					SIF キャラ未購入
						CALL 実績解除, "もう全部あいつ一人でいいんじゃないかな"
					;実績:芸能プロダクションの収入のみでSTRATEGYモードをクリアする
					SIF バンめし
						CALL 実績解除, "他人がバンドで稼いだ金でめしが食いたい"
					CALL ENDING_CHECK
				ENDIF
		ENDSELECT
	;周回
	CASE 501
		SIF FLAG:周回可能
			CALL SUCCESSION
	;コナミコマンド
	CASE 573
		PRINTW このコマンドはもう意味無いよ
		CALL 実績解除, "いつのバージョンだよ"
	;ゲーセン営業変更
	CASE 600
		IF FLAG:ゲーセン営業中 > 0
			PRINTFORMW %GC店名%の経営を一時中断しました
			FLAG:ゲーセン営業中 = -1
		ELSE
			PRINTFORMW %GC店名%の経営を開始しました
			FLAG:ゲーセン営業中 = DAY
		ENDIF
	;娼館営業変更
	CASE 601
		IF FLAG:娼館営業中 > 0
			PRINTFORMW %娼館店名%の経営を一時中断しました
			;REPEAT CHARANUM
			;	SIF CSTR:COUNT:配属 == "娼館"
			;		CFLAG:COUNT:配属完了 = 0
			;REND
			FLAG:娼館営業中 = -1
		ELSE
			PRINTFORMW %娼館店名%の経営を開始しました
			FLAG:娼館営業中 = DAY
		ENDIF
	;デバッグ用
	;#;CASE 998
	;#;	CALL DEBUG
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE {RESULT}に対応するコマンドはありません
ENDSELECT

RETURN 0

@SHOP_UI
;ゲーム内で表示されるショップ画面
;□XXXX年XX月XX日(曜日) X日目(昼夜) X周目 MODE:XXXXX
;　天気:XX 気温:XX.X℃ XXX(月齢.XXX) 
;　所持金:$XXXX 昨日の所持金:$XXXX XXXX 変動価格
;　難易度:XXXX
;　総奴隷数:X/陥落済み:X
;
;□調教中
;　No.X XXXXXX(♂♀)[ふたなり]【陥落素質】(依存度 ストレス値)
;　従順:LVXX 欲望:LVXX 技巧:LVXX [BURST]
;　Ｃ感:LVXX Ｖ感:LVXX Ａ感:LVXX Ｂ感:LVXX [部位陥落素質]
;　苦痛刻印:LVX [XXX]　快楽刻印:LVX [XXX]　屈服刻印:LVX [XXX]　反発刻印:LVX [XXX]
;　体力[********************************](XXXX/XXXX)[排卵誘発中]
;　気力[********************************](XXXX/XXXX)
;
;　保管済み母乳:Xml　保管済み精液:Xml
;　主人:XXX
;　助手:XXX
;-------------------------------------------------------------------------------------
;□ゲームセンターの状況
;　営業中or休業中 [600]営業を開始するor休業する
;　前日の収入:($XXX) 店長:XXXX
;-------------------------------------------------------------------------------------
;□娼館の状況
;　営業中or休業中 [601]営業を開始するor休業する
;　前日の収入:($XXX)
;

;日数表示
DRAWLINE
PRINT □
IF YEAR > 0
	PRINTFORM {YEAR}
ELSE
	PRINTFORM BC{ABS(YEAR)+1}
ENDIF
PRINTFORM 年{MONTH}月{DAYS}日(%DOW(FLAG:曜日)%) 

REPEAT 5
	SIF HOLIDAY:COUNT == ""
		BREAK
	PRINTFORM %HOLIDAY:COUNT% 
REND 

PRINTFORM {DAY}日目

IF TIME == 0
	PRINT (昼) 
ELSE
	PRINT (夜) 
ENDIF

SIF FLAG:周回数 > 1
	PRINTFORM  {FLAG:周回数}周目
SELECTCASE FLAG:ゲームモード
	CASE 0
		PRINT  MODE:STANDARD
	CASE 1
		PRINT  MODE:STRATEGY
	CASE 7
		PRINT  MODE:新婚生活
	CASE 8
		PRINT  MODE:段位認定 
		CALL GRADE, FLAG:選択段位
	CASE 9
		PRINT  MODE:PREMIUM FREE
ENDSELECT
PRINTL 

PRINT 　天気:
SELECTCASE FLAG:天気
	CASE 1
		PRINT 晴れ
	CASE 2
		PRINT 曇り
	CASE 3
		PRINT 雨
	CASE 4
		PRINT 大雨
	CASE 5
		PRINT 雷雨
	CASE 6
		PRINT 雪
	CASE 7
		PRINT 大雪
ENDSELECT

PRINTFORM  気温:{FLAG:気温/10}.{ABS(FLAG:気温)%10}℃ 

SELECTCASE 月齢
	CASE 0 TO 199
		PRINT 新月
	CASE 200 TO 299
		PRINT 繊月
	CASE 300 TO 699
		PRINT 三日月
	CASE 700 TO 999
		PRINT 上弦の月
	CASE 1000 TO 1399
		PRINT 十日夜
	CASE 1400 TO 1499
		PRINT 小望月
	CASE 1500 TO 1599
		PRINT 満月
	CASE 1600 TO 1699
		PRINT 十六夜
	CASE 1700 TO 1799 
		PRINT 立待月
	CASE 1800 TO 1899
		PRINT 居待月
	CASE 1900 TO 1999
		PRINT 寝待月
	CASE 2000 TO 2299
		PRINT 二十日月
	CASE 2300 TO 2599
		PRINT 下弦の月
	CASE 2600 TO 2899
		PRINT 暁月
	CASE 2900 TO 3000
		PRINT 三十日月
ENDSELECT

PRINTFORM (月齢:{月齢/100}.\@月齢%100 < 10 ? 0 # \@{月齢%100})
PRINTL 

;段位認定のときのみ表示を変える
IF FLAG:ゲームモード != 8
	;所持金表示
	CALL PASELI, @"　所持金:{MONEY}＄"
	SIF 借金 > 0
		CALL PASELI, @" 借金:{借金}＄"
	CALL PASELI, @" 昨日の所持金:{LASTMONEY}＄"
	IF MONEY == LASTMONEY
		PRINTL →0 KEEP
	ELSEIF MONEY > LASTMONEY
		PRINTFORML ↑{MONEY-LASTMONEY} UP!
	ELSEIF MONEY < LASTMONEY
		PRINTFORML ↓{LASTMONEY-MONEY} DOWN...
	ENDIF

	;新婚生活中は非表示
	IF FLAG:ゲームモード != 7
		;難易度表示
		PRINT 　難易度:
		IF QMA
			SELECTCASE FLAG:難易度
				CASE 1
					PRINTL フェアリー級
				CASE 2
					PRINTL ミノフェニ級
				CASE 3
					PRINTL ドラゴン級
			ENDSELECT
		ELSE
			SELECTCASE FLAG:難易度
				CASE 1
					PRINTL NORMAL
				CASE 2
					PRINTL HYPER
				CASE 3
					PRINTL ANOTHER
			ENDSELECT
		ENDIF

		;奴隷人数表示
		LOCAL = 0
		;陥落人数計算
		REPEAT CHARANUM
			SIF CFLAG:COUNT:陥落
				LOCAL++
		REND
		PRINTFORML 　総奴隷数:{CHARANUM-1}\/陥落済み:{LOCAL}
	ENDIF
ELSE
	;所持金表示
	CALL PASELI, @"　所持金:{MONEY}＄"
	PRINTL 

	;難易度表示
	PRINT 　難易度:
	IF QMA
		SELECTCASE FLAG:難易度
			CASE 1
				PRINTL フェアリー級 
			CASE 2
				PRINTL ミノフェニ級 
			CASE 3
				PRINTL ドラゴン級 
		ENDSELECT
	ELSE
		SELECTCASE FLAG:難易度
			CASE 1
				PRINT NORMAL 
			CASE 2
				PRINT HYPER 
			CASE 3
				PRINT ANOTHER 
		ENDSELECT
	ENDIF

	;奴隷人数表示
	LOCAL = 0
	;売却可能人数計算
	REPEAT CHARANUM
		SIF CFLAG:COUNT:状態
			LOCAL++
	REND
	PRINTFORML 総奴隷数:{CHARANUM-1}\/売却可能:{LOCAL}
	
	PRINT 　
	CALL 段位ゲージ表示
ENDIF

;奴隷の状態
IF TARGET > 0
	PRINTL 
	IF FLAG:ゲームモード == 7
		PRINTL □同棲中
		PRINTFORM 　%NAME:TARGET%
	ELSE
		PRINTL □調教中
		PRINTFORM 　No.{TARGET} %NAME:TARGET%
	ENDIF
	IF TALENT:性転換済
		SELECTCASE SEX(TARGET)
			CASE 1
				PRINT (元♀の♂)
			CASE 2
				PRINT (元♂の♀)
		ENDSELECT
	ELSE
		SELECTCASE SEX(TARGET)
			CASE 1
				PRINT (♂)
			CASE 2
				PRINT (♀)
		ENDSELECT
	ENDIF
	SIF TALENT:ふたなり
		PRINT [ふたなり]
	SIF TALENT:アニマル
		PRINT [獣]
	SIF TALENT:妊娠
		PRINT 【妊娠中】
	SIF TALENT:恋慕
		PRINT 【恋慕】
	SIF TALENT:淫乱
		PRINT 【淫乱】
	SIF TALENT:服従
		PRINT 【服従】
	SIF TALENT:親愛
		PRINT 【親愛】
	SIF TALENT:娼婦
		PRINT 【娼婦】
	SIF TALENT:隷属
		PRINT 【隷属】
	SIF TALENT:崩壊
		PRINT 【崩壊】
	SIF TALENT:リミットバースト
		PRINT 【BURST】
	IF TALENT:淫核
		PRINT 【淫核】
	ELSEIF TALENT:淫膣
		PRINT 【淫膣】
	ELSEIF TALENT:淫尻
		PRINT 【淫尻】
	ELSEIF TALENT:淫乳
		PRINT 【淫乳】
	ENDIF
	SIF TALENT:MASTER:孔雀の瞳 || (ASSI > 0 && TALENT:ASSI:孔雀の瞳)
		PRINTFORM (依存度:{CFLAG:依存度} ストレス値{CFLAG:ストレス値})
	PRINTL 
	VARSET LOCALS
	
	LOCALS = %SET_TOOLTIP(@"　従順:LV{ABL:従順, 2, LEFT}", ABL_COMMENT("従順"))%
	LOCALS:1 = %SET_TOOLTIP(@"欲望:LV{ABL:欲望, 2, LEFT}", ABL_COMMENT("欲望"))%
	LOCALS:2 = %SET_TOOLTIP(@"技巧:LV{ABL:技巧, 2, LEFT}", ABL_COMMENT("技巧"))%
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2
	;PRINTFORM 　従順:LV{ABL:従順, 2, LEFT} 欲望:LV{ABL:欲望, 2, LEFT} 技巧:LV{ABL:技巧, 2, LEFT}

	VARSET LOCALS
	LOCALS = %SET_TOOLTIP(@"　Ｃ感:LV{ABL:Ｃ感覚, 2, LEFT}", ABL_COMMENT("Ｃ感覚"))%
	SIF VAGINA(TARGET)
		LOCALS:1 = %SET_TOOLTIP(@"Ｖ感:LV{ABL:Ｖ感覚, 2, LEFT}", ABL_COMMENT("Ｖ感覚"))%
	LOCALS:2 = %SET_TOOLTIP(@"Ａ感:LV{ABL:Ａ感覚, 2, LEFT}", ABL_COMMENT("Ａ感覚"))%
	LOCALS:3 = %SET_TOOLTIP(@"Ｂ感:LV{ABL:Ｂ感覚, 2, LEFT}", ABL_COMMENT("Ｂ感覚"))%
	HTML_PRINT LOCALS+LOCALS:1+LOCALS:2+LOCALS:3
	;PRINTFORM 　Ｃ感:LV{ABL:Ｃ感覚, 2, LEFT} 
	;SIF SEX(TARGET) == 2 || TALENT:ふたなり
	;	PRINTFORM Ｖ感:LV{ABL:Ｖ感覚, 2, LEFT} 
	;PRINTFORM Ａ感:LV{ABL:Ａ感覚, 2, LEFT} Ｂ感:LV{ABL:Ｂ感覚, 2, LEFT}
	;PRINTL 
	
	CALL SHOW_INFO_MARK, TARGET
	PRINTFORML 　体力%BARBASE("体力")%
	PRINTFORML 　気力%BARBASE("気力")%
	SIF TALENT:魔術師
		PRINTFORML 　魔力%BARBASE("魔力")%
ENDIF
PRINTL 
PRINTFORML 　保管済み母乳:{FLAG:保管済み母乳}ml　保管済み精液:{FLAG:保管済み精液}ml

PRINTFORM 　主人:%NAME:MASTER%
SELECTCASE SEX(MASTER)
	CASE 1
		PRINT (♂)
	CASE 2
		PRINT (♀)
ENDSELECT
SIF TALENT:MASTER:ふたなり
	PRINT [ふたなり]
PRINTL 
SIF TALENT:MASTER:魔術師
	PRINTFORML 　魔力%BARBASE("魔力", MASTER)%

IF ASSI >= 0
	IF TALENT:ASSI:アニマル
		PRINTFORM 　ペット:%NAME:ASSI%
	ELSE
		PRINTFORM 　助手:%NAME:ASSI%
	ENDIF
	SELECTCASE SEX(ASSI)
		CASE 1
			PRINT (♂)
		CASE 2
			PRINT (♀)
	ENDSELECT
	SIF TALENT:ASSI:ふたなり
		PRINT [ふたなり]
	SIF TALENT:ASSI:アニマル
		PRINT [獣]
	PRINTL 
	SIF TALENT:ASSI:魔術師
		PRINTFORML 　魔力%BARBASE("魔力", ASSI)%
ENDIF

DRAWLINE
IF GC物件
	LOCAL = 0
	PRINTFORML □%GC店名%の状況
	IF FLAG:ゲーセン営業中 > 0
		PRINTPLAIN 　営業中 
		PRINTL [600] - 一時休業する
		LOCAL = FINDCHARA(CFLAG:ゲーセン店長, 1)
		SIF LOCAL == -1
			LOCAL = 0
		CALL PASELIL, @"　前日の収入:{基本収入}＄ 店長:%NAME:LOCAL%"
	ELSE
		PRINTPLAIN 　休業中 
		PRINTL [600] - 営業を開始する
	ENDIF
	DRAWLINE
ENDIF

IF ITEM:風俗店営業許可証
	PRINTFORML □%娼館店名%の状況
	IF FLAG:娼館営業中 > 0
		PRINTPLAIN 　営業中 
		PRINTL [601] - 一時休業する
		CALL PASELIL, @"　前日の収入:{娼館前日の収入}＄"
	ELSE
		PRINTPLAIN 　休業中 
		PRINTL [601] - 営業を開始する
	ENDIF
	DRAWLINE
ENDIF

LOCAL = 0
REPEAT 10000
	SIF !EXISTCSV(COUNT)
		CONTINUE
	IF (CSVCFLAG(COUNT, GETNUM(CFLAG, "誕生日"))/100) == MONTH && (CSVCFLAG(COUNT, 0)%100) == DAYS
		PRINTFORML 　今日は%CSVNAMEF(COUNT)%の誕生日です
		LOCAL++
	ENDIF
REND
IF LOCAL
	PRINTL 　ハッピーバースデー！
	DRAWLINE
ENDIF
LOCAL = 0
PRINTL □所持アイテム
CALL ITEMPRINT
DRAWLINE

@変化
#DIM DYNAMIC LCOUNT
DRAWLINE
PRINTL □変化させるキャラを選んでください
FOR LCOUNT, 1, CHARANUM
	IF TALENT:LCOUNT:変化
		IF CFLAG:LCOUNT:陥落
			PRINTFORML [{LCOUNT, 2}] %NAME:LCOUNT% 現在：\@ TALENT:LCOUNT:アニマル ? 獣の姿 # 人間の姿 \@
		ELSE
			SETCOLORBYNAME gray
			PRINTFORML [--] %NAME:LCOUNT% 未陥落
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　変化　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASEELSE
			IF CFLAG:RESULT:陥落 && TALENT:RESULT:変化
				LOCAL = RESULT
				IF TALENT:RESULT:アニマル
					;着衣フラグのリセット
					FOR LCOUNT, 40, 47
						CFLAG:LOCAL:LCOUNT = CSVCFLAG(NO:LOCAL, LCOUNT)
					NEXT
					FOR LCOUNT, 100, 107
						CSTR:LOCAL:LCOUNT = %CSVCSTR(NO:LOCAL, LCOUNT)%
					NEXT
					PRINTFORMW %CALLNAME:RESULT%は人間の姿に変化した
					TALENT:RESULT:アニマル = 0
					IF !TRANCE:LOCAL:GETNUM(TALENT, "動物耳")
						TALENT:LOCAL:動物耳 = 0
						TRANCE:LOCAL:GETNUM(TALENT, "動物耳") = 0
					ENDIF
					IF !TRANCE:LOCAL:GETNUM(TALENT, "尻尾")
						TALENT:LOCAL:尻尾 = 0
						TRANCE:LOCAL:GETNUM(TALENT, "尻尾") = 0
					ENDIF
					;茶倉だけ特殊で機械処理なので人間に戻す
					IF CHARANAMEF(LOCAL) == "茶倉"
						IF TRANCE:LOCAL:GETNUM(TALENT, "無機物")
							TALENT:LOCAL:無機物 = 0
							CSTR:LOCAL:種族 = 人間
							NAME:LOCAL = 茶倉
							CALLNAME:LOCAL = 茶倉
							TRANCE:LOCAL:GETNUM(TALENT, "無機物") = 0
						ENDIF
					ELSE
						IF TRANCE:LOCAL:GETNUM(TALENT, "ファーリー")
							TALENT:LOCAL:ファーリー = 0
							TRANCE:LOCAL:GETNUM(TALENT, "ファーリー") = 0
						ENDIF
					ENDIF
					TALENT:LOCAL:バストサイズ = TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ")
					TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ") = 0
				ELSE
					PRINTFORMW %CALLNAME:RESULT%は獣の姿に変化した
					;着衣フラグの変更
					FOR LCOUNT, 40, 47
						CFLAG:LOCAL:LCOUNT = 100
					NEXT
					FOR LCOUNT, 100, 107
						CSTR:LOCAL:LCOUNT = 
					NEXT
					TALENT:LOCAL:アニマル = 1
					IF !TALENT:LOCAL:動物耳
						TALENT:LOCAL:動物耳 = 1
						TRANCE:LOCAL:GETNUM(TALENT, "動物耳") = 1
					ENDIF
					IF !TALENT:LOCAL:尻尾
						TALENT:LOCAL:尻尾 = 1
						TRANCE:LOCAL:GETNUM(TALENT, "尻尾") = 1
					ENDIF
					;茶倉だけ特殊で機械（戌）になる
					IF CHARANAMEF(LOCAL) == "茶倉"
						IF !TALENT:LOCAL:無機物
							TALENT:LOCAL:無機物 = 1
							CSTR:LOCAL:種族 = 機械
							NAME:LOCAL = 戌
							CALLNAME:LOCAL = 戌
							TRANCE:LOCAL:GETNUM(TALENT, "無機物") = 1
						ENDIF
					ELSE
						IF !TALENT:LOCAL:ファーリー
							TALENT:LOCAL:ファーリー = 1
							TRANCE:LOCAL:GETNUM(TALENT, "ファーリー") = 1
						ENDIF
					ENDIF
					TRANCE:LOCAL:GETNUM(TALENT, "バストサイズ") = TALENT:LOCAL:バストサイズ
					TALENT:LOCAL:バストサイズ = 0
				ENDIF
			ENDIF
			RESTART
	ENDSELECT
LOOP 1

