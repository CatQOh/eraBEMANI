;アイテムインターフェース改良用の関数郡
@SHOPITEM_BUY
DRAWLINE
CALL PASELIL, @"所持金:{MONEY}＄"
CALL ITEMPRINT
DRAWLINE

PRINTL 　[0] 調教道具
PRINTL 　[1] 消耗品・お酒
PRINTL 　[2] 素質アイテム
PRINTL 　[3] 薬品類
PRINTL 　[4] 特殊アイテム
PRINTL 　[5] 施設・設備
PRINTL 　[6] すべて
DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [9] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 6
			CALL SHOPITEM_LIST, RESULT
			BREAK
		CASE 9
			RETURN 0
	ENDSELECT
LOOP 1

RESTART

;PRINT_ITEM命令が微妙だったので作成 所持アイテムを表示する
@ITEMPRINT
#DIM DYNAMIC LCOUNT
LOCAL = 0
LOCALS = 
IF FLAG:ゲームモード == 7
	LOCALS = [%CALLNAME:MASTER%と%CALLNAME:FINDCHARA(CFLAG:嫁, 1)%の結婚式の招待状]
	LOCAL += STRLENS(LOCALS)
	PRINTFORM 　%LOCALS%
ENDIF
IF FINDELEMENT(自爆キャラ, 1) >= 0
	FOR LCOUNT, 0, 20000
		IF 自爆キャラ:LCOUNT
			SIF LOCAL == 0
				PRINT 　
			LOCALS = [%CSVCALLNAMEF(LCOUNT)%の破片]
			LOCAL += STRLENS(LOCALS)
			IF LOCAL > 80
				PRINTFORML %LOCALS%
				LOCAL = 0
			ELSE
				PRINTFORM %LOCALS%
			ENDIF
		ENDIF
	NEXT
ENDIF
FOR LCOUNT, 0, 100
	IF ITEM:LCOUNT
		SIF LOCAL == 0
			PRINT 　
		LOCAL += STRLENS(ITEMNAME:LCOUNT)
		IF LOCAL > 80
			PRINTFORML [%ITEMNAME:LCOUNT%]
			LOCAL = 0
		ELSE
			PRINTFORM [%ITEMNAME:LCOUNT%]
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, 100, 200
	IF ITEM:LCOUNT
		LOCALS = %ITEMNAME:LCOUNT%×{ITEM:LCOUNT}
		SIF LOCAL == 0
			PRINT 　
		LOCAL += STRLENS(LOCALS)
		IF LOCAL > 80
			PRINTFORML [%ITEMNAME:LCOUNT%×{ITEM:LCOUNT}]
			LOCAL = 0
		ELSE
			PRINTFORM [%ITEMNAME:LCOUNT%×{ITEM:LCOUNT}]
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, 500, 600
	IF ITEM:LCOUNT
		SIF LOCAL == 0
			PRINT 　
		LOCAL += STRLENS(ITEMNAME:LCOUNT)
		IF LOCAL > 80
			PRINTFORML [%ITEMNAME:LCOUNT%]
			LOCAL = 0
		ELSE
			PRINTFORM [%ITEMNAME:LCOUNT%]
		ENDIF
	ENDIF
NEXT
FOR LCOUNT, 1000, 1100
	IF ITEM:LCOUNT
		SIF LOCAL == 0
			PRINT 　
		LOCAL += STRLENS(ITEMNAME:LCOUNT)
		IF LOCAL > 80
			PRINTFORML [%ITEMNAME:LCOUNT%]
			LOCAL = 0
		ELSE
			PRINTFORM [%ITEMNAME:LCOUNT%]
		ENDIF
	ENDIF
NEXT
SIF !LINEISEMPTY()
	PRINTL 

@SHOPITEM_LIST, ARG
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 単位文字数
#DIM DYNAMIC 調合知識有り
#DIM DYNAMIC 禁断の知識有り
表示数 = 0
VARSET LOCAL, 0
VARSET ITEMSALES, 0
LOCAL:10 = RESULT
;主人もしくは助手が『調合知識』を持っている
SIF TALENT:MASTER:調合知識
	調合知識有り++
SIF ASSI > 0 && (TALENT:ASSI:調合知識)
	調合知識有り++
;主人もしくは助手が『禁断の知識』を持っている
SIF TALENT:MASTER:禁断の知識
	禁断の知識有り++
SIF ASSI > 0 && (TALENT:ASSI:禁断の知識)
	禁断の知識有り++

;基本アイテム
SELECTCASE ARG
	CASE 0, 6
		FOR COUNT, 0, 100
			;触手使役術だけ特殊
			SIF ITEMNAME:COUNT == "触手使役術" && !TALENT:MASTER:禁断の知識
				CONTINUE
			IF ITEM:COUNT
				ITEMSALES:COUNT = 0
			ELSE
				ITEMSALES:COUNT = 1
			ENDIF
		NEXT
ENDSELECT

;消耗アイテム
SELECTCASE ARG
	CASE 1, 6
		;ローション、媚薬、ビデオテープなどはとりあえず99個まで
		;飲みきりのお酒も99個まで
		FOR COUNT, 100, 170
			IF ITEM:COUNT >= 99
				ITEMSALES:COUNT = 0
			ELSE
				ITEMSALES:COUNT = 1
			ENDIF
		NEXT
		;ベースのお酒は90個以上あれば表示しない
		FOR COUNT, 170, 200
			IF ITEM:COUNT >= 90
				ITEMSALES:COUNT = 0
			ELSE
				ITEMSALES:COUNT = 1
			ENDIF
		NEXT
		;ビデオカメラが無いときはビデオテープの販売をしない
		SIF !ITEM:ビデオカメラ
			ITEMSALES:ビデオテープ = 0
ENDSELECT

;素質付与アイテム
SELECTCASE ARG
	CASE 2, 6
		;汚れ無視
		SIF !TALENT:MASTER:汚れ無視
			ITEMSALES:【汚れ無視】 = 1

		;調合知識 主人の技巧が4以上なら調合知識を購入可能
		;SIF !TALENT:MASTER:調合知識 && ABL:MASTER:技巧 >= 4
		;	ITEMSALES:【調合知識】 = 1

		;禁断の知識 調教経験1000以上必要
		;SIF !TALENT:MASTER:禁断の知識 && EXP:MASTER:調教経験 >= 1000
		;	ITEMSALES:【禁断の知識】 = 1

		;悪趣味な宝飾品
		SIF !TALENT:MASTER:孔雀の瞳
			ITEMSALES:【悪趣味な宝飾品】 = 1

		;ドラキュラの牙
		SIF !TALENT:MASTER:吸血鬼
			ITEMSALES:【ドラキュラの牙】 = 1

		;ドラゴンの成体
		SIF !TALENT:MASTER:ドラゴン使い
			ITEMSALES:【ドラゴンの成体】 = 1
ENDSELECT

;奴隷を対象とした薬
SELECTCASE ARG
	CASE 3, 6
		;バナナは無条件で喰える
		ITEMSALES:バナナ = 1
		;元気になる薬・魔剤（条件は調合知識のみ）
		IF 調合知識有り
			REPEAT CHARANUM
				;無機物・機械には使えない
				SIF TALENT:COUNT:無機物 || CSTR:COUNT:種族 == "機械"
					CONTINUE
				;使用不可
				SIF CFLAG:COUNT:使用不可
					CONTINUE
				SIF BASE:COUNT:体力 < MAXBASE:COUNT:体力
					ITEMSALES:怪しげな栄養剤 = 1
				SIF !CFLAG:COUNT:魔剤
					ITEMSALES:魔剤 = 1
			REND
		ENDIF

		;素質変化の薬 調合知識or禁断の知識が必要
		IF 調合知識有り || 禁断の知識有り
			REPEAT CHARANUM
				;無機物・機械には使えない
				SIF TALENT:COUNT:無機物 || CSTR:COUNT:種族 == "機械"
					CONTINUE
				;ふたなり化できるか
				SIF !TALENT:COUNT:ふたなり
					ITEMSALES:ふたなり薬 = 1
				;ふたなりを消去できるか
				SIF TALENT:COUNT:ふたなり && !(SEX(COUNT) == 1 && TALENT:COUNT:妊娠)
					ITEMSALES:ふたなり消去薬 = 1
				;母乳体質にできるか 女性のみ 主人は使えない
				SIF !TALENT:COUNT:母乳体質 && SEX(COUNT) == 1 && COUNT != MASTER
					ITEMSALES:天使のミルク = 1
				;処女膜再生できるか 処女および再生処女だとダメ
				SIF !TALENT:COUNT:処女 && !TALENT:COUNT:再生処女 && VAGINA(COUNT)
					ITEMSALES:ユニコーンテイル = 1
				;以下アニマルは使用不可
				SIF TALENT:COUNT:アニマル
					CONTINUE
				;豊胸できるか 爆乳以外でないとダメ
				SIF TALENT:COUNT:バストサイズ != 5 && SEX(COUNT) == 2
					ITEMSALES:豊胸剤 = 1
				;縮胸できるか 絶壁以外でないとダメ
				SIF TALENT:COUNT:バストサイズ != 1 && SEX(COUNT) == 2
					ITEMSALES:縮胸剤 = 1
				;母乳治療できるか 母乳体質じゃないとダメ 主人は使えない
				SIF TALENT:COUNT:母乳体質 && SEX(COUNT) == 2 && COUNT != MASTER
					ITEMSALES:止乳剤 = 1
				;お漏らし治療できるか お漏らし癖じゃないとダメ 主人は使えない
				SIF TALENT:COUNT:おもらし癖 && COUNT != MASTER
					ITEMSALES:夜尿症改善薬 = 1
				;以下禁断の知識が必要
				IF 禁断の知識有り
					;ゾンビ化する。聖水で元にも戻せるがペナルティがある 主人は使えない
					SIF !TALENT:COUNT:ゾンビ && COUNT != MASTER
						ITEMSALES:ゾンビパウダー = 1
					;ゾンビを消去する。しかし回復量低下などのペナルティがある 主人は使えない
					SIF TALENT:COUNT:ゾンビ && COUNT != MASTER
						ITEMSALES:聖水 = 1
					;性転換する 一度使うと二度と元には戻せない
					SIF !TALENT:COUNT:性転換済 && !TALENT:COUNT:妊娠
						ITEMSALES:性転換薬 = 1
				ENDIF
				;大人化or子供化する 豊胸と縮胸の効果もあるのでそのアイテムより高い
				SIF !TALENT:COUNT:小人体型 && !TALENT:COUNT:アニマル
					ITEMSALES:時の砂時計 = 1
				;おもらしするようにする 主人は使えない
				SIF !TALENT:COUNT:おもらし癖 && COUNT != MASTER
					ITEMSALES:永続的利尿剤 = 1
				;獣耳と尻尾を生やす
				SIF (!TALENT:COUNT:動物耳 || !TALENT:COUNT:尻尾) && CSTR:COUNT:種族 != "獣" && !TALENT:COUNT:ファーリー
					ITEMSALES:ビースティースターター = 1
			REND
		ENDIF
ENDSELECT

;特殊アイテム
SELECTCASE ARG
	CASE 4, 6
		;排卵誘発剤
		REPEAT CHARANUM
			;無機物・機械には使えない
			SIF TALENT:COUNT:無機物 || CSTR:COUNT:種族 == "機械"
				CONTINUE
			;使用不可
			SIF CFLAG:COUNT:使用不可
				CONTINUE
			SIF !CFLAG:COUNT:排卵誘発剤 && VAGINA(COUNT) && 生殖機能♀(COUNT)
				ITEMSALES:排卵誘発剤 = 1
		REND
		;リミッター解除 禁断の知識が必要
		IF 禁断の知識有り
			REPEAT CHARANUM
				SIF !TALENT:COUNT:リミットバースト && CFLAG:COUNT:陥落
					ITEMSALES:リミッター解除 = 1
			REND
		ENDIF

		;本
		IF FLAG:図書室ランク >= 10
			FOR LCOUNT, 250, 269
				SIF ITEMNAME:LCOUNT == ""
					CONTINUE
				ITEMSALES:LCOUNT = 1
			NEXT
		ENDIF
		IF FLAG:図書室ランク >= 20
			FOR LCOUNT, 270, 299
				SIF ITEMNAME:LCOUNT == ""
					CONTINUE
				ITEMSALES:LCOUNT = 1
			NEXT
		ENDIF

		;経営ができるゲームモードなら表示
		SELECTCASE FLAG:ゲームモード
			;STRATEGY、PFREEのみ
			CASE 1, 9
				SIF !ITEM:風俗店営業許可証
					ITEMSALES:風俗店営業許可証 = 1
				SIF !ITEM:楽器セット一式
					ITEMSALES:楽器セット一式 = 1
				SIF !ITEM:事業開発オフィス
					ITEMSALES:事業開発オフィス = 1
				;経営用特殊素質
				REPEAT CHARANUM
					;実務経験が必要
					SIF EXP:COUNT:実務経験 < 1000
						CONTINUE
					;既に何か持ってる場合は無理
					SIF TALENT:COUNT:料理人 || TALENT:COUNT:専業主婦 || TALENT:COUNT:工学博士 || TALENT:COUNT:絶対音感 || TALENT:COUNT:カメラマン
						CONTINUE
					;各経験、能力と実務経験があれば特殊素質を身につけられる
					SIF EXP:COUNT:料理経験 >= 1000 && ABL:COUNT:料理技能 >= 5
						ITEMSALES:【料理人】 = 1
					SIF EXP:COUNT:家事経験 >= 1000 && ABL:COUNT:家事技能 >= 5
						ITEMSALES:【専業主婦】 = 1
					SIF EXP:COUNT:工作経験 >= 1000 && ABL:COUNT:工作技能 >= 5
						ITEMSALES:【工学博士】 = 1
					SIF (EXP:COUNT:歌唱経験 >= 1000 && ABL:COUNT:歌唱技能 >= 5) || (EXP:COUNT:演奏経験 >= 1000 && ABL:COUNT:演奏技能 >= 5)
						ITEMSALES:【絶対音感】 = 1
					SIF EXP:COUNT:撮影経験 >= 1000 && ABL:COUNT:撮影技能 >= 5
						ITEMSALES:【カメラマン】 = 1
				REND
		ENDSELECT
ENDSELECT

SELECTCASE ARG
	CASE 5, 6
		;設備 金さえあれば買える
		FOR COUNT, 500, 600
			IF ITEM:COUNT
				ITEMSALES:COUNT = 0
			ELSE
				ITEMSALES:COUNT = 1
			ENDIF
		NEXT
ENDSELECT

DRAWLINE

;主人か助手が買い物上手ならアイテム2割引き、消耗品3割引き
REPEAT VARSIZE("ITEM")
	LOCALS =
	SIF !ITEMSALES:COUNT || ITEMNAME:COUNT == ""
		CONTINUE
	SELECTCASE COUNT
		CASE 100 TO 199
			LOCALS = [{COUNT}]%ITEMNAME:COUNT%(\@ TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手) ? {ITEMPRICE:COUNT/10*7} # {ITEMPRICE:COUNT} \@＄)
		CASE IS < 1000
			LOCALS = [{COUNT}]%ITEMNAME:COUNT%(\@ TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手) ? {ITEMPRICE:COUNT/10*8} # {ITEMPRICE:COUNT} \@＄)
		CASEELSE
			LOCALS = [{COUNT}]%ITEMNAME:COUNT%({ITEMPRICE:COUNT}＄)
	ENDSELECT

	;表示数が0ならスペースを設ける
	SIF 表示数 == 0
		PRINT 　
	CALL PASELI, LOCALS
	;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
	IF FLAG:パセリ表示
		単位文字数 = 2
	ELSEIF 通貨単位 != ""
		単位文字数 = STRLENS(通貨単位)
	ELSE
		単位文字数 = 1
	ENDIF
	;表示数が0ならスペースの分追加
	SIF 表示数 == 0
		単位文字数 += 2
	;＄の分2文字と通貨単位分
	FOR LCOUNT, 0, 28-(STRLENS(LOCALS)-2)-単位文字数
		PRINT  
	NEXT
	表示数++

	IF 表示数 == 4
		PRINTL 
		表示数 = 0
	ENDIF
REND

SIF !LINEISEMPTY()
	PRINTL 

;表示できるアイテムが無い場合
SIF FINDELEMENT(ITEMSALES, 1) < 0
	PRINTL ***購入できるアイテムはありません***

DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [10000] 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 10000
			RETURN 1
		CASEELSE
			IF ITEMSALES:RESULT
				IF ITEMPRICE:RESULT > MONEY
					PRINTW 所持金が足りません
				ELSE
					CALL ITEMBUY_DECIDE, RESULT
				ENDIF
				RESTART
			ENDIF
	ENDSELECT
LOOP 1

RETURN 0

@ITEMBUY_DECIDE, ARG
DRAWLINE
;アイテムの効果を表示する
PRINTFORM %ITEMNAME:ARG%:
SELECTCASE ITEMNAME:ARG
	CASE "ローター"
		PRINTL リーズナブルながらも強力な振動。器用貧乏な調教者がよく使う
	CASE "電気按摩"
		PRINTL 純粋な健康器具を性具として扱う輩が後を絶たない
	CASE "クリキャップ"
		PRINTL 触るのすら面倒なあなたに
	CASE "オナホール"
		PRINTL 使うのが必ずしも男性とは限らず
	CASE "バイブ"
		PRINTL 人工物は当然理想的な形をしているものだ
	CASE "アナルバイブ"
		PRINTL 臭いが取れやすい抗菌仕様となっております
	CASE "アナルビーズ"
		PRINTL 老若男女問わず使えるお得な3点セット
	CASE "空気ポンプ"
		PRINTL アナル初心者が不快になること間違いなし！
	CASE "拡張バルーン"
		PRINTL 自分に合ったサイズに改造しちゃおう
	CASE "浣腸器"
		PRINTL 性交渉から医療用まで用途は幅広い
	CASE "ニプルキャップ"
		PRINTL ローター2個セットとか言わない
	CASE "搾乳器"
		PRINTL 人間でも安心して使える農機
	CASE "ペニスバンド"
		PRINTL これさえあればレズプレイも安心
	CASE "鞭"
		PRINTL これさえあればあなたも女王様
	CASE "針"
		PRINTL とある盲目の鍼師も愛用の品
	CASE "アイマスク"
		PRINTL スローセックスから拷問まで幅広い世界で愛される道具
	CASE "縄"
		PRINTL どんな相手でも問題無く縛れる不思議なロープ
	CASE "ボールギャグ"
		PRINTL お口の問題はこれで解決
	CASE "ビデオカメラ"
		PRINTL 今時珍しいビデオテープ型のカメラ
	CASE "カメラ"
		PRINTL 最近の調教師はデジカメも使えるらしい
	CASE "プレイマット"
		PRINTL 滑りにくいのでわんぱくな子供でも心配なく使えます
	CASE "大鏡"
		PRINTL 合わせ鏡によく用いられる
	CASE "首輪"
		PRINTL 人間用
	CASE "エプロン"
		PRINTL ピンクをベースにフリルやハート柄など様々な装飾のされたもの とても料理をする時に使えるものではない
	CASE "電極プラグ"
		PRINTL 様々な部位に対応した市場には流通していない道具
	CASE "触手使役術"
		PRINTL 愛玩動物からオナペットまで巷で人気の生物
	CASE "ローション"
		PRINTL 乾きにくく潤い長持ちのスグレモノ
	CASE "媚薬"
		PRINTL とある黒髪ロング女子高生が作ったもの
	CASE "利尿剤"
		PRINTL 混ぜるな危険
	CASE "ビデオテープ"
		PRINTL 再生媒体を持っている客にしか需要がない
	CASE "コンドーム"
		PRINTL 何かと射精しがちな調教者でも完璧に避妊できる万能避妊具
	CASE "低温蝋燭"
		PRINTL 熱い！熱いよ！ でも痕は残らない
	CASE "強精神薬"
		PRINTL オーガズムを封じ込める働きがあるらしい 精神薬の名を冠した何か
	CASE "搾精コンドーム"
		PRINTL 量が多くても安心 鮮度長持ち
	CASE "おイモ"
		PRINTL 特に意味は無い
	CASE "スーパードライ", "一番搾り", "プレミアムモルツ", "ヱビス"
		PRINTL Alc.5% 一般的に飲まれているビール。美味しい！
	CASE "ほろよい"
		PRINTL Alc.3% 定番の低アルコール酎ハイ。お酒が苦手な方も安心
	CASE "ストロングゼロ"
		PRINTL Alc.9% コスパと美味しさを両立させた、呑兵衛にも人気の一品
	CASE "炭酸水"
		PRINTL そのまま飲む他、お酒の割り材にも使える万能飲料
	CASE "コーラ"
		PRINTL 世界で一番有名な清涼飲料水。お酒の割り材にも使える万能飲料
	CASE "ジンジャーエール"
		PRINTL 後味スッキリの生姜入り炭酸。お酒の割り材にも使える万能飲料
	CASE "トニックウォーター"
		PRINTL ほろ苦い大人の味の炭酸飲料。お酒の割り材にも使える万能飲料
	CASE "レモンジュース", "ライムジュース"
		PRINTL まるで搾りたてみたいに新鮮。お酒の割り材にも使える万能飲料
	CASE "皆伝"
		PRINTL Alc.12% 純米吟醸 およそ10杯分ほどの量
	CASE "魔王"
		PRINTL Alc.25% 芋焼酎 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "アルパカ"
		PRINTL Alc.12% 赤ワインだったり白ワインだったりする およそ10杯分ほどの量
	CASE "倉吉"
		PRINTL Alc.40% ピュアモルトウイスキー 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "スペリオール"
		PRINTL Alc.40% ラム酒の定番 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "ビーフィーター"
		PRINTL Alc.47% ロンドン原産の代表的なジン 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "アブソルート"
		PRINTL Alc.40% ウォッカの定番 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "カルーア"
		PRINTL Alc.20% 世界中で愛されるコーヒーリキュール 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "スピリタス"
		PRINTL Alc.96% 世界で一番強いお酒 割ったり割らなかったりでおよそ10杯分ほどの量
	CASE "【汚れ無視】"
		PRINTL 汚れた身体でも愛することが可能に
	CASE "【調合知識】"
		PRINTL カガクに禁忌はつきものデース
	CASE "【禁断の知識】"
		PRINTL 名前の割に調合知識よりできることが少ない
	CASE "【悪趣味な宝飾品】"
		PRINTL プレイヤーに「孔雀の瞳」が付く 中学生が考えたようなデザイン
	CASE "【ドラキュラの牙】"
		PRINTL プレイヤーに「吸血鬼」が付く 歯医者に行って付けてもらおう
	CASE "【ドラゴンの成体】"
		PRINTL プレイヤーに「ドラゴン使い」が付く 人間より一回りほど大きい竜
	CASE "医務室"
		PRINTL 医療版を配属することでキャラたちの健康管理が出来るようになる
	CASE "実験室"
		PRINTL 工学班を配属することでアイテムの生成が可能になる
	CASE "図書室"
		PRINTL 定期的に本を補充することでキャラたちがより多くの知識を得る
	CASE "音楽室"
		PRINTL 演奏経験、歌唱経験により多くのボーナスが得られる
	CASE "風俗店営業許可証"
		PRINTL ゲームセンター及び娼館の経営が可能になる
	CASE "楽器セット一式"
		PRINTL 楽器の練習と芸能プロダクションの経営が可能になる
	CASE "事業開発オフィス"
		PRINTL ゲーム開発及び開発関連のコマンドが可能になる
ENDSELECT

;購入の意志を再確認するように
SELECTCASE ARG
	CASE 0 TO 99, 500 TO 599, 1000 TO 1049
		PRINTFORML 本当に%ITEMNAME:ARG%を購入しますか？
		PRINTL [0] 購入する
		PRINTL [1] やめる
		DO
			INPUT
			SIF RESULT == 1
				RETURN 0
		LOOP LOOPRES(0, 1)
		PRINTFORMW ＜%ITEMNAME:ARG%を購入しました＞
		IF ARG < 100 && (TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手))
			MONEY -= ITEMPRICE:ARG/10*8
		ELSE
			MONEY -= ITEMPRICE:ARG
		ENDIF
		ITEMSALES:ARG = 0
		ITEM:ARG = 1
	CASE 100 TO 199
		;消耗品は専用の関数で
		CALL BUY_PLURAL, ARG
	CASE 200 TO 249
		PRINTFORML 本当に%ITEMNAME:ARG%を購入しますか？
		PRINTL [0] 購入する
		PRINTL [1] やめる
		DO
			INPUT
			SIF RESULT == 1
				RETURN 0
		LOOP LOOPRES(0, 1)
		PRINTFORMW ＜%ITEMNAME:ARG%を購入しました＞
		IF TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手)
			MONEY -= ITEMPRICE:ARG/10*8
		ELSE
			MONEY -= ITEMPRICE:ARG
		ENDIF
		ITEMSALES:ARG = 0
		SELECTCASE ITEMNAME:ARG
			CASE "【汚れ無視】"
				TALENT:MASTER:汚れ無視 = 1
			;CASE "【調合知識】"
			;	TALENT:MASTER:調合知識 = 1
			;CASE "【禁断の知識】"
			;	TALENT:MASTER:禁断の知識 = 1
			CASE "【悪趣味な宝飾品】"
				TALENT:MASTER:孔雀の瞳 = 1
			CASE "【ドラキュラの牙】"
				TALENT:MASTER:吸血鬼 = 1
			CASE "【ドラゴンの成体】"
				TALENT:MASTER:ドラゴン使い = 1
				DO
					PRINTL ドラゴンの名前を入力してください(何も入力しない場合、「ドラゴン」になります)
					PRINTBUTTON "[ランダムで命名する]", "ランダムで命名する"
					PRINTL 
					INPUTS
					IF RESULTS == "ランダムで命名する"
						;元ネタは分かる人だけ分かれば
						STRDATA RESULTS
							DATA ローランド
							DATA スタン
							DATA クラッシュ
							DATA トレント
							DATA テッラ
							DATA プレツァル
							DATA スクォールン
							DATA ヴァス
							DATA ケルヴィン
							DATA リドリー
							DATA ノヴァ
							DATA アポロ
							DATA スピリッツ
							DATA フリント
							DATA ヴァージル
							DATA クレイトン
							DATA クラッカー
							DATA エルデン
							DATA デューク
							DATA ゼルクセス
							DATA ブルーザー
							DATA ノクターン
							DATA ブレイズ
							DATA ゼラタン
							DATA デクスター
							DATA グリフ
							DATA レネス
							DATA エリオット
							DATA ウィンストン
						ENDDATA
					ENDIF
					SIF RESULTS == ""
						RESULTS = ドラゴン
					PRINTFORML %RESULTS%でよろしいですか？(後からオプション画面で変更できます)
					PRINTL [0] - はい
					PRINTL [1] - いいえ
					INPUT
					SELECTCASE RESULT
						CASE 0
							CSTR:MASTER:ドラゴン = %RESULTS%
							SELECTCASE CSTR:MASTER:ドラゴン
								CASE "ドラゴン"
									PRINTFORMW ドラゴンは名前が欲しいと言わんばかりに寂しげな瞳で%CALLNAME:MASTER%を見つめている……
								CASEELSE
									PRINTFORMW %CSTR:MASTER:ドラゴン%は名前をつけてくれた%CALLNAME:MASTER%に懐いているようだ……
							ENDSELECT
						CASE 1
							CONTINUE
					ENDSELECT
				LOOP LOOPRES(0)
		ENDSELECT
	CASE 250 TO 499, 1050 TO 1099
		;奴隷に使用するタイプのアイテムも専用の関数で
		CALL USE_ITEM, ARG
ENDSELECT

RETURN 0

;複数個所持可能なアイテムについて、同時購入個数を選択
@BUY_PLURAL, ARG
#DIM DYNAMIC アイテム価格
VARSET LOCAL, 0
;購入できる最大数
SELECTCASE ITEMNAME:ARG
	CASE "皆伝", "魔王", "アルパカ", "倉吉", "スペリオール", "ビーフィーター", "アブソルート", "カルーア"
		LOCAL = (99-ITEM:ARG)/10
	CASEELSE
		LOCAL = 99-ITEM:ARG
ENDSELECT

IF TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手)
	アイテム価格 = ITEMPRICE:ARG*7/10
ELSE
	アイテム価格 = ITEMPRICE:ARG
ENDIF

;予算が下回った場合、予算内で購入できる最大数に下方修正
SIF LOCAL > MONEY/アイテム価格
	LOCAL = MONEY/アイテム価格

PRINTFORML %ITEMNAME:ARG%をいくつ買いますか？ (最大{LOCAL}個、0で戻る)
CALL TWO_DIGIT, LOCAL, "個"
IF RESULT
	PRINTFORMW ＜%ITEMNAME:ARG%を{RESULT}個購入しました＞
	SELECTCASE ITEMNAME:ARG
		CASE "皆伝", "魔王", "アルパカ", "倉吉", "スペリオール", "ビーフィーター", "アブソルート", "カルーア"
				ITEM:ARG += RESULT*10
		CASEELSE
			ITEM:ARG += RESULT
	ENDSELECT
	MONEY -= アイテム価格*RESULT
	;実績:ター子の交渉で値引きしたアイテムを購入する
	SIF CHARANAMEF(MASTER) == "ター子" || (ASSI > 0 && CHARANAMEF(ASSI) == "ター子")
		CALL 実績解除, "「それ、もうちょい安ぅならんやろか？」"
ENDIF

RETURN 0

;アイテム使用
@USE_ITEM, ARG
VARSET LOCAL, 0
SELECTCASE ITEMNAME:ARG
	CASE "怪しげな栄養剤"
		PRINTL 体力を回復します。とある黒髪ロング女子高生が作ったものらしい
	CASE "ふたなり薬"
		PRINTL ふたなりになります。色欲の悪魔が創りだしてバラまいたもの
	CASE "ふたなり消去薬"
		PRINTL ふたなりを消去します。某製薬会社がふたなり薬のワクチンとして開発した
	CASE "天使のミルク"
		PRINTL 母乳体質になります。幼くして母になってしまったあなたに
	CASE "ユニコーンテイル"
		PRINTL 処女膜が戻ります。ただし初々しかった性格は戻りません
	CASE "豊胸剤"
		PRINTL 胸を大きくします。偽りの美貌をあの人へ
	CASE "縮胸剤"
		PRINTL 胸を小さくします。肩こりで血行の良くない女性に人気の一品
	CASE "止乳剤"
		PRINTL 母乳が出なくなります。流産した際にはぜひお求めを
	CASE "夜尿症改善薬"
		PRINTL お漏らし癖が治ります。これで介護生活ともおさらば
	CASE "胎児卵化薬"
		PRINTL 強制的に卵を産ませます。胎児の生命は保証外です
	CASE "ゾンビパウダー"
		PRINTL ゾンビ化します。死体では物足りないあなたに
	CASE "聖水"
		PRINTL 素質「ゾンビ」を消去します。それが生者と呼べるのかは怪しいものです
	CASE "性転換薬"
		PRINT 対象を性転換させます。
		FONTBOLD
		PRINTL 一度性転換させると二度と戻せないので注意
		FONTREGULAR
	CASE "時の砂時計"
		PRINTL 大人になったり子供になったりします。女性ならバストサイズも変わります
	CASE "永続的利尿剤"
		PRINTL 尿を漏らしやすくなります。そういったフェチズムの方に人気の商品
	CASE "ビースティースターター"
		PRINTL 獣耳と尻尾を生やします。幼稚園のお遊戯会で奥様方にも大人気
	CASE "魔剤"
		PRINTL eraBEMANIでモンエナ買えるって魔剤ンゴ！？
	CASE "バナナ"
		PRINTL 音ゲー廃人の主食
	CASE "排卵誘発剤"
		PRINTL 一日だけ妊娠確率を上昇させます。頼れるのは祈りよりも薬です
	CASE "リミッター解除"
		PRINTL 使用対象の能力上限を解除します。後悔しませんね？
	CASE "古今錬金術大全"
		PRINTL 使用対象が【調合知識】を習得します
	CASE "ひとりでできるもん～オトコのコのためのアナニー入門～"
		PRINTL 男性キャラにプレゼントすると自慰しやすくなります
	CASE "膣性感開発・中イキ完全マニュアル"
		PRINTL 女性キャラにプレゼントすると自慰しやすくなります
	CASE "ドラッカーの『マネジメント』"
		PRINTL 使用対象が【鼓舞】を習得します
	CASE "ヴォイニッチ手稿"
		PRINTL 使用対象が【禁断の知識】を習得します
	CASE "グリモワール"
		PRINTL 使用対象が魔法を学べるようになります
	CASE "解体新書"
		PRINTL 使用対象が【治療】を習得します
	CASE "ネクロノミコン"
		PRINTL 使用対象が【ネクロマンサー】を習得します
	CASE "【料理人】"
		PRINTL 対象に素質【料理人】を習得させます
	CASE "【専業主婦】"
		PRINTL 対象に素質【専業主婦】を習得させます
	CASE "【工学博士】"
		PRINTL 対象に素質【工学博士】を習得させます
	CASE "【絶対音感】"
		PRINTL 対象に素質【絶対音感】を習得させます
	CASE "【カメラマン】"
		PRINTL 対象に素質【カメラマン】を習得させます
ENDSELECT

PRINTL 誰に使いますか？

FOR PAGING, 0, CHARANUM
	LOCAL = 1
	SELECTCASE ARG
		;本
		CASE 250 TO 299
			;使用不可のキャラは除外
			SIF CFLAG:PAGING:使用不可
				LOCAL = 0
			SELECTCASE ITEMNAME:ARG
				CASE "古今錬金術大全"
					SIF TALENT:PAGING:調合知識
						LOCAL = 0
				CASE "ひとりでできるもん～オトコのコのためのアナニー入門～"
					SIF SEX(PAGING) == 2 || TALENT:PAGING:自慰しやすい
						LOCAL = 0
				CASE "膣性感開発・中イキ完全マニュアル"
					SIF SEX(PAGING) == 1 || TALENT:PAGING:自慰しやすい
						LOCAL = 0
				CASE "ドラッカーの『マネジメント』"
					SIF TALENT:PAGING:鼓舞
						LOCAL = 0
				CASE "ヴォイニッチ手稿"
					SIF TALENT:PAGING:禁断の知識
						LOCAL = 0
				CASE "グリモワール"
					SIF ABL:PAGING:魔法技能
						LOCAL = 0
				CASE "解体新書"
					SIF TALENT:PAGING:治療
						LOCAL = 0
				CASE "ネクロノミコン"
					SIF TALENT:PAGING:ネクロマンサー
						LOCAL = 0
			ENDSELECT
		;薬品系と素質系で処理が異なる
		CASE 300 TO 499
			;無機物・機械は除外
			SIF TALENT:PAGING:無機物 || CSTR:PAGING:種族 == "機械"
				LOCAL = 0
			;使用不可のキャラも除外
			SIF CFLAG:PAGING:使用不可
				LOCAL = 0
			;主人は除外 ただしふたなり、処女再生、胸関連、性転換薬、時の砂時計、ビースティースターター、排卵誘発剤、バナナは可能
			IF PAGING == MASTER
				SELECTCASE ITEMNAME:ARG
					CASE "ふたなり薬", "ユニコーンテイル", "豊胸剤", "縮胸剤", "性転換薬", "時の砂時計", "ビースティースターター", "排卵誘発剤", "バナナ"
					CASEELSE
						LOCAL = 0
				ENDSELECT
			ENDIF
			SELECTCASE ITEMNAME:ARG
				CASE "怪しげな栄養剤"
					;体力がマックスだと使えない
					SIF BASE:PAGING:体力 >= MAXBASE:PAGING:体力
						LOCAL = 0
				CASE "ふたなり薬"
					SIF TALENT:PAGING:ふたなり
						LOCAL = 0
				CASE "ふたなり消去薬"
					;男で妊娠中だとヴァギナ塞げない
					SIF !TALENT:PAGING:ふたなり || (SEX(PAGING) == 1 && TALENT:PAGING:妊娠)
						LOCAL = 0
				CASE "天使のミルク"
					SIF TALENT:PAGING:母乳体質 || (SEX(PAGING) == 1 && !TALENT:PAGING:ふたなり)
						LOCAL = 0
				CASE "ユニコーンテイル"
					SIF TALENT:PAGING:処女 || TALENT:PAGING:再生処女 || (SEX(PAGING) == 1 && !TALENT:PAGING:ふたなり)
						LOCAL = 0
				;以下アニマルは使用不可
				CASE "豊胸剤"
					SIF TALENT:PAGING:バストサイズ == 5 || SEX(PAGING) == 1 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "縮胸剤"
					SIF TALENT:PAGING:バストサイズ == 1 || SEX(PAGING) == 1 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "止乳剤"
					SIF !TALENT:PAGING:母乳体質 || SEX(PAGING) == 1 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "夜尿症改善薬"
					SIF !TALENT:PAGING:おもらし癖 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "胎児卵化薬"
					SIF !TALENT:PAGING:妊娠 || (TALENT:PAGING:妊娠 && PREG:PAGING:10 - 3 <= DAY) || BASE:PAGING:体力 < 1000 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "ゾンビパウダー"
					SIF TALENT:PAGING:ゾンビ || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "聖水"
					SIF !TALENT:PAGING:ゾンビ || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "性転換薬"
					;妊娠時は使えない
					SIF TALENT:PAGING:妊娠 || TALENT:PAGING:性転換済 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "時の砂時計"
					SIF TALENT:PAGING:小人体型 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "永続的利尿剤"
					;既におもらし癖ついてると使えない
					SIF TALENT:PAGING:おもらし癖 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "ビースティースターター"
					;ケモミミ尻尾両方あったり、既に獣だったりファーリーだったりすると使えない
					SIF (TALENT:PAGING:動物耳 && TALENT:PAGING:尻尾) || CSTR:PAGING:種族 == "獣" || TALENT:PAGING:ファーリー || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "魔剤"
					;既にキメてる場合は使えない
					SIF CFLAG:PAGING:魔剤 || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "排卵誘発剤"
					SIF CFLAG:PAGING:排卵誘発剤 || !VAGINA(PAGING) || TALENT:PAGING:アニマル
						LOCAL = 0
				CASE "リミッター解除"
					SIF TALENT:PAGING:リミットバースト || !CFLAG:PAGING:陥落
						LOCAL = 0
			ENDSELECT
		;経営用素質は誰でも習得可能 各経験、技能、実務経験が必要
		CASE 1050 TO 1099
			;実務経験が必要
			SIF EXP:PAGING:実務経験 < 1000
				LOCAL = 0
			;既に何か持ってる場合は無理
			SIF TALENT:PAGING:料理人 || TALENT:PAGING:専業主婦 || TALENT:PAGING:工学博士 || TALENT:PAGING:絶対音感 || TALENT:PAGING:カメラマン
				LOCAL = 0
			;各経験、能力と実務経験があれば特殊素質を身につけられる
			SELECTCASE ITEMNAME:ARG
				CASE "【料理人】"
					SIF EXP:PAGING:料理経験 < 1000 || ABL:PAGING:料理技能 < 5
						LOCAL = 0
				CASE "【専業主婦】"
					SIF EXP:PAGING:家事経験 < 1000 || ABL:PAGING:家事技能 < 5
						LOCAL = 0
				CASE "【工学博士】"
					SIF EXP:PAGING:工作経験 < 1000 || ABL:PAGING:工作技能 < 5
						LOCAL = 0
				CASE "【絶対音感】"
					SIF (EXP:PAGING:歌唱経験 < 1000 || ABL:PAGING:歌唱技能 < 5) && (EXP:PAGING:演奏経験 < 1000 || ABL:PAGING:演奏技能 < 5)
						LOCAL = 0
				CASE "【カメラマン】"
					SIF EXP:PAGING:撮影経験 < 1000 || ABL:PAGING:撮影技能 < 5
						LOCAL = 0
			ENDSELECT
	ENDSELECT

	LOCALS = 体力
	LOCALS += @"({BASE:PAGING:体力}/{MAXBASE:PAGING:体力}) %BARSTR(BASE:PAGING:体力, MAXBASE:PAGING:体力, 16)% "
	SIF TALENT:PAGING:処女
		LOCALS += "処女 "
	SIF TALENT:PAGING:再生処女
		LOCALS += "再生処女 "
	SIF TALENT:PAGING:童貞
		LOCALS += "童貞 "
	SIF TALENT:PAGING:恋慕
		LOCALS += "恋慕 "
	SIF TALENT:PAGING:淫乱
		LOCALS += "淫乱 "
	SIF TALENT:PAGING:服従
		LOCALS += "服従 "
	SIF TALENT:PAGING:親愛
		LOCALS += "親愛 "
	SIF TALENT:PAGING:娼婦
		LOCALS += "娼婦 "
	SIF TALENT:PAGING:隷属
		LOCALS += "隷属 "
	SIF TALENT:PAGING:崩壊
		LOCALS += "崩壊 "
	SIF TALENT:PAGING:リミットバースト
		LOCALS += "超 "
	SIF CFLAG:PAGING:状態 == 2
		LOCALS += "助手可 "
	CALL PAGING, LOCAL, @"%LOCALS%", "ITEM USE", "戻る"
NEXT

SIF RESULT == -1
	RETURN 0

LOCAL = RESULT
CALL KOJO_MESSAGE_EVENT, ITEMNAME:ARG, LOCAL

SELECTCASE ITEMNAME:ARG
	;調合知識
	CASE "古今錬金術大全"
		PRINTFORMW ＜%CALLNAME:LOCAL%は【調合知識】を得た＞
		TALENT:LOCAL:調合知識 = 1
	;あぶひゃく
	CASE "ひとりでできるもん～オトコのコのためのアナニー入門～", "膣性感開発・中イキ完全マニュアル"
		PRINTFORMW ＜%CALLNAME:LOCAL%は自慰に深い興味を持つようになった＞
		TALENT:LOCAL:自慰しやすい = 1
	;マネジメント
	CASE "ドラッカーの『マネジメント』"
		PRINTFORMW ＜%CALLNAME:LOCAL%は【鼓舞】を得た＞
		TALENT:LOCAL:鼓舞 = 1
	;禁断の知識
	CASE "ヴォイニッチ手稿"
		PRINTFORMW ＜%CALLNAME:LOCAL%は【禁断の知識】を得た＞
		TALENT:LOCAL:禁断の知識 = 1
	;グリモワール
	CASE "グリモワール"
		PRINTFORMW ＜%CALLNAME:LOCAL%は魔術の基礎を得た＞
		ABL:LOCAL:魔法技能 = 1
	;治療
	CASE "解体新書"
		PRINTFORMW ＜%CALLNAME:LOCAL%は【治療】を得た＞
		TALENT:LOCAL:治療 = 1
	;ネクロ
	CASE "ネクロノミコン"
		PRINTFORMW ＜%CALLNAME:LOCAL%は【ネクロマンサー】を得た＞
		TALENT:LOCAL:ネクロマンサー = 1
	;体力回復
	CASE "怪しげな栄養剤"
		PRINTFORMW ＜%CALLNAME:LOCAL%の体力が回復した＞
		IF TALENT:LOCAL:回復早い
			LOCAL:1 = 400
		ELSEIF TALENT:LOCAL:回復遅い
			LOCAL:1 = 200
		ELSE
			LOCAL:1 = 300
		ENDIF
		SIF TALENT:LOCAL:小柄体型
			LOCAL:1 += 50
		LOCAL:1 += ABL:MASTER:技巧*10
		BASE:LOCAL:体力 += LOCAL:1
		SIF BASE:LOCAL:体力 > MAXBASE:LOCAL:体力
			BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
	;ふたなり化
	CASE "ふたなり薬"
		PRINTFORMW ＜%CALLNAME:LOCAL%はふたなりになった＞
		TALENT:LOCAL:ふたなり = 1
		CFLAG:LOCAL:ストレス値 += 5
		SELECTCASE SEX(LOCAL)
			;対象が男なら、常に処女になる Ｖ経験があれば再生処女
			CASE 1
				IF !EXP:LOCAL:Ｖ経験
					TALENT:LOCAL:処女 = 1
				ELSE
					TALENT:LOCAL:再生処女 = 1
				ENDIF
			;対象が女で、挿入経験が無ければ童貞になる
			CASE 2
				IF VIRGIN:LOCAL:0 == "" && !TALENT:LOCAL:性転換済
					TALENT:LOCAL:童貞 = 1
				ELSEIF VIRGIN:LOCAL:10 == "" && TALENT:LOCAL:性転換済
					TALENT:LOCAL:童貞 = 1
				ENDIF
		ENDSELECT
	;ふたなりを消去
	CASE "ふたなり消去薬"
		PRINTFORMW ＜%CALLNAME:LOCAL%はふたなりではなくなった＞
		TALENT:LOCAL:ふたなり = 0
		CFLAG:LOCAL:ストレス値 += 3
		SELECTCASE SEX(LOCAL)
			CASE 1
				TALENT:LOCAL:処女 = 0
				TALENT:LOCAL:再生処女 = 0
				CALL CLEAR_FLAG, LOCAL
			CASE 2
				TALENT:LOCAL:童貞 = 0
		ENDSELECT
	;母乳体質
	CASE "天使のミルク"
		PRINTFORMW ＜%CALLNAME:LOCAL%は母乳が出るようになった＞
		TALENT:LOCAL:母乳体質 = 1
		CFLAG:LOCAL:ストレス値 += 5
	;処女膜再生
	CASE "ユニコーンテイル"
		PRINTFORMW ＜%CALLNAME:LOCAL%の処女膜が再生された＞
		TALENT:LOCAL:再生処女 = 1
		CFLAG:LOCAL:ストレス値 += 5
	;豊乳化
	CASE "豊胸剤"
		SELECTCASE TALENT:LOCAL:バストサイズ
			;絶壁→貧乳
			CASE 1
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸がほんの少しだけ膨らんだ＞
			;貧乳→普乳
			CASE 2
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸が人並みに大きくなった＞
			;普乳→巨乳
			CASE 3
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸が豊満になった＞
				;実績:リピカをグラマラスにする
				SIF CHARANAMEF(LOCAL) == "赤のリピカ" && !TALENT:LOCAL:小柄体型
					CALL 実績解除, "「ふっふーん 『ないすばでぃ』を手に入れたのさー♪」"
			;巨乳→爆乳
			CASE 4
				;小柄体型、小人体型
				IF TALENT:LOCAL:小柄体型 || TALENT:LOCAL:小人体型
					PRINTFORMW ＜%CALLNAME:LOCAL%の胸が体に不釣合いなまでに大きくなった＞
				;それ以外
				ELSE
					PRINTFORMW ＜%CALLNAME:LOCAL%の胸が溢れんばかりの大きさになった＞
				ENDIF
				;実績:リピカをグラマラスにする
				SIF CHARANAMEF(LOCAL) == "赤のリピカ" && !TALENT:LOCAL:小柄体型
					CALL 実績解除, "「ふっふーん 『ないすばでぃ』を手に入れたのさー♪」"
		ENDSELECT
		TALENT:LOCAL:バストサイズ++
		CFLAG:LOCAL:ストレス値 += 7
	;縮乳化
	CASE "縮胸剤"
		SELECTCASE TALENT:LOCAL:バストサイズ
			;爆乳→巨乳
			CASE 5
				PRINTFORMW ＜%CALLNAME:LOCAL%の豊かすぎる胸が一回り小さくなった＞
			;巨乳→普乳
			CASE 4
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸が人並みに小さくなった＞
			;普乳→貧乳
			CASE 3
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸が小さくなった＞
			;貧乳→絶壁
			CASE 2
				PRINTFORMW ＜%CALLNAME:LOCAL%の胸が極端に薄くなった＞
				IF TALENT:LOCAL:母乳体質
					PRINTFORMW ＜%CALLNAME:LOCAL%の母乳が出なくなった＞
					TALENT:LOCAL:母乳体質 = 0
					CFLAG:LOCAL:ストレス値++
				ENDIF
		ENDSELECT
		TALENT:LOCAL:バストサイズ--
		CFLAG:LOCAL:ストレス値 += 7
	;母乳治療
	CASE "止乳剤"
		PRINTFORMW ＜%CALLNAME:LOCAL%は母乳が出なくなった＞
		TALENT:LOCAL:母乳体質 = 0
		CFLAG:LOCAL:ストレス値 += 3
	;お漏らし治療
	CASE "夜尿症改善薬"
		PRINTFORMW ＜%CALLNAME:LOCAL%のお漏らし癖が治った＞
		TALENT:LOCAL:おもらし癖 = 0
		CFLAG:LOCAL:おもらし改善フラグ = 1
		CFLAG:LOCAL:ストレス値 += 3
	;ゾンビパウダー
	CASE "ゾンビパウダー"
		PRINTFORMW ＜%CALLNAME:LOCAL%はゾンビになった＞
		TALENT:LOCAL:ゾンビ = 1
		CFLAG:LOCAL:ストレス値 += 10
	;聖水
	CASE "聖水"
		PRINTFORMW ＜%CALLNAME:LOCAL%はゾンビから蘇った＞
		TALENT:LOCAL:ゾンビ = 0
		IF !TALENT:LOCAL:回復遅い
			PRINTW その反動で治癒力が低下しました
			TALENT:LOCAL:回復遅い = 1
		ENDIF
		CFLAG:LOCAL:ストレス値 += 5
	;性転換薬 ふたなり時に何があろうと無条件でバージンは復活する
	CASE "性転換薬"
		SELECTCASE SEX(LOCAL)
			CASE 1
				PRINTFORMW ＜%CALLNAME:LOCAL%は女になった＞
				IF TALENT:LOCAL:童貞
					VIRGIN:LOCAL:0 = NOTLOSTVIRGIN
					TALENT:LOCAL:童貞 = 0
				ENDIF
				TALENT:LOCAL:性別 = 2
				TALENT:LOCAL:処女 = 1
				IF TALENT:LOCAL:小柄体型
					TALENT:LOCAL:バストサイズ = 2
				ELSE
					TALENT:LOCAL:バストサイズ = 3
				ENDIF
			CASE 2
				PRINTFORMW ＜%CALLNAME:LOCAL%は男になった＞
				IF TALENT:LOCAL:処女
					VIRGIN:LOCAL:1 = NOTLOSTVIRGIN
					TALENT:LOCAL:処女 = 0
				ENDIF
				TALENT:LOCAL:性別 = 1
				TALENT:LOCAL:童貞 = 1
				TALENT:LOCAL:バストサイズ = 0
		ENDSELECT
		CALL CLEAR_FLAG, LOCAL
		TALENT:LOCAL:性転換済 = 1
		CFLAG:LOCAL:ストレス値 += 20
	;時の砂時計
	CASE "時の砂時計"
		IF TALENT:LOCAL:小柄体型
			PRINTFORMW ＜%CALLNAME:LOCAL%は立派な大人の体になった＞
			TALENT:LOCAL:小柄体型 = 0
			SIF SEX(LOCAL) == 2 && TALENT:LOCAL:バストサイズ != 5
				TALENT:LOCAL:バストサイズ++
			MAXBASE:LOCAL:体力 += 200
			BASE:LOCAL:体力 += 200
			;実績:リピカをグラマラスにする
			SIF CHARANAMEF(LOCAL) == "赤のリピカ" && TALENT:LOCAL:バストサイズ >= 4
				CALL 実績解除, "「ふっふーん 『ないすばでぃ』を手に入れたのさー♪」"
		ELSE
			PRINTFORMW ＜%CALLNAME:LOCAL%は\@ SEX(LOCAL) == 1 ? 少年 # 幼女 \@のような小さな体になった＞
			TALENT:LOCAL:小柄体型 = 1
			SIF SEX(LOCAL) == 2 && TALENT:LOCAL:バストサイズ != 1
				TALENT:LOCAL:バストサイズ--
			MAXBASE:LOCAL:体力 -= 200
			BASE:LOCAL:体力 -= 200
		ENDIF
		CFLAG:LOCAL:ストレス値 += 10
	;永続的利尿剤
	CASE "永続的利尿剤"
		PRINTFORMW ＜%CALLNAME:LOCAL%は尿意を我慢できなくなった＞
		TALENT:LOCAL:おもらし癖 = 1
		CFLAG:LOCAL:ストレス値 += 10
	;ビースティースターター
	CASE "ビースティースターター"
		PRINTFORM ＜%CALLNAME:LOCAL%に
		SIF !TALENT:LOCAL:動物耳
			PRINT 動物の耳
		SIF !TALENT:LOCAL:動物耳 && !TALENT:LOCAL:尻尾
			PRINT と
		SIF !TALENT:LOCAL:尻尾
			PRINT 動物の尻尾
		PRINTW が生えた＞
		TALENT:LOCAL:動物耳 = 1
		TALENT:LOCAL:尻尾 = 1
		;眷属の儀追加により獣人化は廃止
		;SELECTCASE CSTR:LOCAL:種族
		;	CASE "人間", "人間(？)"
		;		CSTR:LOCAL:種族 = 獣人
		;		CFLAG:LOCAL:種族 = 15
		;ENDSELECT
		IF TALENT:LOCAL:好奇心
			CFLAG:LOCAL:依存度 += 50
		ELSE
			CFLAG:LOCAL:ストレス値 += 3
		ENDIF
	;魔剤
	CASE "魔剤"
		PRINTFORMW ＜%CALLNAME:LOCAL%のテンションが上がってきた！＞
		CFLAG:LOCAL:魔剤 = 1
		TIMES CFLAG:LOCAL:魔剤耐性, 1.1
		CFLAG:LOCAL:魔剤耐性 = LIMIT(CFLAG:LOCAL:魔剤耐性, 1100, 2000)
		CFLAG:LOCAL:回復量低下 += 5
		CFLAG:LOCAL:ストレス値 += 10
		SIF CHARANAMEF(LOCAL) == "翔"
			CALL 実績解除, "いいからドーピングだ！"
	;バナナ
	CASE "バナナ"
		PRINTFORMW ＜%CALLNAME:LOCAL%は音楽ゲームが上達した……気がする＞
		EXP:LOCAL:音ゲー経験++
		BASE:LOCAL:体力 += 10
		SIF BASE:LOCAL:体力 > MAXBASE:LOCAL:体力
			BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
	;排卵誘発剤
	CASE "排卵誘発剤"
		IF LOCAL == MASTER
			PRINTFORMW ＜%CALLNAME:MASTER%は排卵誘発剤を使用した＞
		ELSE
			PRINTFORMW ＜%CALLNAME:LOCAL%に排卵誘発剤を使った＞
		ENDIF
		CFLAG:LOCAL:排卵誘発剤 = 1
		CFLAG:LOCAL:ストレス値 += 3
	;リミットバースト
	CASE "リミッター解除"
		PRINTFORMW ＜%NAME:LOCAL%のリミッターを解除した＞
		TALENT:LOCAL:リミットバースト = 1
	CASE "【料理人】"
		PRINTFORMW ＜%NAME:LOCAL%は料理人になった＞
		TALENT:LOCAL:料理人 = 1
	CASE "【専業主婦】"
		PRINTFORMW ＜%NAME:LOCAL%は専業主婦になった＞
		TALENT:LOCAL:専業主婦 = 1
	CASE "【工学博士】"
		PRINTFORMW ＜%NAME:LOCAL%は工学博士になった＞
		TALENT:LOCAL:工学博士 = 1
	CASE "【絶対音感】"
		PRINTFORMW ＜%NAME:LOCAL%は絶対音感を身につけた＞
		TALENT:LOCAL:絶対音感 = 1
	CASE "【カメラマン】"
		PRINTFORMW ＜%NAME:LOCAL%はカメラマンになった＞
		TALENT:LOCAL:カメラマン = 1
ENDSELECT

;アイテムによる崩壊の処理
;撤廃しました
;SIF CFLAG:LOCAL:ストレス値 >= 100 && TALENT:LOCAL:崩壊 == 0
;	CALL COLLAPSE_MIND_ITEM, LOCAL

IF ARG < 1000 && (TALENT:MASTER:買い物上手 || (ASSI > 0 && TALENT:ASSI:買い物上手))
	MONEY -= ITEMPRICE:ARG/10*8
	;実績:ター子の交渉で値引きしたアイテムを購入する
	SIF CHARANAMEF(MASTER) == "ター子" || (ASSI > 0 && CHARANAMEF(ASSI) == "ター子")
		CALL 実績解除, "「それ、もうちょい安ぅならんやろか？」"
ELSE
	MONEY -= ITEMPRICE:ARG
ENDIF

RETURN 0

;アイテムで【崩壊】を得た場合のイベント
@COLLAPSE_MIND_ITEM, ARG
PRINTFORMW %CALLNAME:ARG%の様子がおかしい
PRINTFORMW 投薬の影響によって、%CALLNAME:ARG%の精神に異常をきたしてしまったようだ
PRINTFORMW %NAME:ARG%は【崩壊】してしまった
TALENT:ARG:崩壊 = 1
SIF TALENT:ARG:恋慕
	PRINTFORMW %CALLNAME:ARG%は[恋慕]を失った
SIF TALENT:ARG:淫乱
	PRINTFORMW %CALLNAME:ARG%は[淫乱]を失った
SIF TALENT:ARG:服従
	PRINTFORMW %CALLNAME:ARG%は[服従]を失った
SIF TALENT:ARG:親愛
	PRINTFORMW %CALLNAME:ARG%は[親愛]を失った
SIF TALENT:ARG:娼婦
	PRINTFORMW %CALLNAME:ARG%は[娼婦]を失った
SIF TALENT:ARG:隷属
	PRINTFORMW %CALLNAME:ARG%は[隷属]を失った
TALENT:ARG:恋慕 = 0
TALENT:ARG:淫乱 = 0
TALENT:ARG:服従 = 0
TALENT:ARG:親愛 = 0
TALENT:ARG:娼婦 = 0
TALENT:ARG:隷属 = 0

