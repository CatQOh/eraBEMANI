@USERSAVE
#DIM DYNAMIC LCOUNT
PRINTL □何番にセーブしますか？
PRINTL 　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間

FOR LCOUNT, 0, 20
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			PRINTFORML 　[{LCOUNT, 2}] %RESULTS%
		CASE 1
			SETCOLORBYNAME gray
			;表示テンプレ
			;PRINTFORML 　[ 5] 2016/01/10(曜) 00:10:15 | 2016/3/28(曜) 197日目夜 難易度:(N) ∞ 
			PRINTFORML 　[{LCOUNT, 2}] ----　　　　　　　　　　|
			RESETCOLOR
		CASE 2
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 別バリアントのセーブデータです 上書きは可能です
			RESETCOLOR
		CASE 3
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 互換性の無いセーブデータです 上書きは可能です
			RESETCOLOR
		CASE 4
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] ロードできないセーブデータです 上書きは可能です
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAIN 　SAVE　
PRINTL [100] 戻る
DO
	INPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 19
			SELECTCASE CHKDATA(LOCAL)
				CASE 0, 2, 3, 4
					PRINTL 既にセーブデータが存在します。上書きしますか？
					PRINTL [0] はい
					PRINTL [1] いいえ
					CALL INPUTF, 0, 1
					SIF RESULT == 1
						RESTART
					BREAK
				CASE 1
					BREAK
			ENDSELECT
		CASE 100
			RETURN 0
	ENDSELECT
LOOP 1

SAVEDATA LOCAL, セーブデータ情報()

LOCAL = 0

REPEAT 20
	SIF CHKDATA(COUNT) == 0
		LOCAL++
REND

SIF LOCAL == 20
	CALL 実績解除, "メモリーカードの容量が足りません"

SAVEGLOBAL

RETURN 0

@SYSTEM_AUTOSAVE
IF ゲームモード() == "学園モード"
	LOCAL = 94+((DAY-1)*3%6)
	SELECTCASE ターン
		CASE "昼"
			LOCAL += 1
		CASE "夜"
			LOCAL += 2
	ENDSELECT
ELSE
	LOCAL = 94+((DAY-1)*2%6+TIME)
ENDIF

SAVEDATA LOCAL, セーブデータ情報()

@セーブデータ情報, ARG
#FUNCTIONS
#DIMS DYNAMIC REALTIMENOW
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日

;リアルタイムの計算
年 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
月 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
日 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

REALTIMENOW = %GETTIMES()%
SPLIT REALTIMENOW, " ", LOCALS
LOCALS = %LOCALS%(%DOW(ZELLER(年, 月, 日))%) %LOCALS:1%
LOCALS = %LOCALS% | {YEAR}/%TOSTR(MONTH, "D2")%/%TOSTR(DAYS, "D2")%(%DOW(FLAG:曜日)%)

IF ゲームモード() == "学園モード"
	LOCALS += @" {DAY}日目(%ターン%)"
ELSE
	SELECTCASE TIME
		CASE 0
			LOCALS += @" {DAY, 4}日目昼 "
		CASE 1
			LOCALS += @" {DAY, 4}日目夜 "
	ENDSELECT
ENDIF

SELECTCASE ゲームモード()
	CASE "新婚モード"
	CASE "段位認定"
		LOCALS += "段位認定 "
	CASE "学園モード"
		LOCALS += "学園モード "
	CASEELSE
		SIF ゲームモード() == "経営モード"
			LOCALS += "KA "
		LOCALS += "難易度:"
		SELECTCASE FLAG:難易度
			CASE 1
				LOCALS += "(N) "
			CASE 2
				LOCALS += "(H) "
			CASE 3
				LOCALS += "(A) "
		ENDSELECT
ENDSELECT

SIF GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード")
	LOCALS += "∞ "

SELECTCASE ゲームモード()
	CASE "新婚モード"
		LOCALS += @"%CALLNAME:MASTER%と%CALLNAME:(FINDCHARA(CFLAG:嫁, 1))%が新婚生活中"
	CASE "学園モード"
		LOCALS += @"PLAYER:%NAME:MASTER%"
	CASEELSE
		IF TARGET > 0
			LOCALS += @"%NAME:TARGET%調教中 "
			SIF TALENT:恋慕
				LOCALS += "恋,"
			SIF TALENT:淫乱
				LOCALS += "淫,"
			SIF TALENT:服従
				LOCALS += "服,"
			SIF TALENT:親愛
				LOCALS += "愛,"
			SIF TALENT:娼婦
				LOCALS += "娼,"
			SIF TALENT:隷属
				LOCALS += "隷,"
			SIF TALENT:崩壊
				LOCALS += "崩,"
			LOCALS += @"従{ABL:従順},欲{ABL:欲望},技{ABL:技巧}"
		ENDIF
ENDSELECT

RETURNF LOCALS

@USERLOAD
PRINTL □何番をロードしますか？
PRINTL 　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間

REPEAT 20
	SELECTCASE CHKDATA(COUNT)
		CASE 0
			PRINTFORML 　[{COUNT, 2}] %RESULTS%
		CASE 1
			SETCOLORBYNAME gray
			PRINTFORML 　[{COUNT, 2}] ----　　　　　　　　　　|
			RESETCOLOR
		CASE 2
			SETCOLORBYNAME gray
			PRINTFORML 　[{COUNT, 2}] 別バリアントのセーブデータです
			RESETCOLOR
		CASE 3
			SETCOLORBYNAME gray
			PRINTFORML 　[{COUNT, 2}] 互換性の無いセーブデータです
			RESETCOLOR
		CASE 4
			SETCOLORBYNAME gray
			PRINTFORML 　[{COUNT, 2}] ロードできないセーブデータです
	ENDSELECT
REND
DRAWLINE
FOR COUNT, 94, 100
	SELECTCASE CHKDATA(COUNT)
		CASE 0
			PRINTFORML 　[{COUNT, 2}] %RESULTS%
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAIN 　LOAD　
PRINTL [100] 戻る
INPUT
SELECTCASE RESULT
	CASE 0 TO 19, 94 TO 99
		SELECTCASE CHKDATA(RESULT)
			CASE 0
				;STOPBGM
				LOADDATA RESULT
			CASEELSE
				RESTART
		ENDSELECT
	CASE 100
		RETURN 0
	CASEELSE
		RESTART
ENDSELECT

RETURN 0

