;デバッグ用関数です。スレの>>8が作ってくれました
@DEBUG

CALL 実績解除, "俺はチートマニア！お前は何マニア？"

SIF TARGET == -1
	TARGET = 0

$LOOP_DEBUG

PRINTFORML なにをしますか？　　対象：%NAME:TARGET%
PRINTLC [0] - 対象変更
PRINTLC [1] - 素質操作
PRINTLC [2] - ABL操作
PRINTLC [3] - 経験操作
PRINTLC [4] - 刻印操作
PRINTLC [5] - ITEM操作
PRINTLC [6] - その他
PRINTLC [7] - カジノ テストプレイ
PRINTLC [8] - デバッグ用セーブデータの作成
PRINTLC [9] - 全キャラの売却補正・獲得経験値確認
PRINTL 
PRINTL [-1] 戻る

INPUT

SELECTCASE RESULT
	CASE -1
		SIF TARGET == 0
			TARGET = -1
		RETURN
	CASE 0
		CALL TARGET_CHANGE
	CASE 1
		CALL TALENT_CHANGE
	CASE 2
		CALL ABL_CHANGE
	CASE 3
		CALL EXP_CHANGE
	CASE 4
		CALL MARK_CHANGE
	CASE 5
		CALL ITEM_CHANGE
	CASE 6
		CALL EXTRA_CHANGE
	CASE 7
		CALL GAMBLE
	CASE 8
		CALL DEBUG_SAVEDATA
	CASE 9
		CALL DEBUG_SHOW_CHARADATA
ENDSELECT

GOTO LOOP_DEBUG

;[0] - 対象変更  --------------------------------
@TARGET_CHANGE

;対象を表示
REPEAT CHARANUM
	PRINTFORML [{COUNT + 100}] %NAME:COUNT%
REND
DRAWLINE

INPUT

TARGET = RESULT - 100
PRINTFORMW 対象を%NAME:TARGET%にしました。

RETURN

;[1] - 素質操作  --------------------------------
@TALENT_CHANGE, ARG
LOCAL = 0
DRAWLINE
REPEAT VARSIZE("TALENT")
	SIF TALENTNAME:COUNT == ""
		CONTINUE
	SELECTCASE COUNT
		CASE 0
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □基本素質
			LOCAL = 0
		CASE 10
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □陥落素質
			LOCAL = 0
		CASE 20
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □性格
			LOCAL = 0
		CASE 40
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □性への関心
			LOCAL = 0
		CASE 50
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □乙女心
			LOCAL = 0
		CASE 60
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □体質
			LOCAL = 0
		CASE 70
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □技術
			LOCAL = 0
		CASE 80
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □潔癖度
			LOCAL = 0
		CASE 90
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □正直度
			LOCAL = 0
		CASE 100
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □性愛性
			LOCAL = 0
		CASE 120
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □魅了
			LOCAL = 0
		CASE 130
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □先天的身体特徴
			LOCAL = 0
		CASE 150
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □後天的身体特徴
			LOCAL = 0
		CASE 160
			SIF !LINEISEMPTY()
				PRINTL 
			PRINTL □特殊
			LOCAL = 0
	ENDSELECT
	SIF LOCAL%5 == 0
		PRINT 　
	;素質数値指定
	IF ARG == 1
		PRINTFORM [{COUNT, 3}] %TALENTNAME:COUNT, 12, LEFT% {TALENT:COUNT}
		LOCAL++
	ELSE
		PRINTFORM [{COUNT, 3}] %TALENTNAME:COUNT, 12, LEFT% 
		SELECTCASE TALENTNAME:COUNT
			CASE "性別"
				SELECTCASE SEX(TARGET)
					CASE 1
						PRINT ♂
					CASE 2
						PRINT ♀
				ENDSELECT
			CASEELSE
				IF !TALENT:TARGET:COUNT
					PRINT ×
				ELSE
					PRINT ◯
				ENDIF
		ENDSELECT
		LOCAL++
	ENDIF
	PRINT 　
	SIF LOCAL%5 == 0
		PRINTL 
REND

SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE

PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

SIF TALENTNAME:RESULT == ""
	RESTART

SELECTCASE TALENTNAME:RESULT
	CASE "性別"
		PRINTL [1] - 男にする
		PRINTL [2] - 女にする
		PRINTL [-1] - 戻る

		INPUT
		
		SIF RESULT == -1
			RETURN
		
		SELECTCASE RESULT
			CASE 1, 2
				TALENT:TARGET:性別 = RESULT
		ENDSELECT
		
		RESTART
	CASE "バストサイズ"
		SIF SEX(TARGET) == 1
			RESTART
		PRINTL [1] 絶壁にする
		PRINTL [2] 貧乳にする
		PRINTL [3] 並乳にする
		PRINTL [4] 巨乳にする
		PRINTL [5] 爆乳にする
		INPUT
		DO
		LOOP LOOPRES(1, 2, 3, 4, 5)
		TALENT:バストサイズ = RESULT
		RESTART
	CASE "恋心"
		PRINTL 相手のキャラ番号を入力してください
		$LOOP
		INPUT
		SELECTCASE RESULT
			CASE 0 TO 9999
				IF EXISTCSV(RESULT)
					TALENT:恋心 = RESULT
				ELSE
					PRINTFORMW {RESULT}にはキャラが登録されていません
				ENDIF
			CASE 10000
				TALENT:恋心 = RESULT
			CASEELSE
				GOTO LOOP
		ENDSELECT
		RESTART
	CASE "既婚者"
		PRINTL 相手のキャラ番号を入力してください
		$LOOP2
		INPUT
		SELECTCASE RESULT
			CASE 0 TO 9999
				IF EXISTCSV(RESULT)
					TALENT:既婚者 = RESULT
				ELSE
					PRINTFORMW {RESULT}にはキャラが登録されていません
				ENDIF
			CASE 10000
				TALENT:既婚者 = RESULT
			CASEELSE
				GOTO LOOP2
		ENDSELECT
		RESTART
	CASEELSE
		;数値指定
		IF ARG == 1
			LOCAL = RESULT
			PRINTFORML %TALENTNAME:LOCAL%の数値を入力してください
			INPUT
			TALENT:LOCAL = RESULT
			RESTART
		ELSE
			INVERTBIT TALENT:RESULT, 0
			RESTART
		ENDIF
ENDSELECT

;[2] - ABL操作  --------------------------------
@ABL_CHANGE
DRAWLINE

REPEAT VARSIZE("ABL")
	SIF ABLNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %ABLNAME:COUNT% Lv{ABL:TARGET:COUNT}
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %ABLNAME:RESULT%をいくつにしますか？
PRINTL [0][1][2][3][4]
PRINTL [5][6][7][8][9][10]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

ABL:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%ABLNAME:LOCAL%をLv{RESULT}にしました

;[3] - 経験操作  --------------------------------
@EXP_CHANGE

REPEAT VARSIZE("EXP")
	SIF EXPNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %EXPNAME:COUNT%（{EXP:TARGET:COUNT}）
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %EXPNAME:RESULT%をいくつにしますか？
PRINTL [0][1][9][99][999][9999][99999][999999][9999999]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

EXP:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%EXPNAME:LOCAL%の経験を{RESULT}にしました

;[4] - 刻印操作  --------------------------------
@MARK_CHANGE

REPEAT VARSIZE("MARK")
	SIF MARKNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %MARKNAME:COUNT% Lv{MARK:TARGET:COUNT}
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %MARKNAME:RESULT%をいくつにしますか？
PRINTL [0][1][2][3]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

MARK:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%MARKNAME:LOCAL%をLv{RESULT}にしました

;[5] - ITEM操作  --------------------------------
@ITEM_CHANGE

REPEAT 100
	SIF ITEMNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %ITEMNAME:COUNT% {ITEM:COUNT}個
REND
PRINTL
PRINTL [999] - 全部
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

IF RESULT == 999
	REPEAT 27
		ITEM:COUNT = 1
	REND
	REPEAT 48
		SIF COUNT == 0
			COUNT = 40
		ITEM:COUNT = 99
	REND
	RETURN
ENDIF
LOCAL = RESULT

PRINTFORML %ITEMNAME:RESULT%をいくつにしますか？
PRINTL [0][1][10][50][99]
PRINTL [-1] 戻る

INPUT

SIF RESULT == -1
	RETURN

ITEM:LOCAL = RESULT
PRINTFORML %ITEMNAME:LOCAL%を{RESULT}個にしました

;[6] - その他  --------------------------------
@EXTRA_CHANGE
DRAWLINE
PRINTLC [0] - 日数
PRINTLC [1] - 日付
PRINTLC [2] - 時間帯
PRINTL
PRINTLC [3] - 所持金
PRINTLC [4] - 気温
PRINTLC [-1] - 戻る

INPUT

DRAWLINE
SIF RESULT == -1
	RETURN
IF RESULT == 0
	PRINTFORML 何日にしますか？　　現在の日数：{DAY}
	PRINTL [1][9][99][999][9999][99999][999999]
	PRINTL [-1] 戻る
	INPUT
	SIF RESULT == -1
		RESTART
	DAY = RESULT
	PRINTFORMW {DAY}日目に変更しました
ELSEIF RESULT == 1
	DO
		PRINTFORM どれを変更しますか？　　現在の日付：
		IF YEAR > 0
			PRINTFORM {YEAR}
		ELSE
			PRINTFORM BC{ABS(YEAR)+1}
		ENDIF
		PRINTFORML 年{MONTH}月{DAYS}日(%DOW(ZELLER(YEAR,MONTH,DAYS))%)
		PRINTL [0] - 年
		PRINTL [1] - 月
		PRINTL [2] - 日
		PRINTL [3] - 現在の日時を取得
		PRINTL [-1] - 戻る
		INPUT
		SIF RESULT == -1
			RESTART
		IF RESULT == 0
			PRINTL 何年に変更しますか？値を入力してください
			CALL INPUT_YEAR
			YEAR = RESULT
			PRINTL
			IF YEAR > 0
				PRINTFORM {YEAR}
			ELSE
				PRINTFORM BC{ABS(YEAR)+1}
			ENDIF
			PRINTFORML 年に変更しました
			PRINTL
		ELSEIF RESULT == 1
			PRINTL 何月に変更しますか？
			PRINTL [ 1]１月 [ 2]２月 [ 3]３月 [ 4]４月 [ 5]５月 [ 6]６月
			PRINTL [ 7]７月 [ 8]８月 [ 9]９月 [10]10月 [11]11月 [12]12月
			$MONTH_INPUT
			INPUT
			SELECTCASE RESULT
				CASE 1 TO 12
					MONTH = RESULT
					PRINTL
					PRINTFORML {MONTH}月に変更しました
					PRINTL
				CASEELSE
					GOTO MONTH_INPUT
			ENDSELECT
		ELSEIF RESULT == 2
			PRINTL 何日に変更しますか？値を入力してください
			CALL TWO_DIGIT, 31, "日"
			DAYS = RESULT
			SIF DAYS < 0
				DAYS = 1
			PRINTL
			PRINTFORML {DAYS}日に変更しました
			PRINTL
		ELSEIF RESULT == 3
			GETTIME
			LOCAL = RESULT
			;年月日だけの桁に減らす
			LOCAL /= POWER(10,9)
			YEAR = LOCAL / 10000
			MONTH = LOCAL /100 %100
			DAYS = LOCAL %100
			PRINTL
			PRINTFORML {YEAR}年{MONTH}月{DAYS}日に変更しました
			PRINTL
		ENDIF
		;不正な値の修正
		SIF DAYS <= 0
			DAYS = 1
		SELECTCASE MONTH
			CASE 1, 3, 5, 7, 8, 10, 12
				SIF DAYS > 31
					DAYS = 31
			CASE 4, 6, 9, 11
				SIF DAYS > 30
					DAYS = 30
			CASE 2
				;うるう年かどうかで場合分け
				SELECTCASE YEAR
					;グレゴリオ暦
					CASE IS > 1582
						IF (YEAR%4 == 0 && YEAR%100 != 0) || YEAR%400 == 0
							SIF DAYS > 29
								DAYS = 29
						ELSE
							SIF DAYS > 28
								DAYS = 28
						ENDIF
					;間違った運用がされていた頃のユリウス暦
					CASE -44 TO -5
						IF ABS(YEAR)%3 == 1
							SIF DAYS > 29
								DAYS = 29
						ELSE
							SIF DAYS > 28
								DAYS = 28
						ENDIF
					;上の間違った運用のシワ寄せ期間
					CASE -4 TO 4
						SIF DAYS > 28
							DAYS = 28
					;(紀元前46年以前を含め)残りの期間すべてをユリウス暦とする
					CASEELSE
						IF ABS(YEAR)%4 == 0
							SIF DAYS > 29
								DAYS = 29
						ELSE
							SIF DAYS > 28
								DAYS = 28
						ENDIF
				ENDSELECT
		ENDSELECT
		;1582年10月5日～1582年10月14日は存在しないのでその期間の日付はすべて10月15日にする
		IF YEAR == 1582 && MONTH == 10
			IF 5 <= DAYS && DAYS <= 14
				DAYS = 15
			ENDIF
		ENDIF
	LOOP 1
ELSEIF RESULT == 2
	PRINT いつにしますか？　　現在の時間帯：
	IF TIME == 0
		PRINTL 昼
	ELSE
		PRINTL 夜
	ENDIF
	PRINTL [0] - 昼
	PRINTL [1] - 夜
	PRINTL [-1] - 戻る
	INPUT
	SIF RESULT == -1
		RESTART
	TIME = RESULT
	PRINT 現在の時間帯を
	IF TIME == 0
		PRINTL 昼
	ELSE
		PRINTL 夜
	ENDIF
	PRINTW に変更しました
ELSEIF RESULT == 3
	CALL PASELIL, @"いくらにしますか？　　現在の所持金：{MONEY}＄"
	PRINTLC [0] 無一文
	PRINTLC [10000] 1万
	PRINTLC [1000000] 100万
	PRINTLC [1000000000] 10億
	PRINTLC [1000000000000] 1兆
	PRINTLC [10000000000000000] 1京
	PRINTLC [-1] 戻る
	INPUTS
	IF RESULTS == "BEMANI学円"
		CALL 実績解除, "-5pt"
	ELSEIF ISNUMERIC(RESULTS)
		SIF TOINT(RESULTS) == -1
			RESTART
		MONEY = TOINT(RESULTS)
	ENDIF
	PRINTFORMW 所持金を{MONEY}に変更しました
ELSEIF RESULT == 4
	;今何度？ とか書こうとした
	PRINTL 何度にしますか？　※10倍した値で入力してください
	PRINTFORML 現在の気温：{FLAG:気温/10}.{ABS(FLAG:気温)%10}℃
	PRINTLC [-50] -5℃
	PRINTLC [  0] 0℃
	PRINTLC [ 50] 5℃
	PRINTLC [100] 10℃
	PRINTLC [150] 15℃
	PRINTLC [200] 20℃
	PRINTLC [250] 25℃
	PRINTLC [300] 30℃
	INPUTS
	IF RESULTS == "アペン度" || RESULTS == "So Goo度" || RESULTS == "New Deca度" || RESULTS == "TECH-NOI度"
		CALL 実績解除, "-5pt"
	ELSEIF ISNUMERIC(RESULTS)
		FLAG:気温 = TOINT(RESULTS)
		PRINTFORMW 気温を{FLAG:気温/10}.{ABS(FLAG:気温)%10}℃に変更しました
	ENDIF
ENDIF

RESTART


;年を入力するための関数
@INPUT_YEAR

;LOCALは表示される年
;LOCAL:1は紀元前かどうか
LOCAL = YEAR
IF LOCAL > 0
	LOCAL:1 = 0
ELSE
	LOCAL = ABS(LOCAL) + 1
	LOCAL:1 = 1
ENDIF

DO
	PRINTL
	PRINT 【
	SIF LOCAL:1
		PRINT 紀元前
	PRINTFORML %TOFULL(TOSTR(LOCAL))%年】
	PRINTBUTTON "[+1]", "+1"
	PRINTPLAIN  
	PRINTBUTTON "[+10]", "+10"
	PRINTPLAIN  
	PRINTBUTTON "[+100]", "+100"
	PRINTPLAIN  
	PRINTBUTTON "[+1000]", "+1000"
	PRINTL
	PRINTBUTTON "[-1]", "-1"
	PRINTPLAIN  
	PRINTBUTTON "[-10]", "-10"
	PRINTPLAIN  
	PRINTBUTTON "[-100]", "-100"
	PRINTPLAIN  
	PRINTBUTTON "[-1000]", "-1000"
	PRINTL
	PRINTL [ 0 ][ 1 ][ 2 ][ 3 ][ 4 ]
	PRINTL [ 5 ][ 6 ][ 7 ][ 8 ][ 9 ]
	IF LOCAL:1
		PRINTBUTTON "[ AD/BC ]", "AD"
	ELSE
		PRINTBUTTON "[ AD/BC ]", "BC"
	ENDIF
	PRINTBUTTON "[  AC  ]", "AC"
	PRINTBUTTON "[  OK  ]", "OK"
	INPUTS
	
	IF RESULTS == "" || RESULTS == "OK"
		IF LOCAL:1
			RETURN 1-LOCAL
		ELSE
			RETURN LOCAL
		ENDIF
	ENDIF
	
	SIF RESULTS == "AC"
		LOCAL = 0
	SIF RESULTS == "AD"
		LOCAL:1 = 0
	SIF RESULTS == "BC"
		LOCAL:1 = 1
	SIF RESULTS == "AD/BC"
		INVERTBIT LOCAL:1, 0
	
	SELECTCASE CHARATU(RESULTS, 0)
		CASE "+"
			REPLACE RESULTS, ESCAPE("+"), ""
			IF TOINT(RESULTS) != 0
				RESULT = TOINT(RESULTS)
				IF LOCAL:1
					LOCAL -= RESULT
					IF LOCAL <= 0
						LOCAL:1 = 0
						LOCAL = ABS(LOCAL) + 1
					ENDIF
				ELSE
					LOCAL += RESULT
				ENDIF
			ENDIF
			CONTINUE
		CASE "-"
			REPLACE RESULTS, ESCAPE("-"), ""
			IF TOINT(RESULTS) != 0
				RESULT = TOINT(RESULTS)
				IF LOCAL:1
					LOCAL += RESULT
				ELSE
					LOCAL -= RESULT
					IF LOCAL <= 0
						LOCAL:1 = 1
						LOCAL = ABS(LOCAL) + 1
					ENDIF
				ENDIF
			ENDIF
			CONTINUE
	ENDSELECT
	
	SIF RESULTS != "0" && TOINT(RESULTS) == 0
		CONTINUE
	;見やすさから変数RESULTを使用
	RESULT = TOINT(RESULTS)
	
	;入力値の桁数を計算
	IF RESULT == 0
		LOCAL:2 = 1
	ELSE
		LOCAL:2 = LOG10(RESULT) + 1
	ENDIF
	;表示されている値の桁数を計算
	IF LOCAL == 0
		LOCAL:3 = 1
	ELSE
		LOCAL:3 = LOG10(LOCAL) + 1
	ENDIF
	;18桁に抑える
	SIF LOCAL:2 + LOCAL:3 > 18
		CONTINUE
	LOCAL = LOCAL * POWER(10,LOCAL:2) + RESULT
	
LOOP 1

;[8] - デバッグ用セーブデータの作成  --------------------------------
@DEBUG_SAVEDATA
#DIM DYNAMIC LCOUNT
PRINTL このデータをデバッグ用セーブデータ（所持金MAX、全キャラ購入済み）にします。よろしいですか？
PRINTL [0] - はい
PRINTL [1] - はい（ログを表示する）
PRINTL [2] - いいえ
INPUT
DO
	SIF RESULT == 1
		RETURN
LOOP LOOPRES(0, 1, 2)

;SKIPDISP 1
LOCAL = RESULT
IF RESULT == 1
	FOR LCOUNT, 0, 10000
		IF EXISTCSV(LCOUNT) && NO:MASTER != LCOUNT
			PRINTFORML ＜%CSVNAME(LCOUNT)%を追加中...＞
			CALL CHARA_ADD, LCOUNT
		ENDIF
	NEXT
ELSE
	FOR LCOUNT, 0, 10000
		SIF EXISTCSV(LCOUNT) && NO:MASTER != LCOUNT
			CALL CHARA_ADD, LCOUNT
	NEXT
ENDIF

MONEY = __INT_MAX__

RETURN

@DEBUG_SHOW_CHARADATA
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 売却額補正

FOR LCOUNT, 0, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	SKIPDISP 1
	ADDCHARA LCOUNT
	TARGET = CHARANUM-1
	CALL 売却額補正計算
	売却額補正 = RESULT
	SKIPDISP 0
	PRINTFORML %NAME:TARGET, 30, LEFT% 売却額補正:{売却額補正}\% 獲得経験値:{経験値計算(TARGET)}
	DELCHARA TARGET
NEXT

TARGET = 0
