;調教対象選択(eraBEMANI)
@CHANGE_TARGET
#DIM DYNAMIC LCOUNT

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

DRAWLINE
CALL LIFE_LIST, 1
;RESULT:0には選んだキャラ、RESULT:1には未表示キャラが居るかどうか
DRAWLINE
PRINTPLAIN 　CHANGE TARGET　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 

IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINTL [1010] キャラソート

DO
	INPUT
	SELECTCASE RESULT
		CASE MASTER
			PRINTL 自分を調教することはできません
			RESTART
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 1010
			CALL CHARA_SORT
			RESTART
		CASE 0 TO CHARANUM-1
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			SIF RESULT == 0
				CONTINUE
			CALL ASSIGN_TARGET, RESULT
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

;対象指定用関数
@ASSIGN_TARGET, 対象
#DIM DYNAMIC 対象
TARGET = 対象
SIF TARGET == ASSI
	ASSI = -1
CALL KOJO_MESSAGE_EVENT, "調教対象指定"
;配属してたら自動的に外す
CALL 配属解除, TARGET

@SELECT_ASSI, ARG
#DIM DYNAMIC LTARGET

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

DRAWLINE
CALL LIFE_LIST, 2
;RESULT:0には選んだキャラ、RESULT:1にはまだ表示されてないキャラが居るかどうか
DRAWLINE
PRINTPLAIN 　SELECT ASSI　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 

IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINTL [1010] キャラソート

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 1010
			CALL CHARA_SORT
			RESTART
		CASE 0
			ASSI = -1
			RETURN 0
		CASE 0 TO CHARANUM-1
			SIF CFLAG:RESULT:状態 < 2 && !TALENT:RESULT:アニマル
				CONTINUE
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			CALL ASSIGN_ASSI, RESULT
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

;助手指定用関数
@ASSIGN_ASSI, 助手
#DIM DYNAMIC 助手
ASSI = 助手
CALL KOJO_MESSAGE_EVENT, "助手指定", 助手
;配属してたら自動的に外す
CALL 配属解除, ASSI
;助手にした際の素質変化用関数
CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1
;実績:マタン主人で陥落済みのノクスを助手にする
SIF CHARANAMEF(MASTER) == "マタン・カトルセ" && CHARANAMEF(ASSI) == "ノクス・カトルセ" && CFLAG:ASSI:陥落
	CALL 実績解除, "「姉さんは僕が守る」"

;LIFE_LISTは引数に1を指定すると、調教対象選択用の表示になる
;引数を2にすると、助手選択用の表示になる
;それ以外は通常表示
@LIFE_LIST, ARG, 表示切り替え
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC カウント数
#DIM DYNAMIC 未表示キャラ
#DIM DYNAMIC 対象表示
#DIM DYNAMIC 助手表示
#DIM 表示切り替え
対象表示 = 0
助手表示 = 0
DRAWLINE
IF ゲームモード() == "新婚モード"
	PRINTL □キャラ一覧
ELSE
	SELECTCASE ARG
		CASE 1
			PRINTL □調教対象変更
		CASE 2
			PRINTL □助手変更
		CASEELSE
			PRINTL □奴隷一覧
	ENDSELECT
ENDIF
FOR LCOUNT, 0, CHARANUM
	RESETCOLOR
	;1巡目はMASTERとTARGET及びASSIの能力表示
	IF LCOUNT == 0
		IF ARG == 2
			PRINTL 　[ 0] 助手無し
		ELSE
			PRINTFORM 　[ 0] %CALLNAME:MASTER%(主人) 
			SELECTCASE SEX(MASTER)
				CASE 1
					PRINT ♂ 
				CASE 2
					PRINT ♀ 
			ENDSELECT
			SIF TALENT:MASTER:ふたなり
				PRINT 双
			PRINTL 
		ENDIF
		$表示用
		;TARGETとASSIはMASTERの下に表示する
		IF TARGET > 0 && !対象表示
			LOCAL = TARGET
			対象表示 = 1
			SIF ARG == 2 && CFLAG:状態 < 2 && !TALENT:アニマル
				GOTO 表示用
			GOTO 対象助手
		ENDIF
		IF ASSI > 0 && !助手表示
			LOCAL = ASSI
			助手表示 = 1
			GOTO 対象助手
		ENDIF
		;表示するキャラが居なければDRAWLINE後にLCOUNTを1にして通常の表示処理に戻る
		DRAWLINE
		LCOUNT = 1
		SIF CHARANUM == 1
			BREAK
	ENDIF
	LOCAL = LCOUNT
	;2巡目以降は通常のキャラ表示なので条件を指定
	IF LCOUNT > 0
		;キャラを10人表示したら終了
		IF 表示数 >= 10
			未表示キャラ = 1
			BREAK
		ENDIF
		;助手の指定なら助手不可能なキャラは除外
		SIF ARG == 2 && CFLAG:LOCAL:状態 < 2 && !TALENT:LOCAL:アニマル
			CONTINUE
		;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラは灰色表示
		SIF ARG && CHECK_CHILD_CARE(LOCAL)
			SETCOLORBYNAME gray
		;調教対象の指定、もしくは助手の指定なら、使用不可のキャラは灰色表示
		SIF ARG && CFLAG:LOCAL:使用不可
			SETCOLORBYNAME gray
		;対象は主人の下に表示されるので除外
		SIF TARGET == LOCAL
			CONTINUE
		;助手も同じく
		SIF ASSI == LOCAL
			CONTINUE
		カウント数++
		;ページ数×10まで表示を飛ばす
		SIF TFLAG:ページ数*10 >= カウント数
			CONTINUE
		表示数++
	ENDIF
	
	$対象助手
	IF CFLAG:LOCAL:使用不可
		PRINT 不
	ELSEIF TALENT:LOCAL:アニマル
		PRINT 獣
	ELSEIF CHECK_CHILD_CARE(LOCAL)
		PRINT 妊
	ELSEIF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF CSTR:LOCAL:配属 != ""
		PRINT KA
	ELSEIF ISASSI:LOCAL
		PRINT ●
	ELSEIF CFLAG:LOCAL:状態 >= 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF

	;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラと使用不可キャラは選べない
	IF ARG && (CHECK_CHILD_CARE(LOCAL) || CFLAG:LOCAL:使用不可)
		PRINTFORM [--] %CALLNAME:LOCAL, 20, LEFT%
	ELSE
		PRINTFORM [{LOCAL, 2}] %CALLNAME:LOCAL, 20, LEFT%
	ENDIF

	;上記のキャラを表示したら詳細情報も表示
	;助手選択画面では表示しない
	IF ARG != 2
		;表示切り替え中は経営用の情報表示
		IF 表示切り替え
			PRINT 配属先:
			IF CSTR:LOCAL:配属 != ""
				PRINTFORM %CSTR:LOCAL:配属, 20, LEFT% 
			ELSE
				PRINTFORM %"無し", 20, LEFT% 
			ENDIF
			PRINT モチベーション:
			SELECTCASE CFLAG:LOCAL:モチベーション
				CASE 1 TO 9
					SETCOLORBYNAME gray
					PRINT 皆無
				CASE 10 TO 39
					SETCOLORBYNAME yellow
					PRINT 微妙
				CASE 40 TO 60
					PRINT 普通
				CASE 61 TO 90
					SETCOLORBYNAME cyan
					PRINT 良好
				CASE 91 TO 100
					SETCOLORBYNAME lime
					PRINT 最高
			ENDSELECT
			PRINTFORM {CFLAG:LCOUNT:モチベーション, 3}/100 
			RESETCOLOR
		ELSE
			PRINTFORM 体力%BARBASE("体力", LOCAL, 10), 23, LEFT% 
			PRINTFORM 気力%BARBASE("気力", LOCAL, 10), 23, LEFT% 
		ENDIF
	ENDIF
	SELECTCASE SEX(LOCAL)
		CASE 1
			PRINT ♂ 
		CASE 2
			PRINT ♀ 
	ENDSELECT
	IF ARG == 1
		SIF TALENT:LOCAL:処女
			PRINT 処女 
		SIF TALENT:LOCAL:再生処女
			PRINT 再生処女 
		SIF TALENT:LOCAL:童貞
			PRINT 童貞 
		SIF TALENT:LOCAL:恋慕
			PRINT 恋慕 
		SIF TALENT:LOCAL:淫乱
			PRINT 淫乱 
		SIF TALENT:LOCAL:服従
			PRINT 服従 
		SIF TALENT:LOCAL:親愛
			PRINT 親愛 
		SIF TALENT:LOCAL:娼婦
			PRINT 娼婦 
		SIF TALENT:LOCAL:隷属
			PRINT 隷属 
		SIF TALENT:LOCAL:崩壊
			PRINT 崩壊 
		SIF TALENT:LOCAL:ふたなり
			PRINT 双 
		SIF TALENT:LOCAL:リミットバースト
			PRINT 超 
		SIF TALENT:スライム || TALENT:溶ける快
			PRINT 粘 
	ELSEIF ARG == 2
		SELECTCASE SEX(LOCAL)
			CASE 1
				PRINTFORM 技巧Lv{ABL:LOCAL:技巧} ホモっ気Lv{ABL:LOCAL:ホモっ気} ゲイ中毒Lv{ABL:LOCAL:ゲイ中毒} 
			CASE 2
				PRINTFORM 技巧Lv{ABL:LOCAL:技巧} レズっ気Lv{ABL:LOCAL:レズっ気} レズ中毒Lv{ABL:LOCAL:レズ中毒} 
		ENDSELECT
		SIF TALENT:LOCAL:両刀
			PRINT 両刀 
		SIF TALENT:LOCAL:ふたなり
			PRINT ふたなり
	ELSE
		PRINT ストレス値:
		CALL ストレス表示, LOCAL
	ENDIF
	PRINTL 
	RESETCOLOR
	;対象及び助手が表示できるなら表示
	SIF (TARGET > 0 && !対象表示) || (ASSI > 0 && !助手表示)
		GOTO 表示用
	;一巡目ならDRAWLINEしてCONTINUE
	IF LCOUNT == 0
		DRAWLINE
		CONTINUE
	ENDIF
NEXT
DRAWLINE
PRINTL 　☆：調教中　★：助手　○：助手可　●：元助手
PRINTL 　KA：経営モードに配属中　不：使用不可　妊：妊娠育児中　獣：ペット候補　を表しています

;条件を満たしているにも関わらずまだリスト表示されてないキャラが居れば未表示キャラフラグは1になる
RETURN RESULT, 未表示キャラ

;ステータス表示
@SHOW_CHARADATA
#DIM 表示切り替え
#DIM DYNAMIC LTARGET
DRAWLINE

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

CALL LIFE_LIST, 0, 表示切り替え
;RESULT:0には選んだキャラ、RESULT:1にはまだ表示されてないキャラが居るかどうか
DRAWLINE

PRINTPLAIN 　CHARADATA　
IF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
ELSE
	PRINTPLAIN                
ENDIF

PRINTFORM [1000] 戻る 
IF RESULT:1
	PRINTFORM [1001] 次ページ 
ELSE
	PRINTPLAIN                 
ENDIF

PRINT [1010] キャラソート 
IF 表示切り替え
	PRINTL [1011] 通常表示に切り替え
ELSE
	PRINTL [1011] 経営用表示に切り替え
ENDIF

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1
				TFLAG:ページ数++
		CASE 1010
			CALL CHARA_SORT
			BREAK
		CASE 1011
			INVERTBIT 表示切り替え, 0
			BREAK
		CASE 0 TO CHARANUM-1
			LTARGET = TARGET
			TARGET = RESULT
			IF TARGET == LTARGET
				SHOW_TARGET = LTARGET
			ELSE
				SHOW_TARGET = 0
			ENDIF
			CALL STATUS
			;TARGET変更がされてる場合は戻さない
			SIF !RESULT && LTARGET != ASSI
				TARGET = LTARGET
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

RESTART

@CHARA_SORT
#DIM DYNAMIC LCOUNT
VARSET LOCAL, 0
DRAWLINE
PRINTL □どの要素でソートしますか？
PRINTL 　[1] - 刻印
PRINTL 　[2] - 能力
PRINTL 　[3] - 経験
PRINTL 　[4] - 名前
PRINTL 　[5] - キャラ番号
PRINTL 　[6] - 体力
PRINTL 　[7] - 気力
PRINTL 　[8] - 解禁日
DRAWLINE
PRINTPLAIN 　CHARA SORT　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			DRAWLINE
			PRINTL □刻印
			FOR LCOUNT, 0, 4
				SIF MARKNAME:LCOUNT != ""
					PRINTFORML 　[{LCOUNT}] - %MARKNAME:LCOUNT%
			NEXT
			DRAWLINE
			PRINTPLAIN 　MARK　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 4
						SIF MARKNAME:RESULT == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　MARK　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %MARKNAME:LOCAL%で昇順にソートしました
									SORTCHARA MARK:LOCAL
								CASE 1
									PRINTFORMW %MARKNAME:LOCAL%で降順にソートしました
									SORTCHARA MARK:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 2
			DRAWLINE
			LOCAL = 0
			PRINTL □能力
			FOR LCOUNT, 0, 50
				SIF ABLNAME:LCOUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{LCOUNT, 2}] - %ABLNAME:LCOUNT%
				ELSE
					PRINTFORMLC [{LCOUNT, 2}] - %ABLNAME:LCOUNT%
				ENDIF
				LOCAL++
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　ABL　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 50
						PRINTL □並べ替え
						SIF ABLNAME:RESULT == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %ABLNAME:LOCAL%で昇順にソートしました
									SORTCHARA ABL:LOCAL
								CASE 1
									PRINTFORMW %ABLNAME:LOCAL%で降順にソートしました
									SORTCHARA ABL:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 3
			DRAWLINE
			LOCAL = 0
			PRINTL □経験
			FOR LCOUNT, 0, 100
				SIF EXPNAME:LCOUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{LCOUNT, 2}] - %EXPNAME:LCOUNT%
				ELSE
					PRINTFORMLC [{LCOUNT, 2}] - %EXPNAME:LCOUNT%
				ENDIF
				LOCAL++
			NEXT
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　EXP　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 99
						PRINTL □並べ替え
						SIF EXPNAME:LOCAL == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %EXPNAME:LOCAL%で昇順にソートしました
									SORTCHARA EXP:LOCAL
								CASE 1
									PRINTFORMW %EXPNAME:LOCAL%で降順にソートしました
									SORTCHARA EXP:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 4
			DRAWLINE
			PRINTL □名前
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NAME　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 名前で昇順にソートしました
						SORTCHARA NAME
					CASE 1
						PRINTW 名前で降順にソートしました
						SORTCHARA NAME, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 5
			DRAWLINE
			PRINTL □番号
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NUMBER　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW キャラ番号で昇順にソートしました
						SORTCHARA
					CASE 1
						PRINTW キャラ番号で降順にソートしました
						SORTCHARA BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 6
			DRAWLINE
			PRINTL □体力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　HEALTH　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 体力で昇順にソートしました
						SORTCHARA BASE:体力
					CASE 1
						PRINTW 体力で降順にソートしました
						SORTCHARA BASE:体力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 7
			DRAWLINE
			PRINTL □気力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　VITAL　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 気力で昇順にソートしました
						SORTCHARA BASE:気力
					CASE 1
						PRINTW 気力で降順にソートしました
						SORTCHARA BASE:気力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 8
			FOR LCOUNT, 0, CHARANUM
				ソート用購入日:LCOUNT:0 = BUYY:LCOUNT:0*10000+BUYM:LCOUNT:0*100+BUYD:LCOUNT:0
			NEXT
			PRINTL □解禁日
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　UNLOCK　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 解禁日で昇順にソートしました
						SORTCHARA ソート用購入日:0
					CASE 1
						PRINTW 解禁日で降順にソートしました
						SORTCHARA ソート用購入日:0, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6, 7, 8)

RETURN 0

