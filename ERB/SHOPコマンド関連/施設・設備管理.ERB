@施設
DRAWLINE
PRINTL □管理したい施設・設備を選んでください
SETCOLORBYNAME gray
IF ITEM:医務室
	PRINTDL 　[1] - 医務室
ELSE
	PRINTL 　[-] - 医務室
ENDIF
IF ITEM:実験室
	PRINTDL 　[2] - 実験室
ELSE
	PRINTL 　[-] - 実験室
ENDIF
IF ITEM:図書室
	PRINTDL 　[3] - 図書室
ELSE
	PRINTL 　[-] - 図書室
ENDIF
IF ITEM:音楽室
	PRINTDL 　[4] - 音楽室
ELSE
	PRINTL 　[-] - 音楽室
ENDIF
RESETCOLOR
DRAWLINE
PRINTPLAIN 　施設　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			SIF ITEM:医務室
				CALL 医務室
			BREAK
		CASE 2
			;SIF ITEM:実験室
			;	CALL 実験室
			BREAK
		CASE 3
			SIF ITEM:図書室
				CALL 図書室
			BREAK
		CASE 4
			;SIF ITEM:音楽室
			;	CALL 音楽室
			BREAK
	ENDSELECT
LOOP 1

@医務室
#DIM DYNAMIC 配属人数
#DIM DYNAMIC LCOUNT
配属人数 = CMATCH(CSTR:配属, "医務室")
DRAWLINE
PRINTL □医務室
PRINTL 　医療班を配属させたり、医薬品の補充によってキャラクターの健康管理が可能です
PRINTL 　配属中のキャラの能力に応じて体力・気力・ストレス値の回復量が変動します
PRINT 　医薬品の在庫:
SELECTCASE FLAG:医薬品
	CASE 0
		SETCOLORBYNAME gray
	CASE 1 TO 15
		SETCOLORBYNAME red
	CASE 16 TO 50
		SETCOLORBYNAME yellow
	CASE 51 TO 100
		SETCOLORBYNAME lime
ENDSELECT
RESETCOLOR
CALL PASELIL, @"{FLAG:医薬品}\% 配属人数:{配属人数}/3 所持金:{MONEY}＄"
PRINTL □担当医
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		PRINTFORM 　%NAME:LCOUNT% 
		SIF TALENT:LCOUNT:調合知識
			PRINT [調合知識]
		SIF TALENT:LCOUNT:治療
			PRINT [治療]
		PRINTL 
	ENDIF
NEXT
PRINTL 
PRINTL [1] - キャラを配属させる
;医薬品主人を除くキャラ人数*5000
CALL PASELIL, @"[2] - 医薬品を補充する({(CHARANUM-1)*5000}＄)"
IF 配属人数
	PRINTL [3] - 健康診断を行う
ELSE
	SETCOLORBYNAME gray
	PRINTL [-] - 健康診断を行う(キャラを配属する必要があります)
	RESETCOLOR
ENDIF
DRAWLINE
PRINTPLAIN 　医務室　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			CALL STRATEGY_PERSONAL, "医務室"
			RESTART
		CASE 2
			IF FLAG:医薬品 <= 100
				PRINTW 医薬品は十分に揃っています
			ELSEIF MONEY < (CHARANUM-1)*5000
				MONEY -= (CHARANUM-1)*5000
			ELSE
				PRINTW 所持金が足りません
			ENDIF
		CASE 3
			DRAWLINE
			FOR PAGING, 0, CHARANUM
				;色変えしたいのでここだけ特別処理
				CALL PAGING, PAGING != 0, "健康診断用", "健康診断"
			NEXT
			SIF RESULT == -1
				RESTART
			CALL 健康診断, RESULT
	ENDSELECT
LOOP 1

@健康診断, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC クジャクアイ
;孔雀の瞳を持ってるキャラが主人・助手・担当医に居るかを見る
SIF TALENT:MASTER:孔雀の瞳
	クジャクアイ = 1
SIF ASSI > 0 && TALENT:ASSI:孔雀の瞳
	クジャクアイ = 1
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室" && TALENT:LCOUNT:孔雀の瞳
		クジャクアイ = 1
NEXT

DRAWLINE
PRINTL □患者
PRINTFORML 　名前:%NAME:対象%
PRINTFORML 　体力%BARBASE("体力", 対象, 32)%
PRINTFORML 　気力%BARBASE("気力", 対象, 32)%
PRINT 　ストレス値:
IF クジャクアイ
	SELECTCASE CFLAG:対象:ストレス値
		CASE 1 TO 99
			SETCOLORBYNAME lime
		CASE 100 TO 299
			SETCOLORBYNAME yellow
		CASE 300 TO 499
			SETCOLORBYNAME red
		CASE IS >= 500
			SETCOLORBYNAME darkred
	ENDSELECT
	PRINTFORML {CFLAG:対象:ストレス値}/999
	RESETCOLOR
ELSE
	SELECTCASE CFLAG:対象:ストレス値
		CASE 0
			PRINTL 無
		CASE 1 TO 99
			SETCOLORBYNAME lime
			PRINTL 低
		CASE 100 TO 299
			SETCOLORBYNAME yellow
			PRINTL 中
		CASE 300 TO 499
			SETCOLORBYNAME red
			PRINTL 高
		CASE IS >= 500
			SETCOLORBYNAME darkred
			PRINTL 過多
	ENDSELECT
	RESETCOLOR
ENDIF

PRINTL 

@図書室
#DIM DYNAMIC 補充費
#DIM DYNAMIC 割引率
#DIM DYNAMIC 司書魅力
#DIM DYNAMIC LCOUNT

;本の補充費と司書レベルを計算
CALL 図書室ランク計算
補充費 = 10000
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "図書室" && CFLAG:LCOUNT:配属完了
		;司書の実務経験に応じて減額 一人につき最大1000パセリ
		補充費 -= LIMIT(EXP:LCOUNT:実務経験/10*10, 0, 1000)
		SIF TALENT:LCOUNT:買い物上手
			割引率++
	ENDIF
NEXT
;買い物上手一人につき補充費10％減
補充費 -= 補充費/10*割引率

;最後に*CHARANUM
補充費 *= CHARANUM

DRAWLINE
PRINTL □図書室
PRINTL 　知識の泉を充実させることで、利用者が様々な知識を得られるようになります
PRINTL 　司書を配属することで効率的に新書を補充したり、珍しい奇書を手に入れることができるかもしれません
PRINTFORM 　図書室のランク：{FLAG:図書室ランク}/30 
SIF FLAG:本の最終補充日
	PRINTFORM 本の最終補充日：{FLAG:本の最終補充日/10000}年{FLAG:本の最終補充日/100%100}月{FLAG:本の最終補充日%100}日
PRINTL 
PRINTL □担当司書
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "図書室"
		PRINTFORM 　%NAME:LCOUNT% 
		SIF !CFLAG:LCOUNT:配属完了
			PRINT 【配属準備中】
		SIF TALENT:LCOUNT:買い物上手
			PRINT [買い物上手]
		SIF TALENT:LCOUNT:潔癖症
			PRINT [潔癖症]
		SIF TALENT:LCOUNT:禁断の知識
			PRINT [禁断の知識]
		IF TALENT:LCOUNT:謎の魅力
			PRINT [謎の魅力]
		ELSEIF TALENT:LCOUNT:魅力
			PRINT [魅力]
		ENDIF
		PRINTL 
	ENDIF
NEXT
PRINTL 
CALL PASELIL, @"[0] - 本を補充する（{補充費}＄必要）"
PRINTL [1] - 図書室ランクのボーナスを確認する
PRINTL [2] - 司書を配属する
PRINTL [9] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			IF MONEY < 補充費
				PRINTW 所持金が足りません
				RESTART
			ENDIF
			CALL PASELIL, @"{補充費}＄の経費で本を補充します。よろしいですか？"
			PRINTL [0] はい
			PRINTL [1] いいえ
			INPUT
			DO
				IF RESULT == 0
					;本を補充するとキャラの本への興味が施設ランクに合わせられる
					;どんなにランクが低くても5は保証
					CVARSET CFLAG, GETNUM(CFLAG, "本への興味"), MAX(FLAG:図書室ランク, 5)
					MONEY -= 補充費
					FLAG:本の最終補充日 = YEAR*10000+MONTH*100+DAYS
				ENDIF
			LOOP LOOPRES(0, 1)
			RESTART
		CASE 1
			DRAWLINE
			司書魅力 = 0
			PRINTL □詳細情報
			FOR LCOUNT, 0, CHARANUM
				SIF CSTR:LCOUNT:配属 == "図書室"
					PRINTFORML 　%NAME:LCOUNT%の司書レベル：{司書レベル(LCOUNT)}
				SIF TALENT:LCOUNT:魅力 && TALENT:LCOUNT:謎の魅力
					司書魅力 = 1
			NEXT
			PRINTFORML 　司書一人ごとの能力上限：{30/CMATCH(CSTR:配属, "図書室")}
			PRINTL 
			PRINTFORML 　利用者一人ごとの本の補充費：{補充費/CHARANUM}
			SIF 司書魅力
				PRINTL 　魅力的な司書が居るため利用者増加
			PRINTL □ランクに応じて購入可能な本
			PRINTL 　図書室ランク10以上で購入可能な本
			SIF FLAG:図書室ランク < 10
				SETCOLORBYNAME gray
			FOR LCOUNT, 250, 269
				SIF ITEMNAME:LCOUNT != ""
					PRINTFORML 　　%ITEMNAME:LCOUNT%
			NEXT
			RESETCOLOR
			PRINTL 　図書室ランク20以上で購入可能な本
			SIF FLAG:図書室ランク < 20
				SETCOLORBYNAME gray
			FOR LCOUNT, 270, 299
				SIF ITEMNAME:LCOUNT != ""
					PRINTFORML 　　%ITEMNAME:LCOUNT%
			NEXT
			RESETCOLOR
			PRINTL 上記の本はSHOPのアイテム＞特殊アイテムからお買い求めください
			WAIT
			RESTART
		CASE 2
			CALL STRATEGY_PERSONAL, "図書室"
			RESTART
		CASE 9
			RETURN
	ENDSELECT
LOOP 1

@図書室ランク計算
#DIM DYNAMIC LCOUNT
FLAG:図書室ランク = 0
FOR LCOUNT, 0, CHARANUM
	;司書の実務経験に応じて施設ランク上昇
	;合計最大レベルは30 配属人数に応じて一人頭の能力にキャップを設ける 一人なら30、二人なら15、三人なら10
	SIF CSTR:LCOUNT:配属 == "図書室" && CFLAG:LCOUNT:配属完了
		FLAG:図書室ランク += LIMIT(司書レベル(LCOUNT), 0, 30/CMATCH(CSTR:配属, "図書室"))
NEXT

@司書レベル, ARG
#FUNCTION
#DIM DYNAMIC レベル
;実務経験/100
レベル = EXP:ARG:実務経験/100
;買い物上手 +1
SIF TALENT:ARG:買い物上手
	レベル++
;潔癖症 +2
SIF TALENT:ARG:潔癖症
	レベル += 2
;禁断の知識 +2
SIF TALENT:ARG:禁断の知識
	レベル += 2
;汚れ無視 -1
SIF TALENT:ARG:汚れ無視
	レベル--
;サボり魔 -1
SIF TALENT:ARG:サボり魔
	レベル--

RETURNF レベル

@TURNEND_図書室
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 司書魅力
#DIM DYNAMIC 男性司書
#DIM DYNAMIC 女性司書
#DIM DYNAMIC 意欲
#DIM DYNAMIC 興味
#DIM DYNAMIC 利用者
;まず配属中のキャラの処理
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "図書室"
		;配属完了にする
		CFLAG:LCOUNT:配属完了 = 1
		;実務経験加算
		EXP:LCOUNT:実務経験 += 2
		;習得早ければ+1
		SIF TALENT:LCOUNT:習得早い
			EXP:LCOUNT:実務経験++
		;魅力・謎の魅力持ちなら利用者増える
		SIF TALENT:LCOUNT:謎の魅力 || TALENT:LCOUNT:魅力
			司書魅力++
		;男女数計算
		SELECTCASE SEX(LCOUNT)
			CASE 1
				男性司書++
			CASE 2
				女性司書++
		ENDSELECT
	ENDIF
NEXT

;図書室のランクを計算する
CALL 図書室ランク計算

;ここからは全員の処理
FOR LCOUNT, 0, CHARANUM
	;MASTERは除外
	SIF LCOUNT == MASTER
		CONTINUE
	;知識欲・学習意欲を計算
	意欲 = 1
	SIF TALENT:LCOUNT:大人しい
		意欲++
	SIF TALENT:LCOUNT:好奇心
		意欲++
	SIF TALENT:LCOUNT:悲観的
		意欲++
	SIF TALENT:LCOUNT:恥じらい
		意欲++
	SIF TALENT:LCOUNT:習得早い
		意欲++
	SIF ABL:LCOUNT:欲望 >= 3
		意欲++
	;興味を計算
	興味 = CFLAG:LCOUNT:本への興味
	;魅力・謎の魅力持ちの人数*5
	興味 += 司書魅力*5
	;男好き・女好きなら司書の数*5
	SIF TALENT:LCOUNT:男好き
		興味 += 男性司書*5
	SIF TALENT:LCOUNT:女好き
		興味 += 女性司書*5
	;嫌いなら司書の数*-5
	SIF TALENT:LCOUNT:男嫌い
		興味 -= 男性司書*5
	SIF TALENT:LCOUNT:女嫌い
		興味 -= 女性司書*5
	;{意欲*興味}%の確率で図書室を利用する
	IF RAND(1, 101) <= 意欲*興味
		利用者++
		;利用した場合は{CFLAG:本への興味*20}の習得の珠入手
		;本への興味が皆無で司書目当てでも10は入る
		IF CFLAG:LCOUNT:本への興味 == 0
			JUEL:LCOUNT:習得 += 10
		;習得遅いと半減する
		ELSEIF TALENT:LCOUNT:習得遅い
			JUEL:LCOUNT:習得 += CFLAG:LCOUNT:本への興味*10
		ELSE
			JUEL:LCOUNT:習得 += CFLAG:LCOUNT:本への興味*20
		ENDIF
		;利用した場合は素質に応じて興味が下がる
		SIF TALENT:LCOUNT:反抗的
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:衝動的
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:サボり魔
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:無関心
			CFLAG:LCOUNT:本への興味--
		SIF TALENT:LCOUNT:保守的
			CFLAG:LCOUNT:本への興味--
	ENDIF
	;利用しなくても興味は下がっていく
	CFLAG:LCOUNT:本への興味--
NEXT

DRAWLINE
PRINTFORMW {利用者}人が図書室を利用しました

