@模様替え
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM グリッド表示 = 1
#DIMS DYNAMIC 表示用変数, 100
#DIM DYNAMIC 部屋配置
#DIM DYNAMIC 幅, 2
#DIM DYNAMIC 始点, 2
#DIMS DYNAMIC 部屋情報, 100, 3
#DIMS DYNAMIC オブジェクト情報, 100, 3
#DIM DYNAMIC 設置済み, 100
#DIMS DYNAMIC 部屋名
#DIMS DYNAMIC 表示チップ
#DIM DYNAMIC マップ横幅
#DIM DYNAMIC マップ縦幅
#DIM DYNAMIC 壁無し
#DIM DYNAMIC タイル表示幅
#DIM DYNAMIC タイル始点, 100, 2
#DIM DYNAMIC 生け簀始点, 2

VARLINE = LINECOUNT

LOCAL = 1
;部屋名, X座標の長さ, Y座標の長さ 1次元目の数値は部屋IDとしても扱う 0は欠番
部屋情報:(LOCAL++):0 '= "レッスン室", "7", "5"
部屋情報:(LOCAL++):0 '= "医務室", "5", "4"
部屋情報:(LOCAL++):0 '= "実験室", "5", "4"
部屋情報:(LOCAL++):0 '= "調理室", "8", "6"
部屋情報:(LOCAL++):0 '= "図書室", "12", "8"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "牧場", "12", "12"
部屋情報:(LOCAL++):0 '= "畜舎", "8", "8"
部屋情報:(LOCAL++):0 '= "生け簀", "8", "8"
部屋情報:(LOCAL++):0 '= "養蜂場", "4", "4"

オブジェクト情報:(LOCAL++):0 '= "タコ", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "イカ", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "エビ", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "ウナギ", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "海藻", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "貝類", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "カニ", "4", "4"
オブジェクト情報:(LOCAL++):0 '= "魚", "4", "4"

部屋配置 = 0

VARSET 始点

;SPRITEはここで作成しておく
GDISPOSE 0
GDISPOSE 1
GDISPOSE 2
GDISPOSE 3
GDISPOSE 4
GDISPOSE 5
GDISPOSE 6
GDISPOSE 7
GDISPOSE 8

GCREATEFROMFILE 0, "□.png"
SPRITECREATE "GRID", 0

GCREATEFROMFILE 1, "□.png"
;α値(不透明)+peru
GCLEAR 1, 0xFF000000+COLOR_FROMNAME("peru")
SPRITECREATE "GRID_SNOW", 1

GCREATEFROMFILE 2, "□.png"
;α値(不透明)+yellow
GCLEAR 2, 0xFF000000+COLOR_FROMNAME("yellow")
SPRITECREATE "GRID_OVER", 2

GCREATEFROMFILE 3, "□.png"
;α値(不透明)+saddlebrow
GCLEAR 3, 0xFF000000+COLOR_FROMNAME("saddlebrown")
SPRITECREATE "GRID_WOOD", 3

GCREATEFROMFILE 4, "□.png"
;α値(不透明)+chocolate
GCLEAR 4, 0xFF000000+COLOR_FROMNAME("chocolate")
SPRITECREATE "GRID_GROUND", 4

GCREATEFROMFILE 5, "□.png"
;α値(不透明)+silver
GCLEAR 5, 0xFF000000+COLOR_FROMNAME("silver")
SPRITECREATE "GRID_SILVER", 5

GCREATEFROMFILE 7, "□.png"
;α値(不透明)+gold
GCLEAR 7, 0xFF000000+COLOR_FROMNAME("gold")
SPRITECREATE "GRID_BEEHIVE", 7

GCREATEFROMFILE 8, "□.png"
;α値(不透明)+gray
GCLEAR 8, 0xFF000000+COLOR_FROMNAME("gray")
SPRITECREATE "GRID_GRAY", 8



マップ横幅 = 60
マップ縦幅 = 30

DO
	$RESTART
	VARSET 設置済み
	VARSET タイル始点, -1
	壁無し = 0
	;マップを表示する
	FOR Y座標, 0, マップ縦幅
		VARSET 表示用変数
		FOR X座標, 0, マップ横幅
			SIF X座標 == 0
				表示用変数:X座標 = <nobr>
			IF 部屋配置
				幅:0 = TOINT(部屋情報:部屋配置:1)
				幅:1 = TOINT(部屋情報:部屋配置:2)
			ENDIF
			部屋名 = 
			;重複対策に配置しようとしてる部屋は削除
			SIF 部屋配置 && 土地:Y座標:X座標 == 部屋配置
				土地:Y座標:X座標 = 0
			;始点の座標から計算して配置用の部屋を描画する
			IF 部屋配置 && X座標 >= 始点:0 && Y座標 >= 始点:1 && INRANGE(X座標-始点:0, 0, 幅:0-1) && INRANGE(Y座標-始点:1, 0, 幅:1-1)
				表示用変数:X座標 += "<img src='GRID_OVER'>"
			ELSE
				表示チップ =
				部屋名 = %部屋情報:(土地:Y座標:X座標):0%
				表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
				;まずオブジェクトを表示する
				;IF オブジェクト:Y座標:X座標 != ""
				;	;始点(左上)を記録しておく
				;	IF タイル始点:(オブジェクト:Y座標:X座標):0 == -1
				;		タイル始点:(オブジェクト:Y座標:X座標):0 = X座標
				;		タイル始点:(オブジェクト:Y座標:X座標):1 = Y座標
				;	ENDIF
				;その土地に部屋が設置してあったら対応した色で表示する
				IF 土地:Y座標:X座標 != 0
					;始点(左上)を記録しておく
					IF タイル始点:(土地:Y座標:X座標):0 == -1
						タイル始点:(土地:Y座標:X座標):0 = X座標
						タイル始点:(土地:Y座標:X座標):1 = Y座標
					ENDIF
					;設置済み判定に始点も入れておく
					設置済み:(土地:Y座標:X座標) = Y座標*100+X座標
					SELECTCASE 部屋名
						CASE "レッスン室", "調理室", "図書室", "畜舎"
							表示チップ = GRID_WOOD
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "医務室", "実験室"
							表示チップ = GRID_SILVER
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "畑", "田んぼ"
							表示チップ = GRID_GROUND
							壁無し = 1
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "プランター"
							表示チップ = GRID_GRAY
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "牧場"
							;牧場 12*12
							表示チップ = GRID_GRASS
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "生け簀"
							;生け簀は8*8
							;養殖中ならその画像を表示
							DEBUGPRINTFORML {X座標}*{Y座標} {X座標-タイル始点:(土地:Y座標:X座標):0} {Y座標-タイル始点:(土地:Y座標:X座標):1}

							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 3
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 4
							ELSE
								LOCAL = 0
							ENDIF
							;始点から4つ区切りで表示
							IF 生け簀の養殖:LOCAL != ""
								DEBUGPRINTL OK
								SELECTCASE 生け簀の養殖:LOCAL
									CASE "タコ"
										表示用変数:X座標 += "<img src='タコ'"
									CASE "イカ"
										表示用変数:X座標 += "<img src='イカ'"
									CASE "エビ"
										表示用変数:X座標 += "<img src='エビ'"
									CASE "ウナギ"
										表示用変数:X座標 += "<img src='ウナギ'"
									CASE "貝類"
										表示用変数:X座標 += "<img src='貝類'"
									CASE "海藻"
										表示用変数:X座標 += "<img src='海藻'"
									CASE "カニ"
										表示用変数:X座標 += "<img src='カニ'"
									CASEELSE
										表示用変数:X座標 += "<img src='魚'"
								ENDSELECT
								表示用変数:X座標 += " srcb='' height='400' width='400'>"
							ENDIF
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%4 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%4 == 0
								表示チップ = GRID_WATER
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'>"
							ENDIF
						CASE "養蜂場"
							表示チップ = GRID_BEEHIVE
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
					ENDSELECT							
				ELSEIF グリッド表示
					表示チップ = GRID
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ELSE
					表示チップ = GRID_SNOW
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ENDIF
				表示用変数:X座標 += "</button>"
				;壁の確認 端でも壁表示
				IF !壁無し
					;左壁
					IF X座標 == 0 || 土地:Y座標:(X座標-1) != 土地:Y座標:X座標
						;上壁もあれば┌壁
						IF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┌壁'></nonbutton>"
						;下壁もあれば└壁
						ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='└壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='左壁'></nonbutton>"
						ENDIF
					;上壁
					ELSEIF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
						;右壁もあれば┐壁
						IF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┐壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='上壁'></nonbutton>"
						ENDIF
					;右壁
					ELSEIF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
						;下壁もあれば┘壁
						IF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┘壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='右壁'></nonbutton>"
						ENDIF
					;下壁
					ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='下壁'></nonbutton>"
					ENDIF
				ENDIF
			ENDIF
		NEXT
		SIF X座標 == マップ横幅-1
			表示用変数:X座標 += "</nobr>"
		HTML_PRINT STRJOIN(表示用変数, "")
	NEXT
	PRINTL 

	IF 部屋配置
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [7] 左回転　　　　 [9] 右回転
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUT

		SELECTCASE RESULT
			CASE 0
				部屋配置 = 0
			CASE 8
				SIF 始点:1 > 0
					始点:1--
			CASE 4
				SIF 始点:0 > 0
					始点:0--
			CASE 6
				SIF 始点:0+幅:0 < マップ横幅 
					始点:0++
			CASE 2
				SIF 始点:1+幅:1 < マップ縦幅
					始点:1++
			;四角形だから同義
			CASE 7, 9
				SWAP 部屋情報:部屋配置:1, 部屋情報:部屋配置:2
			CASE 5
				CLEARLINE LINECOUNT-VARLINE
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					SIF LCOUNT >= マップ縦幅
						GOTO RESTART
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF LCOUNT2 >= マップ横幅
							GOTO RESTART
						SIF 土地:LCOUNT:LCOUNT2 != 0
							GOTO RESTART
					NEXT
				NEXT
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						土地:LCOUNT:LCOUNT2 = 部屋配置
					NEXT
				NEXT
				部屋配置 = 0
				GOTO RESTART
		ENDSELECT

		CLEARLINE LINECOUNT-VARLINE
		CONTINUE
	ELSE
		PRINTL [50] - 部屋を配置する [51] - 部屋を初期化する
		PRINTFORML [52]  - グリッド表示\@ グリッド表示 ? しない # する \@ [60] 戻る
		INPUT
		SELECTCASE RESULT
			CASE 60
				RETURN
			CASE 50
				PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
				FOR LCOUNT, 1, VARSIZE("部屋情報", 0)
					SIF 部屋情報:LCOUNT:0 == ""
						BREAK
					PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
				NEXT
				SIF !LINEISEMPTY()
					PRINTL 
				DRAWLINE
				PRINTL [0] 戻る
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							BREAK
						CASE 1 TO VARSIZE("部屋情報", 0)
							部屋配置 = RESULT
							IF 設置済み:LCOUNT
								始点:0 = (設置済み:LCOUNT)%100
								始点:1 = (設置済み:LCOUNT)/100
							ENDIF
							BREAK
					ENDSELECT
				LOOP 1
				CONTINUE
			CASE 51
				PRINTL 部屋情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET 土地
				CONTINUE
			CASE 52
				INVERTBIT グリッド表示, 0
				CONTINUE
		ENDSELECT
	ENDIF
LOOP 1





