@模様替え
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM DYNAMIC グリッド表示 = 1
#DIMS DYNAMIC 表示用変数, 40
#DIM DYNAMIC 部屋配置
#DIM DYNAMIC 幅, 2
#DIM DYNAMIC 始点, 2
#DIMS DYNAMIC 部屋情報, 100, 5
#DIM DYNAMIC 設置済み, 100
#DIMS DYNAMIC 部屋名
#DIMS DYNAMIC 表示チップ

VARLINE = LINECOUNT

LOCAL = 1
;部屋名, X座標の長さ, Y座標の長さ 1次元目の数値は部屋IDとしても扱う 0は欠番
部屋情報:(LOCAL++):0 '= "レッスン室", "7", "5"
部屋情報:(LOCAL++):0 '= "医務室", "5", "4"
部屋情報:(LOCAL++):0 '= "実験室", "5", "4"
部屋情報:(LOCAL++):0 '= "調理室", "8", "6"
部屋情報:(LOCAL++):0 '= "図書室", "12", "8"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "プランター", "2", "2"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "牧場", "12", "12"
部屋情報:(LOCAL++):0 '= "畜舎", "8", "8"
部屋情報:(LOCAL++):0 '= "生け簀", "8", "8"
部屋情報:(LOCAL++):0 '= "養蜂場", "4", "4"

部屋配置 = 0

VARSET 始点

;SPRITEはここで作成しておく
GDISPOSE 0
GDISPOSE 1
GDISPOSE 2
GDISPOSE 3

GCREATEFROMFILE 0, "□.png"
SPRITECREATE "GRID", 0

GCREATEFROMFILE 1, "□.png"
;α値(不透明)+snow
GCLEAR 1, 0xFF000000+COLOR_FROMNAME("snow")
SPRITECREATE "GRID_SNOW", 1

GCREATEFROMFILE 2, "□.png"
;α値(不透明)+yellow
GCLEAR 2, 0xFF000000+COLOR_FROMNAME("yellow")
SPRITECREATE "GRID_OVER", 2

GCREATEFROMFILE 3, "□.png"
GCLEAR 3, 0xFF000000+COLOR_FROMNAME("gold")
SPRITECREATE "GRID_GOLD", 3

DO
	$RESTART
	VARSIZE 設置済み
	;40*40のマップを表示する
	FOR Y座標, 0, 40
		VARSET 表示用変数
		FOR X座標, 0, 40
			SIF X座標 == 0
				表示用変数:X座標 = <nobr>
			IF 部屋配置
				幅:0 = TOINT(部屋情報:部屋配置:1)
				幅:1 = TOINT(部屋情報:部屋配置:2)
			ENDIF
			部屋名 = 
			;始点の座標から計算して配置用の部屋を描画する
			IF 部屋配置 && X座標 >= 始点:0 && Y座標 >= 始点:1 && INRANGE(X座標-始点:0, 0, 幅:0-1) && INRANGE(Y座標-始点:1, 0, 幅:1-1)
				表示用変数:X座標 += "<img src='GRID_OVER'>"
			ELSE
				;その土地に部屋が設置してあったら対応した色で表示する
				IF 土地:Y座標:X座標 != 0
					部屋名 = %部屋情報:(土地:Y座標:X座標):0%
					;設置済み判定に始点も入れておく
					設置済み:(土地:Y座標:X座標) = Y座標*100+X座標
					表示チップ = GRID_GOLD
				ELSEIF グリッド表示
					表示チップ = GRID
				ELSE
					表示チップ = GRID_SNOW
				ENDIF
				表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'><img src='%表示チップ%'></button>"
				;壁の確認 端でも壁表示
				;左壁
				IF X座標 == 0 || 土地:Y座標:(X座標-1) != 土地:Y座標:X座標
					;上壁もあれば┌壁
					IF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┌壁'></nonbutton>"
					;下壁もあれば└壁
					ELSEIF Y座標 == 39 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='└壁'></nonbutton>"
					ELSE
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='左壁'></nonbutton>"
					ENDIF
				;上壁
				ELSEIF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
					;右壁もあれば┐壁
					IF X座標 == 39 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┐壁'></nonbutton>"
					ELSE
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='上壁'></nonbutton>"
					ENDIF
				;右壁
				ELSEIF X座標 == 39 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
					;下壁もあれば┘壁
					IF Y座標 == 39 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┘壁'></nonbutton>"
					ELSE
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='右壁'></nonbutton>"
					ENDIF
				;下壁
				ELSEIF Y座標 == 39 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
					表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='下壁'></nonbutton>"
				ENDIF
			ENDIF
		NEXT
		SIF X座標 == 39
			表示用変数:X座標 += "</nobr>"
		HTML_PRINT STRJOIN(表示用変数, "")
	NEXT
	PRINTL 

	IF 部屋配置
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUT

		SELECTCASE RESULT
			CASE 0
				部屋配置 = 0
			CASE 8
				始点:1--
			CASE 4
				始点:0--
			CASE 6
				始点:0++
			CASE 2
				始点:1++
			CASE 5
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF 土地:LCOUNT:LCOUNT2 != 0
							GOTO RESTART
					NEXT
				NEXT
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						土地:LCOUNT:LCOUNT2 = 部屋配置
					NEXT
				NEXT
				部屋配置 = 0
		ENDSELECT

		CLEARLINE LINECOUNT-VARLINE
		CONTINUE
	ELSE
		PRINTL [50] - 部屋を配置する [51] - 部屋を初期化する
		PRINTFORML [52]  - グリッド表示\@ グリッド表示 ? しない # する \@ [60] 戻る
		INPUT
		SELECTCASE RESULT
			CASE 60
				RETURN
			CASE 50
				PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
				FOR LCOUNT, 1, VARSIZE("部屋情報", 0)
					SIF 部屋情報:LCOUNT:0 == ""
						BREAK
					PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
				NEXT
				SIF !LINEISEMPTY()
					PRINTL 
				DRAWLINE
				PRINTL [0] 戻る
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							BREAK
						CASE 1 TO VARSIZE("部屋情報", 0)
							部屋配置 = RESULT
							IF 設置済み:LCOUNT
								始点:0 = (設置済み:LCOUNT)%100
								始点:1 = (設置済み:LCOUNT)/100
							ENDIF
							BREAK
					ENDSELECT
				LOOP 1
				CONTINUE
			CASE 51
				PRINTL 部屋情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET 土地
				CONTINUE
			CASE 52
				INVERTBIT グリッド表示, 0
				CONTINUE
		ENDSELECT
	ENDIF
LOOP 1





