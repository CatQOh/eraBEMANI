;調教対象選択(eraBEMANI)
@CHANGE_TARGET, ARG
;ARG = 調教可能なキャラ数
VARSET LOCAL, 0
;調教可能キャラの終端を確認
REPEAT CHARANUM
	SIF COUNT != MASTER && !CHECK_CHILD_CARE(COUNT) && !CFLAG:COUNT:使用不可
		LOCAL = COUNT
REND
DRAWLINE
CALL LIFE_LIST, 1
;RESULT:0には選んだキャラ、RESULT:1にはこのリストでどのキャラまで表示したか
DRAWLINE
PRINTPLAIN 　CHANGE TARGET　
SIF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
PRINTFORM [1000] 戻る 
;調教可能キャラの終端と、RESULT:1を参照して次ページ表示できるかの判定
SIF LOCAL > RESULT:1
	PRINTFORM [1001] 次ページ
DO
	INPUT
	SELECTCASE RESULT
		CASE MASTER
			PRINTL 自分を調教することはできません
			RESTART
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF LOCAL > RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 0 TO CHARANUM-1
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			SIF RESULT != 0
				TARGET = RESULT
			SIF TARGET == ASSI
				ASSI = -1
			CALL KOJO_MESSAGE_EVENT, 24
			;配属してたら自動的に外す
			CFLAG:配属 = 0
			CFLAG:配属完了 = 0
			CFLAG:ゲーセン店長 = 0
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

@SELECT_ASSI, ARG
#DIM LTARGET
;ARG = 助手可能なキャラ数
VARSET LOCAL, 0
;助手可能キャラの終端を確認
REPEAT CHARANUM
	SIF COUNT != MASTER && !CHECK_CHILD_CARE(COUNT) && CFLAG:COUNT:状態 >= 2
		LOCAL = COUNT
REND
DRAWLINE
CALL LIFE_LIST, 2
;RESULT:0には選んだキャラ、RESULT:1にはこのリストでどのキャラまで表示したか
DRAWLINE
PRINTPLAIN 　SELECT ASSI　
SIF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
PRINTFORM [1000] 戻る 
;助手可能キャラの終端と、RESULT:1を参照して次ページ表示できるかの判定
SIF LOCAL > RESULT:1
	PRINTFORM [1001] 次ページ
DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
			RESTART
		CASE 1000
			RETURN 0
		CASE 1001
			SIF LOCAL > RESULT:1
				TFLAG:ページ数++
			RESTART
		CASE 0
			ASSI = -1
			RETURN 0
		CASE 0 TO CHARANUM-1
			SIF CFLAG:RESULT:状態 < 2
				CONTINUE
			SIF CHECK_CHILD_CARE(RESULT)
				CONTINUE
			SIF CFLAG:RESULT:使用不可
				CONTINUE
			ASSI = RESULT
			CALL KOJO_MESSAGE_EVENT, 25, RESULT
			;配属してたら自動的に外す
			CFLAG:ASSI:配属 = 0
			CFLAG:ASSI:配属完了 = 0
			CFLAG:ASSI:ゲーセン店長 = 0
			;助手にした際の素質変化用関数
			CALL ASSI_CHANGE
			ISASSI:ASSI = 1
			SIF ASSI == TARGET
				TARGET = -1
			BREAK
			;実績:マタン主人で陥落済みのノクスを助手にする
			SIF NO:MASTER != 0 && NO:ASSI != 0 && NAME:MASTER == "マタン・カトルセ" && NAME:ASSI == "ノクス・カトルセ" && CFLAG:ASSI:陥落
				CALL 実績解除, "「姉さんは僕が守る」"
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

;LIFE_LISTは引数に1を指定すると、調教対象選択用の表示になる
;引数を2にすると、助手選択用の表示になる
;それ以外は通常表示
@LIFE_LIST, ARG
#DIM LCOUNT
VARSET LOCAL, 0
DRAWLINE
IF ARG == 1
	PRINTL □調教対象変更
ELSEIF ARG == 2
	PRINTL □助手変更
ELSE
	PRINTL □奴隷一覧
ENDIF
IF ARG == 2
	PRINTL 　[ 0]助手無し
ELSE
	PRINTFORM 　[ 0]%CALLNAME:MASTER%(主人) 
	SELECTCASE SEX(MASTER)
		CASE 1
			PRINT ♂ 
		CASE 2
			PRINT ♀ 
	ENDSELECT
	SIF TALENT:MASTER:ふたなり
		PRINT 双
	PRINTL 
ENDIF
;調教対象の選択で、調教中のキャラが居るなら主人の下に表示
IF ARG == 1 && TARGET > 0
	PRINTFORM ☆[{TARGET, 2}]%CALLNAME:TARGET, 20, LEFT%
	LOCAL = TARGET
ENDIF
;助手の選択で、助手中のキャラが居るなら主人の下に表示
IF ARG == 2 && ASSI > 0
	PRINTFORM ★[{ASSI, 2}]%CALLNAME:ASSI, 20, LEFT%
	LOCAL = ASSI
ENDIF
;上記のキャラを表示したら詳細情報も表示
IF LOCAL == TARGET
	PRINT 体力
	BAR BASE:LOCAL:体力, MAXBASE:LOCAL:体力, 10
	PRINTFORM ({BASE:LOCAL:体力}/{MAXBASE:LOCAL:体力}) 
	SELECTCASE SEX(LOCAL)
		CASE 1
			PRINT ♂ 
		CASE 2
			PRINT ♀ 
	ENDSELECT
	SIF TALENT:LOCAL:処女
		PRINT 処女 
	SIF TALENT:LOCAL:再生処女
		PRINT 再生処女 
	SIF TALENT:LOCAL:童貞
		PRINT 童貞 
	SIF TALENT:LOCAL:恋慕
		PRINT 恋慕 
	SIF TALENT:LOCAL:淫乱
		PRINT 淫乱 
	SIF TALENT:LOCAL:服従
		PRINT 服従 
	SIF TALENT:LOCAL:親愛
		PRINT 親愛 
	SIF TALENT:LOCAL:娼婦
		PRINT 娼婦 
	SIF TALENT:LOCAL:隷属
		PRINT 隷属 
	SIF TALENT:LOCAL:崩壊
		PRINT 崩壊 
	SIF TALENT:LOCAL:ふたなり
		PRINT 双 
	SIF TALENT:LOCAL:リミットバースト
		PRINT 超
	PRINTL 
ELSEIF LOCAL == ASSI
	SELECTCASE SEX(LOCAL)
		CASE 1
			PRINTFORM ♂ 技巧Lv{ABL:LOCAL:技巧} ホモっ気Lv{ABL:LOCAL:ホモっ気} ゲイ中毒Lv{ABL:LOCAL:ゲイ中毒} 
		CASE 2
			PRINTFORM ♀ 技巧Lv{ABL:LOCAL:技巧} レズっ気Lv{ABL:LOCAL:レズっ気} レズ中毒Lv{ABL:LOCAL:レズ中毒} 
	ENDSELECT
	SIF TALENT:LOCAL:両刀
		PRINT 両刀 
	SIF TALENT:LOCAL:ふたなり
		PRINT ふたなり
	PRINTL 
ENDIF
DRAWLINE
FOR LCOUNT, 0, CHARANUM
	;キャラを10人表示したら終了
	IF LOCAL:1 >= 10
		LOCAL:3 = 1
		BREAK
	ENDIF

	;主人は除外
	SIF LCOUNT == MASTER
		CONTINUE
	;調教対象の指定なら調教中のキャラは除外
	SIF ARG == 1 && LCOUNT == TARGET
		CONTINUE
	;助手の指定なら助手中のキャラは除外
	SIF ARG == 2 && LCOUNT == ASSI
		CONTINUE
	;助手の指定なら助手不可能なキャラは除外
	SIF ARG == 2 && CFLAG:LCOUNT:状態 < 2
		CONTINUE
	;調教対象の指定、もしくは助手の指定なら、育児部屋に移動しているキャラは除外
	SIF ARG && CHECK_CHILD_CARE(LCOUNT)
		CONTINUE
	;調教対象の指定、もしくは助手の指定なら、使用不可のキャラは除外
	SIF ARG && CFLAG:LCOUNT:使用不可
		CONTINUE

	LOCAL:2++
	SIF TFLAG:ページ数*10 >= LOCAL:2
		CONTINUE

	IF LCOUNT == TARGET
		PRINT ☆
	ELSEIF LCOUNT == ASSI
		PRINT ★
	ELSEIF CFLAG:LCOUNT:配属
		PRINT KA
	ELSEIF ISASSI:LCOUNT
		PRINT ●
	ELSEIF CFLAG:LCOUNT:状態 >= 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	PRINTFORM [{LCOUNT, 2}]%CALLNAME:LCOUNT,20,LEFT%
	IF ARG != 2
		PRINT 体力
		BAR BASE:LCOUNT:体力, MAXBASE:LCOUNT:体力, 10
		PRINTFORM ({BASE:LCOUNT:体力}/{MAXBASE:LCOUNT:体力}) 
		SELECTCASE SEX(LCOUNT)
			CASE 1
				PRINT ♂ 
			CASE 2
				PRINT ♀ 
		ENDSELECT
		SIF TALENT:LCOUNT:処女
			PRINT 処女 
		SIF TALENT:LCOUNT:再生処女
			PRINT 再生処女 
		SIF TALENT:LCOUNT:童貞
			PRINT 童貞 
		SIF TALENT:LCOUNT:恋慕
			PRINT 恋慕 
		SIF TALENT:LCOUNT:淫乱
			PRINT 淫乱 
		SIF TALENT:LCOUNT:服従
			PRINT 服従 
		SIF TALENT:LCOUNT:親愛
			PRINT 親愛 
		SIF TALENT:LCOUNT:娼婦
			PRINT 娼婦 
		SIF TALENT:LCOUNT:隷属
			PRINT 隷属 
		SIF TALENT:LCOUNT:崩壊
			PRINT 崩壊 
		SIF TALENT:LCOUNT:ふたなり
			PRINT 双 
		SIF TALENT:LCOUNT:リミットバースト
			PRINT 超
		PRINTL 
	ELSEIF ARG == 2
		SELECTCASE SEX(LCOUNT)
			CASE 1
				PRINTFORM ♂ 技巧Lv{ABL:LCOUNT:技巧} ホモっ気Lv{ABL:LCOUNT:ホモっ気} ゲイ中毒Lv{ABL:LCOUNT:ゲイ中毒} 
			CASE 2
				PRINTFORM ♀ 技巧Lv{ABL:LCOUNT:技巧} レズっ気Lv{ABL:LCOUNT:レズっ気} レズ中毒Lv{ABL:LCOUNT:レズ中毒} 
		ENDSELECT
		SIF TALENT:LCOUNT:両刀
			PRINT 両刀 
		SIF TALENT:LCOUNT:ふたなり
			PRINT ふたなり
		PRINTL 
	ENDIF
	LOCAL:1++
NEXT
;FOR-NEXTをBREAKせずに抜けた場合、LCOUNTには「リスト最後のキャラ登録番号+1(=CHARANUM)」が
;BREAKで抜けた場合には「リスト最後のキャラ登録番号+2」が代入されている
IF LOCAL:3
	LCOUNT -= 2
ELSE
	LCOUNT -= 1
ENDIF

DRAWLINE
PRINTL 　☆：調教中　★：助手　○：助手可　●：元助手　KA：経営モードに配属中　を表しています

RETURN RESULT, LCOUNT

;ステータス表示
@SHOW_CHARADATA
DRAWLINE
CALL LIFE_LIST
;RESULT:0には選んだキャラ、RESULT:1にはこのリストでどのキャラまで表示したか
DRAWLINE

PRINTPLAIN 　CHARADATA　
SIF TFLAG:ページ数 > 0
	PRINTFORM [999] 前ページ 
PRINTFORM [1000] 戻る 
SIF RESULT:1 < CHARANUM-1
	PRINTFORM [1001] 次ページ 

PRINTL [1010] キャラソート

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF TFLAG:ページ数 > 0
				TFLAG:ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			SIF RESULT:1 < CHARANUM-1
				TFLAG:ページ数++
		CASE 1010
			CALL CHARA_SORT
			BREAK
		CASE 0 TO CHARANUM-1
			CALL STATUS, RESULT
			BREAK
	ENDSELECT
LOOP LOOPRES(999, 1000, 1001)

RESTART

@CHARA_SORT
VARSET LOCAL, 0
DRAWLINE
PRINTL □どの要素でソートしますか？
PRINTL 　[1] - 刻印
PRINTL 　[2] - 能力
PRINTL 　[3] - 経験
PRINTL 　[4] - 名前
PRINTL 　[5] - キャラ番号
PRINTL 　[6] - 体力
PRINTL 　[7] - 気力
DRAWLINE
PRINTPLAIN 　CHARA SORT　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			DRAWLINE
			PRINTL □刻印
			REPEAT 4
				SIF MARKNAME:COUNT != ""
					PRINTFORML 　[{COUNT}] - %MARKNAME:COUNT%
			REND
			DRAWLINE
			PRINTPLAIN 　MARK　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 4
						SIF MARKNAME:RESULT == ""
							CONTINUE
						PRINTL □並べ替え
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　MARK　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %MARKNAME:LOCAL%で昇順にソートしました
									SORTCHARA MARK:LOCAL
								CASE 1
									PRINTFORMW %MARKNAME:LOCAL%で降順にソートしました
									SORTCHARA MARK:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 2
			DRAWLINE
			LOCAL = 0
			PRINTL □能力
			REPEAT 50
				SIF ABLNAME:COUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{COUNT, 2}] - %ABLNAME:COUNT%
				ELSE
					PRINTFORMLC [{COUNT, 2}] - %ABLNAME:COUNT%
				ENDIF
				LOCAL++
			REND
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　ABL　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 50
						PRINTL □並べ替え
						SIF ABLNAME:RESULT == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %ABLNAME:LOCAL%で昇順にソートしました
									SORTCHARA ABL:LOCAL
								CASE 1
									PRINTFORMW %ABLNAME:LOCAL%で降順にソートしました
									SORTCHARA ABL:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 3
			DRAWLINE
			LOCAL = 0
			PRINTL □経験
			REPEAT 100
				SIF EXPNAME:COUNT == ""
					CONTINUE
				IF LOCAL == 0 || LOCAL % 4 == 0
					PRINTFORMLC 　[{COUNT, 2}] - %EXPNAME:COUNT%
				ELSE
					PRINTFORMLC [{COUNT, 2}] - %EXPNAME:COUNT%
				ENDIF
				LOCAL++
			REND
			SIF LINEISEMPTY() == 0
				PRINTL 
			DRAWLINE
			PRINTPLAIN 　EXP　
			PRINTL [100] - 戻る
			DO
				INPUT
				LOCAL = RESULT
				SELECTCASE RESULT
					CASE 0 TO 99
						PRINTL □並べ替え
						SIF EXPNAME:LOCAL == ""
							CONTINUE
						PRINTL 　[0] - 昇順
						PRINTL 　[1] - 降順
						DRAWLINE
						PRINTPLAIN 　ABL　
						PRINTL [10] - 戻る
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTFORMW %EXPNAME:LOCAL%で昇順にソートしました
									SORTCHARA EXP:LOCAL
								CASE 1
									PRINTFORMW %EXPNAME:LOCAL%で降順にソートしました
									SORTCHARA EXP:LOCAL, BACK
							ENDSELECT
						LOOP LOOPRES(0, 1, 10)
						BREAK
				ENDSELECT
			LOOP LOOPRES(100)
			RETURN 0
		CASE 4
			DRAWLINE
			PRINTL □名前
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NAME　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 名前で昇順にソートしました
						SORTCHARA NAME
					CASE 1
						PRINTW 名前で降順にソートしました
						SORTCHARA NAME, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 5
			DRAWLINE
			PRINTL □番号
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　NUMBER　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW キャラ番号で昇順にソートしました
						SORTCHARA
					CASE 1
						PRINTW キャラ番号で降順にソートしました
						SORTCHARA BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 6
			DRAWLINE
			PRINTL □体力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　HEALTH　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 体力で昇順にソートしました
						SORTCHARA BASE:体力
					CASE 1
						PRINTW 体力で降順にソートしました
						SORTCHARA BASE:体力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
		CASE 7
			DRAWLINE
			PRINTL □気力
			PRINTL 　[0] - 昇順
			PRINTL 　[1] - 降順
			DRAWLINE
			PRINTPLAIN 　VITAL　
			PRINTL [10] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 気力で昇順にソートしました
						SORTCHARA BASE:気力
					CASE 1
						PRINTW 気力で降順にソートしました
						SORTCHARA BASE:気力, BACK
				ENDSELECT
			LOOP LOOPRES(0, 1, 10)
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6, 7)

RETURN 0

;引数の条件に一致するキャラをページ形式で表示する
@PAGING, 論理演算, キャラ変数, 変数名, 条件, キャラ変数:1, 変数名:1, 条件:1, キャラ変数:2, 変数名:2, 条件:2, キャラ変数:3, 変数名:3, 条件:3, キャラ変数:4, 変数名:4, 条件:4, キャラ変数:5, 変数名:5, 条件:5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした人数
#DIM DYNAMIC 条件チェック
#DIM DYNAMIC キャラ
#DIM DYNAMIC 未表示キャラ
#DIMS DYNAMIC キャラ変数, 6
#DIMS DYNAMIC 変数名, 6
#DIMS DYNAMIC 条件, 6
#DIM DYNAMIC 条件ID
#DIMS DYNAMIC 論理演算

VARSET LOCAL, 0
飛ばした人数 = 0
表示数 = 0
未表示キャラ = 0

DRAWLINE
PRINTL □キャラを選択してください

FOR キャラ, 0, CHARANUM
	;条件に一致するかをチェックする 全部網羅してないとダメ
	SELECTCASE 論理演算
		CASE "AND", "NOR"
			条件チェック = 1
		CASE "OR", "NAND"
			条件チェック = 0
	ENDSELECT
	FOR 条件ID, 0, 5
		SIF キャラ変数:条件ID == ""
			BREAK
		
		SELECTCASE キャラ変数:条件ID
			CASE "TALENT"
				FOR LCOUNT, 0, VARSIZE("TALENT")
					IF TALENTNAME:LCOUNT == 変数名:条件ID
						SELECTCASE 論理演算
							CASE "AND"
								SIF TALENT:キャラ:LCOUNT != TOINT(条件:条件ID)
									条件チェック = 0
							CASE "OR"
								SIF TALENT:キャラ:LCOUNT == TOINT(条件:条件ID)
									条件チェック = 1
							CASE "NOR"
								SIF TALENT:キャラ:LCOUNT == TOINT(条件:条件ID)
									条件チェック = 0
							CASE "NAND"
								SIF TALENT:キャラ:LCOUNT != TOINT(条件:条件ID)
									条件チェック = 1
						ENDSELECT
					ENDIF
				NEXT
			CASE "CFLAG"
				FOR LCOUNT, 0, VARSIZE("CFLAG")
					IF CFLAGNAME:LCOUNT == 変数名:条件ID
						SELECTCASE 論理演算
							CASE "AND"
								SIF CFLAG:キャラ:LCOUNT != TOINT(条件:条件ID)
									条件チェック = 0
							CASE "OR"
								SIF CFLAG:キャラ:LCOUNT == TOINT(条件:条件ID)
									条件チェック = 1
							CASE "NOR"
								SIF CFLAG:キャラ:LCOUNT == TOINT(条件:条件ID)
									条件チェック = 0
							CASE "NAND"
								SIF CFLAG:キャラ:LCOUNT != TOINT(条件:条件ID)
									条件チェック = 1
						ENDSELECT
					ENDIF
				NEXT
			CASE "ABL"
				FOR LCOUNT, 0, VARSIZE("ABL")
					IF ABLNAME:LCOUNT == 変数名:条件ID
						;条件が正の数ならそれ以上が条件、負の数ならそれ以下が条件
						SELECTCASE 論理演算
							CASE "AND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF ABL:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF ABL:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "OR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF ABL:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF ABL:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
							CASE "NOR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF ABL:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF ABL:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "NAND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF ABL:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF ABL:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
						ENDSELECT
					ENDIF
				NEXT
			CASE "EXP"
				FOR LCOUNT, 0, VARSIZE("EXP")
					IF EXPNAME:LCOUNT == 変数名:条件ID
						;条件が正の数ならそれ以上が条件、負の数ならそれ以下が条件
						SELECTCASE 論理演算
							CASE "AND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF EXP:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF EXP:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "OR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF EXP:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF EXP:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
							CASE "NOR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF EXP:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF EXP:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "NAND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF EXP:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF EXP:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
						ENDSELECT
					ENDIF
				NEXT
			CASE "MARK"
				FOR LCOUNT, 0, VARSIZE("MARK")
					IF MARKNAME:LCOUNT == 変数名:条件ID
						;条件が正の数ならそれ以上が条件、負の数ならそれ以下が条件
						SELECTCASE 論理演算
							CASE "AND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF MARK:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF MARK:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "OR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF MARK:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF MARK:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
							CASE "NOR"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF MARK:キャラ:LCOUNT >= TOINT(条件:条件ID)
											条件チェック = 0
									CASE -1
										SIF MARK:キャラ:LCOUNT <= ABS(TOINT(条件:条件ID))
											条件チェック = 0
								ENDSELECT
							CASE "NAND"
								SELECTCASE SIGN(TOINT(条件:条件ID))
									CASE 0, 1
										SIF MARK:キャラ:LCOUNT < TOINT(条件:条件ID)
											条件チェック = 1
									CASE -1
										SIF MARK:キャラ:LCOUNT > ABS(TOINT(条件:条件ID))
											条件チェック = 1
								ENDSELECT
						ENDSELECT
					ENDIF
				NEXT
			CASE "CSTR"
				FOR LCOUNT, 0, VARSIZE("CSTR")
					IF CSTRNAME:LCOUNT == 変数名:条件ID
						SELECTCASE 論理演算
							CASE "AND"
								SIF CSTR:キャラ:LCOUNT != 条件:条件ID
									条件チェック = 0
							CASE "OR"
								SIF CSTR:キャラ:LCOUNT == 条件:条件ID
									条件チェック = 1
							CASE "NOR"
								SIF CSTR:キャラ:LCOUNT == 条件:条件ID
									条件チェック = 0
							CASE "NAND"
								SIF CSTR:キャラ:LCOUNT != 条件:条件ID
									条件チェック = 1
						ENDSELECT
					ENDIF
				NEXT
		ENDSELECT
	NEXT
	;条件通ったら表示処理 20人づつ
	IF 条件チェック
		;2ページ目以降はページ数×20まで表示を飛ばす
		IF 飛ばした人数 < ページ数*20
			飛ばした人数++
			CONTINUE
		ENDIF
		;20人表示したら未表示キャラが居るフラグ立ててページ終わり
		IF 表示数 == 20
			未表示キャラ = 1
			BREAK
		ENDIF
		PRINTFORM 　[{キャラ, 2, RIGHT}] - %NAME:キャラ, 26, LEFT% \@ SEX(キャラ) == 1 ? ♂ # ♀ \@ 
		FOR 条件ID, 0, 5
			IF キャラ変数:条件ID == "CSTR" && 変数名:条件ID == "種族"
				PRINTFORM %CSTR:キャラ:種族% 
				BREAK
			ENDIF
		NEXT
		PRINTL 
		LOCAL:キャラ = 1
		表示数++
	ENDIF
NEXT

DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数+1}　
SIF ページ数 > 0
	PRINTLC [999]前ページ
PRINTLC [1000] 戻る　
;表示できるキャラがまだ居れば次ページ
SIF 未表示キャラ
	PRINTLC [1001]次ページ
PRINTL 
DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 0
				ページ数--
		CASE 1000
			RETURN -1
		CASE 1001
			;表示できるキャラがまだ居れば次ページ
			SIF 未表示キャラ
				ページ数++
		CASEELSE
			SIF !LOCAL:RESULT
				CONTINUE
			RETURN RESULT
	ENDSELECT
LOOP 1

