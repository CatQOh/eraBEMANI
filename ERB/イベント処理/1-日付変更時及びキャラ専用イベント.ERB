;追加処理及びサブイベント
;-------------------------------------------------
;ターン終了時
;-------------------------------------------------
@EVENTTURNEND
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 人数
#DIM 着床告知;DYNAMICじゃない変数 プレーヤーへの告知用なので起動ごとに破棄してもよい
#DIM DYNAMIC DEUIL
#DIM DYNAMIC 売却可能条件
#DIM DYNAMIC 妖精イベント, 2
#DIM DYNAMIC YORIDORI
#DIM DYNAMIC 最大体力
#DIM DYNAMIC 鳳凰イベント, 3

IF TARGET >= 0
	;売却可能&助手可能判定 陥落系で無視できるって条件はほとんど意味無いか
	;従順+欲望が6以上
	SIF ABL:従順+ABL:欲望 >= 6
		売却可能条件 = 1

	;[反抗的][気丈]なら従順LV4以上必要 服従系陥落素質があれば無視できる
	IF (TALENT:反抗的 || TALENT:気丈) && !TALENT:服従 && !TALENT:隷属
		SIF ABL:従順 < 4
			売却可能条件 = 0
	ENDIF

	;[自制心][無関心]なら欲望LV4以上必要 淫乱系陥落素質があれば無視できる
	IF (TALENT:自制心 || TALENT:無関心) && !TALENT:淫乱 && !TALENT:娼婦
		SIF ABL:欲望 < 4
			売却可能条件 = 0
	ENDIF

	;[プライド高い][生意気]なら奉仕精神LV3以上必要
	IF TALENT:プライド高い || TALENT:生意気
		SIF ABL:奉仕精神 < 3
			売却可能条件 = 0
	ENDIF

	;段位モード専用の特殊条件
	IF ゲームモード() == "段位認定"
		;段位認定裏七級の場合、陥落していないと売却できない
		IF 受験段位名 == "裏七級"
			SIF !CFLAG:陥落
				売却可能条件 = 0
		;「炎の孕ませポップン学園」の場合、陥落させたうえで着床を認知していないと売却できない
		ELSEIF 受験段位名 == "炎の孕ませポップン学園"
			売却可能条件 = 0
			IF CFLAG:陥落
				IF TALENT:妊娠
					売却可能条件 = 1
				ELSEIF FALLTYPE(TARGET) == 1 && PREG:TARGET:11
					売却可能条件 = 1
					;この場合一度だけ着床したことを教えてもらえる(再起動でフラグが消失するのは仕様)
					;変数「着床告知」にビットで管理
					;(GETBIT)0:ミニッツ 1:ルート 2:翠里 3:鈴花 4:氷海
					SELECTCASE CALLNAME:TARGET
						CASE "ミニッツ"
							IF !GETBIT(着床告知, 0)
								SETBIT 着床告知, 0
								PRINTFORML ミニッツは%CALLNAME:MASTER%に妊娠検査薬の結果をこっそりと見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%はミニッツの幼い体に子種を植え付けたようだ……
							ENDIF
						CASE "ルート"
							IF !GETBIT(着床告知, 1)
								SETBIT 着床告知, 1
								PRINTFORML ルートは%CALLNAME:MASTER%に妊娠の診断結果を教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%はルートとの交尾によって受精させたようだ……
							ENDIF
						CASE "翠里"
							IF !GETBIT(着床告知, 2)
								SETBIT 着床告知, 2
								PRINTFORML 翠里は%CALLNAME:MASTER%に妊娠した可能性が高いことを教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%は翠里の自称超絶名器の奥に遺伝子を残せたようだ……
							ENDIF
						CASE "鈴花"
							IF !GETBIT(着床告知, 3)
								SETBIT 着床告知, 3
								PRINTFORML 鈴花は%CALLNAME:MASTER%に妊娠検査薬の結果を嬉しそうに見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は鈴花のめしべとの交配に成功したようだ……
							ENDIF
						CASE "氷海"
							IF !GETBIT(着床告知, 4)
								SETBIT 着床告知, 4
								PRINTFORML 氷海は%CALLNAME:MASTER%に妊娠の診断結果が陽性であることを伝えてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は氷海への膣内射精で着床させたようだ……
							ENDIF
					ENDSELECT
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;憑依中は売却可能・助手可能にならない
	IF 売却可能条件 && CFLAG:憑依 == -1
		;上記をクリアし、技巧3以上or奉仕精神3以上orマゾっ気3以上orCVAB感覚合計8以上or従順5以上or欲望5以上なら売却可能
		{
		IF ABL:技巧 >= 3 || ABL:奉仕精神 >= 3 || ABL:マゾっ気 >= 3 || 
		   ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 8 || ABL:従順 >= 5 || ABL:欲望 >= 5
		}
			IF CFLAG:状態 <= 0 && ゲームモード() != "新婚モード"
				PRINTFORMW %NAME:TARGET%が売却可能になりました
				CFLAG:状態 = 1
				;実績:愛撫コマンドのみで売却可能
				SIF 愛撫双:TARGET:0 == 0
					CALL 実績解除, "I've so Happy"
				CALL 経験値取得, TARGET, "売却可能"
			ENDIF
			;さらに従順+欲望+技巧が14以上なら助手可能
			IF ABL:従順+ABL:欲望+ABL:技巧 >= 14
				IF CFLAG:状態 <= 1
					PRINTFORMW %NAME:TARGET%が助手可能になりました
					CFLAG:状態 = 2
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

CALL CHECKDRAWLINE

IF ASSI >= 0
	IF TALENT:ASSI:妖狐 && ITEM:媚薬 == 0 && !CFLAG:MASTER:使用不可
		PRINTFORMW [妖狐]の%CALLNAME:ASSI%が媚薬を調達してきたようです
		ITEM:媚薬++
	ENDIF
ENDIF

CALL CHECKDRAWLINE

FOR LCOUNT, 0, CHARANUM
	;休憩フラグを消すが、配属中のキャラは下記収入関数でフラグ管理してるので対象外
	SIF CSTR:LCOUNT:配属 == ""
		CFLAG:LCOUNT:休憩 = 0
NEXT

IF TIME == 1
	LASTMONEY = MONEY
	;主人が行動不可だと収入入らない
	SIF !CFLAG:MASTER:使用不可
		CALL STRATEGY_INCOME
ENDIF
;主人が行動不可だと発生しない
IF !CFLAG:MASTER:使用不可
	CALL TURNEND_IDOL
	SIF TIME == 1
		CALL TURNEND_監禁室
ENDIF

;畜産農業関係
CALL TURNEND_畜産農業
;家畜の体調次第で食材が変わるので献立を確認する
CALL 献立条件確認

;温泉
CALL TURNEND_温泉探索

SIF ITEM:実験室
	CALL TURNEND_実験室

;施設の計算と利用人数表示
IF !CFLAG:MASTER:使用不可 && ゲームモード() != "新婚モード"
	SIF ITEM:図書室
		CALL TURNEND_図書室
	SIF ITEM:医務室
		CALL TURNEND_医務室
	CALL CHECKDRAWLINEW
ENDIF

;居住部屋
CALL TURNEND_居住部屋

;新婚モード以外で所持金が5000万以上なら裏カジノへの招待状が届く
IF ゲームモード() != "新婚モード" && MONEY >= TONUM("5000万") && !FLAG:裏カジノ
	CALL KOJO_MESSAGE_EVENT, "裏カジノ招待"
	PRINTW 裏カジノが使用可能になりました
	FLAG:裏カジノ = 1
ENDIF

FOR LCOUNT, 0, CHARANUM
	;主人が使用不可だと体力・気力・ストレス値等は変動しない
	IF !CFLAG:MASTER:使用不可
		;ミッドナイトスペシャル
		IF CFLAG:LCOUNT:ミッドナイトスペシャル > 0
			SIF --CFLAG:LCOUNT:ミッドナイトスペシャル == 0
				PRINTFORMW %CALLNAME:LCOUNT%のミッドナイトスペシャルの効果が切れました
		ENDIF
		;夜なら貞操帯装着日数増加
		SIF CFLAG:LCOUNT:貞操帯 && TIME == 1
			CFLAG:LCOUNT:貞操帯++
		;以下主人は除外
		SIF LCOUNT == MASTER
			CONTINUE
		;休憩フラグが立っていて配属中でない場合はキャラ固有の休憩フラグが立つ
		;配属先でいろいろあって休憩してる場合はそちらで休憩フラグが立っている
		IF FLAG:休憩フラグ
			SIF CSTR:LCOUNT:配属 == ""
				CFLAG:LCOUNT:休憩 = 1
		ENDIF

		;種族妖精ならランダムで消費アイテム取得 休憩中のみ
		IF CSTR:LCOUNT:種族 == "妖精" && CFLAG:LCOUNT:陥落 && CFLAG:LCOUNT:休憩
			妖精イベント = RAND(GETNUM(ITEM, "ローション"), GETNUM(ITEM, "魅惑の粘液")+1)
			妖精イベント:1 = RAND(1, 4)
			PRINTFORMW %CALLNAME:LCOUNT%が{妖精イベント:1}個の%ITEMNAME:妖精イベント%を拾ってきました
			ITEM:妖精イベント += 妖精イベント:1
		ENDIF

		;ストレス値の計算
		CALL ストレス値計算, LCOUNT

		;魔剤フラグ消去
		CFLAG:LCOUNT:魔剤 = 0
		;魔剤耐性減少
		IF TALENT:LCOUNT:小人体型
			CFLAG:LCOUNT:魔剤耐性 -= 5
		ELSEIF TALENT:LCOUNT:小柄体型
			CFLAG:LCOUNT:魔剤耐性 -= 6
		ELSE
			CFLAG:LCOUNT:魔剤耐性 -= 7
		ENDIF
		;魔剤耐性は1000～2000の間で変動する
		SIF CFLAG:LCOUNT:魔剤耐性 < 1000
			CFLAG:LCOUNT:魔剤耐性 = 1000

		;気力の計算→体力の計算
		CALL 気力計算, LCOUNT
		CALL 体力計算, LCOUNT

		;医務室利用判定をリセット
		CFLAG:LCOUNT:医務室利用 = 0

		;依存度は-1000～5000
		CFLAG:LCOUNT:依存度 = LIMIT(CFLAG:LCOUNT:依存度, -1000, 5000)

		;回復量低下処理
		SIF CFLAG:LCOUNT:回復量低下 > 0
			CFLAG:LCOUNT:回復量低下--

		;狂気度を下げる
		SIF CFLAG:LCOUNT:狂気度 > 0
			CFLAG:LCOUNT:狂気度--
	ENDIF
	;以下は主人使用不可でも処理する
	CFLAG:LCOUNT:二日酔い = 0
	;酔が抜ける 必ずしも1ターンで全快するわけではない
	SELECTCASE TIME
		;昼→夜
		CASE 0
			IF TALENT:LCOUNT:酒豪
				CFLAG:LCOUNT:酔 -= 20+(特質("代謝強化", LCOUNT)/2)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
			ELSEIF TALENT:LCOUNT:下戸
				CFLAG:LCOUNT:酔 -= 5+(特質("代謝強化", LCOUNT)/4)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/5
			ELSE
				CFLAG:LCOUNT:酔 -= 10+(特質("代謝強化", LCOUNT)/3)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
			ENDIF
		;夜→昼
		CASE 1
			IF TALENT:LCOUNT:酒豪
				CFLAG:LCOUNT:酔 -= 50+(特質("代謝強化", LCOUNT)/2)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/2
			ELSEIF TALENT:LCOUNT:下戸
				CFLAG:LCOUNT:酔 -= 10+(特質("代謝強化", LCOUNT)/4)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
			ELSE
				CFLAG:LCOUNT:酔 -= 30+(特質("代謝強化", LCOUNT)/3)
				CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
			ENDIF
			;これで酔いが残っていると二日酔いを引き起こす
			IF CFLAG:LCOUNT:酔 > 0
				CFLAG:LCOUNT:二日酔い = 1
				CFLAG:LCOUNT:酔 = 0
			ENDIF
	ENDSELECT
	TEQUIP:LCOUNT:ウォッカ = 0
	SIF CFLAG:LCOUNT:酔 < 0
		CFLAG:LCOUNT:酔 = 0

	;使用不可フラグ
	IF CFLAG:LCOUNT:使用不可 != 0
		SIF LCOUNT == MASTER
			CONTINUE
		SIF LCOUNT == TARGET
			TARGET = -1
		SIF LCOUNT == ASSI
			ASSI = -1
		CALL 配属解除, LCOUNT
		CFLAG:LCOUNT:使用不可--
		IF CFLAG:LCOUNT:使用不可 <= 0
			SELECTCASE CFLAG:LCOUNT:使用不可状態
				CASE 0
					PRINTFORMW %CALLNAME:LCOUNT%が再び調教可能になりました
				CASE 1
					PRINTFORMW %CALLNAME:LCOUNT%は体外受精を終えたため、再び調教可能になりました
				CASE 2
					PRINTFORMW %CALLNAME:LCOUNT%は3日間の研修を受け、帰宅しました
					CALL ABLマイナス打ち消し, LCOUNT
			ENDSELECT
			CFLAG:LCOUNT:使用不可 = 0
		ENDIF
	ENDIF
NEXT
IF TFLAG:料理
	IF !TFLAG:裸体盛り
		CALL KOJO_MESSAGE_EVENT, "料理"
	ELSE
		CALL KOJO_MESSAGE_EVENT, "裸体盛り"
	ENDIF
ENDIF
SIF TFLAG:お菓子
	CALL KOJO_MESSAGE_EVENT, "お菓子"
TFLAG:料理 = 0
TFLAG:お菓子 = 0
TFLAG:裸体盛り = 0

;調理室の処理は回復後に行う
CALL TURNEND_調理室

;スライム化処理
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:スライム度 <= 0
		CONTINUE
	;機械と無機物と霊体は感染しない
	IF CSTR:LCOUNT:種族 == "機械" || TALENT:LCOUNT:無機物 || TALENT:LCOUNT:霊体
		CFLAG:LCOUNT:スライム度 = 0
		CONTINUE
	ENDIF
	;眷属化したキャラは感染しない
	IF CSTR:LCOUNT:種族 != "スライム" && CSTR:LCOUNT:眷属主 != ""
		CFLAG:LCOUNT:スライム度 = 0
		CONTINUE
	ENDIF
	IF FLAG:抗スライム && !TALENT:LCOUNT:スライム && !TALENT:LCOUNT:溶ける快
		CFLAG:LCOUNT:スライム度 = 0
		CONTINUE
	ENDIF
	;妊娠育児中は感染進まない
	SIF TALENT:LCOUNT:妊娠 || TALENT:LCOUNT:育児中
		CONTINUE
	;スライム度は1日1～3ランダムで増加
	CFLAG:LCOUNT:スライム度 += RAND(1, 4)
	;30を超えるとスライム化する
	IF CFLAG:LCOUNT:スライム度 >= 30 && !TALENT:LCOUNT:スライム && !TALENT:LCOUNT:溶ける快
		CALL KOJO_MESSAGE_EVENT, "スライム化", LCOUNT
		スライム化イベント回数++
		SAVEGLOBAL
		SIF スライム化イベント回数 >= 10
			CALL 実績解除, "そして世界は粘液に満ちた"
		TALENT:LCOUNT:スライム = 1
		CSTR:LCOUNT:種族 = スライム
		CALL CLEAR_FLAG, LCOUNT
	ENDIF
	;1500に到達する事で、もう元には戻れない完全なスライムとなる
	IF CFLAG:LCOUNT:スライム度 >= 1500 && TALENT:LCOUNT:スライム
		CALL KOJO_MESSAGE_EVENT, "完全スライム化", LCOUNT
		TALENT:LCOUNT:溶ける快 = 1
		TALENT:LCOUNT:妄信 = 1
		TALENT:LCOUNT:スライム = 0
		CALL CLEAR_FLAG, LCOUNT
	ENDIF
	SIF CFLAG:LCOUNT:スライム度 >= 1500
		CFLAG:LCOUNT:スライム度 = 1500
NEXT

CALL EVENTCHECK_ABL
FLAG:休憩フラグ = 0

;午後なら次の日、午前なら午後にする
SELECTCASE TIME
	CASE 0
		PRINTW 夜になった……
		TIME = 1
	CASE 1
		;日付変更時のイベント呼び出し
		CALL EVENT_NEXTDAY
		;色仕掛けイベント
		SIF !CFLAG:MASTER:使用不可
			CALL EVENT_HONEYTRAP
		;主人が使用不可の場合、期限的な日付加算は行わない
		IF !CFLAG:MASTER:使用不可
			DAY++
			DAY:1 = DAY
		;もし主人が妊娠で使用不可だった場合のために、妊娠フラグを進めるためのDAY:1はカウントする
		ELSE
			DAY:1++
		ENDIF
		PRINTW 一日が終わった……
		;実績：調教対象が居ない状態で夜を過ごす
		SIF TARGET == -1 && !CFLAG:MASTER:使用不可
			CALL 実績解除, "Without you tonight"
		TIME = 0
		;ゲームモード7(新婚モード)か9(PREMIUM FREE)なら10日に一度、借金の返済・新規借入ができる
		IF GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード") && DAY%10 == 0
			CALL DEBT
		;借金がある場合は10日に一度返済を要求される
		ELSEIF 借金
			CALL DEBT, 1
		ENDIF
		;バッドエンドの判定
		;期限切れ
		SIF (DAY > 100 && ゲームモード() == "通常モード") || (DAY > 300 && ゲームモード() == "経営モード")
			JUMP BADENDING, 1
		;金欠エンド
		;ただしゲームモード7(新婚モード)と9(PREMIUM FREE)では呼び出されない
		IF MONEY < 0 && !GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード")
			;実績:STRATEGYモードで金欠エンドを迎える
			CALL 実績解除, "無能な経営者"
			JUMP BADENDING, 2
		ENDIF
		;朝になった時のイベント呼び出し
		CALL EVENT_NEWDAY
		CALL EVENT_NEWDAY_SELF
ENDSELECT

;実績「タルタでラジェ・ガレト・ヴァロ全員の童貞を奪う」
;ビットで処理(ラジェが1、ガレトが2、ヴァロが4)
;7になったら実績解除
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:童貞 == "タルタ" || CSTR:LCOUNT:性転換童貞 == "タルタ"
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ラジェ"
				タルタ童貞喰い |= 1
			CASE "ガレト"
				タルタ童貞喰い |= 2
			CASE "ヴァロ"
				タルタ童貞喰い |= 4
		ENDSELECT
	ENDIF
NEXT
SIF タルタ童貞喰い & 7
	CALL 実績解除, "I LOVE SAKURA"

;実績：陥落したイロドリミドリの全員と同棲する
;実績：ユーリ、アッシュ、スマイルの三人を陥落させる
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:陥落
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる"
				YORIDORI++
			CASE "ユーリ", "アッシュ", "スマイル"
				DEUIL++
		ENDSELECT
		SIF YORIDORI == 5
			CALL 実績解除, "ヨリドリミドリ"
		SIF DEUIL == 3
			CALL 実績解除, "Deuilの熱烈すぎる追っかけ"
	ENDIF
NEXT

;実績:育江の出産から7年を経過させる
SIF この子の七つのお祝いに && (YEAR*10000+MONTH*100+DAYS) - この子の七つのお祝いに >= 70000
	CALL 実績解除, "この子の七つのお祝いに"

;実績:6928日過ごす
SIF DAY >= 6928
	CALL 実績解除, "INFINITE PLAYER"

;気温の算出
CALL TEMPERATURE
;特定のキャラがいる場合、気温に補正を加える(ただし昼のみ)
;主人・奴隷・助手の場合の方が影響が強い
IF TIME == 0
	FOR LCOUNT, 0, CHARANUM
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "フローラ"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 60
				ELSE
					FLAG:気温 += 6
				ENDIF
			CASE "烈", "ウェルダン"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 30
				ELSE
					FLAG:気温 += 3
				ENDIF
			CASE "氷海"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 30
				ELSE
					FLAG:気温 -= 3
				ENDIF
			CASE "茜"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 50
				ELSE
					FLAG:気温 += 5
				ENDIF
			CASE "スノーフェアリー"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 60
				ELSE
					FLAG:気温 -= 6
				ENDIF
			CASE "氷雪ちゃん"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 150
				ELSE
					FLAG:気温 -= 15
				ENDIF
		ENDSELECT
	NEXT
	;魔法の影響
	FLAG:気温 -= フリージングアトモスフィア*30
	FLAG:気温 += バーニンザフロア*30
ENDIF
;天気の決定
CALL WEATHER

FOR LCOUNT, 0, CHARANUM
	;フローラ専用イベント
	IF CHARANAMEF(LCOUNT) == "フローラ"
		最大体力 = MAXBASE:LCOUNT:体力
		MAXBASE:LCOUNT:体力 = CSVBASE(NO:LCOUNT, 0) + (FLAG:気温-180)*2
		SIF TALENT:LCOUNT:妊娠
			MAXBASE:LCOUNT:体力 -= 500
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-最大体力
		;体力、気力の値整理
		CALL SETBASE, LCOUNT
	ENDIF
	;鳳凰専用イベント
	IF CSTR:LCOUNT:種族 == "鳳凰" && FLAG:気温 > 0
		鳳凰イベント = FLAG:気温
		SELECTCASE 天気()
			CASE "晴れ"
			CASE "曇り"
				TIMES 鳳凰イベント , 0.50
			CASEELSE
				TIMES 鳳凰イベント , 0.20
		ENDSELECT
		;気温と天気で計算したボーナス分体力気力が増加
		鳳凰イベント:1 = MAXBASE:LCOUNT:体力
		鳳凰イベント:2 = MAXBASE:LCOUNT:気力
		MAXBASE:LCOUNT:体力 = MAXBASE:LCOUNT:鳳凰基礎体力+鳳凰イベント
		MAXBASE:LCOUNT:気力 = MAXBASE:LCOUNT:鳳凰基礎気力+鳳凰イベント
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-鳳凰イベント:1
		BASE:LCOUNT:気力 += MAXBASE:LCOUNT:気力-鳳凰イベント:2
		;体力、気力の値整理
		CALL SETBASE, LCOUNT
	ENDIF
NEXT

;主人が使用不可の場合はTARGETとASSIを外してSHOP画面表示し、収入と支出の一切を中断して時間だけ経過させる。DAYは増えないので期限には影響無し
IF CFLAG:MASTER:使用不可 > 0
	TARGET = -1
	ASSI = -1

	;曜日計算
	FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

	;祝日・記念日の設定
	CALL HOLIDAY

	CALL SHOP_UI

	IF --CFLAG:MASTER:使用不可 == 0
		PRINTFORMW %CALLNAME:MASTER%が復帰しました。次のターンから再開できます
	ELSE
		PRINTFORMW %CALLNAME:MASTER%の都合により行動を終了します
	ENDIF
	TARGET = -1
	ASSI = -1
	FLAG:休憩フラグ = 1
	RESTART
ENDIF

;魔法の効果終了は最後
IF フリージングアトモスフィア > 0
	SIF --フリージングアトモスフィア == 0
		PRINTW [フリージングアトモスフィア]の効果が切れました
ENDIF
IF バーニンザフロア > 0
	SIF --バーニンザフロア == 0
		PRINTW [バーニンザフロア]の効果が切れました
ENDIF
IF ストームバスター > 0
	SIF --ストームバスター == 0
		PRINTW [ストームバスター]の効果が切れました
ENDIF
IF スウィートレイン > 0
	SIF --スウィートレイン == 0
		PRINTW [スウィートレイン]の効果が切れました
ENDIF
IF スノープリズム > 0
	SIF --スノープリズム == 0
		PRINTW [スノープリズム]の効果が切れました
ENDIF
IF リジェネレーション > 0
	SIF --リジェネレーション == 0
		PRINTW [リ：ジェネレーション]の効果が切れました
ENDIF
IF ヒプノティッククライシス > 0
	SIF --ヒプノティッククライシス == 0
		PRINTW [ヒプノティッククライシス]の効果が切れました
ENDIF
IF エナジードライブ > 0
	IF --エナジードライブ == 0
		PRINTFORMW %CALLNAME:MASTER%にかけられた[エナジードライブ]の効果が切れました
		FOR LCOUNT, 0, VARSIZE("ABL")
			SIF ABLNAME:LCOUNT != ""
				ABL:MASTER:LCOUNT -= エナジードライブ:1
		NEXT
		VARSET エナジードライブ
	ENDIF
ENDIF
FOR LCOUNT, 0, CHARANUM
	IF ビーストモード:LCOUNT:0 > 0
		IF --ビーストモード:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ビーストモード]の効果が切れました
			SIF ビーストモード:LCOUNT:1
				TALENT:LCOUNT:動物耳 = 0
			SIF ビーストモード:LCOUNT:2
				TALENT:LCOUNT:尻尾 = 0
			SIF ビーストモード:LCOUNT:3
				TALENT:LCOUNT:ファーリー = 0
			CSTR:LCOUNT:種族 = %ビーストモードS:LCOUNT:0%
			VARSET ビーストモード:LCOUNT:0
			VARSET ビーストモードS:LCOUNT:0
		ENDIF
	ENDIF
	IF ジェントルストレス:LCOUNT:0 > 0
		IF --ジェントルストレス:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられたジェントルストレスの効果が切れました
			ジェントルストレス:LCOUNT:1 = 0
		ENDIF
	ENDIF
	IF ティルナノーグ:LCOUNT:0 > 0
		IF --ティルナノーグ:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ティル・ナ・ノーグ]の効果が切れました
			TALENT:LCOUNT:小人体型 = 1
		ENDIF
	ENDIF
	IF CFLAG:LCOUNT:ポゼ
		IF --CFLAG:LCOUNT:ポゼ == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ポゼッション]の効果が切れました
			FOR LCOUNT2, 0, 30
				SIF ABLNAME:LCOUNT2 == "魔法技能"
					CONTINUE
				ABL:LCOUNT:LCOUNT2 = ポゼッション:LCOUNT:LCOUNT2
			NEXT
			VARSET ポゼッション:LCOUNT:0
		ENDIF
	ENDIF
NEXT

BEGIN SHOP

;-------------------------------------------------
;日付が変わった時のイベント
;-------------------------------------------------
@EVENT_NEXTDAY
#DIM DYNAMIC LCOUNT

;調教対象・助手が豹変しているとき、元に戻る
;現在豹変し得るのは、紅刃・冷音
IF TARGET >= 0 && TRANCE:200
	SELECTCASE CHARANAMEF(TARGET)
		CASE "紅刃", "青雨 冷音"
			CALL TRANCE_OFF, TARGET
			TRANCE:200 = 0
			PRINTFORMW ＜%CALLNAME:TARGET%は元の性格にもどった＞
	ENDSELECT
ENDIF
IF ASSI >= 0 && TRANCE:ASSI:200
	SELECTCASE CHARANAMEF(ASSI)
		CASE "紅刃", "青雨 冷音"
			CALL TRANCE_OFF, ASSI
			TRANCE:ASSI:200 = 0
			PRINTFORMW ＜%CALLNAME:ASSI%は元の性格にもどった＞
	ENDSELECT
ENDIF

;特定キャラの日付変更時イベント
FOR LCOUNT, 0, CHARANUM
	;らいむ専用イベント 陥落済みでどこにも配属していないフリーな状態である場合に発生
	IF CHARANAMEF(LCOUNT) == "高須 らいむ"
		IF CFLAG:LCOUNT:陥落 && CSTR:LCOUNT:配属 == "" && !CFLAG:LCOUNT:使用不可 && !TALENT:LCOUNT:育児中 && !TALENT:LCOUNT:妊娠
			IF TALENT:LCOUNT:溶ける快
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めると、とろとろにとろけた秘所からはしたなく粘液を溢れさせて瓶に入れ、それを渡してきた。
						PRINTFORML 瓶の中で、%CALLNAME:LCOUNT%の獲れたて粘液が音を立てて蠢いている…　とりあえず保管する事にした。
						PRINTFORMW ＜保管粘液量が100ml増加した＞
						FLAG:保管済み粘液 += 100
					CASE 1
						PRINTFORML %CALLNAME:LCOUNT%は、もじもじとした様子で%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML どうやら彼女の粘液のようだが、調子が悪いのか微妙に粘度が低いようだ。
						PRINTFORMW ＜この粘液は、ローションとして使わせてもらう事にした＞
						ITEM:ローション++
					CASE 2
						PRINTFORML 熱っぽいような表情をした%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 粘液のようにも見えるが、なんてことのない、ただの媚薬のようだ。
						PRINTFORML 媚薬と自身の粘液を混ぜ合わせる研究をしていたようだが今日のところは失敗に終わったのだろう。
						PRINTFORMW ＜%CALLNAME:LCOUNT%の色をしているだけの媚薬を手に入れた＞
						ITEM:媚薬++
					CASE 3
						PRINTFORML 全身を淫らに震わせながら%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 中で蠢く%CALLNAME:LCOUNT%の粘液と思わしきそれは、密封状態であるにも関わらず濃厚な甘い香りを漂わせている。
						PRINTFORML %CALLNAME:LCOUNT%は、にちゃぁと水音を立てて笑うと上機嫌で自室へと戻っていった…
						PRINTFORMW ＜%CALLNAME:LCOUNT%手製の魅惑の粘液を２回分手に入れた＞
						ITEM:魅惑の粘液 += 2
				ENDSELECT
			ELSEIF TALENT:LCOUNT:スライム
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めると、秘所からとろりとした粘液を垂らして瓶に入れ、それを渡してきた。
						PRINTFORML 瓶の中で、%CALLNAME:LCOUNT%の獲れたて粘液が蠢いている…　とりあえず保管する事にした。
						PRINTFORMW ＜保管粘液量が30ml増加した＞
						FLAG:保管済み粘液 += 30
					CASE 1
						PRINTFORML %CALLNAME:LCOUNT%は、もじもじとした様子で%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML どうやら彼女の粘液のようだが、粘度が低い。ローションの代わりとしてなら使えそうだ。
						PRINTFORMW ＜この粘液は、ローションとして使わせてもらう事にした＞
						ITEM:ローション++
					CASE 2
						PRINTFORML 熱っぽいような表情をした%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 粘液のようにも見えるが、なんてことのない、ただの媚薬のようだ。
						PRINTFORML 媚薬と自身の粘液を混ぜ合わせる研究をしていたようだが今日のところは失敗に終わったのだろう。
						PRINTFORMW ＜%CALLNAME:LCOUNT%の色をしているだけの媚薬を手に入れた＞
						ITEM:媚薬++
					CASE 3
						PRINTFORML ほんのりと甘い香りを漂わせた%CALLNAME:LCOUNT%が、%CALLNAME:MASTER%を呼び止め、液体の詰められた瓶を渡してきた。
						PRINTFORML 中で蠢く%CALLNAME:LCOUNT%の粘液と思わしきそれは、密封状態であるにも関わらず甘い香りを漂わせている。
						PRINTFORML %CALLNAME:LCOUNT%は、満足そうに自室へと戻っていった…
						PRINTFORMW ＜%CALLNAME:LCOUNT%手製の魅惑の粘液を手に入れた＞
						ITEM:魅惑の粘液++
				ENDSELECT
			ELSE
				PRINTFORML %CALLNAME:LCOUNT%は、%CALLNAME:MASTER%を呼び止めて、研究過程で生じた副産物として、スライムの粘液を渡してきた。
				PRINTFORML 瓶の中で、粘液が微かに蠢いている…　とりあえず保管する事にした。
				PRINTFORMW ＜保管粘液量が10ml増加した＞
				FLAG:保管済み粘液 += 10
			ENDIF
		ENDIF
	ENDIF
NEXT


;スライム奴隷の処女膜再生イベント
;スライム化しているキャラであり、かつ処女を喪失しており、再生処女を持たない場合
FOR LCOUNT, 0, CHARANUM
	IF (TALENT:LCOUNT:スライム || CSTR:LCOUNT:種族 == "スライム") && TALENT:LCOUNT:処女 == 0 && TALENT:LCOUNT:再生処女 == 0
		TALENT:LCOUNT:再生処女 = 1
		PRINTFORMW ＜%CALLNAME:LCOUNT%は、スライムの身体が持つ再生力で失った処女膜を再生させた＞
	ENDIF
NEXT

;奴隷の幼児退行イベント
IF TARGET >= 0
	;反発刻印3があり、かつ幼稚である時に
	IF TALENT:幼稚 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が7以上になる
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && EXP:異常経験 >= 7
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	;反発刻印3があり、かつ幼稚がない時
	ELSEIF TALENT:幼稚 == 0 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が9以上に加え露出癖5とおもらし癖がある
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && ABL:露出癖 >= 5 && EXP:異常経験 >= 9 && TALENT:おもらし癖 == 1
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	ENDIF
ENDIF

;段位認定におけるゲージ処理
SIF ゲームモード() == "段位認定" && !CFLAG:MASTER:使用不可
	CALL 段位ゲージ処理

;-------------------------------------------------
;朝になった時のイベント
;-------------------------------------------------
@EVENT_NEWDAY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 治療コスト

;日付加算関数
CALL NEXTDAY

;祝日・記念日の処理はSHOPで行うようにしました

;定休日の場合は休みにする
;営業フラグが-1の場合はコマンドでの休業中なのでオープンしない
IF GC定休日() || FLAG:ゲーセン営業中 == -1
	SIF FLAG:ゲーセン営業中 != -1
		FLAG:ゲーセン営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "ゲームセンター"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF FLAG:ゲーセン営業中 != -1
	FLAG:ゲーセン営業中 = DAY-1
ENDIF

IF BR定休日() || FLAG:娼館営業中 == -1
	SIF FLAG:娼館営業中 != -1
		FLAG:娼館営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "娼館"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF FLAG:娼館営業中 != -1
	FLAG:娼館営業中 = DAY-1
ENDIF

IF INN定休日() || FLAG:飲食店営業中 == -1 || 業種 == ""
	SIF FLAG:飲食店営業中 != -1
		FLAG:飲食店営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "飲食店"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF FLAG:飲食店営業中 != -1
	FLAG:飲食店営業中 = DAY-1
ENDIF

;実績:新年を迎える
SIF MONTH == 1 && DAYS == 1
	CALL 実績解除, "あけおめアゲイ～ン"

;放尿経験100以上でおもらし癖がつく
;[幼稚]なら放尿経験30以上でつく
;アイテムで治した場合は発生しない
IF TARGET >= 0
	IF !TALENT:おもらし癖 && !CFLAG:おもらし改善フラグ
		IF (TALENT:幼稚 && EXP:放尿経験 >= 30) || (!TALENT:幼稚 && EXP:放尿経験 >= 100)
			CALL CHECKDRAWLINE
			CALL KOJO_MESSAGE_EVENT, "おもらし癖"
			TALENT:おもらし癖 = 1
		ENDIF
	ENDIF
ENDIF

;調合知識習得イベント
IF ASSI > 0 && !TALENT:MASTER:調合知識
	;助手が調合知識持ちで陥落済み
	IF TALENT:ASSI:調合知識 && CFLAG:ASSI:陥落
		;技巧LV3以上 奉仕精神LV3以上
		IF ABL:ASSI:技巧 >= 3 && ABL:ASSI:奉仕精神 >= 3
			;主人の技巧LV4以上
			IF ABL:MASTER:技巧 >= 4
				CALL CHECKDRAWLINE
				CALL KOJO_MESSAGE_EVENT, "調合知識伝授", ASSI
				TALENT:MASTER:調合知識 = 1
				CFLAG:ASSI:主人に調合知識を伝授した経験++
			ENDIF
		ENDIF
	ENDIF
ENDIF

;全体の妊娠発覚時のイベント
CALL GET_CHILD

;排卵誘発剤の効果終了と、不妊治療薬のコスト処理
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:排卵誘発剤 > 0
		SIF --CFLAG:LCOUNT:排卵誘発剤 <= 0
			PRINTFORMW %CALLNAME:LCOUNT%の排卵誘発剤の効果が切れたようだ
	ENDIF
	IF CFLAG:LCOUNT:生殖力強化 == 1
		治療コスト = 300
		SIF TALENT:LCOUNT:小柄体型
			治療コスト += 200
		SIF !ORIGIN
			CALL PASELIW, @"%CALLNAME:LCOUNT%の不妊治療薬に{治療コスト}＄の費用が発生しました"
		MONEY -= 治療コスト
		CFLAG:LCOUNT:ストレス値 += 5
	ENDIF
NEXT

CALL PREG_READY

;懸賞が届く 現状は必ず当選する
IF FLAG:ナンプレ懸賞
	IF --FLAG:ナンプレ懸賞 <= 0
		PRINTL 応募していた懸賞の景品が届いた
		PRINTW 「ミッドナイトスペシャル」を手に入れた
		ITEM:ミッドナイトスペシャル++
	ENDIF
ENDIF

;毎月1日に仕送りが届く
IF !ORIGIN
	IF DAYS == 1 && FLAG:仕送り
		CALL PASELIW, @"%CALLNAME:MASTER%の子供たちから{FLAG:仕送り*30000}＄の仕送りを受け取りました"
		MONEY += FLAG:仕送り*30000
	ENDIF
ENDIF

@TRANCE_ON, ARG
#DIM DYNAMIC LCOUNT
;変身フラグにTRANCEを使用。変身での素質変更を行なう。
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:LCOUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TALENT:ARG:LCOUNT
						TRANCE:ARG:LCOUNT = 1
						TALENT:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:LCOUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:LCOUNT
						TRANCE:ARG:LCOUNT = 1
						TALENT:ARG:LCOUNT = 1
					ENDIF
			ENDSELECT
		NEXT
	CASE "青雨 冷音"
		;雨が降ると気力にボーナス
		;無条件に700増えるほかに、Y=1000/XのグラフでのX=1～X=[雨量]までの面積
		;つまり1000LOG([雨量])だけ増加する
		;ただし、整数への切り捨ての関係で100LOG([雨量]^10)としている
		;雨量に対する気力の参考値
		;雨量1:1700 雨量2:2300 雨量3:2700 雨量4:3000 雨量5:3300 雨量10:4000 雨量15:4400 雨量20:4600 雨量78以上:6000
		MAXBASE:ARG:気力 += 700
		MAXBASE:ARG:気力 += 100*LOG(POWER(LIMIT(FLAG:降水量, 1, 78), 10))
		BASE:ARG:気力 = MAXBASE:ARG:気力
		FOR LCOUNT, 0, VARSIZE("TALENT")
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:LCOUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TALENT:ARG:LCOUNT
						TRANCE:ARG:LCOUNT = 1
						TALENT:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:LCOUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:LCOUNT
						TRANCE:ARG:LCOUNT = 1
						TALENT:ARG:LCOUNT = 1
					ENDIF
			ENDSELECT
		NEXT
ENDSELECT

@TRANCE_OFF, ARG
#DIM DYNAMIC LCOUNT
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			;変身解除時、習得処理を行なう
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:LCOUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TRANCE:ARG:LCOUNT
						TALENT:ARG:LCOUNT = 1
						TRANCE:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:LCOUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:LCOUNT
						TALENT:ARG:LCOUNT = 0
						TRANCE:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
		NEXT
	CASE "青雨 冷音"
		MAXBASE:ARG:気力 = CSVBASE(NO:ARG, 1)
		FOR LCOUNT, 0, VARSIZE("TALENT")
			;変身解除時、習得処理を行なう
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:LCOUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TRANCE:ARG:LCOUNT
						TALENT:ARG:LCOUNT = 1
						TRANCE:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:LCOUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:LCOUNT
						TALENT:ARG:LCOUNT = 0
						TRANCE:ARG:LCOUNT = 0
					ENDIF
			ENDSELECT
		NEXT
ENDSELECT

;-------------------------------------------------
;ユリア天使化
;-------------------------------------------------
@ユリア天使化
#DIM DYNAMIC LTARGET

IF GETCHARANAME("鬼蝮 ユリア") >= 0
	LTARGET = TARGET
	TARGET = GETCHARANAME("鬼蝮 ユリア")
	CALL KOJO_MESSAGE_EVENT, "天使化"
	PRINTW 鬼蝮 ユリアは大天使ユリアになった
	NAME:TARGET = 大天使ユリア
	SIF EXP:歌唱経験 < 200
		EXP:歌唱経験 = 200
	SIF EXP:ダンス経験 < 100
		EXP:ダンス経験 = 50
	SIF ABL:歌唱技能 < 5
		ABL:歌唱技能 = 5
	SIF ABL:舞踊技能 < 2
		ABL:舞踊技能 = 2
	SIF ABL:魔法技能 < 3
		ABL:魔法技能 = 3
	SIF MAXBASE:魔力 < 3000
		MAXBASE:魔力 = 3000
	TALENT:保守的 = 1
	TALENT:薬毒耐性 = 1
	TALENT:献身的 = 1
	TALENT:両刀 = 1
	TALENT:魅惑 = 1
	TALENT:統率 = 1
	TALENT:羽 = 1
	TALENT:回復早い = 1
	TALENT:治療 = 1
	TALENT:母性 = 1
	TALENT:魔術師 = 1
	TALENT:絶対音感 = 1
	TARGET = LTARGET
ENDIF

;-------------------------------------------------
;女主人による色仕掛けイベント
;-------------------------------------------------
@EVENT_HONEYTRAP
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ポイント

ポイント = 0
;調教後夜這いが発生してたら発生しない
IF TFLAG:調教後夜這い
	TFLAG:調教後夜這い = 0
	RETURN 0
ENDIF

;ペニス使用不可では発生しない
SIF Ｐ使用不可(TARGET)
	RETURN 0

SIF !FLAG:色仕掛け
	RETURN 0
SIF TARGET <= 0
	RETURN 0
SIF TALENT:MASTER:性別 != 2
	RETURN 0

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "生意気", "無関心", "貞操観念", "抑圧", "抵抗", "冷静", "強気"
			ポイント -= 1
		CASE "素直", "衝動的", "弱味", "弱気", "献身的", "妄信"
			ポイント += 1
		CASE "反抗的", "ツンデレ", "自制心", "一線越えない", "快感の否定"
			ポイント -= 2
		CASE "好奇心", "貞操無頓着", "解放", "快感に素直", "女好き"
			ポイント += 2
		CASE "潔癖症", "汚臭敏感"
			SIF TALENT:MASTER:ファーリー
				ポイント -= 2
		CASE "同族嫌悪"
			SIF CSTR:種族 == CSTR:MASTER:種族
				ポイント -= 2
	ENDSELECT
NEXT

IF TALENT:性別 == 2
	IF TALENT:両刀
		ポイント += 1
	ELSE
		ポイント -= 2
	ENDIF
ENDIF
IF TALENT:恋心 >= 0
	IF TALENT:恋心 != MASTER && !CFLAG:陥落
		ポイント -= 3
	ELSEIF TALENT:恋心 == MASTER
		ポイント += 3
	ENDIF
ENDIF
IF TALENT:既婚者 >= 0
	IF TALENT:既婚者 != MASTER && !CFLAG:陥落
		ポイント -= 5
	ELSEIF TALENT:既婚者 == MASTER
		ポイント += 5
	ENDIF
ENDIF

SIF TALENT:MASTER:魅力
	ポイント += 1
SIF TALENT:MASTER:魅惑
	ポイント += 1
SIF TALENT:MASTER:謎の魅力
	ポイント += 2
SIF TALENT:MASTER:小柄体型
	ポイント -= 1
SIF ヒプノティッククライシス
	ポイント += RAND(15, 21)

ポイント += ABL:従順
ポイント += ABL:欲望
ポイント += ABL:奉仕精神
ポイント -= MARK:反発刻印*2
ポイント -= MARK:苦痛刻印

SIF CFLAG:陥落
	ポイント += 20

;地の文分岐に使う
TFLAG:誘惑ポイント = ポイント

CALL CHECKDRAWLINE
PRINTPLAINFORM 就寝間際、寝付こうとしている%CALLNAME:TARGET%の姿を目撃した 
PRINTL [3] イベント非表示 オプションから再度表示出来ます
PRINTL [0] 夜這いをかける
PRINTL [1] 誘惑する
PRINTL [2] 今日はやめとく
CALL INPUTF, 0, 1, 2, 3
SELECTCASE RESULT
	CASE 2
		RETURN 0
	CASE 3
		FLAG:色仕掛け = 0
		RETURN 0
ENDSELECT

;夜這い
SELECTCASE RESULT
	CASE 0
		IF TFLAG:誘惑ポイント >= 12
			TFLAG:色仕掛け = 1
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				PRINTFORML ＜%CALLNAME:TARGET%童貞喪失＞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					CSTR:性転換童貞 = %NAME:MASTER%
				ELSE
					CSTR:童貞 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女 && PENIS(TARGET)
				PRINTFORML ＜%CALLNAME:MASTER%処女喪失＞
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					CSTR:MASTER:性転換処女 = %NAME:TARGET%
				ELSE
					CSTR:MASTER:処女 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				中出し回数:(NO:MASTER)++
				中出され回数:MASTER:(NO:TARGET)++
				CALL EXPUP, "射精経験", 2
				CALL EXPUP, "絶頂経験", 2
				CALL EXPUP, "Ｖ経験", 1, MASTER
				CALL EXPUP, "膣射経験", 1, MASTER
			ELSE
				CALL EXPUP, "絶頂経験", 3
			ENDIF
			IF PENIS(MASTER)
				CALL EXPUP, "精液経験", 1
				CALL EXPUP, "射精経験", 1, MASTER
				CALL EXPUP, "絶頂経験", 1, MASTER
			ENDIF
			IF TALENT:性別 == 2
				CALL EXPUP, "レズ経験", 3
				CALL EXPUP, "レズ経験", 3, MASTER
			ENDIF
			IF PENIS(TARGET)
				CALL EXPUP, "性交経験", 1
				CALL EXPUP, "性交経験", 1, MASTER
				経験回数:MASTER:(NO:TARGET)++
				経験回数:(NO:MASTER)++
			ENDIF
			CALL EXPUP, "主人調教経験", 2
			CFLAG:依存度 += TFLAG:誘惑ポイント
			PRINTL ＜依存度増加＞
			CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*50
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*50
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*70
			CALL JUELUP, "屈服", TFLAG:誘惑ポイント*100
		ELSE
			TFLAG:色仕掛け = 2
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
				CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
				CALL JUELUP, "恐怖", TFLAG:誘惑ポイント*30
			ENDIF
		ENDIF
	CASE 1
		IF TFLAG:誘惑ポイント >= 16
			TFLAG:色仕掛け = 3
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				PRINTFORML ＜%CALLNAME:TARGET%童貞喪失＞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					CSTR:性転換童貞 = %NAME:MASTER%
				ELSE
					CSTR:童貞 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女 && PENIS(TARGET)
				PRINTFORML ＜%CALLNAME:MASTER%処女喪失＞
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					CSTR:MASTER:性転換処女 = %NAME:TARGET%
				ELSE
					CSTR:MASTER:処女 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				中出し回数:(NO:MASTER) += 2
				中出され回数:MASTER:(NO:TARGET) += 2
				CALL EXPUP, "射精経験", 2
				CALL EXPUP, "絶頂経験", 2
				CALL EXPUP, "Ｖ経験", 2, MASTER
				CALL EXPUP, "膣射経験", 2, MASTER
			ELSE
				CALL EXPUP, "絶頂経験", 3
			ENDIF
			IF PENIS(MASTER)
				CALL EXPUP, "精液経験", 2
				CALL EXPUP, "射精経験", 2, MASTER
				CALL EXPUP, "絶頂経験", 2, MASTER
			ENDIF
			IF TALENT:性別 == 2
				CALL EXPUP, "レズ経験", 5
				CALL EXPUP, "レズ経験", 5, MASTER
			ENDIF
			IF PENIS(TARGET)
				CALL EXPUP, "性交経験", 2
				CALL EXPUP, "性交経験", 2, MASTER
				経験回数:MASTER:(NO:TARGET) += 2
				経験回数:(NO:MASTER) += 2
			ENDIF
			CALL EXPUP, "奉仕快楽経験", 3
			CFLAG:依存度 += TFLAG:誘惑ポイント*3/2
			PRINTL ＜依存度増加＞
			CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*70
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*100
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*150
			CALL JUELUP, "屈服", TFLAG:誘惑ポイント*50
			CALL JUELUP, "習得", TFLAG:誘惑ポイント*70
		ELSE
			TFLAG:色仕掛け = 4
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
				CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
				CALL JUELUP, "恥情", TFLAG:誘惑ポイント*30
			ENDIF
		ENDIF
ENDSELECT

RETURN 0

