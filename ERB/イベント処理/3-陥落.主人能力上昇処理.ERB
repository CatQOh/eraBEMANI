;素質変化イベント.ERB
@EVENTCHECK_ABL
;奴隷の素質変化 憑依中は変化しない
SIF TARGET >= 0 && !CFLAG:ポゼ
	CALL EVENTCHECK_T
IF ハットトリック
	ハットトリック = 0
	ハットトリック:1++
	SIF ハットトリック:1 >= 3
		CALL 実績解除, "HAT TRICK"
ELSE
	VARSET ハットトリック, 0
ENDIF
;調教者の素質変化 エナジードライブ中は変化しない
SIF MASTER >= 0 && !エナジードライブ
	CALL EVENTCHECK_M
;助手の素質変化 憑依中は変化しない
SIF ASSI >= 0 && !CFLAG:ASSI:ポゼ
	CALL EVENTCHECK_A

;---------------------------------------------------------
;陥落素質 崩壊した奴隷は陥落しない アニマルも陥落しない
;---------------------------------------------------------
@EVENTCHECK_T
#DIM DYNAMIC LCOUNT

;陥落素質
IF !CFLAG:陥落 && !TALENT:崩壊 && !TALENT:アニマル
	;恋慕 = 従順、奉仕精神3以上+苦痛刻印Lv0+反発刻印Lv0+奉仕快楽経験200以上+依存度1000以上+主人調教経験200以上+助手調教経験500未満+異常経験2未満or増加なし
	{
	IF ABL:従順 >= 3 && ABL:奉仕精神 >= 3 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && EXP:奉仕快楽経験 >= 100 && CFLAG:依存度 >= 1000 && 
	   EXP:主人調教経験 >= 200 && EXP:助手調教経験 < 500 && (EXP:異常経験 < 2 || (EXISTCSV(NO:TARGET) && EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験"))))
	}
		CALL KOJO_MESSAGE_EVENT, "恋慕"
		IF 相性値(TARGET, MASTER) < 100
			CALL 相性加算, TARGET, MASTER, 200-相性値(TARGET, MASTER)
		ELSE
			CALL 相性加算, TARGET, MASTER, 100
		ENDIF
		TALENT:恋慕 = 1
		FLAG:累計恋慕++
		CFLAG:陥落 = 1
		CALL 実績解除, "決めた 明日 好きって言うってね"
		CALL 経験値取得, TARGET, "陥落"
	;淫乱 = 欲望3以上+CVAB感覚合計12以上+快楽刻印Lv3+絶頂経験100以上+異常経験2以上+主人調教経験100以上+助手調教経験100以上
	{
	ELSEIF ABL:欲望 >= 3 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 12 && MARK:快楽刻印 == 3 && 
		   EXP:絶頂経験 >= 100 && EXP:異常経験 >= 2 && EXP:主人調教経験 >= 100 && EXP:助手調教経験 >= 100
	}
		CALL KOJO_MESSAGE_EVENT, "淫乱"
		TALENT:淫乱 = 1
		FLAG:累計淫乱++
		CFLAG:陥落 = 1
		CALL 経験値取得, TARGET, "陥落"
	;服従 = 従順、マゾっ気3以上+屈服or苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計100以上+依存度1000以上+助手調教経験500未満
	{
	ELSEIF ABL:従順 >= 3 && ABL:マゾっ気 >= 3 && (MARK:屈服刻印 == 3 || MARK:苦痛刻印 == 3) && 
		   MARK:反発刻印 == 0 && EXP:苦痛快楽経験+EXP:緊縛経験 >= 100 && CFLAG:依存度 >= 1000 && EXP:助手調教経験 < 500
	}
		CALL KOJO_MESSAGE_EVENT, "服従"
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 120
		;		RELATION:COUNT = 120
		;REND
		TALENT:服従 = 1
		FLAG:累計服従++
		CFLAG:陥落 = 1
		CALL 経験値取得, TARGET, "陥落"
	ENDIF
	IF CFLAG:陥落
		;実績:女性主人で識を陥落させる
		SIF SEX(MASTER) == 2 && CHARANAMEF(TARGET) == "識"
		    CALL 実績解除, "寂しげな薬指"
		ハットトリック:0 = 1
		;実績:Ｖ経験、Ａ経験、性交経験が0のまま陥落させる
		SIF EXP:Ｖ経験 == 0 && EXP:Ａ経験 == 0 && EXP:性交経験 == 0
			CALL 実績解除, "前☆戯☆王"
		;ユリア天使化イベント
		SELECTCASE CHARANAMEF(TARGET)
			{
			CASE "愛野 ヒカリ", "美嶋 はるな", "兎音 ココ", "葉和 とれび",
				 "八咫烏 鋼太郎", "観音寺 にこる", "津久井 シズノ", "新井 桃子",
				 "イングリット・オーリック・コーネル", "知雲 ひばり", "伊賀崎ノ 楠子",
				 "天稲荷 コテツ"
			}
				GUMINDAYS++
		ENDSELECT
	ENDIF
ENDIF

;上位陥落素質
IF !TALENT:親愛 && !TALENT:娼婦 && !TALENT:隷属 && !TALENT:崩壊 && !TALENT:アニマル
	;親愛 = 恋慕取得済み、従順、奉仕精神5以上+苦痛刻印Lv0+反発刻印Lv0+愛情経験200以上+奉仕快楽経験400以上+依存度3000以上+主人調教経験500以上+助手調教経験1000未満+異常経験5未満or増加なし
	{
	IF TALENT:恋慕 && ABL:従順 >= 5 && ABL:奉仕精神 >= 5 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && 
	   EXP:愛情経験 >= 200 && EXP:奉仕快楽経験 >= 400 && CFLAG:依存度 >= 3000 && EXP:主人調教経験 > 500 && 
	   EXP:助手調教経験 < 1000 && (EXP:異常経験 < 5 || EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験")))
	}
		CALL KOJO_MESSAGE_EVENT, "親愛"
		IF 相性値(TARGET, MASTER) < 200
			CALL 相性加算, TARGET, MASTER, 300-相性値(TARGET, MASTER)
		ELSE
			CALL 相性加算, TARGET, MASTER, 100
		ENDIF
		TALENT:恋慕 = 0
		TALENT:親愛 = 1
		;実績「理子に親愛を付ける」
		SIF CHARANAMEF(TARGET) == "理子"
			CALL 実績解除, "Affection"
	;娼婦 = 淫乱取得済み、欲望5以上+CVAB感覚合計20以上+反発刻印Lv0+絶頂経験300以上+異常経験5以上+主人調教経験300以上+助手調教経験300以上
	{
	ELSEIF TALENT:淫乱 && ABL:欲望 >= 5 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 20 && MARK:反発刻印 == 0 && 
		   EXP:絶頂経験 >= 300 && EXP:異常経験 >= 5 && EXP:主人調教経験 >= 300 && EXP:助手調教経験 >= 300
	}
		CALL KOJO_MESSAGE_EVENT, "娼婦"
		TALENT:淫乱 = 0
		TALENT:娼婦 = 1
	;隷属 = 服従取得済み、従順、マゾっ気5以上+屈服刻印Lv3+苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計300以上+依存度3000以上+助手調教経験1000未満+異常経験3以上
	{
	ELSEIF TALENT:服従 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && MARK:屈服刻印 == 3 && MARK:苦痛刻印 == 3 && MARK:反発刻印 == 0 && 
		   EXP:苦痛快楽経験+EXP:緊縛経験 >= 300 && CFLAG:依存度 >= 3000 && EXP:助手調教経験 < 1000 && EXP:異常経験 >= 3
	}
		CALL KOJO_MESSAGE_EVENT, "隷属"
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 150
		;		RELATION:COUNT = 150
		;REND
		TALENT:服従 = 0
		TALENT:隷属 = 1
	ENDIF
ENDIF

;---------------------------------------------------------
;特殊性感素質、一つしか取得はできない（重複した場合は上から優先）
;---------------------------------------------------------
IF !TALENT:淫核 && !TALENT:淫膣 && !TALENT:淫尻 && !TALENT:淫乳
	;淫核：Ｃ感覚6以上、露出癖4以上、自慰経験300以上、絶頂経験500以上
	;男なら加えてＡ感覚4以上、Ａ経験300以上必要
	IF ABL:Ｃ感覚 >= 6 && ABL:露出癖 >= 4 && EXP:自慰経験 >= 300 && EXP:絶頂経験 >= 500
		IF SEX(TARGET) == 1
			IF ABL:Ａ感覚 >= 3 && EXP:Ａ経験 >= 300
				CALL KOJO_MESSAGE_EVENT, "淫核"
				TALENT:淫核 = 1
				GOTO 性感素質終了
			ENDIF
		ELSE
			CALL KOJO_MESSAGE_EVENT, "淫核"
			TALENT:淫核 = 1
			GOTO 性感素質終了
		ENDIF
	ENDIF
	;淫膣：Ｖ感覚6以上、性交中毒4以上、性交経験1000以上、絶頂経験500以上
	;男だと取得できない ふたなりならできる
	IF ABL:Ｖ感覚 >= 6 && ABL:性交中毒 >= 4 && EXP:性交経験 >= 1000 && EXP:絶頂経験 >= 500 && VAGINA(TARGET)
		CALL KOJO_MESSAGE_EVENT, "淫膣"
		TALENT:淫膣 = 1
		GOTO 性感素質終了
	ENDIF
	;淫尻：Ａ感覚6以上、性交中毒4以上、Ａ経験1000以上、絶頂経験500以上
	IF ABL:Ａ感覚 >= 6 && ABL:性交中毒 >= 4 && EXP:Ａ経験 >= 1000 && EXP:絶頂経験 >= 500
		CALL KOJO_MESSAGE_EVENT, "淫尻"
		TALENT:淫尻 = 1
		GOTO 性感素質終了
	ENDIF
	;淫乳：Ｂ感覚6以上、露出癖4以上噴乳経験300以上、絶頂経験500以上
	;男だと取得できない……？
	IF ABL:Ｂ感覚 >= 6 && ABL:露出癖 >= 4 && EXP:噴乳経験 >= 300 && EXP:絶頂経験 >= 500
		CALL KOJO_MESSAGE_EVENT, "淫乳"
		TALENT:淫乳 = 1
		GOTO 性感素質終了
	ENDIF
ENDIF
$性感素質終了

;---------------------------------------------------------
;特殊性癖素質
;---------------------------------------------------------
;バストサイズ:0(すなわち男)のとき、B感覚Lv10以上かつメス度Lv6以上で胸が膨らむ
IF ABL:Ｂ感覚 >= 10 && ABL:メス度 >= 6
	IF FLAG:メスイキ豊胸ランダム == 0 && TALENT:バストサイズ == 0
		TALENT:バストサイズ = 2
		CALL KOJO_MESSAGE_EVENT, "乳首開発"
	ELSEIF FLAG:メスイキ豊胸ランダム == 1 && TALENT:バストサイズ == 0 && CFLAG:メスイキ豊胸限界 == 0
	;ここからメスイキ豊胸限界値の設定
		IF TALENT:小柄体型 ==1
			SELECTCASE RAND:100
				;小柄体型は15%で絶壁まで
				CASE IS < 15
					CFLAG:メスイキ豊胸限界 = 1
				;小柄体型は40%で貧乳まで
				CASE IS < 55
					CFLAG:メスイキ豊胸限界 = 2
				;小柄体型は25%で並乳まで
				CASE IS < 80
					CFLAG:メスイキ豊胸限界 = 3
				;小柄体型は8%で巨乳まで
				CASE IS < 88
					CFLAG:メスイキ豊胸限界 = 4
				;小柄体型は7%で豊乳まで
				CASE IS < 95
					CFLAG:メスイキ豊胸限界 = 5
				;小柄体型は5%で爆乳
				CASEELSE
					CFLAG:メスイキ豊胸限界 = 6
			ENDSELECT
		ELSEIF TALENT:小柄体型 == 0
			SELECTCASE RAND:100
				;普通体型は10%で絶壁まで
				CASE IS < 10
					CFLAG:メスイキ豊胸限界 = 1
				;普通体型は23%で貧乳まで
				CASE IS < 33
					CFLAG:メスイキ豊胸限界 = 2
				;普通体型は34%で並乳まで
				CASE IS < 67
					CFLAG:メスイキ豊胸限界 = 3
				;普通体型は13%で巨乳まで
				CASE IS < 80
					CFLAG:メスイキ豊胸限界 = 4
				;普通体型は10%で豊乳まで
				CASE IS < 90
					CFLAG:メスイキ豊胸限界 = 5
				;普通体型は10%で爆乳まで
				CASEELSE
					CFLAG:メスイキ豊胸限界 = 6
			ENDSELECT
		ENDIF
	ELSEIF FLAG:メスイキ豊胸ランダム == 1 && CFLAG:メスイキ豊胸限界 > 0
		IF CFLAG:メスイキ豊胸限界 == 1 ;ここからランダム化時の豊胸処理
			TALENT:バストサイズ = 1
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ELSEIF CFLAG:メスイキ豊胸限界 >= 2 && TALENT:バストサイズ == 0
			TALENT:バストサイズ = 2
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ELSEIF CFLAG:メスイキ豊胸限界 >= 3 && ABL:メス度 >= 7 && TALENT:バストサイズ <= 2
			TALENT:バストサイズ = 3
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ELSEIF CFLAG:メスイキ豊胸限界 >= 4 && ABL:メス度 >= 8 && TALENT:バストサイズ <= 3
			TALENT:バストサイズ = 4
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ELSEIF CFLAG:メスイキ豊胸限界 >= 5 && ABL:メス度 >= 9 && TALENT:バストサイズ <= 4
			TALENT:バストサイズ = 5
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ELSEIF CFLAG:メスイキ豊胸限界 == 6 && ABL:メス度 >= 10 && TALENT:バストサイズ <= 5
			TALENT:バストサイズ = 5
			CALL KOJO_MESSAGE_EVENT, "乳首開発"
		ENDIF
	ENDIF
ENDIF

;サド：サドっ気Lv4以上、嗜虐快楽経験300以上、技巧Lv4以上
IF !TALENT:サド && ABL:サドっ気 >= 4 && EXP:嗜虐快楽経験 >= 300 && ABL:技巧 >= 4
	CALL KOJO_MESSAGE_EVENT, "サド"
	TALENT:サド = 1
	;実績「駄天使ちゃんにサドを付ける」
	SIF CHARANAMEF(TARGET) == "駄天使ちゃん"
		CALL 実績解除, "(天使じゃ)ないです"
ENDIF

;マゾ：マゾっ気Lv6以上
IF !TALENT:マゾ && ABL:マゾっ気 >= 6
	CALL KOJO_MESSAGE_EVENT, "マゾ"
	TALENT:マゾ = 1
ENDIF

;音ゲーマー：音ゲー経験100以上
IF !TALENT:音ゲーマー && EXP:音ゲー経験 >= 100
	CALL KOJO_MESSAGE_EVENT, "音ゲーマー"
	TALENT:音ゲーマー = 1
ENDIF

;魔術師：魔法技能3以上
IF ABL:魔法技能 >= 3 && !TALENT:魔術師
	CALL KOJO_MESSAGE_EVENT, "魔術師"
	TALENT:魔術師 = 1
	MAXBASE:魔力 = ABL:魔法技能*1000
	BASE:魔力 = MAXBASE:魔力
ENDIF

;サキュバス：小悪魔持ち 経験人数10人以上
IF TALENT:小悪魔 && 経験人数(TARGET) >= 10 && !淫魔(TARGET)
	;精液中毒5以上でサキュバス
	IF ABL:精液中毒 >= 5
		CALL KOJO_MESSAGE_EVENT, "サキュバス"
		TALENT:サキュバス = 1
	;ペニス持ち 性交中毒5以上 中出し経験200以上(穴問わず)でインキュバス
	ELSEIF PENIS(TARGET) && ABL:性交中毒 >= 5 && 総中出し回数(TARGET) >= 200
		CALL KOJO_MESSAGE_EVENT, "インキュバス"
		TALENT:インキュバス = 1
	ENDIF
ENDIF

;流産による素質変動
IF TFLAG:流産
	CALL KOJO_MESSAGE_EVENT, "流産"
	TFLAG:流産 = 0
	;初流産による崩壊 [隷属]もしくは[崩壊]持ちなら回避できる
	IF !TALENT:隷属 && !TALENT:崩壊 && EXP:流産経験 == 0
		CFLAG:陥落 = 0
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "反抗的", "プライド高い", "生意気", "自制心", "好奇心", "楽観的"
					IF TALENT:LCOUNT
						PRINTFORMW %CALLNAME:TARGET%は【%TALENTNAME:LCOUNT%】を失った
						TALENT:LCOUNT = 0
					ENDIF
				CASE "崩壊", "無関心", "感情乏しい", "悲観的", "狂気"
					IF !TALENT:LCOUNT
						PRINTFORMW %CALLNAME:TARGET%は【%TALENTNAME:LCOUNT%】を得た
						TALENT:LCOUNT = 1
					ENDIF
			ENDSELECT
		NEXT
		CALL EXPUP, "異常経験", 3
	ENDIF
	CALL EXPUP, "流産経験", 1
	WAIT
	CALL CLEAR_FLAG, TARGET
ENDIF

;---------------------------------------------------------
;主人の素質および能力
;---------------------------------------------------------
@EVENTCHECK_M
;主人の撮影技能：撮影経験が10、50、200、500、1000、1600、2300、3100、4000、5000のたびに上昇、Lv10まで
SELECTCASE ABL:MASTER:撮影技能
	CASE 0
		SIF EXP:MASTER:撮影経験 >= 10
			ABL:MASTER:撮影技能++
	CASE 1
		SIF EXP:MASTER:撮影経験 >= 50
			ABL:MASTER:撮影技能++
	CASE 2
		SIF EXP:MASTER:撮影経験 >= 200
			ABL:MASTER:撮影技能++
	CASE 3
		SIF EXP:MASTER:撮影経験 >= 500
			ABL:MASTER:撮影技能++
	CASE 4
		SIF EXP:MASTER:撮影経験 >= 1000
			ABL:MASTER:撮影技能++
	CASE 5
		SIF EXP:MASTER:撮影経験 >= 1600
			ABL:MASTER:撮影技能++
	CASE 6
		SIF EXP:MASTER:撮影経験 >= 2300
			ABL:MASTER:撮影技能++
	CASE 7
		SIF EXP:MASTER:撮影経験 >= 3100
			ABL:MASTER:撮影技能++
	CASE 8
		SIF EXP:MASTER:撮影経験 >= 4000
			ABL:MASTER:撮影技能++
	CASE 9
		SIF EXP:MASTER:撮影経験 >= 5000
			ABL:MASTER:撮影技能++
ENDSELECT

;主人の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:MASTER:残数 <= 0 && !TALENT:MASTER:絶倫 && PENIS(MASTER) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:MASTER%は【絶倫】を身につけた
	TALENT:MASTER:絶倫 = 1
ENDIF

;主人のマゾ：マゾっ気がLv4以上でサド 現在主人のマゾっ気は上がらないので取得できないはず
IF ABL:MASTER:マゾっ気 >= 4 && !TALENT:MASTER:マゾ
	PRINTFORMW %NAME:MASTER%は【マゾ】を身につけた
	TALENT:MASTER:マゾ = 1
ENDIF

;主人の魔術師 魔法技能3以上
IF ABL:MASTER:魔法技能 >= 3 && !TALENT:MASTER:魔術師
	PRINTFORMW %NAME:MASTER%は【魔術師】を身につけた
	TALENT:MASTER:魔術師 = 1
	MAXBASE:MASTER:魔力 = ABL:MASTER:魔法技能*1000
	BASE:MASTER:魔力 = MAXBASE:MASTER:魔力
ENDIF

;ネクロマンサー 素質[霊感]持ちかつネクロ経験30以上
IF TALENT:MASTER:霊感 && CFLAG:MASTER:ネクロ経験 >= 30 && !TALENT:MASTER:ネクロマンサー
	PRINTFORMW %NAME:MASTER%は【ネクロマンサー】を身につけた
	TALENT:MASTER:ネクロマンサー = 1
ENDIF

;---------------------------------------------------------
;助手の素質および能力
;---------------------------------------------------------
@EVENTCHECK_A
;助手のサド：サドっ気がLv4以上、嗜虐快楽経験300以上でサド
IF ABL:ASSI:サドっ気 >= 4 && EXP:ASSI:嗜虐快楽経験 >= 300 && !TALENT:ASSI:サド
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は人を痛めつける喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【サド】を身につけた
	TALENT:ASSI:サド = 1
	;実績「駄天使ちゃんにサドを付ける」
	SIF CHARANAMEF(ASSI) == "駄天使ちゃん"
		CALL 実績解除, "(天使じゃ)ないです"
ENDIF

;助手のマゾ：マゾっ気がLv6以上でマゾ
IF ABL:ASSI:マゾっ気 >= 6 && !TALENT:ASSI:マゾ
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は体を痛めつけられる喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【マゾ】を身につけた
	TALENT:ASSI:マゾ = 1
ENDIF

;助手の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:ASSI:残数 <= 0 && !TALENT:ASSI:絶倫 && PENIS(ASSI) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:ASSI%は【絶倫】を身につけた
	TALENT:ASSI:絶倫 = 1
ENDIF

;ネクロマンサー 素質[霊感]持ちかつネクロ経験30以上
IF TALENT:ASSI:霊感 && CFLAG:ASSI:ネクロ経験 >= 30 && !TALENT:ASSI:ネクロマンサー
	PRINTFORMW %NAME:ASSI%は【ネクロマンサー】を身につけた
	TALENT:ASSI:ネクロマンサー = 1
ENDIF

;助手にした際の素質変化
@ASSI_CHANGE
;[狐]で技巧LV5、Ｃ感覚LV5、処女なら妖狐化
IF TALENT:ASSI:狐 && ABL:ASSI:技巧 >= 5 && ABL:ASSI:Ｃ感覚 >= 5 && TALENT:ASSI:処女
	CALL CHECKDRAWLINE
	PRINTFORMW %CALLNAME:ASSI%は[妖狐]となった
	DRAWLINE
	TALENT:ASSI:狐 = 0
	TALENT:ASSI:妖狐 = 1
ENDIF
