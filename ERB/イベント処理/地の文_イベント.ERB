;-------------------------------------------
;イベント口上テキスト
;-------------------------------------------
;朝フェラ
@EVENT_MESSAGE_朝フェラ
SELECTCASE RAND:3
	CASE 0
		PRINTFORML 朝、%CALLNAME:TARGET%のフェラで目を覚ました
		PRINTFORMW %CALLNAME:TARGET%は精液の貼りついた顔で朝の挨拶をしてきた
	CASE 1
		PRINTFORML 夢うつつの中でペニスへの快感が高まっていき、射精と同時に現実に引き戻される
		PRINTFORMW そこにはたった今出したばかりの精液を幸せそうに啜る%CALLNAME:TARGET%が居た
	CASE 2
		PRINTFORML 心地よい眠りの中、精液のこみ上げる感覚に慌てて目を覚ますと、
		PRINTFORMW %CALLNAME:TARGET%が%CALLNAME:MASTER%のペニスを咥え込んで精液を飲み下していた
ENDSELECT

;朝パイズリ 絶壁だと起こらない
@EVENT_MESSAGE_朝パイズリ
SELECTCASE TALENT:バストサイズ
	;貧乳の場合
	CASE 2
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 朝、目が覚めると同時に下腹部に違和感があるのに気づいた
				PRINTFORML 気づかれないように目をやると、%CALLNAME:TARGET%が胸でペニスを弄っていた
				PRINTFORMW どうやらパイズリの練習をしているらしい……
			CASE 1
				PRINTFORML 股間の辺りに重みを感じて目を覚ますと、%CALLNAME:TARGET%が薄い胸で懸命に奉仕していた
				PRINTFORMW 胸を押し付けるようにして扱いているため、%CALLNAME:MASTER%の服も精液で汚れてしまっている……
		ENDSELECT
	;普通
	CASE 3
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 朝、目が覚めると同時に下腹部に違和感があるのに気づいた
				PRINTFORMW 目をやると%CALLNAME:TARGET%が胸でペニスを擦り上げ、出てきた精液を吸っている
			CASE 1
				PRINTFORML 下半身が浮くような射精の快感に、%CALLNAME:MASTER%は喘ぎ声を上げて目を覚ました
				PRINTFORMW 射精へと導いた張本人の%CALLNAME:TARGET%は、乳房に吐き出された精液を愛おしげに見つめている
		ENDSELECT
	;巨乳、爆乳の場合
	CASE 4, 5
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 朝、目が覚めると同時に下腹部に違和感があるのに気づいた
				PRINTFORMW 目をやると%CALLNAME:TARGET%が大きな胸でペニスを挟み、刷り上げている
			CASE 1
				PRINTFORML 射精の快感に目を覚ました%CALLNAME:MASTER%の剛直が、%CALLNAME:TARGET%の柔らかく大きな乳房に挟まれている
				PRINTFORMW びくびくと震えながら胸の中に精液溜まりを作るペニスを、%CALLNAME:TARGET%はぎゅっと押さえ込んで離さない
		ENDSELECT
ENDSELECT

;朝騎乗位 Ｖ感覚orＡ感覚が3以上で発生 Ｖが優先 ペニスあれば対象も射精する
@EVENT_MESSAGE_朝騎乗位
IF ABL:Ｖ感覚 >= 3 && VAGINA(TARGET)
	SELECTCASE RAND:2
		CASE 0
			IF TALENT:性転換済 && SEX(TARGET) == 2
				PRINTFORML 下腹部に重みを感じて目を覚ますと、%CALLNAME:TARGET%が膣でペニスを咥え込んでいた
				PRINTFORML 女性の体にもすっかり慣れた%CALLNAME:TARGET%の膣内に射精すると、%CALLNAME:TARGET%は可愛らしい嬌声を上げて膣壁を震わせた
			ELSE
				PRINTFORML 夢か現実かも分からない中でどんどんとペニスへの快感が高まっていき、半ば寝ぼけたまま射精へと達してしまった
				PRINTFORML %CALLNAME:MASTER%がやっと目を覚ますと、互いの下半身で繋がり合った%CALLNAME:TARGET%が子種を搾り取っていた
			ENDIF
		CASE 1
			IF TALENT:小柄体型 && TALENT:小人体型
				PRINTFORML 喘ぎ声と水音にうとうと目を覚ますと、%CALLNAME:TARGET%が小さなヴァギナで朝勃ちしたペニスを咥え込んでいる
				PRINTFORML 未成熟な体で深く深く繋がろうとする%CALLNAME:TARGET%の体を撫でながら、求めるままに締め付けてくる膣内を精液を潤した
			ELSE
				PRINTFORML 朝起きると、%CALLNAME:TARGET%が騎乗位で喘いでいた
				PRINTFORML %CALLNAME:TARGET%の潤んだ瞳と視線が合うと、%CALLNAME:MASTER%はたまらず膣内に射精した
			ENDIF
	ENDSELECT
	SIF PENIS(TARGET)
		PRINTFORML ペニスで膣内が擦れる快感に、%CALLNAME:TARGET%も一緒に射精している……
ELSEIF ABL:Ａ感覚 >= 3
	SELECTCASE RAND:2
		CASE 0
			IF SEX(TARGET) == 1
				PRINTFORML 朝起きると、尻穴でペニスを受け入れた%CALLNAME:TARGET%が腰を動かしていた
				PRINTFORML 前立腺への刺激に柔らかいまま先走りを漏らし続ける%CALLNAME:TARGET%のペニスを扱いてやると、
				PRINTFORML 待ちわびていたかのように硬く勃起していった
			ELSE
				PRINTFORML 目を覚ますと、%CALLNAME:TARGET%が跨って腰を動かしていた
				PRINTFORML 朝から活発にアナルで快感を貪る%CALLNAME:TARGET%は、
				PRINTFORML 前の穴からだらだらと愛液を流して%CALLNAME:MASTER%の腹部を濡らしている
			ENDIF
		CASE 1
			PRINTFORML 朝一の射精の快感にはっと目を覚ますと、%CALLNAME:TARGET%が射精に脈打つ%CALLNAME:MASTER%のペニスを尻穴で咥え込んでいた
			PRINTFORML %CALLNAME:TARGET%は息を切らしながら尻穴に流れ込む精液を堪能している
	ENDSELECT
ENDIF
WAIT

;調教後3P
@EVENT_MESSAGE_調教後3P
;PRINTFORMW 調教が終わった後も%CALLNAME:MASTER%と%CALLNAME:ASSI%と%CALLNAME:TARGET%の3人は延々と行為に浸っている……
;全員男
IF SEX(MASTER) == 1 && SEX(ASSI)== 1 && SEX(TARGET) == 1
	PRINTFORML 調教後、3人は精液が出尽くすまで同性愛の営みに興じた……
	;助手or対象が童貞
	IF TALENT:童貞 || TALENT:ASSI:童貞
		SIF TALENT:童貞
			PRINTFORM %CALLNAME:TARGET%
		SIF TALENT:童貞 && TALENT:ASSI:童貞
			PRINT と
		SIF TALENT:ASSI:童貞
			PRINTFORM %CALLNAME:ASSI%
		PRINTL の初めての性交は、同性が相手ながらもしっかりと射精に達することができたようだ……
	ELSE
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 3人共、互いに出すのも出されるのにも楽しんで行為に夢中になっていた……
			CASE 1
				PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は二人共、同性が相手ながらもしっかりと射精まで行為を楽しんだ……
		ENDSELECT
	ENDIF
;主人だけ男
ELSEIF SEX(MASTER) == 1 && SEX(ASSI) == 2 && SEX(TARGET) == 2
	PRINTFORML 調教後、%CALLNAME:MASTER%は誘惑する二人の奴隷を前に剛直を滾らせた……
	IF !TALENT:処女 && !TALENT:再生処女 && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女
		PRINTFORML まるで違う二つの名器を交互に堪能する%CALLNAME:MASTER%は、互いの奥深くにしっかりと種を植え付けた……
	ELSEIF (TALENT:処女 || TALENT:再生処女) && (TALENT:ASSI:処女 || TALENT:ASSI:再生処女)
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%の息の合ったプレイに%CALLNAME:MASTER%は身を震わせて射精を繰り返した……
	ELSEIF TALENT:処女 || TALENT:再生処女
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は%CALLNAME:MASTER%に体の至るところを使って奉仕した……
	ENDIF
	;奴隷か助手にペニスあり
	IF PENIS(TARGET) || PENIS(ASSI)
		PRINTFORM %CALLNAME:MASTER%はひとしきり射精を楽しんだ後、期待に勃起する
		SIF PENIS(TARGET)
			PRINTFORM %CALLNAME:TARGET%
		SIF PENIS(TARGET) && PENIS(ASSI)
			PRINT と
		SIF PENIS(ASSI)
			PRINTFORM %CALLNAME:ASSI%
		PRINTFORML にも自分を犯させることにした……
		PRINTFORML 彼女たちは体を委ねる主人の口と尻穴を心ゆくまで堪能し、何度も中に射精していた……
	ENDIF
;助手だけ男
ELSEIF SEX(MASTER) == 2 && SEX(ASSI) == 1 && SEX(TARGET) == 2
	;主人非処女、もしくは奴隷非処女
	IF (!TALENT:MASTER:処女 && !TALENT:MASTER:再生処女) || (!TALENT:処女 && !TALENT:再生処女)
		PRINTFORML 調教後、%CALLNAME:MASTER%は%CALLNAME:TARGET%を抱き寄せて倒れ込むと、秘貝を擦り合わせて%CALLNAME:ASSI%を誘い込んだ……
	;両方処女
	ELSE
		PRINTFORML 調教後、%CALLNAME:MASTER%と%CALLNAME:TARGET%の息の合った奉仕で%CALLNAME:ASSI%に尽くすと、%CALLNAME:ASSI%は気持ち良さそうに射精を繰り返した……
	ENDIF
	;主人非処女、奴隷も非処女
	IF !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女 && !TALENT:処女 && !TALENT:再生処女
		PRINTFORML %CALLNAME:MASTER%と%CALLNAME:TARGET%は互いに唇を重ね合いながら、ふたつの女性器に何度もペニスを突き入れてくる%CALLNAME:ASSI%の精液を一滴残らず搾り取った……
	;主人だけ非処女
	ELSEIF !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		PRINTFORML %CALLNAME:TARGET%の処女に気を使った%CALLNAME:MASTER%は、\@ PENIS(TARGET) ? 腹部で%CALLNAME:TARGET%のペニスを擦りながら # 濡れた陰部で%CALLNAME:TARGET%のクリトリスを擦りながら \@、%CALLNAME:TARGET%の分まで%CALLNAME:ASSI%からの膣内射精を楽しんだ……
	;奴隷だけ非処女
	ELSEIF !TALENT:処女 && !TALENT:再生処女
		PRINTFORML 処女の%CALLNAME:MASTER%は%CALLNAME:ASSI%と%CALLNAME:TARGET%の結合部を指先で撫で回しながら、快楽に悶える二人の反応を見て自慰を楽しんだ……
	;両方処女
	ELSE
		PRINTFORML 互いの体に降り掛かった白い欲望を丁寧に舐め合う度、%CALLNAME:ASSI%のペニスは何度も勃起を繰り返している……
	ENDIF
	;主人か奴隷にペニスあり
	IF PENIS(MASTER) || PENIS(TARGET)
		PRINTFORM %CALLNAME:ASSI%が何度か射精した後、
		SIF PENIS(MASTER)
			PRINTFORM %CALLNAME:MASTER%
		SIF PENIS(MASTER) && PENIS(TARGET)
			PRINT と
		SIF PENIS(TARGET)
			PRINTFORM %CALLNAME:TARGET%
		PRINTFORML も勃起して収まらないペニスを見せつける
		PRINTFORML \@ PENIS(MASTER) && PENIS(TARGET) ? %CALLNAME:ASSI%は硬くそそり勃った二本のペニスを見て息を呑むが、目の前で欲情する女たちの # %CALLNAME:ASSI%は誘いに乗るように床に手をついては、硬くそそり勃つ \@男根を尻穴で受け入れた……
		SIF ABL:ASSI:Ａ感覚 >= 3
			PRINTFORML 内側から前立腺を激しく擦られる%CALLNAME:ASSI%は、中に出された精液がそのまま零れ落ちるかのように、自らのペニスからも精液を垂れ流している……
	ENDIF
;奴隷だけ男
ELSEIF SEX(MASTER) == 2 && SEX(ASSI) == 2 && SEX(TARGET) == 1
	;主人、助手共に非処女
	IF (!TALENT:MASTER:処女 && !TALENT:MASTER:再生処女) && (!TALENT:ASSI:処女 && !TALENT:ASSI:再生処女)
		PRINTFORML 調教後、%CALLNAME:MASTER%は%CALLNAME:ASSI%と一緒に、%CALLNAME:TARGET%の前で誘うように二人四つん這いになった
		PRINTFORML 二人の淫乱を前にして剛直を滾らせる%CALLNAME:TARGET%を、%CALLNAME:MASTER%と%CALLNAME:ASSI%は奥深くまで受け入れながら精液を搾り取った……
	;主人だけ非処女
	ELSEIF !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		PRINTFORML 調教後、%CALLNAME:ASSI%の処女を案じる%CALLNAME:MASTER%は、%CALLNAME:ASSI%にやり方を教えるように%CALLNAME:TARGET%から騎乗位で何度も搾り取った……
	;助手だけ非処女
	ELSEIF !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女
		PRINTFORML 調教後、%CALLNAME:MASTER%は%CALLNAME:ASSI%と%CALLNAME:TARGET%の営みを手伝うように二人の性器を愛撫しながら交配させた……
	;どっちも処女
	ELSE
		PRINTFORML 調教後、%CALLNAME:MASTER%と%CALLNAME:ASSI%は%CALLNAME:TARGET%を押し倒して、口や手で弄り回しながら何度も射精させた
		PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%は二人とも、処女膜に閉ざされた膣内から愛液を滴らせて精液を味わっている……
	ENDIF
	;主人か助手にペニスあり
	IF PENIS(MASTER) || PENIS(ASSI)
		PRINTFORM 射精の余韻に浸る%CALLNAME:TARGET%を
		SIF PENIS(MASTER)
			PRINTFORM %CALLNAME:MASTER%
		SIF PENIS(MASTER) && PENIS(ASSI)
			PRINT と
		SIF PENIS(ASSI)
			PRINTFORM %CALLNAME:ASSI%
		PRINTFORML は勃起するペニスを擦り付けるようにして抱き締める
		PRINTFORML 体に当たる硬い肉棒に%CALLNAME:TARGET%はごくりと唾を飲み込み、自らの尻穴を捧げてその欲望を受け止めた……
		SIF ABL:Ａ感覚 >= 3
			PRINTFORML %CALLNAME:TARGET%も尻穴に突き入れられるペニスに雌のような声で喘ぎながら射精している……
	ENDIF
;主人だけ女
ELSEIF SEX(MASTER) == 2 && SEX(ASSI) == 1 && SEX(TARGET) == 1
	;主人処女
	IF TALENT:MASTER:処女 || TALENT:MASTER:再生処女
		PRINTFORML 調教後も情欲の収まらない%CALLNAME:ASSI%と%CALLNAME:TARGET%の二本のペニスを、%CALLNAME:MASTER%は両手で扱きながら交互に舌を這わせて鎮めた……
		PRINTFORML すっかり成すがままにされる二人から%CALLNAME:MASTER%は何度も精液を搾り取った……
	;主人非処女
	ELSE
		PRINTFORML 調教後も情欲の収まらない%CALLNAME:ASSI%と%CALLNAME:TARGET%を、%CALLNAME:MASTER%は二人交互に自分の膣内に射精させた……
		PRINTFORML %CALLNAME:MASTER%の子宮は二人分の精液で満たされ、%CALLNAME:ASSI%と%CALLNAME:TARGET%も満足したようだ……
	ENDIF
	;主人ペニスあり
	IF PENIS(MASTER)
		PRINTFORML しかし一人満足に至らない%CALLNAME:MASTER%は男二人に劣らないほどにペニスを勃起させ、二人の奉仕を要求する
		PRINTFORML %CALLNAME:ASSI%と%CALLNAME:TARGET%は敬愛する主人に後ろの穴を差し出し、%CALLNAME:TARGET%は二つの尻穴を交互に楽しみながら両方の腸内に射精した……
		IF ABL:ASSI:Ａ感覚 >= 3 || ABL:Ａ感覚 >= 3
			SIF ABL:ASSI:Ａ感覚 >= 3
				PRINTFORM %CALLNAME:ASSI%
			SIF ABL:ASSI:Ａ感覚 >= 3 && ABL:Ａ感覚 >= 3
				PRINT と
			SIF ABL:Ａ感覚 >= 3
				PRINTFORM %CALLNAME:TARGET%
			PRINTL もまた尻穴から前立腺を犯される快感に、陰嚢に残った精液を絞り出すかのように精液を垂らしていた……
		ENDIF
	ENDIF
;助手だけ女
ELSEIF SEX(MASTER) == 1 && SEX(ASSI) == 2 && SEX(TARGET) == 1
	;助手処女
	IF TALENT:ASSI:処女 || TALENT:ASSI:再生処女
		PRINTFORML 調教後、未だ満足に至らない%CALLNAME:TARGET%のペニスを%CALLNAME:MASTER%と%CALLNAME:ASSI%の二人が責め立てる
		PRINTFORML 射精の余韻に浸る%CALLNAME:TARGET%を尻目に、%CALLNAME:MASTER%と%CALLNAME:ASSI%が互いに手淫と口淫で快感を高め合うと、それに興奮した%CALLNAME:TARGET%はすぐに次の準備ができたようだ……
	;助手非処女
	ELSE
		PRINTFORML 調教後、未だ満足に至らない%CALLNAME:TARGET%に%CALLNAME:ASSI%を抱かせることにした
		PRINTFORML 性交に夢中になる二人を手伝うように撫で回しながら、膣内で果てる%CALLNAME:TARGET%と替わるように%CALLNAME:MASTER%も%CALLNAME:ASSI%と交わり始めた……
	ENDIF
	;助手ペニスあり
	IF PENIS(ASSI)
		PRINTFORML 劣情を隠し切れない%CALLNAME:ASSI%のペニスを見かねた%CALLNAME:MASTER%は、%CALLNAME:TARGET%に手本を見せるように尻穴で咥え込んで精液を受け止める
		PRINTFORML 萎え切らない%CALLNAME:ASSI%のそれを、%CALLNAME:TARGET%も尻穴に誘い込んでは自らの腸内に白い欲望を導いた……
		IF ABL:MASTER:Ａ感覚 >= 3 || ABL:Ａ感覚 >= 3
			SIF ABL:MASTER:Ａ感覚 >= 3
				PRINTFORM %CALLNAME:MASTER%
			SIF ABL:MASTER:Ａ感覚 >= 3 && ABL:Ａ感覚 >= 3
				PRINT と
			SIF ABL:Ａ感覚 >= 3
				PRINTFORM %CALLNAME:TARGET%
			PRINTL は女のように激しく抱かれると、硬度を取り戻した男根から今一度の射精を迎えた……
		ENDIF
	ENDIF
;奴隷だけ女
ELSEIF SEX(MASTER) == 1 && SEX(ASSI) == 1 && SEX(TARGET) == 2
	;奴隷処女
	IF TALENT:処女 || TALENT:再生処女
		PRINTFORML 調教後、興奮の収まらない%CALLNAME:MASTER%と%CALLNAME:ASSI%に、%CALLNAME:TARGET%は精一杯奉仕している
		PRINTFORML 口内や体の至る所に降りかかる二人分の精液でべっとりと汚れながら、%CALLNAME:TARGET%は淫靡な笑みを浮かべている……
	;奴隷非処女
	ELSE
		PRINTFORML 調教後、%CALLNAME:MASTER%と%CALLNAME:ASSI%の冷めやらぬ興奮を、%CALLNAME:TARGET%はしっかりとその膣で受け止めている
		PRINTFORML 二人のたくさんの子種を注がれた%CALLNAME:TARGET%は、膣口から生温かい精液を漏らしながら幸せそうにお腹を撫でている……
	ENDIF
	;奴隷ペニスあり
	IF PENIS(TARGET)
		PRINTFORML さらには限界まで勃起する自分のペニスからだらだらと先走りを垂らし、射精に満足する二人を見つめる
		PRINTFORML その光景に尻穴を疼かせる%CALLNAME:MASTER%と%CALLNAME:ASSI%は、%CALLNAME:TARGET%が満足するまで互いの腸内に射精させた……
		IF ABL:MASTER:Ａ感覚 >= 3 || ABL:ASSI:Ａ感覚 >= 3
			SIF ABL:MASTER:Ａ感覚
				PRINTFORM %CALLNAME:MASTER%
			SIF ABL:MASTER:Ａ感覚 && ABL:ASSI:Ａ感覚
				PRINT と
			SIF ABL:ASSI:Ａ感覚
				PRINTFORM %CALLNAME:ASSI%
			PRINTFORML は%CALLNAME:TARGET%の欲望をアナルにぶつけられ、残り汁のような薄い精液を吐き出した……
		ENDIF
	ENDIF
;全員女
ELSEIF SEX(MASTER) == 2 && SEX(ASSI) == 2 && SEX(TARGET) == 2
	PRINTFORML 調教後、三人はじゃれあうように女同士の後戯を楽しんだ
	;全員ペニス持ち
	IF PENIS(MASTER) && PENIS(ASSI) && PENIS(TARGET)
		PRINTFORML 三人はそれぞれ持った男根で男性器と女性器の快感を心ゆくまで楽しんだ……
	;主人と助手がペニス持ち
	ELSEIF PENIS(MASTER) && PENIS(ASSI)
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%と%CALLNAME:ASSI%の精液を浴びて喜んでいる……
	;主人と奴隷がペニス持ち
	ELSEIF PENIS(MASTER) && PENIS(TARGET)
		PRINTFORML %CALLNAME:ASSI%は%CALLNAME:MASTER%と%CALLNAME:TARGET%の精液を精一杯受け止めている……
	;助手と奴隷がペニス持ち
	ELSEIF PENIS(ASSI) && PENIS(TARGET)
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%と%CALLNAME:ASSI%のどちらの精液が多いかを確かめるように最後の一滴まで搾り取っている……
	;主人だけペニス持ち
	ELSEIF PENIS(MASTER)
		PRINTFORML %CALLNAME:MASTER%は射精の快感に愛液を垂らしながら二人の体を精液で汚している……
	;助手だけペニス持ち
	ELSEIF PENIS(ASSI)
		PRINTFORML &CALLNAME:ASSI&は子宮を疼かせる%CALLNAME:MASTER%と%CALLNAME:ASSI%に何度も射精している……
	;奴隷だけペニス持ち
	ELSEIF PENIS(TARGET)
		PRINTFORML 唯一ペニスを持つ%CALLNAME:TARGET%は%CALLNAME:MASTER%と%CALLNAME:ASSI%に執拗に責められ、精液が出尽くすまで二人に搾り取られている……
	;誰もペニス持ってない
	ELSE
		PRINTFORML 見境なく快感を貪り続ける三人は、精液のように濃い愛液を膣口から溢れさせている……
	ENDIF
ENDIF

;調教後セックス 条件で分岐
@EVENT_MESSAGE_調教後セックス
IF (SEX(TARGET) == 1 && SEX(MASTER) == 1 && ABL:Ａ感覚 <= 3) || (SEX(TARGET) == 2 && (TALENT:妊娠 || TALENT:処女 || TALENT:再生処女))
	CALL EVENT_MESSAGE_調教後セックス_SOFT
ELSE
	CALL EVENT_MESSAGE_調教後セックス_HARD
ENDIF

;調教後セックス
@EVENT_MESSAGE_調教後セックス_HARD
PRINTFORML %CALLNAME:MASTER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、ベッドの上でまぐわっていた……
IF ABL:Ｖ感覚 >= ABL:Ａ感覚
	LOCAL = ABL:Ｖ感覚
ELSEIF ABL:Ａ感覚 >= ABL:Ｖ感覚
	LOCAL = ABL:Ａ感覚
ELSE
	LOCAL = ABL:Ａ感覚
ENDIF
IF ABL:従順+LOCAL+ABL:奉仕精神 >= 13
	PRINTFORML %CALLNAME:TARGET%は名残惜しげに袖を引っ張る
	PRINTFORML しかしその手を振り解き、%CALLNAME:MASTER%は部屋を後にした……
ENDIF
WAIT

;調教後後戯
@EVENT_MESSAGE_調教後セックス_SOFT
IF SEX(TARGET) == 1 && SEX(MASTER) == 1 && ABL:Ａ感覚 <= 3
	PRINTFORML %CALLNAME:TARGET%は調教後も淫らな行為を求めてくるが、
	PRINTFORMW %CALLNAME:MASTER%を%CALLNAME:TARGET%は口と指で互いを慰め合った……
ELSEIF SEX(TARGET) == 2 && TALENT:妊娠
	PRINTFORML %CALLNAME:TARGET%は冷めやらぬ昂奮を抑えられず%CALLNAME:MASTER%に行為をせがんでくるが
	PRINTFORMW 身重の%CALLNAME:TARGET%を案じた%CALLNAME:MASTER%は口と指で互いを鎮めあった……
ELSEIF SEX(TARGET) == 2 && (TALENT:処女 || TALENT:再生処女)
	PRINTFORML %CALLNAME:TARGET%は冷めやらぬ昂奮を抑えられず%CALLNAME:MASTER%に行為をせがんでくるが
	PRINTFORMW %CALLNAME:TARGET%の貞操を案じた%CALLNAME:MASTER%は口と指で互いを慰め合った……
ENDIF

;強精神薬の反動
@EVENT_MESSAGE_強精神薬の反動
PRINTFORML ……部屋の向こうから%CALLNAME:TARGET%の甘い悲鳴が聞こえてきた
PRINTW 強精神剤の効果が切れ、強い快楽に溺れているようだ

;調教後搾乳
@EVENT_MESSAGE_調教後搾乳
PRINTL 調教終了後も胸の張りが納まらず、母乳を搾り出していたようだ
IF ABL:従順+ABL:露出癖+ABL:マゾっ気 >= 10
	PRINTFORML その母乳を%CALLNAME:TARGET%は器に溜めて持ってきた
	PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%が持ってきた母乳を頂くことにした……
ENDIF
WAIT

;調教後自慰
@EVENT_MESSAGE_調教後自慰
PRINTFORML どうやら%CALLNAME:TARGET%は自慰をしていたようだ……
SIF ABL:自慰中毒+ABL:Ｃ感覚+ABL:欲望 >= 10 && TIME == 0
	PRINTFORML そのことを%CALLNAME:TARGET%は報告してきた
SIF ABL:従順 >= 5 || ABL:欲望 >= 5
	PRINTFORML しかし何度自分を慰めてみても、%CALLNAME:MASTER%への欲望は満たされなかった……
WAIT

;調教後夜這い
@EVENT_MESSAGE_調教後夜這い
IF SEX(TARGET) == 1 || TALENT:処女 || TALENT:再生処女 || !PENIS(MASTER) || !CFLAG:MASTER:残数 || BASE:体力 < 500
	PRINTFORML 床に就く%CALLNAME:MASTER%に%CALLNAME:TARGET%が添い寝してくる
	IF TALENT:MASTER:小柄体型 && TALENT:小柄体型
		PRINTFORML %CALLNAME:MASTER%と%CALLNAME:TARGET%は孤独な夜を退けるように身を寄せ合って眠った
	ELSEIF (TALENT:MASTER:小柄体型 && !TALENT:小柄体型) || (SEX(TARGET) == 1 && SEX(MASTER) == 2 && !TALENT:小柄体型)
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の腕に抱かれながらぐっすりと眠った
	ELSE
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の寝顔を見ながらぐっすりと眠った
	ENDIF
ELSEIF SEX(TARGET) == 2
	PRINTFORML 調教が終わり、%CALLNAME:MASTER%が床に就こうとしたとき、突然%CALLNAME:TARGET%が部屋を訪ねてきた
	PRINTFORML そのまま彼女は%CALLNAME:MASTER%を押し倒し、蜜壷でペニスを咥え込み激しく腰を打ち付け始めた
	PRINTFORML 飢えた獣の口は涎を滴らせて怒張を貪り、子宮の渇きを癒している
	PRINTFORMW 想いを遂げた二人は繋がったまま抱き合い、深い眠りに落ちた……
ENDIF

;妊娠発覚時のイベント口上
@EVENT_MESSAGE_妊娠発覚時
;妊娠が確定していた場合
IF TALENT:妊娠確定
	PRINTFORMW 覚悟していたとはいえ、妊娠が体に及ぼす影響は%CALLNAME:TARGET%にとってあまり気持ちの良いものではないようだ……
;父親が主人で母親が恋慕か親愛持ち
ELSEIF FALLTYPE(TARGET) == 1 && PREG:21 == NO:MASTER
	PRINTFORMW %CALLNAME:TARGET%は困惑していたものの%CALLNAME:MASTER%の子を授かった喜びに表情を緩ませていた……
;淫乱か娼婦持ち
ELSEIF FALLTYPE(TARGET) == 2
	;父親との相性が100より大きい場合
	IF RELATION:(PREG:21) > 100
		PRINTFORMW %CALLNAME:TARGET%は%CSVCALLNAMEF(PREG:21)%の子を宿したことに顔を綻ばせていた……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の顔は青褪めていたが、何とか自分が妊娠した事実を受け止めれたようだ……
	ENDIF
;服従か隷属持ち
ELSEIF FALLTYPE(TARGET) == 3
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%を見上げ、謝罪の言葉を述べた……
	PRINTFORMW しかしその顔には密かに暗い笑みが浮かんでいた……
ELSE
	;娘の場合
	IF NO:TARGET > 10000
		IF EXP:出産経験
			PRINTFORMW かつて妊娠した時の苦難を思い出してか、%CALLNAME:TARGET%はあまり嬉しそうな様子ではない……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は妊娠の意味も分からずに、膨らんだお腹を撫でては首を傾げている……
		ENDIF
	ELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に何かを訴えるかのような激情の篭った視線を投げかけてきた
		PRINTFORMW しかし、%CALLNAME:TARGET%は自分が妊娠した事実を何とか受け止められたようだ……
	ENDIF
ENDIF

;出産時のイベント口上
@EVENT_MESSAGE_出産時
;崩壊がついている場合
IF TALENT:崩壊
	PRINTFORMW しかし、%CALLNAME:TARGET%は産まれた赤ん坊にいっこうに関心を示さなかった……
;母親が男の場合
ELSEIF SEX(TARGET) == 1
	IF EXP:出産経験 == 0
		PRINTFORMW %CALLNAME:TARGET%はまさか男である自分が本当に子を産むとは思っていなかったのか、赤ん坊の扱いが分からずにいる……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は赤ん坊の扱いにも慣れてきたのか、哺乳瓶でミルクを飲ませ始めた……
	ENDIF
;恋慕か親愛で、父親がマスターの場合
ELSEIF FALLTYPE(TARGET) == 1 && PREG:21 == NO:MASTER
	PRINTFORMW %CALLNAME:TARGET%は産まれたばかりの赤ん坊を受け取ると、優しく抱きかかえ母乳を飲ませ始めた……
;それ以外
ELSE
	PRINTFORMW %CALLNAME:TARGET%は何か陰のある表情をしながらも、赤ん坊を受け取ると母乳を飲ませ始めた……
ENDIF

;育児室でのイベント口上
@EVENT_MESSAGE_育児室
;まだ出産前の場合
IF TALENT:妊娠
	;恋慕か親愛で父親がMASTER
	IF FALLTYPE(TARGET) == 1 && PREG:20 == 1
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%の顔を見ると、
		PRINTFORML 元気づけられたように笑顔を向けてきた
		PRINTFORMW %CALLNAME:MASTER%はもうすぐ出産を迎える%CALLNAME:TARGET%を心の底から励ました
	;崩壊の場合
	ELSEIF TALENT:崩壊
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%を見ても特段の反応を示さなかった
		PRINTFORMW %CALLNAME:MASTER%はこれも罰なのかもしれないと思いながら帰るほかなかった……
	;それ以外
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の顔を見ると、少し戸惑いながらも笑顔を向けてきた……
	ENDIF
;恋慕か親愛で父親がMASTER
ELSEIF FALLTYPE(TARGET) == 1 && PREG:20 == 1
	IF RAND:2 == 0
		PRINTFORML %CALLNAME:MASTER%が部屋に入ってきたことに気づくと、%CALLNAME:TARGET%は満面の笑顔で出迎えた
		PRINTFORMW %CALLNAME:MASTER%はそれからしばらくの間、親子水入らずの時間を楽しんだ
	ELSE
		PRINTFORML %CALLNAME:MASTER%が部屋に入ってくると、子供がちょうど寝付いたところらしく、%CALLNAME:TARGET%は音を立てないようにと仕草で伝えてきた
		PRINTFORMW %CALLNAME:MASTER%は子供の寝顔を%CALLNAME:TARGET%と二人でかわいい寝顔をのぞき込み、そして、どちらからというわけでもなく顔を見合わせ、ほころばせていた
	ENDIF
;それ以外
ELSE
	PRINTFORML %CALLNAME:MASTER%が部屋に入ってくると、%CALLNAME:TARGET%は少しとまどいながらもそれを迎えた
	PRINTW それから、子供の様子を見ながら二人の会話を楽しんだ
ENDIF

;色仕掛けのイベント口上
@EVENT_MESSAGE_色仕掛け
SELECTCASE TFLAG:色仕掛け
	;夜這い成功
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%が寝静まった頃を見計らい、%CALLNAME:TARGET%の部屋へと忍び込む
		IF TALENT:MASTER:小柄体型
			PRINTFORM %CALLNAME:MASTER%が小さな体を活かして%CALLNAME:TARGET%の足元からベッドに潜り込み、
		ELSE
			PRINTFORM %CALLNAME:MASTER%が%CALLNAME:TARGET%の寝顔に軽くキスをして布団を剥ぎ、
		ENDIF
		IF PENIS(TARGET)
			PRINTW 服を脱がして無防備なペニスを口に含むと、生理現象で勃起していたペニスは簡単に精液を吐き出した
			PRINTFORMW 夢精で目を覚ました%CALLNAME:TARGET%の頭が覚醒する頃には既に、%CALLNAME:MASTER%が跨って先端に陰部を擦りつけていた……
		ELSE
			PRINTW 服を脱がして無防備な女性器に舌を這わせると、色気の混じる寝息を立てながらすぐ絶頂に達してしまった
			PRINTFORMW 予期せぬ絶頂に寝汗を浮かべる%CALLNAME:TARGET%だが、目を覚ます時には既に、体を走る快感をただ享受するしか出来なかった……
		ENDIF
	;夜這い失敗
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%が寝静まった頃を見計らい、%CALLNAME:TARGET%の部屋へと忍び込んだ
		IF TALENT:MASTER:小柄体型
			PRINTFORMW %CALLNAME:MASTER%が小さな体を活かして%CALLNAME:TARGET%の足元からベッドに潜り込むと、すぐさま違和感に気付いた%CALLNAME:TARGET%が目を覚ましてしまった……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%を覆う布団を剥いで服を脱がせようとすると、%CALLNAME:TARGET%はうめき声を上げて目を覚ましてしまった……
		ENDIF
		IF TALENT:臆病 || TALENT:大人しい
			PRINTFORMW %CALLNAME:TARGET%が悲鳴を上げたのち、やっと灯りを点けた時には、もう部屋の中に%CALLNAME:MASTER%の姿はなかった……
		ELSEIF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い || TALENT:ツンデレ || TALENT:女嫌い
			PRINTFORMW それがすぐさまどこぞの痴女だと気付いた%CALLNAME:TARGET%は、怒号を上げて%CALLNAME:MASTER%を部屋から締め出した……
		ELSE
			IF TALENT:MASTER:小柄体型
				PRINTFORMW 不意に覚醒した%CALLNAME:TARGET%に冗談交じりの挨拶をすると、%CALLNAME:TARGET%は%CALLNAME:MASTER%を部屋の外へ放り出し鍵をしてしまった……
			ELSE
				PRINTFORMW 招かざる訪問者に%CALLNAME:TARGET%は身を引いて衣服を正すと、%CALLNAME:MASTER%を追い出してしまった……
			ENDIF
		ENDIF
	;誘惑成功
	CASE 3
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%を誘惑すべく、誘う言葉と共に寝間着をはだけて見せた
		IF CFLAG:陥落
			IF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い
				PRINTFORMW %CALLNAME:TARGET%は媚びる%CALLNAME:MASTER%を見下すように不敵な笑みを浮かべながら、%CALLNAME:MASTER%を半ば強引に自室へと連れ込んだ……
			ELSEIF TALENT:無関心 || TALENT:臆病 || TALENT:保守的 || TALENT:恥じらい || TALENT:大人しい || TALENT:女嫌い
				PRINTFORMW 普段通り消極的な態度を見せつつ戸惑う%CALLNAME:TARGET%の手を引いて、%CALLNAME:MASTER%の自室へと連れ込んだ……
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は待っていたと言わんばかりに%CALLNAME:MASTER%の誘いに乗り、ふたりで共に一夜を過ごした……
			ENDIF
		ELSE
			IF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い
				PRINTFORMW %CALLNAME:MASTER%に情欲を煽られ表情を顰める%CALLNAME:TARGET%だが、%CALLNAME:MASTER%がさらに過激な誘惑を仕掛けると自棄になってその場で行為を始めた……
			ELSEIF TALENT:無関心 || TALENT:臆病 || TALENT:保守的 || TALENT:恥じらい || TALENT:大人しい || TALENT:女嫌い
				PRINTFORMW その場から動けずにいる%CALLNAME:TARGET%の手を引き、%CALLNAME:MASTER%の自室へと連れ込んだ……
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は理性と衝動の板挟みに耐えているが、お構いなしに%CALLNAME:MASTER%が互いの服を脱がし始めると諦めて誘いに乗った……
			ENDIF
		ENDIF
	;誘惑失敗
	CASE 4
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%を誘惑すべく、誘う言葉と共に寝間着をはだけて見せた
		SELECTCASE TFLAG:誘惑ポイント
			CASE IS <= 5
				IF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い || TALENT:女嫌い
					SELECTCASE TALENT:性別
						CASE 1
							PRINTFORMW しかし%CALLNAME:TARGET%はそれを鼻で笑い、自室へと戻ってしまった……
						CASE 2
							PRINTFORMW しかし%CALLNAME:TARGET%は哀れむような視線を投げかけながら、自室へと戻ってしまった……
					ENDSELECT
				ELSE
					PRINTFORMW しかし%CALLNAME:TARGET%は表情一つ変えずに、自室へと戻ってしまった……
				ENDIF
			CASE 6 TO 10
				IF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い || TALENT:女嫌い
					PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の姿を一瞥すると、若干不機嫌そうな顔をして自室へと戻ってしまった……
				ELSE
					PRINTFORMW %CALLNAME:TARGET%は一瞬たじろいつつも、逃げるように自室へと戻ってしまった……
				ENDIF
			CASE 11 TO 15
				IF TALENT:生意気 || TALENT:反抗的 || TALENT:プライド高い || TALENT:女嫌い
					PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%からわざとらしく視線を逸らすと、軽く文句を言いながら足取り重く自室へと戻っていった……
				ELSE
					PRINTFORMW %CALLNAME:MASTER%の突然の誘惑に、%CALLNAME:TARGET%は顔を赤くしながら自室へと逃げてしまった……
				ENDIF
		ENDSELECT
ENDSELECT

;売却時のイベント口上
@EVENT_MESSAGE_売却時

;ビデオ売却時のイベント口上
@EVENT_MESSAGE_ビデオ売却時

;写真売却時
@EVENT_MESSAGE_写真売却時

;ビデオ保管時のイベント口上
@EVENT_MESSAGE_ビデオ保管時

;調教対象に選んだ時
@EVENT_MESSAGE_調教対象指定

;助手に選んだ時
@EVENT_MESSAGE_助手指定

;主人による吸血イベント
@EVENT_MESSAGE_主人吸血
PRINTFORML %CALLNAME:MASTER%は抵抗できない%CALLNAME:TARGET%を抱き起してその首筋に噛みつくと、
PRINTFORMW 容赦なく%CALLNAME:TARGET%の体内から血を吸い始めた……
PRINTFORMW ………………
PRINTFORMW …………
PRINTFORMW ……
IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
	PRINTFORMW %CALLNAME:TARGET%は吸血鬼になってしまった！
	IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
		PRINTFORML 異形の存在にさせられてしまった%CALLNAME:TARGET%は困惑しつつも、
		PRINTFORMW %CALLNAME:MASTER%の眷属となれた喜びから、跪いて手の甲に口づけをして忠誠を誓った……
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:TARGET%はゾンビになってしまった！
ENDIF

;助手による吸血イベント
@EVENT_MESSAGE_助手吸血
PRINTFORML 体力を失って動けずにいる%CALLNAME:TARGET%の後始末を%CALLNAME:ASSI%に任せることにした。
PRINTFORMW 無抵抗な%CALLNAME:TARGET%を見下ろしながらそっとほくそ笑むと、%CALLNAME:ASSI%は%CALLNAME:TARGET%から血を吸い始めた……
PRINTFORMW ………………
PRINTFORMW …………
PRINTFORMW ……
IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
	PRINTFORMW %CALLNAME:TARGET%は吸血鬼になってしまった！
ELSE
	PRINTFORMW %CALLNAME:TARGET%はゾンビになってしまった！
ENDIF

;恋慕取得時
@EVENT_MESSAGE_恋慕
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%との行為に悦びを感じているようだ
PRINTFORMW %NAME:TARGET%は【恋慕】を得た

;淫乱取得時
@EVENT_MESSAGE_淫乱
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に与えられる快楽に溺れているようだ
PRINTFORMW %NAME:TARGET%は【淫乱】を得た

;服従取得時
@EVENT_MESSAGE_服従
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に心が平伏しきっているようだ
PRINTFORMW %NAME:TARGET%は【服従】を得た

;親愛取得時
@EVENT_MESSAGE_親愛
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は身も心も%CALLNAME:MASTER%に依存しきっているようだ
PRINTFORMW %NAME:TARGET%の【恋慕】は【親愛】になった

;娼婦取得時
@EVENT_MESSAGE_娼婦
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は身も心も%CALLNAME:MASTER%に与えられる快楽に依存しきっているようだ
PRINTFORMW %NAME:TARGET%の【淫乱】は【娼婦】になった

;隷属取得時
@EVENT_MESSAGE_隷属
PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい
PRINTFORML %CALLNAME:TARGET%は身も心も%CALLNAME:MASTER%の奴隷となりきっているようだ
PRINTFORMW %NAME:TARGET%の【服従】は【隷属】になった

;淫核
@EVENT_MESSAGE_淫核
IF SEX(TARGET) == 1
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%はペニスとアナルを弄りながら物欲しげな視線を%CALLNAME:MASTER%に送っている
	PRINTFORMW %NAME:TARGET%は【淫核】を得た
ELSE
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%は\@ PENIS(TARGET) ? ペニス # クリトリス \@を弄りながら物欲しげな視線を%CALLNAME:MASTER%に送っている
	PRINTFORMW %NAME:TARGET%は【淫核】を得た
ENDIF

;淫膣
@EVENT_MESSAGE_淫膣い
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%は%CALLNAME:MASTER%に擦り寄り抱いて欲しいと懇願してきた
PRINTFORMW %NAME:TARGET%は【淫膣い】を得た

;淫尻
@EVENT_MESSAGE_淫尻
IF SEX(TARGET) == 1
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%はアナルを弄りながら物欲しげな視線を%CALLNAME:MASTER%に送っている
	PRINTFORMW %NAME:TARGET%は【淫尻】を得た
ELSE
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%は%CALLNAME:MASTER%に擦り寄り抱いて欲しいと懇願してきた
	PRINTFORMW %NAME:TARGET%は【淫尻】を得た
ENDIF

;淫乳
@EVENT_MESSAGE_淫乳
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
PRINTFORML 調教が終わった後にもかかわらず、%CALLNAME:TARGET%は乳首を弄りながら物欲しげな視線を%CALLNAME:MASTER%に送っている
PRINTFORMW %NAME:TARGET%は【淫乳】を得た

;絶倫取得時
@EVENT_MESSAGE_絶倫
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
PRINTFORML %CALLNAME:TARGET%はより多くの精液を出すことに慣れてきたようだ
PRINTFORMW %NAME:TARGET%は【絶倫】を身につけた

;サド取得時
@EVENT_MESSAGE_サド
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
PRINTFORML %CALLNAME:TARGET%は人を痛めつける喜びに目覚めたようだ
PRINTFORMW %NAME:TARGET%は【サド】を得た

;マゾ取得時
@EVENT_MESSAGE_マゾ
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
PRINTFORML %CALLNAME:TARGET%は体を痛めつけられる喜びに目覚めたようだ
PRINTFORMW %NAME:TARGET%は【マゾ】を得た

;流産の調教後
@EVENT_MESSAGE_流産
PRINTFORML %CALLNAME:TARGET%は過度な調教により流産しました
;初流産による崩壊 [隷属]もしくは[崩壊]持ちなら回避できる
IF !TALENT:隷属 && !TALENT:崩壊 && EXP:流産経験 == 0
	PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
	PRINTFORML %CALLNAME:TARGET%は流産によるショックで壊れてしまったようだ
ENDIF

;おもらし癖取得時
@EVENT_MESSAGE_おもらし癖
PRINTFORMW %CALLNAME:TARGET%は慌てた様子を見せている
PRINTFORM %CALLNAME:TARGET%は
SIF TALENT:幼稚 && EXP:放尿経験 >= 30
	PRINTFORM おねしょを
SIF !TALENT:幼稚 && EXP:放尿経験 >= 100
	PRINTFORM 粗相を
PRINTFORMW してしまったようだ……
PRINTFORMW %NAME:TARGET%は[おもらし癖]がついた

;幼児退行時
@EVENT_MESSAGE_幼児退行
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい……
PRINTFORMW %CALLNAME:TARGET%は過酷な調教に耐え切れず、【幼児退行】してしまったようだ……

;調合知識伝授イベント
@EVENT_MESSAGE_調合知識伝授
PRINTFORMW どうやら%CALLNAME:ASSI%が%CALLNAME:MASTER%に薬学の知識を教えてくれるそうだ
PRINTFORML %CALLNAME:ASSI%の説明で、%CALLNAME:MASTER%は基本的な調合知識を得ることが出来た
PRINTFORMW %NAME:MASTER%は【調合知識】を得た

;料理を振る舞うイベント
@EVENT_MESSAGE_料理
SIF !TFLAG:お菓子
	PRINTFORMW %CALLNAME:TARGET%が作った料理をみんなでいただくことにした……

;お菓子を振る舞うイベント
@EVENT_MESSAGE_お菓子
IF TFLAG:料理
	PRINTFORMW %CALLNAME:TARGET%が作った料理とお菓子をみんなでいただくことにした……
ELSE
	PRINTFORMW %CALLNAME:TARGET%が作ったお菓子をみんなでいただくことにした……
ENDIF

;コマンド拒否時のイベント口上
@EVENT_MESSAGE_コマンド拒否時
IF FLAG:ゲームモード == 7
	PRINTFORMW %CALLNAME:TARGET%は言うことを聞いてくれない。どうやらまだその気になっていないようだ
ELSE
	PRINTFORMW %CALLNAME:TARGET%は言うことを聞いてくれない。どうやら調教が足りないようだ
ENDIF

;飲酒拒否時の口上
@EVENT_MESSAGE_飲酒拒否時
SELECTCASE ITEMNAME:(TFLAG:お酒)
	CASE "スーパードライ", "一番搾り", "プレミアムモルツ", "ヱビス"
		IF TALENT:小柄体型 || TALENT:下戸
			PRINTFORMW 酒を勧めたが断られてしまった。ビールは苦手らしい……
		ELSE
			IF CFLAG:陥落
				PRINTFORMW 酒を勧めたが断られてしまった。今はまだお酒を飲む気分にはなれないようだ……
			ELSE
				PRINTFORMW 酒を勧めたが断られてしまった。お酒を飲む気分にはなれないようだ……
			ENDIF
		ENDIF
	CASEELSE
		IF TALENT:小柄体型 && !TALENT:酒豪
			PRINTFORMW 酒を勧めたが断られてしまった。%CALLNAME:TARGET%にお酒は少し早すぎるかもしれない……
		ELSEIF TALENT:無関心 && !TALENT:酒豪
			PRINTFORMW 酒を勧めたが断られてしまった。あまりお酒に関心が無いらしい……
		ELSE
			PRINTFORMW 酒を勧めたが断られてしまった。%CALLNAME:TARGET%にとっては少し強すぎるようだ……
		ENDIF
ENDSELECT

;【音ゲーマー】取得時
@EVENT_MESSAGE_音ゲーマー
PRINTFORML %CALLNAME:TARGET%は音楽ゲームの楽しさを覚えたようだ
PRINTFORMW %CALLNAME:TARGET%は【音ゲーマー】を得た

