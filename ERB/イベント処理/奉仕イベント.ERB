;奴隷からの奉仕などのイベント
;-------------------------------------------------
;朝に起こるイベント
;-------------------------------------------------
@EVENT_NEWDAY_SELF
;調教対象が居ない場合は終了
SIF TARGET < 0
	RETURN 0

;アニマルで懐いてる場合は朝に甘えるイベント
IF TALENT:アニマル && CFLAG:依存度 >= 1500
	CALL EVENT_CHECK, "アニマル"
	RETURN
ENDIF

;陥落素質が無い場合は関数終了
SIF !CFLAG:陥落
	RETURN 0

;主人が射精ができない場合は関数終了
SIF !PENIS(MASTER)
	RETURN 0

;対象が二日酔い起こしてると発生しない
SIF CFLAG:二日酔い
	RETURN 0

;朝騎乗位判定
;出産トラウマがある場合は発生しない時がある
LOCAL = CFLAG:出産トラウマ
;1+(トラウマ値/10)/2+(トラウマ値/10) 
IF ((!TALENT:処女 && !TALENT:再生処女 && ABL:Ｖ感覚 >= 3) || ABL:Ａ感覚 >= 3) && ABL:奉仕精神 >= 7 && !(LOCAL && RAND:(LOCAL/10+2) <= LOCAL/10)
	CALL EVENT_CHECK, "朝騎乗位"
;朝パイズリ判定
ELSEIF TALENT:バストサイズ >= 2 && ABL:奉仕精神 >= 5
	CALL EVENT_CHECK, "朝パイズリ"
;朝フェラ判定
ELSE
	CALL EVENT_CHECK, "朝フェラ"
ENDIF

;DISABLE_EVENTが行われていた場合は表示しない
SIF RESULT == 0
	RETURN 0

SELECTCASE FALLTYPE(TARGET)
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%たちは\@ FLAG:ゲームモード == 7 ? 朝から濃厚な # 調教そっちのけで \@情事に耽っている……
		EXP:愛情経験 += 5
		JUEL:恭順 += 250
		PRINTL 愛情経験+5
		PRINTL 恭順の珠+250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			EXP:ゲイ経験 += 5
			PRINTL ゲイ経験+5
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			EXP:レズ経験 += 5
			PRINTL レズ経験+5
		ENDIF
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の腕を離さずに次の夢へと誘っている……
		EXP:精液経験 += 5
		JUEL:欲情 += 250
		PRINTL 精液経験+5
		PRINTL 欲情の珠+250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			EXP:ゲイ経験 += 5
			PRINTL ゲイ経験+5
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			EXP:レズ経験 += 5
			PRINTL レズ経験+5
		ENDIF
	CASE 3
		PRINTFORMW %CALLNAME:TARGET%は今朝も%CALLNAME:MASTER%への忠誠を誓っている……
		EXP:主人調教経験 += 20
		JUEL:屈服 += 250
		PRINTL 主人調教経験+20
		PRINTL 屈服の珠+250
		IF SEX(MASTER) == 1 && SEX(TARGET) == 1
			EXP:ゲイ経験 += 5
			PRINTL ゲイ経験+5
		ELSEIF SEX(MASTER) == 2 && SEX(TARGET) == 2
			EXP:レズ経験 += 5
			PRINTL レズ経験+5
		ENDIF
ENDSELECT

CALL CHECKDRAWLINEW


@EVENT_朝フェラ
#DIM DYNAMIC フェラ回数
#DIM DYNAMIC 射精回数

DRAWLINE
CALL KOJO_MESSAGE_EVENT, "朝フェラ"

;ここからフェラ回数の計算
フェラ回数 = (RELATION:TARGET:(NO:MASTER))/100
フェラ回数 += ABL:精液中毒

SIF TALENT:汚臭鈍感
	フェラ回数++
SIF TALENT:汚臭敏感
	フェラ回数--
SIF TALENT:潔癖症
	フェラ回数--
SIF TALENT:献身的
	フェラ回数++
SIF TALENT:淫乱
	フェラ回数 += 2
SIF TALENT:娼婦
	フェラ回数 += 3

SIF フェラ回数 < 1
	フェラ回数 = 1

;ここから射精回数の計算
;ただしどれだけ多くてもフェラ回数よりは多く射精しない
射精回数 = 10*フェラ回数;整数しか扱えない関係で一旦10倍に
SIF !TALENT:汚れ無視
	TIMES 射精回数, 0.90
SIF !TALENT:舌使い
	TIMES 射精回数, 0.80
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
射精回数 *= ABL:技巧
射精回数 /= 30
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
射精回数 /= 10
射精回数 = LIMIT(射精回数, 1, フェラ回数)
IF TALENT:MASTER:絶倫
	射精回数 = MIN(射精回数, 15)
ELSE
	射精回数 = MIN(射精回数, 10)
ENDIF

EXP:フェラ経験 += フェラ回数
EXP:精液経験 += 射精回数
JUEL:恭順 += フェラ回数*100
JUEL:屈服 += フェラ回数*30
JUEL:習得 += フェラ回数*40
EXP:MASTER:射精経験 += 射精回数
PRINTFORMW 今朝は%CALLNAME:TARGET%のフェラで{射精回数}回射精した

PRINTFORML フェラ経験+{フェラ回数}
PRINTFORML 精液経験+{射精回数}
PRINTFORML 恭順の珠+{フェラ回数*100}
PRINTFORML 屈服の珠+{フェラ回数*30}
PRINTFORML 習得の珠+{フェラ回数*40}
PRINTFORMW 射精経験+{射精回数}（%CALLNAME:MASTER%）

CFLAG:朝フェラ経験++
CFLAG:MASTER:朝フェラ経験++
CFLAG:口で射精させた経験++


@EVENT_朝パイズリ
#DIM DYNAMIC パイズリ回数
#DIM DYNAMIC 射精回数

DRAWLINE
CALL KOJO_MESSAGE_EVENT, "朝パイズリ"

;ここからパイズリ回数の計算
パイズリ回数 = (RELATION:TARGET:(NO:MASTER))/75
パイズリ回数 += ABL:精液中毒
パイズリ回数 += ABL:奉仕精神/2
パイズリ回数 += ABL:Ｂ感覚/10

SIF TALENT:汚臭鈍感
	パイズリ回数++
SIF TALENT:汚臭敏感
	パイズリ回数--
SIF TALENT:潔癖症
	パイズリ回数--
SIF TALENT:献身的
	パイズリ回数 += 2
SIF TALENT:淫乱
	パイズリ回数 += 2
SIF TALENT:娼婦
	パイズリ回数 += 3
SIF TALENT:淫乳
	パイズリ回数 += 3

SIF パイズリ回数 < 1
	パイズリ回数 = 1

;ここから射精回数の計算
;ただしどれだけ多くてもパイズリ回数よりは多く射精しない
射精回数 = 10*パイズリ回数;整数しか扱えない関係で一旦10倍に
SIF !TALENT:汚れ無視
	TIMES 射精回数, 0.90
SIF !TALENT:舌使い
	TIMES 射精回数, 0.80
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
射精回数 *= ABL:技巧
射精回数 /= 30
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
SELECTCASE TALENT:バストサイズ
	CASE 2
		TIMES 射精回数, 0.90
	CASE 4
		TIMES 射精回数, 1.10
	CASE 5
		TIMES 射精回数, 1.30
ENDSELECT
射精回数 /= 10
射精回数 = LIMIT(射精回数, 1, パイズリ回数)
IF TALENT:MASTER:絶倫
	射精回数 = MIN(射精回数, 15)
ELSE
	射精回数 = MIN(射精回数, 10)
ENDIF

EXP:パイズリ経験 += パイズリ回数
EXP:フェラ経験 += パイズリ回数
EXP:精液経験 += 射精回数
JUEL:快Ｂ += パイズリ回数*20
JUEL:恭順 += パイズリ回数*100
JUEL:屈服 += パイズリ回数*30
JUEL:習得 += パイズリ回数*40
PRINTFORMW その後も%CALLNAME:TARGET%のパイズリを楽しみ、今朝は{射精回数}回射精した

PRINTFORML パイズリ経験+{パイズリ回数}
PRINTFORML フェラ経験+{パイズリ回数}
PRINTFORML 精液経験+{射精回数}
PRINTFORML 快Ｂの珠+{パイズリ回数*20}
PRINTFORML 恭順の珠+{パイズリ回数*100}
PRINTFORML 屈服の珠+{パイズリ回数*30}
PRINTFORML 習得の珠+{パイズリ回数*40}
PRINTFORMW 射精経験+{射精回数}（%CALLNAME:MASTER%）

CFLAG:朝パイズリ経験++
CFLAG:MASTER:朝パイズリ経験++
CFLAG:胸で射精させた経験++


@EVENT_朝騎乗位
#DIM DYNAMIC CHK_V;1ならＶ、0ならＡ
#DIM DYNAMIC プレイ回数
#DIM DYNAMIC 射精回数

DRAWLINE
CALL KOJO_MESSAGE_EVENT, "朝騎乗位"

;どこの穴でするか
IF ABL:Ｖ感覚 >= 3 && VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
	CHK_V = 1
ELSE
	CHK_V = 0
ENDIF

;何回するか
プレイ回数 = (RELATION:TARGET:(NO:MASTER))/60
プレイ回数 += ABL:性交中毒
プレイ回数 += ABL:精液中毒/2
プレイ回数 += ABL:奉仕精神/4
SIF TALENT:献身的
	プレイ回数++
SIF TALENT:淫乱
	プレイ回数 += 2
SIF TALENT:娼婦
	プレイ回数 += 3
IF CHK_V
	IF TALENT:淫膣
		プレイ回数 += 5
		プレイ回数 += ABL:Ｖ感覚/10
	ENDIF
ELSE
	IF TALENT:淫尻
		プレイ回数 += 5
		プレイ回数 += ABL:Ａ感覚/10
	ENDIF
ENDIF
SIF プレイ回数 < 1
	プレイ回数 = 1

;何発出したか
射精回数 = 10*プレイ回数;小数を扱うため一旦10倍
SIF !TALENT:MASTER:絶倫
	TIMES 射精回数, 0.60
SIF TALENT:小柄体型
	TIMES 射精回数, 1.20
射精回数 *= ABL:技巧
射精回数 /= 30
射精回数 *= (10 + ABL:MASTER:Ｃ感覚)
射精回数 /= 10
射精回数 /= 10
射精回数 = LIMIT(射精回数, 1, プレイ回数)
IF TALENT:MASTER:絶倫
	射精回数 = MIN(射精回数, 15)
ELSE
	射精回数 = MIN(射精回数, 10)
ENDIF

IF CHK_V
	EXP:Ｖ経験 += プレイ回数
	CFLAG:騎乗位で射精させた経験++
	JUEL:快Ｖ += プレイ回数*100
	IF PENIS(TARGET)
		EXP:射精経験++
		CFLAG:膣を責められて射精した経験++
		CFLAG:犯されて射精した経験++
	ENDIF
	EXP:膣射経験 += 射精回数
	PREG:0 += 射精回数
	CALL IN_VAGINA
ELSE
	EXP:Ａ経験 += プレイ回数
	CFLAG:アナル騎乗位で射精させた経験++
	JUEL:快Ａ += プレイ回数*100
	IF PENIS(TARGET)
		EXP:射精経験++
		CFLAG:アナルを責められて射精した経験++
		CFLAG:アナルを犯されて射精した経験++
	ENDIF
ENDIF
EXP:性交経験 += プレイ回数
EXP:MASTER:性交経験 += プレイ回数
EXP:精液経験 += 射精回数
EXP:MASTER:射精経験 += 射精回数
JUEL:恭順 += プレイ回数*100
JUEL:屈服 += プレイ回数*30
JUEL:習得 += プレイ回数*40

PRINTFORM %CALLNAME:MASTER%のペニスを咥え込んで離さない%CALLNAME:TARGET%は、卑猥な腰使いで
SIF 射精回数 > 1
	PRINTFORM {射精回数}回ほど
PRINTW 精液を搾り取っていった

IF CHK_V
	PRINTFORML Ｖ経験+{プレイ回数}
ELSE
	PRINTFORML Ａ経験+{プレイ回数}
ENDIF
PRINTFORML 性交経験+{プレイ回数}
経験回数:MASTER:(NO:TARGET) += 1
経験回数:TARGET:(NO:MASTER) += 1

PRINTFORML 精液経験+{射精回数}
IF CHK_V
	PRINTFORML 膣射経験+{射精回数}
	PRINTFORML 快Ｖの珠+{プレイ回数*100}
	中出し回数:MASTER:(NO:TARGET) += 射精回数
	中出され回数:(NO:MASTER) += 射精回数
ELSE
	PRINTFORML 快Ａの珠+{プレイ回数*100}
	アナル中出し回数:MASTER:(NO:TARGET) += 射精回数
	アナル中出され回数:(NO:MASTER) += 射精回数
ENDIF
SIF PENIS(TARGET)
	PRINTFORML 射精経験+1
PRINTFORML 恭順の珠+{プレイ回数*100}
PRINTFORML 屈服の珠+{プレイ回数*30}
PRINTFORML 習得の珠+{プレイ回数*40}
PRINTFORMW 射精経験+{射精回数}（%CALLNAME:MASTER%）

CFLAG:朝騎乗位経験++
CFLAG:MASTER:朝騎乗位経験++

;-------------------------------------------------
;調教後に起こるイベント
;-------------------------------------------------
@SELF_CHECK
;調教者がペニス持ちで、調教者の射精残数が0の場合は発生しない
;SIF PENIS(MASTER) && CFLAG:MASTER:残数 == 0
;	RETURN 0

;アニマルだと発生しない
SIF TALENT:アニマル
	RETURN 0

;調教後3P、調教後セックスどちらか一つ（強精神剤の反動が起こる時は無効）
;助手がいる場合は3P判定 対象と助手が陥落済み、対象と助手間の相性がそれぞれ100より↑、対象の体力500以上
IF ASSI >= 0 && CFLAG:陥落 && CFLAG:ASSI:陥落 && RELATION:(NO:ASSI) > 100 && RELATION:ASSI:(NO:TARGET) > 100 && BASE:体力 >= 500 && TEQUIP:強精神薬 <= 2
	;どちらかが貞操帯つけてると発生しない
	SIF CFLAG:貞操帯 || CFLAG:ASSI:貞操帯
		GOTO ELSE
	;ただし出産トラウマがある場合は発生しない時がある
	LOCAL = CFLAG:出産トラウマ+CFLAG:ASSI:出産トラウマ
	;1+(合計値/10)/2+(合計値/10) 
	SIF !(LOCAL && RAND:(LOCAL/10+2) <= LOCAL/10)
		CALL EVENT_CHECK, "調教後3P"
;ふたりエッチ判定 対象が陥落済み、対象の体力500以上
ELSEIF CFLAG:陥落 && BASE:体力 >= 500 && TEQUIP:強精神薬 <= 2
	;貞操帯装着中は発生しない
	SIF CFLAG:貞操帯
		GOTO ELSE
	;出産トラウマがある場合はry 式は上と同じ1
	LOCAL = CFLAG:出産トラウマ
	SIF !(LOCAL && RAND:(LOCAL/10+2) <= LOCAL/10)
		CALL EVENT_CHECK, "調教後セックス"
ENDIF

$ELSE

;強精神薬の反動、調教後搾乳、調教後自慰。どれか一つ
IF TEQUIP:強精神薬 > 2
	CALL EVENT_CHECK, "強精神薬の反動"
ELSEIF ABL:Ｂ感覚 >= 4 && (ABL:噴乳中毒 > ABL:自慰中毒) && TALENT:母乳体質
	CALL EVENT_CHECK, "調教後搾乳"
ELSEIF ABL:自慰中毒 >= 3 && !CFLAG:貞操帯
	CALL EVENT_CHECK, "調教後自慰"
ENDIF

;調教後夜這い
SIF CFLAG:陥落 && TIME == 1
	CALL EVENT_CHECK, "調教後夜這い"

;調教後3P
@EVENT_調教後3P
#DIM DYNAMIC プレイ回数
#DIM DYNAMIC 射精回数基礎値, 3;0,1,2でMASTER,ASSI,TARGET
#DIM DYNAMIC 射精回数, 3, 3;射精回数:(出す人):(出される人)
#DIM DYNAMIC V2

射精回数基礎値:0 = CFLAG:MASTER:残数/5+1
射精回数基礎値:1 = CFLAG:ASSI:残数/5+1
射精回数基礎値:2 = CFLAG:TARGET:残数/5+1

;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後3P"
LOCAL = 1

;対象：主人か助手に同性がいればレズ中毒、ゲイ中毒を見る
IF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	IF SEX(TARGET) == 1
		SELECTCASE ABL:ゲイ中毒
			CASE 1
				LOCAL += 1
			CASE 2
				LOCAL += 2
			CASE 3
				LOCAL += 3
			CASE 4
				LOCAL += 5
			CASE IS >= 5
				LOCAL += 7
		ENDSELECT
	ELSEIF SEX(TARGET) == 2
		SELECTCASE ABL:レズ中毒
			CASE 1
				LOCAL += 1
			CASE 2
				LOCAL += 2
			CASE 3
				LOCAL += 3
			CASE 4
				LOCAL += 5
			CASE IS >= 5
				LOCAL += 7
		ENDSELECT
	ENDIF
;いなければ奉仕精神を見る
ELSE
	SELECTCASE ABL:奉仕精神
		CASE 1 TO 4
			LOCAL += ABL:奉仕精神+1
		CASE IS >= 5
			LOCAL += 6
	ENDSELECT
ENDIF

;助手：奴隷が同性ならレズ中毒、ゲイ中毒を見る
IF SEX(ASSI) == SEX(TARGET)
	IF SEX(TARGET) == 1
		SELECTCASE ABL:ゲイ中毒
			CASE 1
				LOCAL += 1
			CASE 2
				LOCAL += 2
			CASE 3
				LOCAL += 5
			CASE 4
				LOCAL += 8
			CASE IS >= 5
				LOCAL += 13
		ENDSELECT
	ELSEIF SEX(TARGET) == 2
		SELECTCASE ABL:レズ中毒
			CASE 1
				LOCAL += 1
			CASE 2
				LOCAL += 2
			CASE 3
				LOCAL += 5
			CASE 4
				LOCAL += 8
			CASE IS >= 5
				LOCAL += 13
		ENDSELECT
	ENDIF
;同性でなければ技巧を見る
ELSE
	SELECTCASE ABL:技巧
		CASE 1 TO 4
			LOCAL += ABL:技巧+2
		CASE IS >= 5
			LOCAL += 7
	ENDSELECT
ENDIF

;両刀
SIF TALENT:両刀
	LOCAL += 3

;相性処理
LOCAL *= RELATION:(NO:ASSI)
LOCAL /= 100

;主人か助手に同性がいれば同性愛経験増加
IF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			EXP:ゲイ経験 += LOCAL
			PRINTFORML ゲイ経験+{LOCAL}(%CALLNAME:TARGET%)
		CASE 2
			EXP:レズ経験 += LOCAL
			PRINTFORML レズ経験+{LOCAL}(%CALLNAME:TARGET%)
	ENDSELECT
ENDIF

;数値次第で絶頂
SIF LOCAL*100*ABL:従順/2500 > 0
	PRINTFORML 絶頂経験+{LOCAL*100*ABL:従順/2500}(%CALLNAME:TARGET%)
PRINTFORML 快Ｃの珠+{LOCAL*10*ABL:Ｃ感覚}(%CALLNAME:TARGET%)
PRINTFORML 欲情の珠+{LOCAL*50}(%CALLNAME:TARGET%)

EXP:絶頂経験 += LOCAL*100*ABL:従順/2500
JUEL:快Ｃ += LOCAL*10*ABL:Ｃ感覚
JUEL:欲情 += LOCAL*50

;奴隷が男で他に竿役がいれば奴隷掘られる
IF SEX(TARGET) == 1 && (PENIS(MASTER) || PENIS(ASSI))
	SELECTCASE ABL:Ａ感覚
		CASE 0 TO 4
			LOCAL:1 = ABL:Ａ感覚+1
		CASE IS >= 5
			LOCAL:1 = 6
	ENDSELECT
	プレイ回数 = (LOCAL/5+1)*LOCAL:1
	IF PENIS(MASTER)
		EXP:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:MASTER:(NO:TARGET) += プレイ回数
		経験回数:(NO:MASTER) += プレイ回数
		射精回数:0:2 += 射精回数基礎値:0
		アナル中出し回数:MASTER:(NO:TARGET) += 射精回数基礎値:0
		アナル中出され回数:(NO:MASTER) += 射精回数基礎値:0
	ENDIF
	IF PENIS(ASSI)
		EXP:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:ASSI:(NO:TARGET) += プレイ回数
		経験回数:(NO:ASSI) += プレイ回数
		射精回数:1:2 += 射精回数基礎値:1
		アナル中出し回数:ASSI:(NO:TARGET) += 射精回数基礎値:1
		アナル中出され回数:(NO:ASSI) += 射精回数基礎値:1
	ENDIF
	SIF PENIS(MASTER) && PENIS(ASSI)
		プレイ回数 *= 2
		
	PRINTFORML Ａ経験+{プレイ回数}(%CALLNAME:TARGET%)
	PRINTFORML 苦痛快楽経験+{プレイ回数/2+1)*LOCAL:1}(%CALLNAME:TARGET%)
	PRINTFORML 快Ａの珠+{プレイ回数*80}(%CALLNAME:TARGET%)
	EXP:Ａ経験 += プレイ回数
	EXP:苦痛快楽経験 += プレイ回数/2+1
	JUEL:快Ａ += プレイ回数*80
ENDIF

;助手が男で他に竿役がいれば助手掘られる
IF SEX(ASSI) == 1 && (PENIS(MASTER) || PENIS(TARGET))
	SELECTCASE ABL:ASSI:Ａ感覚
		CASE 0 TO 4
			LOCAL:1 = ABL:ASSI:Ａ感覚+1
		CASE IS >= 5
			LOCAL:1 = 6
	ENDSELECT
	プレイ回数 = (LOCAL/5+1)*LOCAL:1
	IF PENIS(MASTER)
		EXP:MASTER:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:MASTER:(NO:ASSI) += プレイ回数
		経験回数:ASSI:(NO:MASTER) += プレイ回数
		射精回数:0:1 += 射精回数基礎値:0
		アナル中出し回数:MASTER:(NO:ASSI) += 射精回数基礎値:0
		アナル中出され回数:ASSI:(NO:MASTER) += 射精回数基礎値:0
	ENDIF
	IF PENIS(TARGET)
		EXP:TARGET:性交経験 += プレイ回数
		EXP:ASSI:性交経験 += プレイ回数
		経験回数:TARGET:(NO:ASSI) += プレイ回数
		経験回数:ASSI:(NO:TARGET) += プレイ回数
		射精回数:2:1 += 射精回数基礎値:2
		アナル中出し回数:(NO:ASSI) += 射精回数基礎値:2
		アナル中出され回数:ASSI:(NO:TARGET) += 射精回数基礎値:2
	ENDIF
	SIF PENIS(MASTER) && PENIS(TARGET)
		プレイ回数 *= 2
		
	PRINTFORML Ａ経験+{プレイ回数}(%CALLNAME:ASSI%)
	PRINTFORML 苦痛快楽経験+{プレイ回数/2+1)*LOCAL:1}(%CALLNAME:ASSI%)
	PRINTFORML 快Ａの珠+{プレイ回数*80}(%CALLNAME:ASSI%)
	EXP:ASSI:Ａ経験 += プレイ回数
	EXP:ASSI:苦痛快楽経験 += プレイ回数/2+1
	JUEL:ASSI:快Ａ += プレイ回数*80
ENDIF

;主人が男で他に竿役がいれば主人掘られる
IF SEX(MASTER) == 1 && (PENIS(ASSI) || PENIS(TARGET))
	SELECTCASE ABL:MASTER:Ａ感覚
		CASE 0 TO 4
			LOCAL:1 = ABL:MASTER:Ａ感覚+1
		CASE IS >= 5
			LOCAL:1 = 6
	ENDSELECT
	プレイ回数 = (LOCAL/5+1)*LOCAL:1
	IF PENIS(TARGET)
		EXP:TARGET:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:TARGET:(NO:MASTER) += プレイ回数
		経験回数:MASTER:(NO:TARGET) += プレイ回数
		射精回数:2:0 += 射精回数基礎値:2
		アナル中出し回数:(NO:MASTER) += 射精回数基礎値:2
		アナル中出され回数:MASTER:(NO:TARGET) += 射精回数基礎値:2
	ENDIF
	IF PENIS(ASSI)
		EXP:ASSI:性交経験 += プレイ回数
		EXP:MASTER:性交経験 += プレイ回数
		経験回数:MASTER:(NO:ASSI) += プレイ回数
		経験回数:ASSI:(NO:MASTER) += プレイ回数
		射精回数:1:0 += 射精回数基礎値:1
		アナル中出し回数:ASSI:(NO:MASTER) += 射精回数基礎値:1
		アナル中出され回数:MASTER:(NO:ASSI) += 射精回数基礎値:1
	ENDIF
	SIF PENIS(MASTER) && PENIS(TARGET)
		プレイ回数 *= 2
	
	PRINTFORML Ａ経験+{プレイ回数}(%CALLNAME:MASTER%)
	PRINTFORML 苦痛快楽経験+{プレイ回数/2+1)*LOCAL:1}(%CALLNAME:MASTER%)
	EXP:ASSI:Ａ経験 += プレイ回数
	EXP:ASSI:苦痛快楽経験 += プレイ回数/2+1
ENDIF

;奴隷に竿があり、主人か助手がヴァギナあれば中出しされる
IF PENIS(TARGET)
	IF VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:MASTER:Ｖ感覚, 8)
		SIF TALENT:MASTER:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:MASTER%)
		PRINTFORML %CALLNAME:MASTER%の膣射経験(%CALLNAME:TARGET%の精液)+{射精回数基礎値:2}
		EXP:MASTER:Ｖ経験 += プレイ回数
		EXP:MASTER:膣射経験 += 射精回数基礎値:2
		経験回数:(NO:MASTER) += プレイ回数
		経験回数:MASTER:(NO:TARGET) += プレイ回数
		中出し回数:(NO:MASTER) += 射精回数基礎値:2
		中出され回数:MASTER:(NO:TARGET) += 射精回数基礎値:2
		射精回数:2:0 += 射精回数基礎値:2
		;既に誰かの精液がある場合はランダムで上書きされる
		IF PREG:MASTER:9 == ASSI || RAND:3 == 0
			PREG:MASTER:4++
			PREG:MASTER:9 = TARGET
		ENDIF
	ENDIF
	IF VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:ASSI:Ｖ感覚, 8)
		SIF TALENT:ASSI:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:ASSI%)
		PRINTFORML %CALLNAME:ASSI%の膣射経験(%CALLNAME:TARGET%の精液)+{射精回数基礎値:2}
		EXP:ASSI:Ｖ経験 += プレイ回数
		EXP:ASSI:膣射経験 += 射精回数基礎値:2
		経験回数:(NO:ASSI) += プレイ回数
		経験回数:ASSI:(NO:TARGET) += プレイ回数
		中出し回数:(NO:ASSI) += 射精回数基礎値:2
		中出され回数:ASSI:(NO:TARGET) += 射精回数基礎値:2
		射精回数:2:0 += 射精回数基礎値:2
		PREG:ASSI:2 += 射精回数基礎値:2
	ENDIF
ENDIF

;助手に竿があり、主人か奴隷がヴァギナあれば中出しされる
IF PENIS(ASSI)
	IF VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
		プレイ回数 = MIN(2+ABL:MASTER:Ｖ感覚, 8)
		SIF TALENT:MASTER:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:MASTER%)
		PRINTFORML %CALLNAME:MASTER%の膣射経験(%CALLNAME:ASSI%の精液)+{射精回数基礎値:1}
		EXP:MASTER:Ｖ経験 += プレイ回数
		EXP:MASTER:膣射経験 += 射精回数基礎値:1
		経験回数:ASSI:(NO:MASTER) += プレイ回数
		経験回数:MASTER:(NO:ASSI) += プレイ回数
		中出し回数:ASSI:(NO:MASTER) += 射精回数基礎値:1
		中出され回数:MASTER:(NO:ASSI) += 射精回数基礎値:1
		射精回数:1:0 += 射精回数基礎値:1
		;既に誰かの精液がある場合はランダムで上書きされる
		IF PREG:MASTER:9 == TARGET || RAND:3 == 0
			PREG:MASTER:4++
			PREG:MASTER:9 = ASSI
		ENDIF
	ENDIF
	IF VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
		プレイ回数 = MIN(2+ABL:Ｖ感覚, 8)
		SIF TALENT:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:TARGET%)
		PRINTFORML %CALLNAME:TARGET%の膣射経験(%CALLNAME:ASSI%の精液)+{射精回数基礎値:1}
		EXP:Ｖ経験 += プレイ回数
		EXP:膣射経験 += 射精回数基礎値:1
		経験回数:ASSI:(NO:TARGET) += プレイ回数
		経験回数:(NO:ASSI) += プレイ回数
		中出し回数:ASSI:(NO:TARGET) += 射精回数基礎値:1
		中出され回数:(NO:ASSI) += 射精回数基礎値:1
		射精回数:1:2 += 射精回数基礎値:1
		PREG:1 += 射精回数基礎値:1
	ENDIF
ENDIF

;主人に竿があり、助手か奴隷がヴァギナあれば中出しされる
IF PENIS(MASTER)
	IF VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女
		プレイ回数 = MIN(2+ABL:ASSI:Ｖ感覚, 8)
		SIF TALENT:ASSI:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:ASSI%)
		PRINTFORML %CALLNAME:ASSI%の膣射経験(%CALLNAME:MASTER%の精液)+{射精回数基礎値:0}
		EXP:ASSI:Ｖ経験 += プレイ回数
		EXP:ASSI:膣射経験 += 射精回数基礎値:0
		経験回数:MASTER:(NO:ASSI) += プレイ回数
		経験回数:ASSI:(NO:MASTER) += プレイ回数
		中出し回数:MASTER:(NO:ASSI) += 射精回数基礎値:0
		中出され回数:ASSI:(NO:MASTER) += 射精回数基礎値:0
		射精回数:0:1 += 射精回数基礎値:0
		PREG:ASSI:0 += 射精回数基礎値:0
	ENDIF
	IF VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
		プレイ回数 = MIN(2+ABL:Ｖ感覚, 8)
		SIF TALENT:淫膣
			プレイ回数 += 2
		PRINTFORML Ｖ経験+{プレイ回数}(%CALLNAME:ASSI%)
		PRINTFORML %CALLNAME:ASSI%の膣射経験(%CALLNAME:MASTER%の精液)+{射精回数基礎値:0}
		EXP:Ｖ経験 += プレイ回数
		EXP:膣射経験 += 射精回数基礎値:0
		経験回数:MASTER:(NO:TARGET) += プレイ回数
		経験回数:(NO:MASTER) += プレイ回数
		中出し回数:MASTER:(NO:TARGET) += 射精回数基礎値:0
		中出され回数:(NO:MASTER) += 射精回数基礎値:0
		射精回数:0:2 += 射精回数基礎値:0
		PREG:0 += 射精回数基礎値:0
	ENDIF
ENDIF

;上記イベントが発生した場合は竿役のバージンが失われる
IF TALENT:童貞
	IF (SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)) && (SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女))
		IF TALENT:性転換済
			IF RAND:2
				VIRGIN:10 = %NAME:MASTER%
			ELSE
				VIRGIN:10 = %NAME:ASSI%
			ENDIF
		ELSE
			IF RAND:2
				VIRGIN:0 = %NAME:MASTER%
			ELSE
				VIRGIN:0 = %NAME:ASSI%
			ENDIF
		ENDIF
		TALENT:童貞 = 0
	ELSEIF SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)
		IF TALENT:性転換済
			VIRGIN:10 = %NAME:MASTER%
		ELSE
			VIRGIN:0 = %NAME:MASTER%
		ENDIF
		TALENT:童貞 = 0
	ELSEIF SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女)
		IF TALENT:性転換済
			VIRGIN:10 = %NAME:ASSI%
		ELSE
			VIRGIN:0 = %NAME:ASSI%
		ENDIF
		TALENT:童貞 = 0
	ENDIF
ENDIF
IF TALENT:MASTER:童貞
	IF (SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)) && (SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女))
		IF TALENT:MASTER:性転換済
			IF RAND:2
				VIRGIN:MASTER:10 = %NAME:TARGET%
			ELSE
				VIRGIN:MASTER:10 = %NAME:ASSI%
			ENDIF
		ELSE
			IF RAND:2
				VIRGIN:MASTER:0 = %NAME:TARGET%
			ELSE
				VIRGIN:MASTER:0 = %NAME:ASSI%
			ENDIF
		ENDIF
		TALENT:MASTER:童貞 = 0
	ELSEIF SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)
		IF TALENT:MASTER:性転換済
			VIRGIN:MASTER:10 = %NAME:TARGET%
		ELSE
			VIRGIN:MASTER:0 = %NAME:TARGET%
		ENDIF
		TALENT:MASTER:童貞 = 0
	ELSEIF SEX(ASSI) == 1 || (VAGINA(ASSI) && !TALENT:ASSI:処女 && !TALENT:ASSI:再生処女)
		IF TALENT:MASTER:性転換済
			VIRGIN:MASTER:10 = %NAME:ASSI%
		ELSE
			VIRGIN:MASTER:0 = %NAME:ASSI%
		ENDIF
		TALENT:MASTER:童貞 = 0
	ENDIF
ENDIF
IF TALENT:ASSI:童貞
	IF (SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)) && (SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女))
		IF TALENT:ASSI:性転換済
			IF RAND:2
				VIRGIN:ASSI:10 = %NAME:MASTER%
			ELSE
				VIRGIN:ASSI:10 = %NAME:TARGET%
			ENDIF
		ELSE
			IF RAND:2
				VIRGIN:ASSI:0 = %NAME:MASTER%
			ELSE
				VIRGIN:ASSI:0 = %NAME:TARGET%
			ENDIF
		ENDIF
		TALENT:ASSI:童貞 = 0
	ELSEIF SEX(MASTER) == 1 || (VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女)
		IF TALENT:ASSI:性転換済
			VIRGIN:ASSI:10 = %NAME:MASTER%
		ELSE
			VIRGIN:ASSI:0 = %NAME:MASTER%
		ENDIF
		TALENT:ASSI:童貞 = 0
	ELSEIF SEX(TARGET) == 1 || (VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女)
		IF TALENT:ASSI:性転換済
			VIRGIN:ASSI:10 = %NAME:TARGET%
		ELSE
			VIRGIN:ASSI:0 = %NAME:TARGET%
		ENDIF
		TALENT:ASSI:童貞 = 0
	ENDIF
ENDIF

IF PENIS(MASTER)
	プレイ回数 = LOCAL
	IF TALENT:バストサイズ >= 4 && TALENT:ASSI:バストサイズ >= 4
		プレイ回数++
		SIF TALENT:バストサイズ == 5 && TALENT:ASSI:バストサイズ == 5
			プレイ回数++
		EXP:パイズリ経験 += プレイ回数
		EXP:ASSI:パイズリ経験 += プレイ回数
		PRINTFORML パイズリ経験+{プレイ回数}(%CALLNAME:TARGET%)
		PRINTFORML パイズリ経験+{プレイ回数}(%CALLNAME:ASSI%)
	ENDIF
	EXP:フェラ経験 += プレイ回数
	EXP:ASSI:フェラ経験 += プレイ回数
	PRINTFORML フェラ経験+{プレイ回数}(%CALLNAME:TARGET%)
	PRINTFORML フェラ経験+{プレイ回数}(%CALLNAME:ASSI%)
	射精回数:0:1 += 射精回数基礎値:0
	射精回数:0:2 += 射精回数基礎値:0
ENDIF

;竿があるキャラは射精経験増加 竿役が居れば竿役以外の精液経験増加
IF PENIS(TARGET)
	PRINTFORML 射精経験+{射精回数:2:0 + 射精回数:2:1}(%CALLNAME:TARGET%)
	EXP:射精経験 += 射精回数:2:0 + 射精回数:2:1
ENDIF
IF PENIS(ASSI)
	PRINTFORML 射精経験+{射精回数:1:0 + 射精回数:1:2}(%CALLNAME:ASSI%)
	EXP:ASSI:射精経験 += 射精回数:1:0 + 射精回数:1:2
ENDIF
IF PENIS(MASTER)
	PRINTFORML 射精経験+{射精回数:0:1 + 射精回数:0:2}(%CALLNAME:MASTER%)
	EXP:MASTER:射精経験 += 射精回数:0:1 + 射精回数:0:2
ENDIF
IF PENIS(TARGET) || PENIS(ASSI) || PENIS(MASTER)
	PRINTFORML 精液経験+{射精回数:0:2 + 射精回数:1:2}(%CALLNAME:TARGET%)
	PRINTFORML 精液経験+{射精回数:0:1 + 射精回数:2:1}(%CALLNAME:ASSI%)
	PRINTFORML 精液経験+{射精回数:1:0 + 射精回数:2:0}(%CALLNAME:MASTER%)
	EXP:精液経験 += 射精回数:0:2 + 射精回数:1:2
	EXP:ASSI:精液経験 += 射精回数:0:1 + 射精回数:2:1
	EXP:MASTER:精液経験 += 射精回数:1:0 + 射精回数:2:0
ENDIF

PRINTFORML 奉仕快楽経験+{LOCAL}(%CALLNAME:TARGET%)
PRINTFORML 欲情の珠+{LOCAL*100}(%CALLNAME:TARGET%)
PRINTFORML 屈服の珠+{LOCAL*10*(ABL:技巧+ABL:奉仕精神)/2}(%CALLNAME:TARGET%)
PRINTFORML 習得の珠+{LOCAL*10*(ABL:技巧+ABL:奉仕精神)/2}(%CALLNAME:TARGET%)
EXP:奉仕快楽経験 += LOCAL
JUEL:欲情 += LOCAL*100
JUEL:屈服 += LOCAL*10*(ABL:技巧+ABL:奉仕精神)/2
JUEL:習得 += LOCAL*10*(ABL:技巧+ABL:奉仕精神)/2

CFLAG:調教後3P経験++
CFLAG:ASSI:調教後3P経験++
CFLAG:MASTER:調教後3P経験++

SIF (CHARANAMEF(TARGET) == "野増菜 かなで" && CHARANAMEF(ASSI) == "粒焼 りむる") || (CHARANAMEF(TARGET) == "粒焼 りむる" && CHARANAMEF(ASSI) == "野増菜 かなで")
	CALL 実績解除, "帝国軍ご奉仕部隊"

WAIT
DRAWLINE

;調教後セックス
@EVENT_調教後セックス
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後セックス"

LOCAL = 3

;非処女ならV感覚
IF VAGINA(TARGET) && !TALENT:処女 && !TALENT:再生処女
	SELECTCASE ABL:Ｖ感覚
		CASE 1 TO 4
			LOCAL += ABL:Ｖ感覚
		CASE IS >= 5
			LOCAL += 5
	ENDSELECT
ENDIF

;A感覚
IF SEX(TARGET) == 1
	SELECTCASE ABL:Ａ感覚
		CASE 1 TO 4
			LOCAL += ABL:Ａ感覚+2
		CASE IS >= 5
			LOCAL += 7
	ENDSELECT
ELSEIF SEX(TARGET) == 2
	SELECTCASE ABL:Ａ感覚
		CASE 1 TO 4
			LOCAL += ABL:Ａ感覚
		CASE IS >= 5
			LOCAL += 5
	ENDSELECT
ENDIF

;欲望と奉仕精神によるボーナス
IF ABL:欲望 >= 5 && ABL:奉仕精神 >= 5 && PALAM:恭順 >= PALAMLV:5
	LOCAL += 3
ELSEIF ABL:欲望 >= 4 && ABL:奉仕精神 >= 4 && PALAM:恭順 >= PALAMLV:4
	LOCAL += 2
ELSEIF ABL:欲望 >= 3 && ABL:奉仕精神 >= 3 && PALAM:恭順 >= PALAMLV:3
	LOCAL += 1
ENDIF

;淫膣
SIF TALENT:淫膣
	LOCAL += 3

;主人の射精制限
;SIF LOCAL > CFLAG:MASTER:残数
;	LOCAL = CFLAG:MASTER:残数

;場合によってはオーラル、手淫に
IF (SEX(TARGET) == 1 && SEX(MASTER) == 1 && ABL:Ａ感覚 <= 3) || (SEX(TARGET) == 2 && (TALENT:妊娠 || TALENT:処女 || TALENT:再生処女))
	IF SEX(TARGET) == 1 && SEX(MASTER) == 1 && ABL:Ａ感覚 <= 3
		PRINTFORML フェラ経験+{LOCAL}
		PRINTFORML 奉仕快楽経験+{LOCAL}
		PRINTFORML ゲイ経験+{LOCAL/2+1}
		PRINTFORML 射精経験+{CFLAG:残数/5+1}
		PRINTFORML フェラ経験+{LOCAL}(%CALLNAME:MASTER%)
		PRINTFORML 射精経験+{CFLAG:MASTER:残数/5+1}(%CALLNAME:MASTER%)
		PRINTFORML 習得の珠+{LOCAL*150}
		EXP:フェラ経験 += LOCAL
		EXP:奉仕快楽経験 += LOCAL
		EXP:ゲイ経験 += LOCAL/2+1
		EXP:射精経験 += CFLAG:残数/5+1
		EXP:PLAYER:フェラ経験 += LOCAL
		EXP:MASTER:射精経験 += CFLAG:MASTER:残数/5+1
		JUEL:習得 += LOCAL*150
		CFLAG:調教後奉仕経験++
		CFLAG:MASTER:調教後奉仕経験++
	ELSEIF SEX(TARGET) == 2 && (TALENT:妊娠 || TALENT:処女 || TALENT:再生処女)
		SIF PENIS(MASTER)
			PRINTFORML フェラ経験+{LOCAL}
		PRINTFORML 奉仕快楽経験+{LOCAL}
		SIF SEX(MASTER) == 2
			PRINTFORML レズ経験+{LOCAL/2+1}
		PRINTFORML 絶頂経験+{LOCAL/3}
		SIF PENIS(TARGET)
			PRINTFORML 射精経験+{CFLAG:残数/5+1}
		SIF PENIS(TARGET)
			PRINTFORML フェラ経験+{LOCAL}(%CALLNAME:MASTER%)
		SIF PENIS(MASTER)
			PRINTFORML 射精経験+{CFLAG:MASTER:残数/5+1}(%CALLNAME:MASTER%)
		PRINTFORML 習得の珠+{LOCAL*150}
		SIF PENIS(MASTER)
			EXP:フェラ経験 += LOCAL
		EXP:奉仕快楽経験 += LOCAL
		SIF SEX(MASTER) == 2
			EXP:レズ経験 += LOCAL/2+1
		EXP:絶頂経験 += LOCAL/3
		SIF PENIS(TARGET)
			EXP:射精経験 += CFLAG:残数/5+1
		SIF PENIS(TARGET)
			EXP:MASTER:フェラ経験 += LOCAL
		SIF PENIS(MASTER)
			EXP:MASTER:射精経験 += CFLAG:MASTER:残数/5+1
		JUEL:習得 += LOCAL*150
		CFLAG:調教後奉仕経験++
		CFLAG:MASTER:調教後奉仕経験++
	ENDIF
ELSEIF SEX(TARGET) == 2
	IF PENIS(MASTER) && !TALENT:処女 && !TALENT:再生処女
		PRINTFORML Ｖ経験+{LOCAL}
		PRINTFORML 快Ｖの珠+{LOCAL*400}
		PRINTFORML 膣射経験+{CFLAG:MASTER:残数/2+1}
		PRINTFORML 精液経験+{CFLAG:MASTER:残数}
		PRINTFORML 性交経験+{LOCAL}
		経験回数:MASTER:(NO:TARGET) += LOCAL
		経験回数:(NO:MASTER) += LOCAL
		中出し回数:MASTER:(NO:TARGET) += CFLAG:MASTER:残数/2+1
		中出され回数:(NO:MASTER) += CFLAG:MASTER:残数/2+1
		EXP:性交経験 += LOCAL
		EXP:MASTER:性交経験 += LOCAL
		EXP:Ｖ経験 += LOCAL
		JUEL:快Ｖ += LOCAL*400
		EXP:膣射経験 += CFLAG:MASTER:残数/2+1
		PREG:0 += CFLAG:MASTER:残数/2+1
		EXP:精液経験 += CFLAG:MASTER:残数/2+1
		IF TALENT:MASTER:童貞
			IF TALENT:MASTER:性転換済
				VIRGIN:MASTER:10 = %NAME:TARGET%
			ELSE
				VIRGIN:MASTER:0 = %NAME:TARGET%
			ENDIF
			TALENT:童貞 = 0
		ENDIF
	ENDIF
	IF PENIS(TARGET)
		PRINTFORML 射精経験+{CFLAG:残数/2+1}
		EXP:射精経験 += CFLAG:残数/2+1
		IF VAGINA(MASTER) && !TALENT:MASTER:処女 && !TALENT:MASTER:再生処女
			PRINTFORML 膣射経験+{CFLAG:残数/2+1}(%CALLNAME:MASTER%)
			EXP:MASTER:膣射経験 += CFLAG:残数/2+1
			中出し回数:(NO:MASTER) += CFLAG:残数/2+1
			中出され回数:MASTER:(NO:TARGET) += CFLAG:残数/2+1
			PREG:4 += CFLAG:残数/2+1
			PREG:9 = TARGET
		ELSEIF SEX(MASTER) == 1
			PRINTFORML Ａ経験+{LOCAL/2+1}(%CALLNAME:MASTER%)
			中出し回数:(NO:MASTER) += CFLAG:残数/2+1
			中出され回数:MASTER:(NO:TARGET) += CFLAG:残数/2+1
			EXP:MASTER:Ａ経験 += LOCAL/2+1
		ENDIF
		IF TALENT:童貞
			IF TALENT:性転換済
				VIRGIN:10 = %NAME:MASTER%
			ELSE
				VIRGIN:0 = %NAME:MASTER%
			ENDIF
			TALENT:童貞 = 0
		ENDIF
	ENDIF 
	IF SEX(MASTER) == 2
		PRINTFORML レズ経験+{LOCAL/2+1}
		EXP:レズ経験 += LOCAL/2+1
	ENDIF
	PRINTFORML 恭順の珠+{LOCAL*200}
	PRINTFORML 欲情の珠+{LOCAL*400}
	JUEL:恭順 += LOCAL*200
	JUEL:欲情 += LOCAL*400
	CFLAG:調教後セックス経験++
	CFLAG:MASTER:調教後セックス経験++
ELSEIF SEX(TARGET) == 1
	IF PENIS(MASTER)
		PRINTFORML Ａ経験+{LOCAL}
		PRINTFORML 快Ａの珠+{LOCAL*500}
		PRINTFORML 精液経験+{CFLAG:MASTER:残数}
		アナル中出し回数:MASTER:(NO:TARGET) += CFLAG:MASTER:残数
		アナル中出され回数:(NO:MASTER) += CFLAG:MASTER:残数
		EXP:Ａ経験 += LOCAL
		JUEL:快Ａ += LOCAL*500
		EXP:精液経験 += CFLAG:MASTER:残数
		IF TALENT:MASTER:童貞
			IF TALENT:MASTER:性転換済
				VIRGIN:MASTER:10 = %NAME:TARGET%
			ELSE
				VIRGIN:MASTER:0 = %NAME:TARGET%
			ENDIF
			TALENT:童貞 = 0
		ENDIF
	ENDIF
	IF SEX(MASTER) == 2
		PRINTFORML 奉仕快楽経験+{LOCAL}
		PRINTFORML 膣射経験+{CFLAG:TARGET:残数/2+1}(%CALLNAME:MASTER%)
		PRINTFORML Ｖ経験+{LOCAL}(%CALLNAME:MASTER%)
		中出し回数:(NO:MASTER) += CFLAG:残数/2+1
		中出され回数:MASTER:(NO:TARGET) += CFLAG:残数/2+1
		EXP:奉仕快楽経験 += LOCAL
		EXP:MASTER:膣射経験 += CFLAG:TARGET:残数/2+1
		PREG:4 += CFLAG:TARGET:残数/2+1
		PREG:9 = TARGET
	ENDIF
	IF SEX(MASTER) == 1
		PRINTFORML 射精経験+{CFLAG:残数/2+1}
		PRINTFORML Ａ経験+{LOCAL/2+1}(%CALLNAME:MASTER%)
		PRINTFORML ゲイ経験+{LOCAL/2+1}
		アナル中出し回数:(NO:MASTER) += CFLAG:残数/2+1
		アナル中出され回数:MASTER:(NO:TARGET) += CFLAG:残数/2+1
		EXP:射精経験 += CFLAG:残数/2+1
		EXP:MASTER:Ａ経験 += LOCAL/2+1
		EXP:ゲイ経験 += LOCAL/2+1
	ENDIF

	PRINTFORML 性交経験+{LOCAL}
	PRINTFORML 恭順の珠+{LOCAL*200}
	PRINTFORML 欲情の珠+{LOCAL*400}
	EXP:性交経験 += LOCAL
	EXP:MASTER:性交経験 += LOCAL
	経験回数:MASTER:(NO:TARGET) += LOCAL
	経験回数:(NO:MASTER) += LOCAL
	JUEL:恭順 += LOCAL*200
	JUEL:欲情 += LOCAL*400
	CFLAG:調教後セックス経験++
	CFLAG:MASTER:調教後セックス経験++
	IF TALENT:童貞
		IF TALENT:性転換済
			VIRGIN:10 = %NAME:MASTER%
		ELSE
			VIRGIN:0 = %NAME:MASTER%
		ENDIF
		TALENT:童貞 = 0
	ENDIF
ENDIF

WAIT

;強精神薬の反動
@EVENT_強精神薬の反動
;イベント口上呼び出し
DRAWLINE

;強精神薬による絶頂チェック
LOCAL = 0
IF TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント > 0
	CALL KOJO_MESSAGE_EVENT, "強精神薬の反動"
	;強精神薬の効果が残ったまま調教を終了した場合の処理
	;通常時の効果が再現できそうにないので同時多重絶頂したという処理に変更
	;LOCALは多重絶頂の種類を示す(1の場合は○絶頂、2の場合は○＋△絶頂、4の場合は四重絶頂となっている)
	SIF TFLAG:Ｃ絶頂カウント
		LOCAL++
	SIF TFLAG:Ｖ絶頂カウント
		LOCAL++
	SIF TFLAG:Ａ絶頂カウント
		LOCAL++
	SIF TFLAG:Ｂ絶頂カウント
		LOCAL++

	PRINTFORML 絶頂経験+{TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント}
	SIF TFLAG:Ｃ絶頂カウント > 0
		PRINTFORML 快Ｃの珠+{LOCAL*TFLAG:Ｃ絶頂カウント*1000}
	SIF TFLAG:Ｖ絶頂カウント > 0
		PRINTFORML 快Ｖの珠+{LOCAL*TFLAG:Ｖ絶頂カウント*1000}
	SIF TFLAG:Ａ絶頂カウント > 0
		PRINTFORMW 快Ａの珠+{LOCAL*TFLAG:Ａ絶頂カウント*1000}
	SIF TFLAG:Ｂ絶頂カウント > 0
		PRINTFORML 快Ｂの珠+{LOCAL*TFLAG:Ｂ絶頂カウント*1000}
	PRINTL 
	EXP:絶頂経験 += TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント
	JUEL:快Ｃ += LOCAL*TFLAG:Ｃ絶頂カウント*1000
	JUEL:快Ｖ += LOCAL*TFLAG:Ｖ絶頂カウント*1000
	JUEL:快Ａ += LOCAL*TFLAG:Ａ絶頂カウント*1000
	JUEL:快Ｂ += LOCAL*TFLAG:Ｂ絶頂カウント*1000

	;絶頂強度合計が10以上で四重絶頂だとその経験が加算される
	IF TFLAG:Ｃ絶頂カウント+TFLAG:Ｖ絶頂カウント+TFLAG:Ａ絶頂カウント+TFLAG:Ｂ絶頂カウント >= 10 && LOCAL == 4
		IF !CFLAG:調教後強精神薬反動経験強
			PRINTL 異常経験+1
			EXP:異常経験++
		ENDIF
		CFLAG:調教後強精神薬反動経験強++
	ENDIF

	;リセット
	TFLAG:Ｃ絶頂カウント = 0
	TFLAG:Ｖ絶頂カウント = 0
	TFLAG:Ａ絶頂カウント = 0
	TFLAG:Ｂ絶頂カウント = 0
	TEQUIP:強精神薬 = 0
	CFLAG:調教後強精神薬反動経験++

	;副作用で体力が500に
	SIF BASE:体力 > 500
		BASE:体力 = 500
	WAIT
ENDIF

;調教後搾乳
@EVENT_調教後搾乳
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後搾乳"

;噴乳中毒による搾乳チェック
;男はだめ。母乳体質、噴乳中毒、一定能力、体力が必要
LOCAL = 1

SELECTCASE ABL:噴乳中毒
	CASE 1
		LOCAL += 1
	CASE 2
		LOCAL += 2
	CASE 3
		LOCAL += 3
	CASE 4
		LOCAL += 5
	CASE IS >= 5
		LOCAL += 7
ENDSELECT

;欲望と奉仕精神によるボーナス
IF ABL:欲望 >= 5 && ABL:奉仕精神 >= 5 && PALAM:恭順 >= PALAMLV:5
	LOCAL += 3
ELSEIF ABL:欲望 >= 4 && ABL:奉仕精神 >= 4 && PALAM:恭順 >= PALAMLV:4
	LOCAL += 2
ELSEIF ABL:欲望 >= 3 && ABL:奉仕精神 >= 3 && PALAM:恭順 >= PALAMLV:3
	LOCAL += 1
ENDIF

;自慰しやすい
SIF TALENT:自慰しやすい
	LOCAL += 2
;淫乳
SIF TALENT:淫乳
	LOCAL += 3

PRINTFORML 噴乳経験+{LOCAL}
PRINTFORML 快Ｂの珠+{LOCAL*300}
PRINTFORML 欲情の珠+{LOCAL*50}
PRINTFORML 習得の珠+{LOCAL*20}
EXP:噴乳経験 += LOCAL
JUEL:快Ｂ += LOCAL*300
JUEL:欲情 += LOCAL*50
JUEL:習得 += LOCAL*20
IF ABL:従順+ABL:露出癖+ABL:マゾっ気 >= 10
	PRINTFORML 恥情の珠+{LOCAL*150}
	JUEL:恥情 += LOCAL*150
ENDIF
CFLAG:調教後搾乳経験++
WAIT

;調教後自慰
@EVENT_調教後自慰
;イベント口上呼び出し
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後自慰"

;自慰中毒による自慰チェック
LOCAL = 1
;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE IS <= 3
		LOCAL += 3
	CASE 4
		LOCAL += 4
	CASE 5
		LOCAL += 5
	CASE 6
		LOCAL += 7
	CASE IS >= 7
		LOCAL += 9
ENDSELECT

;欲望とＣ感覚によるボーナス
IF ABL:欲望 >= 5 && ABL:Ｃ感覚 >= 5 && PALAM:恭順 >= PALAMLV:5
	LOCAL += 3
ELSEIF ABL:欲望 >= 4 && ABL:Ｃ感覚 >= 4 && PALAM:恭順 >= PALAMLV:4
	LOCAL += 2
ELSEIF ABL:欲望 >= 3 && ABL:Ｃ感覚 >= 3 && PALAM:恭順 >= PALAMLV:3
	LOCAL += 1
ENDIF

;自慰しやすい
SIF TALENT:自慰しやすい
	LOCAL += 2
;淫核
SIF TALENT:淫核
	LOCAL += 3

;助手が鼓舞
IF ASSI >= 0
	SIF TALENT:ASSI:鼓舞
		LOCAL += 1
ENDIF

PRINTFORML 自慰経験+{LOCAL}
IF PENIS(TARGET)
	PRINTFORML 射精経験+{LOCAL/3+1}
	EXP:射精経験 += LOCAL/3+1
ENDIF
PRINTFORML 快Ｃの珠+{LOCAL*500}
PRINTFORML 恭順の珠+{LOCAL*100}
PRINTFORML 欲情の珠+{LOCAL*250}
EXP:自慰経験 += LOCAL
JUEL:快Ｃ += LOCAL*500
JUEL:恭順 += LOCAL*100
JUEL:欲情 += LOCAL*250
IF ABL:自慰中毒+ABL:Ｃ感覚+ABL:欲望 >= 10 && TIME == 0
	PRINTFORML 恥情の珠+{LOCAL*200}
	JUEL:恥情 += LOCAL*200
ENDIF
CFLAG:調教後自慰経験++
WAIT

;調教後夜這い
@EVENT_調教後夜這い
DRAWLINE
CALL KOJO_MESSAGE_EVENT, "調教後夜這い"

;対象が男or対象が処女or主人竿無しor残数0or体力500未満で添い寝イベント
;添い寝イベントなので体力が大きく回復する
IF SEX(TARGET) == 1 || TALENT:処女 || TALENT:再生処女 || !PENIS(MASTER) || !CFLAG:MASTER:残数 || BASE:体力 < 500 || CFLAG:貞操帯
	PRINTFORMW %CALLNAME:TARGET%の体力が大きく回復しました
	BASE:体力 += EXP:愛情経験+200
	SIF BASE:体力 > MAXBASE:体力
		BASE:体力 = MAXBASE:体力
	CFLAG:調教後添い寝経験++
	CFLAG:MASTER:調教後添い寝経験++
	;実績;添い寝してくるキュベリアと一夜を過ごす
	SIF CHARANAMEF(TARGET) == "キュベリア"
		CALL 実績解除, "天空の夜明け"
ELSEIF SEX(TARGET) == 2
	LOCAL = 1
	;V感覚
	SELECTCASE ABL:Ｖ感覚
		CASE 1, 2
			LOCAL += 1
		CASE 3, 4
			LOCAL += 2
		CASE IS >= 5
			LOCAL += 3
	ENDSELECT

	;淫膣い
	SIF TALENT:淫膣
		LOCAL += 3

	;主人の射精制限
	SIF LOCAL > CFLAG:MASTER:残数
		LOCAL = CFLAG:MASTER:残数

	PRINTFORML Ｖ経験+{LOCAL}
	PRINTFORML 性交経験+{LOCAL}
	PRINTFORML 膣射経験+{LOCAL/2}
	PRINTFORML 精液経験+{LOCAL/2}
	PRINTFORML 快Ｖの珠+{LOCAL*600}
	PRINTFORML 恭順の珠+{LOCAL*400}
	PRINTFORML 欲情の珠+{LOCAL*400}
	経験回数:MASTER:(NO:TARGET) += LOCAL
	経験回数:(NO:MASTER) += LOCAL
	中出し回数:MASTER:(NO:TARGET) += LOCAL/2
	中出され回数:(NO:MASTER) += LOCAL/2
	EXP:Ｖ経験 += LOCAL
	EXP:性交経験 += LOCAL
	EXP:膣射経験 += LOCAL/2
	PREG:0 += LOCAL/2
	EXP:精液経験 += LOCAL/2
	JUEL:快Ｖ += LOCAL*600
	JUEL:恭順 += LOCAL*400
	JUEL:欲情 += LOCAL*400
	CFLAG:MASTER:残数 -= LOCAL
	CFLAG:調教後夜這い経験++
	CFLAG:MASTER:調教後夜這い経験++
	WAIT
ENDIF

TFLAG:調教後夜這い = 1

@EVENT_アニマル
DRAWLINE
;イベント口上呼び出し
CALL KOJO_MESSAGE_EVENT, "アニマル"

;効果は単純 恭順と愛情経験
PRINTL 愛情経験+3
PRINTL 恭順の珠+500
EXP:愛情経験 += 3
JUEL:恭順 += 500
WAIT

