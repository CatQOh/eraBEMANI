;追加処理及びサブイベント
;-------------------------------------------------
;ターン終了時
;-------------------------------------------------
@EVENTTURNEND
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 人数
#DIM 着床告知;DYNAMICじゃない変数 プレーヤーへの告知用なので起動ごとに破棄してもよい
#DIM DYNAMIC DEUIL
VARSET LOCAL, 0

IF TARGET >= 0
	;売却可能&助手可能判定 陥落系で無視できるって条件はほとんど意味無いか
	;従順+欲望が6以上
	SIF ABL:従順+ABL:欲望 >= 6
		LOCAL = 1

	;[反抗的][気丈]なら従順LV4以上必要 服従系陥落素質があれば無視できる
	IF (TALENT:反抗的 || TALENT:気丈) && !TALENT:服従 && !TALENT:隷属
		SIF ABL:従順 < 4
			LOCAL = 0
	ENDIF

	;[自制心][無関心]なら欲望LV4以上必要 淫乱系陥落素質があれば無視できる
	IF (TALENT:自制心 || TALENT:無関心) && !TALENT:淫乱 && !TALENT:娼婦
		SIF ABL:欲望 < 4
			LOCAL = 0
	ENDIF

	;[プライド高い][生意気]なら奉仕精神LV3以上必要
	IF TALENT:プライド高い || TALENT:生意気
		SIF ABL:奉仕精神 < 3
			LOCAL = 0
	ENDIF

	;段位モード専用の特殊条件
	IF ゲームモード() == "段位認定"
		;段位認定裏七級の場合、陥落していないと売却できない
		IF 受験段位名 == "裏七級"
			SIF !CFLAG:陥落
				LOCAL = 0
		;「炎の孕ませポップン学園」の場合、陥落させたうえで着床を認知していないと売却できない
		ELSEIF 受験段位名 == "炎の孕ませポップン学園"
			LOCAL = 0
			IF CFLAG:陥落
				IF TALENT:妊娠
					LOCAL = 1
				ELSEIF (TALENT:恋慕 || TALENT:親愛) && PREG:TARGET:11
					LOCAL = 1
					;この場合一度だけ着床したことを教えてもらえる(再起動でフラグが消失するのは仕様)
					;変数「着床告知」にビットで管理
					;1:ミニッツ 2:ルート 4:翠里 8:鈴花 16:氷海
					SELECTCASE CALLNAME
						CASE "ミニッツ"
							IF !(着床告知 & 1)
								着床告知 |= 1
								PRINTFORML ミニッツは%CALLNAME:MASTER%に妊娠検査薬の結果をこっそりと見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%はミニッツの幼い体に子種を植え付けたようだ……
							ENDIF
						CASE "ルート"
							IF !(着床告知 & 2)
								着床告知 |= 2
								PRINTFORML ルートは%CALLNAME:MASTER%に妊娠の診断結果を教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%はルートとの交尾によって受精させたようだ……
							ENDIF
						CASE "翠里"
							IF !(着床告知 & 4)
								着床告知 |= 4
								PRINTFORML 翠里は%CALLNAME:MASTER%に妊娠した可能性が高いことを教えてくれた
								PRINTFORMW どうやら%CALLNAME:MASTER%は翠里の自称超絶名器の奥に遺伝子を残せたようだ……
							ENDIF
						CASE "鈴花"
							IF !(着床告知 & 8)
								着床告知 |= 8
								PRINTFORML 鈴花は%CALLNAME:MASTER%に妊娠検査薬の結果を嬉しそうに見せてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は鈴花のめしべとの交配に成功したようだ……
							ENDIF
						CASE "氷海"
							IF !(着床告知 & 16)
								着床告知 |= 16
								PRINTFORML 氷海は%CALLNAME:MASTER%に妊娠の診断結果が陽性であることを伝えてきた
								PRINTFORMW どうやら%CALLNAME:MASTER%は氷海への膣内射精で着床させたようだ……
							ENDIF
					ENDSELECT
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	IF LOCAL
		;上記をクリアし、技巧3以上or奉仕精神3以上orマゾっ気3以上orCVAB感覚合計8以上or従順5以上or欲望5以上なら売却可能
		{
		IF ABL:技巧 >= 3 || ABL:奉仕精神 >= 3 || ABL:マゾっ気 >= 3 || 
		   ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 8 || ABL:従順 >= 5 || ABL:欲望 >= 5
		}
			IF CFLAG:状態 <= 0 && ゲームモード() != "新婚モード"
				PRINTFORMW %NAME:TARGET%が売却可能になりました
				CFLAG:状態 = 1
				CALL 経験値取得, TARGET, "売却可能"
			ENDIF
			;さらに従順+欲望+技巧が14以上なら助手可能
			IF ABL:従順+ABL:欲望+ABL:技巧 >= 14
				IF CFLAG:状態 <= 1
					PRINTFORMW %NAME:TARGET%が助手可能になりました
					CFLAG:状態 = 2
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

CALL CHECKDRAWLINE

IF ASSI >= 0
	IF TALENT:ASSI:妖狐 && ITEM:媚薬 == 0 && !CFLAG:MASTER:使用不可
		PRINTFORMW [妖狐]の%CALLNAME:ASSI%が媚薬を調達してきたようです
		ITEM:媚薬++
	ENDIF
ENDIF

CALL CHECKDRAWLINE

FOR LCOUNT, 0, CHARANUM
	;休憩フラグを消すが、配属中のキャラは下記収入関数でフラグ管理してるので対象外
	SIF CSTR:LCOUNT:配属 == ""
		CFLAG:LCOUNT:休憩 = 0
NEXT

IF TIME == 1
	LASTMONEY = MONEY
	;主人が行動不可だと収入入らない
	SIF !CFLAG:MASTER:使用不可
		CALL STRATEGY_INCOME
ENDIF
;主人が行動不可だと発生しない
SIF !CFLAG:MASTER:使用不可
	CALL TURNEND_IDOL

;畜産農業関係
CALL TURNEND_畜産農業
;家畜の体調次第で食材が変わるので献立を確認する
CALL 献立条件確認

SIF ITEM:実験室
	CALL TURNEND_実験室

;施設の計算と利用人数表示
IF !CFLAG:MASTER:使用不可 && ゲームモード() != "新婚モード"
	SIF ITEM:図書室
		CALL TURNEND_図書室
	SIF ITEM:医務室
		CALL TURNEND_医務室
	CALL CHECKDRAWLINEW
ENDIF

;新婚モード以外で所持金が5000万以上なら裏カジノへの招待状が届く
IF ゲームモード() != "新婚モード" && MONEY >= TONUM("50000万") && !FLAG:裏カジノ
	CALL KOJO_MESSAGE_EVENT, "裏カジノ招待"
	PRINTW 裏カジノが使用可能になりました
	FLAG:裏カジノ = 1
ENDIF

;主人が使用不可だと体力・気力・ストレス値は変動しない
IF !CFLAG:MASTER:使用不可
	FOR LCOUNT, 0, CHARANUM
		;ミッドナイトスペシャル
		IF CFLAG:LCOUNT:ミッドナイトスペシャル > 0
			SIF --CFLAG:LCOUNT:ミッドナイトスペシャル == 0
				PRINTFORMW %CALLNAME:LCOUNT%のミッドナイトスペシャルの効果が切れました
		ENDIF
		;夜なら貞操帯装着日数増加
		SIF CFLAG:LCOUNT:貞操帯 && TIME == 1
			CFLAG:LCOUNT:貞操帯++
		;以下主人は除外
		SIF LCOUNT == MASTER
			CONTINUE
		;休憩フラグが立っていて配属中でない場合はキャラ固有の休憩フラグが立つ
		;配属先でいろいろあって休憩してる場合はそちらで休憩フラグが立っている
		IF FLAG:休憩フラグ
			SIF CSTR:LCOUNT:配属 == ""
				CFLAG:LCOUNT:休憩 = 1
		ENDIF

		;種族妖精ならランダムで消費アイテム取得 休憩中のみ
		IF CSTR:LCOUNT:種族 == "妖精" && CFLAG:LCOUNT:陥落 && CFLAG:LCOUNT:休憩
			LOCAL = RAND(100, 108)
			LOCAL:1 = RAND(1, 4)
			PRINTFORMW %CALLNAME:LCOUNT%が{LOCAL:1}個の%ITEMNAME:LOCAL%を拾ってきました
			ITEM:LOCAL += LOCAL:1
		ENDIF

		;ストレス値の計算
		CALL ストレス値計算, LCOUNT

		;魔剤フラグ消去
		CFLAG:LCOUNT:魔剤 = 0
		;魔剤耐性減少
		IF TALENT:LCOUNT:小人体型
			CFLAG:LCOUNT:魔剤耐性 -= 5
		ELSEIF TALENT:LCOUNT:小柄体型
			CFLAG:LCOUNT:魔剤耐性 -= 6
		ELSE
			CFLAG:LCOUNT:魔剤耐性 -= 7
		ENDIF
		;魔剤耐性は1000～2000の間で変動する
		SIF CFLAG:LCOUNT:魔剤耐性 < 1000
			CFLAG:LCOUNT:魔剤耐性 = 1000

		;気力の計算→体力の計算
		CALL 気力計算, LCOUNT
		CALL 体力計算, LCOUNT

		;医務室利用判定をリセット
		CFLAG:LCOUNT:医務室利用 = 0

		;依存度は-1000～5000
		CFLAG:LCOUNT:依存度 = LIMIT(CFLAG:LCOUNT:依存度, -1000, 5000)

		;回復量低下処理
		SIF CFLAG:LCOUNT:回復量低下 > 0
			CFLAG:LCOUNT:回復量低下--

		;狂気度を下げる
		SIF CFLAG:LCOUNT:狂気度 > 0
			CFLAG:LCOUNT:狂気度--

		CFLAG:LCOUNT:二日酔い = 0
		;酔が抜ける 必ずしも1ターンで全快するわけではない
		SELECTCASE TIME
			;昼→夜
			CASE 0
				IF TALENT:LCOUNT:酒豪
					CFLAG:LCOUNT:酔 -= 20
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
				ELSEIF TALENT:LCOUNT:下戸
					CFLAG:LCOUNT:酔 -= 5
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/5
				ELSE
					CFLAG:LCOUNT:酔 -= 10
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
				ENDIF
			;夜→昼
			CASE 1
				IF TALENT:LCOUNT:酒豪
					CFLAG:LCOUNT:酔 -= 50
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/2
				ELSEIF TALENT:LCOUNT:下戸
					CFLAG:LCOUNT:酔 -= 10
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/4
				ELSE
					CFLAG:LCOUNT:酔 -= 30
					CFLAG:LCOUNT:酔 -= TEQUIP:LCOUNT:ウォッカ/3
				ENDIF
				;これで酔いが残っていると二日酔いを引き起こす
				IF CFLAG:LCOUNT:酔 > 0
					CFLAG:LCOUNT:二日酔い = 1
					CFLAG:LCOUNT:酔 = 0
				ENDIF
		ENDSELECT
		TEQUIP:LCOUNT:ウォッカ = 0
		SIF CFLAG:LCOUNT:酔 < 0
			CFLAG:LCOUNT:酔 = 0

		;使用不可フラグ
		IF CFLAG:LCOUNT:使用不可 != 0
			SIF LCOUNT == TARGET
				TARGET = -1
			SIF LCOUNT == ASSI
				ASSI = -1
			CALL 配属解除, LCOUNT
			CFLAG:LCOUNT:使用不可--
			IF CFLAG:LCOUNT:使用不可 <= 0
				SELECTCASE CFLAG:LCOUNT:使用不可状態
					CASE 0
						PRINTFORMW %CALLNAME:LCOUNT%が再び調教可能になりました
					CASE 1
						PRINTFORMW %CALLNAME:LCOUNT%は体外受精を終えたため、再び調教可能になりました
					CASE 2
						PRINTFORMW %CALLNAME:LCOUNT%は3日間の研修を受け、帰宅しました
						CALL ABLマイナス打ち消し, LCOUNT
				ENDSELECT
				CFLAG:LCOUNT:使用不可 = 0
			ENDIF
		ENDIF
	NEXT
ENDIF
SIF TFLAG:料理
	CALL KOJO_MESSAGE_EVENT, "料理"
SIF TFLAG:お菓子
	CALL KOJO_MESSAGE_EVENT, "お菓子"
TFLAG:料理 = 0
TFLAG:お菓子 = 0

;調理室の処理は回復後に行う
CALL TURNEND_調理室

CALL EVENTCHECK_ABL
FLAG:休憩フラグ = 0

;午後なら次の日、午前なら午後にする
SELECTCASE TIME
	CASE 0
		PRINTW 夜になった……
		TIME = 1
	CASE 1
		;日付変更時のイベント呼び出し
		CALL EVENT_NEXTDAY
		;色仕掛けイベント
		SIF !CFLAG:MASTER:使用不可
			CALL EVENT_HONEYTRAP
		;主人が使用不可の場合、期限的な日付加算は行わない
		IF !CFLAG:MASTER:使用不可
			DAY++
			DAY:1 = DAY
		;もし主人が妊娠で使用不可だった場合のために、妊娠フラグを進めるためのDAY:1はカウントする
		ELSE
			DAY:1++
		ENDIF
		PRINTW 一日が終わった……
		;実績：調教対象が居ない状態で夜を過ごす
		SIF TARGET == -1 && !CFLAG:MASTER:使用不可
			CALL 実績解除, "Without you tonight"
		TIME = 0
		;ゲームモード7(新婚モード)か9(PREMIUM FREE)なら10日に一度、借金の返済・新規借入ができる
		IF GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード") && DAY%10 == 0
			CALL DEBT
		;借金がある場合は10日に一度返済を要求される
		ELSEIF 借金
			CALL DEBT, 1
		ENDIF
		;バッドエンドの判定
		;期限切れ
		SIF (DAY > 100 && ゲームモード() == "通常モード") || (DAY > 300 && ゲームモード() == "経営モード")
			JUMP BADENDING, 1
		;金欠エンド
		;ただしゲームモード7(新婚モード)と9(PREMIUM FREE)では呼び出されない
		IF MONEY < 0 && !GROUPMATCH(ゲームモード(), "新婚モード", "フリーモード")
			;実績:STRATEGYモードで金欠エンドを迎える
			CALL 実績解除, "無能な経営者"
			JUMP BADENDING, 2
		ENDIF
		;朝になった時のイベント呼び出し
		CALL EVENT_NEWDAY
		CALL EVENT_NEWDAY_SELF
ENDSELECT

;実績「タルタでラジェ・ガレト・ヴァロ全員の童貞を奪う」
;ビットで処理(ラジェが1、ガレトが2、ヴァロが4)
;7になったら実績解除
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:童貞 == "タルタ" || CSTR:LCOUNT:性転換童貞 == "タルタ"
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ラジェ"
				タルタ童貞喰い |= 1
			CASE "ガレト"
				タルタ童貞喰い |= 2
			CASE "ヴァロ"
				タルタ童貞喰い |= 4
		ENDSELECT
	ENDIF
NEXT
SIF タルタ童貞喰い & 7
	CALL 実績解除, "I LOVE SAKURA"

;実績：陥落したイロドリミドリの全員と同棲する
;実績：ユーリ、アッシュ、スマイルの三人を陥落させる
LOCAL = 0
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:陥落
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる"
				LOCAL++
			CASE "ユーリ", "アッシュ", "スマイル"
				DEUIL++
		ENDSELECT
		SIF LOCAL == 5
			CALL 実績解除, "ヨリドリミドリ"
		SIF DEUIL == 3
			CALL 実績解除, "Deuilの熱烈すぎる追っかけ"
	ENDIF
NEXT

;実績:育江の出産から7年を経過させる
SIF この子の七つのお祝いに && (YEAR*10000+MONTH*100+DAYS) - この子の七つのお祝いに >= 70000
	CALL 実績解除, "この子の七つのお祝いに"

;実績:6928日過ごす
SIF DAY >= 6928
	CALL 実績解除, "INFINITE PLAYER"

;気温の算出
CALL TEMPERATURE
;特定のキャラがいる場合、気温に補正を加える(ただし昼のみ)
;主人・奴隷・助手の場合の方が影響が強い
IF TIME == 0
	FOR LCOUNT, 0, CHARANUM
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "フローラ"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 60
				ELSE
					FLAG:気温 += 6
				ENDIF
			CASE "烈"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 30
				ELSE
					FLAG:気温 += 3
				ENDIF
			CASE "氷海"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 30
				ELSE
					FLAG:気温 -= 3
				ENDIF
			CASE "茜"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 += 50
				ELSE
					FLAG:気温 += 5
				ENDIF
			CASE "スノーフェアリー"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 60
				ELSE
					FLAG:気温 -= 6
				ENDIF
			CASE "氷雪ちゃん"
				IF GROUPMATCH(LCOUNT, MASTER, TARGET, ASSI)
					FLAG:気温 -= 150
				ELSE
					FLAG:気温 -= 15
				ENDIF
		ENDSELECT
	NEXT
	;魔法の影響
	FLAG:気温 -= フリージングアトモスフィア*30
	FLAG:気温 += バーニンザフロア*30
ENDIF
;天気の決定
CALL WEATHER

FOR LCOUNT, 0, CHARANUM
	;フローラ専用イベント
	IF CHARANAMEF(LCOUNT) == "フローラ"
		LOCAL = MAXBASE:LCOUNT:体力
		MAXBASE:LCOUNT:体力 = CSVBASE(NO:LCOUNT, 0) + (FLAG:気温-180)*2
		SIF TALENT:LCOUNT:妊娠
			MAXBASE:LCOUNT:体力 -= 500
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-LOCAL
		SIF BASE:LCOUNT:体力 > MAXBASE:LCOUNT:体力
			BASE:LCOUNT:体力 = MAXBASE:LCOUNT:体力
	ENDIF
	;鳳凰専用イベント
	IF CSTR:LCOUNT:種族 == "鳳凰" && FLAG:気温 > 0
		LOCAL = FLAG:気温
		SELECTCASE 天気()
			CASE "晴れ"
			CASE "曇り"
				TIMES LOCAL , 0.50
			CASEELSE
				TIMES LOCAL , 0.20
		ENDSELECT
		;気温と天気で計算したボーナス分体力気力が増加
		LOCAL:1 = MAXBASE:LCOUNT:体力
		LOCAL:2 = MAXBASE:LCOUNT:気力
		MAXBASE:LCOUNT:体力 = MAXBASE:LCOUNT:鳳凰基礎体力+LOCAL
		MAXBASE:LCOUNT:気力 = MAXBASE:LCOUNT:鳳凰基礎気力+LOCAL
		BASE:LCOUNT:体力 += MAXBASE:LCOUNT:体力-LOCAL:1
		BASE:LCOUNT:気力 += MAXBASE:LCOUNT:気力-LOCAL:2
		SIF BASE:LCOUNT:体力 > MAXBASE:LCOUNT:体力
			BASE:LCOUNT:体力 = MAXBASE:LCOUNT:体力
		SIF BASE:LCOUNT:気力 > MAXBASE:LCOUNT:気力
			BASE:LCOUNT:気力 = MAXBASE:LCOUNT:気力
	ENDIF
NEXT

;主人が使用不可の場合はTARGETとASSIを外してSHOP画面表示し、収入と支出の一切を中断して時間だけ経過させる。DAYは増えないので期限には影響無し
IF CFLAG:MASTER:使用不可 > 0
	TARGET = -1
	ASSI = -1

	;曜日計算
	FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

	;祝日・記念日の設定
	CALL HOLIDAY

	CALL SHOP_UI

	IF --CFLAG:MASTER:使用不可 == 0
		PRINTFORMW %CALLNAME:MASTER%が復帰しました。次のターンから再開できます
	ELSE
		PRINTFORMW %CALLNAME:MASTER%の都合により行動を終了します
	ENDIF
	TARGET = -1
	ASSI = -1
	FLAG:休憩フラグ = 1
	RESTART
ENDIF

;魔法の効果終了は最後
IF フリージングアトモスフィア > 0
	SIF --フリージングアトモスフィア == 0
		PRINTW [フリージングアトモスフィア]の効果が切れました
ENDIF
IF バーニンザフロア > 0
	SIF --バーニンザフロア == 0
		PRINTW [バーニンザフロア]の効果が切れました
ENDIF
IF ストームバスター > 0
	SIF --ストームバスター == 0
		PRINTW [ストームバスター]の効果が切れました
ENDIF
IF スウィートレイン > 0
	SIF --スウィートレイン == 0
		PRINTW [スウィートレイン]の効果が切れました
ENDIF
IF スノープリズム > 0
	SIF --スノープリズム == 0
		PRINTW [スノープリズム]の効果が切れました
ENDIF
IF リジェネレーション > 0
	SIF --リジェネレーション == 0
		PRINTW [リ：ジェネレーション]の効果が切れました
ENDIF
IF ヒプノティッククライシス > 0
	SIF --ヒプノティッククライシス == 0
		PRINTW [ヒプノティッククライシス]の効果が切れました
ENDIF
IF エナジードライブ > 0
	IF --エナジードライブ == 0
		PRINTFORMW %CALLNAME:MASTER%にかけられた[エナジードライブ]の効果が切れました
		FOR LCOUNT, 0, VARSIZE("ABL")
			SIF ABLNAME:LCOUNT != ""
				ABL:MASTER:LCOUNT -= エナジードライブ:1
		NEXT
		VARSET エナジードライブ
	ENDIF
ENDIF
FOR LCOUNT, 0, CHARANUM
	IF ビーストモード:LCOUNT:0 > 0
		IF --ビーストモード:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ビーストモード]の効果が切れました
			SIF ビーストモード:LCOUNT:1
				TALENT:LCOUNT:動物耳 = 0
			SIF ビーストモード:LCOUNT:2
				TALENT:LCOUNT:尻尾 = 0
			SIF ビーストモード:LCOUNT:3
				TALENT:LCOUNT:ファーリー = 0
			CSTR:LCOUNT:種族 = %ビーストモードS:LCOUNT:0%
			VARSET ビーストモード:LCOUNT:0
			VARSET ビーストモードS:LCOUNT:0
		ENDIF
	ENDIF
	IF ジェントルストレス:LCOUNT:0 > 0
		IF --ジェントルストレス:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられたジェントルストレスの効果が切れました
			ジェントルストレス:LCOUNT:1 = 0
		ENDIF
	ENDIF
	IF ティルナノーグ:LCOUNT:0 > 0
		IF --ティルナノーグ:LCOUNT:0 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ティル・ナ・ノーグ]の効果が切れました
			TALENT:LCOUNT:小人体型 = 1
		ENDIF
	ENDIF
	IF CFLAG:LCOUNT:憑依中
		IF --CFLAG:LCOUNT:憑依中 == 0
			PRINTFORMW %CALLNAME:LCOUNT%にかけられた[ポゼッション]の効果が切れました
			FOR LCOUNT2, 0, 30
				SIF ABLNAME:LCOUNT2 == "魔法技能"
					CONTINUE
				ABL:LCOUNT:LCOUNT2 = ポゼッション:LCOUNT:LCOUNT2
			NEXT
			VARSET ポゼッション:LCOUNT:0
		ENDIF
	ENDIF
NEXT

BEGIN SHOP

;-------------------------------------------------
;日付が変わった時のイベント
;-------------------------------------------------
@EVENT_NEXTDAY
;調教対象・助手が豹変しているとき、元に戻る
;現在豹変し得るのは、紅刃・冷音
IF TARGET >= 0 && TRANCE:200
	SELECTCASE CHARANAMEF(TARGET)
		CASE "紅刃", "春雨 冷音"
			CALL TRANCE_OFF, TARGET
			TRANCE:200 = 0
			PRINTFORMW ＜%CALLNAME:TARGET%は元の性格にもどった＞
	ENDSELECT
ENDIF
IF ASSI >= 0 && TRANCE:ASSI:200
	SELECTCASE CHARANAMEF(ASSI)
		CASE "紅刃", "春雨 冷音"
			CALL TRANCE_OFF, ASSI
			TRANCE:ASSI:200 = 0
			PRINTFORMW ＜%CALLNAME:ASSI%は元の性格にもどった＞
	ENDSELECT
ENDIF

;奴隷の幼児退行イベント
IF TARGET >= 0
	;反発刻印3があり、かつ幼稚である時に
	IF TALENT:幼稚 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が7以上になる
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && EXP:異常経験 >= 7
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	;反発刻印3があり、かつ幼稚がない時
	ELSEIF TALENT:幼稚 == 0 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が9以上に加え露出癖5とおもらし癖がある
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && ABL:露出癖 >= 5 && EXP:異常経験 >= 9 && TALENT:おもらし癖 == 1
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	ENDIF
ENDIF

;段位認定におけるゲージ処理
SIF ゲームモード() == "段位認定" && !CFLAG:MASTER:使用不可
	CALL 段位ゲージ処理

;素質でのイベント
;REPEAT CHARANUM
;	IF TALENT:COUNT:娼婦
;		IF RAND:5 == 0
;			LOCAL = RAND:(CHARANUM-1)
;			SIF TALENT:COUNT:処女 || TALENT:LOCAL:再生処女
;				CONTINUE
;			SIF LOCAL == 0
;				CONTINUE
;			IF TALENT:LOCAL:淫乱 || TALENT:LOCAL:娼婦
;				PRINTFORML %CALLNAME:COUNT%と%CALLNAME:LOCAL%は互いの肉欲を満たすように何度も体を重ねたらしい……
;				IF SEX:LOCAL:0 == 2 && TALENT:LOCAL:処女 == 0 && TALENT:LOCAL:再生処女 == 0
;					PRINTFORML %CALLNAME:LOCAL%のＶ経験+3
;					EXP:LOCAL:Ｖ経験 += 3
;				ENDIF
;				IF SEX:COUNT:0 == 1 || TALENT:COUNT:ふたなり
;					PRINTFORML %CALLNAME:LOCAL%の精液経験+2
;					EXP:LOCAL:精液経験 += 2
;					IF TALENT:LOCAL:処女 == 0 && TALENT:LOCAL:再生処女 == 0
;						PRINTFORML %CALLNAME:LOCAL%の性交経験
;				ENDIF
;				IF SEX:LOCAL:0 == 1
;					PRINTFORML %CALLNAME:LOCAL%のＡ経験+3
;					EXP:LOCAL:Ａ経験 += 3
;				ENDIF
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:LOCAL%の射精経験+2
;					EXP:COUNT:射精経験 += 2
;				ENDIF
;
;				IF SEX:COUNT:0 == 2
;					PRINTFORML %CALLNAME:COUNT%のＶ経験+3
;					EXP:COUNT:Ｖ経験 += 3
;				ENDIF
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:COUNT%の精液経験+2
;					EXP:COUNT:精液経験 += 2
;				ENDIF
;				IF SEX:COUNT:0 == 1
;					PRINTFORML %CALLNAME:COUNT%のＡ経験+3
;					EXP:COUNT:Ａ経験 += 3
;				ENDIF
;				IF SEX:COUNT:0 == 1 || TALENT:COUNT:ふたなり
;					PRINTFORML %CALLNAME:COUNT%の射精経験+2
;					EXP:COUNT:射精経験 += 2
;				ENDIF
;
;			ELSEIF SEX:COUNT:0 == 1
;				IF TALENT:LOCAL:処女 || TALENT:LOCAL:再生処女 || SEX:LOCAL:0 == 1
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%のアナルを無理やり犯したらしい……
;					
;				ELSE
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%と半ば強引にセックスをしたらしい……
;				ENDIF
;			ELSEIF SEX:COUNT:0 == 2
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%を捕まえて精液を搾り取ったらしい……
;				ELSEIF SEX:LOCAL:0 == 2
;					PRINTFORML %CALLNAME:COUNT%の誘いで%CALLNAME:LOCAL%はレズプレイに興じたらしい……
;				ENDIF
;			ENDIF
;			SIF
;			EXP:
;			WAIT
;		ELSEIF RAND:5 == 1
;			PRINTFORML %CALLNAME:COUNT%は特製の媚薬を作り上げたようだ……
;			ITEM:媚薬 += 3
;	ENDIF
;REND
;WAIT
;	
;				
;				
;		PRINTFORM %CALLNAME:COUNT%は
;		IF TALENT:LOCAL:処女 || TALENT:LOCAL:再生処女

;-------------------------------------------------
;朝になった時のイベント
;-------------------------------------------------
@EVENT_NEWDAY
#DIM DYNAMIC LCOUNT
VARSET LOCAL, 0

;日付加算関数
CALL NEXTDAY

;祝日・記念日の処理はSHOPで行うようにしました

;定休日の場合は休みにする
;営業フラグが-1の場合はコマンドでの休業中なのでオープンしない
IF GC定休日() || FLAG:ゲーセン営業中 == -1
	SIF FLAG:ゲーセン営業中 != -1
		FLAG:ゲーセン営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "ゲームセンター"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF FLAG:ゲーセン営業中 != -1
	FLAG:ゲーセン営業中 = DAY-1
ENDIF

IF BR定休日() || FLAG:娼館営業中 == -1
	SIF FLAG:娼館営業中 != -1
		FLAG:娼館営業中 = 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "娼館"
			CFLAG:LCOUNT:休憩 = 1
	NEXT
ELSEIF FLAG:娼館営業中 != -1
	FLAG:娼館営業中 = DAY-1
ENDIF

;実績:新年を迎える
SIF MONTH == 1 && DAYS == 1
	CALL 実績解除, "あけおめアゲイ～ン"

;放尿経験100以上でおもらし癖がつく
;[幼稚]なら放尿経験30以上でつく
;アイテムで治した場合は発生しない
IF TARGET >= 0
	IF !TALENT:おもらし癖 && !CFLAG:おもらし改善フラグ
		IF (TALENT:幼稚 && EXP:放尿経験 >= 30) || (!TALENT:幼稚 && EXP:放尿経験 >= 100)
			CALL CHECKDRAWLINE
			CALL KOJO_MESSAGE_EVENT, "おもらし癖"
			TALENT:おもらし癖 = 1
		ENDIF
	ENDIF
ENDIF

;調合知識習得イベント
IF ASSI > 0 && !TALENT:MASTER:調合知識
	;助手が調合知識持ちで陥落済み
	IF TALENT:ASSI:調合知識 && CFLAG:ASSI:陥落
		;技巧LV3以上 奉仕精神LV3以上
		IF ABL:ASSI:技巧 >= 3 && ABL:ASSI:奉仕精神 >= 3
			;主人の技巧LV4以上
			IF ABL:MASTER:技巧 >= 4
				CALL CHECKDRAWLINE
				CALL KOJO_MESSAGE_EVENT, "調合知識伝授", ASSI
				TALENT:MASTER:調合知識 = 1
				CFLAG:ASSI:主人に調合知識を伝授した経験++
			ENDIF
		ENDIF
	ENDIF
ENDIF

;全体の妊娠発覚時のイベント
CALL GET_CHILD

;排卵誘発剤の効果終了と、不妊治療薬のコスト処理
FOR LCOUNT, 0, CHARANUM
	IF CFLAG:LCOUNT:排卵誘発剤 > 0
		SIF --CFLAG:LCOUNT:排卵誘発剤 <= 0
			PRINTFORMW %CALLNAME:LCOUNT%の排卵誘発剤の効果が切れたようだ
	ENDIF
	IF CFLAG:LCOUNT:生殖力強化 == 1
		LOCAL = 300
		SIF TALENT:LCOUNT:小柄体型
			LOCAL += 200
		CALL PASELIW, @"%CALLNAME:LCOUNT%の不妊治療薬に{LOCAL}＄の費用が発生しました"
		CFLAG:LCOUNT:ストレス値 += 5
	ENDIF
NEXT

CALL PREG_READY

;懸賞が届く 現状は必ず当選する
IF FLAG:ナンプレ懸賞
	IF --FLAG:ナンプレ懸賞 <= 0
		PRINTL 応募していた懸賞の景品が届いた
		PRINTW 「ミッドナイトスペシャル」を手に入れた
		ITEM:ミッドナイトスペシャル++
	ENDIF
ENDIF

;毎月1日に仕送りが届く
IF DAYS == 1 && FLAG:仕送り
	CALL PASELIW, @"%CALLNAME:MASTER%の子供たちから{FLAG:仕送り*30000}＄の仕送りを受け取りました"
	MONEY += FLAG:仕送り*30000
ENDIF

@TRANCE_ON, ARG
;変身フラグにTRANCEを使用。変身での素質変更を行なう。
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		REPEAT 200
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:COUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 1
					ENDIF
			ENDSELECT
		REND
	CASE "春雨 冷音"
		;雨が降ると気力にボーナス
		;無条件に700増えるほかに、Y=1000/XのグラフでのX=1～X=[雨量]までの面積
		;つまり1000LOG([雨量])だけ増加する
		;ただし、整数への切り捨ての関係で100LOG([雨量]^10)としている
		;雨量に対する気力の参考値
		;雨量1:1700 雨量2:2300 雨量3:2700 雨量4:3000 雨量5:3300 雨量10:4000 雨量15:4400 雨量20:4600 雨量78以上:6000
		MAXBASE:ARG:気力 += 700
		MAXBASE:ARG:気力 += 100*LOG(POWER(LIMIT(FLAG:降水量, 1, 78), 10))
		BASE:ARG:気力 = MAXBASE:ARG:気力
		REPEAT 200
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:COUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 1
					ENDIF
			ENDSELECT
		REND
ENDSELECT

@TRANCE_OFF, ARG
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		REPEAT 200
			;変身解除時、習得処理を行なう
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:COUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 1
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 0
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
		REND
	CASE "春雨 冷音"
		MAXBASE:ARG:気力 = CSVBASE(NO:ARG, 1)
		REPEAT 200
			;変身解除時、習得処理を行なう
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:COUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 1
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 0
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
		REND
ENDSELECT

;-------------------------------------------------
;女主人による色仕掛けイベント
;-------------------------------------------------
@EVENT_HONEYTRAP
#DIM DYNAMIC LCOUNT

LOCAL = 0
;調教後夜這いが発生してたら発生しない
IF TFLAG:調教後夜這い
	TFLAG:調教後夜這い = 0
	RETURN 0
ENDIF

SIF !FLAG:色仕掛け
	RETURN 0
SIF TARGET < 0
	RETURN 0
SIF TALENT:MASTER:性別 != 2
	RETURN 0

FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "生意気", "無関心", "貞操観念", "抑圧", "抵抗", "冷静", "強気"
			LOCAL -= 1
		CASE "素直", "衝動的", "弱味", "弱気", "献身的", "妄信"
			LOCAL += 1
		CASE "反抗的", "ツンデレ", "自制心", "一線越えない", "快感の否定"
			LOCAL -= 2
		CASE "好奇心", "貞操無頓着", "解放", "快感に素直", "女好き"
			LOCAL += 2
		CASE "潔癖症", "汚臭敏感"
			SIF TALENT:MASTER:ファーリー
				LOCAL -= 2
		CASE "同族嫌悪"
			SIF CSTR:種族 == CSTR:MASTER:種族
				LOCAL -= 2
	ENDSELECT
NEXT

IF TALENT:性別 == 2
	IF TALENT:両刀
		LOCAL += 1
	ELSE
		LOCAL -= 2
	ENDIF
ENDIF
IF TALENT:恋心 >= 0
	IF TALENT:恋心 != MASTER && !CFLAG:陥落
		LOCAL -= 3
	ELSEIF TALENT:恋心 == MASTER
		LOCAL += 3
	ENDIF
ENDIF
IF TALENT:既婚者 >= 0
	IF TALENT:既婚者 != MASTER && !CFLAG:陥落
		LOCAL -= 5
	ELSEIF TALENT:既婚者 == MASTER
		LOCAL += 5
	ENDIF
ENDIF

SIF TALENT:MASTER:魅力
	LOCAL += 1
SIF TALENT:MASTER:魅惑
	LOCAL += 1
SIF TALENT:MASTER:謎の魅力
	LOCAL += 2
SIF TALENT:MASTER:小柄体型
	LOCAL -= 1
SIF ヒプノティッククライシス
	LOCAL += RAND(15, 21)

LOCAL += ABL:従順
LOCAL += ABL:欲望
LOCAL += ABL:奉仕精神
LOCAL -= MARK:反発刻印*2
LOCAL -= MARK:苦痛刻印

SIF CFLAG:陥落
	LOCAL += 20

;地の文分岐に使う
TFLAG:誘惑ポイント = LOCAL

CALL CHECKDRAWLINE
DO
	PRINTFORML 就寝間際、寝付こうとしている%CALLNAME:TARGET%の姿を目撃した
	PRINTL [0] 夜這いをかける
	PRINTL [1] 誘惑する
	PRINTL [2] 今日はやめとく
	PRINTL [3] そんなつもりは無い(イベント非表示 オプションから再度表示出来ます)
	INPUT
	SELECTCASE RESULT
		CASE 2
			RETURN 0
		CASE 3
			FLAG:色仕掛け = 0
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1)

;夜這い
SELECTCASE RESULT
	CASE 0
		IF TFLAG:誘惑ポイント >= 12
			TFLAG:色仕掛け = 1
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					CSTR:性転換童貞 = %NAME:MASTER%
				ELSE
					CSTR:童貞 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					CSTR:MASTER:性転換処女 = %NAME:TARGET%
				ELSE
					CSTR:MASTER:処女 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				中出し回数:(NO:MASTER)++
				中出され回数:MASTER:(NO:TARGET)++
				CALL EXPUP, "射精経験", 2
				CALL EXPUP, "絶頂経験", 2
				CALL EXPUP, "Ｖ経験", 1, MASTER
				CALL EXPUP, "膣射経験", 1, MASTER
			ELSE
				CALL EXPUP, "絶頂経験", 3
			ENDIF
			IF PENIS(MASTER)
				CALL EXPUP, "精液経験", 1
				CALL EXPUP, "射精経験", 1, MASTER
				CALL EXPUP, "絶頂経験", 1, MASTER
			ENDIF
			IF TALENT:性別 == 2
				CALL EXPUP, "レズ経験", 3
				CALL EXPUP, "レズ経験", 3, MASTER
			ENDIF
			IF PENIS(TARGET)
				CALL EXPUP, "性交経験", 1
				CALL EXPUP, "性交経験", 1, MASTER
				経験回数:MASTER:(NO:TARGET)++
				経験回数:(NO:MASTER)++
			ENDIF
			CALL EXPUP, "主人調教経験", 2
			CFLAG:依存度 += TFLAG:誘惑ポイント
			PRINTL 依存度増加
			CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*50
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*50
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*70
			CALL JUELUP, "屈服", TFLAG:誘惑ポイント*100
		ELSE
			TFLAG:色仕掛け = 2
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
				CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
				CALL JUELUP, "恐怖", TFLAG:誘惑ポイント*30
			ENDIF
		ENDIF
	CASE 1
		IF TFLAG:誘惑ポイント >= 16
			TFLAG:色仕掛け = 3
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					CSTR:性転換童貞 = %NAME:MASTER%
				ELSE
					CSTR:童貞 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					CSTR:MASTER:性転換処女 = %NAME:TARGET%
				ELSE
					CSTR:MASTER:処女 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				中出し回数:(NO:MASTER) += 2
				中出され回数:MASTER:(NO:TARGET) += 2
				CALL EXPUP, "射精経験", 2
				CALL EXPUP, "絶頂経験", 2
				CALL EXPUP, "Ｖ経験", 2, MASTER
				CALL EXPUP, "膣射経験", 2, MASTER
			ELSE
				CALL EXPUP, "絶頂経験", 3
			ENDIF
			IF PENIS(MASTER)
				CALL EXPUP, "精液経験", 2
				CALL EXPUP, "射精経験", 2, MASTER
				CALL EXPUP, "絶頂経験", 2, MASTER
			ENDIF
			IF TALENT:性別 == 2
				CALL EXPUP, "レズ経験", 5
				CALL EXPUP, "レズ経験", 5, MASTER
			ENDIF
			IF PENIS(TARGET)
				CALL EXPUP, "性交経験", 2
				CALL EXPUP, "性交経験", 2, MASTER
				経験回数:MASTER:(NO:TARGET) += 2
				経験回数:(NO:MASTER) += 2
			ENDIF
			CALL EXPUP, "奉仕快楽経験", 3
			CFLAG:依存度 += TFLAG:誘惑ポイント*3/2
			PRINTL 依存度増加
			CALL JUELUP, "快Ｃ", TFLAG:誘惑ポイント*70
			CALL JUELUP, "恭順", TFLAG:誘惑ポイント*100
			CALL JUELUP, "欲情", TFLAG:誘惑ポイント*150
			CALL JUELUP, "屈服", TFLAG:誘惑ポイント*50
			CALL JUELUP, "習得", TFLAG:誘惑ポイント*70
		ELSE
			TFLAG:色仕掛け = 4
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				CALL JUELUP, "恭順", TFLAG:誘惑ポイント*10
				CALL JUELUP, "欲情", TFLAG:誘惑ポイント*10
				CALL JUELUP, "恥情", TFLAG:誘惑ポイント*30
			ENDIF
		ENDIF
ENDSELECT

RETURN 0

