;追加処理及びサブイベント
;-------------------------------------------------
;日付が変わった時のイベント
;-------------------------------------------------
@EVENT_NEXTDAY
;奴隷の幼児退行イベント
IF TARGET >= 0
	;反発刻印3があり、かつ幼稚である時に
	IF TALENT:幼稚 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が7以上になる
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && EXP:異常経験 >= 7
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	;反発刻印3があり、かつ幼稚がない時
	ELSEIF TALENT:幼稚 == 0 && MARK:反発刻印 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が9以上に加え露出癖5とおもらし癖がある
		IF ABL:欲望 >= 5 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && ABL:露出癖 >= 5 && EXP:異常経験 >= 9 && TALENT:おもらし癖 == 1
			CALL KOJO_MESSAGE_EVENT, "幼児退行"
			TALENT:幼稚 = 0
			TALENT:幼児／幼児退行 = 1
			MARK:反発刻印 = 0
		ENDIF
	ENDIF
ENDIF

;段位認定におけるゲージ処理
SIF FLAG:ゲームモード == 8 && !CFLAG:MASTER:使用不可
	CALL 段位ゲージ処理

;実績:育江の出産から7年を経過させる
SIF この子の七つのお祝いに && (YEAR*10000+MONTH*100+DAYS) - この子の七つのお祝いに >= 70000
	CALL 実績解除, "この子の七つのお祝いに"

;実績:6928日過ごす
SIF DAY >= 6928
	CALL 実績解除, "INFINITE PLAYER"

;素質でのイベント
;REPEAT CHARANUM
;	IF TALENT:COUNT:娼婦
;		IF RAND:5 == 0
;			LOCAL = RAND:(CHARANUM-1)
;			SIF TALENT:COUNT:処女 || TALENT:LOCAL:再生処女
;				CONTINUE
;			SIF LOCAL == 0
;				CONTINUE
;			IF TALENT:LOCAL:淫乱 || TALENT:LOCAL:娼婦
;				PRINTFORML %CALLNAME:COUNT%と%CALLNAME:LOCAL%は互いの肉欲を満たすように何度も体を重ねたらしい……
;				IF SEX:LOCAL:0 == 2 && TALENT:LOCAL:処女 == 0 && TALENT:LOCAL:再生処女 == 0
;					PRINTFORML %CALLNAME:LOCAL%のＶ経験+3
;					EXP:LOCAL:Ｖ経験 += 3
;				ENDIF
;				IF SEX:COUNT:0 == 1 || TALENT:COUNT:ふたなり
;					PRINTFORML %CALLNAME:LOCAL%の精液経験+2
;					EXP:LOCAL:精液経験 += 2
;					IF TALENT:LOCAL:処女 == 0 && TALENT:LOCAL:再生処女 == 0
;						PRINTFORML %CALLNAME:LOCAL%の性交経験
;				ENDIF
;				IF SEX:LOCAL:0 == 1
;					PRINTFORML %CALLNAME:LOCAL%のＡ経験+3
;					EXP:LOCAL:Ａ経験 += 3
;				ENDIF
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:LOCAL%の射精経験+2
;					EXP:COUNT:射精経験 += 2
;				ENDIF
;
;				IF SEX:COUNT:0 == 2
;					PRINTFORML %CALLNAME:COUNT%のＶ経験+3
;					EXP:COUNT:Ｖ経験 += 3
;				ENDIF
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:COUNT%の精液経験+2
;					EXP:COUNT:精液経験 += 2
;				ENDIF
;				IF SEX:COUNT:0 == 1
;					PRINTFORML %CALLNAME:COUNT%のＡ経験+3
;					EXP:COUNT:Ａ経験 += 3
;				ENDIF
;				IF SEX:COUNT:0 == 1 || TALENT:COUNT:ふたなり
;					PRINTFORML %CALLNAME:COUNT%の射精経験+2
;					EXP:COUNT:射精経験 += 2
;				ENDIF
;
;			ELSEIF SEX:COUNT:0 == 1
;				IF TALENT:LOCAL:処女 || TALENT:LOCAL:再生処女 || SEX:LOCAL:0 == 1
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%のアナルを無理やり犯したらしい……
;					
;				ELSE
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%と半ば強引にセックスをしたらしい……
;				ENDIF
;			ELSEIF SEX:COUNT:0 == 2
;				IF SEX:LOCAL:0 == 1 || TALENT:LOCAL:ふたなり
;					PRINTFORML %CALLNAME:COUNT%は%CALLNAME:LOCAL%を捕まえて精液を搾り取ったらしい……
;				ELSEIF SEX:LOCAL:0 == 2
;					PRINTFORML %CALLNAME:COUNT%の誘いで%CALLNAME:LOCAL%はレズプレイに興じたらしい……
;				ENDIF
;			ENDIF
;			SIF
;			EXP:
;			WAIT
;		ELSEIF RAND:5 == 1
;			PRINTFORML %CALLNAME:COUNT%は特製の媚薬を作り上げたようだ……
;			ITEM:媚薬 += 3
;	ENDIF
;REND
;WAIT
;	
;				
;				
;		PRINTFORM %CALLNAME:COUNT%は
;		IF TALENT:LOCAL:処女 || TALENT:LOCAL:再生処女
			
;-------------------------------------------------
;ターン終了時
;-------------------------------------------------
@EVENTTURNEND
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 人数
VARSET LOCAL, 0

IF TARGET >= 0
	;未陥落なら反発刻印に応じてストレス値増加
	;未陥落じゃなくても上がるように
	;SIF !CFLAG:陥落
		CFLAG:ストレス値 += MARK:反発刻印
	;ストレス値100以上で崩壊を得る
	;撤廃しました
	;SIF CFLAG:ストレス値 >= 100 && !TALENT:崩壊
	;	CALL COLLAPSE_MIND_TRAIN

	;売却可能&助手可能判定 陥落系で無視できるって条件はほとんど意味無いか
	;従順+欲望が6以上
	SIF ABL:従順+ABL:欲望 >= 6
		LOCAL = 1

	;[反抗的][気丈]なら従順LV4以上必要 服従系陥落素質があれば無視できる
	IF (TALENT:反抗的 || TALENT:気丈) && !TALENT:服従 && !TALENT:隷属
		SIF ABL:従順 < 4
			LOCAL = 0
	ENDIF

	;[自制心][無関心]なら欲望LV4以上必要 淫乱系陥落素質があれば無視できる
	IF (TALENT:自制心 || TALENT:無関心) && !TALENT:淫乱 && !TALENT:娼婦
		SIF ABL:欲望 < 4
			LOCAL = 0
	ENDIF

	;[プライド高い][生意気]なら奉仕精神LV4以上必要
	IF TALENT:プライド高い || TALENT:生意気
		SIF ABL:奉仕精神 < 4
			LOCAL = 0
	ENDIF

	;段位認定裏七級の場合、陥落していないと売却できない
	IF FLAG:ゲームモード == 8 && 受験段位名 == "裏七級"
		SIF !CFLAG:陥落
			LOCAL = 0
	ENDIF

	IF LOCAL
		;上記をクリアし、技巧3以上or奉仕精神3以上orマゾっ気3以上orCVAB感覚合計8以上or従順5以上or欲望5以上なら売却可能
		IF ABL:技巧 >= 3 || ABL:奉仕精神 >= 3 || ABL:マゾっ気 >= 3 || ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 8 || ABL:従順 >= 5 || ABL:欲望 >= 5
			IF CFLAG:状態 <= 0 && FLAG:ゲームモード != 7
				PRINTFORMW %NAME:TARGET%が売却可能になりました
				CFLAG:状態 = 1
			ENDIF
			;さらに従順+欲望+技巧が14以上なら助手可能
			IF ABL:従順+ABL:欲望+ABL:技巧 >= 14
				IF CFLAG:状態 <= 1
					PRINTFORMW %NAME:TARGET%が助手可能になりました
					CFLAG:状態 = 2
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF ASSI >= 0
	IF TALENT:ASSI:妖狐 && ITEM:媚薬 == 0
		PRINTFORMW [妖狐]の%CALLNAME:ASSI%が媚薬を調達してきたようです
		ITEM:媚薬++
	ENDIF
ENDIF

IF TIME == 1
	LASTMONEY = MONEY
	;主人が行動不可だと収入入らない
	SIF !CFLAG:MASTER:使用不可
		CALL STRATEGY_INCOME
ENDIF
;主人が行動不可だと発生しない
SIF !CFLAG:MASTER:使用不可
	CALL TURNEND_IDOL

REPEAT CHARANUM
	;種族妖精ならランダムで消費アイテム取得
	IF CSTR:COUNT:種族 == "妖精" && CFLAG:COUNT:陥落 && !CFLAG:MASTER:使用不可
		LOCAL = RAND:8+100
		LOCAL:1 = RAND:3+1
		PRINTFORMW %CALLNAME:COUNT%が{LOCAL:1}個の%ITEMNAME:LOCAL%を拾ってきました
		ITEM:LOCAL += LOCAL:1
	ENDIF

	LOCAL = 0
	;ヤンデレのストレス処理
	IF TALENT:COUNT:ヤンデレ
		IF TARGET != COUNT
			;陥落済み
			IF CFLAG:COUNT:陥落
				;MASTERとの相性が良いほど待機中のストレス増加
				LOCAL = (RELATION:COUNT:(NO:MASTER)-100)/10+1
				;しかしTARGETとの相性が良いと緩和 悪いと増加
				SIF TARGET > 0
					LOCAL -= (RELATION:COUNT:(NO:TARGET)-100)/10
				;ASSIとの相性が悪いと増加
				SIF ASSI > 0 && RELATION:COUNT:(NO:ASSI) < 100
					LOCAL += ABS(RELATION:COUNT:(NO:ASSI)-100)/10+1
			;未陥落
			ELSE
				;TARGETとの相性が良いと待機中のストレス増加
				SIF TARGET > 0 && RELATION:COUNT:(NO:TARGET) > 0
					LOCAL += (RELATION:COUNT:(NO:TARGET))/10+1
				;ASSIとの相性が良くても待機中のストレスが増加
				SIF ASSI > 0 && RELATION:COUNT:(NO:ASSI) > 0
					LOCAL += (RELATION:COUNT:(NO:ASSI))/10+1
			ENDIF
			SIF LOCAL > 0
				CFLAG:COUNT:ストレス値 += LOCAL
		;逆に調教対象の場合は相性に応じて大幅にストレス値減少
		ELSEIF TARGET == COUNT
			;(MASTERとの相性-100)/5 つまり200%(恋慕時)だと20減少 上がることはない
			LOCAL = MAX((RELATION:COUNT:(NO:MASTER)-100)/5, 0)
			;(ASSIとの相性-100)/10 こちらもストレス値上がることはない
			SIF ASSI > 0
				LOCAL += MAX((RELATION:COUNT:(NO:ASSI)-100)/10, 0)
		ENDIF
	ENDIF
	
	;その他ストレス処理
	;調教対象の場合
	IF COUNT == TARGET
		;未陥落 +5
		SIF !CFLAG:陥落
			CFLAG:ストレス値 += 5
		;反発刻印*2
		CFLAG:ストレス値 += MARK:反発刻印*2
		;主人との相性 100-相性値/10(50%なら+5、70%なら+3 低下はしない)
		CFLAG:ストレス値 += MAX(100-RELATION:(NO:MASTER), 0)/10
		;助手が居る場合も上と同じ式で計算
		SIF ASSI > 0
			CFLAG:ストレス値 += MAX(100-RELATION:(NO:ASSI), 0)/10
	ENDIF
	;助手の場合
	SIF COUNT == ASSI
		CFLAG:ASSI:ストレス値 += 3
	;反発刻印
	CFLAG:COUNT:ストレス値 += MARK:COUNT:反発刻印
	LOCAL = 0
	LOCAL:1 = 0
	;素質の計算
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF !TALENT:COUNT:LCOUNT
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT
			;ツンデレ 陥落済みかどうかでストレス±2
			CASE "ツンデレ"
				IF CFLAG:COUNT:陥落
					LOCAL:1 -= 2
				ELSE
					LOCAL:1 += 2
				ENDIF
			;寂しがり屋 未配属でTARGETでもASSIでもない場合は+3
			CASE "寂しがり屋"
				SIF CSTR:COUNT:配属 == "" && COUNT != TARGET && COUNT != ASSI
					LOCAL += 3
			;抑圧 +ABL:欲望
			CASE "抑圧"
				LOCAL += ABL:COUNT:欲望
			;男好き、男嫌い、女好き、女嫌い 配属中は同じ配属先の男女キャラ数に応じてストレス増減 最大5
			CASE "男好き", "男嫌い"
				人数 = 0
				IF CSTR:COUNT:配属 != ""
					FOR LCOUNT2, 0, CHARANUM
						SIF TALENT:LCOUNT2:性別 == 1 && CSTR:LCOUNT2:配属 == CSTR:COUNT:配属
							人数++
					NEXT
				ENDIF
				IF TALENTNAME:LCOUNT == "男好き"
					LOCAL -= MIN(人数, 5)
				ELSEIF TALENTNAME:LCOUNT == "男嫌い"
					LOCAL += MIN(人数, 5)
				ENDIF
			CASE "女好き", "女嫌い"
				人数 = 0
				IF CSTR:COUNT:配属 != ""
					FOR LCOUNT2, 0, CHARANUM
						SIF TALENT:LCOUNT2:性別 == 2 && CSTR:LCOUNT2:配属 == CSTR:COUNT:配属
							人数++
					NEXT
				ENDIF
				IF TALENTNAME:LCOUNT == "女好き"
					LOCAL -= MIN(人数, 5)
				ELSEIF TALENTNAME:LCOUNT == "女嫌い"
					LOCAL += MIN(人数, 5)
				ENDIF
			;同族嫌悪 配属中は同じ種族のキャラに応じてストレス増加 最大5
			CASE "同族嫌悪"
				人数 = 0
				IF CSTR:COUNT:配属 != ""
					FOR LCOUNT2, 0, CHARANUM
						SIF CSTR:LCOUNT2:種族 == CSTR:COUNT:種族 && CSTR:LCOUNT2:配属 == CSTR:COUNT:配属
							人数++
					NEXT
				ENDIF
				LOCAL += MIN(人数, 5)
			;バストサイズ 巨乳以上だと+1
			CASE "バストサイズ"
				SIF TALENT:COUNT:バストサイズ >= 4
					LOCAL:1 += 1
			;日光浴 昼なら-1
			CASE "日光浴"
				SIF TIME == 0
					LOCAL -= 1
			;月光浴 夜なら-1
			CASE "月光浴"
				SIF TIME == 1
					LOCAL -= 1
			;-3
			CASE "ハイテンション", "解放", "無機物"
				LOCAL:1 -= 3
			;-2
			CASE "好奇心", "楽観的", "自慰しやすい", "幼稚"
				LOCAL:1 -= 2
			;-1
			CASE "衝動的", "冷静", "無関心", "感情乏しい", "食欲皆伝"
				LOCAL:1 -= 1
			;+1
			CASE "臆病", "強気", "弱気", "潔癖症"
				LOCAL:1 += 1
			;+2
			CASE "生意気","反抗的", "自制心", "悲観的"
				LOCAL:1 += 2
			;+3
			CASE "性器破損", "育児中"
				LOCAL:1 += 3
			;+4
			CASE "プライド高い", "弱味"
				LOCAL:1 += 4
			;+5
			CASE "妊娠"
				LOCAL:1 += 5
		ENDSELECT
	NEXT
	
	;LOCALは毎ターン計算
	;LOCAL:1は配属されてなく休憩フラグが立っていれば無視できるが、LOCAL:1の結果でストレスが低下することはない
	CFLAG:COUNT:ストレス値 += LOCAL
	SIF !FLAG:休憩フラグ || CSTR:COUNT:配属 != ""
		CFLAG:COUNT:ストレス値 += MAX(LOCAL:1, 0)
	
	
	;魔剤フラグ消去
	CFLAG:COUNT:魔剤 = 0
	;魔剤耐性減少
	IF TALENT:COUNT:小人体型
		CFLAG:COUNT:魔剤耐性 -= 5
	ELSEIF TALENT:COUNT:小柄体型
		CFLAG:COUNT:魔剤耐性 -= 6
	ELSE
		CFLAG:COUNT:魔剤耐性 -= 7
	ENDIF
	;魔剤耐性は1000～2000の間で変動する
	SIF CFLAG:COUNT:魔剤耐性 < 1000
		CFLAG:COUNT:魔剤耐性 = 1000

	;気力の回復
	;基本的に昼なら1/3回復、夜なら2/3回復
		SELECTCASE TIME
		CASE 0
			LOCAL = MAXBASE:COUNT:気力/3
		CASE 1
			LOCAL = MAXBASE:COUNT:気力/3*2
	ENDSELECT
	;まずストレス値だけ引く 割合じゃないので気力が低いキャラほどストレスの影響が大きい
	LOCAL -= CFLAG:COUNT:ストレス値
	
	;素質の計算
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF !TALENT:COUNT:LCOUNT
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT
			;臆病 未陥落なら苦痛刻印と屈服刻印に応じて減少(50～150)
			CASE "臆病"
				SIF !CFLAG:COUNT:陥落
					LOCAL -= 50*LIMIT(MARK:COUNT:苦痛刻印-MARK:COUNT:屈服刻印, 1, 3)
			;+10%
			CASE "気丈", "冷静", "楽観的", "目立ちたがり"
				LOCAL += MAXBASE:COUNT:気力/10
			;+5%
			CASE "サボり魔", "貞操無頓着", "恥薄い"
				LOCAL += MAXBASE:COUNT:気力/20
			;-5%
			CASE "抵抗", "恥じらい"
				LOCAL -= MAXBASE:COUNT:気力/20
			;-10%
			CASE "弱気", "悲観的", "貞操観念"
				LOCAL -= MAXBASE:COUNT:気力/10
		ENDSELECT
	NEXT
	BASE:COUNT:気力 += MAX(LOCAL, 1)
	
	;体力の回復（午後の調教後は夜の休みが入るので回復大きい）
	SELECTCASE TIME
		CASE 0
			SELECTCASE FLAG:難易度
				;NORMAL
				CASE 1
					LOCAL = MAXBASE:COUNT:体力 / 8
				;HYPER
				CASE 2
					LOCAL = MAXBASE:COUNT:体力 / 10
				;ANOTHER
				CASE 3
					LOCAL = MAXBASE:COUNT:体力 / 15
			ENDSELECT
		CASE 1
			SELECTCASE FLAG:難易度
				;NORMAL
				CASE 1
					LOCAL = MAXBASE:COUNT:体力 / 3
				;HYPER
				CASE 2
					LOCAL = MAXBASE:COUNT:体力 / 4
				;ANOTHER
				CASE 3
					LOCAL = MAXBASE:COUNT:体力 / 5
			ENDSELECT
	ENDSELECT

	;日光浴、月光浴（吸血鬼）、夢喰い
	SELECTCASE TIME
		CASE 0
			SIF TALENT:COUNT:日光浴
				LOCAL += 100
		CASE 1
			IF TALENT:COUNT:月光浴 || TALENT:COUNT:吸血鬼
				SELECTCASE 月齢
					CASE 0 TO 1500
						LOCAL += 月齢/10
					CASE 1500 TO 3000
						LOCAL += (3000-月齢)/10
				ENDSELECT
			ENDIF
			;主人と当人引いて-2
			SIF TALENT:COUNT:夢喰い && CHARANUM-2 > 0
				LOCAL += (CHARANUM-2)*30
			;こちらも医務室配属での効果にしたい
			;SIF TALENT:MASTER:夢喰い || (ASSI > 0 && TALENT:ASSI:夢喰い)
			;	LOCAL += 50
	ENDSELECT

	;主人もしくは助手が治療持ち
	;医務室の実装により廃止
	;SIF TALENT:MASTER:治療 || (ASSI >= 0 && TALENT:ASSI:治療)
	;	LOCAL += 150

	;使用不可のキャラは休憩フラグの影響を受けない
	IF !CFLAG:COUNT:使用不可
		;休憩フラグが立っていて配属中じゃない
		IF (FLAG:休憩フラグ && CSTR:COUNT:配属 == "") || CFLAG:COUNT:休憩
			LOCAL += 200
			SELECTCASE FLAG:難易度
				;NORMALなら休憩でストレス値-15
				CASE 1
					CFLAG:COUNT:ストレス値 -= 15
				;HYPERは-10
				CASE 2
					CFLAG:COUNT:ストレス値 -= 10
				;ANOTHERは-5
				CASE 3
					CFLAG:COUNT:ストレス値 -= 5
			ENDSELECT
		ENDIF

		;休憩コマンドで休んだ場合
		;IF FLAG:休憩フラグ == 2
		;	TIMES LOCAL , 2.00
		;	CFLAG:COUNT:ストレス値 -= 2
		;	SIF TALENT:COUNT:ハイテンション
		;		CFLAG:COUNT:ストレス値--
		;ENDIF
	ENDIF

	;宇宙人
	SIF CSTR:COUNT:種族 == "宇宙人"
		TIMES LOCAL , 3.00

	;回復早い、回復遅い
	IF TALENT:COUNT:回復早い
		TIMES LOCAL , 1.50
	ELSEIF TALENT:COUNT:回復遅い
		TIMES LOCAL , 0.75
	ENDIF
	
	;小柄体型 子供の回復力は高いうんぬん
	SIF TALENT:COUNT:小柄体型
		TIMES LOCAL , 1.20

	;無機物、ゾンビ
	SIF TALENT:COUNT:無機物
		TIMES LOCAL , 0.80
	SIF TALENT:COUNT:ゾンビ
		TIMES LOCAL , 0.50

	;調教中に料理作った場合は料理技能に応じて全員の回復量アップ
	IF TFLAG:料理
		LOCAL:1 = TFLAG:料理
		;食欲皆伝はどんな料理も美味しく食べる
		SIF TALENT:COUNT:食欲皆伝
			LOCAL:1 += 3
		SELECTCASE LOCAL:1
			;料理技能0だと逆に回復量下がる
			CASE -1
				LOCAL -= 50
			CASE 1 TO 9
				LOCAL += TFLAG:料理*20
			CASE IS >= 10
				LOCAL += 250
		ENDSELECT
		CALL KOJO_MESSAGE_EVENT, "料理"
	ENDIF

	;調教中にお菓子作った場合は料理技能に応じて全員の回復量アップ
	IF TFLAG:お菓子
		LOCAL:1 = TFLAG:お菓子
		;食欲皆伝はどんなお菓子も美味しく食べる
		SIF TALENT:COUNT:食欲皆伝
			LOCAL:1 += 3
		SELECTCASE LOCAL:1
			;料理技能0だと逆に回復量下がる
			CASE -1
				LOCAL -= 30
			CASE 1 TO 9
				LOCAL += TFLAG:料理*10
			CASE 10
				LOCAL += 120
		ENDSELECT
		CALL KOJO_MESSAGE_EVENT, "お菓子"
	ENDIF
	
	;リ：ジェネレーション発動中は詠唱者のスキルに応じて回復量アップ
	LOCAL += リジェネレーション*100

	;娼館配属中は回復量下がる
	SIF CSTR:COUNT:配属 == "娼館" && CFLAG:COUNT:休憩 == 0 && FLAG:娼館営業中 == 1
		LOCAL /= 10
	;芸能プロダクション配属中で「レッスンなし」以外でも回復量下がる
	SIF CSTR:COUNT:配属 == "芸能プロダクション" &&  CFLAG:COUNT:レッスン != 0
		LOCAL /= 8

	;「暑さに弱い」「寒さに弱い」 最低100は保証
	IF TALENT:COUNT:暑さに弱い
		SIF FLAG:気温 >= 200
			LOCAL -= FLAG:気温-200
		LOCAL = MAX(LOCAL, 100)
	ENDIF
	IF TALENT:COUNT:寒さに弱い
		SIF FLAG:気温 <= 150
			LOCAL -= 150-FLAG:気温
		LOCAL = MAX(LOCAL, 100)
	ENDIF

	;酔ってると回復量低下 最低100は保証
	IF CFLAG:COUNT:酔
		IF TALENT:COUNT:酒豪
			LOCAL -= CFLAG:COUNT:酔
		ELSEIF TALENT:COUNT:下戸
			LOCAL -= CFLAG:COUNT:酔*3
		ELSE
			LOCAL -= CFLAG:COUNT:酔*2
		ENDIF
		LOCAL = MAX(LOCAL, 100)
	ENDIF

	;「回復量低下」と「ストレス値」によって回復量がさらに下がる
	;ストレス値が気力回復量に影響するようになったので、気力の割合で回復量が決まる
	;それぞれ100倍にしてから計算
	LOCAL = (LOCAL*100)/((MAXBASE:COUNT:気力*100)/BASE:COUNT:気力)

	BASE:COUNT:体力 += LOCAL / (1 + CFLAG:COUNT:回復量低下); / CBRT(1000*(1+CFLAG:COUNT:ストレス値))
	SIF BASE:COUNT:体力 > MAXBASE:COUNT:体力
		BASE:COUNT:体力 = MAXBASE:COUNT:体力

	;ストレス値は0～999にする
	CFLAG:COUNT:ストレス値 = LIMIT(CFLAG:COUNT:ストレス値, 0, 999)
	;依存度が-1000未満の場合、-1000にする
	SIF CFLAG:COUNT:依存度 < -1000
		CFLAG:COUNT:依存度 = -1000

	;キャラごとの休憩フラグが経つのは一日の終了時（夜）なので、夜は休憩フラグを外さない
	SIF TIME == 0
		CFLAG:COUNT:休憩 = 0
	;回復量低下処理
	SIF CFLAG:COUNT:回復量低下 > 0
		CFLAG:COUNT:回復量低下--

	CFLAG:COUNT:二日酔い = 0
	;酔が抜ける 必ずしも1ターンで全快するわけではない
	SELECTCASE TIME
		;昼→夜
		CASE 0
			IF TALENT:COUNT:酒豪
				CFLAG:COUNT:酔 -= 20
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/3
			ELSEIF TALENT:COUNT:下戸
				CFLAG:COUNT:酔 -= 5
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/5
			ELSE
				CFLAG:COUNT:酔 -= 10
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/4
			ENDIF
		;夜→昼
		CASE 1
			IF TALENT:COUNT:酒豪
				CFLAG:COUNT:酔 -= 50
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/2
			ELSEIF TALENT:COUNT:下戸
				CFLAG:COUNT:酔 -= 10
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/4
			ELSE
				CFLAG:COUNT:酔 -= 30
				CFLAG:COUNT:酔 -= TEQUIP:COUNT:ウォッカ/3
			ENDIF
			;これで酔いが残っていると二日酔いを引き起こす
			IF CFLAG:COUNT:酔 > 0
				CFLAG:COUNT:二日酔い = 1
				CFLAG:COUNT:酔 = 0
			ENDIF
	ENDSELECT
	TEQUIP:COUNT:ウォッカ = 0
	SIF CFLAG:COUNT:酔 < 0
		CFLAG:COUNT:酔 = 0
	
	;使用不可フラグ
	IF CFLAG:COUNT:使用不可
		SIF COUNT == TARGET
			TARGET = -1
		SIF COUNT == ASSI
			ASSI = -1
		CFLAG:COUNT:使用不可--
		IF CFLAG:COUNT:使用不可 == 0
			IF CFLAG:COUNT:改造中 == 1
				PRINTFORMW %CALLNAME:COUNT%は体外受精を終えたため、再び調教可能になりました
			ELSE
				PRINTFORMW %CALLNAME:COUNT%が再び調教可能になりました
			ENDIF
		ENDIF
	ENDIF
	
	;体力、気力の値整理
	CALL SETBASE, COUNT
REND
TFLAG:料理 = 0
TFLAG:お菓子 = 0

CALL EVENTCHECK_ABL
FLAG:休憩フラグ = 0

;午後なら次の日、午前なら午後にする
SELECTCASE TIME
	CASE 0
		PRINTW 夜になった……
		TIME = 1
	CASE 1
		;日付変更時のイベント呼び出し
		CALL EVENT_NEXTDAY_BEMANI
		CALL EVENT_NEXTDAY
		SIF !CFLAG:MASTER:使用不可
			CALL EVENT_HONEYTRAP
		;主人が使用不可の場合、期限的な日付加算は行わない
		IF !CFLAG:MASTER:使用不可
			DAY++
			DAY:1 = DAY
		;もし主人が妊娠で使用不可だった場合のために、妊娠フラグを進めるためのDAY:1はカウントする
		ELSE
			DAY:1++
		ENDIF
		PRINTW 一日が終わった……
		SIF TARGET == -1 && !CFLAG:MASTER:使用不可
			CALL 実績解除, "Without you tonight"
		TIME = 0
		;ゲームモード7(新婚モード)か9(PREMIUM FREE)なら10日に一度、借金の返済・新規借入ができる
		SIF (FLAG:ゲームモード == 7 || FLAG:ゲームモード == 9) && DAY%10 == 0
			CALL DEBT
		;バッドエンドの判定
		;期限切れ
		SIF (DAY > 100 && FLAG:ゲームモード == 0) || (DAY > 300 && FLAG:ゲームモード == 1)
			JUMP BADENDING, 1
		;金欠エンド
		;ただしゲームモード7(新婚モード)と9(PREMIUM FREE)では呼び出されない
		IF MONEY < 0 && FLAG:ゲームモード != 7 && FLAG:ゲームモード != 9
			;実績:STRATEGYモードで金欠エンドを迎える
			CALL 実績解除, "無能な経営者"
			JUMP BADENDING, 2
		ENDIF
		;朝になった時のイベント呼び出し
		CALL EVENT_NEWDAY
		CALL EVENT_NEWDAY_SELF
ENDSELECT

;実績「タルタでラジェ・ガレト・ヴァロ全員の童貞を奪う」
;ビットで処理(ラジェが1、ガレトが2、ヴァロが4)
;7になったら実績解除
REPEAT CHARANUM
	IF VIRGIN:COUNT:0 == "タルタ" || VIRGIN:COUNT:10 == "タルタ"
		SELECTCASE CHARANAMEF(COUNT)
			CASE "ラジェ"
				タルタ童貞喰い |= 1
			CASE "ガレト"
				タルタ童貞喰い |= 2
			CASE "ヴァロ"
				タルタ童貞喰い |= 4
		ENDSELECT
	ENDIF
REND
SIF タルタ童貞喰い & 7
	CALL 実績解除, "I LOVE SAKURA"

;実績：陥落したイロドリミドリの全員と同棲する
LOCAL = 0
REPEAT CHARANUM
	IF CFLAG:COUNT:陥落
		SELECTCASE CHARANAMEF(COUNT)
			CASE "明坂 芹菜", "御形 アリシアナ", "天王洲 なずな", "小仏 凪", "箱部 なる"
				LOCAL++
		ENDSELECT
		SIF LOCAL == 5
			CALL 実績解除, "ヨリドリミドリ"
	ENDIF
REND

;気温の算出
CALL TEMPERATURE
;特定のキャラがいる場合、気温に補正を加える(ただし昼のみ)
;主人・奴隷・助手の場合の方が影響が強い
IF TIME == 0
	REPEAT CHARANUM
		SELECTCASE CHARANAMEF(COUNT)
			CASE "フローラ"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 += 60
				ELSE
					FLAG:気温 += 6
				ENDIF
			CASE "烈"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 += 30
				ELSE
					FLAG:気温 += 3
				ENDIF
			CASE "氷海"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 -= 30
				ELSE
					FLAG:気温 -= 3
				ENDIF
			CASE "茜"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 += 50
				ELSE
					FLAG:気温 += 5
				ENDIF
			CASE "スノーフェアリー"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 -= 60
				ELSE
					FLAG:気温 -= 6
				ENDIF
			CASE "氷雪ちゃん"
				IF COUNT == MASTER || COUNT == TARGET || COUNT == ASSI
					FLAG:気温 -= 150
				ELSE
					FLAG:気温 -= 15
				ENDIF
		ENDSELECT
	REND
	;魔法の影響
	FLAG:気温 -= フリージングアトモスフィア*30
	FLAG:気温 += バーニンザフロア*30
ENDIF
;天気の決定
CALL WEATHER

REPEAT CHARANUM
	;フローラ専用イベント
	IF CHARANAMEF(COUNT) == "フローラ"
		LOCAL = MAXBASE:COUNT:体力
		MAXBASE:COUNT:体力 = CSVBASE(NO:COUNT, 0) + (FLAG:気温-180)*2
		BASE:COUNT:体力 += MAXBASE:COUNT:体力-LOCAL
		SIF BASE:COUNT:体力 > MAXBASE:COUNT:体力
			BASE:COUNT:体力 = MAXBASE:COUNT:体力
	ENDIF
	;鳳凰専用イベント
	IF CSTR:COUNT:種族 == "鳳凰" && FLAG:気温 > 0
		LOCAL = FLAG:気温
		SELECTCASE FLAG:天気
			CASE 2
				TIMES LOCAL , 0.50
			CASE 3 TO 7
				TIMES LOCAL , 0.20
		ENDSELECT
		;気温と天気で計算したボーナス分体力気力が増加
		LOCAL:1 = MAXBASE:COUNT:体力
		LOCAL:2 = MAXBASE:COUNT:気力
		MAXBASE:COUNT:体力 = MAXBASE:COUNT:鳳凰基礎体力+LOCAL
		MAXBASE:COUNT:気力 = MAXBASE:COUNT:鳳凰基礎気力+LOCAL
		BASE:COUNT:体力 += MAXBASE:COUNT:体力-LOCAL:1
		BASE:COUNT:気力 += MAXBASE:COUNT:気力-LOCAL:2
		SIF BASE:COUNT:体力 > MAXBASE:COUNT:体力
			BASE:COUNT:体力 = MAXBASE:COUNT:体力
		SIF BASE:COUNT:気力 > MAXBASE:COUNT:気力
			BASE:COUNT:気力 = MAXBASE:COUNT:気力
	ENDIF
REND

;主人が使用不可の場合はTARGETとASSIを外してSHOP画面表示し、収入と支出の一切を中断して時間だけ経過させる。DAYは増えないので期限には影響無し
IF CFLAG:MASTER:使用不可
	TARGET = -1
	ASSI = -1

	;曜日計算
	FLAG:曜日 = ZELLER(YEAR,MONTH,DAYS)

	;祝日・記念日の設定
	CALL HOLIDAY

	CALL SHOP_UI

	PRINTFORMW %CALLNAME:MASTER%の都合により行動を終了します
	TARGET = -1
	ASSI = -1
	FLAG:休憩フラグ = 1
	RESTART
ENDIF

;魔法の効果終了は最後
IF フリージングアトモスフィア > 0
	SIF --フリージングアトモスフィア == 0
		PRINTW [フリージングアトモスフィア]の効果が切れました
ENDIF
IF バーニンザフロア > 0
	SIF --バーニンザフロア == 0
		PRINTW [バーニンザフロア]の効果が切れました
ENDIF
IF ストームバスター > 0
	SIF --ストームバスター == 0
		PRINTW [ストームバスター]の効果が切れました
ENDIF
IF スウィートレイン > 0
	SIF --スウィートレイン == 0
		PRINTW [スウィートレイン]の効果が切れました
ENDIF
IF スノープリズム > 0
	SIF --スノープリズム == 0
		PRINTW [スノープリズム]の効果が切れました
ENDIF
IF リジェネレーション > 0
	SIF --リジェネレーション == 0
		PRINTW [リ：ジェネレーション]の効果が切れました
ENDIF
IF ヒプノティッククライシス > 0
	SIF --ヒプノティッククライシス == 0
		PRINTW [ヒプノティッククライシス]の効果が切れました
ENDIF
IF エナジードライブ > 0
	IF --エナジードライブ == 0
		PRINTFORMW %CALLNAME:MASTER%にかけられた[エナジードライブ]の効果が切れました
		REPEAT VARSIZE("ABL")
			SIF ABLNAME:COUNT != ""
				ABL:MASTER:COUNT -= エナジードライブ:1
		REND
		VARSET エナジードライブ
	ENDIF
ENDIF
REPEAT CHARANUM
	IF ビーストモード:COUNT:0 > 0
		IF --ビーストモード:COUNT:0 == 0
			PRINTFORMW %CALLNAME:COUNT%にかけられた[ビーストモード]の効果が切れました
			SIF ビーストモード:COUNT:1
				TALENT:COUNT:動物耳 = 0
			SIF ビーストモード:COUNT:2
				TALENT:COUNT:尻尾 = 0
			SIF ビーストモード:COUNT:3
				TALENT:COUNT:ファーリー = 0
			CSTR:COUNT:種族 = %ビーストモードS:COUNT:0%
			VARSET ビーストモード:COUNT:0
			VARSET ビーストモードS:COUNT:0
		ENDIF
	ENDIF
	IF ティルナノーグ:COUNT:0 > 0
		IF --ティルナノーグ:COUNT:0 == 0
			PRINTFORMW %CALLNAME:COUNT%にかけられた[ティル・ナ・ノーグ]の効果が切れました
			TALENT:COUNT:小人体型 = 1
		ENDIF
	ENDIF
	IF CFLAG:COUNT:憑依中
		IF --CFLAG:COUNT:憑依中 == 0
			PRINTFORMW %CALLNAME:COUNT%にかけられた[ポゼッション]の効果が切れました
			FOR LCOUNT, 0, 30
				SIF ABLNAME:LCOUNT == "魔法技能"
					CONTINUE
				ABL:COUNT:LCOUNT = ポゼッション:COUNT:LCOUNT
			NEXT
			VARSET ポゼッション:COUNT:0
		ENDIF
	ENDIF
REND

BEGIN SHOP

;-------------------------------------------------
;朝になった時のイベント
;-------------------------------------------------
@EVENT_NEWDAY
#DIM LCOUNT
VARSET LOCAL, 0

;日付加算関数
CALL NEXTDAY

;祝日・記念日の処理はSHOPで行うようにしました

;実績:新年を迎える
SIF MONTH == 1 && DAYS == 1
	CALL 実績解除, "あけおめアゲイ～ン"

;放尿経験100以上でおもらし癖がつく
;[幼稚]なら放尿経験30以上でつく
;アイテムで治した場合は発生しない
IF TARGET >= 0
	IF !TALENT:おもらし癖 && !CFLAG:おもらし改善フラグ
		IF (TALENT:幼稚 && EXP:放尿経験 >= 30) || (!TALENT:幼稚 && EXP:放尿経験 >= 100)
			DRAWLINE
			CALL KOJO_MESSAGE_EVENT, "おもらし癖"
			TALENT:おもらし癖 = 1
		ENDIF
	ENDIF
ENDIF

;調合知識習得イベント
IF ASSI > 0 && !TALENT:MASTER:調合知識
	;助手が調合知識持ちで陥落済み
	IF TALENT:ASSI:調合知識 && CFLAG:ASSI:陥落
		;技巧LV3以上 奉仕精神LV3以上
		IF ABL:ASSI:技巧 >= 3 && ABL:ASSI:奉仕精神 >= 3
			;主人の技巧LV4以上
			IF ABL:MASTER:技巧 >= 4
				DRAWLINE
				CALL KOJO_MESSAGE_EVENT, "調合知識伝授", ASSI
				TALENT:MASTER:調合知識 = 1
				CFLAG:ASSI:主人に調合知識を伝授した経験++
			ENDIF
		ENDIF
	ENDIF
ENDIF

;全体の妊娠発覚時のイベント
CALL GET_CHILD

;排卵誘発剤の効果終了と、不妊治療薬のコスト処理
REPEAT CHARANUM
	IF CFLAG:COUNT:排卵誘発剤
		PRINTFORMW %CALLNAME:COUNT%の排卵誘発剤の効果が切れたようだ
		CFLAG:COUNT:排卵誘発剤 = 0
	ENDIF
	IF CFLAG:COUNT:生殖力強化 == 1
		LOCAL = 300
		SIF TALENT:COUNT:小柄体型
			LOCAL += 200
		CALL PASELIW, @"%CALLNAME:COUNT%の不妊治療薬に{LOCAL}＄の費用が発生しました"
		CFLAG:COUNT:ストレス値++
	ENDIF
REND

CALL PREG_READY

@EVENT_NEXTDAY_BEMANI
;調教対象・助手が豹変しているとき、元に戻る
;現在豹変し得るのは、紅刃・冷音
IF TARGET >= 0 && TRANCE:200
	SELECTCASE CHARANAMEF(TARGET)
		CASE "紅刃", "春雨 冷音"
			CALL TRANCE_OFF, TARGET
			TRANCE:200 = 0
			PRINTFORMW ＜%CALLNAME:TARGET%は元の性格にもどった＞
	ENDSELECT
ENDIF
IF ASSI >= 0 && TRANCE:ASSI:200
	SELECTCASE CHARANAMEF(ASSI)
		CASE "紅刃", "春雨 冷音"
			CALL TRANCE_OFF, ASSI
			TRANCE:ASSI:200 = 0
			PRINTFORMW ＜%CALLNAME:ASSI%は元の性格にもどった＞
	ENDSELECT
ENDIF

@TRANCE_ON, ARG
;変身フラグにTRANCEを使用。変身での素質変更を行なう。
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		REPEAT 200
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:COUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 1
					ENDIF
			ENDSELECT
		REND
	CASE "春雨 冷音"
		;雨が降ると気力にボーナス
		;無条件に700増えるほかに、Y=1000/XのグラフでのX=1～X=[雨量]までの面積
		;つまり1000LOG([雨量])だけ増加する
		;ただし、整数への切り捨ての関係で100LOG([雨量]^10)としている
		;雨量に対する気力の参考値
		;雨量1:1700 雨量2:2300 雨量3:2700 雨量4:3000 雨量5:3300 雨量10:4000 雨量15:4400 雨量20:4600 雨量78以上:6000
		MAXBASE:ARG:気力 += 700
		MAXBASE:ARG:気力 += 100*LOG(POWER(LIMIT(FLAG:降水量, 1, 78), 10))
		BASE:ARG:気力 = MAXBASE:ARG:気力
		REPEAT 200
			;変身時(豹変時)
			;素質の消滅処理
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:COUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;素質の習得処理
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF !TALENT:ARG:COUNT
						TRANCE:ARG:COUNT = 1
						TALENT:ARG:COUNT = 1
					ENDIF
			ENDSELECT
		REND
ENDSELECT

@TRANCE_OFF, ARG
SELECTCASE CHARANAMEF(ARG)
	CASE "紅刃"
		REPEAT 200
			;変身解除時、習得処理を行なう
			;消滅素質：臆病、大人しい、保守的、一線越えない、貞操観念、痛みに弱い、濡れにくい、汚臭敏感、献身的
			SELECTCASE TALENTNAME:COUNT
				CASE "臆病", "大人しい", "保守的", "一線を越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 1
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：気丈、好奇心、楽観的、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 0
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
		REND
	CASE "春雨 冷音"
		MAXBASE:ARG:気力 = CSVBASE(NO:ARG, 1)
		REPEAT 200
			;変身解除時、習得処理を行なう
			;消滅素質：大人しい、無関心、一線越えない、貞操観念、恥じらい
			SELECTCASE TALENTNAME:COUNT
				CASE "大人しい", "無関心", "一線を越えない", "貞操観念", "恥じらい"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 1
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
			;変身解除時、消滅処理を行なう
			;習得素質：生意気、反抗的、衝動的、プライド高い、好奇心、目立ちたがり、貞操無頓着、恥薄い、痛みに強い、濡れやすい、汚臭鈍感、汚れ無視、快感に素直、倒錯的、両刀、サド
			SELECTCASE TALENTNAME:COUNT
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					IF TRANCE:ARG:COUNT
						TALENT:ARG:COUNT = 0
						TRANCE:ARG:COUNT = 0
					ENDIF
			ENDSELECT
		REND
ENDSELECT

;-------------------------------------------------
;女主人による色仕掛けイベント
;-------------------------------------------------
@EVENT_HONEYTRAP
LOCAL = 0
SIF !FLAG:色仕掛け
	RETURN 0
SIF TARGET < 0
	RETURN 0
SIF TALENT:MASTER:性別 != 2
	RETURN 0

SIF TALENT:生意気
	LOCAL -= 1
SIF TALENT:素直
	LOCAL += 1
SIF TALENT:反抗的
	LOCAL -= 2
SIF TALENT:衝動的
	LOCAL += 1
SIF TALENT:ツンデレ
	LOCAL -= 2
SIF TALENT:自制心
	LOCAL -= 2
SIF TALENT:無関心
	LOCAL -= 1
SIF TALENT:好奇心
	LOCAL += 2
SIF TALENT:一線越えない
	LOCAL -= 2
SIF TALENT:貞操観念
	LOCAL -= 1
SIF TALENT:貞操無頓着
	LOCAL += 2
SIF TALENT:抑圧
	LOCAL -= 1
SIF TALENT:解放
	LOCAL += 2
SIF TALENT:抵抗
	LOCAL -= 1
SIF TALENT:冷静
	LOCAL -= 1
SIF TALENT:弱味
	LOCAL += 1
SIF TALENT:強気
	LOCAL -= 1
SIF TALENT:弱気
	LOCAL += 1
SIF TALENT:献身的
	LOCAL += 1
SIF (TALENT:潔癖症 || TALENT:汚臭敏感) && TALENT:MASTER:ファーリー
	LOCAL -= 2
SIF TALENT:快感に素直
	LOCAL += 2
SIF TALENT:快感の否定
	LOCAL -= 2
IF TALENT:性別 == 2
	IF TALENT:両刀
		LOCAL += 1
	ELSE
		LOCAL -= 2
	ENDIF
ENDIF
SIF TALENT:女好き
	LOCAL += 2
SIF TALENT:同族嫌悪 && CSTR:種族 == CSTR:MASTER:種族
	LOCAL -= 2
SIF TALENT:妄信
	LOCAL += 1
IF TALENT:恋心 
	IF TALENT:恋心 != MASTER && !CFLAG:陥落
		LOCAL -= 3
	ELSEIF TALENT:恋心 == MASTER
		LOCAL += 3
	ENDIF
ENDIF
IF TALENT:既婚者
	IF TALENT:既婚者 != MASTER && !CFLAG:陥落
		LOCAL -= 5
	ELSEIF TALENT:既婚者 == MASTER
		LOCAL += 5
	ENDIF
ENDIF

SIF TALENT:MASTER:魅力
	LOCAL += 1
SIF TALENT:MASTER:魅惑
	LOCAL += 1
SIF TALENT:MASTER:謎の魅力
	LOCAL += 2
SIF TALENT:MASTER:小柄体型
	LOCAL -= 1
SIF ヒプノティッククライシス
	LOCAL += RAND(15, 21)

LOCAL += ABL:従順
LOCAL += ABL:欲望
LOCAL += ABL:奉仕精神
LOCAL -= MARK:反発刻印*2
LOCAL -= MARK:苦痛刻印

SIF CFLAG:陥落
	LOCAL += 20

;地の文分岐に使う
TFLAG:誘惑ポイント = LOCAL

DRAWLINE
DO
	PRINTFORML 就寝間際、寝付こうとしている%CALLNAME:TARGET%の姿を目撃した
	PRINTL [0] 夜這いをかける
	PRINTL [1] 誘惑する
	PRINTL [2] 今日はやめとく
	PRINTL [3] そんなつもりは無い(イベント非表示 オプションから再度表示出来ます)
	INPUT
	SELECTCASE RESULT
		CASE 2
			RETURN 0
		CASE 3
			FLAG:色仕掛け = 0
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1)

;夜這い
SELECTCASE RESULT
	CASE 0
		IF TFLAG:誘惑ポイント >= 12
			TFLAG:色仕掛け = 1
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					VIRGIN:10 = %NAME:MASTER%
				ELSE
					VIRGIN:0 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					VIRGIN:MASTER:11 = %NAME:TARGET%
				ELSE
					VIRGIN:MASTER:1 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				EXP:射精経験 += 2
				EXP:MASTER:Ｖ経験 += 1
				EXP:MASTER:膣射経験 += 1
				PRINTL 射精経験+2
				PRINTFORML %CALLNAME:MASTER%のＶ経験+1、膣内射精経験+1
			ELSE
				EXP:絶頂経験 += 3
				PRINTL 絶頂経験+3
			ENDIF
			IF PENIS(MASTER)
				EXP:MASTER:射精経験 += 1
				EXP:精液経験 += 1
				PRINTFORML %CALLNAME:MASTER%の射精経験+1
				PRINTL 精液経験+1
			ENDIF
			IF TALENT:性別 == 2
				EXP:レズ経験 += 3
				PRINTL レズ経験+3
			ELSE
				EXP:MASTER:性交経験 += 1
				EXP:性交経験 += 1
				PRINTL お互いの性交経験+1
			ENDIF
			EXP:主人調教経験 += 2
			PRINTL 主人調教経験+2
			CFLAG:依存度 += TFLAG:誘惑ポイント
			PRINTL 依存度増加
			JUEL:快Ｃ += TFLAG:誘惑ポイント*50
			JUEL:恭順 += TFLAG:誘惑ポイント*50
			JUEL:欲情 += TFLAG:誘惑ポイント*70
			JUEL:屈服 += TFLAG:誘惑ポイント*100
			PRINTFORML 快Ｃの珠+{TFLAG:誘惑ポイント*50}
			PRINTFORML 恭順の珠+{TFLAG:誘惑ポイント*50}
			PRINTFORML 欲情の珠+{TFLAG:誘惑ポイント*70}
			PRINTFORML 屈服の珠+{TFLAG:誘惑ポイント*100}
		ELSE
			TFLAG:色仕掛け = 2
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				JUEL:恭順 += TFLAG:誘惑ポイント*10
				JUEL:欲情 += TFLAG:誘惑ポイント*10
				JUEL:恐怖 += TFLAG:誘惑ポイント*30
				PRINTFORML 恭順の珠+{TFLAG:誘惑ポイント*10}
				PRINTFORML 欲情の珠+{TFLAG:誘惑ポイント*10}
				PRINTFORML 恐怖の珠+{TFLAG:誘惑ポイント*30}
			ENDIF
		ENDIF
	CASE 1
		IF TFLAG:誘惑ポイント >= 16
			TFLAG:色仕掛け = 3
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			;童貞喪失処理
			IF PENIS(TARGET) && TALENT:童貞
				TALENT:童貞 = 0
				IF TALENT:性転換済
					VIRGIN:10 = %NAME:MASTER%
				ELSE
					VIRGIN:0 = %NAME:MASTER%
				ENDIF
			ENDIF
			;主人の処女喪失処理
			IF TALENT:MASTER:処女
				TALENT:MASTER:処女 = 0
				IF TALENT:MASTER:性転換済
					VIRGIN:MASTER:11 = %NAME:TARGET%
				ELSE
					VIRGIN:MASTER:1 = %NAME:TARGET%
				ENDIF
			ENDIF
			IF PENIS(TARGET)
				EXP:射精経験 += 2
				EXP:MASTER:Ｖ経験 += 2
				EXP:MASTER:膣射経験 += 2
				PRINTL 射精経験+2
				PRINTFORML %CALLNAME:MASTER%のＶ経験+2、膣内射精経験+2
			ELSE
				EXP:絶頂経験 += 3
				PRINTL 絶頂経験+3
			ENDIF
			IF PENIS(MASTER)
				EXP:MASTER:射精経験 += 2
				EXP:精液経験 += 2
				PRINTFORML %CALLNAME:MASTER%の射精経験+2
				PRINTL 精液経験+2
			ENDIF
			IF TALENT:性別 == 2
				EXP:レズ経験 += 5
				PRINTL レズ経験+5
			ELSE
				EXP:MASTER:性交経験 += 2
				EXP:性交経験 += 2
				PRINTL お互いの性交経験+2
			ENDIF
			EXP:奉仕快楽経験 += 3
			PRINTL 奉仕快楽経験+3
			CFLAG:依存度 += TFLAG:誘惑ポイント*3/2
			PRINTL 依存度増加
			JUEL:快Ｃ += TFLAG:誘惑ポイント*70
			JUEL:恭順 += TFLAG:誘惑ポイント*100
			JUEL:欲情 += TFLAG:誘惑ポイント*150
			JUEL:屈服 += TFLAG:誘惑ポイント*50
			JUEL:習得 += TFLAG:誘惑ポイント*70
			PRINTFORML 快Ｃの珠+{TFLAG:誘惑ポイント*70}
			PRINTFORML 恭順の珠+{TFLAG:誘惑ポイント*100}
			PRINTFORML 欲情の珠+{TFLAG:誘惑ポイント*150}
			PRINTFORML 屈服の珠+{TFLAG:誘惑ポイント*50}
			PRINTFORML 習得の珠+{TFLAG:誘惑ポイント*70}
		ELSE
			TFLAG:色仕掛け = 4
			CALL KOJO_MESSAGE_EVENT, "色仕掛け"
			IF TFLAG:誘惑ポイント > 0
				JUEL:恭順 += TFLAG:誘惑ポイント*10
				JUEL:欲情 += TFLAG:誘惑ポイント*10
				JUEL:恥情 += TFLAG:誘惑ポイント*30
				PRINTFORML 恭順の珠+{TFLAG:誘惑ポイント*10}
				PRINTFORML 欲情の珠+{TFLAG:誘惑ポイント*10}
				PRINTFORML 恥情の珠+{TFLAG:誘惑ポイント*30}
			ENDIF
		ENDIF
ENDSELECT

RETURN 0

