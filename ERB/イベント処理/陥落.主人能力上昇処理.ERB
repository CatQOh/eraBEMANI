;素質変化イベント.ERB
@EVENTCHECK_ABL
;奴隷の素質変化 憑依中は変化しない
SIF TARGET >= 0 && !CFLAG:憑依中
	CALL EVENTCHECK_T
IF ハットトリック
	ハットトリック = 0
	ハットトリック:1++
	SIF ハットトリック:1 >= 3
		CALL 実績解除, "HAT TRICK"
ELSE
	VARSET ハットトリック, 0
ENDIF
;調教者の素質変化 エナジードライブ中は変化しない
SIF MASTER >= 0 && !エナジードライブ
	CALL EVENTCHECK_M
;助手の素質変化 憑依中は変化しない
SIF ASSI >= 0 && !CFLAG:ASSI:憑依中
	CALL EVENTCHECK_A

;---------------------------------------------------------
;陥落素質 崩壊した奴隷は陥落しない アニマルも陥落しない
;---------------------------------------------------------
@EVENTCHECK_T
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC DEUIL
;陥落素質
IF !CFLAG:陥落 && !TALENT:崩壊 && !TALENT:アニマル
	;恋慕 = 従順、奉仕精神3以上+苦痛刻印Lv0+反発刻印Lv0+奉仕快楽経験200以上+依存度1000以上+主人調教経験500以上+助手調教経験500未満+異常経験2未満or増加なし
	{
	IF ABL:従順 >= 3 && ABL:奉仕精神 >= 3 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && EXP:奉仕快楽経験 >= 200 && CFLAG:依存度 >= 1000 && 
	   EXP:主人調教経験 >= 500 && EXP:助手調教経験 < 500 && (EXP:異常経験 < 2 || EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験")))
	}
		CALL KOJO_MESSAGE_EVENT, "恋慕"
		IF RELATION:(NO:MASTER) < 100
			RELATION:(NO:MASTER) = 200
		ELSE
			RELATION:(NO:MASTER) += 100
		ENDIF
		TALENT:恋慕 = 1
		FLAG:累計恋慕++
		CFLAG:陥落 = 1
		CALL 実績解除, "決めた 明日 好きって言うってね"
		CALL 経験値取得, TARGET, "陥落"
	;淫乱 = 欲望3以上+CVAB感覚合計12以上+快楽刻印Lv3+絶頂経験200以上+異常経験2以上+主人調教経験300以上+助手調教経験300以上
	{
	ELSEIF ABL:欲望 >= 3 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 12 && MARK:快楽刻印 == 3 && 
		   EXP:絶頂経験 >= 200 && EXP:異常経験 >= 2 && EXP:主人調教経験 >= 300 && EXP:助手調教経験 >= 300
	}
		CALL KOJO_MESSAGE_EVENT, "淫乱"
		TALENT:淫乱 = 1
		FLAG:累計淫乱++
		CFLAG:陥落 = 1
		CALL 経験値取得, TARGET, "陥落"
	;服従 = 従順、マゾっ気3以上+屈服or苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計200以上+依存度1000以上+助手調教経験500未満
	{
	ELSEIF ABL:従順 >= 3 && ABL:マゾっ気 >= 3 && (MARK:屈服刻印 == 3 || MARK:苦痛刻印 == 3) && 
		   MARK:反発刻印 == 0 && EXP:苦痛快楽経験+EXP:緊縛経験 >= 200 && CFLAG:依存度 >= 1000 && EXP:助手調教経験 < 500
	}
		CALL KOJO_MESSAGE_EVENT, "服従"
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 120
		;		RELATION:COUNT = 120
		;REND
		TALENT:服従 = 1
		FLAG:累計服従++
		CFLAG:陥落 = 1
		CALL 経験値取得, TARGET, "陥落"
	ENDIF
	IF CFLAG:陥落
		SIF SEX(MASTER) == 2 && CHARANAMEF(TARGET) == "識"
		    CALL 実績解除, "寂しげな薬指"
		ハットトリック:0 = 1
		SIF EXP:Ｖ経験 == 0 && EXP:Ａ経験 == 0 && EXP:性交経験 == 0
			CALL 実績解除, "前☆戯☆王"
	ENDIF
ENDIF

;上位陥落素質
IF !TALENT:親愛 && !TALENT:娼婦 && !TALENT:隷属 && !TALENT:崩壊 && !TALENT:アニマル
	;親愛 = 恋慕取得済み、従順、奉仕精神5以上+苦痛刻印Lv0+反発刻印Lv0+愛情経験200以上+奉仕快楽経験400以上+依存度3000以上+主人調教経験1000以上+助手調教経験1000未満+異常経験5未満or増加なし
	{
	IF TALENT:恋慕 && ABL:従順 >= 5 && ABL:奉仕精神 >= 5 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && 
	   EXP:愛情経験 >= 200 && EXP:奉仕快楽経験 >= 400 && CFLAG:依存度 >= 3000 && EXP:主人調教経験 > 1000 && 
	   EXP:助手調教経験 < 1000 && (EXP:異常経験 < 5 || EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験")))
	}
		CALL KOJO_MESSAGE_EVENT, "親愛"
		IF RELATION:(NO:MASTER) < 200
			RELATION:(NO:MASTER) = 300
		ELSE
			RELATION:(NO:MASTER) += 100
		ENDIF
		TALENT:恋慕 = 0
		TALENT:親愛 = 1
		;実績「理子に親愛を付ける」
		SIF CHARANAMEF(TARGET) == "理子"
			CALL 実績解除, "Affection"
	;娼婦 = 淫乱取得済み、欲望5以上+CVAB感覚合計20以上+反発刻印Lv0+絶頂経験500以上+異常経験5以上+主人調教経験500以上+助手調教経験500以上
	{
	ELSEIF TALENT:淫乱 && ABL:欲望 >= 5 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 20 && MARK:反発刻印 == 0 && 
		   EXP:絶頂経験 >= 500 && EXP:異常経験 >= 5 && EXP:主人調教経験 >= 500 && EXP:助手調教経験 >= 500
	}
		CALL KOJO_MESSAGE_EVENT, "娼婦"
		TALENT:淫乱 = 0
		TALENT:娼婦 = 1
	;隷属 = 服従取得済み、従順、マゾっ気5以上+屈服刻印Lv3+苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計500以上+依存度3000以上+助手調教経験1000未満+異常経験3以上
	{
	ELSEIF TALENT:服従 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && MARK:屈服刻印 == 3 && MARK:苦痛刻印 == 3 && MARK:反発刻印 == 0 && 
		   EXP:苦痛快楽経験+EXP:緊縛経験 >= 500 && CFLAG:依存度 >= 3000 && EXP:助手調教経験 < 1000 && EXP:異常経験 >= 3
	}
		CALL KOJO_MESSAGE_EVENT, "隷属"
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 150
		;		RELATION:COUNT = 150
		;REND
		TALENT:服従 = 0
		TALENT:隷属 = 1
	ENDIF
ENDIF

;---------------------------------------------------------
;特殊性感素質、一つしか取得はできない（重複した場合は上から優先）
;---------------------------------------------------------
IF !TALENT:淫核 && !TALENT:淫膣 && !TALENT:淫尻 && !TALENT:淫乳
	;淫核：Ｃ感覚6以上、露出癖4以上、自慰経験300以上、絶頂経験500以上
	;男なら加えてＡ感覚4以上、Ａ経験300以上必要
	IF ABL:Ｃ感覚 >= 6 && ABL:露出癖 >= 4 && EXP:自慰経験 >= 300 && EXP:絶頂経験 >= 500
		IF SEX(TARGET) == 1
			IF ABL:Ａ感覚 >= 3 && EXP:Ａ経験 >= 300
				CALL KOJO_MESSAGE_EVENT, "淫核"
				TALENT:淫核 = 1
			ENDIF
		ELSE
			CALL KOJO_MESSAGE_EVENT, "淫核"
			TALENT:淫核 = 1
		ENDIF
	;淫膣：Ｖ感覚6以上、性交中毒4以上、性交経験1000以上、絶頂経験500以上
	;男だと取得できない ふたなりならできる
	ELSEIF ABL:Ｖ感覚 >= 6 && ABL:性交中毒 >= 4 && EXP:性交経験 >= 1000 && EXP:絶頂経験 >= 500 && VAGINA(TARGET)
		CALL KOJO_MESSAGE_EVENT, "淫膣"
		TALENT:淫膣 = 1
	;淫尻：Ａ感覚6以上、性交中毒4以上、Ａ経験1000以上、絶頂経験500以上
	ELSEIF ABL:Ａ感覚 >= 6 && ABL:性交中毒 >= 4 && EXP:Ａ経験 >= 1000 && EXP:絶頂経験 >= 500
		CALL KOJO_MESSAGE_EVENT, "淫尻"
		TALENT:淫尻 = 1
	;淫乳：Ｂ感覚6以上、露出癖4以上噴乳経験300以上、絶頂経験500以上
	;男だと取得できない……？
	ELSEIF ABL:Ｂ感覚 >= 6 && ABL:露出癖 >= 4 && EXP:噴乳経験 >= 300 && EXP:絶頂経験 >= 500
		CALL KOJO_MESSAGE_EVENT, "淫乳"
		TALENT:淫乳 = 1
	ENDIF
ENDIF

;---------------------------------------------------------
;特殊性癖素質
;---------------------------------------------------------
;バストサイズ:0(すなわち男)のとき、B感覚Lv10以上かつメス度Lv7以上で胸が膨らむ
IF TALENT:バストサイズ == 0 && ABL:Ｂ感覚 >= 10 && ABL:メス度 >= 7
	CALL KOJO_MESSAGE_EVENT, "乳首開発"
	TALENT:バストサイズ = 2
ENDIF

;絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:残数 <= 0 && !TALENT:絶倫 && PENIS(TARGET) && FLAG:休憩フラグ == 0
	CALL KOJO_MESSAGE_EVENT, "絶倫"
	TALENT:絶倫 = 1
ENDIF

;サド：サドっ気Lv4以上、嗜虐快楽経験300以上、技巧Lv4以上
IF !TALENT:サド && ABL:サドっ気 >= 4 && EXP:嗜虐快楽経験 >= 300 && ABL:技巧 >= 4
	CALL KOJO_MESSAGE_EVENT, "サド"
	TALENT:サド = 1
	;実績「駄天使ちゃんにサドを付ける」
	SIF CHARANAMEF(TARGET) == "駄天使ちゃん"
		CALL 実績解除, "(天使じゃ)ないです"
ENDIF

;マゾ：マゾっ気Lv6以上
IF !TALENT:マゾ && ABL:マゾっ気 >= 6
	CALL KOJO_MESSAGE_EVENT, "マゾ"
	TALENT:マゾ = 1
ENDIF

;音ゲーマー：音ゲー経験100以上
IF !TALENT:音ゲーマー && EXP:音ゲー経験 >= 100
	CALL KOJO_MESSAGE_EVENT, "音ゲーマー"
	TALENT:音ゲーマー = 1
ENDIF

;魔術師：魔法技能3以上
IF ABL:魔法技能 >= 3 && !TALENT:魔術師
	CALL KOJO_MESSAGE_EVENT, "魔術師"
	TALENT:魔術師 = 1
	MAXBASE:魔力 = ABL:魔法技能*1000
	BASE:魔力 = MAXBASE:魔力
ENDIF

;流産による素質変動
IF TFLAG:流産
	CALL KOJO_MESSAGE_EVENT, "流産"
	TFLAG:流産 = 0
	;初流産による崩壊 [隷属]もしくは[崩壊]持ちなら回避できる
	IF !TALENT:隷属 && !TALENT:崩壊 && EXP:流産経験 == 0
		CFLAG:陥落 = 0
		REPEAT VARSIZE("TALENT")
			SELECTCASE TALENTNAME:COUNT
				CASE "恋慕", "淫乱", "服従", "親愛", "娼婦", "反抗的", "プライド高い", "生意気", "自制心", "好奇心", "楽観的"
					IF TALENT:COUNT
						PRINTFORMW %CALLNAME:TARGET%は【%TALENTNAME:COUNT%】を失った
						TALENT:COUNT = 0
					ENDIF
				CASE "崩壊", "無関心", "感情乏しい", "悲観的", "狂気"
					IF !TALENT:COUNT
						PRINTFORMW %CALLNAME:TARGET%は【%TALENTNAME:COUNT%】を得た
						TALENT:COUNT = 1
					ENDIF
			ENDSELECT
		REND
		CALL EXPUP, "異常経験", 3
	ENDIF
	CALL EXPUP, "流産経験", 1
	WAIT
	CALL CLEAR_FLAG
ENDIF

;---------------------------------------------------------
;主人の素質および能力
;---------------------------------------------------------
@EVENTCHECK_M
;主人の撮影技能：撮影経験が10、50、200、500、1000、1600、2300、3100、4000、5000のたびに上昇、Lv10まで
SELECTCASE ABL:MASTER:撮影技能
	CASE 0
		SIF EXP:MASTER:撮影経験 >= 10
			ABL:MASTER:撮影技能++
	CASE 1
		SIF EXP:MASTER:撮影経験 >= 50
			ABL:MASTER:撮影技能++
	CASE 2
		SIF EXP:MASTER:撮影経験 >= 200
			ABL:MASTER:撮影技能++
	CASE 3
		SIF EXP:MASTER:撮影経験 >= 500
			ABL:MASTER:撮影技能++
	CASE 4
		SIF EXP:MASTER:撮影経験 >= 1000
			ABL:MASTER:撮影技能++
	CASE 5
		SIF EXP:MASTER:撮影経験 >= 1600
			ABL:MASTER:撮影技能++
	CASE 6
		SIF EXP:MASTER:撮影経験 >= 2300
			ABL:MASTER:撮影技能++
	CASE 7
		SIF EXP:MASTER:撮影経験 >= 3100
			ABL:MASTER:撮影技能++
	CASE 8
		SIF EXP:MASTER:撮影経験 >= 4000
			ABL:MASTER:撮影技能++
	CASE 9
		SIF EXP:MASTER:撮影経験 >= 5000
			ABL:MASTER:撮影技能++
ENDSELECT

;主人の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:MASTER:残数 <= 0 && !TALENT:MASTER:絶倫 && PENIS(MASTER) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:MASTER%は【絶倫】を身につけた
	TALENT:MASTER:絶倫 = 1
ENDIF

;主人のマゾ：マゾっ気がLv4以上でサド 現在主人のマゾっ気は上がらないので取得できないはず
IF ABL:MASTER:マゾっ気 >= 4 && !TALENT:MASTER:マゾ
	PRINTFORMW %NAME:MASTER%は【マゾ】を身につけた
	TALENT:MASTER:マゾ = 1
ENDIF

;主人の魔術師 魔法技能3以上
IF ABL:MASTER:魔法技能 >= 3 && !TALENT:MASTER:魔術師
	PRINTFORMW %NAME:MASTER%は【魔術師】を身につけた
	TALENT:MASTER:魔術師 = 1
	MAXBASE:MASTER:魔力 = ABL:MASTER:魔法技能*1000
	BASE:MASTER:魔力 = MAXBASE:MASTER:魔力
ENDIF

;---------------------------------------------------------
;助手の素質および能力
;---------------------------------------------------------
@EVENTCHECK_A
;助手のサド：サドっ気がLv4以上、嗜虐快楽経験300以上でサド
IF ABL:ASSI:サドっ気 >= 4 && EXP:ASSI:嗜虐快楽経験 >= 300 && !TALENT:ASSI:サド
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は人を痛めつける喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【サド】を身につけた
	TALENT:ASSI:サド = 1
	;実績「駄天使ちゃんにサドを付ける」
	SIF CHARANAMEF(ASSI) == "駄天使ちゃん"
		CALL 実績解除, "(天使じゃ)ないです"
ENDIF

;助手のマゾ：マゾっ気がLv6以上でマゾ
IF ABL:ASSI:マゾっ気 >= 6 && !TALENT:ASSI:マゾ
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は体を痛めつけられる喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【マゾ】を身につけた
	TALENT:ASSI:マゾ = 1
ENDIF

;助手の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:ASSI:残数 <= 0 && !TALENT:ASSI:絶倫 && PENIS(ASSI) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:ASSI%は【絶倫】を身につけた
	TALENT:ASSI:絶倫 = 1
ENDIF


;助手にした際の素質変化
@ASSI_CHANGE
;[狐]で技巧LV5、Ｃ感覚LV5、処女なら妖狐化
IF TALENT:ASSI:狐 && ABL:ASSI:技巧 >= 5 && ABL:ASSI:Ｃ感覚 >= 5 && TALENT:ASSI:処女
	CALL CHECKDRAWLINE
	PRINTFORMW %CALLNAME:ASSI%は[妖狐]となった
	DRAWLINE
	TALENT:ASSI:狐 = 0
	TALENT:ASSI:妖狐 = 1
ENDIF
