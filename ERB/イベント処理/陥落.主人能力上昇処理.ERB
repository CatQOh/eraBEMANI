;素質変化イベント.ERB
@EVENTCHECK_ABL

IF !STRATEGY_TURNEND
	;奴隷の素質変化
	SIF TARGET >= 0
		CALL EVENTCHECK_T
	;調教者の素質変化
	SIF MASTER >= 0
		CALL EVENTCHECK_M
	;助手の素質変化
	SIF ASSI >= 0
		CALL EVENTCHECK_A
ENDIF

SIF STRATEGY_TURNEND
	STRATEGY_TURNEND = 0


;---------------------------------------------------------
;陥落素質 崩壊した奴隷は陥落しない
;---------------------------------------------------------
@EVENTCHECK_T
;陥落素質
IF !CFLAG:陥落 && !TALENT:崩壊
	;恋慕 = 従順、奉仕精神3以上+苦痛刻印Lv0+反発刻印Lv0+奉仕快楽経験200以上+依存度1000以上+主人調教経験500以上+助手調教経験500未満+異常経験2未満or増加なし
	IF ABL:従順 >= 3 && ABL:奉仕精神 >= 3 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && EXP:奉仕快楽経験 >= 200 && CFLAG:依存度 >= 1000 && EXP:主人調教経験 >= 500 && EXP:助手調教経験 < 500 && ( EXP:異常経験 < 2 || EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験")) )
		CALL KOJO_MESSAGE_EVENT, 30
		IF RELATION:(NO:MASTER) < 100
			RELATION:(NO:MASTER) = 200
		ELSE
			RELATION:(NO:MASTER) += 100
		ENDIF
		TALENT:恋慕 = 1
		FLAG:累計恋慕++
		CFLAG:陥落 = 1
	;淫乱 = 欲望3以上+CVAB感覚合計12以上+快楽刻印Lv3+絶頂経験200以上+異常経験2以上+主人調教経験300以上+助手調教経験300以上
	ELSEIF ABL:欲望 >= 3 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 12 && MARK:快楽刻印 == 3 && EXP:絶頂経験 >= 200 && EXP:異常経験 >= 2 && EXP:主人調教経験 >= 300 && EXP:助手調教経験 >= 300
		CALL KOJO_MESSAGE_EVENT, 31
		TALENT:淫乱 = 1
		FLAG:累計淫乱++
		CFLAG:陥落 = 1
	;服従 = 従順、マゾっ気3以上+屈服or苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計200以上+依存度1000以上+助手調教経験500未満
	ELSEIF ABL:従順 >= 3 && ABL:マゾっ気 >= 3 && (MARK:屈服刻印 == 3 || MARK:苦痛刻印 == 3) && MARK:反発刻印 == 0 && EXP:苦痛快楽経験+EXP:緊縛経験 >= 200 && CFLAG:依存度 >= 1000 && EXP:助手調教経験 < 500
		CALL KOJO_MESSAGE_EVENT, 32
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 120
		;		RELATION:COUNT = 120
		;REND
		TALENT:服従 = 1
		FLAG:累計服従++
		CFLAG:陥落 = 1
	ENDIF
ENDIF

;上位陥落素質
IF TALENT:親愛== 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0 && TALENT:崩壊 == 0
	;親愛 = 恋慕取得済み、従順、奉仕精神5以上+苦痛刻印Lv0+反発刻印Lv0+愛情経験200以上+奉仕快楽経験400以上+依存度3000以上+主人調教経験1000以上+助手調教経験1000未満+異常経験5未満or増加なし
	IF TALENT:恋慕 && ABL:従順 >= 5 && ABL:奉仕精神 >= 5 && MARK:苦痛刻印 == 0 && MARK:反発刻印 == 0 && EXP:愛情経験 >= 200 && EXP:奉仕快楽経験 >= 400 && CFLAG:依存度 >= 3000 && EXP:主人調教経験 > 1000 && EXP:助手調教経験 < 1000 && ( EXP:異常経験 < 5 || EXP:異常経験 == CSVEXP(NO:TARGET, GETNUM(EXP, "異常経験")) )
		CALL KOJO_MESSAGE_EVENT, 33
		IF RELATION:(NO:MASTER) < 200
			RELATION:(NO:MASTER) = 300
		ELSE
			RELATION:(NO:MASTER) += 100
		ENDIF
		TALENT:恋慕 = 0
		TALENT:親愛 = 1
		;実績No.69「理子に親愛を付ける」
		CALL GET_ACV_69
	;娼婦 = 淫乱取得済み、欲望5以上+CVAB感覚合計20以上+反発刻印Lv0+絶頂経験500以上+異常経験5以上+主人調教経験500以上+助手調教経験500以上
	ELSEIF TALENT:淫乱 && ABL:欲望 >= 5 && ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 20 && MARK:反発刻印 == 0 && EXP:絶頂経験 >= 500 && EXP:異常経験 >= 5 && EXP:主人調教経験 >= 500 && EXP:助手調教経験 >= 500
		CALL KOJO_MESSAGE_EVENT, 34
		TALENT:淫乱 = 0
		TALENT:娼婦 = 1
	;隷属 = 服従取得済み、従順、マゾっ気5以上+屈服刻印Lv3+苦痛刻印Lv3+反発刻印Lv0+苦痛快楽経験と緊縛経験の合計500以上+依存度3000以上+助手調教経験1000未満+異常経験3以上
	ELSEIF TALENT:服従 && ABL:従順 >= 5 && ABL:マゾっ気 >= 5 && MARK:屈服刻印 == 3 && MARK:苦痛刻印 == 3 && MARK:反発刻印 == 0 && EXP:苦痛快楽経験+EXP:緊縛経験 >= 500 && CFLAG:依存度 >= 3000 && EXP:助手調教経験 < 1000 && EXP:異常経験 >= 3
		CALL KOJO_MESSAGE_EVENT, 35
		;一律相性変更は廃止 調教開始時に逐次変更するように
		;REPEAT 10000
		;	SIF RELATION:COUNT < 150
		;		RELATION:COUNT = 150
		;REND
		TALENT:服従 = 0
		TALENT:隷属 = 1
	ENDIF
ENDIF

;---------------------------------------------------------
;特殊性感素質、一つしか取得はできない（重複した場合は上から優先）
;---------------------------------------------------------
IF !TALENT:自慰狂い && !TALENT:セックス狂 && !TALENT:尻穴狂い && !TALENT:淫乳
	;自慰狂い：Ｃ感覚4以上、自慰経験100以上、絶頂経験150以上
	;男なら加えてＡ感覚3以上、Ａ経験100以上必要
	IF ABL:Ｃ感覚 >= 4 && EXP:自慰経験 >= 100 && EXP:絶頂経験 >= 150
		IF SEX(TARGET) == 1
			IF ABL:Ａ感覚 >= 3 && EXP:Ａ経験 >= 100
				CALL KOJO_MESSAGE_EVENT, 36
				TALENT:自慰狂い = 1
			ENDIF
		ELSE
			CALL KOJO_MESSAGE_EVENT, 36
			TALENT:自慰狂い = 1
		ENDIF
	;セックス狂：Ｖ感覚4以上、性交経験300以上、絶頂経験150以上
	;男だと取得できない ふたなりならできる
	ELSEIF ABL:Ｖ感覚 >= 4 && EXP:性交経験 >= 300 && EXP:絶頂経験 >= 150
		CALL KOJO_MESSAGE_EVENT, 37
		TALENT:セックス狂 = 1
	;尻穴狂い：Ａ感覚4以上、Ａ経験300以上、絶頂経験150以上
	;男なら加え精液経験150以上必要
	ELSEIF ABL:Ａ感覚 >= 4 && EXP:Ａ経験 >= 300 && EXP:絶頂経験 >= 150
		IF SEX(TARGET) == 1
			IF EXP:精液経験 >= 150
				CALL KOJO_MESSAGE_EVENT, 38
				TALENT:尻穴狂い = 1
			ENDIF
		ELSE
			CALL KOJO_MESSAGE_EVENT, 38
			TALENT:尻穴狂い = 1
		ENDIF
	;淫乳：Ｂ感覚4以上、噴乳経験100以上、絶頂経験150以上
	;男だと取得できない……？
	ELSEIF ABL:Ｂ感覚 >= 4 && EXP:噴乳経験 >= 100 && EXP:絶頂経験 >= 150
		CALL KOJO_MESSAGE_EVENT, 39
		TALENT:淫乳 = 1
	ENDIF
ENDIF

;---------------------------------------------------------
;特殊性癖素質
;---------------------------------------------------------
;絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:残数 <= 0 && !TALENT:絶倫 && PENIS(TARGET) && FLAG:休憩フラグ == 0
	CALL KOJO_MESSAGE_EVENT, 40
	TALENT:絶倫 = 1
ENDIF

;サド：サドっ気Lv4以上、嗜虐快楽経験300以上、技巧Lv4以上
IF !TALENT:サド && ABL:サドっ気 >= 4 && EXP:嗜虐快楽経験 >= 300 && ABL:技巧 >= 4
	CALL KOJO_MESSAGE_EVENT, 41
	TALENT:サド = 1
	;実績No.70「駄天使ちゃんにサドを付ける」
	CALL GET_ACV_70, TARGET
ENDIF

;マゾ：マゾっ気Lv6以上
IF !TALENT:マゾ && ABL:マゾっ気 >= 6
	CALL KOJO_MESSAGE_EVENT, 42
	TALENT:マゾ = 1
ENDIF

;流産による素質変動
IF TFLAG:流産
	CALL KOJO_MESSAGE_EVENT, 43
	TFLAG:流産 = 0
	;初流産による崩壊 [隷属]もしくは[崩壊]持ちなら回避できる
	IF !TALENT:隷属 && !TALENT:崩壊 && EXP:流産経験 == 0
		CFLAG:陥落 = 0
		IF TALENT:恋慕
			PRINTFORMW %CALLNAME:TARGET%は【恋慕】を失った
			TALENT:恋慕 = 0
		ENDIF
		IF TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%は【淫乱】を失った
			TALENT:淫乱 = 0
		ENDIF
		IF TALENT:服従
			PRINTFORMW %CALLNAME:TARGET%は【服従】を失った
			TALENT:服従 = 0
		ENDIF
		IF TALENT:親愛
			PRINTFORMW %CALLNAME:TARGET%は【親愛】を失った
			TALENT:親愛 = 0
		ENDIF
		IF TALENT:娼婦
			PRINTFORMW %CALLNAME:TARGET%は【娼婦】を失った
			TALENT:娼婦 = 0
		ENDIF
		IF TALENT:崩壊 == 0
			PRINTFORMW %CALLNAME:TARGET%は【崩壊】を得た
			TALENT:崩壊 = 1
		ENDIF
		IF TALENT:反抗的
			PRINTFORMW %CALLNAME:TARGET%は【反抗的】を失った
			TALENT:反抗的 = 0
		ENDIF
		IF TALENT:プライド高い
			PRINTFORMW %CALLNAME:TARGET%は【プライド高い】を失った
			TALENT:プライド高い = 0
		ENDIF
		IF TALENT:生意気
			PRINTFORMW %CALLNAME:TARGET%は【生意気】を失った
			TALENT:生意気 = 0
		ENDIF
		IF TALENT:自制心
			PRINTFORMW %CALLNAME:TARGET%は【自制心】を失った
			TALENT:自制心 = 0
		ENDIF
		IF TALENT:無関心 == 0
			PRINTFORMW %CALLNAME:TARGET%は【無関心】を得た
			TALENT:無関心 = 1
		ENDIF
		IF TALENT:感情乏しい == 0
			PRINTFORMW %CALLNAME:TARGET%は【感情乏しい】を得た
			TALENT:感情乏しい = 1
		ENDIF
		IF TALENT:好奇心
			PRINTFORMW %CALLNAME:TARGET%は【好奇心】を失った
			TALENT:好奇心 = 0
		ENDIF
		IF TALENT:楽観的
			PRINTFORMW %CALLNAME:TARGET%は【楽観的】を失った
			TALENT:楽観的 = 0
		ENDIF
		IF TALENT:悲観的 == 0
			PRINTFORMW %CALLNAME:TARGET%は【悲観的】を得た
			TALENT:悲観的 = 1
		ENDIF
		IF TALENT:狂気 == 0
			PRINTFORMW %CALLNAME:TARGET%は【狂気】を得た
			TALENT:狂気 = 1
		ENDIF
		PRINTW 異常経験+3
		EXP:異常経験 += 3
	ENDIF
	PRINTW 流産経験+1
	EXP:流産経験++
	CALL CLEAR_FLAG
ENDIF

;---------------------------------------------------------
;主人の素質および能力
;---------------------------------------------------------
@EVENTCHECK_M
;主人の感度：各経験が20、100、250、400、600のたびに上昇、Lv5まで
IF (EXP:MASTER:Ｖ経験 >= 600 && ABL:MASTER:Ｖ感覚 <= 4) || (EXP:MASTER:Ｖ経験 >= 400 && ABL:MASTER:Ｖ感覚 <= 3) || (EXP:MASTER:Ｖ経験 >= 250 && ABL:MASTER:Ｖ感覚 <= 2) || (EXP:MASTER:Ｖ経験 >= 100 && ABL:MASTER:Ｖ感覚 <= 1) || (EXP:MASTER:Ｖ経験 >= 20 && ABL:MASTER:Ｖ感覚 <= 0)
	ABL:MASTER:Ｖ感覚++
	PRINTFORMW %NAME:MASTER%のＶ感覚が{ABL:MASTER:Ｖ感覚}に上がった
ENDIF
IF (EXP:MASTER:Ａ経験 >= 600 && ABL:MASTER:Ａ感覚 <= 4) || (EXP:MASTER:Ａ経験 >= 400 && ABL:MASTER:Ａ感覚 <= 3) || (EXP:MASTER:Ａ経験 >= 250 && ABL:MASTER:Ａ感覚 <= 2) || (EXP:MASTER:Ａ経験 >= 100 && ABL:MASTER:Ａ感覚 <= 1) || (EXP:MASTER:Ａ経験 >= 20 && ABL:MASTER:Ａ感覚 <= 0)
	ABL:MASTER:Ａ感覚++
	PRINTFORMW %NAME:MASTER%のＡ感覚が{ABL:MASTER:Ａ感覚}に上がった
ENDIF
;主人のＣ感覚：射精回数、絶頂回数で上昇 LV5まで
IF (EXP:MASTER:射精経験 >= 600 && ABL:MASTER:Ｃ感覚 <= 4) || (EXP:MASTER:射精経験 >= 400 && ABL:MASTER:Ｃ感覚 <= 3) || (EXP:MASTER:射精経験 >= 250 && ABL:MASTER:Ｃ感覚 <= 2) || (EXP:MASTER:射精経験 >= 100 && ABL:MASTER:Ｃ感覚 <= 1) || (EXP:MASTER:射精経験 >= 20 && ABL:MASTER:Ｃ感覚 <= 0)
	ABL:MASTER:Ｃ感覚++
	PRINTFORMW %NAME:MASTER%のＣ感覚が{ABL:MASTER:Ｃ感覚}に上がった
ENDIF
IF (EXP:MASTER:絶頂経験 >= 600 && ABL:MASTER:Ｃ感覚 <= 4) || (EXP:MASTER:絶頂経験 >= 400 && ABL:MASTER:Ｃ感覚 <= 3) || (EXP:MASTER:絶頂経験 >= 250 && ABL:MASTER:Ｃ感覚 <= 2) || (EXP:MASTER:絶頂経験 >= 100 && ABL:MASTER:Ｃ感覚 <= 1) || (EXP:MASTER:絶頂経験 >= 20 && ABL:MASTER:Ｃ感覚 <= 0)
	ABL:MASTER:Ｃ感覚++
	PRINTFORMW %NAME:MASTER%のＣ感覚が{ABL:MASTER:Ｃ感覚}に上がった
ENDIF

;主人の技巧：調教経験が50、400、800、3000、10000のたびに上昇、Lv5まで
IF (EXP:MASTER:調教経験 >= 10000 && ABL:MASTER:技巧 <= 4) || (EXP:MASTER:調教経験 >= 3000 && ABL:MASTER:技巧 <= 3) || (EXP:MASTER:調教経験 >= 800 && ABL:MASTER:技巧 <= 2) || (EXP:MASTER:調教経験 >= 400 && ABL:MASTER:技巧 <= 1) || (EXP:MASTER:調教経験 >= 50 && ABL:MASTER:技巧 <= 0)
	ABL:MASTER:技巧++
	PRINTFORMW %NAME:MASTER%の技巧が{ABL:MASTER:技巧}に上がった
ENDIF

;主人のサドっ気：嗜虐快楽経験が30、200、500、1000、3000のたびに上昇、Lv5まで
IF (EXP:MASTER:嗜虐快楽経験 >= 3000 && ABL:MASTER:サドっ気 <= 4) || (EXP:MASTER:嗜虐快楽経験 >= 1000 && ABL:MASTER:サドっ気 <= 3) || (EXP:MASTER:嗜虐快楽経験 >= 500 && ABL:MASTER:サドっ気 <= 2) || (EXP:MASTER:嗜虐快楽経験 >= 200 && ABL:MASTER:サドっ気 <= 1) || (EXP:MASTER:嗜虐快楽経験 >= 30 && ABL:MASTER:サドっ気 <= 0)
	ABL:MASTER:サドっ気++
	PRINTFORMW %NAME:MASTER%のサドっ気が{ABL:MASTER:サドっ気}に上がった
ENDIF

;主人の撮影技能：撮影経験が10、50、200、500、1000のたびに上昇、Lv5まで
IF (EXP:MASTER:撮影経験 >= 1000 && ABL:MASTER:撮影技能 <= 4) || (EXP:MASTER:撮影経験 >= 500 && ABL:MASTER:撮影技能 <= 3) || (EXP:MASTER:撮影経験 >= 200 && ABL:MASTER:撮影技能 <= 2) || (EXP:MASTER:撮影経験 >= 50 && ABL:MASTER:撮影技能 <= 1) || (EXP:MASTER:撮影経験 >= 10 && ABL:MASTER:撮影技能 <= 0)
	ABL:MASTER:撮影技能++
	PRINTFORMW %NAME:MASTER%の撮影技能が{ABL:MASTER:撮影技能}に上がった
ENDIF

;主人の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:MASTER:残数 <= 0 && !TALENT:MASTER:絶倫 && PENIS(MASTER) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:MASTER%は【絶倫】を身につけた
	TALENT:MASTER:絶倫 = 1
ENDIF

;主人の謎の魅力：恋慕･服従にした奴隷の数が5人以上
IF (FLAG:累計恋慕+FLAG:累計服従) >= 5 && !TALENT:MASTER:謎の魅力
	PRINTFORMW %NAME:MASTER%は【謎の魅力】を身につけた
	TALENT:MASTER:謎の魅力 = 1
ENDIF

;主人のサド：サドっ気がLv4以上でサド
IF ABL:MASTER:サドっ気 >= 4 && !TALENT:MASTER:サド
	PRINTFORMW %NAME:MASTER%は【サド】を身につけた
	TALENT:MASTER:サド = 1
	;実績No.70「駄天使ちゃんにサドを付ける」
	CALL GET_ACV_70, MASTER
ENDIF

;主人のマゾ：マゾっ気がLv4以上でサド 現在主人のマゾっ気は上がらないので取得できないはず
IF ABL:MASTER:マゾっ気 >= 4 && !TALENT:MASTER:マゾ
	PRINTFORMW %NAME:MASTER%は【マゾ】を身につけた
	TALENT:MASTER:マゾ = 1
ENDIF

;---------------------------------------------------------
;助手の素質および能力
;---------------------------------------------------------
@EVENTCHECK_A
;助手のサド：サドっ気がLv4以上、嗜虐快楽経験300以上でサド
IF ABL:ASSI:サドっ気 >= 4 && EXP:ASSI:嗜虐快楽経験 >= 300 && !TALENT:ASSI:サド
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は人を痛めつける喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【サド】を身につけた
	TALENT:ASSI:サド = 1
	;実績No.70「駄天使ちゃんにサドを付ける」
	CALL GET_ACV_70, ASSI
ENDIF

;助手のマゾ：マゾっ気がLv6以上でマゾ
IF ABL:ASSI:マゾっ気 >= 6 && !TALENT:ASSI:マゾ
	PRINTFORMW %CALLNAME:ASSI%の様子がおかしい
	PRINTFORML %CALLNAME:ASSI%は体を痛めつけられる喜びに目覚めたようだ
	PRINTFORMW %NAME:ASSI%は【マゾ】を身につけた
	TALENT:ASSI:マゾ = 1
ENDIF

;助手の絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:ASSI:残数 <= 0 && !TALENT:ASSI:絶倫 && PENIS(ASSI) && FLAG:休憩フラグ == 0
	PRINTFORMW %NAME:ASSI%は【絶倫】を身につけた
	TALENT:ASSI:絶倫 = 1
ENDIF


;助手にした際の素質変化
@ASSI_CHANGE
;[狐]で技巧LV5、Ｃ感覚LV5、処女なら妖狐化
IF TALENT:ASSI:狐 && ABL:ASSI:技巧 >= 5 && ABL:ASSI:Ｃ感覚 >= 5 && TALENT:ASSI:処女
	DRAWLINE
	PRINTFORMW %CALLNAME:ASSI%は[妖狐]となった
	DRAWLINE
	TALENT:ASSI:狐 = 0
	TALENT:ASSI:妖狐 = 1
ENDIF


;---------------------------------------------------------
;能力上昇に伴う実績取得の処理
;---------------------------------------------------------
@GET_ACV_69
;実績No.69「理子に親愛を付ける」
IF NAME:TARGET == "理子"
	実績:69 = 1
	SAVEGLOBAL
ENDIF


@GET_ACV_70, ARG
;実績No.70「駄天使ちゃんにサドを付ける」
IF NAME:ARG == "駄天使ちゃん"
	実績:70 = 1
	SAVEGLOBAL
ENDIF
