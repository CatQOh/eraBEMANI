@キャラステータス最適化, 対象 = -1
#DIM DYNAMIC 対象
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点
#DIM DYNAMIC LCOUNT

IF 対象 == -1
	始点 = 0
	終点 = CHARANUM
ELSE
	始点 = 対象
	終点 = 対象+1
ENDIF

FOR LCOUNT, 始点, 終点
	;CSVを確認する
	SIF !EXISTCSV(NO:LCOUNT) && NO:LCOUNT < 10000
		PRINTFORMW %NAME:LCOUNT%のCSVが存在しません。ゲームを続行するとエラーが起こる場合があります
	;ジェントルストレスの発動中はストレスが上がらない
	IF ジェントルストレス:LCOUNT:0
		ジェントルストレス:LCOUNT:1 = MIN(CFLAG:LCOUNT:ストレス値, ジェントルストレス:LCOUNT:1)
		CFLAG:LCOUNT:ストレス値 = ジェントルストレス:LCOUNT:1
	ENDIF
	;グリムキャラの接頭語
	IF CFLAG:LCOUNT:グリム
		SELECTCASE FLAG:グリムコンフィグ
			CASE 0
				CALLNAME:LCOUNT = %CSVCALLNAME(NO:LCOUNT)%
				CSTR:LCOUNT:英語呼び名 = %CSVCSTR(NO:LCOUNT, GETNUM(CSTR, "英語呼び名"))%
			CASE 1
				CALLNAME:LCOUNT = グリム・%CSVCALLNAME(NO:LCOUNT)%
				CSTR:LCOUNT:英語呼び名 = Grim %CSVCSTR(NO:LCOUNT, GETNUM(CSTR, "英語呼び名"))%
		ENDSELECT
	ENDIF
	;魔術師は魔法技能3が必須
	IF TALENT:LCOUNT:魔術師
		SIF ABL:LCOUNT:魔法技能 < 3
			ABL:LCOUNT:魔法技能 = 3
		MAXBASE:LCOUNT:魔力 = ABL:LCOUNT:魔法技能*1000
		BASE:LCOUNT:魔力 = LIMIT(BASE:LCOUNT:魔力, 0, MAXBASE:LCOUNT:魔力)
	ENDIF
	;なんらかの原因で鳳凰じゃないキャラに鳳凰用ステータスが設定されていた場合
	IF CSTR:LCOUNT:種族 != "鳳凰" && MAXBASE:LCOUNT:鳳凰基礎体力
		MAXBASE:LCOUNT:体力 = MAXBASE:LCOUNT:鳳凰基礎体力
		MAXBASE:LCOUNT:気力 = MAXBASE:LCOUNT:鳳凰基礎気力
		MAXBASE:LCOUNT:鳳凰基礎体力 = 0
		MAXBASE:LCOUNT:鳳凰基礎気力 = 0
	ENDIF
	;陥落済みなら陥落済みフラグをオン
	IF FALLTYPE(LCOUNT) != 0
		CFLAG:LCOUNT:陥落 = 1
	ELSE
		CFLAG:LCOUNT:陥落 = 0
	ENDIF
NEXT
