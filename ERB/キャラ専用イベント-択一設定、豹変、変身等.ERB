
;-------------------------------------------------
;キャラを追加した際に編集する部分
;-------------------------------------------------

;;;同一人物などの片方しか奴隷にできない択一キャラの名前を返す 複数いる場合/区切りになるためSPLIT必須
;;;択一設定を追加する場合はこの関数の中に記述すること
@択一キャラ, キャラ名
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC キャラ名
{
#DIMS DYNAMIC 択一キャラリスト = 
"マルクト", "ア・ミリア", 
"芽兎 めう", "白兎先生", 
"DJつよし", "OJつよし", 
"テスタメントネメシス", "ミィム・ミクラー", 
"ジュナ・サラキア", "ジュナ・フェリクス", 
"MIR-202【アルテミス・レナ】", "レナ・イシュメイル",
"レウィシア・アクアブルー", "ブルーベリー",
}
;↑1:1用 二人並べて記述する

FOR LCOUNT, 0, VARSIZE("択一キャラリスト")
	SIF 択一キャラリスト:LCOUNT != キャラ名
		CONTINUE
	RETURNF 択一キャラリスト:(LCOUNT/2*2+!(LCOUNT%2))
NEXT
;2:1や1:1:1は個別
SELECTCASE キャラ名
	;ニア・ユーディット+バテシバ・アヒトフェル:ニア・バテシバ
	CASE "ニア・バテシバ"
		RETURNF "ニア・ユーディット/バテシバ・アヒトフェル"
	CASE "ニア・ユーディット", "バテシバ・アヒトフェル"
		RETURNF "ニア・バテシバ"
	;マリィ:レイヴガール:トルマリン
	CASE "マリィ"
		RETURNF "レイヴガール/トルマリン"
	CASE "レイヴガール"
		RETURNF "マリィ/トルマリン"
	CASE "トルマリン"
		RETURNF "マリィ/レイヴガール"
ENDSELECT
RETURNF ""


;;;豹変キャラの追加はこの関数内と下のTRANCE_ONだけで完結する
;;;追加する場合この関数に豹変条件を設定すること（TRANCE_ONも忘れずに見る）
@豹変イベント, オン
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC オン
#DIM DYNAMIC 対象
#DIM DYNAMIC 豹変フラグ

FOR LCOUNT, 0, 3
	豹変フラグ = 0
	SELECTCASE LCOUNT
		CASE 0
			対象 = TARGET
		CASE 1
			対象 = MASTER
		CASE 2
			対象 = ASSI
	ENDSELECT
	SIF 対象 == -1
		CONTINUE

	IF オン && 豹変可能(対象) && !TRANCEFLAG:対象:豹変中
		;キャラごとの条件を設定
		SELECTCASE CHARANAMEF(対象)
			CASE "紅刃"
				SIF RANDPER(20)
					豹変フラグ = 1
			CASE "青雨 冷音"
				SIF GROUPMATCH(天気(), "雨", "大雨", "雷雨")
					豹変フラグ = 1
			CASE "シュープリス"
				SIF RANDPER(50)
					豹変フラグ = 1
			CASE "クーデルカ・プルミエール"
				SIF RANDPER(30)
					豹変フラグ = 1
		ENDSELECT
		IF 豹変フラグ
			SELECTCASE CHARANAMEF(対象)
				CASE "シュープリス"
					PRINTFORMW ＜%CALLNAME:対象%は記憶を思い出そうと同時に湧き上がる破壊衝動で性格が豹変した……＞
				CASE "クーデルカ・プルミエール"
					PRINTFORMW ＜%CALLNAME:対象%は突如意識を失い、しばらくすると別人格が現れた……＞
				CASEELSE
					PRINTFORMW ＜%CALLNAME:対象%は性格が豹変した＞
			ENDSELECT
			CALL TRANCE_ON, 対象
			TRANCEFLAG:対象:豹変中 = 1
		ENDIF
	ELSEIF TRANCEFLAG:対象:豹変中
		SELECTCASE CHARANAMEF(対象)
			CASE ""
			CASEELSE
				PRINTFORMW ＜%CALLNAME:対象%は元の性格に戻った＞
		ENDSELECT
		;冷音は気力を元に戻す
		SIF CHARANAMEF(対象) == "青雨 冷音"
			MAXBASE:対象:気力 = TRANCEFLAG:対象:気力保存
		;変更されている素質を全て元に戻す 今のところ他の素質変化システム（獣変化とミゼランとハート）とは競合しないので不都合はないはず
		CALL ALLTRANCEOFF, 対象
		TRANCEFLAG:対象:豹変中 = 0
	ENDIF
	CALL CHECKDRAWLINE
NEXT


;;;豹変キャラを追加する場合はこの関数内で素質を設定すること（豹変イベントも忘れずに見る）
@TRANCE_ON, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
SELECTCASE CHARANAMEF(対象)
	CASE "紅刃"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "臆病", "大人しい", "保守的", "一線越えない", "貞操観念", "痛みに弱い", "濡れにくい", "汚臭敏感", "献身的"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "気丈", "好奇心", "楽観的", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "倒錯的", "両刀", "サド"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
	CASE "青雨 冷音"
		;雨が降ると気力にボーナス 無条件に700増えるほかに、Y=1000/XのグラフでのX=1～X=[雨量]までの面積
		;つまり1000LOG([雨量])だけ増加する ただし、整数への切り捨ての関係で100LOG([雨量]^10)としている
		;雨量に対する気力の参考値 雨量1:1700 雨量2:2300 雨量3:2700 雨量4:3000 雨量5:3300 雨量10:4000 雨量15:4400 雨量20:4600 雨量78以上:6000
		TRANCEFLAG:対象:気力保存 = MAXBASE:対象:気力
		MAXBASE:対象:気力 += 700
		MAXBASE:対象:気力 += 100*LOG(POWER(LIMIT(FLAG:降水量, 1, 78), 10))
		BASE:対象:気力 = MAXBASE:対象:気力

		;以下素質処理
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "大人しい", "無関心", "一線越えない", "貞操観念", "恥じらい"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "生意気", "反抗的", "衝動的", "プライド高い", "好奇心", "目立ちたがり", "貞操無頓着", "恥薄い", "痛みに強い", "濡れやすい", "汚臭鈍感", "汚れ無視", "快感に素直", "倒錯的", "両刀", "サド"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
	CASE "シュープリス"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "臆病", "プライド低い", "弱気", "寂しがり屋", "自制心", "保守的", "悲観的", "抑圧", "恥じらい", "痛みに弱い", "回復遅い", "習得遅い"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "サド", "無関心", "強気", "痛みに強い", "好奇心", "生意気", "倒錯的", "戦士"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
	CASE "クーデルカ・プルミエール"
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SELECTCASE TALENTNAME:LCOUNT
				;消滅する素質
				CASE "プライド低い", "大人しい","弱気", "悲観的"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 0
				;習得する素質
				CASE "強気", "冷静", "気丈", "戦士", "汚臭鈍感", "抵抗", "一線越えない"
					CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 1
			ENDSELECT
		NEXT
ENDSELECT


;;;種族嫌悪による相性低下を無効化する組み合わせがある場合ここに追記する
@種族嫌悪例外処理, 対象, 相手
#FUNCTION
#DIMS DYNAMIC 対象
#DIMS DYNAMIC 相手
SIF 対象 == "マッドラット" && 相手 != "ハート"
	RETURNF 1


;;;恋心や既婚者の先が不明かつ固有名が存在する場合はここに任意で記述（csvに入れていいかも）
@恋心名, 対象
#FUNCTIONS
#DIM DYNAMIC 対象
IF (TALENT:対象:恋心 && CSTR:対象:恋心 == "不明") || (TALENT:対象:既婚者 && CSTR:対象:既婚者 == "不明")
	SELECTCASE CSVNAME(NOF(対象))
		CASE "マゼラン・マゼラン"
			RETURNF "フェルディナンド＝カジキ先輩"
		CASE "カゲトラ"
			RETURNF "雪姫"
		CASE "怪盗いいんちょ"
			RETURNF "同じクラスの名探偵"
		CASE "天乃 探女"
			RETURNF "金剛様"
		CASE "モアイよし子"
			RETURNF "モアイよし夫"
		CASE "ジュナ・サラキア", "ジュナ・フェリクス"
			RETURNF "ギュスターブ"
		CASEELSE
			IF TALENT:対象:恋心
				RETURNF @"%NAME:対象%の想い人"
			ELSE
				RETURNF @"%NAME:対象%の結婚相手"
			ENDIF
	ENDSELECT
ELSEIF TALENT:対象:恋心
	RETURNF CSTR:対象:恋心
ELSEIF TALENT:対象:既婚者
	RETURNF CSTR:対象:既婚者
ELSE
	RETURNF ""
ENDIF



;-------------------------------------------------
;SHOP画面からの変化
;-------------------------------------------------

@変化
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET

DRAWLINE
PRINTL □変化させるキャラを選んでください
FOR LCOUNT, 1, CHARANUM
	SIF !TALENT:LCOUNT:変化
		CONTINUE
	SIF CFLAG:LCOUNT:キャラロスト
		CONTINUE
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	SIF CFLAG:LCOUNT:監禁
		CONTINUE
	IF 陥落(LCOUNT)
		SELECTCASE CHARANAMEF(LCOUNT)
			CASE "ミゼラン・ミゼラン"
				IF TALENT:LCOUNT:妊娠
					CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 現在：%ミゼラン変身先名()%に変身中(妊娠中は変身できません)", "gray"
				ELSEIF TRANCEFLAG:LCOUNT:ミゼラン専用 < 0
					CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 現在：%ミゼラン変身先名()%に変身中(休憩中です)", "gray"
				ELSE
					PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：%ミゼラン変身先名()%に変身中
				ENDIF
			CASEELSE
				PRINTFORML 　[{LCOUNT, 2}] %NAME:LCOUNT% 現在：\@ TALENT:LCOUNT:アニマル ? 獣 # 人間 \@\@ TRANCEFLAG:LCOUNT:獣変化 ? に変身中 # の姿 \@
		ENDSELECT
	ELSE
		CALL PRINTCOLORL, @"　[--] %NAME:LCOUNT% 未陥落", "gray"
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　変化　
PRINTL [0] 戻る

BINPUT
IF !RESULT
	RETURN
ELSE
	;TRANCE変数を使うのは変化後に付いた素質を維持するため
	LTARGET = TARGET
	TARGET = RESULT
	;一部キャラは専用の挙動
	SELECTCASE CHARANAMEF(TARGET)
		CASE "ミゼラン・ミゼラン"
			;ミゼランは今いるキャラの知り合い（全キャラ選べると面倒）に変身する
			IF !TRANCEFLAG:ミゼラン専用
				CALL ミゼラン変身先選択
			ELSE
				CALL ミゼラン変身解除
			ENDIF
		CASEELSE
			;人間←→アニマルに変化
			IF !TRANCEFLAG:獣変化
				PRINTFORMW ＜%CALLNAME:TARGET%は獣の姿に変化した＞
				CALL TRANCETALENT, TARGET, "アニマル", 1
				CALL TRANCETALENT, TARGET, "ファーリー", 1
				CALL TRANCETALENT, TARGET, "動物耳", 1
				CALL TRANCETALENT, TARGET, "尻尾", 1
				CALL TRANCETALENT, TARGET, "バストサイズ", 0
				;着衣フラグの変更
				VARSET CFLAG, 1, GETNUM(CFLAG, "特殊着衣不可"), GETNUM(CFLAG, "足元着衣不可")+1
				VARSET CSTR, "", GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
			ELSE
				PRINTFORMW ＜%CALLNAME:TARGET%は人間の姿に変化した＞
				CALL ALLTRANCEOFF, TARGET
				;着衣フラグのリセット
				FOR LCOUNT, GETNUM(CFLAG, "特殊着衣不可"), GETNUM(CFLAG, "足元着衣不可")+1
					CFLAG:LCOUNT = CSVCFLAG(NOF(TARGET), LCOUNT)
				NEXT
				FOR LCOUNT, GETNUM(CSTR, "特殊"), GETNUM(CSTR, "足元")+1
					CSTR:LCOUNT = %CSVCSTR(NOF(TARGET), LCOUNT)%
				NEXT
			ENDIF
			INVERTBIT TRANCEFLAG:TARGET:獣変化, 0
	ENDSELECT
	TARGET = LTARGET
	RESTART
ENDIF



;-------------------------------------------------
;ミゼラン関連の関数
;-------------------------------------------------

@ミゼラン変身先名
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ミゼラン
ミゼラン = GETCHARANAME("ミゼラン・ミゼラン")
SIF ミゼラン == -1
	RETURNF ""
SELECTCASE TRANCEFLAG:ミゼラン:ミゼラン専用
	CASE IS <= 0
		RETURNF "マゼラン・マゼラン"
	CASE 10000
		FOR LCOUNT, 0, CHARANUM
			SIF !ミゼラン変身中:LCOUNT:0
				CONTINUE
			RETURNF 恋心名(LCOUNT)
		NEXT
	CASEELSE
		RETURNF CSVNAME(TRANCEFLAG:ミゼラン:ミゼラン専用)
ENDSELECT

;;;対象が変身中のミゼランなら1 NO参照フラグに
@ISMIGELLAN, 対象
#FUNCTION
#DIM DYNAMIC 対象
SIF 対象 == -1
	RETURNF 0
SIF 対象 == GETCHARANAME("ミゼラン・ミゼラン") && TRANCEFLAG:対象:ミゼラン専用
	RETURNF 1
RETURNF 0

;;;もっとマシな関数名はないのか
@ISMIGELLANNTR, 対象, ミゼラン
#FUNCTION
#DIM DYNAMIC 対象
#DIM DYNAMIC ミゼラン
SIF !ISMIGELLAN(ミゼラン)
	RETURNF 0
;不明の場合対象キャラに残り時間が入っているのでこれでOK
SIF ミゼラン変身中:対象:0
	RETURNF 1
SIF GROUPMATCH(CSVNAMEF(TRANCEFLAG:ミゼラン:ミゼラン専用, 1), CSTR:対象:恋心, CSTR:対象:既婚者)
	RETURNF 1
RETURNF 0


;;;SHOPの変化から飛んでくるのでミゼランがTARGETになっている
@ミゼラン変身先選択
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 相性参照キャラ
#DIM DYNAMIC 変身先
#DIM DYNAMIC CSV体格

PRINTL □誰の知り合いに変身しますか？
FOR PAGING, 0, CHARANUM
	CALL PAGING, NO:PAGING < 10000 && PAGING != TARGET
NEXT
SIF RESULT == -1
	RETURN
相性参照キャラ = RESULT

PRINTL □変身先を選んでください
;恋心か既婚者持ちの場合別枠（不明も可能）ただしハニーバニーと重複不可
CALL DISABLE, "EVENT_ミゼラン変身", 相性参照キャラ
IF !RESULT && !ハニーバニー:相性参照キャラ:0 && !GROUPMATCH(恋心名(相性参照キャラ), "マゼラン・マゼラン", "ミゼラン・ミゼラン")
	IF CSTR:相性参照キャラ:恋心 == "不明" || CSTR:相性参照キャラ:既婚者 == "不明"
		PRINTFORML 　[10000] %恋心名(相性参照キャラ)%
	ELSEIF TALENT:相性参照キャラ:恋心 || TALENT:相性参照キャラ:既婚者
		PRINTFORML 　[{GETNO(恋心名(相性参照キャラ))}] %恋心名(相性参照キャラ)%
	ENDIF
ENDIF
FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;相性のあるキャラのみ変身可能（正負は問わない）
	SIF !RELATION:相性参照キャラ:LCOUNT
		CONTINUE
	;マゼランにはもうなってる（自分にもなれない）
	SIF GROUPMATCH(CSVNAME(LCOUNT), "マゼラン・マゼラン", "ミゼラン・ミゼラン")
		CONTINUE
	;ハニバ対象は上で表示済
	SIF GROUPMATCH(CSVNAME(LCOUNT), CSTR:相性参照キャラ:恋心, CSTR:相性参照キャラ:既婚者)
		CONTINUE
	PRINTFORML 　[{LCOUNT}] %CSVNAME(LCOUNT)%
NEXT
DRAWLINE
PRINTFORML 　[-1] 戻る

BINPUT
SIF RESULT == -1
	RESTART
変身先 = RESULT

;残り時間は不明の場合参照キャラに記録 それ以外はミゼランに記録
TRANCEFLAG:ミゼラン専用 = 変身先
IF 変身先 == 10000
	ミゼラン変身中:相性参照キャラ:0 = 6
ELSE
	ミゼラン変身中:TARGET:0 = 6
ENDIF
PRINTFORMW ＜ミゼランは%ミゼラン変身先名()%に変身した＞
SIF 変身先 == 10000 || GROUPMATCH(CSVNAME(変身先), CSTR:相性参照キャラ:恋心, CSTR:相性参照キャラ:既婚者)
	CALL 実績解除, "もっとマシな使い方はないのか"
;CSVのある対象に変身する場合一部素質を模倣（性別、体格、バストサイズ、羽、尻尾、動物耳、ファーリー）種族はそのままとする
IF 変身先 != 10000
	CSV体格 = CSVTALENT(変身先, TALENTF("体格")) > 0 ? CSVTALENT(変身先, TALENTF("体格")) # 3
	CALL TRANCETALENT, TARGET, "体格", CSV体格
	CALL TRANCETALENT, TARGET, "バストサイズ", CSVTALENT(変身先, TALENTF("バストサイズ"))
	;後天的に付与される可能性も一応考慮しておく
	CALL TRANCETALENT, TARGET, "羽", CSVTALENT(変身先, TALENTF("羽"))
	CALL TRANCETALENT, TARGET, "尻尾", CSVTALENT(変身先, TALENTF("尻尾"))
	CALL TRANCETALENT, TARGET, "動物耳", CSVTALENT(変身先, TALENTF("動物耳"))
	CALL TRANCETALENT, TARGET, "ファーリー", CSVTALENT(変身先, TALENTF("ファーリー"))
	;TS処理を最後に行う
	SIF CSVTALENT(変身先, TALENTF("性別")) > 0 && TALENT:性別 != CSVTALENT(変身先, TALENTF("性別"))
		GOTO TS
ELSEIF 1
	;CSVない場合参照キャラが同性なら性別だけ変えておく バストサイズは女体化の場合3
	IF SEX(TARGET) == SEX(相性参照キャラ)
		CALL TRANCETALENT, TARGET, "バストサイズ", SEX(TARGET) == 1 ? 3 # 0
		GOTO TS
	ENDIF
ELSE
	$TS
	;TS処理 性転換薬使用済などで変になりそう
	TRANCE:TALENTF("性別") = TALENT:性別
	IF SEX(TARGET) == 2
		TALENT:性別 = 1
		SIF CSTR:童貞 == ""
			TALENT:童貞 = 1
		CALL TRANCETALENT, TARGET, "処女", 0
	ELSE
		TALENT:性別 = 2
		SIF CSTR:処女 == ""
			TALENT:処女 = 1
		CALL TRANCETALENT, TARGET, "童貞", 0
	ENDIF
	CALL 装飾継続チェック, TARGET
ENDIF
;服装をどうするかは未定


@ミゼラン変身解除
#DIM DYNAMIC ミゼラン
ミゼラン = GETCHARANAME("ミゼラン・ミゼラン")

PRINTFORMW ＜ミゼランはマゼランの姿に戻った＞
	TRANCEFLAG:ミゼラン:ミゼラン専用 = -6
VARSET ミゼラン変身中
;クールタイム無しは強すぎなので
PRINTFORMW ミゼランは変身中の演技で疲れたようです。すぐにまた変身させるのは控えましょう
;素質を戻す処理
CALL TRANCETALENT, ミゼラン, "体格", 999
CALL TRANCETALENT, ミゼラン, "バストサイズ", 999
CALL TRANCETALENT, ミゼラン, "羽", 999
CALL TRANCETALENT, ミゼラン, "尻尾", 999
CALL TRANCETALENT, ミゼラン, "動物耳", 999
CALL TRANCETALENT, ミゼラン, "ファーリー", 999
IF TRANCE:ミゼラン:TALENTF("性別")
	IF SEX(ミゼラン) == 1
		TALENT:ミゼラン:童貞 = 0
		CALL TRANCETALENT, ミゼラン, "処女", 1
	ELSE
		TALENT:ミゼラン:処女 = 0
		CALL TRANCETALENT, ミゼラン, "童貞", 1
	ENDIF
	TALENT:ミゼラン:性別 = TRANCE:ミゼラン:TALENTF("性別")
	TRANCE:ミゼラン:TALENTF("性別") = 0
ENDIF


@ミゼラン変身時間経過処理
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ミゼラン
SIF GETCHARANAME("ミゼラン・ミゼラン") == -1
	RETURN
ミゼラン = GETCHARANAME("ミゼラン・ミゼラン")
SIF !TRANCEFLAG:ミゼラン:ミゼラン専用
	RETURN

IF TRANCEFLAG:ミゼラン:ミゼラン専用 < 0
	;クールタイムの処理
	TRANCEFLAG:ミゼラン:ミゼラン専用++
	RETURN
ELSE
	FOR LCOUNT, 0, CHARANUM
		IF ミゼラン変身中:LCOUNT:0 > 0
			ミゼラン変身中:LCOUNT:0--
			SIF !ミゼラン変身中:LCOUNT:0
				CALL ミゼラン変身解除
			BREAK
		ENDIF
	NEXT
ENDIF
CALL CHECKDRAWLINE


;-------------------------------------------------
;変身系の処理
;-------------------------------------------------

;;;調教の場にいれば1 一応睡眠と死亡をここで弾いておく
@豹変可能, 対象
#FUNCTION
#DIM DYNAMIC 対象
SIF 対象 == -1
	RETURNF 0
SIF CFLAG:対象:死亡 || TEQUIP:対象:睡眠
	RETURNF 0
;既に弾いてるからASSI不在に一致することはないはず
IF GROUPMATCH(対象, TARGET, MASTER, ASSI)
	RETURNF 1
ENDIF
RETURNF 0

;;;TRANCE変数をオンオフする（要改良）
;;;1で習得、0で消滅 既に習得している素質に習得処理を行った場合などはその状態が保存されて消滅処理を行った時フラグを折るだけになる
;;;999で強制的に元に戻す 変身時に1か0を指定して解除時に全て999を指定など
@TRANCETALENT, 対象, 素質名, 数値
#DIM DYNAMIC 対象
#DIMS DYNAMIC 素質名
#DIM DYNAMIC 数値

IF 数値 == 999
	IF TRANCE:対象:TALENTF(素質名) == -1
		TRANCE:対象:TALENTF(素質名) = 0
	ELSEIF 素質名 == "バストサイズ" || 素質名 == "体格"
		TALENT:対象:TALENTF(素質名) = TRANCE:対象:TALENTF(素質名)
	ELSEIF TRANCE:対象:TALENTF(素質名) == 1
		INVERTBIT TALENT:対象:TALENTF(素質名), 0
	ENDIF
	TRANCE:対象:TALENTF(素質名) = 0
	RETURN
ENDIF
SELECTCASE 素質名
	CASE "バストサイズ"
		SELECTCASE TRANCE:対象:TALENTF("バストサイズ")
			CASE -1
				TALENT:対象:バストサイズ = 0
				TRANCE:対象:TALENTF("バストサイズ") = 0
			CASE 0
				;男のバストサイズ無しの場合は-1にする
				TRANCE:対象:TALENTF("バストサイズ") = TALENT:対象:バストサイズ > 0 ? TALENT:対象:バストサイズ # -1
				TALENT:対象:バストサイズ = 数値
			CASEELSE
				TALENT:対象:バストサイズ = TRANCE:対象:TALENTF("バストサイズ")
				TRANCE:対象:TALENTF("バストサイズ") = 0
		ENDSELECT
	CASE "体格"
		IF TRANCE:対象:TALENTF("体格")
			TALENT:対象:体格 = TRANCE:対象:TALENTF("体格")
			TRANCE:対象:TALENTF("体格") = 0
		ELSE
			TRANCE:対象:TALENTF("体格") = TALENT:対象:体格
			TALENT:対象:体格 = 数値 > 0 ? 数値 # 3
		ENDIF
	CASEELSE
		IF TRANCE:対象:TALENTF(素質名) == -1
			TRANCE:対象:TALENTF(素質名) = 0
		ELSE
			IF TALENT:対象:TALENTF(素質名) != 数値
				TALENT:対象:TALENTF(素質名) = 数値
				INVERTBIT TRANCE:対象:TALENTF(素質名), 0
			ELSE
				 TRANCE:対象:TALENTF(素質名) = -1
			ENDIF
		ENDIF
ENDSELECT

;;;TRANCERACEをオンオフする 種族名に何も入っていないと元に戻す
@TRANCERACE, 対象, 種族名
#DIM DYNAMIC 対象
#DIMS DYNAMIC 種族名
IF 種族名 != ""
	SIF TRANCERACE:対象:0 == ""
		TRANCERACE:対象:0 = %CSTR:対象:種族%
	CSTR:対象:種族 = %種族名%
ELSEIF TRANCERACE:対象:0 != ""
	CSTR:対象:種族 = %TRANCERACE:対象:0%
	TRANCERACE:対象:0 = 
ENDIF

;;;素質と種族をまとめて全部戻す用 使う場合は競合がないか注意が必要（ビーストモードとは競合しない）
@ALLTRANCEOFF, 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 対象
FOR LCOUNT, 0, VARSIZE("TALENT")
	SIF TRANCE:対象:LCOUNT
		CALL TRANCETALENT, 対象, TALENTNAME:LCOUNT, 999
NEXT
CALL TRANCERACE, 対象, ""



;-------------------------------------------------
;古い処理
;-------------------------------------------------

;;;獣変化から移動

;茶倉だけ特殊で機械（戌）になる
;スーツだと判明したので除外
[SKIPSTART]
IF CHARANAMEF(TARGET) == "大伴 茶倉"
	IF !TALENT:無機物
		TALENT:無機物 = 1
		TRANCERACE:TARGET:0 = %CSTR:種族%
		CSTR:種族 = 機械
		NAME:TARGET = 戌
		CALLNAME:TARGET = 戌
		TRANCE:TALENTF("無機物") = 1
	ENDIF
ELSE
ENDIF
[SKIPEND]

;茶倉だけ特殊で機械処理なので人間に戻す
;スーツだと判明したので除外
[SKIPSTART]
IF CHARANAMEF(TARGET) == "戌"
	IF TRANCE:TALENTF("無機物")
		TALENT:無機物 = 0
		CSTR:種族 = %TRANCERACE:TARGET:0%
		NAME:TARGET = 大伴 茶倉
		CALLNAME:TARGET = 茶倉
		TRANCE:TALENTF("無機物") = 0
	ENDIF
ELSE
ENDIF
[SKIPEND]
