@CHARA_MANUAL, ARG
#DIM DYNAMIC LCOUNT

VARSET LOCAL, 0

IF GETCHARA(ARG) > 0
	PRINTFORMW %CSVNAMEF(ARG)%は既に購入されています
	RETURN 1
ELSEIF CCB:ARG
	PRINTFORMW %CSVNAMEF(ARG)%は購入できません
	RETURN 1
ELSEIF ARG == NO:MASTER
	RETURN 1
ENDIF

FOR LCOUNT, 0, 100
	SIF CSVCSTR(ARG, LCOUNT) == ""
		BREAK
	CALL MANUALPRINT, CSVCSTR(ARG, LCOUNT)
	SIF !LINEISEMPTY()
		PRINTL 
NEXT

SIF CSVTALENT(ARG, GETNUM(TALENT, "アニマル"))
	PRINTL ※このキャラはペット候補なので売却できません。所持金が少ない状態で購入するとゲームの攻略が不可能になる場合があります
SIF FLAG:チャレンジモード
	PRINTL ※CHALLENGEモードに挑戦中です。表示されているマニュアル・素質とは齟齬がある場合があります

;キャラマニュアル内でタグ付けされていれば特殊表示する
;これでCSV内でマニュアルも完結できる
;マニュアル用と題してるが普通の文章表示でもタグ付け機能が使える その際はLINEISEMPTYでの改行を忘れずに
@MANUALPRINT, マニュアル:0
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 検索位置
#DIMS DYNAMIC マニュアル, 2
#DIM DYNAMIC 始点
#DIM DYNAMIC 文字数

;タグを探す
始点 = STRFINDU(マニュアル, "(", 検索位置)
文字数 = STRFINDU(マニュアル, ")", 検索位置)-始点
;タグ付けされてれば処理実行
IF 始点 >= 0
	;タグ付けまでの文章を通常表示
	PRINTFORM %SUBSTRINGU(マニュアル, 0, 始点)%
	;処理、内容、本文でSPLITする
	SPLIT SUBSTRINGU(マニュアル, 始点+1, 文字数-1), ":", LOCALS
	;LOCAL:0 = 特殊処理の種類 LOCAL:1 = 特殊処理の内容 LOCAL:2 = 本文
	;必要に応じて追加するとよい
	SELECTCASE LOCALS:0
		;フォント変更系
		CASE "FONT"
			SELECTCASE LOCALS:1
				;太字
				CASE "BOLD"
					FONTBOLD
					CALL MANUALPRINT, LOCALS:2
					FONTREGULAR
				;打ち消し線
				CASE "STRIKE"
					FONTSTYLE 4
					CALL MANUALPRINT, LOCALS:2
					FONTSTYLE 0
			ENDSELECT
		;色指定 現時点では色名のみで数値指定はできない
		;指定した色名が無効であればデフォルト色表示
		CASE "COLOR"
			IF COLOR_FROMNAME(LOCALS:1) == -1
				CALL MANUALPRINT, LOCALS:2
			ELSE
				SETCOLOR COLOR_FROMNAME(LOCALS:1)
				CALL MANUALPRINT, LOCALS:2
				RESETCOLOR
			ENDIF
		;その他
		CASE "OTHER"
			SELECTCASE LOCALS:1
				;反転タグ 実質シャルロット専用 ALIGHNMENT命令の仕様上、タグ重複使用は不可能
				CASE "REVERSE"
					マニュアル:1 =
					FOR LCOUNT, STRLENSU(LOCALS:2), -1, -1
						SELECTCASE CHARATU(LOCALS:2, LCOUNT)
							CASE "【"
								マニュアル:1 += "】"
							CASE "】"
								マニュアル:1 += "【"
							CASE "「"
								マニュアル:1 += "」"
							CASE "」"
								マニュアル:1 += "「"
							CASE "["
								マニュアル:1 += "]"
							CASE "]"
								マニュアル:1 += "["
							CASE "（"
								マニュアル:1 += "）"
							CASE "）"
								マニュアル:1 += "（"
							CASEELSE
								マニュアル:1 += CHARATU(LOCALS:2, LCOUNT)
						ENDSELECT
					NEXT
					ALIGNMENT RIGHT
					PRINTFORML %マニュアル:1%
					ALIGNMENT LEFT
				;WAIT このタグを使用すると改行されるので、文末までタグに含めないと表示がおかしくなる
				CASE "WAIT"
					CALL MANUALPRINT, LOCALS:2
					WAIT
			ENDSELECT
	ENDSELECT
	;次の始動位置をタグ終了後の次の文字に設定
	検索位置 = STRFINDU(マニュアル, LOCALS:2)+STRLENSU(LOCALS:2)
	IF 検索位置 < STRLENSU(マニュアル)
		;残りの文章が閉じカッコならループ終了
		IF 検索位置+1 == STRLENSU(マニュアル) && CHARATU(マニュアル, 検索位置) == ")"
			RETURN
		;残りの文章が閉じカッコ以外にもあれば再帰処理
		ELSE
			;閉じカッコの分+1
			CALL MANUALPRINT, SUBSTRINGU(マニュアル, 検索位置+1)
			RETURN
		ENDIF
	ENDIF
;タグが無ければ通常表示
ELSE
	PRINTFORM %マニュアル%
	RETURN
ENDIF

@CHARA_BUY_CHECK, ARG
#DIM DYNAMIC LCOUNT
LOCAL = 0
IF !EXISTCSV(ARG)
	PRINTL 
	PRINTW CSVが未実装です（仕様）
	RETURN 1
ELSEIF MONEY < CSVCFLAG(ARG, 50)
	PRINTL 
	PRINTW ＜金額が足りません＞
	RETURN 1
ELSE
	DO
		PRINTL 
		PRINTL 購入しますか？
		PRINTL [0]はい
		PRINTL [1]いいえ
		SIF LOCAL == 0
			PRINTL [2]素質を表示する
		INPUT
		SELECTCASE RESULT
			CASE 0
				CALL CHARA_ADD, ARG
				RETURN 0
			CASE 1
				RETURN 1
			CASE 2
				LOCAL = 1
				PRINTL 
				PRINTFORML 種族:%CSVCSTR(ARG, GETNUM(CSTR, "種族"))%
				PRINT 素質
				REPEAT 200
					SELECTCASE TALENTNAME:COUNT
						CASE "性別"
							CONTINUE
						CASE "バストサイズ"
							SELECTCASE CSVTALENT(ARG, COUNT)
								CASE 1
									PRINT 【絶壁】
								CASE 2
									PRINT 【貧乳】
								CASE 3
									PRINT 【並乳】
								CASE 4
									PRINT 【巨乳】
								CASE 5
									PRINT 【爆乳】
							ENDSELECT
							CONTINUE
					ENDSELECT
					SIF CSVTALENT(ARG, COUNT)
						PRINTFORM 【%TALENTNAME:COUNT%】
				REND
				PRINTL 
		ENDSELECT
	LOOP LOOPRES(0, 1)
ENDIF

RETURN 0

@CHARA_ADD, ARG, 段位認定
#DIM DYNAMIC LCOUNT
;今のところは50人まで
#DIMS DYNAMIC 相性設定用, 50
#DIM DYNAMIC 段位認定

購入人数++
ADDCHARA ARG
TARGET = CHARANUM-1
IF !段位認定
	MONEY -= CSVCFLAG(ARG , 50)
	PRINTFORMW ＜%CSVNAMEF(ARG)%を解禁しました＞
	TRYCALLFORM K{ARG}_CHARABUY
	TRYCALLFORM MASTER_K{NO:MASTER}_CHARABUY, ARG
	SIF CFLAG:誕生日 == (MONTH*100)+DAYS
		CALL 実績解除, "アンハッピーバースデー"
	SIF CHARANAMEF(TARGET) == "つらら" && MONTH == 12 && (DAYS == 24 || DAYS == 25)
		CALL 実績解除, "クリスマス中止のお知らせ"
ENDIF
;ヨミは性別を選択する
CALL ヨミの性別, TARGET
CCB:ARG = 1
キャラ未購入 = 0
;マルクトとミリアは片方しか奴隷にできない
SELECTCASE ARG
	CASE GETNO("マルクト")
		CCB:GETNO("ア・ミリア") = 1
	CASE GETNO("ア・ミリア")
		CCB:GETNO("マルクト") = 1
ENDSELECT
BUYD = DAYS
BUYM = MONTH
BUYY = YEAR
;初期フラグいろいろ
CFLAG:生殖力担当魔術師 = -1
CFLAG:不妊担当魔術師 = -1
CFLAG:男性器提供者 = -1
IF PENIS(TARGET)
	CFLAG:ペニスサイズ = 2
	SIF TALENT:小柄体型 || TALENT:小人体型
		CFLAG:ペニスサイズ = 1
ENDIF
CALL 初期経験セット, TARGET
MAXBASE:魔力 = ABL:魔法技能*1000
;アニマルなら助手可能にする
SIF TALENT:アニマル
	CFLAG:状態 = 2
TFLAG:ページ数 = 0
;CHALLENGEモードなら性経験を消してバージンにする
IF FLAG:チャレンジモード
	FOR LCOUNT, 0, 20
		ABL:LCOUNT = 0
	NEXT
	FOR LCOUNT, 0, 100
		EXP:LCOUNT = 0
	NEXT
	SELECTCASE SEX(TARGET)
		CASE 1
			TALENT:童貞 = 1
		CASE 2
			TALENT:処女 = 1
	ENDSELECT
ENDIF
;相性の設定
SPLIT CSTR:相性, "/", 相性設定用
FOR LCOUNT, 0, RESULT
	;なぜか行連結で行頭にスペースが入るので削る
	相性設定用:LCOUNT = %SUBSTRINGU(相性設定用:LCOUNT, 1)%
	SIF 相性設定用:LCOUNT == ""
		BREAK
	SPLIT 相性設定用:LCOUNT, "*", LOCALS
	RELATION:GETNO(LOCALS) = TOINT(LOCALS:1)
NEXT

