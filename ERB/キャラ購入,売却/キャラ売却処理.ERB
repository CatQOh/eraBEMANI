;奴隷売却の処理
@CHARA_SELL
#DIM DYNAMIC LOCALTARGET
#DIM DYNAMIC LOCALASSI
#DIM DYNAMIC PRICE
#DIM DYNAMIC LCOUNT

VARSET LOCAL, 0

DRAWLINE
PRINTL □誰を売りますか？
FOR PAGING, 0, CHARANUM
	LOCALS = 
	LOCAL = 1
	;主人は除外
	SIF PAGING == MASTER
		LOCAL = 0
	;育児部屋に移動しているキャラは除外
	SIF CHECK_CHILD_CARE(PAGING)
		LOCAL = 0
	;売却不可フラグが立ってるキャラは除外
	SIF CFLAG:PAGING:売却不可
		LOCAL = 0
	;魔法の効果発動中のキャラは除外
	SIF CFLAG:PAGING:憑依中
		LOCAL = 0
	;アニマルは売却できない
	SIF TALENT:PAGING:アニマル
		LOCAL = 0
	SIF ビーストモード:PAGING:0
		LOCAL = 0
	SIF ティルナノーグ:PAGING:0
		LOCAL = 0
	IF ASSI == PAGING
		LOCALS = (助手)
	ELSEIF ISASSI:PAGING
		LOCALS = (元助手)
	ELSEIF CFLAG:PAGING:状態 == 2
		LOCALS = (助手可)
	ENDIF
	CALL PAGING, CFLAG:PAGING:状態 >= 1 && LOCAL, @"%LOCALS%", "SELL SLAVE"
NEXT

SELECTCASE RESULT
	CASE -1
		RETURN 0
	CASE 0 TO CHARANUM
		;売却処理
		LOCALTARGET = TARGET
		LOCALASSI = ASSI
		TARGET = RESULT
ENDSELECT

SIF TARGET == MASTER
	RESTART

PRICE = 0

;ABLによる基礎値計算
LOCAL = 0
REPEAT VARSIZE("ABL")
	SELECTCASE ABLNAME:COUNT
		;従順、欲望、技巧、奉仕精神、露出癖のテーブル
		CASE "従順", "欲望", "技巧", "奉仕精神", "露出癖"
			SELECTCASE ABL:COUNT
				CASE 0
					LOCAL = 0
				CASE 1
					LOCAL = 1000
				CASE 2
					LOCAL = 1500
				CASE 3
					LOCAL = 2500
				CASE 4
					LOCAL = 4000
				CASE 5
					LOCAL = 6000
				CASE 6
					LOCAL = 8500
				CASE 7
					LOCAL = 11500
				CASE 8
					LOCAL = 15000
				CASE 9
					LOCAL = 19000
				CASEELSE
					LOCAL = 25000
			ENDSELECT
			SIF LOCAL > 0
				PRINTFORML %ABLNAME:COUNT%LV{ABL:COUNT}+{LOCAL}
			PRICE += LOCAL
		;感度のテーブル
		CASE "Ｃ感覚", "Ｖ感覚", "Ａ感覚", "Ｂ感覚"
			SELECTCASE ABL:COUNT
				CASE 0
					LOCAL = 0
				CASE 1
					LOCAL = 1000
				CASE 2
					LOCAL = 1600
				CASE 3
					LOCAL = 2800
				CASE 4
					LOCAL = 4600
				CASE 5
					LOCAL = 7000
				CASE 6
					LOCAL = 10000
				CASE 7
					LOCAL = 13600
				CASE 8
					LOCAL = 17800
				CASE 9
					LOCAL = 22600
				CASEELSE
					LOCAL = 30000
			ENDSELECT
			SIF LOCAL > 0
				PRINTFORML %ABLNAME:COUNT%LV{ABL:COUNT}+{LOCAL}
			PRICE += LOCAL
		;サドっ気、レズっ気、サドっ気、ホモっ気のテーブル
		CASE "マゾっ気", "サドっ気", "レズっ気", "ホモっ気"
			SELECTCASE ABL:COUNT
				CASE 0
					LOCAL = 0
				CASE 1
					LOCAL = 1000
				CASE 2
					LOCAL = 1400
				CASE 3
					LOCAL = 2200
				CASE 4
					LOCAL = 3400
				CASE 5
					LOCAL = 5000
				CASE 6
					LOCAL = 7000
				CASE 7
					LOCAL = 9400
				CASE 8
					LOCAL = 12200
				CASE 9
					LOCAL = 15400
				CASEELSE
					LOCAL = 20000
			ENDSELECT
			SIF LOCAL > 0
				PRINTFORML %ABLNAME:COUNT%LV{ABL:COUNT}+{LOCAL}
			PRICE += LOCAL
		;料理技能、家事技能、工作技能、歌唱技能、演奏技能、撮影技能、舞踊技能、魔法技能のテーブル
		CASE "料理技能", "家事技能", "工作技能", "歌唱技能", "演奏技能", "撮影技能", "舞踊技能", "魔法技能"
			SELECTCASE ABL:COUNT
				CASE 0
					LOCAL = 0
				CASE 1
					LOCAL = 800
				CASE 2
					LOCAL = 1100
				CASE 3
					LOCAL = 1700
				CASE 4
					LOCAL = 2600
				CASE 5
					LOCAL = 3800
				CASE 6
					LOCAL = 5300
				CASE 7
					LOCAL = 7100
				CASE 8
					LOCAL = 9200
				CASE 9
					LOCAL = 11600
				CASEELSE
					LOCAL = 15000
			ENDSELECT
			SIF LOCAL > 0
				PRINTFORML %ABLNAME:COUNT%LV{ABL:COUNT}+{LOCAL}
			PRICE += LOCAL
		;中毒のテーブル
		CASE "自慰中毒", "精液中毒", "レズ中毒", "ゲイ中毒", "性交中毒", "噴乳中毒"
			SELECTCASE ABL:COUNT
				CASE 0
					LOCAL = 0
				CASE 1
					LOCAL = 2000
				CASE 2
					LOCAL = 3000
				CASE 3
					LOCAL = 5000
				CASE 4
					LOCAL = 8000
				CASE 5
					LOCAL = 12000
				CASE 6
					LOCAL = 17000
				CASE 7
					LOCAL = 23000
				CASE 8
					LOCAL = 30000
				CASE 9
					LOCAL = 38000
				CASEELSE
					LOCAL = 50000
			ENDSELECT
			SIF LOCAL > 0
				PRINTFORML %ABLNAME:COUNT%LV{ABL:COUNT}+{LOCAL}
			PRICE += LOCAL
	ENDSELECT
REND

SETCOLOR 100, 150, 250
PRINTFORML 能力価格 = {PRICE}
RESETCOLOR

PRINTL 
LOCAL = 100
;素質による売却額補正値計算
IF TALENT:処女
	LOCAL += 100
	PRINTL 処女 +100%
ENDIF
IF TALENT:再生処女
	LOCAL += 50
	PRINTL 再生処女 +50%
ENDIF

;素質による増減
;陥落素質
IF TALENT:恋慕
	PRINTL 恋慕 -30%
	LOCAL -= 30
ENDIF
IF TALENT:淫乱
	PRINTL 淫乱 +30%
	LOCAL += 30
ENDIF
IF TALENT:服従
	PRINTL 服従 +100%
	LOCAL += 100
ENDIF
IF TALENT:親愛
	PRINTL 親愛 -50%
	LOCAL -= 50
ENDIF
IF TALENT:娼婦
	PRINTL 娼婦 +60%
	LOCAL += 60
ENDIF
IF TALENT:隷属
	PRINTL 隷属 +200%
	LOCAL += 200
ENDIF

IF SEX(TARGET) == 1
	PRINTL 男 +30%
	LOCAL += 30
ENDIF

;その他素質
FOR LCOUNT, 0 ,VARSIZE("TALENT")
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		;バストサイズ
		CASE "バストサイズ"
			SELECTCASE TALENT:バストサイズ
				;絶壁
				CASE 1
					PRINTL 絶壁 -20%
					LOCAL -= 20
				;貧乳
				CASE 2
					PRINTL 貧乳 -10%
					LOCAL -= 10
				;巨乳
				CASE 4
					PRINTL 巨乳 +20%
					LOCAL += 20
				;爆乳
				CASE 5
					PRINTL 爆乳 +30%
					LOCAL += 30
			ENDSELECT
		;長いのは見づらいから数行に分ける
		;[素直]及び[解放]は[抵抗][抑圧]消滅時に付いたものならマイナス補正無効
		CASE "素直"
			IF !(NO:TARGET < 10000 && CSVTALENT(NO:TARGET, GETNUM(TALENT, "抵抗")))
				PRINTL 素直 -20%
				LOCAL -= 20
			ENDIF
		CASE "解放"
			IF !(NO:TARGET < 10000 && CSVTALENT(NO:TARGET, GETNUM(TALENT, "抑圧")))
				PRINTL 解放 -20%
				LOCAL -= 20
			ENDIF
		;-20%
		CASE "好奇心", "解放", "習得早い", "献身的", "快感に素直", "倒錯的", "回復早い", "弱気"
			PRINTFORML %TALENTNAME:LCOUNT% -20\%
			LOCAL -= 20
		;-10%
		CASE "臆病", "大人しい", "プライド低い", "目立ちたがり", "貞操無頓着", "恥薄い", "濡れやすい", "自慰しやすい"
			PRINTFORML %TALENTNAME:LCOUNT% -10\%
			LOCAL -= 10
		;-10%
		CASE "汚臭鈍感", "汚れ無視", "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "日光浴", "月光浴", "幼稚", "寂しがり屋"
			PRINTFORML %TALENTNAME:LCOUNT% -10\%
			LOCAL -= 10
		;-10%
		CASE "ナルシスト", "男好き", "女好き", "ハイテンション", "衝動的", "買い物上手", "夢喰い", "サボり魔"
			PRINTFORML %TALENTNAME:LCOUNT% -10\%
			LOCAL -= 10
		;+10%
		CASE "舌使い", "針さばき", "猫舌", "おもらし癖", "小悪魔", "妖狐", "羽", "尻尾", "ネクロマンサー", "冷静"
			PRINTFORML %TALENTNAME:LCOUNT% +10\%
			LOCAL += 10
		;+20%
		CASE "恥じらい", "濡れにくい", "調合知識", "汚臭敏感", "男嫌い", "女嫌い", "小柄体型", "ふたなり", "母乳体質", "絶倫"
			PRINTFORML %TALENTNAME:LCOUNT% +20\%
			LOCAL += 20
		;+20%
		CASE "ファーリー", "潔癖症", "ドラゴン使い", "暑さに弱い", "寒さに弱い", "魔術師", "ヤンデレ"
			PRINTFORML %TALENTNAME:LCOUNT% +20\%
			LOCAL += 20
		;+30%
		CASE "ツンデレ", "貞操観念", "習得遅い", "薬毒耐性", "サド", "魅力", "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "母性"
			PRINTFORML %TALENTNAME:LCOUNT% +30\%
			LOCAL += 30
		;+30%
		CASE "同族嫌悪", "料理人", "専業主婦", "絶対音感", "工学博士", "カメラマン"
			PRINTFORML %TALENTNAME:LCOUNT% +30\%
			LOCAL += 30
		;+40%
		CASE "保守的", "妄信", "回復遅い", "恋心", "強気"
			PRINTFORML %TALENTNAME:LCOUNT% +40\%
			LOCAL += 40
		;+50%
		CASE "反抗的", "気丈", "プライド高い", "生意気", "自制心", "無関心", "淫核", "淫膣", "淫尻", "淫乳"
			PRINTFORML %TALENTNAME:LCOUNT% +50\%
			LOCAL += 50
		;+50%
		CASE "謎の魅力", "戦士", "抑圧", "抵抗"
			PRINTFORML %TALENTNAME:LCOUNT% +50\%
			LOCAL += 50
		;+60%
		CASE "感情乏しい", "一線越えない"
			PRINTFORML %TALENTNAME:LCOUNT% +60\%
			LOCAL += 60
		;+70%
		CASE "快感の否定", "小人体型", "無機物", "既婚者"
			PRINTFORML %TALENTNAME:LCOUNT% +70\%
			LOCAL += 70
		;+300%
		CASE "リミットバースト"
			PRINTFORML %TALENTNAME:LCOUNT% +300\%
			LOCAL += 300
	ENDSELECT
NEXT

;調教が進むと消えるマイナス素質（抑圧、抵抗）
IF NO:TARGET < 10000
	IF CSVTALENT(NO:TARGET, GETNUM(TALENT, "抑圧")) && !TALENT:抑圧
		LOCAL += 50
		PRINTL 抑圧消滅済み +50%
	ENDIF
	IF CSVTALENT(NO:TARGET, GETNUM(TALENT, "抵抗")) && !TALENT:抵抗
		LOCAL += 50
		PRINTL 抵抗消滅済み +50%
	ENDIF
ENDIF

;娼館で働かせた日数だけ倍率減
IF TALENT:傷物
	LOCAL -= TALENT:傷物
	PRINTFORML 傷物 -{TALENT:傷物}\%
ENDIF

PRINTFORML 素質倍率 = {LOCAL}\%
;補正値はどんなに低くても最低30%
IF LOCAL < 30
	PRINTL 最低保証 30%
	LOCAL = 30
ENDIF 
;補正値の適用
PRICE *= LOCAL
PRICE /= 100

SETCOLOR 100, 150, 250
PRINTFORML 能力価格×素質補正 = {PRICE}
RESETCOLOR

PRINTL 
LOCAL = 100
IF TALENT:崩壊
	PRINTL 崩壊 -20%
	LOCAL -= 20
ENDIF
IF TALENT:妊娠
	PRINTL 妊娠 -30%
	LOCAL -= 30
ENDIF
IF TALENT:狂気
	PRINTL 狂気 -30%
	LOCAL -= 30
ENDIF
IF TALENT:即落ち
	PRINTL 即落ち -80%
	LOCAL -= 80
ENDIF

IF CSTR:眷属主 != "" && CSTR:種族 != "ヴァンパイア"
	PRINTL 眷属化 -20%
	LOCAL -= 20
ENDIF

IF CFLAG:眷属化した人数 && CSTR:種族 != "ヴァンパイア"
	PRINTFORML 眷属主 -{20*CFLAG:眷属化した人数}\%
	LOCAL -= 20*CFLAG:眷属化した人数
ENDIF

IF CFLAG:生殖力強化 == 1
	PRINTL 不妊治療中 -10%
	LOCAL -= 10
ENDIF

IF CFLAG:卵管結紮 == 1
	PRINTL 不妊手術済み -10%
	LOCAL -= 10
ENDIF

SIF LOCAL != 100
	PRINTFORML 特殊素質補正 = {LOCAL}\%

IF LOCAL < 10
	PRINTFORML 最低保証10\%
	LOCAL = 10
ENDIF

PRICE *= LOCAL
PRICE /= 100

IF LOCAL != 100
	SETCOLOR 100, 150, 250
	PRINTFORML 価格×特殊素質補正 = {PRICE}
	RESETCOLOR
ENDIF

;難易度処理
SELECTCASE FLAG:難易度
	;NORMAL
	CASE 1
		PRINTFORML 難易度:NORMAL ×100\%
		TIMES PRICE , 1.00
	;HYPER
	CASE 2
		PRINTFORML 難易度:HYPER ×80\%
		TIMES PRICE , 0.80
	;ANOTHER
	CASE 3
		PRINTFORML 難易度:ANOTHER ×60\%
		TIMES PRICE , 0.60
ENDSELECT
SETCOLOR 100, 150, 250
PRINTFORML 価格×難易度補正 = {PRICE}
RESETCOLOR
;価格×種族補正
SELECTCASE CSTR:種族
	;人間,人間(？),獣
	CASE "人間", "人間(？)", "獣"
		LOCAL = 100
	;宇宙人,獣人,人魚,ヴァンパイア
	CASE "宇宙人", "獣人", "人魚", "ヴァンパイア"
		LOCAL = 120
	;改造人間,機械,精霊,妖精
	CASE "改造人間", "機械", "精霊", "妖精"
		LOCAL = 110
	;天使,鳳凰
	CASE "天使", "鳳凰"
		LOCAL = 140
	;悪魔,魔術師
	CASE "悪魔", "魔術師"
		LOCAL = 130
	;神
	CASE "神"
		LOCAL = 150
	;ゾンビ
	CASE "ゾンビ"
		LOCAL = 95
ENDSELECT

IF LOCAL != 100
	PRINTL 
	SETCOLOR 100, 150, 250
	PRINTFORML 価格×種族(%CSTR:種族%)補正:{LOCAL}\% = {PRICE*LOCAL/100}
	RESETCOLOR
	PRICE *= LOCAL
	PRICE /= 100
ENDIF
PRINTL 

SETCOLOR 100, 150, 250
CALL PASELIL, @"価格:{PRICE}＄＋元の価格:{CFLAG:価格}＄ = {PRICE+CFLAG:価格}＄"
RESETCOLOR
PRICE += CFLAG:価格

;元助手
IF ISASSI:TARGET == 1
	PRINTFORML 助手経験あり -50\%
	TIMES PRICE , 0.50
ENDIF


DO
	;売却確認
	PRINTFORM %NAME:TARGET%は
	SETCOLOR 50, 150, 250
	CALL PASELI, @"{PRICE}＄"
	RESETCOLOR
	PRINTL で売れそうです
	PRINTL 売却しますか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	CFLAG:売却寸止め = 1
	INPUT
LOOP LOOPRES(0, 1)

SELECTCASE RESULT
	CASE 0
		SELECTCASE 月齢
			CASE 2300 TO 2599
				SIF FALLTYPE(TARGET) == 1
					CALL 実績解除, "下弦の月に願うは君への罰"
		ENDSELECT
		IF NO:TARGET != 0
			SELECTCASE CHARANAMEF(TARGET)
				;実績「Σかリアンを素数価格で売却する」
				CASE "Σ", "リアン"
					SIF ISPRIME(PRICE)
						CALL 実績解除, "I only Divide Me"
				;実績「緋浮美を140,000パセリ以上で売却する」
				CASE "梅桐 緋浮美"
					SIF PRICE >= 140000
						CALL 実績解除, "GOLD RUSH"
				;実績:オトベアを売り飛ばす
				CASE "オトベア"
					CALL 実績解除, "お前よりくまモンの方がいい！"
			ENDSELECT
		ENDIF
		;売却時のイベント口上
		CALL KOJO_MESSAGE_EVENT, "売却時"
		;売却額
		CALL PASELIW, @"結果、%NAME:TARGET%は{PRICE}＄で売れました"
		バンめし = 0
		MONEY += PRICE
		LOCAL = TARGET
		;アイドルユニットに属していた場合、そのユニットは解散
		SIF CFLAG:LOCAL:ユニット番号
			CALL BREAKUP, CFLAG:LOCAL:ユニット番号
		TARGET = LOCALTARGET
		ASSI = LOCALASSI
		DELCHARA LOCAL
		SIF LOCAL == LOCALTARGET
			TARGET = -1
		SIF LOCAL == LOCALASSI
			ASSI = -1
		SIF LOCAL < LOCALTARGET
			TARGET--
		SIF LOCAL < LOCALASSI
			ASSI--
	CASE 1
		TARGET = LOCALTARGET
		RESTART
ENDSELECT
