@CHARA_BUY
#DIM LRESULT

CALL CHARA_LIST

SIF RESULT == 10000
	RETURN 0

LRESULT = RESULT

CALL CHARA_MANUAL, LRESULT
CALL CHARA_BUY_CHECK, LRESULT


IF RESULT == 0
	RETURN 0
ELSE
	RESTART
ENDIF

;2017/07/11に、表示～選択までの全てを関数内で行うように
@CHARA_LIST, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 単位文字数
#DIMS 登場作品フィルタ
#DIMS キャラデザフィルタ
#DIMS 種族フィルタ

VARSET LOCAL, 0
;ARG
;0 = 奴隷購入用キャラリスト
;1 = その他とりあえず全キャラ羅列するためのキャラリスト
;2 = PFREEで主人を選択する際のキャラリスト
;3 = STRATEGYモードでパートナーを選択する際のキャラリスト
;4 = 段位認定エディットで表示する際のキャラリスト

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT
DRAWLINE

IF ARG == 0
	PRINTFORM {DAY}日目
	SELECTCASE TIME
		CASE 0
			PRINTL (昼)
		CASE 1
			PRINTL (夜)
	ENDSELECT

	CALL PASELI, @"所持金:{MONEY}＄"
	PRINT  難易度:
	SELECTCASE FLAG:難易度
		CASE 1
			PRINTL NORMAL
		CASE 2
			PRINTL HYPER
		CASE 3
			PRINTL ANOTHER
	ENDSELECT

	PRINTFORM 主人:%NAME:MASTER% 
	SIF TARGET >= 0
		PRINTFORM 奴隷:%NAME:TARGET% 
	SIF ASSI >= 0
		PRINTFORM 助手:%NAME:ASSI%
	PRINTL 
ENDIF


FOR LCOUNT, 0, 10000
	SIF LCOUNT == 0
		CONTINUE
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;アニマルは主人にできない
	SIF ARG == 2 && CSVTALENT(LCOUNT, GETNUM(TALENT, "アニマル"))
		CONTINUE
	IF ARG == 0 || ARG == 4
		SIF CCB:LCOUNT
			CONTINUE
		SIF FLAG:男女フィルタ != CSVTALENT(LCOUNT , GETNUM(TALENT, "性別")) && FLAG:男女フィルタ
			CONTINUE
		SIF FLAG:フィルタスイッチ１ == 1 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ１) == 0
			CONTINUE
		SIF FLAG:フィルタスイッチ１ == 2 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ１) == 1
			CONTINUE
		SIF FLAG:フィルタスイッチ２ == 1 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ２) == 0
			CONTINUE
		SIF FLAG:フィルタスイッチ２ == 2 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ２) == 1
			CONTINUE
		SIF FLAG:バストサイズフィルタ != CSVTALENT(LCOUNT, GETNUM(TALENT, "バストサイズ")) && FLAG:バストサイズフィルタ
			CONTINUE
		SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品２")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品３"))) == 0
			CONTINUE
		SIF キャラデザフィルタ != "" && GROUPMATCH(キャラデザフィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ")), CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ２"))) == 0
			CONTINUE
		SIF 種族フィルタ != "" && 種族フィルタ != CSVCSTR(LCOUNT, GETNUM(CSTR, "種族"))
			CONTINUE

		SELECTCASE CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))
			CASE FLAG:価格フィルタ下限 TO FLAG:価格フィルタ上限
				SIF FLAG:価格フィルタ == 2
					CONTINUE
			CASEELSE
				SIF FLAG:価格フィルタ == 1
					CONTINUE
		ENDSELECT

		SIF LCOUNT == NO:MASTER
			CONTINUE
	ENDIF

	IF LOCAL >= 30
		LOCAL:4 = 1
		BREAK
	ENDIF

	LOCAL:2++
	SIF LOCAL:2 <= (TFLAG:ページ数*30)
		CONTINUE

	IF LOCAL:1 >= 3
		;SIF LINEISEMPTY() == 0
			PRINTL 
		LOCAL:1 = 0
	ENDIF

	IF ARG == 1
		LOCALS = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)%
	ELSE
		LOCALS = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)% ({CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))}＄)
	ENDIF
	
	;口上表示設定、かつ口上の存在するキャラは青文字表示
	IF FLAG:口上表示
		IF ARG == 0
			TRYCCALLFORM EXIST_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ELSEIF ARG == 2
			TRYCCALLFORM EXIST_MASTER_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ELSEIF ARG == 3
			TRYCCALLFORM EXIST_STRATEGY_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ENDIF
	ENDIF
	IF ARG == 1
		PRINTFORM %LOCALS , 38 , LEFT%
	ELSE
		CALL PASELI, LOCALS
		;3人目は空白埋めしない
		IF LOCAL:1 != 2
			;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
			IF FLAG:パセリ表示
				単位文字数 = 2
			ELSEIF 通貨単位 != ""
				単位文字数 = STRLENS(通貨単位)
			ELSE
				単位文字数 = 1
			ENDIF
			;＄の分2文字と通貨単位分
			REPEAT 38-(STRLENS(LOCALS)-2)-単位文字数
				PRINT  
			REND
		ENDIF
	ENDIF
	RESETCOLOR
	
	LOCAL:1++

	LOCAL++
NEXT

PRINTL 

DRAWLINE
IF ARG == 0 || ARG == 4
	PRINTPLAIN 　CHARA  FILTER　
	PRINT [20000] フィルタ設定
	PRINTPLAIN 　
	PRINTFORM [20001] 男女フィルタ：
	SELECTCASE FLAG:男女フィルタ
		CASE 0
			PRINTL 切
		CASE 1
			PRINTL 男性を表示
		CASE 2
			PRINTL 女性を表示
	ENDSELECT
	PRINTPLAIN 　CUSTOM FILTER　
	PRINT [20003] カスタムフィルタ1：
	IF FLAG:カスタムフィルタ１ >= 0 && FLAG:フィルタスイッチ１
		PRINTFORM %TALENTNAME:(FLAG:カスタムフィルタ１)%
		SELECTCASE FLAG:フィルタスイッチ１
			CASE 1
				PRINT (表示)
			CASE 2
				PRINT (除外)
		ENDSELECT
	ELSE
		PRINT 切
	ENDIF
	PRINTPLAIN 　
	PRINT [20005] カスタムフィルタ2：
	IF FLAG:カスタムフィルタ２ >= 0 && FLAG:フィルタスイッチ２
		PRINTFORM %TALENTNAME:(FLAG:カスタムフィルタ２)%
		SELECTCASE FLAG:フィルタスイッチ２
			CASE 1
				PRINT (表示)
			CASE 2
				PRINT (除外)
		ENDSELECT
	ELSE
		PRINT 切
	ENDIF
	PRINTL 

	PRINTPLAIN 　PRICE  FILTER　
	PRINT [20007] 価格フィルタ：
	IF FLAG:価格フィルタ != 0
		CALL PASELI, @"{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
		SELECTCASE FLAG:価格フィルタ
			CASE 1
				PRINT (表示)
			CASE 2
				PRINT (除外)
		ENDSELECT
	ELSE
		PRINT 切
	ENDIF
	PRINTPLAIN 　
	PRINT [20008] バストサイズフィルタ：
	SELECTCASE FLAG:バストサイズフィルタ
		CASE 0
			PRINTL 切
		CASE 1
			PRINTL 絶壁キャラを表示
		CASE 2
			PRINTL 貧乳キャラを表示
		CASE 3
			PRINTL 並乳キャラを表示
		CASE 4
			PRINTL 巨乳キャラを表示
		CASE 5
			PRINTL 爆乳キャラを表示
	ENDSELECT
	
	PRINTPLAIN 　EXTRA  FILTER　
	PRINT [20010] 原作別カテゴリ：
	IF 登場作品フィルタ != ""
		PRINTFORM %登場作品フィルタ% 
	ELSE
		PRINT 切 
	ENDIF
	PRINT [20011] キャラデザイナー別カテゴリ：
	IF キャラデザフィルタ != ""
		PRINTFORM %キャラデザフィルタ% 
	ELSE
		PRINT 切 
	ENDIF
	PRINT [20012] 種族別カテゴリ：
	IF 種族フィルタ != ""
		PRINTFORML %種族フィルタ%
	ELSE
		PRINTL 切
	ENDIF
	DRAWLINE
ENDIF

PRINTPLAINFORM 　PAGE.{TFLAG:ページ数+1}　
IF TFLAG:ページ数 > 0
	PRINTLC [10001] 前のページ
ELSE
	PRINTLC  
ENDIF
SELECTCASE ARG
	CASE 0
		PRINT [10000] 購入しない
	CASE 2
		PRINT [10000] 変更しない
	CASE 4
		PRINT [10000] 最初から設定する
ENDSELECT
SIF LOCAL:4
	PRINTC [10009] 後のページ
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 10000
			SELECTCASE ARG
				CASE 0, 2, 4
					VARLINE = 0
					RETURN 10000
			ENDSELECT
		CASE 10001
			SIF TFLAG:ページ数
				TFLAG:ページ数--
		CASE 10009
			SIF LOCAL:4
				TFLAG:ページ数++
		CASE 20000
			SELECTCASE ARG
				CASE 0, 4
					CALL CHARA_FILTER
					BREAK
			ENDSELECT
		CASE 20001, 20003, 20005, 20007, 20008
			SELECTCASE ARG
				CASE 0, 4
					CALL CHARA_FILTER2, (RESULT-20000)
					TFLAG:ページ数 = 0
					BREAK
			ENDSELECT
		CASE 20010
			CALL 登場作品フィルタ設定, 登場作品フィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASE 20011
			CALL キャラデザフィルタ設定, キャラデザフィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASE 20012
			CALL 種族フィルタ設定, 種族フィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASEELSE
			IF EXISTCSV(RESULT)
				VARLINE = 0
				DRAWLINE
				RETURN RESULT
			ENDIF
	ENDSELECT
LOOP LOOPRES(10001, 10009)

RESTART

@CHARA_FILTER
TFLAG:ページ数 = 0
DRAWLINE
PRINTL キャラ購入画面にフィルタをかけることができます
PRINTL 
SELECTCASE FLAG:男女フィルタ
	CASE 0
		PRINTL [1] 男性キャラだけ表示する
	CASE 1
		PRINTL [1] 女性キャラだけ表示する
	CASE 2
		PRINTL [1] 全キャラ表示する
ENDSELECT

PRINT [2] カスタムフィルタ1を設定する（現在
IF FLAG:カスタムフィルタ１ >= 0
	PRINTFORML 【%TALENTNAME:(FLAG:カスタムフィルタ１)%】のフィルタ）
ELSE
	PRINTL フィルタは設定されていません）
ENDIF

PRINT [3] カスタムフィルタ1の切り替え（現在
SELECTCASE FLAG:フィルタスイッチ１
	CASE 0
		PRINTL オフ）
	CASE 1
		PRINTL 【マッチフィルタオン】）
	CASE 2
		PRINTL 【除外フィルタオン】）
ENDSELECT

PRINT [4] カスタムフィルタ2を設定する（現在
IF FLAG:カスタムフィルタ２ >= 0
	PRINTFORML 【%TALENTNAME:(FLAG:カスタムフィルタ２)%】のフィルタ）
ELSE
	PRINTL フィルタは設定されていません）
ENDIF

PRINT [5] カスタムフィルタ2の切り替え（現在
SELECTCASE FLAG:フィルタスイッチ２
	CASE 0
		PRINTL オフ）
	CASE 1
		PRINTL 【マッチフィルタオン】）
	CASE 2
		PRINTL 【除外フィルタオン】）
ENDSELECT

PRINT [6] 価格フィルタを設定する（現在
SELECTCASE FLAG:価格フィルタ
	CASE 0
		PRINTL オフ）
	CASEELSE
		CALL PASELIL, @"【{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄】のフィルタ）"
ENDSELECT

PRINT [7] 価格フィルタの切り替え（現在
SELECTCASE FLAG:価格フィルタ
	CASE 0
		PRINTL オフ）
	CASE 1
		PRINTL 【マッチフィルタオン】）
	CASE 2
		PRINTL 【除外フィルタオン】）
ENDSELECT
PRINT [8] バストサイズフィルタの切り替え（現在
SELECTCASE FLAG:バストサイズフィルタ
	CASE 0
		PRINTL オフ）
	CASE 1
		PRINTL 絶壁キャラを表示）
	CASE 2
		PRINTL 貧乳キャラを表示）
	CASE 3
		PRINTL 並乳キャラを表示）
	CASE 4
		PRINTL 巨乳キャラを表示）
	CASE 5
		PRINTL 爆乳キャラを表示）
ENDSELECT

PRINTL [0] 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1 TO 7
			CALL CHARA_FILTER2, RESULT
			TFLAG:ページ数 = 0
			RESTART
	ENDSELECT
LOOP 1

@CHARA_FILTER2, ARG
TFLAG:ページ数 = 0
SELECTCASE ARG
	CASE 1
		SELECTCASE FLAG:男女フィルタ
			CASE 0
				PRINTW 男性キャラのみ表示します
				FLAG:男女フィルタ = 1
			CASE 1
				PRINTW 女性キャラのみ表示します
				FLAG:男女フィルタ = 2
			CASE 2
				PRINTW 全キャラ表示します
				FLAG:男女フィルタ = 0
		ENDSELECT
	CASE 2
		DO
			DRAWLINE
			PRINTL フィルタをかけたい素質番号を入力してください。
			PRINTL [1000] 戻る
			PRINTL [1001] フィルタをリセット
			PRINTL [1002] 素質一覧表示
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RETURN 0
					CASE 1001
						PRINTW フィルタをリセットしました
						FLAG:フィルタスイッチ１ = 0
					CASE 1002
						CALL TALENT_PRINT
					CASEELSE
						IF TALENTNAME:RESULT == ""
							PRINTFORML {RESULT}番には素質が設定されていません
						ELSE
							FLAG:カスタムフィルタ１ = RESULT
							SIF FLAG:フィルタスイッチ１ == 0
								FLAG:フィルタスイッチ１ = 1
							PRINTFORMW 素質「%TALENTNAME:(FLAG:カスタムフィルタ１)%」にフィルタをかけました。
							RETURN 0
						ENDIF
				ENDSELECT
			LOOP LOOPRES(1000, 1001, 1002)
		LOOP LOOPRES(1000, 1001)
	CASE 3
		SELECTCASE FLAG:フィルタスイッチ１
			CASE 0
				PRINTW 条件にマッチするキャラのみ表示します
				FLAG:フィルタスイッチ１ = 1
			CASE 1
				PRINTW 条件にマッチするキャラを除外します
				FLAG:フィルタスイッチ１ = 2
			CASE 2
				PRINTW カスタムフィルタ1をオフにしました
				FLAG:フィルタスイッチ１ = 0
		ENDSELECT
	CASE 4
		DO
			DRAWLINE
			PRINTL フィルタをかけたい素質番号を入力してください。
			PRINTL [1000] 戻る
			PRINTL [1001] フィルタをリセット
			PRINTL [1002] 素質一覧表示
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RETURN 0
					CASE 1001
						PRINTW フィルタをリセットしました
						FLAG:フィルタスイッチ２ = 0
					CASE 1002
						CALL TALENT_PRINT
					CASEELSE
						IF TALENTNAME:RESULT == ""
							PRINTFORMW {RESULT}番には素質が設定されていません
						ELSE
							FLAG:カスタムフィルタ２ = RESULT
							SIF FLAG:フィルタスイッチ２ == 0
								FLAG:フィルタスイッチ２ = 1
							PRINTFORMW 素質「%TALENTNAME:(FLAG:カスタムフィルタ２)%」にフィルタをかけました。
							RETURN 0
						ENDIF
				ENDSELECT
			LOOP LOOPRES(1000, 1001, 1002)
		LOOP LOOPRES(1000, 1001)
	CASE 5
		SELECTCASE FLAG:フィルタスイッチ２
			CASE 0
				PRINTW 条件にマッチするキャラのみ表示します
				FLAG:フィルタスイッチ２ = 1
			CASE 1
				PRINTW 条件にマッチするキャラを除外します
				FLAG:フィルタスイッチ２ = 2
			CASE 2
				PRINTW カスタムフィルタ2をオフにしました
				FLAG:フィルタスイッチ２ = 0
		ENDSELECT
	CASE 6
		DO
			PRINTL 価格でフィルタをかけることができます
			CALL PASELIL, @"[101] 下限を設定する(現在{FLAG:価格フィルタ下限}＄)"
			CALL PASELIL, @"[102] 上限を設定する(現在{FLAG:価格フィルタ上限}＄)"
			CALL PASELIL, @"現在の設定 {FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
			DRAWLINE
			PRINTL [100] 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 100
						RETURN 0
					CASE 101
						PRINTL 数値を入力してください
						DO
							INPUT
							IF RESULT > FLAG:価格フィルタ上限
								PRINTL 上限より高い値を設定することはできません
							ELSE
								FLAG:価格フィルタ下限 = RESULT
								BREAK
							ENDIF
						LOOP 1
						BREAK
					CASE 102
						PRINTL 数値を入力してください
						DO
							INPUT
							IF RESULT < FLAG:価格フィルタ下限
								PRINTL 下限より低い値を設定することはできません
							ELSE
								FLAG:価格フィルタ上限 = RESULT
								BREAK
							ENDIF
						LOOP 1
						BREAK
				ENDSELECT
			LOOP LOOPRES(101, 102)
		LOOP 1
	CASE 7
		SELECTCASE FLAG:価格フィルタ
			CASE 0
				PRINTW 条件にマッチするキャラを除外します
				FLAG:価格フィルタ = 1
			CASE 1
				PRINTW 条件にマッチするキャラのみ表示します
				FLAG:価格フィルタ = 2
			CASE 2
				PRINTW 価格フィルタをオフにしました
				FLAG:価格フィルタ = 0
		ENDSELECT
	CASE 8
		DO
			PRINTL 表示したいキャラの胸のサイズを選んでください
			PRINTL [1] 絶壁
			PRINTL [2] 貧乳
			PRINTL [3] 並乳
			PRINTL [4] 巨乳
			PRINTL [5] 爆乳
			PRINTL [0] フィルタオフ
			INPUT
			SELECTCASE RESULT
				CASE 0 TO 5
					FLAG:バストサイズフィルタ = RESULT
					BREAK
				CASEELSE
					CONTINUE
			ENDSELECT
		LOOP 1
ENDSELECT

RETURN 0

@TALENT_PRINT
REPEAT 200
	SELECTCASE TALENTNAME:COUNT
		CASE "性別", "バストサイズ", ""
			CONTINUE
	ENDSELECT
	PRINTFORMLC [{COUNT}] %TALENTNAME:COUNT%
REND
PRINTL 

@登場作品フィルタ設定, 登場作品フィルタ
#DIMS REF 登場作品フィルタ
#DIMS DYNAMIC 原作, 300
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 登場作品
#DIM DYNAMIC 登場作品２
#DIM DYNAMIC 登場作品３
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

VARSET 原作

登場作品 = GETNUM(CSTR, "登場作品")
登場作品２ = GETNUM(CSTR, "登場作品２")
登場作品３ = GETNUM(CSTR, "登場作品３")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	IF CSVCSTR(LCOUNT, 登場作品) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品))) == -1
			原作:FINDELEMENT(原作, "", 0, 99, 1) = %CSVCSTR(LCOUNT, 登場作品)%
	ENDIF
	;登場作品２は原作:100～199の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品２) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品２))) == -1
			原作:FINDELEMENT(原作, "", 100, 199, 1) = %CSVCSTR(LCOUNT, 登場作品２)%
	ENDIF
	;登場作品３は原作:200～299の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品３) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品３))) == -1
			原作:FINDELEMENT(原作, "", 200, 299, 1) = %CSVCSTR(LCOUNT, 登場作品３)%
	ENDIF
NEXT

DO
	始点 = FINDELEMENT(原作, "", 0, VARSIZE("原作"), 1)
	FOR LCOUNT, 始点, VARSIZE("原作")
		IF 原作:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		SIF LCOUNT == VARSIZE("原作")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 原作, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラの登場作品を選んでください
FOR LCOUNT, 0, VARSIZE("原作")
	SIF 原作:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %原作:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　原作別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("原作")
			IF 原作:RESULT != ""
				登場作品フィルタ = %原作:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			登場作品フィルタ = 
			RETURN
	ENDSELECT
LOOP 1

@キャラデザフィルタ設定, キャラデザフィルタ
#DIMS REF キャラデザフィルタ
#DIMS DYNAMIC 作画, 1000
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC キャラデザ
#DIM DYNAMIC キャラデザ２
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

VARSET 作画

キャラデザ = GETNUM(CSTR, "キャラデザ")
キャラデザ２ = GETNUM(CSTR, "キャラデザ２")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	IF CSVCSTR(LCOUNT, キャラデザ) != ""
		SIF FINDELEMENT(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ)), 0, VARSIZE("作画"), 1) == -1
			作画:FINDELEMENT(作画, "", 0, 499, 1) = %CSVCSTR(LCOUNT, キャラデザ)%
	ENDIF
	;キャラデザ２は作画:500～999の範囲に入れる
	IF CSVCSTR(LCOUNT, キャラデザ２) != ""
		SIF FINDELEMENT(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ２)), 0, VARSIZE("作画"), 1) == -1
			作画:FINDELEMENT(作画, "", 500, 999, 1) = %CSVCSTR(LCOUNT, キャラデザ２)%
	ENDIF
NEXT

DO
	始点 = FINDELEMENT(作画, "", 0, VARSIZE("作画"), 1)
	FOR LCOUNT, 始点, VARSIZE("作画")
		IF 作画:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		SIF LCOUNT == VARSIZE("作画")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 作画, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラのデザイナーを選んでください
FOR LCOUNT, 0, VARSIZE("作画")
	SIF 作画:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %作画:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　キャラデザイナー別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("作画")
			IF 作画:RESULT != ""
				キャラデザフィルタ = %作画:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			キャラデザフィルタ = 
			RETURN
	ENDSELECT
LOOP 1

@種族フィルタ設定, 種族フィルタ
#DIMS REF 種族フィルタ
#DIMS DYNAMIC 種族名, 50
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 種族
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

VARSET 種族名

種族 = GETNUM(CSTR, "種族")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	IF CSVCSTR(LCOUNT, 種族) != ""
		SIF FINDELEMENT(種族名, ESCAPE(CSVCSTR(LCOUNT, 種族)), 0, VARSIZE("種族名"), 1) == -1
			種族名:FINDELEMENT(種族名, "", 0, VARSIZE("種族名"), 1) = %CSVCSTR(LCOUNT, 種族)%
	ENDIF
NEXT

PRINTL □表示したいキャラの種族を選んでください
FOR LCOUNT, 0, VARSIZE("種族名")
	SIF 種族名:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %種族名:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　種族別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("種族名")
			IF 種族名:RESULT != ""
				種族フィルタ = %種族名:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			種族フィルタ = 
			RETURN
	ENDSELECT
LOOP 1

