@CHARA_BUY
#DIM LRESULT

CALL CHARA_LIST

SIF RESULT == 10000
	RETURN 0

LRESULT = RESULT

CALL CHARA_MANUAL, LRESULT
CALL CHARA_BUY_CHECK, LRESULT


IF RESULT == 0
	RETURN 0
ELSE
	RESTART
ENDIF

;2017/07/11に、表示～選択までの全てを関数内で行うように
@CHARA_LIST, ARG, ARG:1
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 単位文字数
#DIM DYNAMIC 価格上限
#DIM DYNAMIC 選択可能, 10000
#DIMS 登場作品フィルタ
#DIMS キャラデザフィルタ
#DIMS 種族フィルタ

;選択可能キャラのフラグを立てる
FOR LCOUNT, 0, 10000
	SIF LCOUNT == 0 && ARG != 1
		CONTINUE
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;隠しキャラは選べない
	SIF CSVTALENT(LCOUNT, GETNUM(TALENT, "隠しキャラ"))
		CONTINUE
	;アニマルは主人にできない
	SIF ARG == 2 && CSVTALENT(LCOUNT, GETNUM(TALENT, "アニマル"))
		CONTINUE
	;以上を抜けると選択可能
	;すなわちキーボードの直接入力によるフィルタの無視は仕様とする
	選択可能:LCOUNT = 1
NEXT

$リスト表示

VARSET LOCAL, 0
;ARG
;0 = 奴隷購入用キャラリスト
;1 = その他とりあえず全キャラ羅列するためのキャラリスト
;2 = PFREEで主人を選択する際のキャラリスト
;3 = STRATEGYモードでパートナーを選択する際のキャラリスト
;4 = 段位認定エディットで表示する際のキャラリスト

;ARG:1がTRUEならCASEELSEを通す

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT
DRAWLINE

IF ARG == 0
	PRINT 　
	CALL 日付表示
	IF LANGUAGE == "ENG"
		CALL PASELI, @"Moneys:{MONEY}＄"
		PRINTFORM  Difficulty:%難易度()% 
		PRINTFORM Master:%NAMEDISP(MASTER)% 
		SIF TARGET >= 0
			PRINTFORM Target:%NAMEDISP(TARGET)% 
		SIF ASSI >= 0
			PRINTFORM Assistant:%NAMEDISP(ASSI)%
		PRINTL 
	ELSE
		CALL PASELI, @"所持金:{MONEY}＄"
		PRINTFORM  難易度:%難易度()% 
		PRINTFORM 主人:%NAME:MASTER% 
		SIF TARGET >= 0
			PRINTFORM 奴隷:%NAME:TARGET% 
		SIF ASSI >= 0
			PRINTFORM 助手:%NAME:ASSI%
		PRINTL 
	ENDIF
	DRAWLINE 
ENDIF

SIF GROUPMATCH(ARG, 0, 2, 3)
	CALL SHOW_MODS

IF FLAG:価格フィルタ所持金依存
	価格上限 = MONEY
ELSE
	価格上限 = FLAG:価格フィルタ上限
ENDIF

FOR LCOUNT, 0, 10000
	;選択可能じゃないキャラは非表示
	SIF !選択可能:LCOUNT
		CONTINUE
	;フィルタによる除外
	IF GROUPMATCH(ARG, 0, 4)
		SIF CCB:LCOUNT
			CONTINUE
		SIF FLAG:男女フィルタ != CSVTALENT(LCOUNT , GETNUM(TALENT, "性別")) && FLAG:男女フィルタ
			CONTINUE
		SIF FLAG:フィルタスイッチ１ == 1 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ１) == 0
			CONTINUE
		SIF FLAG:フィルタスイッチ１ == 2 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ１) == 1
			CONTINUE
		SIF FLAG:フィルタスイッチ２ == 1 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ２) == 0
			CONTINUE
		SIF FLAG:フィルタスイッチ２ == 2 && CSVTALENT(LCOUNT, FLAG:カスタムフィルタ２) == 1
			CONTINUE
		SIF FLAG:バストサイズフィルタ != CSVTALENT(LCOUNT, GETNUM(TALENT, "バストサイズ")) && FLAG:バストサイズフィルタ
			CONTINUE
		SIF 登場作品フィルタ != "" && GROUPMATCH(登場作品フィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品２")), CSVCSTR(LCOUNT, GETNUM(CSTR, "登場作品３"))) == 0
			CONTINUE
		SIF キャラデザフィルタ != "" && GROUPMATCH(キャラデザフィルタ, CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ")), CSVCSTR(LCOUNT, GETNUM(CSTR, "キャラデザ２"))) == 0
			CONTINUE
		SIF 種族フィルタ != "" && 種族フィルタ != CSVCSTR(LCOUNT, GETNUM(CSTR, "種族"))
			CONTINUE

		SELECTCASE CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))
			CASE FLAG:価格フィルタ下限 TO 価格上限
				SIF FLAG:価格フィルタ == 2
					CONTINUE
			CASEELSE
				SIF FLAG:価格フィルタ == 1
					CONTINUE
		ENDSELECT

		SIF LCOUNT == NO:MASTER
			CONTINUE
	ENDIF

	;30人表示したら次ページフラグ立ててBREAK
	IF LOCAL >= 30
		LOCAL:4 = 1
		BREAK
	ENDIF

	;ページ数に該当するカウントまでスキップ
	LOCAL:2++
	SIF LOCAL:2 <= (TFLAG:ページ数*30)
		CONTINUE

	;3人表示したら改行
	IF LOCAL:1 >= 3
		;SIF LINEISEMPTY() == 0
			PRINTL 
		LOCAL:1 = 0
	ENDIF

	IF ARG != 0
		IF LANGUAGE == "ENG"
			LOCALS = [{LCOUNT, 4}] %CSVENGNAME(LCOUNT)%
		ELSE
			LOCALS = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)%
		ENDIF
	ELSE
		IF LANGUAGE == "ENG"
			LOCALS = [{LCOUNT, 4}] %CSVENGNAME(LCOUNT)% ({CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))}＄)
		ELSE
			LOCALS = [{LCOUNT, 4}] %CSVNAMEF(LCOUNT)% ({CSVCFLAG(LCOUNT, GETNUM(CFLAG, "価格"))}＄)
		ENDIF
	ENDIF
	
	;口上表示設定、かつ口上の存在するキャラは青文字表示
	IF FLAG:口上表示
		IF ARG == 0
			TRYCCALLFORM EXIST_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ELSEIF ARG == 2
			TRYCCALLFORM EXIST_MASTER_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ELSEIF ARG == 3
			TRYCCALLFORM EXIST_STRATEGY_KOJO_K{LCOUNT}
				SETCOLOR 100, 150, 200
			CATCH
			ENDCATCH
		ENDIF
	ENDIF
	IF ARG == 1
		PRINTFORM %LOCALS , 38 , LEFT%
	ELSE
		CALL PASELI, LOCALS
		;3人目は空白埋めしない
		IF LOCAL:1 != 2
			;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
			IF FLAG:パセリ表示
				単位文字数 = 2
			ELSEIF 通貨単位 != ""
				単位文字数 = STRLENS(通貨単位)
			ELSE
				単位文字数 = 1
			ENDIF
			;＄の分2文字と通貨単位分
			REPEAT 44-(STRLENS(LOCALS)-2)-単位文字数
				PRINT  
			REND
		ENDIF
	ENDIF
	RESETCOLOR
	
	LOCAL:1++

	LOCAL++
NEXT

PRINTL 

DRAWLINE
IF GROUPMATCH(ARG, 0, 4)
	IF LANGUAGE == "ENG"
		PRINTPLAIN 　CHARA  FILTER　
		PRINT [20000] Filter Setting
		PRINTPLAIN 　
		PRINTFORM [20001] Gender Filter：
		SELECTCASE FLAG:男女フィルタ
			CASE 0
				PRINTL None
			CASE 1
				PRINTL Show Male
			CASE 2
				PRINTL Show Female
		ENDSELECT
		PRINTPLAIN 　CUSTOM FILTER　
		PRINT [20003] Talent Filter1：
		IF FLAG:カスタムフィルタ１ >= 0 && FLAG:フィルタスイッチ１
			PRINTFORM %TALENTDISP(FLAG:カスタムフィルタ１)%
			SELECTCASE FLAG:フィルタスイッチ１
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTPLAIN 　
		PRINT [20005] Talent Filter2：
		IF FLAG:カスタムフィルタ２ >= 0 && FLAG:フィルタスイッチ２
			PRINTFORM %TALENTDISP(FLAG:カスタムフィルタ２)%
			SELECTCASE FLAG:フィルタスイッチ２
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTL 

		PRINTPLAIN 　             　
		PRINT [20007] Price Filter：
		IF FLAG:価格フィルタ != 0
			IF FLAG:価格フィルタ所持金依存
				CALL PASELI, @"{FLAG:価格フィルタ下限}＄ ～ Moneys"
			ELSE
				CALL PASELI, @"{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
			ENDIF
			SELECTCASE FLAG:価格フィルタ
				CASE 1
					PRINT (Show)
				CASE 2
					PRINT (Hide)
			ENDSELECT
		ELSE
			PRINT None
		ENDIF
		PRINTPLAIN 　
		PRINT [20008] BreastSize Filter：
		SELECTCASE FLAG:バストサイズフィルタ
			CASE 0
				PRINTL None
			CASE 1
				PRINTL Show none breast
			CASE 2
				PRINTL Show little breast
			CASE 3
				PRINTL Show basic breast
			CASE 4
				PRINTL Show large breast
			CASE 5
				PRINTL Show extra large breast
			CASE 6
				PRINTL Show huge breast
		ENDSELECT
		
		PRINTPLAIN 　EXTRA  FILTER　
		PRINT [20010] Original Category：
		IF 登場作品フィルタ != ""
			PRINTFORM %登場作品フィルタ% 
		ELSE
			PRINT None 
		ENDIF
		PRINT [20011] Designer Category：
		IF キャラデザフィルタ != ""
			PRINTFORM %キャラデザフィルタ% 
		ELSE
			PRINT None 
		ENDIF
		PRINT [20012] Race Category：
		IF 種族フィルタ != ""
			PRINTFORML %種族フィルタ%
		ELSE
			PRINTL None
		ENDIF
		DRAWLINE
	ELSE
		PRINTPLAIN 　CHARA  FILTER　
		PRINT [20000] フィルタ設定
		PRINTPLAIN 　
		PRINTFORM [20001] 男女フィルタ：
		SELECTCASE FLAG:男女フィルタ
			CASE 0
				PRINTL 切
			CASE 1
				PRINTL 男性を表示
			CASE 2
				PRINTL 女性を表示
		ENDSELECT
		PRINTPLAIN 　CUSTOM FILTER　
		PRINT [20003] カスタムフィルタ1：
		IF FLAG:カスタムフィルタ１ >= 0 && FLAG:フィルタスイッチ１
			PRINTFORM %TALENTNAME:(FLAG:カスタムフィルタ１)%
			SELECTCASE FLAG:フィルタスイッチ１
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		PRINTPLAIN 　
		PRINT [20005] カスタムフィルタ2：
		IF FLAG:カスタムフィルタ２ >= 0 && FLAG:フィルタスイッチ２
			PRINTFORM %TALENTNAME:(FLAG:カスタムフィルタ２)%
			SELECTCASE FLAG:フィルタスイッチ２
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		PRINTL 

		PRINTPLAIN 　PRICE  FILTER　
		PRINT [20007] 価格フィルタ：
		IF FLAG:価格フィルタ != 0
			IF FLAG:価格フィルタ所持金依存
				CALL PASELI, @"{FLAG:価格フィルタ下限}＄ ～ 所持金"
			ELSE
				CALL PASELI, @"{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
			ENDIF
			SELECTCASE FLAG:価格フィルタ
				CASE 1
					PRINT (表示)
				CASE 2
					PRINT (除外)
			ENDSELECT
		ELSE
			PRINT 切
		ENDIF
		PRINTPLAIN 　
		PRINT [20008] バストサイズフィルタ：
		SELECTCASE FLAG:バストサイズフィルタ
			CASE 0
				PRINTL 切
			CASE 1
				PRINTL 絶壁キャラを表示
			CASE 2
				PRINTL 貧乳キャラを表示
			CASE 3
				PRINTL 並乳キャラを表示
			CASE 4
				PRINTL 巨乳キャラを表示
			CASE 5
				PRINTL 豊乳キャラを表示
			CASE 6
				PRINTL 爆乳キャラを表示
		ENDSELECT
		
		PRINTPLAIN 　EXTRA  FILTER　
		PRINT [20010] 原作別カテゴリ：
		IF 登場作品フィルタ != ""
			PRINTFORM %登場作品フィルタ% 
		ELSE
			PRINT 切 
		ENDIF
		PRINT [20011] キャラデザイナー別カテゴリ：
		IF キャラデザフィルタ != ""
			PRINTFORM %キャラデザフィルタ% 
		ELSE
			PRINT 切 
		ENDIF
		PRINT [20012] 種族別カテゴリ：
		IF 種族フィルタ != ""
			PRINTFORML %種族フィルタ%
		ELSE
			PRINTL 切
		ENDIF
		DRAWLINE
	ENDIF
ENDIF

PRINTPLAINFORM 　PAGE.{TFLAG:ページ数+1}　
IF TFLAG:ページ数 > 0
	IF LANGUAGE == "ENG"
		PRINTLC [10001] Prev Page
	ELSE
		PRINTLC [10001] 前のページ
	ENDIF
ELSE
	PRINTLC  
ENDIF
IF LANGUAGE == "ENG"
	SELECTCASE ARG
		CASE 0
			PRINT [10000] Don't unlock
		CASE 2
			PRINT [10000] Don't change
		CASE 4
			PRINT [10000] Set from the beggining
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 0
			PRINT [10000] 購入しない
		CASE 2
			PRINT [10000] 変更しない
		CASE 4
			PRINT [10000] 最初から設定する
	ENDSELECT
ENDIF
IF LOCAL:4
	IF LANGUAGE == "ENG"
		PRINTC [10009] Next Page
	ELSE
		PRINTC [10009] 後のページ
	ENDIF
ENDIF
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 10000
			SELECTCASE ARG
				CASE 0, 2, 4
					VARLINE = 0
					RETURN 10000
			ENDSELECT
		CASE 10001
			SIF TFLAG:ページ数
				TFLAG:ページ数--
		CASE 10009
			SIF LOCAL:4
				TFLAG:ページ数++
		CASE 20000
			SELECTCASE ARG
				CASE 0, 4
					CALL CHARA_FILTER
					BREAK
			ENDSELECT
		CASE 20001, 20003, 20005, 20007, 20008
			SELECTCASE ARG
				CASE 0, 4
					CALL CHARA_FILTER2, (RESULT-20000)
					TFLAG:ページ数 = 0
					BREAK
			ENDSELECT
		CASE 20010
			CALL 登場作品フィルタ設定, 登場作品フィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASE 20011
			CALL キャラデザフィルタ設定, キャラデザフィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASE 20012
			CALL 種族フィルタ設定, 種族フィルタ
			TFLAG:ページ数 = 0
			BREAK
		CASEELSE
			IF EXISTCSV(RESULT) && 選択可能:RESULT
				VARLINE = 0
				DRAWLINE
				RETURN RESULT
			ELSEIF ARG:1
				VARLINE = 0
				DRAWLINE
				RETURN RESULT
			ENDIF
	ENDSELECT
LOOP LOOPRES(10001, 10009)

GOTO リスト表示

@CHARA_FILTER
TFLAG:ページ数 = 0
DRAWLINE
IF LANGUAGE == "ENG"
	PRINTL You can set filter the characters.
ELSE
	PRINTL キャラ購入画面にフィルタをかけることができます
ENDIF
PRINTL 
IF LANGUAGE == "ENG"
	SELECTCASE FLAG:男女フィルタ
		CASE 0
			PRINTL [1] Show male characters only
		CASE 1
			PRINTL [1] Show female characters only
		CASE 2
			PRINTL [1] Show all characters
	ENDSELECT
ELSE
	SELECTCASE FLAG:男女フィルタ
		CASE 0
			PRINTL [1] 男性キャラだけ表示する
		CASE 1
			PRINTL [1] 女性キャラだけ表示する
		CASE 2
			PRINTL [1] 全キャラ表示する
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [2] Set talent filter 1(Now:
	IF FLAG:カスタムフィルタ１ >= 0
		PRINTFORML 【%TALENTDISP(FLAG:カスタムフィルタ１)%】)
	ELSE
		PRINTL None)
	ENDIF
ELSE
	PRINT [2] カスタムフィルタ1を設定する（現在
	IF FLAG:カスタムフィルタ１ >= 0
		PRINTFORML 【%TALENTDISP(FLAG:カスタムフィルタ１)%】のフィルタ）
	ELSE
		PRINTL フィルタは設定されていません）
	ENDIF
ENDIF

IF LANGUAGE == "ENG"
	PRINT [3] Switch talent filter 1(Now
	SELECTCASE FLAG:フィルタスイッチ１
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show filter ON】)
		CASE 2
			PRINTL 【Hide filter ON】)
	ENDSELECT
ELSE
	PRINT [3] カスタムフィルタ1の切り替え（現在
	SELECTCASE FLAG:フィルタスイッチ１
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [4] Set talent filter 2(Now:
	IF FLAG:カスタムフィルタ２ >= 0
		PRINTFORML 【%TALENTDISP(FLAG:カスタムフィルタ２)%】)
	ELSE
		PRINTL None)
	ENDIF
ELSE
	PRINT [4] カスタムフィルタ2を設定する（現在
	IF FLAG:カスタムフィルタ２ >= 0
		PRINTFORML 【%TALENTDISP(FLAG:カスタムフィルタ２)%】のフィルタ）
	ELSE
		PRINTL フィルタは設定されていません）
	ENDIF
ENDIF

IF LANGUAGE == "ENG"
	PRINT [5] Switch talent filter 2(Now
	SELECTCASE FLAG:フィルタスイッチ２
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show filter ON】)
		CASE 2
			PRINTL 【Hide filter ON】)
	ENDSELECT
ELSE
	PRINT [5] カスタムフィルタ2の切り替え（現在
	SELECTCASE FLAG:フィルタスイッチ２
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [6] Set price filter(Now
	SELECTCASE FLAG:価格フィルタ
		CASE 0
			PRINTL :None)
		CASEELSE
			IF FLAG:価格フィルタ所持金依存
				CALL PASELIL, @"【{FLAG:価格フィルタ下限}＄ ～ Moneys】)"
			ELSE
				CALL PASELIL, @"【{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄】)"
			ENDIF
	ENDSELECT
ELSE
	PRINT [6] 価格フィルタを設定する（現在
	SELECTCASE FLAG:価格フィルタ
		CASE 0
			PRINTL オフ）
		CASEELSE
			IF FLAG:価格フィルタ所持金依存
				CALL PASELIL, @"【{FLAG:価格フィルタ下限}＄ ～ 所持金】のフィルタ）"
			ELSE
				CALL PASELIL, @"【{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄】のフィルタ）"
			ENDIF
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [7] Switch price filter(Now
	SELECTCASE FLAG:価格フィルタ
		CASE 0
			PRINTL :OFF)
		CASE 1
			PRINTL 【Show filter ON】）
		CASE 2
			PRINTL 【Hide filter ON】）
	ENDSELECT
ELSE
	PRINT [7] 価格フィルタの切り替え（現在
	SELECTCASE FLAG:価格フィルタ
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 【マッチフィルタオン】）
		CASE 2
			PRINTL 【除外フィルタオン】）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINT [8] BriestSize filter(Now:
	SELECTCASE FLAG:バストサイズフィルタ
		CASE 0
			PRINTL None)
		CASE 1
			PRINTL Show none breast characters)
		CASE 2
			PRINTL Show little breast characters)
		CASE 3
			PRINTL Show basic breast characters)
		CASE 4
			PRINTL Show large breast characters)
		CASE 5
			PRINTL Show extra large breast characters)
		CASE 6
			PRINTL Show huge breast characters)
	ENDSELECT
ELSE
	PRINT [8] バストサイズフィルタの切り替え（現在
	SELECTCASE FLAG:バストサイズフィルタ
		CASE 0
			PRINTL オフ）
		CASE 1
			PRINTL 絶壁キャラを表示）
		CASE 2
			PRINTL 貧乳キャラを表示）
		CASE 3
			PRINTL 並乳キャラを表示）
		CASE 4
			PRINTL 巨乳キャラを表示）
		CASE 5
			PRINTL 豊乳キャラを表示）
		CASE 6
			PRINTL 爆乳キャラを表示）
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINTL [0] Back
ELSE
	PRINTL [0] 戻る
ENDIF
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1 TO 7
			CALL CHARA_FILTER2, RESULT
			TFLAG:ページ数 = 0
			RESTART
	ENDSELECT
LOOP 1

@CHARA_FILTER2, ARG
TFLAG:ページ数 = 0
SELECTCASE ARG
	CASE 1
		SELECTCASE FLAG:男女フィルタ
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show male characters only.
				ELSE
					PRINTW 男性キャラのみ表示します
				ENDIF
				FLAG:男女フィルタ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Show female characters only.
				ELSE
					PRINTW 女性キャラのみ表示します
				ENDIF
				FLAG:男女フィルタ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Show all characters.
				ELSE
					PRINTW 全キャラ表示します
				ENDIF
				FLAG:男女フィルタ = 0
		ENDSELECT
	CASE 2
		DO
			CALL TALENT_PRINT
			DRAWLINE
			IF LANGUAGE == "ENG"
				PRINT [1000] Back 
				PRINTL [1001] Clear filter
			ELSE
				PRINT [1000] 戻る 
				PRINTL [1001] フィルタをリセット
			ENDIF
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RETURN 0
					CASE 1001
						IF LANGUAGE == "ENG"
							PRINTW Filter was been cleared.
						ELSE
							PRINTW フィルタをリセットしました
						ENDIF
						FLAG:フィルタスイッチ１ = 0
					CASEELSE
						IF TALENTNAME:RESULT == ""
							IF LANGUAGE == "ENG"
								PRINTFORML Does not define No.{RESULT} in TALENT.
							ELSE
								PRINTFORML {RESULT}番には素質が設定されていません
							ENDIF
						ELSE
							FLAG:カスタムフィルタ１ = RESULT
							SIF FLAG:フィルタスイッチ１ == 0
								FLAG:フィルタスイッチ１ = 1
							IF LANGUAGE == "ENG"
								PRINTFORMW Now filtering "%TALENTDISP(FLAG:カスタムフィルタ１)%"
							ELSE
								PRINTFORMW 素質「%TALENTDISP(FLAG:カスタムフィルタ１)%」にフィルタをかけました。
							ENDIF
							RETURN 0
						ENDIF
				ENDSELECT
			LOOP LOOPRES(1000, 1001, 1002)
		LOOP LOOPRES(1000, 1001)
	CASE 3
		SELECTCASE FLAG:フィルタスイッチ１
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				FLAG:フィルタスイッチ１ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				FLAG:フィルタスイッチ１ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned Talent filter 1 off.
				ELSE
					PRINTW カスタムフィルタ1をオフにしました
				ENDIF
				FLAG:フィルタスイッチ１ = 0
		ENDSELECT
	CASE 4
		DO
			CALL TALENT_PRINT
			DRAWLINE
			IF LANGUAGE == "ENG"
				PRINT [1000] Back 
				PRINTL [1001] Clear filter
			ELSE
				PRINT [1000] 戻る 
				PRINTL [1001] フィルタをリセット
			ENDIF
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RETURN 0
					CASE 1001
						IF LANGUAGE == "ENG"
							PRINTW Filter was been cleared.
						ELSE
							PRINTW フィルタをリセットしました
						ENDIF
						FLAG:フィルタスイッチ２ = 0
					CASEELSE
						IF TALENTNAME:RESULT == ""
							IF LANGUAGE == "ENG"
								PRINTFORML Does not define No.{RESULT} in TALENT.
							ELSE
								PRINTFORML {RESULT}番には素質が設定されていません
							ENDIF
						ELSE
							FLAG:カスタムフィルタ２ = RESULT
							SIF FLAG:フィルタスイッチ２ == 0
								FLAG:フィルタスイッチ２ = 1
							IF LANGUAGE == "ENG"
								PRINTFORMW Now filtering "%TALENTDISP(FLAG:カスタムフィルタ２)%"
							ELSE
								PRINTFORMW 素質「%TALENTDISP(FLAG:カスタムフィルタ２)%」にフィルタをかけました。
							ENDIF
							RETURN 0
						ENDIF
				ENDSELECT
			LOOP LOOPRES(1000, 1001, 1002)
		LOOP LOOPRES(1000, 1001)
	CASE 5
		SELECTCASE FLAG:フィルタスイッチ２
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				FLAG:フィルタスイッチ２ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide the characters talent have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				FLAG:フィルタスイッチ２ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned Talent filter 2 off.
				ELSE
					PRINTW カスタムフィルタ2をオフにしました
				ENDIF
				FLAG:フィルタスイッチ２ = 0
		ENDSELECT
	CASE 6
		DO
			IF LANGUAGE == "ENG"
				PRINTL You can filter for price.
				CALL PASELIL, @"[101] Set price the min(Now:{FLAG:価格フィルタ下限}＄)"
				IF FLAG:価格フィルタ所持金依存
					PRINTL [102] Set price your money
				ELSE
					CALL PASELIL, @"[102] Set price the max(Now:{FLAG:価格フィルタ上限}＄)"
				ENDIF
				SIF !FLAG:価格フィルタ所持金依存
					PRINTL [103] Set price the max(Your money)
				IF FLAG:価格フィルタ所持金依存
					CALL PASELIL, @"Now:{FLAG:価格フィルタ下限}＄ ～ {MONEY}"
				ELSE
					CALL PASELIL, @"Now:{FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
				ENDIF
				DRAWLINE
				PRINTL [100] Back
			ELSE
				PRINTL 価格でフィルタをかけることができます
				CALL PASELIL, @"[101] 下限を設定する(現在{FLAG:価格フィルタ下限}＄)"
				IF FLAG:価格フィルタ所持金依存
					PRINTL [102] 上限を設定する(現在所持金まで)
				ELSE
					CALL PASELIL, @"[102] 上限を設定する(現在{FLAG:価格フィルタ上限}＄)"
				ENDIF
				SIF !FLAG:価格フィルタ所持金依存
					PRINTL [103] 上限を所持金にする
				IF FLAG:価格フィルタ所持金依存
					CALL PASELIL, @"現在の設定 {FLAG:価格フィルタ下限}＄ ～ 所持金"
				ELSE
					CALL PASELIL, @"現在の設定 {FLAG:価格フィルタ下限}＄ ～ {FLAG:価格フィルタ上限}＄"
				ENDIF
				DRAWLINE
				PRINTL [100] 戻る
			ENDIF
			DO
				INPUT
				SELECTCASE RESULT
					CASE 100
						RETURN 0
					CASE 101
						IF LANGUAGE == "ENG"
							PRINTL Please enter min price.
						ELSE
							PRINTL 数値を入力してください
						ENDIF
						DO
							INPUT
							IF RESULT > FLAG:価格フィルタ上限 && !FLAG:価格フィルタ所持金依存
								IF LANGUAGE == "ENG"
									PRINTL Can't set expensive than max price.
								ELSE
									PRINTL 上限より高い値を設定することはできません
								ENDIF
							ELSE
								FLAG:価格フィルタ下限 = RESULT
								BREAK
							ENDIF
						LOOP 1
						BREAK
					CASE 102
						FLAG:価格フィルタ所持金依存 = 0
						IF LANGUAGE == "ENG"
							PRINTL Please enter max price.
						ELSE
							PRINTL 数値を入力してください
						ENDIF
						DO
							INPUT
							IF RESULT < FLAG:価格フィルタ下限
								IF LANGUAGE == "ENG"
									PRINTL Can't set lower than min price.
								ELSE
									PRINTL 下限より低い値を設定することはできません
								ENDIF
							ELSE
								FLAG:価格フィルタ上限 = RESULT
								BREAK
							ENDIF
						LOOP 1
						BREAK
					CASE 103
						FLAG:価格フィルタ所持金依存 = 1
						BREAK
				ENDSELECT
			LOOP LOOPRES(101, 102)
		LOOP 1
	CASE 7
		SELECTCASE FLAG:価格フィルタ
			CASE 0
				IF LANGUAGE == "ENG"
					PRINTW Show characters range have.
				ELSE
					PRINTW 条件にマッチするキャラのみ表示します
				ENDIF
				FLAG:価格フィルタ = 1
			CASE 1
				IF LANGUAGE == "ENG"
					PRINTW Hide characters range have.
				ELSE
					PRINTW 条件にマッチするキャラを除外します
				ENDIF
				FLAG:価格フィルタ = 2
			CASE 2
				IF LANGUAGE == "ENG"
					PRINTW Turned price filter OFF.
				ELSE
					PRINTW 価格フィルタをオフにしました
				ENDIF
				FLAG:価格フィルタ = 0
		ENDSELECT
	CASE 8
		DO
			IF LANGUAGE == "ENG"
				PRINTL Want size do you want?
				PRINTL [1] None breast
				PRINTL [2] Little breast
				PRINTL [3] Basic breast
				PRINTL [4] Large breast
				PRINTL [5] XLarge breast
				PRINTL [6] Huge breast
				PRINTL [0] Filter OFF
			ELSE
				PRINTL 表示したいキャラの胸のサイズを選んでください
				PRINTL [1] 絶壁
				PRINTL [2] 貧乳
				PRINTL [3] 並乳
				PRINTL [4] 巨乳
				PRINTL [5] 豊乳
				PRINTL [6] 爆乳
				PRINTL [0] フィルタオフ
			ENDIF
			INPUT
			SELECTCASE RESULT
				CASE 0 TO 6
					FLAG:バストサイズフィルタ = RESULT
					BREAK
				CASEELSE
					CONTINUE
			ENDSELECT
		LOOP 1
ENDSELECT

RETURN 0

@TALENT_PRINT
REPEAT 200
	SELECTCASE TALENTNAME:COUNT
		CASE "性別", "バストサイズ", ""
			CONTINUE
	ENDSELECT
	PRINTFORMLC [{COUNT}] %TALENTDISP(COUNT)%
REND
PRINTL 

@登場作品フィルタ設定, 登場作品フィルタ
#DIMS REF 登場作品フィルタ
#DIMS DYNAMIC 原作, 300
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 登場作品
#DIM DYNAMIC 登場作品２
#DIM DYNAMIC 登場作品３
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;動的変数だけど一応初期化しておく
VARSET 原作

;GETNUMの記述を省略
登場作品 = GETNUM(CSTR, "登場作品")
登場作品２ = GETNUM(CSTR, "登場作品２")
登場作品３ = GETNUM(CSTR, "登場作品３")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「原作」の配列に該当作品がまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, 登場作品) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品))) == -1
			原作:FINDELEMENT(原作, "", 0, 99, 1) = %CSVCSTR(LCOUNT, 登場作品)%
	ENDIF
	;登場作品２は原作:100～199の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品２) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品２))) == -1
			原作:FINDELEMENT(原作, "", 100, 199, 1) = %CSVCSTR(LCOUNT, 登場作品２)%
	ENDIF
	;登場作品３は原作:200～299の範囲に入れる
	IF CSVCSTR(LCOUNT, 登場作品３) != ""
		SIF FINDELEMENT(原作, ESCAPE(CSVCSTR(LCOUNT, 登場作品３))) == -1
			原作:FINDELEMENT(原作, "", 200, 299, 1) = %CSVCSTR(LCOUNT, 登場作品３)%
	ENDIF
NEXT

DO
	;配列の使っていない範囲を検索してARRAYMOVEで前詰めする
	始点 = FINDELEMENT(原作, "", 0, VARSIZE("原作"), 1)
	FOR LCOUNT, 始点, VARSIZE("原作")
		IF 原作:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		;配列の終端にたどり着いたら前詰め処理終了
		SIF LCOUNT == VARSIZE("原作")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 原作, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラの登場作品を選んでください
FOR LCOUNT, 0, VARSIZE("原作")
	SIF 原作:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %原作:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　原作別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("原作")
			IF 原作:RESULT != ""
				;参照型変数でフィルタを返す
				登場作品フィルタ = %原作:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			登場作品フィルタ = 
			RETURN
	ENDSELECT
LOOP 1

@キャラデザフィルタ設定, キャラデザフィルタ
#DIMS REF キャラデザフィルタ
#DIMS DYNAMIC 作画, 1000
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC キャラデザ
#DIM DYNAMIC キャラデザ２
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

;動的変数だけど一応初期化しておく
VARSET 作画

;GETNUMの記述を省略
キャラデザ = GETNUM(CSTR, "キャラデザ")
キャラデザ２ = GETNUM(CSTR, "キャラデザ２")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「作画」の配列に該当デザイナーがまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, キャラデザ) != ""
		SIF FINDELEMENT(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ)), 0, VARSIZE("作画"), 1) == -1
			作画:FINDELEMENT(作画, "", 0, 499, 1) = %CSVCSTR(LCOUNT, キャラデザ)%
	ENDIF
	;キャラデザ２は作画:500～999の範囲に入れる
	IF CSVCSTR(LCOUNT, キャラデザ２) != ""
		SIF FINDELEMENT(作画, ESCAPE(CSVCSTR(LCOUNT, キャラデザ２)), 0, VARSIZE("作画"), 1) == -1
			作画:FINDELEMENT(作画, "", 500, 999, 1) = %CSVCSTR(LCOUNT, キャラデザ２)%
	ENDIF
NEXT

DO
	;配列の使っていない範囲を検索してARRAYMOVEで前詰めする
	始点 = FINDELEMENT(作画, "", 0, VARSIZE("作画"), 1)
	FOR LCOUNT, 始点, VARSIZE("作画")
		IF 作画:LCOUNT != ""
			終点 = LCOUNT
			BREAK
		ENDIF
		;配列の終端にたどり着いたら前詰め処理終了
		SIF LCOUNT == VARSIZE("作画")-1
			GOTO BREAK
	NEXT
	ARRAYREMOVE 作画, 始点, 終点-始点
LOOP 1
$BREAK

PRINTL □表示したいキャラのデザイナーを選んでください
FOR LCOUNT, 0, VARSIZE("作画")
	SIF 作画:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %作画:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　キャラデザイナー別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("作画")
			IF 作画:RESULT != ""
				;参照型変数でフィルタを返す
				キャラデザフィルタ = %作画:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			キャラデザフィルタ = 
			RETURN
	ENDSELECT
LOOP 1

@種族フィルタ設定, 種族フィルタ
#DIMS REF 種族フィルタ
#DIMS DYNAMIC 種族名, 50
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 種族
#DIM DYNAMIC 始点
#DIM DYNAMIC 終点

VARSET 種族名

種族 = GETNUM(CSTR, "種族")

FOR LCOUNT, 1, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE
	;変数「種族名」の配列に該当種族名がまだ入ってない場合は入れる
	IF CSVCSTR(LCOUNT, 種族) != ""
		SIF FINDELEMENT(種族名, ESCAPE(CSVCSTR(LCOUNT, 種族)), 0, VARSIZE("種族名"), 1) == -1
			種族名:FINDELEMENT(種族名, "", 0, VARSIZE("種族名"), 1) = %CSVCSTR(LCOUNT, 種族)%
	ENDIF
NEXT

PRINTL □表示したいキャラの種族を選んでください
FOR LCOUNT, 0, VARSIZE("種族名")
	SIF 種族名:LCOUNT == ""
		BREAK
	PRINTFORMLC [{LCOUNT}] %種族名:LCOUNT%
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTPLAIN 　種族別カテゴリ　
PRINT [-1] 戻る 
PRINT [-2] フィルタをリセットする

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("種族名")
			IF 種族名:RESULT != ""
				;参照型変数でフィルタを返す
				種族フィルタ = %種族名:RESULT%
				RETURN
			ENDIF
		CASE -1
			RETURN
		CASE -2
			種族フィルタ = 
			RETURN
	ENDSELECT
LOOP 1

