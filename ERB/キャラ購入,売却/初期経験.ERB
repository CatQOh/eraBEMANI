@初期経験セット(ID)
#DIM DYNAMIC ID;登録番号
#DIM DYNAMIC LCOUNT
;50個もあれば大丈夫か？
#DIMS DYNAMIC 性経験設定用,100
#DIMS DYNAMIC 相手, 2

IF CSTR:ID:恋心 != ""
	IF CSTR:ID:恋心 == "不明"
		TALENT:ID:恋心 = 10000
	ELSE
		TALENT:ID:恋心 = GETNO(CSTR:ID:恋心)
	ENDIF
ENDIF
IF CSTR:ID:既婚者 != ""
	IF CSTR:ID:既婚者 == "不明"
		TALENT:ID:既婚者 = 10000
	ELSE
		TALENT:ID:既婚者 = GETNO(CSTR:ID:既婚者)
	ENDIF
ENDIF

SPLIT CSTR:ID:性経験, "/", 性経験設定用
FOR LCOUNT, 0, RESULT, 2
	SIF 性経験設定用:LCOUNT == ""
		BREAK
	;なぜか行連結で行頭にスペースが入るので削る
	性経験設定用:LCOUNT = %SUBSTRINGU(性経験設定用:LCOUNT, 1)%
	SELECTCASE 性経験設定用:LCOUNT
		CASE "モブ経験人数"
			モブ経験人数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブ経験回数"
			モブ経験回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブ中出し回数"
			モブ中出し回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブアナル中出し回数"
			モブアナル中出し回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブ中出され回数"
			モブ中出され回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブアナル中出され回数"
			モブアナル中出され回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブ孕み回数"
			モブ孕み回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		CASE "モブ孕ませ回数"
			モブ孕ませ回数:ID:0 = TOINT(性経験設定用:(LCOUNT+1))
		;以下
		;相手:0 = 相手のNAME
		;相手:1 = 回数
		CASE "経験回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			経験回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "中出し回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			中出し回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "アナル中出し回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			アナル中出し回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "中出され回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			中出され回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "アナル中出され回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			アナル中出され回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "孕み回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			孕み回数:ID:GETNO(相手) = TOINT(相手:1)
		CASE "孕ませ回数"
			SPLIT 性経験設定用:(LCOUNT+1), "*", 相手
			孕ませ回数:ID:GETNO(相手) = TOINT(相手:1)
	ENDSELECT
NEXT

@初期経験リセット(ID)
#DIM DYNAMIC ID;登録番号

;全ての値を0にする
;引数IDは登録番号(購入・売却などで0から順番に付けられる方)

VARSET 経験回数:ID:0
VARSET 中出し回数:ID:0
VARSET アナル中出し回数:ID:0
VARSET 孕ませ回数:ID:0
VARSET 中出され回数:ID:0
VARSET アナル中出され回数:ID:0
VARSET 孕み回数:ID:0

VARSET モブ経験人数:ID:0
VARSET モブ経験回数:ID:0
VARSET モブ中出し回数:ID:0
VARSET モブアナル中出し回数:ID:0
VARSET モブ孕ませ回数:ID:0
VARSET モブ中出され回数:ID:0
VARSET モブアナル中出され回数:ID:0
VARSET モブ孕み回数:ID:0


@経験人数(ID)
#FUNCTION
#DIM DYNAMIC ID;登録番号
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 経験人数

FOR LCOUNT, 0, 20000
	SIF 経験回数:ID:LCOUNT
		経験人数 += 1
NEXT
経験人数 += モブ経験人数:ID:0

RETURNF 経験人数

@経験回数増加, 対象, 対象:1, 回数, 行為
#DIM DYNAMIC 対象, 2
#DIM DYNAMIC 回数
#DIMS DYNAMIC 行為

経験回数:対象:(NO:(対象:1)) += 回数
経験回数:(対象:1):(NO:対象) += 回数

;中出し系は対象→対象:1の場合となる
SELECTCASE 行為
	CASE "中出し"
		中出し回数:対象:(NO:(対象:1)) += 回数
		中出され回数:(対象:1):(NO:対象) += 回数
	CASE "アナル中出し"
		アナル中出し回数:対象:(NO:(対象:1)) += 回数
		アナル中出され回数:(対象:1):(NO:対象) += 回数
ENDSELECT

@総中出し回数, 対象, 部位
#FUNCTION
#DIM DYNAMIC 対象
#DIMS DYNAMIC 部位

SELECTCASE 部位
	CASE "Ｖ"
		RETURNF SUMARRAY(中出し回数:対象:0)+モブ中出し回数:対象:0
	CASE "Ａ"
		RETURNF SUMARRAY(アナル中出し回数:対象:0)+モブアナル中出し回数:対象:0
	CASEELSE
		RETURNF SUMARRAY(中出し回数:対象:0)+SUMARRAY(アナル中出し回数:対象:0)+モブ中出し回数:対象:0+モブアナル中出し回数:対象:0
ENDSELECT