@タイトル_MOD
#DIMS DYNAMIC HTML
SELECTCASE MODページ
	CASE 0
		CALL COLUMNPRINTL, 0, "□エクストラMODの設定ができます"
		CALL COLUMNPRINTL, 0, "　エクストラMODとは、セーブデータ全体で共有されるオプションで、"
		CALL COLUMNPRINTL, 0, "　ゲームバランスに大きな影響を及ぼしかねない機能をあえて解禁することで、遊びの幅を広げる試みです"
		CALL COLUMNPRINTL, 0, "　必要に応じて設定してください。段位認定モードでは適用されません"
		CALL COLUMNPRINTL, 0, "　また、体質変更MODは既に解禁済みのキャラにも適用されません。新たに解禁したキャラにのみ適用されます"
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, "□性別関連MOD(新しく購入、追加したキャラにのみ適用。各MODの重複は不可)"
		CALL COLUMNPRINTL, 0, @"　[1] - 男性化MOD(現在:\@ 男性化MOD ? オン # オフ \@) 全てのキャラが男性になる"
		CALL COLUMNPRINTL, 0, @"　[2] - 女性化MOD(現在:\@ 女性化MOD ? オン # オフ \@) 全てのキャラが女性になる"
		CALL COLUMNPRINTL, 0, @"　[3] - 性転換MOD(現在:\@ 性転換MOD ? オン # オフ \@) 全てのキャラが性転換する"
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, "□体質関連MOD(新しく購入、追加したキャラにのみ適用)"
		CALL COLUMNPRINTL, 0, @"　[11] - 爆乳MOD(現在:\@ 爆乳MOD ? オン # オフ \@) 全ての女性キャラが爆乳になる。絶壁MODとの重複不可"
		CALL COLUMNPRINTL, 0, @"　[12] - 絶壁MOD(現在:\@ 絶壁MOD ? オン # オフ \@) 全ての女性キャラが絶壁になる。爆乳MODとの重複不可"
		CALL COLUMNPRINTL, 0, @"　[13] - 母乳MOD(現在:\@ 母乳MOD ? オン # オフ \@) 全ての女性キャラが母乳体質になる"
		CALL COLUMNPRINTL, 0, @"　[14] - おもらしMOD(現在:\@ おもらしMOD ? オン # オフ \@) 全てのキャラが漏らすようになる"
		CALL COLUMNPRINTL, 0, @"　[15] - ふたなりMOD(現在:\@ ふたなりMOD ? オン # オフ \@) 全てのキャラがふたなりになる"
		CALL COLUMNPRINTL, 0, @"　[16] - ロリショタMOD(現在:\@ ロリショタMOD ? オン # オフ \@) 全てのキャラが小柄体型になる。大人化MODとの重複不可"
		CALL COLUMNPRINTL, 0, @"　[17] - 大人化MOD(現在:\@ 大人化MOD ? オン # オフ \@) 全てのキャラが大人体型になる。ロリショタMODとの重複不可"
		CALL COLUMNPRINTL, 0, @"　[18] - チャレンジMOD(現在:\@ チャレンジMOD ? オン # オフ \@) 全てのキャラの性経験が皆無になる 旧チャレンジモード"
		CALL COLUMNPRINTL, 0, @"　[19] - スライムMOD(現在:\@ スライムMOD ? オン # オフ \@) 全てのキャラがスライムになる"
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, "□妊娠関連MOD(既存のキャラにも適用される)"
		CALL COLUMNPRINTL, 0, @"　[20] - 人口飽和MOD(現在:\@ 人口飽和MOD ? オン # オフ \@) 中出しした翌日に妊娠し、すぐに出産するMOD"
		CALL COLUMNPRINTL, 0, @"　[21] - 百発百中MOD(現在:\@ 百発百中MOD ? オン # オフ \@) 着床する確率が100\%になるMOD"
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, "□その他MOD"
		HTML += COLUMNPRINTF(@"　[30] - Scrap that Slave!(現在:\@ STSMOD ? オン # オフ \@) キャラを")
		HTML += COLUMNPRINTF("解体", "red")
		HTML += COLUMNPRINTF("して血液やDNAデータ、その他リソースを回収できるようにする")
		CALL COLUMNPRINTL, 0, HTML
	CASE 1
		CALL COLUMNPRINTL, 0, "□陥落素質MOD(新しく購入、追加したキャラにのみ適用。各MODの重複は不可。一度適用されたセーブでは実績解除不可)"
		CALL COLUMNPRINTL, 0, @"　[40] 恋慕MOD(現在:\@ 恋慕MOD ? オン # オフ \@ キャラが最初から恋慕状態になる)"
		CALL COLUMNPRINTL, 0, @"　[41] 淫乱MOD(現在:\@ 淫乱MOD ? オン # オフ \@ キャラが最初から淫乱状態になる)"
		CALL COLUMNPRINTL, 0, @"　[42] 服従MOD(現在:\@ 服従MOD ? オン # オフ \@ キャラが最初から服従状態になる)"
ENDSELECT
CALL COLUMNDRAWLINE, 0
CALL COLUMNPRINT, 0, @"%RETURNSPACE(MODページ > 0, "[101] 前ページ ")%"
CALL COLUMNPRINT, 0, @"%RETURNSPACE(MODページ < 1, "[109] 次ページ")%"
CALL COLUMNPRINTL, 0, ""

@タイトル_MOD_INPUT
SELECTCASE CRESULT:1
	CASE 101
		MODページ--
	CASE 109
		MODページ++
	CASE 1
		INVERTBIT 男性化MOD, 0
		女性化MOD = 0
		性転換MOD = 0
	CASE 2
		INVERTBIT 女性化MOD, 0
		男性化MOD = 0
		性転換MOD = 0
	CASE 3
		INVERTBIT 性転換MOD, 0
		男性化MOD = 0
		女性化MOD = 0
	CASE 11
		INVERTBIT 爆乳MOD, 0
		絶壁MOD = 0
	CASE 12
		INVERTBIT 絶壁MOD, 0
		爆乳MOD = 0
	CASE 13
		INVERTBIT 母乳MOD, 0
	CASE 14
		INVERTBIT おもらしMOD, 0
	CASE 15
		INVERTBIT ふたなりMOD, 0
	CASE 16
		INVERTBIT ロリショタMOD, 0
		大人化MOD = 0
	CASE 17
		INVERTBIT 大人化MOD, 0
		ロリショタMOD = 0
	CASE 18
		INVERTBIT チャレンジMOD, 0
	CASE 20
		INVERTBIT 人口飽和MOD, 0
	CASE 21
		INVERTBIT 百発百中MOD, 0
	CASE 30
		INVERTBIT STSMOD, 0
	CASE 40
		INVERTBIT 恋慕MOD, 0
	CASE 41
		INVERTBIT 淫乱MOD, 0
	CASE 42
		INVERTBIT 服従MOD, 0
ENDSELECT
SAVEGLOBAL

@MOD情報セーブ
SIF 恋慕MOD || 淫乱MOD || 服従MOD
	実績解除不可 = 1

@SHOW_MODS
#DIMS DYNAMIC 表示用変数 = "　"

SIF 男性化MOD
	表示用変数 += "[男性化MOD]"
SIF 女性化MOD
	表示用変数 += "[女性化MOD]"
SIF 性転換MOD
	表示用変数 += "[性転換MOD]"
SIF 爆乳MOD
	表示用変数 += "[爆乳MOD]"
SIF 絶壁MOD
	表示用変数 += "[絶壁MOD]"
SIF 母乳MOD
	表示用変数 += "[母乳MOD]"
SIF おもらしMOD
	表示用変数 += "[おもらしMOD]"
SIF ふたなりMOD
	表示用変数 += "[ふたなりMOD]"
SIF ロリショタMOD
	表示用変数 += "[ロリショタMOD]"
SIF 大人化MOD
	表示用変数 += "[大人化MOD]"
SIF チャレンジMOD
	表示用変数 += "[チャレンジMOD]"
SIF 人口飽和MOD
	表示用変数 += "[人口飽和MOD]"
SIF 百発百中MOD
	表示用変数 += "[百発百中MOD]"
SIF STSMOD
	表示用変数 += "[STSMOD]"

IF 表示用変数 != "　"
	CALL CHECKDRAWLINE
	PRINTL □適用中のMOD
	PRINTFORML %表示用変数%
	DRAWLINE
ENDIF

@APPLY_男性化MOD, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LTARGET

LTARGET = TARGET
TARGET = 対象

IF TALENT:性別 == 2
	TALENT:性別 = 1
	;以下ふたなりMODが入ってなければ適用
	IF !ふたなりMOD
		IF TALENT:処女
			TALENT:童貞 = 1
			TALENT:処女 = 0
		ELSE
			CSTR:童貞 = %CSTR:処女%
			CSTR:処女 = 
		ENDIF
		IF TALENT:濡れやすい
			TALENT:勃ちやすい = 1
			TALENT:濡れやすい = 0
		ENDIF
		IF TALENT:濡れにくい
			TALENT:勃ちにくい = 1
			TALENT:濡れにくい = 0
		ENDIF
		;消す要素
		EXP:Ｖ経験 = 0
		EXP:膣射経験 = 0
		EXP:Ｖ拡張経験 = 0
		EXP:出産経験 = 0
		EXP:流産経験 = 0
		ABL:Ｖ感覚 = 0
		TALENT:Ｖ敏感 = 0
		TALENT:Ｖ鈍感 = 0
		;性経験履歴 経験回数、人数以外はリセットで
		VARSET 中出し回数
		VARSET アナル中出し回数
		VARSET 孕ませ回数
		VARSET 中出され回数
		VARSET アナル中出され回数
		VARSET 孕み回数

		VARSET モブ中出し回数
		VARSET モブアナル中出し回数
		VARSET モブ孕ませ回数
		VARSET モブ中出され回数
		VARSET モブアナル中出され回数
		VARSET モブ孕み回数
	ENDIF
	;性別変わって適応される要素
	EXP:射精経験 += EXP:絶頂経験
	EXP:ゲイ経験 += EXP:レズ経験
	EXP:レズ経験 = 0
	IF !EXP:ゲイ経験
		EXP:精液経験 = 0
		EXP:フェラ経験 = 0
		EXP:Ａ拡張経験 = 0
	ENDIF
	ABL:ホモっ気 += ABL:レズっ気
	ABL:レズっ気 = 0
	ABL:ゲイ中毒 += ABL:レズ中毒
	ABL:レズ中毒 = 0
	
	;消す要素
	EXP:噴乳経験 = 0
	EXP:パイズリ経験 = 0
	ABL:噴乳中毒 = 0
	TALENT:母性 = 0
	TALENT:母乳体質 = 0
	TALENT:バストサイズ = 0

	CSTR:適用MOD += "[男性化MOD]"
ENDIF

TARGET = LTARGET

@APPLY_女性化MOD, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LTARGET

LTARGET = TARGET
TARGET = 対象

IF TALENT:性別 == 1
	TALENT:性別 = 2
	;以下ふたなりMODが入ってなければ適用
	IF !ふたなりMOD
		IF TALENT:童貞
			TALENT:処女 = 1
			TALENT:童貞 = 0
		ELSE
			CSTR:処女 = %CSTR:童貞%
			CSTR:童貞 = 
		ENDIF
		IF TALENT:勃ちやすい
			TALENT:濡れやすい = 1
			TALENT:勃ちやすい = 0
		ENDIF
		IF TALENT:勃ちにくい
			TALENT:濡れにくい = 1
			TALENT:勃ちにくい = 0
		ENDIF
		;性別変わって適応される要素
		EXP:絶頂経験 += EXP:射精経験
		EXP:射精経験 = 0
		SIF !TALENT:処女
			EXP:Ｖ経験 += EXP:性交経験
		;消す要素
		EXP:射精経験 = 0
		;性経験履歴 経験回数、人数以外はリセットで
		VARSET 中出し回数
		VARSET アナル中出し回数
		VARSET 孕ませ回数
		VARSET 中出され回数
		VARSET アナル中出され回数
		VARSET 孕み回数

		VARSET モブ中出し回数
		VARSET モブアナル中出し回数
		VARSET モブ孕ませ回数
		VARSET モブ中出され回数
		VARSET モブアナル中出され回数
		VARSET モブ孕み回数
	ENDIF

	EXP:レズ経験 += EXP:ゲイ経験
	EXP:ゲイ経験 = 0
	ABL:レズっ気 += ABL:ホモっ気
	ABL:ホモっ気 = 0
	ABL:レズ中毒 += ABL:ゲイ中毒
	ABL:ゲイ中毒 = 0
	IF TALENT:小柄体型
		TALENT:バストサイズ = 2
	ELSE
		TALENT:バストサイズ = 3
	ENDIF
	ABL:メス度 = 0

	CSTR:適用MOD += "[女性化MOD]"
ENDIF

TARGET = LTARGET

@APPLY_性転換MOD, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LTARGET

LTARGET = TARGET
TARGET = 対象

SELECTCASE TALENT:性別
	CASE 1
		CALL APPLY_女性化MOD, TARGET
	CASE 2
		CALL APPLY_男性化MOD, TARGET
ENDSELECT

CSTR:適用MOD += "[性転換MOD]"

TARGET = LTARGET

@APPLY_ふたなりMOD, 対象
#DIM DYNAMIC 対象

IF !TALENT:対象:ふたなり
	TALENT:対象:ふたなり = 1
	SELECTCASE SEX(対象)
		CASE 1
			TALENT:対象:処女 = 1
		CASE 2
			TALENT:対象:童貞 = 1
	ENDSELECT
	CSTR:対象:適用MOD += "[ふたなりMOD]"
ENDIF

@APPLY_爆乳MOD, 対象
#DIM DYNAMIC 対象

IF SEX(対象) == 2 && TALENT:対象:バストサイズ < 6
	TALENT:対象:バストサイズ = 6
	CSTR:対象:適用MOD += "[爆乳MOD]"
ENDIF

@APPLY_絶壁MOD, 対象
#DIM DYNAMIC 対象

IF SEX(対象) == 2 && TALENT:対象:バストサイズ > 1
	TALENT:対象:バストサイズ = 1
	CSTR:対象:適用MOD += "[絶壁MOD]"
ENDIF

@APPLY_おもらしMOD, 対象
#DIM DYNAMIC 対象

IF !TALENT:対象:おもらし癖
	TALENT:対象:おもらし癖 = 1
	CSTR:対象:適用MOD += "[おもらしMOD]"
ENDIF

@APPLY_ロリショタMOD, 対象
#DIM DYNAMIC 対象

IF !TALENT:対象:小柄体型
	TALENT:対象:小柄体型 = 1
	CSTR:対象:適用MOD += "[ロリショタMOD]"
ENDIF

@APPLY_大人化MOD, 対象
#DIM DYNAMIC 対象

IF TALENT:対象:小柄体型
	TALENT:対象:小柄体型 = 0
	CSTR:対象:適用MOD += "[大人化MOD]"
ENDIF

@APPLY_スライムMOD, 対象
#DIM DYNAMIC 対象

IF !TALENT:対象:スライム && !TALENT:対象:溶ける快
	TALENT:対象:スライム = 1
	CSTR:対象:種族 = スライム
	CSTR:対象:適用MOD += "[スライムMOD]"
ENDIF

@APPLY_恋慕MOD, 対象
#DIM DYNAMIC 対象

TALENT:対象:恋慕 = 1
CSTR:対象:適用MOD += "[恋慕MOD]"

@APPLY_淫乱MOD, 対象
#DIM DYNAMIC 対象

TALENT:対象:淫乱 = 1
CSTR:対象:適用MOD += "[淫乱MOD]"

@APPLY_服従MOD, 対象
#DIM DYNAMIC 対象

TALENT:対象:服従 = 1
CSTR:対象:適用MOD += "[服従MOD]"
