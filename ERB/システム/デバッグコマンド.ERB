;デバッグ用関数です。スレの>>8が作ってくれました
@DEBUG

SIF TARGET == -1
	TARGET = 0

$LOOP_DEBUG
PRINTFORML なにをしますか？　　対象：%NAME:TARGET%
PRINTLC [0] - 対象変更
PRINTLC [1] - 素質操作
PRINTLC [2] - ABL操作
PRINTLC [3] - 経験操作
PRINTLC [4] - 刻印操作
PRINTLC [5] - ITEM操作
PRINTLC [6] - その他
PRINTL 
PRINTL [-1] 戻る

INPUT

SELECTCASE RESULT
	CASE -1
		SIF TARGET == 0
			TARGET = -1
		RETURN
	CASE 0
		CALL TARGET_CHANGE
	CASE 1
		CALL TALENT_CHANGE
	CASE 2
		CALL ABL_CHANGE
	CASE 3
		CALL EXP_CHANGE
	CASE 4
		CALL MARK_CHANGE
	CASE 5
		CALL ITEM_CHANGE
	CASE 6
		CALL EXTRA_CHANGE
ENDSELECT

GOTO LOOP_DEBUG

@TARGET_CHANGE

;対象を表示
REPEAT CHARANUM
	PRINTFORML [{COUNT + 100}] %NAME:COUNT%
REND
DRAWLINE

INPUT

TARGET = RESULT - 100
PRINTFORMW 対象を%NAME:TARGET%にしました。

RETURN

@TALENT_CHANGE
DRAWLINE
REPEAT VARSIZE("TALENT")
	SIF TALENTNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %TALENTNAME:COUNT%
	IF !TALENT:TARGET:COUNT
		PRINT ×
	ELSE
		PRINT ◯
	ENDIF
	PRINT 　
REND
PRINTL
DRAWLINE

PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT
IF LOCAL == 0
	PRINTL [1] - 男にする
	PRINTL [2] - 女にする
	PRINTL [-1] - 戻る

	INPUT
	
	SIF RESULT == -1
		RETURN
	
	SELECTCASE RESULT
		CASE 1, 2
			TALENT:TARGET:性別 = RESULT
	ENDSELECT
	
	RESTART
ELSE
	PRINTFORML [0] - %TALENTNAME:LOCAL%を付加
	PRINTFORML [1] - %TALENTNAME:LOCAL%を削除
	PRINTL [-1] - 戻る

	INPUT

	SIF RESULT == -1
		RETURN

	PRINTFORML %NAME:TARGET%の%TALENTNAME:LOCAL%を\@RESULT ? 削除 # 付加 \@します

	IF RESULT == 0
		TALENT:TARGET:LOCAL = 1
	ELSEIF RESULT == 1
		TALENT:TARGET:LOCAL = 0
	ENDIF

	RESTART
ENDIF

@ABL_CHANGE
DRAWLINE

REPEAT VARSIZE("ABL")
	SIF ABLNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %ABLNAME:COUNT% Lv{ABL:TARGET:COUNT}
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %ABLNAME:RESULT%をいくつにしますか？
PRINTL [0][1][5][10]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

ABL:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%ABLNAME:LOCAL%をLv{RESULT}にしました

@EXP_CHANGE

REPEAT VARSIZE("EXP")
	SIF EXPNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %EXPNAME:COUNT%（{EXP:TARGET:COUNT}）
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %EXPNAME:RESULT%をいくつにしますか？
PRINTL [0][1][9][99][999][9999][99999][999999][9999999]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

EXP:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%EXPNAME:LOCAL%の経験を{RESULT}にしました

@MARK_CHANGE

REPEAT VARSIZE("MARK")
	SIF MARKNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %MARKNAME:COUNT% Lv{MARK:TARGET:COUNT}
REND
PRINTL
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

LOCAL = RESULT

PRINTFORML %MARKNAME:RESULT%をいくつにしますか？
PRINTL [0][1][2][3]
PRINTL [-1] - 戻る

INPUT

SIF RESULT == -1
	RETURN

MARK:TARGET:LOCAL = RESULT
PRINTFORML %NAME:TARGET%の%MARKNAME:LOCAL%をLv{RESULT}にしました

@ITEM_CHANGE

REPEAT 100
	SIF ITEMNAME:COUNT == ""
		CONTINUE
	PRINTFORMLC [{COUNT}] %ITEMNAME:COUNT% {ITEM:COUNT}個
REND
PRINTL
PRINTL [999] - 全部
PRINTL [-1] - 戻る
DRAWLINE

INPUT

SIF RESULT == -1
	RETURN

IF RESULT == 999
	REPEAT 27
		ITEM:COUNT = 1
	REND
	REPEAT 48
		SIF COUNT == 0
			COUNT = 40
		ITEM:COUNT = 99
	REND
	RETURN
ENDIF
LOCAL = RESULT

PRINTFORML %ITEMNAME:RESULT%をいくつにしますか？
PRINTL [0][1][10][50][99]
PRINTL [-1] 戻る

INPUT

SIF RESULT == -1
	RETURN

ITEM:LOCAL = RESULT
PRINTFORML %ITEMNAME:LOCAL%を{RESULT}個にしました

@EXTRA_CHANGE
DRAWLINE
PRINTLC [0] - 所持金
PRINTLC [1] - 日数
PRINTLC [2] - 時間帯
PRINTL [-1] - 戻る

INPUT

DRAWLINE
SIF RESULT == -1
	RETURN
IF RESULT == 0
	PRINTFORML いくらにしますか？　　現在の所持金：{MONEY}
	PRINTLC [0] 無一文
	PRINTLC [10000] 1万
	PRINTLC [100000000] 1億
	PRINTLC [1000000000000] 1兆
	PRINTLC [999999999999999999] 限界
	PRINTL [-1] 戻る
	INPUT
	SIF RESULT == -1
		RESTART
	MONEY = RESULT
	PRINTFORMW 所持金を{MONEY}に変更しました
ELSEIF RESULT == 1
	PRINTFORML 何日にしますか？　　現在の日数：{DAY + 1}
	PRINTFORML [1][9][99][999][9999][99999][999999]
	PRINTFORML [-1] 戻る
	INPUT
	SIF RESULT == -1
		RESTART
	DAY = RESULT - 1
ELSEIF RESULT == 2
	PRINTFORM いつにしますか？　　現在の時間帯：
	IF TIME == 0
		PRINTL 昼
	ELSE
		PRINTL 夜
	ENDIF
	PRINTL [0] - 昼
	PRINTL [1] - 夜
	PRINTL [-1] - 戻る
	INPUT
	SIF RESULT == -1
		RESTART
	TIME = RESULT
	PRINT 現在の時間帯を
	IF TIME == 0
		PRINTL 昼
	ELSE
		PRINTL 夜
	ENDIF
	PRINTL に変更しました
ENDIF

RESTART
