@EVENTFIRST
TARGET = -1
ASSI = -1

VARLINE = 0

LOADGLOBAL

;周回数を1に
FLAG:周回数 = 1

;コンフィグ初期設定
FLAG:地の文表示 = 1
FLAG:口上表示 = 1

;データIDを設定
データID = GETSECOND()

;経営モードフラグの初期化
CALL CLEAR_STRATEGY_FLAG

LOADGLOBAL

;モード選択
DRAWLINE
;PRINTL ★★モードを選択してください★★
;CALL PASELIL, @"[0] - STANDARD (100日期限 目標金額1,000,000＄ 経営使用不可 クリア後は可能)"
;CALL PASELIL, @"[1] - STRATEGY (300日期限 目標金額100,000,000＄ 経営使用可能)"
;PRINTL [7] - 新婚モード (期限なし 好きなキャラと好きなだけイチャイチャする)
;PRINTL [8] - 段位認定 (期限あり 特定の4人を攻略)
;PRINTL [9] - PREMIUM FREE (期限なし エンドレス)

PRINT 　
FONTBOLD
PRINT M
FONTREGULAR
PRINTL ODE SELECT　　　　　　　　　　　　　　　　　＼ＳＰ／
PRINTL 　クリックで選択 価格はすべて税込みとなります　　＼／DP＼

VARSET LOCALS

LOCALS =<nobr><button value='7' pos='100'> <img src='MODE' srcb='MODEB' height='700'></button>
LOCALS:1 =<button value='8' pos='1700'> <img src='MODE' srcb='MODEB' height='700'></button>
LOCALS:2 =<button value='9' pos='3300'> <img src='MODE' srcb='MODEB' height='700'></button>

HTML_PRINT STRJOIN(LOCALS, "")

FONTBOLD
PRINT 　　　　
SETCOLORBYNAME black
PRINT WE
SETCOLORBYNAME hotpink
PRINT DD
SETCOLORBYNAME black
PRINT ING
PRINT 　　　　　　　　　　 

PRINT CLA
SETCOLORBYNAME darkseagreen
PRINT SS

SETCOLORBYNAME black
PRINT 　　　　　　　　　　　 
PRINT PRE
SETCOLORBYNAME lime
PRINT M
SETCOLORBYNAME black
PRINT IU
SETCOLORBYNAME lime
PRINT M
SETCOLORBYNAME black
PRINTL 
PRINT 　　　　新婚モード　　　　　　　　　 段位認定　　　　　　　　　　
PRINT FREE
PRINTL 
PRINTL 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　プレミアムフリー
FONTREGULAR
PRINTL 　　　　期限無し　　　　　　　　　　　期限あり　　　　　　　　　　
PRINTL 　　　　所持金:3億＄　　　　　　　　　特定の4人を攻略　　　　　　　　期限無し
RESETCOLOR
PRINTL 

VARSET LOCALS

LOCALS =<nobr><button value='0' pos='900'> <img src='MODE' srcb='MODEB' height='700'></button>
LOCALS:1 =<button value='1' pos='2500'> <img src='MODE' srcb='MODEB' height='700'></button>

HTML_PRINT STRJOIN(LOCALS, "")

FONTBOLD
PRINT 　　　　　　　　　　　
SETCOLORBYNAME black
PRINT STAN
SETCOLORBYNAME darkorange
PRINT D
SETCOLORBYNAME black
PRINT AR
SETCOLORBYNAME darkorange
PRINT D

PRINT 　　　　　　　　　　
SETCOLORBYNAME black
PRINT S
SETCOLORBYNAME deepskyblue
PRINT T
SETCOLORBYNAME black
PRINT RA
SETCOLORBYNAME deepskyblue
PRINT T
SETCOLORBYNAME black
PRINT EGY
PRINTL 
PRINTL 　　　　　　　　　　　通常モード　　　　　　　　　 経営モード
PRINTL 
FONTREGULAR
PRINTL 　　　　　　　　　　　目標:1,000,000＄　　　　　　 目標:100,000,000＄
PRINTL 　　　　　　　　　　　期限:100日　　　　　　　　　　期限:300日
PRINTL 
RESETCOLOR
PRINTL CREDIT:1　　　　　　　　　　　　　　　　　┏━━━━━━┓　　　　　　　　　　　　　　　　　　CREDIT:0
PRINTL PASELI:NO ACCOUNT　　　　　　　　　　　　 ┃EFFECTOR OFF┃　　　　　　　　　　　　　　　　　　PASELI:NO ACCOUNT

;各ゲームモードごとの初期設定
DO
	INPUT
	SELECTCASE RESULT
		CASE 0, 1
			FLAG:ゲームモード = RESULT
			PRINTL CHALLENGEモードに挑戦可能です。挑戦しますか？
			PRINTL (購入する全てのキャラが性経験の無い状態になります。オプションで辞退することも可能です)
			PRINTL [0] - いいえ
			PRINTL [1] - はい
			DO
				INPUT
				FLAG:チャレンジモード = RESULT
			LOOP LOOPRES(0, 1)
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL SELECT_SEX
			CALL INPUT_NAME
			CALL SELECT_VIRGIN
			CALL SELECT_DIFFICULT
			BREAK
		CASE 7
			FLAG:ゲームモード = 7
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL START_CHARA_SELECT
			BREAK
		CASE 8
			FLAG:ゲームモード = 8
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			TALENT:MASTER:性別 = 1
			CSTR:MASTER:上半身上着 = シャツ
			CSTR:MASTER:下半身上着 = ズボン
			CSTR:MASTER:下半身下着 = トランクス
			LOADGLOBAL
			CALL 段位認定緑
			SELECTCASE 受験段位名
				CASE "EDIT"
					CALL SELECT_DIFFICULT
				CASE "裏七級"
					FLAG:難易度 = 3
				CASEELSE
					FLAG:難易度 = 1
			ENDSELECT
			BREAK
		CASE 9
			FLAG:ゲームモード = 9
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL START_CHARA_SELECT
			CALL SELECT_DIFFICULT
			BREAK
	ENDSELECT
LOOP 1

;オープニング表示
SELECTCASE ゲームモード()
	CASE "通常モード"
		IF チュートリアル設定 && デフォルトオプション
			DRAWLINE
			PRINTL オープニングストーリーを表示しますか？
			PRINTL [0] - 表示する
			PRINTL [1] - 表示しない
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						DRAWLINE
						CALL OPENING
						BREAK
					CASE 1
						BREAK
				ENDSELECT
			LOOP 1
		ENDIF
	;STRATEGYモード専用処理
	CASE "経営モード"
		DRAWLINE
		TFLAG:ページ数 = 0
		PRINTL ★★パートナーを選択してください★★
		CALL CHARA_LIST, 3
		ADDCHARA RESULT
		CCB:RESULT = 1
		ASSI = 1
		CALL ヨミの性別, ASSI
		;この時点では口上表示の設定はできないので強制表示
		TRYCCALLFORM PARTNER_K{NO:ASSI}
		CATCH
			PRINTFORMW %NAME:ASSI%をパートナーに選択しました
		ENDCATCH
		PRINTL 陥落状態を選択してください。
		PRINTL [0] - 恋慕
		PRINTL [1] - 淫乱
		PRINTL [2] - 服従
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					TALENT:ASSI:恋慕 = 1
					PRINTL 恋慕に設定しました。
				CASE 1
					TALENT:ASSI:淫乱 = 1
					PRINTL 淫乱に設定しました。
				CASE 2
					TALENT:ASSI:服従 = 1
					PRINTL 服従に設定しました。
			ENDSELECT
		LOOP LOOPRES(0, 1, 2, 3)
		CFLAG:ASSI:売却不可 = 1
		CFLAG:ASSI:状態 = 2
		CFLAG:ASSI:陥落 = 1
		MARK:ASSI:屈服刻印 = 3
		FLAG:パートナー = NO:ASSI
		SIF RELATION:ASSI:(NO:MASTER) < 200
			RELATION:ASSI:(NO:MASTER) = 200
		SIF RELATION:MASTER:(NO:ASSI) < 200
			RELATION:MASTER:(NO:ASSI) = 200
		キャラ未購入 = 1
		バンめし = 1
ENDSELECT

IF ゲームモード() != "新婚モード"
	;チュートリアル表示
	IF チュートリアル設定 && デフォルトオプション
		DRAWLINE
		PRINTL チュートリアルを表示しますか？（オプションからでも確認できます）
		PRINTL [0] - 表示する
		PRINTL [1] - 表示しない
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					DRAWLINE
					IF ゲームモード() == "段位認定"
						CALL TEST_TUTORIAL
					ELSE
						CALL TUTORIAL
					ENDIF
					BREAK
				CASE 1
					BREAK
			ENDSELECT
		LOOP 1
	ENDIF
ENDIF

DRAWLINE
IF デフォルトオプション
	FLAG:地の文表示 = 地の文設定
	FLAG:口上表示 = 口上設定
ELSE
	PRINTL 口上を表示しますか？(オプションでも変更できます)
	PRINTL [0] - 口上+地の文を表示する
	PRINTL [1] - 口上のみ表示する
	PRINTL [2] - 地の文のみ表示する
	PRINTL [3] - 口上も地の文も表示しない
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				FLAG:地の文表示 = 1
				FLAG:口上表示 = 1
			CASE 1
				FLAG:地の文表示 = 0
				FLAG:口上表示 = 1
			CASE 2
				FLAG:地の文表示 = 1
				FLAG:口上表示 = 0
			CASE 3
				FLAG:地の文表示 = 0
				FLAG:口上表示 = 0
		ENDSELECT
	LOOP LOOPRES(0, 1, 2, 3)
ENDIF

IF デフォルトオプション
	FLAG:実績メッセージ = 実績解除メッセージ設定
ELSE
	DRAWLINE
	PRINTL 実績解除時のメッセージを表示しますか？(オプションからでも変更できます)
	PRINTL [0] - 表示しない
	PRINTL [1] - 表示する
	DO
		INPUT
		FLAG:実績メッセージ = RESULT
	LOOP LOOPRES(0, 1)
ENDIF

IF QMA
	通貨単位 = マジカ
ELSEIF デフォルトオプション
	FLAG:パセリ表示 = パセリ表示設定
ELSE
	DRAWLINE
	PRINTL PASELI表示をオンにしますか？(オプションからでも変更できます)
	PRINTL [0] - オンにしない
	PRINTL [1] - オンにする
	DO
		INPUT
		FLAG:パセリ表示 = RESULT
	LOOP LOOPRES(0, 1)
ENDIF

IF デフォルトオプション
	SIF 着衣オプション設定
		FLAG:着衣設定 = 3
ELSE
	DRAWLINE
	PRINTL 奴隷の着衣設定を変更しますか？（オプションでも変更できます）
	PRINTL [0] - 服を着たままでいい
	PRINTL [1] - 面倒だから脱がせてから調教始める
	DO
		INPUT
		SELECTCASE RESULT
			CASE 1
				FLAG:着衣設定 = 3
		ENDSELECT
	LOOP LOOPRES(0, 1)
ENDIF

;天気の決定
CALL WEATHER

BEGIN SHOP


;ここまでがメインとなる@EVENTFIRST
;以下@EVENTFIRST内で処理される関数
@START_CHARA_SELECT
LOADGLOBAL
DRAWLINE
PRINTL ★★プレイヤーを変更できます★★
PRINTL ★★キャラを選択してください★★

CALL CHARA_LIST, 2

SELECTCASE RESULT
	CASE 10000
		PRINTW プレイヤーを変更しませんでした
		CALL SELECT_SEX
		CALL INPUT_NAME
		CALL SELECT_STYLE
		SIF ゲームモード() == "フリーモード"
			CALL SELECT_VIRGIN
	CASEELSE
		DELCHARA 0
		ADDCHARA RESULT
		MASTER = 0
		PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
		CALL ヨミの性別, MASTER
		CALL 初期経験セット, MASTER
		DRAWLINE
		IF ゲームモード() == "フリーモード"
			PRINTFORML %CALLNAME:MASTER%に調教されたい人向けに奴隷としてあなたを追加しますか？
			PRINTL [0] - 調教されたい！
			PRINTL [1] - いらん
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						ADDCHARA 0
						TARGET = 1
						CALL SELECT_SEX, TARGET
						CALL INPUT_NAME, TARGET
						CALL SELECT_VIRGIN, TARGET
						CALL SELECT_STYLE, TARGET
						BREAK
					CASE 1
						BREAK
				ENDSELECT
			LOOP 1
		ENDIF
ENDSELECT

;新婚モード専用処理
IF ゲームモード() == "新婚モード"
	FLAG:難易度 = 1
	TFLAG:ページ数 = 0
	PRINTL ★★嫁、もしくは婿を選択してください★★
	CALL CHARA_LIST, 1
	ADDCHARA RESULT
	TARGET = 1
	IF NO:TARGET == 0
		CALL SELECT_SEX, TARGET
		CALL INPUT_NAME, TARGET
		CALL SELECT_STYLE, TARGET
	ENDIF
	CALL ヨミの性別, TARGET
	RESULT = 0
	TRYCALLFORM WEDDING_K{NO:TARGET}
	LOCAL = RESULT
	PRINTFORMW %NAME:TARGET%とのご成婚おめでとうございます
	CFLAG:嫁 = 1
	CFLAG:陥落 = 1
	TALENT:恋心 = 0
	TALENT:親愛 = 1
	TALENT:MASTER:恋心 = 0
	TALENT:既婚者 = NO:MASTER
	TALENT:MASTER:既婚者 = NO:TARGET
	MARK:屈服刻印 = 3
	MARK:MASTER:屈服刻印 = 3
	CFLAG:依存度 = 5000
	CFLAG:MASTER:依存度 = 5000
	;経営、施設、設備アイテム
	ITEM:医務室 = 1
	ITEM:図書室 = 1
	;SKIPDISPで農業用アイテムを全解禁しておく
	SKIPDISP 1
	CALL 栽培解禁
	SKIPDISP 0
	ITEM:田んぼ = 2
	ITEM:畑 = 8
	ITEM:プランター = 4
	田んぼの作物:1 = 米
	田んぼの作物:2 = 麦
	ITEM:牧場 = 1
	ITEM:畜舎 = 1
	ITEM:養蜂場 = 1
	ITEM:生け簀 = 1
	牛の状態 = 100
	馬の状態 = 100
	羊の状態 = 100
	豚の状態 = 100
	鶏の状態 = 100
	蜂の状態 = 100
	牧場の予算 = 1000000
	畜舎の予算 = 1000000
	養蜂場の予算 = 100000
	生け簀の予算 = 1000000
	ITEM:風俗店営業許可証 = 1
	ITEM:楽器セット一式 = 1
	ITEM:事業開発オフィス = 1
	SIF RELATION:(NO:MASTER) < 300
		RELATION:(NO:MASTER) = 300
	SIF RELATION:MASTER:(NO:TARGET) < 300
		RELATION:MASTER:(NO:TARGET) = 300
	;WEDDING口上の返り値による
	SELECTCASE LOCAL
		CASE 0
			PRINTL ご結婚前からいろいろ経験なさりましたよね？
			PRINTL [0] - そりゃまぁ
			PRINTL [1] - 純潔を保ってきた
		CASE 1
			GOTO 経験あり
		CASE 2
			GOTO 経験無し
	ENDSELECT
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				$経験あり
				SELECTCASE SEX(MASTER)
					CASE 1
						EXP:MASTER:射精経験 += 10
						EXP:MASTER:性交経験 += 10
						TALENT:MASTER:童貞 = 0
						EXP:性交経験 += 10
						VIRGIN:MASTER:0 = %NAME:TARGET%
						SELECTCASE SEX(TARGET)
							CASE 1
								EXP:MASTER:Ａ経験 += 10
								EXP:射精経験 += 10
								TALENT:童貞 = 0
								VIRGIN:0 = %NAME:MASTER%
							CASE 2
								EXP:膣射経験 += 10
								EXP:Ｖ経験 += 10
								TALENT:処女 = 0
								VIRGIN:1 = %NAME:MASTER%
								CFLAG:処女の奪われ方 = 2
						ENDSELECT
					CASE 2
						EXP:MASTER:Ｖ経験 += 10
						TALENT:MASTER:処女 = 0
						EXP:MASTER:性交経験 += 10
						VIRGIN:MASTER:1 = %NAME:TARGET%
						CFLAG:MASTER:処女の奪われ方 = 2
						SELECTCASE SEX(TARGET)
							CASE 1
								EXP:MASTER:膣射経験 += 10
								EXP:射精経験 += 10
								TALENT:童貞 = 0
								VIRGIN:0 = %NAME:MASTER%
							CASE 2
								EXP:Ｖ経験 += 10
								TALENT:処女 = 0
								VIRGIN:1 = %NAME:MASTER%
								CFLAG:処女の奪われ方 = 2
						ENDSELECT
				ENDSELECT
				BREAK
			CASE 1
				$経験無し
				SELECTCASE SEX(MASTER)
					CASE 1
						TALENT:MASTER:童貞 = 1
						EXP:MASTER:性交経験 = 0
					CASE 2
						TALENT:MASTER:処女 = 1
						EXP:MASTER:性交経験 = 0
						EXP:MASTER:Ｖ経験 = 0
						EXP:MASTER:膣射経験 = 0
				ENDSELECT
				SELECTCASE SEX(TARGET)
					CASE 1
						TALENT:童貞 = 1
						EXP:性交経験 = 0
					CASE 2
						TALENT:処女 = 1
						EXP:性交経験 = 0
						EXP:Ｖ経験 = 0
						EXP:膣射経験 = 0
				ENDSELECT
				BREAK
		ENDSELECT
	LOOP 1
ENDIF

RETURN 0

@SELECT_SEX, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性別を選択してください
PRINTL [0] - 男性
PRINTL [1] - 女性
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			TALENT:ARG:性別 = 1
			CSTR:ARG:上半身上着 = シャツ
			CSTR:ARG:下半身上着 = ズボン
			CSTR:ARG:下半身下着 = トランクス
		CASE 1
			TALENT:ARG:性別 = 2
			TALENT:ARG:バストサイズ = 3
			CSTR:ARG:全身上着 = ワンピース
			CSTR:ARG:上半身下着 = ブラジャー
			CSTR:ARG:下半身下着 = パンティ
			CFLAG:ARG:挿入可 = 1
	ENDSELECT
LOOP LOOPRES(0, 1)

@INPUT_NAME, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
DO
	PRINT 名前を入力してください(何も入力しない場合は「
	PRINTBUTTON "あなた", "あなた"
	PRINTL 」になります)
	INPUTS
	SELECTCASE RESULTS
		CASE ""
			CALLNAME:ARG = あなた
			NAME:ARG = あなた
		CASEELSE
			CALLNAME:ARG = %RESULTS%
			NAME:ARG = %RESULTS%
	ENDSELECT
	PRINTFORML 「%CALLNAME:ARG%」でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
LOOP LOOPRES(0)

@SELECT_VIRGIN, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性交の経験は……
PRINTL [0] - ある
PRINTL [1] - 無い
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			SELECTCASE SEX(ARG)
				CASE 1
					TALENT:ARG:童貞 = 1
				CASE 2
					TALENT:ARG:処女 = 1
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SELECT_DIFFICULT
DRAWLINE
PRINTL ★★難易度を選択してください★★
IF QMA
	PRINTL [0] - フェアリー級(初心者でも安心)
	PRINTL [1] - ミノフェニ級(それなりに)
	PRINTL [2] - ドラゴン級(ようこそ廃人の入口へ)
ELSE
	PRINTL [0] - NORMAL
	PRINTL [1] - HYPER
	PRINTL [2] - ANOTHER
ENDIF
DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 2
			FLAG:難易度 = RESULT+1
			BREAK
	ENDSELECT
LOOP 1

@SELECT_STYLE, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 体型はどんな感じですか？
PRINTL [0] - 普通の人です
SELECTCASE SEX(ARG)
	CASE 1
		PRINTL [1] - ショタです
	CASE 2
		PRINTL [1] - ロリです
ENDSELECT
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			TALENT:ARG:小柄体型 = 1
			MAXBASE:ARG:体力 -= 200
			BASE:ARG:体力 -= 200
			SELECTCASE SEX(ARG)
				CASE 2
					TALENT:ARG:バストサイズ = 2
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SETUP_GAMESTART
DAY = 1
SELECTCASE ゲームモード()
	CASE "通常モード"
		MONEY = 3000
		FLAG:価格フィルタ上限 = 3000
		ITEM:医務室 = 1
		ITEM:図書室 = 1
	CASE "経営モード"
		MONEY = TONUM("100万")
		ITEM:風俗店営業許可証 = 1
	CASE "新婚モード"
		MONEY = TONUM("3億")
	CASE "段位認定"
		MONEY = 0
	CASE "フリーモード"
		MONEY = 30000
		FLAG:価格フィルタ上限 = 30000
		ITEM:風俗店営業許可証 = 1
ENDSELECT
FLAG:カスタムフィルタ１ = 1
FLAG:カスタムフィルタ２ = 3
FLAG:色仕掛け = 1
FLAG:お誘い = 1
LASTMONEY = MONEY
;リアルタイム計算処理 変数REALTIMEも残しておくこと
;リアルタイム(文字列式)からリアル西暦を抜き出して数値型に変換して代入する……
REALTIME:0 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
REALTIME:1 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
REALTIME:2 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

YEAR = REALTIME:0
MONTH = REALTIME:1
DAYS = REALTIME:2

;曜日計算
FLAG:曜日 = ZELLER(YEAR, MONTH, DAYS)

;月齢計算
CALL 月齢逆算, 30, -1

;過去19年の午前0時で最も新月に近いと思われる日と現在の日付の日数差を調べる
LOCAL = GETDAYS(YEAR, MONTH, DAYS) - GETDAYS(RESULT, RESULT:1, RESULT:2)

SELECTCASE LOCAL
	CASE IS <= 30
		月齢 = LOCAL*100
	CASEELSE
		LOCAL:1 = LOCAL-(LOCAL%30)
		月齢 = ((LOCAL:1*100000)%2953059)/1000
		月齢 += (LOCAL%30)*100
ENDSELECT

月齢 %= 3000

VARSET LOCAL, 0

VARSET SELECTCOM, -1
SELECTCOM = 0

;気温の算出
CALL TEMPERATURE

FLAG:TSBランダム = 1

