@EVENTFIRST
TARGET = -1
ASSI = -1

VARLINE = 0

LOADGLOBAL

;周回数を1に
FLAG:周回数 = 1

;コンフィグ初期設定
FLAG:地の文表示 = 1
FLAG:口上表示 = 1

;モード選択
DRAWLINE
PRINTL ★★モードを選択してください★★
CALL PASELIL, @"[0] - STANDARD (100日期限 目標金額1,000,000＄ 経営使用不可 クリア後は可能)"
CALL PASELIL, @"[1] - STRATEGY (300日期限 目標金額100,000,000＄ 経営使用可能)"
PRINTL [7] - 新婚モード (期限なし 好きなキャラと好きなだけイチャイチャする)
PRINTL [8] - 段位認定 (期限あり 特定の4人を攻略)
PRINTL [9] - PREMIUM FREE (期限なし エンドレス)
DO
	INPUT
	SELECTCASE RESULT
		CASE 0, 1
			FLAG:ゲームモード = RESULT
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL SELECT_SEX
			CALL INPUT_NAME
			CALL SELECT_VIRGIN
			CALL SELECT_DIFFICULT
			BREAK
		CASE 7
			FLAG:ゲームモード = 7
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL START_CHARA_SELECT
			BREAK
		CASE 8
			FLAG:ゲームモード = 8
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			TALENT:MASTER:性別 = 1
			CSTR:MASTER:上半身上着 = シャツ
			CSTR:MASTER:下半身上着 = ズボン
			CSTR:MASTER:下半身下着 = トランクス
			LOADGLOBAL
			CALL 段位認定緑
			SELECTCASE FLAG:選択段位
				CASE 99
					CALL SELECT_DIFFICULT
				CASE 50
					FLAG:難易度 = 3
				CASEELSE
					FLAG:難易度 = 1
			ENDSELECT
			BREAK
		CASE 9
			FLAG:ゲームモード = 9
			CALL SETUP_GAMESTART
			ADDCHARA 0
			MASTER = 0
			CALL START_CHARA_SELECT
			CALL SELECT_DIFFICULT
			BREAK
	ENDSELECT
LOOP 1

;オープニング表示
SELECTCASE FLAG:ゲームモード
	CASE 0
		DRAWLINE
		PRINTL オープニングストーリーを表示しますか？
		PRINTL [0] - 表示する
		PRINTL [1] - 表示しない
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					DRAWLINE
					CALL OPENING
					BREAK
				CASE 1
					BREAK
			ENDSELECT
		LOOP 1
	;STRATEGYモード専用処理
	CASE 1
		DRAWLINE
		TFLAG:ページ数 = 0
		PRINTL ★★パートナーを選択してください★★
		CALL CHARA_LIST, 3
		ADDCHARA RESULT
		CCB:RESULT = 1
		ASSI = 1
		;この時点では口上表示の設定はできないので強制表示
		TRYCCALLFORM PARTNER_K{NO:ASSI}
		CATCH
			PRINTFORMW %NAME:ASSI%をパートナーに選択しました
		ENDCATCH
		CFLAG:ASSI:売却不可 = 1
		TALENT:ASSI:服従 = 1
		CFLAG:ASSI:状態 = 2
		CFLAG:ASSI:陥落 = 1
		MARK:ASSI:屈服刻印 = 3
		FLAG:パートナー = NO:ASSI
		SIF RELATION:ASSI:(NO:MASTER) < 200
			RELATION:ASSI:(NO:MASTER) = 200
		SIF RELATION:MASTER:(NO:ASSI) < 200
			RELATION:MASTER:(NO:ASSI) = 200
		キャラ未購入 = 1
ENDSELECT

IF FLAG:ゲームモード != 7
	;チュートリアル表示
	DRAWLINE
	PRINTL チュートリアルを表示しますか？（オプションからでも確認できます）
	PRINTL [0] - 表示する
	PRINTL [1] - 表示しない
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				DRAWLINE
				IF FLAG:ゲームモード == 8
					CALL TEST_TUTORIAL
				ELSE
					CALL TUTORIAL
				ENDIF
				BREAK
			CASE 1
				BREAK
		ENDSELECT
	LOOP 1
ENDIF

DRAWLINE
PRINTL 口上を表示しますか？(オプションでも変更できます)
PRINTL [0] - 口上+地の文を表示する
PRINTL [1] - 口上のみ表示する
PRINTL [2] - 地の文のみ表示する
PRINTL [3] - 口上も地の文も表示しない
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			FLAG:地の文表示 = 1
			FLAG:口上表示 = 1
		CASE 1
			FLAG:地の文表示 = 0
			FLAG:口上表示 = 1
		CASE 2
			FLAG:地の文表示 = 1
			FLAG:口上表示 = 0
		CASE 3
			FLAG:地の文表示 = 0
			FLAG:口上表示 = 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3)

DRAWLINE
PRINTL 実績解除時のメッセージを表示しますか？(オプションからでも変更できます)
PRINTL [0] - 表示しない
PRINTL [1] - 表示する
DO
	INPUT
	FLAG:実績メッセージ = RESULT
LOOP LOOPRES(0, 1)

DRAWLINE
IF QMA
	通貨単位 = マジカ
ELSE
	PRINTL PASELI表示をオンにしますか？(オプションからでも変更できます)
	PRINTL [0] - オンにしない
	PRINTL [1] - オンにする
	DO
		INPUT
		FLAG:パセリ表示 = RESULT
	LOOP LOOPRES(0, 1)
ENDIF

DRAWLINE
PRINTL 奴隷の着衣設定を変更しますか？（オプションでも変更できます）
PRINTL [0] - 服を着たままでいい
PRINTL [1] - 面倒だから脱がせてから調教始める
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			FLAG:着衣設定 = 3
	ENDSELECT
LOOP LOOPRES(0, 1)

BEGIN SHOP

@START_CHARA_SELECT
LOADGLOBAL
DRAWLINE
PRINTL ★★プレイヤーを変更できます★★
PRINTL ★★キャラを選択してください★★

CALL CHARA_LIST, 2

SELECTCASE RESULT
	CASE 10000
		PRINTW プレイヤーを変更しませんでした
		CALL SELECT_SEX
		CALL INPUT_NAME
		CALL SELECT_STYLE
		SELECTCASE FLAG:ゲームモード
			CASE 9
				CALL SELECT_VIRGIN
		ENDSELECT
	CASEELSE
		DELCHARA 0
		ADDCHARA RESULT
		MASTER = 0
		PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
		DRAWLINE
		SELECTCASE FLAG:ゲームモード
			CASE 9
				PRINTFORML %CALLNAME:MASTER%に調教されたい人向けに奴隷としてあなたを追加しますか？
				PRINTL [0] - 調教されたい！
				PRINTL [1] - いらん
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							ADDCHARA 0
							TARGET = 1
							CALL SELECT_SEX, TARGET
							CALL INPUT_NAME, TARGET
							CALL SELECT_VIRGIN, TARGET
							CALL SELECT_STYLE, TARGET
							BREAK
						CASE 1
							BREAK
					ENDSELECT
				LOOP 1
		ENDSELECT
ENDSELECT



;新婚モード専用処理
IF FLAG:ゲームモード == 7
	FLAG:難易度 = 1
	TFLAG:ページ数 = 0
	PRINTL ★★嫁、もしくは婿を選択してください★★
	CALL CHARA_LIST, 1
	ADDCHARA RESULT
	TARGET = 1
	TRYCALLFORM WEDDING_K{NO:TARGET}
	PRINTFORMW %NAME:TARGET%とのご成婚おめでとうございます
	CFLAG:嫁 = 1
	CFLAG:陥落 = 1
	TALENT:恋心 = 0
	TALENT:親愛 = 1
	TALENT:MASTER:恋心 = 0
	TALENT:既婚者 = NO:MASTER
	TALENT:MASTER:既婚者 = NO:TARGET
	MARK:屈服刻印 = 3
	MARK:MASTER:屈服刻印 = 3
	SIF RELATION:(NO:MASTER) < 300
		RELATION:(NO:MASTER) = 300
	SIF RELATION:MASTER:(NO:TARGET) < 300
		RELATION:MASTER:(NO:TARGET) = 300
	PRINTL ご結婚前からいろいろ経験なさりましたよね？
	PRINTL [0] - そりゃまぁ
	PRINTL [1] - 純潔を保ってきた
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				SELECTCASE SEX(MASTER)
					CASE 1
						EXP:MASTER:射精経験 += 10
						EXP:MASTER:性交経験 += 10
						TALENT:MASTER:童貞 = 0
						EXP:性交経験 += 10
						VIRGIN:MASTER:0 = %NAME:TARGET%
						SELECTCASE SEX(TARGET)
							CASE 1
								EXP:MASTER:Ａ経験 += 10
								EXP:射精経験 += 10
								TALENT:童貞 = 0
								VIRGIN:0 = %NAME:MASTER%
							CASE 2
								EXP:膣射経験 += 10
								EXP:Ｖ経験 += 10
								TALENT:処女 = 0
								VIRGIN:1 = %NAME:MASTER%
								CFLAG:処女の奪われ方 = 2
						ENDSELECT
					CASE 2
						EXP:MASTER:Ｖ経験 += 10
						TALENT:MASTER:処女 = 0
						EXP:MASTER:性交経験 += 10
						VIRGIN:MASTER:1 = %NAME:TARGET%
						CFLAG:MASTER:処女の奪われ方 = 2
						SELECTCASE SEX(TARGET)
							CASE 1
								EXP:MASTER:膣射経験 += 10
								EXP:射精経験 += 10
								TALENT:童貞 = 0
								VIRGIN:0 = %NAME:MASTER%
							CASE 2
								EXP:Ｖ経験 += 10
								TALENT:処女 = 0
								VIRGIN:1 = %NAME:MASTER%
								CFLAG:処女の奪われ方 = 2
						ENDSELECT
				ENDSELECT
			CASE 1
				SELECTCASE SEX(MASTER)
					CASE 1
						TALENT:MASTER:童貞 = 1
						EXP:MASTER:性交経験 = 0
					CASE 2
						TALENT:MASTER:処女 = 1
						EXP:MASTER:性交経験 = 0
						EXP:MASTER:Ｖ経験 = 0
						EXP:MASTER:膣射経験 = 0
				ENDSELECT
				SELECTCASE SEX(TARGET)
					CASE 1
						TALENT:童貞 = 1
						EXP:性交経験 = 0
					CASE 2
						TALENT:処女 = 1
						EXP:性交経験 = 0
						EXP:Ｖ経験 = 0
						EXP:膣射経験 = 0
				ENDSELECT
		ENDSELECT
	LOOP LOOPRES(0, 1)
ENDIF

RETURN 0

@SELECT_SEX, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性別を選択してください
PRINTL [0] - 男性
PRINTL [1] - 女性
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			TALENT:ARG:性別 = 1
			CSTR:ARG:上半身上着 = シャツ
			CSTR:ARG:下半身上着 = ズボン
			CSTR:ARG:下半身下着 = トランクス
		CASE 1
			TALENT:ARG:性別 = 2
			TALENT:ARG:バストサイズ = 3
			CSTR:ARG:全身上着 = ワンピース
			CSTR:ARG:上半身下着 = ブラジャー
			CSTR:ARG:下半身下着 = パンティ
			CFLAG:ARG:挿入可 = 1
	ENDSELECT
LOOP LOOPRES(0, 1)

@INPUT_NAME, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
DO
	PRINT 名前を入力してください(何も入力しない場合は「
	PRINTBUTTON "あなた", "あなた"
	PRINTL 」になります)
	INPUTS
	SELECTCASE RESULTS
		CASE ""
			CALLNAME:ARG = あなた
			NAME:ARG = あなた
		CASEELSE
			CALLNAME:ARG = %RESULTS%
			NAME:ARG = %RESULTS%
	ENDSELECT
	PRINTFORML 「%CALLNAME:ARG%」でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
LOOP LOOPRES(0)

@SELECT_VIRGIN, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 性交の経験は……
PRINTL [0] - ある
PRINTL [1] - 無い
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			SELECTCASE SEX(ARG)
				CASE 1
					TALENT:ARG:童貞 = 1
				CASE 2
					TALENT:ARG:処女 = 1
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SELECT_DIFFICULT
DRAWLINE
PRINTL ★★難易度を選択してください★★
IF QMA
	PRINTL [0] - フェアリー級(初心者でも安心)
	PRINTL [1] - ミノフェニ級(それなりに)
	PRINTL [2] - ドラゴン級(ようこそ廃人の入口へ)
ELSE
	PRINTL [0] - NORMAL
	PRINTL [1] - HYPER
	PRINTL [2] - ANOTHER
ENDIF
DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 2
			FLAG:難易度 = RESULT+1
			BREAK
	ENDSELECT
LOOP 1

@SELECT_STYLE, ARG = -1
SIF ARG == -1
	ARG = MASTER
DRAWLINE
PRINTL 体型はどんな感じですか？
PRINTL [0] - 普通の人です
SELECTCASE SEX(ARG)
	CASE 1
		PRINTL [1] - ショタです
	CASE 2
		PRINTL [1] - ロリです
ENDSELECT
DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			TALENT:ARG:小柄体型 = 1
			MAXBASE:ARG:体力 -= 200
			BASE:ARG:体力 -= 200
			SELECTCASE SEX(ARG)
				CASE 2
					TALENT:ARG:バストサイズ = 2
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(0, 1)

@SETUP_GAMESTART
DAY = 1
SELECTCASE FLAG:ゲームモード
	CASE 0
		MONEY = 3000
		FLAG:価格フィルタ上限 = 3000
	CASE 1
		MONEY = 1000000
		ITEM:風俗店営業許可証 = 1
		娼館広さ = 1
		娼館部屋数 = 1
		娼館レベルアップ必要額 = 1000
	CASE 7
		MONEY = 3000000
	CASE 8
		MONEY = 0
		FLAG:価格フィルタ上限 = 30000
	CASE 9
		MONEY = 30000
		FLAG:価格フィルタ上限 = 30000
ENDSELECT
FLAG:カスタムフィルタ１ = 1
FLAG:カスタムフィルタ２ = 3
FLAG:色仕掛け = 1
LASTMONEY = MONEY
;リアルタイム計算処理 変数REALTIMEも残しておくこと
;リアルタイム(文字列式)からリアル西暦を抜き出して数値型に変換して代入する……
REALTIME:0 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
REALTIME:1 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
REALTIME:2 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

YEAR = REALTIME:0
MONTH = REALTIME:1
DAYS = REALTIME:2

;曜日計算
FLAG:曜日 = ZELLER(YEAR, MONTH, DAYS)

;月齢計算
LOCAL = ((YEAR-11)%19)*11
SELECTCASE MONTH
	CASE 1, 3
		LOCAL:1 = 0
	CASE 2, 4, 5
		LOCAL:1 = 2
	CASE 6 TO 12
		LOCAL:1 = MONTH-2
ENDSELECT

月齢 = (LOCAL+LOCAL:1+DAYS)%30

VARSET LOCAL, 0

VARSET SELECTCOM, -1
SELECTCOM = 0

;気温の算出
CALL TEMPERATURE
;天気の決定
CALL WEATHER


