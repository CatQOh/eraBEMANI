@WEATHER
#DIM DYNAMIC 降雪確率補正

;FLAG:天気 1=晴れ 2=曇り 3=雨 4=雪

;梅雨入りと梅雨明けの処理
SELECTCASE MONTH
	CASE 6
		SIF RAND:101 < DAYS*10
			FLAG:梅雨 = 1
	CASE 7
		SIF RAND:101 < DAYS*3
			FLAG:梅雨 = 0
	CASE 8
		SIF FLAG:梅雨 && RAND:101 < DAYS*3+93
			FLAG:梅雨 = 0
ENDSELECT

SELECTCASE TIME
	;昼 前日の気温には左右されるが、前日の天気は引きずりにくい
	CASE 0
		;ここで雨量指数(FLAG:降水量)を設定する
		CALL PRECIPITATION
		;梅雨のときは降水量を増やす 数値は適当
		SIF FLAG:梅雨
			FLAG:降水量 *= 2
		SIF FLAG:梅雨 && FLAG:降水量
			FLAG:降水量 += RAND:20
		
		;1ミリでも降水量があるとき、雨とする
		IF FLAG:降水量
			FLAG:天気 = 3
		;それ以外の場合、曇りか晴れかの判定
		ELSE
			;前日の昼との温度差を比較する
			;気温が上がっている場合、曇りになる確率は低い
			;気温が下がっている場合、曇りになる確率が若干高い
			LOCAL = 150+(FLAG:気温-前の昼の気温)
			;前日が晴れなら曇りになりにくい
			SIF FLAG:天気 == 1
				LOCAL += 10
			;補正なしの状態で1/4で曇り
			IF RAND:200 >= LOCAL
				FLAG:天気 = 2
			ELSE
				FLAG:天気 = 1
			ENDIF
		ENDIF
		
		;前の昼の気温の参照が完了したら、1,2ターン後のためにフラグ代入しておく
		前の昼の気温 = FLAG:気温
		
	;夜 昼の天気を引きずりやすいが、気温に左右されにくい
	;夜になって昼より気温が上がることはないが、デバッグモード使えば出来るから一応処理書いとく
	CASE 1
		;現在の天気を参照
		SELECTCASE FLAG:天気
			;晴れ
			CASE 1
				;気温が下がってない場合、晴れをキープ
				IF 前の昼の気温 <= FLAG:気温
					FLAG:天気 = 1
				;気温が下がっている場合
				ELSE
					;昼との温度差を比較する
					;気温が下がっているほど、曇りになる確率が高くなる
					LOCAL = 160+(FLAG:気温-前の昼の気温)
					;補正なしの状態で1/5で曇り
					IF RAND:200 >= LOCAL
						FLAG:天気 = 2
					ELSE
						FLAG:天気 = 1
					ENDIF
				ENDIF
			;曇り
			CASE 2
				;気温が下がってない場合、晴れにする
				IF 前の昼の気温 <= FLAG:気温
					FLAG:天気 = 1
				;気温が下がっている場合
				ELSE
					;雨量指数(FLAG:降水量)を再計算
					CALL PRECIPITATION
					;1ミリでも降水量があるとき、雨とする
					IF FLAG:降水量
						FLAG:天気 = 3
					;それ以外の場合、曇りか晴れかの判定
					ELSE
						;前日の昼との温度差を比較する
						;気温が下がっているほど、曇りになる確率が高くなる
						LOCAL = 150+(FLAG:気温-前の昼の気温)
						;補正なしの状態で1/4で曇り
						IF RAND:200 >= LOCAL
							FLAG:天気 = 2
						ELSE
							FLAG:天気 = 1
						ENDIF
					ENDIF
				ENDIF
			;雨,雪
			CASE 3
				;気温が下がってない場合、曇りにする
				IF 前の昼の気温 <= FLAG:気温
					FLAG:天気 = 2
					FLAG:降水量 = 0
				;気温が下がった場合、雨,雪をキープし降水量を増やす
				ELSE
					FLAG:降水量 += RAND:(前の昼の気温-FLAG:気温)
				ENDIF
		ENDSELECT
ENDSELECT


;降水時に雨か雪かの処理
;一応5月～10月の設定はしてるけど、この条件式だと雪になることはまず無いと思われる
SELECTCASE MONTH
	CASE 1, 2, 12
		降雪確率補正 = 25
	CASE 3, 4, 11
		降雪確率補正 = 20
	CASE 5, 6, 10
		降雪確率補正 = 10
	CASE 7, 8, 9
		降雪確率補正 = 5
ENDSELECT

;雨,雪の分岐処理 一定以下の気温だと乱数次第でずっと雪が続く
IF FLAG:天気 >= 3
	SELECTCASE FLAG:気温
		;気温0℃以下は雪
		CASE IS <= 0
			FLAG:天気 = 4
		;0.1℃以上の場合
		CASEELSE
			;12月～2月は2.5℃以下で雪確定
			;3月、4月、11月は2.0℃以下で雪確定
			;5月、6月、10月は1.0℃以下で雪確定
			;7月～9月は0.5℃以下で雪確定
			SELECTCASE FLAG:気温 - 降雪確率補正
				CASE IS <= 0
					FLAG:天気 = 4
				CASEELSE
					;降雪確率補正が低いほど雪になる確率は低い
					;雪が降る可能性のある最高気温
					;1月、2月、12月は12.9℃
					;3月、4月、11月は8.3℃
					;5月、6月、10月は2.1℃
					;7月～9月は0.5℃ つまり確定時以外降らない
					IF FLAG:気温*5/降雪確率補正 <= 降雪確率補正
						;上記の気温以下なら、気温が低ければ低いほど雪の確率アップ
						IF RAND:(FLAG:気温) < 降雪確率補正
							FLAG:天気 = 4
						ELSE
							FLAG:天気 = 3
						ENDIF
					ELSE
						FLAG:天気 = 3
					ENDIF
			ENDSELECT
	ENDSELECT
ENDIF
