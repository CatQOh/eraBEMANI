@WEATHER
#DIM DYNAMIC 降雪確率補正

;FLAG:天気 1=晴れ 2=雨 3=雪

;梅雨入りと梅雨明けの処理
SELECTCASE MONTH
	CASE 6
		SIF RAND:101 < DAYS*10
			FLAG:梅雨 = 1
	CASE 7
		SIF RAND:101 < DAYS*3
			FLAG:梅雨 = 0
	CASE 8
		SIF FLAG:梅雨 && RAND:101 < DAYS*3+93
			FLAG:梅雨 = 0
ENDSELECT


;ここで雨量指数(FLAG:降水量)を設定する


SELECTCASE TIME
	;昼 前日の気温には左右されるが、前日の天気は引きずりにくい
	CASE 0
		;梅雨のときは降水量を増やす 数値は適当
		SIF FLAG:梅雨
			FLAG:降水量 *= 2
		
		;気温が上がっている場合、雨になる確率は低い（雨天に必要な降水量が多い）
		IF 前ターンの気温 < FLAG:気温
			;温度差があればあるほど晴れやすい
			LOCAL = 20+(FLAG:気温-前ターンの気温)
		;気温が下がっている場合、雨になる確率が若干高い
		ELSEIF 前ターンの気温 > FLAG:気温
			;温度差があればあるほど降りやすい
			LOCAL = 20-(前ターンの気温-FLAG:気温)
		;気温の変動が無い場合
		ELSE
			;特に補正はなし
			LOCAL = 20
		ENDIF
		;前日が晴れなら雨になりにくい
		SIF FLAG:天気 == 1
			LOCAL += 5
		;降水量がしきい値を超えたら雨
		IF FLAG:降水量 > LOCAL
			FLAG:天気 = 2
		;それ以外は晴れ
		ELSE
			FLAG:天気 = 1
		ENDIF
	;夜 昼の天気を引きずりやすいが、気温に左右されにくい
	;夜になって昼より気温が上がることはないが、デバッグモード使えば出来るから一応処理書いとく
	CASE 1
		;現在の天気を参照
		SELECTCASE FLAG:天気
			;晴れ
			CASE 1
				;気温が上がっている場合、晴れをキープ
				IF 前ターンの気温 < FLAG:気温
					FLAG:天気 = 1
				;気温が下がっている場合
				ELSEIF 前ターンの気温 > FLAG:気温
					;降水量次第でランダムに雨 数値は適当
					LOCAL = 20
					;気温の下がり具合に応じて降水量を増やし、しきい値を超えれば雨になる
					FLAG:降水量 += RAND:(前ターンの気温-FLAG:気温)
					IF FLAG:降水量 > LOCAL
						FLAG:天気 = 2
					;上記条件を満たしていなければ晴れをキープ
					ELSE
						FLAG:天気 = 1
					ENDIF
				;気温の変動が無い場合、晴れをキープ
				ELSE
					FLAG:天気 = 1
				ENDIF
			;雨
			CASE 2
				;気温が上がっている場合
				IF 前ターンの気温 < FLAG:気温
					;降水量次第でランダムに晴れ 数値は適当
					LOCAL = 10
					SIF FLAG:梅雨
						LOCAL = 5
					;気温の上がり具合に応じて降水量を減らし、しきい値を下回れば晴れになる
					FLAG:降水量 -= RAND:(FLAG:気温-前ターンの気温)
					IF FLAG:降水量 > LOCAL
						FLAG:天気 = 2
					;上記条件を満たしていなければ晴れをキープ
					ELSE
						FLAG:天気 = 1
					ENDIF
				;気温が下がっている場合、雨をキープ 気温次第で雪に派生
				ELSEIF 前ターンの気温 > FLAG:気温
					FLAG:天気 = 2
				;気温の変動が無い場合、雨をキープ
				ELSE
					FLAG:天気 = 2
				ENDIF
			;雪
			CASE 3
				;気温が上がっている場合
				IF 前ターンの気温 < FLAG:気温
				
				;気温が下がっている場合
				ELSEIF 前ターンの気温 > FLAG:気温
				
				;気温の変動が無い場合
				ELSE
				
				ENDIF
		ENDSELECT
ENDSELECT


;降水時に雨か雪かの処理
;一応5月〜10月の設定はしてるけど、この条件式だと雪になることはまず無いと思われる
SELECTCASE MONTH
	CASE 1, 2, 12
		降雪確率補正 = 25
	CASE 3, 4, 11
		降雪確率補正 = 20
	CASE 5, 6, 10
		降雪確率補正 = 10
	CASE 7, 8, 9
		降雪確率補正 = 5
ENDSELECT

;雨→雪の派生処理 雪→雨の派生は上で行うため、一定以下の気温だと乱数次第でずっと雪が続く
IF FLAG:天気 >= 2
	SELECTCASE FLAG:気温
		;気温0℃以下は雪
		CASE IS <= 0
			FLAG:天気 = 3
		;0.1℃以上の場合
		CASEELSE
			;12月〜2月は2.5℃以下で雪確定
			;3月、4月、11月は2.0℃以下で雪確定
			;5月、6月、10月は1.0℃以下で雪確定
			;7月〜9月は0.5℃以下で雪確定
			SELECTCASE FLAG:気温 - 降雪確率補正
				CASE IS <= 0
					FLAG:天気 = 3
				CASEELSE
					;降雪確率補正が低いほど雪になる確率は低い
					;雪が降る可能性のある最高気温
					;1月、2月、12月は12.9℃
					;3月、4月、11月は8.3℃
					;5月、6月、10月は2.1℃
					;7月〜9月は0.5℃ つまり確定時以外降らない
					IF FLAG:気温*5/降雪確率補正 <= 降雪確率補正
						;上記の気温以下なら、気温が低ければ低いほど雪の確率アップ
						SIF RAND:(FLAG:気温) < 降雪確率補正
							FLAG:天気 = 3
					ENDIF
			ENDSELECT
	ENDSELECT
ENDIF

;全ての天気処理が終了したら、次のターンのためにフラグ代入しておく
前ターンの気温 = FLAG:気温

