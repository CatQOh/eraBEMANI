@GAMBLE, カジノの種類
#DIM DYNAMIC チップ
#DIM DYNAMIC 利子
#DIM DYNAMIC 利益計算用
#DIM DYNAMIC パートナー
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC パートナー必要
#DIM DYNAMIC ディーラーあり
#DIM DYNAMIC 収支
#DIM DYNAMIC カジノの種類 = 1;引数指定が無ければ表カジノ扱い
#DIM DYNAMIC キャラ価格
#DIM DYNAMIC 売却候補, 1000
#DIM DYNAMIC 売却総額
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LASSI
;新しくゲームを追加する場合はここに名称を追加する
{
#DIMS DYNAMIC ゲーム名, 10 = 
"ルーレット",
"クラップス",
"ブラックジャック",
"ビンゴピンボール",
"ビデオポーカー",
"ポーカーソリティア",
"レットイットライド",
"バカラ",
"サーティー",
"素数ダイス",
}
;"神経衰弱",

;ギャンブルで借金を背負った場合の利子(難易度によって変動)
SELECTCASE FLAG:難易度
	;NORMAL
	CASE 1
		利子 = 25
	;HYPER
	CASE 2
		利子 = 55
	;ANOTHER
	CASE 3
		利子 = 90
ENDSELECT

;始めるときの金額を記憶
利益計算用 = MONEY

DRAWLINE
IF (TARGET > 0 && TALENT:アニマル) || (ASSI > 0 && TALENT:ASSI:アニマル)
	IF LANGUAGE == "ENG"
		PRINT Casino is not allowed entry with pet. 
		IF TALENT:アニマル
			PRINTFORM %CALLNAMEDISP(TARGET)% 
			TARGET = -1
			IF ASSI > 0 && TALENT:ASSI:アニマル
				PRINTFORM and %CALLNAMEDISP(ASSI)% are 
				ASSI = -1
			ELSE
				PRINT is 
			ENDIF
		ELSEIF ASSI > 0 && TALENT:ASSI:アニマル
			PRINTFORM %CALLNAMEDISP(ASSI)% is 
			ASSI = -1
		ENDIF
		PRINTFORMW stay at home.
	ELSE
		PRINT カジノにペットの入店は許可されていません。
		IF TALENT:アニマル
			PRINTFORM %CALLNAME:TARGET%
			TARGET = -1
			IF ASSI > 0 && TALENT:ASSI:アニマル
				PRINTFORM と%CALLNAME:ASSI%
				ASSI = -1
			ENDIF
		ELSEIF ASSI > 0 && TALENT:ASSI:アニマル
			PRINTFORM %CALLNAME:ASSI%
			ASSI = -1
		ENDIF
		PRINTFORMW を家に置いてきました
	ENDIF
	DRAWLINE
ENDIF
SELECTCASE カジノの種類
	;表カジノ
	CASE 1
		;PLAYBGM カジノBGM
		IF LANGUAGE == "ENG"
			PRINTL Welcome to casino! What play game you do?
			PRINTL But If you debt and can't payment,
			PRINTFORML {利子}\% interest rate will arise.(We claim debt every 10 days)
		ELSE
			PRINTL カジノへようこそ。どのギャンブルで遊びますか？
			PRINTL ただし、ギャンブル中に借金を背負って返済できなかった場合、
			PRINTFORML その借金に対し、{利子}\%の利子が付きます。(10日に一度、返済を要求されます。その度に利子が発生します)
		ENDIF
	;裏カジノ
	CASE 2
		IF LANGUAGE == "ENG"
			PRINTL Welcome to Darkness casino! What play game you do?
			PRINTL But If you debt and can't payment,
			PRINTL You will get 
			FONTBOLD
			PRINTL TERRIBLE PENALTY.
			FONTREGULAR
		ELSE
			PRINTL 大金と命を賭けたクレイジーなカジノへようこそ！どのギャンブルで遊びますか？
			PRINTL ただし、ギャンブル中に借金を背負って返済できなかった場合、
			FONTBOLD
			PRINT 裏カジノ特有のやり方
			FONTREGULAR
			PRINTL で借金の返済をしていただきます。
		ENDIF
ENDSELECT
IF LANGUAGE== "ENG"
	CALL PASELIL, @"Moneys:{MONEY}＄"
	[IF_DEBUG]
		CALL PASELIL, @"Total Income:{ギャンブル総収支}＄"
		PRINTFORML Carol's Rating:{キャロル好感度}
		PRINTFORML Kotomi's Rating:{ことみちゃん好感度}
	[ENDIF]
ELSE
	CALL PASELIL, @"所持金:{MONEY}＄"
	[IF_DEBUG]
		CALL PASELIL, @"総収支:{ギャンブル総収支}＄"
		PRINTFORML キャロル好感度:{キャロル好感度}
		PRINTFORML ことみちゃん好感度:{ことみちゃん好感度}
	[ENDIF]
ENDIF

FOR LCOUNT, 0, VARSIZE("ゲーム名")
	SELECTCASE ゲーム名:LCOUNT
		;バカラは裏専用
		CASE "バカラ"
			SIF カジノの種類 == 1
				CONTINUE
	ENDSELECT
	IF LANGUAGE == "ENG"
		PRINTFORMLC [{LCOUNT}] %GAMBLEDISP(ゲーム名:LCOUNT)%
	ELSE
		PRINTFORMLC [{LCOUNT}] %ゲーム名:LCOUNT%
	ENDIF
NEXT
SIF !LINEISEMPTY()
	PRINTL
[IF_DEBUG]
	PRINTL [96] モンテカルロ
	PRINTL [97] ナンプレ
	PRINTL [98] ねらえ！ぴったり７
[ENDIF]
DRAWLINE
PRINTPLAIN 　GAMBLE　
PRINTL [99] やめておく
$SELECT_GAMBLE
DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO VARSIZE("ゲーム名")
			SIF LANGUAGE == "JP"
				PRINTFORMW %CALLNAME:MASTER%は%ゲーム名:RESULT%で遊ぶことにした。
			;ディーラーが存在するゲームならフラグを立てる
			SELECTCASE ゲーム名:RESULT
				CASE "ルーレット", "クラップス", "ブラックジャック", "ポーカーソリティア", "レットイットライド", "バカラ"
					ディーラーあり = 1
			ENDSELECT
			;パートナーが必要なゲームならフラグを立てる
			;SELECTCASE ゲーム名:RESULT
			;	CASE
			;		パートナー必要 = 1
			;ENDSELECT
			IF パートナー必要
				TRYCCALLFORM %ゲーム名:RESULT%, パートナー
				CATCH
					PRINTFORMW %ゲーム名:RESULT%は未実装です
					RESTART
				ENDCATCH
			ELSE
				TRYCCALLFORM %ゲーム名:RESULT%, カジノの種類
				CATCH
					PRINTFORMW %ゲーム名:RESULT%は未実装です
					RESTART
				ENDCATCH
			ENDIF
			BREAK
		CASE 96
			[IF_DEBUG]
				CALL モンテカルロ
				BREAK
			[ENDIF]
		CASE 97
			[IF_DEBUG]
				CALL ナンプレ
				BREAK
			[ENDIF]
		CASE 98
			[IF_DEBUG]
				CALL ねらえ！ぴったり７, 100000
				BREAK
			[ENDIF]
		CASE 99
			CALL FADEOUTBGM
			RETURN 0
	ENDSELECT
LOOP 1

CALL CHECKDRAWLINE

;今回のギャンブルでの収支
収支 = MONEY-利益計算用
;ギャンブルでの総収支
ギャンブル総収支 += 収支

;バンドだけでめしを食ってないのでフラグを消す
バンめし = 0

キャロルお誘いフラグ = 0
ことみちゃんお誘いフラグ = 0

PRINTFORML %CALLNAME:MASTER%はギャンブルを終えた。
SELECTCASE 収支
	CASE IS > 0
		;PLAYSOUND 出入金
		IF LANGUAGE == "ENG"
			CALL PASELIW, @"You profited {収支}＄!"
		ELSE
			CALL PASELIW, @"{収支}＄儲かった！"
		ENDIF
		;勝ち分の桁数の20倍だけキャロルの好感度が増える
		SIF ディーラーあり
			キャロル好感度 += 20*(1+LOG10(収支))
		;ことみちゃんの好感度は収支が5桁なら+100,6桁なら+200,…と増える
		SIF 収支 >= 10000
			ことみちゃん好感度 += 100*(1+LOG10(収支/10000))
		;総収支がプラスかつキャロルの好感度が1000以上のときお誘いを受ける
		IF ギャンブル総収支 > 0 && キャロル好感度 >= 1000 && GETCHARANAME("キャロル") == -1 && キャロル進行フラグ >= 0
			SELECTCASE キャロル進行フラグ
				CASE 0
					PRINTFORML 今日も勝つことができた%CALLNAME:MASTER%が屋敷に戻ろうとしたとき
					PRINTL バニーガールの格好をしたディーラーが後ろからいきなり話しかけてきた。
					PRINTL 彼女の名前はキャロルというようだ。
					PRINTFORML どうやら何度も勝利を収める%CALLNAME:MASTER%に惚れ込みこの後お相手してほしいそうだ。
					キャロル進行フラグ = 1
				CASE 1
					PRINTFORML キャロルが%CALLNAME:MASTER%に再び誘いをかけてきた。
			ENDSELECT
			キャロルお誘いフラグ = 1
		;ことみちゃんの好感度が1000以上のときお誘いを受ける
		ELSEIF ことみちゃん好感度 >= 1000 && GETCHARANAME("ことみちゃん") == -1 && ことみちゃん進行フラグ >= 0
			SELECTCASE ことみちゃん進行フラグ
				CASE 0
					PRINTFORML %CALLNAME:MASTER%が帰ろうとしたそのとき、物陰から優しく袖を引っ張られた。
					PRINTL 引っ張ってきたのは「ことみちゃん」と呼ばれる凄腕のギャンブラーであり、
					PRINTFORML 派手な勝ちを挙げている%CALLNAME:MASTER%に魅力を感じたらしくこの後お相手してほしいそうだ。
					ことみちゃん進行フラグ = 1
				CASE 1
					PRINTFORML ことみちゃんが%CALLNAME:MASTER%にこっそり誘いをかけてきた。
			ENDSELECT
			ことみちゃんお誘いフラグ = 1
		ENDIF
	CASE IS < 0
		CALL PASELIW, @"{ABS(収支)}＄損してしまった……"
		;負け分の桁数の10倍だけキャロルの好感度が減る
		SIF ディーラーあり
			キャロル好感度 -= 10*(1+LOG10(-収支))
	CASE 0
		CALL PASELIW, "今回の収支はちょうど0＄のようだ。"
ENDSELECT

;お誘いを受けたとき
IF キャロルお誘いフラグ
	CALL キャロルイベント
ELSEIF ことみちゃんお誘いフラグ
	CALL ことみちゃんイベント
ENDIF

;借金を背負ったとき
IF MONEY < 0
	SELECTCASE カジノの種類
		;表カジノ 借金に上乗せ
		CASE 1
			IF LANGUAGE == "ENG"
				CALL PASELIL, @"You debted {ABS(MONEY)}＄,"
				CALL PASELIL, @"Added {ABS(MONEY)*利子/100}＄ interest. You got {ABS(MONEY)+ABS(MONEY)*利子/100}＄ debt."
				借金 += ABS(MONEY)+ABS(MONEY)*利子/100
				MONEY = 0
				CALL PASELIW, @"You have total of {借金}＄ debt."
			ELSE
				CALL PASELIL, @"ギャンブル中に借金{ABS(MONEY)}＄を背負ったため、"
				CALL PASELIL, @"その{ABS(MONEY)}＄には利子{ABS(MONEY)*利子/100}＄が付き、借金は{ABS(MONEY)+ABS(MONEY)*利子/100}＄となります。"
				借金 += ABS(MONEY)+ABS(MONEY)*利子/100
				MONEY = 0
				CALL PASELIW, @"借金の総額は{借金}＄となりました。"
			ENDIF
		CASE 2
			PRINTW ………………
			PRINTW …………
			PRINTW ……
			$返済
			IF LANGUAGE == "ENG"
				PRINTFORMW %CALLNAMEDISP(MASTER)% is losed and debt.
				PRINTFORMW We give TERRIBLE PENALTY for %CALLNAMEDISP(MASTER)%.
				PRINTL It is...
				PRINTL [0] Sell Characters
				PRINTL [1] You payment your body
			ELSE
				PRINTFORMW %CALLNAME:MASTER%はギャンブルに負け、借金を背負ってしまった
				PRINTW このカジノでは負けた場合、金銭以外のもので返済に当てなくてはならない
				PRINTL 返済の方法は……
				PRINTL [0] キャラを売り飛ばす
				PRINTL [1] 自らの身体で払う
			ENDIF
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						$売却キャラ選択
						IF LANGUAGE == "ENG"
							CALL PASELIL, @"□Who do you sell?(Total Debt:{ABS(MONEY)}＄)"
						ELSE
							CALL PASELIL, @"□誰を売り飛ばしますか？(返済額:{ABS(MONEY)}＄)"
						ENDIF
						;売却可能でないキャラも売り飛ばせるが、総じて売値は半額になる 隠しキャラとアニマル、魔法効果発動中のキャラは売れない
						FOR PAGING, 0, CHARANUM
							LOCAL = 1
							SIF PAGING == MASTER
								LOCAL = 0
							SIF CFLAG:PAGING:憑依中
								LOCAL = 0
							SIF TALENT:PAGING:アニマル
								LOCAL = 0
							SIF TALENT:PAGING:隠しキャラ
								LOCAL = 0
							IF PAGING > 0
								CALL CHARA_SELL, PAGING
								IF RESULT
									キャラ価格 = RESULT/2
								ELSE
									キャラ価格 = 0
								ENDIF
							ENDIF
							IF LANGUAGE == "ENG"
								CALL PAGING, LOCAL, @"Price:{キャラ価格} \@ 売却候補:PAGING ? [SELECT] #  \@"
							ELSE
								CALL PAGING, LOCAL, @"価格:{キャラ価格} \@ 売却候補:PAGING ? 売却候補 #  \@"
							ENDIF
						NEXT
						SELECTCASE RESULT
							CASE -1
								VARSET 売却候補
								GOTO 返済
							CASE 1 TO CHARANUM-1
								INVERTBIT 売却候補:RESULT, 0
						ENDSELECT
						売却総額 = 0
						FOR LCOUNT, 1, 1000
							IF 売却候補:LCOUNT
								CALL CHARA_SELL, LCOUNT
								SIF RESULT > 0
									売却総額 += RESULT/2
							ENDIF
							SIF 売却総額 >= ABS(MONEY)
								BREAK
						NEXT
						SIF 売却総額 < ABS(MONEY)
							GOTO 売却キャラ選択
						PRINTL □以下のキャラを裏カジノに対する返済として売り飛ばします。本当によろしいですか？
						FOR LCOUNT, 1, 1000
							SIF 売却候補:LCOUNT
								PRINTFORML 　%NAME:LCOUNT%
						NEXT
						DRAWLINE
						PRINTFORML 　総額:{売却総額}
						PRINTL 　[0] はい
						PRINTL 　[1] いいえ
						DO
							INPUT
							SELECTCASE RESULT
								CASE 0
									PRINTL 裏カジノに売り飛ばされた者たちは、別れを惜しむ間もなく黒ずくめの関係者に連れて行かれてしまった
									PRINTFORMW 皆がこれからどんな目に遭うのか、%CALLNAME:MASTER%には知る由もない……
									MONEY += 売却総額
									FOR LCOUNT, 1, 1000
										IF 売却候補:LCOUNT
											LOCAL = LCOUNT
											;アイドルユニットに属していた場合、そのユニットは解散
											SIF CFLAG:LOCAL:ユニット番号
												CALL BREAKUP, CFLAG:LOCAL:ユニット番号
											DELCHARA LOCAL
											LTARGET = TARGET
											LASSI = ASSI
											SIF LOCAL == LTARGET
												TARGET = -1
											SIF LOCAL == LASSI
												ASSI = -1
											SIF LOCAL < LTARGET
												TARGET--
											SIF LOCAL < LASSI
												ASSI--
										ENDIF
									NEXT
									RETURN 1
								CASE 1
									VARSET 売却候補
									GOTO 売却キャラ選択
							ENDSELECT
						LOOP 1
					CASE 1
						PRINTFORMW %CALLNAME:MASTER%はやむを得ず、自らの身体で借金を支払うことにした……
						PRINTW ………………
						PRINTW …………
						PRINTW ……
						;ペナルティは借金10万ごとに1日+3日
						;STRATEGYモードで裏カジノ負けた場合はバッドエンド直行
						IF ゲームモード() == "経営モード"
							CALL BADENDING_GAMBLE
						ELSE
							PRINTFORMW 調教する立場の%CALLNAME:MASTER%が散々に乱暴な調教を受け、解放されたときには{ABS(収支)/100000+3}日が経過していた……
							PRINTFORMW %CALLNAME:MASTER%は思い出しただけで吐き気を催す仕打ちに身を震わせながら、ぼろぼろの体を抱えてかろうじて屋敷に帰った……
							LOCAL = ABS(収支)/100000+3
							SELECTCASE SEX(MASTER)
								CASE 1
									PRINTFORML Ａ経験+{LOCAL/3}(%NAME:MASTER%)
									PRINTFORML 絶頂経験+{LOCAL/5*2}(%NAME:MASTER%)
									PRINTFORML 性交経験+{LOCAL/3}(%NAME:MASTER%)
									PRINTFORML 射精経験+{LOCAL/5}(%NAME:MASTER%)
									PRINTFORML 精液経験+{LOCAL}(%NAME:MASTER%)
									PRINTFORML ゲイ経験+{LOCAL}(%NAME:MASTER%)
									PRINTFORML フェラ経験+{LOCAL/5}(%NAME:MASTER%)
									PRINTFORML 緊縛経験+{LOCAL/7}(%NAME:PLAYER%)
									PRINTFORML Ａ拡張経験+{LOCAL/10}(%NAME:PLAYER%)
									EXP:MASTER:Ａ経験 += LOCAL/3
									EXP:MASTER:絶頂経験 += LOCAL/5*2
									EXP:MASTER:性交経験 += LOCAL/3
									EXP:MASTER:射精経験 += LOCAL/5
									EXP:MASTER:精液経験 += LOCAL
									EXP:MASTER:ゲイ経験 += LOCAL
									EXP:MASTER:フェラ経験 += LOCAL/5
									EXP:MASTER:緊縛経験 += LOCAL/7
									EXP:MASTER:Ａ拡張経験 += LOCAL/10
									WAIT
								CASE 2
									PRINTFORML Ｖ経験+{LOCAL/4}(%NAME:MASTER%)
									PRINTFORML Ａ経験+{LOCAL/6}(%NAME:MASTER%)
									PRINTFORML 絶頂経験+{LOCAL/5}(%NAME:MASTER%)
									PRINTFORML 性交経験+{LOCAL/3}(%NAME:MASTER%)
									PRINTFORML 膣射経験+{LOCAL/4}(%NAME:MASTER%)
									PRINTFORML 放尿経験+{LOCAL/10}(%NAME:MASTER%)
									PRINTFORML フェラ経験+{LOCAL/5}(%NAME:MASTER%)
									PRINTFORML 緊縛経験+{LOCAL/7}(%NAME:MASTER%)
									PRINTFORML Ｖ拡張経験+{LOCAL/10}(%NAME:MASTER%)
									PRINTFORML Ａ拡張経験+{LOCAL/10}(%NAME:MASTER%)
									PRINTFORML 流産経験+{LOCAL/100}(%NAME:MASTER%)
									EXP:MASTER:Ｖ経験 += LOCAL/4
									EXP:MASTER:Ａ経験 += LOCAL/6
									EXP:MASTER:絶頂経験 += LOCAL/5
									EXP:MASTER:性交経験 += LOCAL/3
									EXP:MASTER:膣射経験 += LOCAL/4
									EXP:MASTER:放尿経験 += LOCAL/10
									EXP:MASTER:フェラ経験 += LOCAL/5
									EXP:MASTER:緊縛経験 += LOCAL/7
									EXP:MASTER:Ｖ拡張経験 += LOCAL/10
									EXP:MASTER:Ａ拡張経験 += LOCAL/10
									EXP:MASTER:流産経験 += LOCAL/100
									WAIT
							ENDSELECT
							REPEAT ABS(収支)/100000+3
								CALL NEXTDAY
							REND
							MONEY += ABS(収支)
							RETURN 1
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
ENDIF

CALL FADEOUTBGM

RETURN 1

@キャロルイベント
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 道具フラグ退避用, 2000
PRINTL [0] - キャロルの誘いを受ける
SIF ASSI > 0
	PRINTFORML [1] - %CALLNAME:ASSI%と一緒にキャロルの誘いを受ける
PRINTL [2] - キャロルの誘いを断る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0, 1
			IF RESULT == 0
				ASSI = -1
			ELSEIF RESULT == 1 && ASSI < 0
				CONTINUE
			ENDIF
			PRINTFORMW キャロルの誘いに乗ると、%CALLNAME:MASTER%はカジノ施設の一級スイートルームへと案内された
			PRINTFORML きらびやかな装飾や家具、ワイングラスが部屋を彩る中、キャロルは制服のバニースーツで淫らなポーズを取りながら、
			PRINTFORMW %CALLNAME:MASTER%をベッドへと誘惑している。さて、どうしようか……
			;道具は一時的に無しにする
			ARRAYCOPY "ITEM", "道具フラグ退避用"
			VARSET ITEM
			;お酒と避妊具は用意してある
			ITEM:コンドーム = 99
			ITEM:スーパードライ = 12
			ITEM:一番搾り = 12
			ITEM:プレミアムモルツ = 12
			ITEM:ヱビス = 12
			ITEM:皆伝 = 10
			ITEM:魔王 = 10
			ITEM:アルパカ = 10
			ITEM:倉吉 = 10
			;調教開始準備
			;過去に誘いに乗ったならそのステータスを引き継ぎ
			IF CHKCHARADATA(@"キャロル_{データID}") == 0
				LOADCHARA @"キャロル_{データID}"
			ELSE
				ADDCHARA GETNO("キャロル")
			ENDIF
			TARGET = CHARANUM-1
			;好感度がそのまま依存度になる
			CFLAG:依存度 = キャロル好感度
			;ここから調教開始
			;BEGIN TRAINで行われる変数初期化
			ASSIPLAY:0 = 0
			PREVCOMS = 
			SELECTCOMS = 
			;体力気力を回復する
			FOR LCOUNT, 0, VARSIZE("BASE")
				SIF BASENAME:LCOUNT == ""
					CONTINUE
				BASE:LCOUNT = MAXBASE:LCOUNT
			NEXT

			CALL EVENTUSERTRAIN
			DO
				CALL SHOW_STATUS
				CALL SHOW_USERCOM
				INPUT
				CALL USERCOM
			LOOP TFLAG:調教終了 == 0
			CALLEVENT EVENTEND
			DO
				CALL SHOW_JUEL
				CALL SHOW_ABLUP_SELECT
				INPUT
				SIF RESULT == 100
					BREAK
				TRYCALLFORM ABLUP{RESULT}
			LOOP RESULT != 100
			CALL EVENTCHECK_T
			;陥落したら加入する
			IF CFLAG:陥落
				PRINTFORMW とうとう%CALLNAME:MASTER%の虜になったキャロルは、カジノの仕事よりも%CALLNAME:MASTER%のことを優先したいようだ
				PRINTFORMW ＜キャロルを家に迎え入れました＞
				CFLAG:売却不可 = 1
				キャロル進行フラグ = -1
			ELSE
				SAVECHARA　@"キャロル_{データID}", "", TARGET
				DELCHARA TARGET
				TARGET = -1
				DRAWLINE
				PRINTFORMW キャロルは%CALLNAME:MASTER%との行為も大変気に入った様子で、またのご来店を促している
				PRINTFORMW 彼女に見損なわれないようにカジノで勝ち続けよう
				ARRAYCOPY "道具フラグ退避用", "ITEM"
			ENDIF
			BREAK
		CASE 2
			PRINTFORMW %CALLNAME:MASTER%はキャロルの誘いを丁寧に断った……
			BREAK
	ENDSELECT
LOOP 1

@ことみちゃんイベント
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 道具フラグ退避用, 2000
PRINTL [0] - ことみちゃんの誘いを受ける
SIF ASSI > 0
	PRINTFORML [1] - %CALLNAME:ASSI%と一緒にことみちゃんの誘いを受ける
PRINTL [2] - ことみちゃんの誘いを断る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0, 1
			IF RESULT == 0
				ASSI = -1
			ELSEIF RESULT == 1 && ASSI < 0
				CONTINUE
			ENDIF
			PRINTFORMW ことみちゃんの誘いに乗ると、%CALLNAME:MASTER%はカジノ施設の一級スイートルームへと案内された
			PRINTFORML きらびやかな装飾や家具、ワイングラスが部屋を彩る中、ことみちゃんはドレスの裾をたくし上げ、
			PRINTFORMW %CALLNAME:MASTER%をベッドへと誘惑している。さて、どうしようか……
			;道具は一時的に無しにする
			ARRAYCOPY "ITEM", "道具フラグ退避用"
			VARSET ITEM
			;お酒と避妊具は用意してある
			ITEM:コンドーム = 99
			ITEM:スーパードライ = 12
			ITEM:一番搾り = 12
			ITEM:プレミアムモルツ = 12
			ITEM:ヱビス = 12
			ITEM:皆伝 = 10
			ITEM:魔王 = 10
			ITEM:アルパカ = 10
			ITEM:倉吉 = 10
			;調教開始準備
			;過去に誘いに乗ったならそのステータスを引き継ぎ
			IF CHKCHARADATA(@"ことみちゃん_{データID}") == 0
				LOADCHARA @"ことみちゃん_{データID}"
			ELSE
				ADDCHARA GETNO("ことみちゃん")
			ENDIF
			TARGET = CHARANUM-1
			;高感度がそのまま依存度になる
			CFLAG:依存度 = ことみちゃん好感度
			;ここから調教開始
			;BEGIN TRAINで行われる変数初期化
			ASSIPLAY:0 = 0
			PREVCOMS = 
			SELECTCOMS = 
			;体力気力を回復する
			FOR LCOUNT, 0, VARSIZE("BASE")
				SIF BASENAME:LCOUNT == ""
					CONTINUE
				BASE:LCOUNT = MAXBASE:LCOUNT
			NEXT

			CALL EVENTUSERTRAIN
			DO
				CALL SHOW_STATUS
				CALL SHOW_USERCOM
				INPUT
				CALL USERCOM
			LOOP TFLAG:調教終了 == 0
			CALLEVENT EVENTEND
			DO
				CALL SHOW_JUEL
				CALL SHOW_ABLUP_SELECT
				INPUT
				SIF RESULT == 100
					BREAK
				TRYCALLFORM ABLUP{RESULT}
			LOOP RESULT != 100
			CALL EVENTCHECK_T
			;陥落したら加入する
			IF CFLAG:陥落
				PRINTFORMW とうとう%CALLNAME:MASTER%の虜になったことみちゃんは、カジノ遊びよりも%CALLNAME:MASTER%のことを優先したいようだ
				PRINTFORMW ＜ことみちゃんを家に迎え入れました＞
				CFLAG:売却不可 = 1
				ことみちゃん進行フラグ = -1
			ELSE
				SAVECHARA　@"ことみちゃん_{データID}", "", TARGET
				DELCHARA TARGET
				TARGET = -1
				DRAWLINE
				PRINTFORMW ことみちゃんは%CALLNAME:MASTER%との行為も大変気に入った様子で、またのご来店を促している
				PRINTFORMW 彼女に見損なわれないようにカジノで勝ち続けよう
				ARRAYCOPY "道具フラグ退避用", "ITEM"
			ENDIF
			BREAK
		CASE 2
			PRINTFORMW %CALLNAME:MASTER%はことみちゃんの誘いを丁寧に断った……
			BREAK
	ENDSELECT
LOOP 1

;以下、各ゲーム共通の賭け金設定
@STAKE, 賭け金上限, カジノの種類
#DIM DYNAMIC 利子
#DIM DYNAMIC 賭け金上限
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ループ用ステップ = 5
#DIM DYNAMIC マックスベット可
#DIM DYNAMIC カジノの種類

SIF 賭け金上限 <= 0
	RETURN 0

;ギャンブルで借金を背負った場合の利子(難易度によって変動)
IF カジノの種類 == 1
	SELECTCASE FLAG:難易度
		;NORMAL
		CASE 1
			利子 = 25
		;HYPER
		CASE 2
			利子 = 55
		;ANOTHER
		CASE 3
			利子 = 90
	ENDSELECT
ENDIF

IF LANGUAGE == "ENG"
	PRINTL How many stake you do?
	IF 利子
		PRINTL But If you debt and can't payment,
		CALL PASELIL, @"{利子}\% interest rate will arise."
	ENDIF
	IF カジノの種類 == 3
		PRINT (Medal:
	ELSE
		PRINT (Moneys:
	ENDIF
ELSE
	PRINTL いくら賭けますか？
	IF 利子
		PRINTL ただし、ギャンブル中に新たな借金を背負って返済できなかった場合、
		CALL PASELIL, @"その借金に対し、{利子}\%の利子が付きます。"
	ENDIF
	IF カジノの種類 == 3
		PRINT (所持メダル:
	ELSE
		PRINT (所持金:
	ENDIF
ENDIF
SIF MONEY < 0
	SETCOLORBYNAME red
CALL PASELI, @"{MONEY}＄"
RESETCOLOR

CALL PASELIL, @" / MAXBET:{賭け金上限}＄)"

SETCOLORBYNAME skyblue
CALL PASELI, "[1] 1＄  "  

FOR LCOUNT, 0, 賭け金上限+1, ループ用ステップ
	SELECTCASE LCOUNT
		CASE 5
			SETCOLORBYNAME crimson
		CASE 25
			SETCOLORBYNAME springgreen
		CASE 100
			SETCOLORBYNAME darkgray
		CASE 500
			SETCOLORBYNAME purple
		CASE 1000
			SETCOLORBYNAME yellow
		CASE 5000
			SETCOLORBYNAME brown
		CASE 25000
			SETCOLORBYNAME orange
		CASE 100000
			SETCOLORBYNAME cyan
		CASE 500000
			SETCOLORBYNAME magenta
		CASE 1000000
			SETCOLORBYNAME silver
		CASE 2000000
			SETCOLORBYNAME gold
		CASEELSE
			マックスベット可 = 1
			CONTINUE
	ENDSELECT
	CALL PASELI, @"[{LCOUNT}] {LCOUNT}＄  "
	ループ用ステップ = LCOUNT
	マックスベット可 = 0
NEXT
PRINTL 
RESETCOLOR
SIF マックスベット可
	CALL PASELIL, @"[{賭け金上限}] MAXBET({賭け金上限}＄)"
$STAKE_INPUT
DO
	INPUT
	SELECTCASE RESULT
		CASE IS > 賭け金上限
			IF LANGUAGE == "ENG"
				CALL PASELIL, @"You can stake lower than {賭け金上限}. Please reinput."
			ELSE
				CALL PASELIL, @"賭け金上限は{賭け金上限}です。再入力してください"
			ENDIF
		CASE IS > 0
			BREAK
	ENDSELECT
LOOP 1

;PLAYSOUND カジノコイン

RETURN RESULT

;フィッシャー・イェーツのシャッフル
;CARD:0からCARD:{ARG-1}までの配列にシャッフルした順列を返す
;第二引数が渡された場合はARG:0～ARG:1間のカードをシャッフルする
@SHUFFLE, ARG, ARG:1
#DIM DYNAMIC LCOUNT0
#DIM DYNAMIC LCOUNT1

IF ARG:1 > 0
	;ARG以下にあるカードは初期値に含まれない LCOUNT1を使用
	FOR LCOUNT0, ARG, ARG:1
		WHILE FINDELEMENT(CARD, LCOUNT1, 0, ARG-1) >= 0
			LCOUNT1++
		WEND
		CARD:LCOUNT0 = LCOUNT1
		LCOUNT1++
	NEXT
	;LCOUNT0はARG:1の値から1づつ下げていく
	FOR LCOUNT0, ARG:1, ARG, -1
		LCOUNT1 = RAND(ARG, LCOUNT0);ARG以上LCOUNT0未満の数を決めてこれをLCOUNT1とする
		;LCOUNT1番目と(LCOUNT0-1)番目を入れ替える
		SWAP CARD:(LCOUNT0-1), CARD:LCOUNT1
	NEXT
ELSE
	FOR LCOUNT0, 0, ARG
		CARD:LCOUNT0 = LCOUNT0
	NEXT

	;LCOUNT0はARGの値から1づつ下げていく
	FOR LCOUNT0, ARG, 0, -1
		LCOUNT1 = RAND:LCOUNT0;0以上LCOUNT0未満の数を決めてこれをLCOUNT1とする
		;LCOUNT1番目と(LCOUNT0-1)番目を入れ替える
		SWAP CARD:(LCOUNT0-1), CARD:LCOUNT1
	NEXT
ENDIF

;第1引数を52で割った余りに対し以下を表示
;クラブK(余り0)→スペードA(余り1)→スペード2(余り2)→ … →ハートA(余り14)→ … →ダイヤA(余り27)→ … →クラブA(余り40)→ … →クラブQ(余り51)
;第2引数にSETCOLORBYNAMEで使える文字列を入れると強制的にその色になる
@PRTCARD, ARG, ARGS

IF ARGS == "" || COLOR_FROMNAME(ARGS) == -1
	SELECTCASE ((ARG+51)%52)/13
		;スペード
		CASE 0
			SETCOLORBYNAME deepskyblue
		;ハート
		CASE 1
			SETCOLORBYNAME hotpink
		;ダイヤ
		CASE 2
			SETCOLORBYNAME red
		;クラブ
		CASE 3
			SETCOLORBYNAME skyblue
	ENDSELECT
ELSE
	SETCOLOR COLOR_FROMNAME(ARGS)
ENDIF

SELECTCASE ((ARG+51)%52)/13
	;スペード
	CASE 0
		PRINT ♠
	;ハート
	CASE 1
		PRINT ♥
	;ダイヤ
	CASE 2
		PRINT ♦
	;クラブ
	CASE 3
		PRINT ♣
ENDSELECT

SELECTCASE ARG%13
	CASE 1
		PRINT Ａ
	;2～9はUIに配慮して全角にする
	CASE 2 TO 9
		PRINTFORM %TOFULL(TOSTR(ARG%13))%
	CASE 10
		PRINT 10
	CASE 11
		PRINT Ｊ
	CASE 12
		PRINT Ｑ
	CASEELSE
		PRINT Ｋ
ENDSELECT
RESETCOLOR

;上のボタン化関数 第二引数に押した時の返り値が入る
@PRTCARDBUTTON, ARG, ボタン, ARGS
#DIM DYNAMIC ボタン
#DIMS DYNAMIC スート
#DIMS DYNAMIC 数字

IF ARGS == "" || COLOR_FROMNAME(ARGS) == -1
	SELECTCASE ((ARG+51)%52)/13
		;スペード
		CASE 0
			SETCOLORBYNAME deepskyblue
		;ハート
		CASE 1
			SETCOLORBYNAME hotpink
		;ダイヤ
		CASE 2
			SETCOLORBYNAME red
		;クラブ
		CASE 3
			SETCOLORBYNAME skyblue
	ENDSELECT
ELSE
	SETCOLOR COLOR_FROMNAME(ARGS)
ENDIF

SELECTCASE ((ARG+51)%52)/13
	;スペード
	CASE 0
		スート = ♠
	;ハート
	CASE 1
		スート = ♥
	;ダイヤ
	CASE 2
		スート = ♦
	;クラブ
	CASE 3
		スート = ♣
ENDSELECT

SELECTCASE ARG%13
	CASE 1
		数字 = Ａ
	;2～9はUIに配慮して全角にする
	CASE 2 TO 9
		数字 = %TOFULL(TOSTR(ARG%13))%
	CASE 10
		数字 = 10
	CASE 11
		数字 = Ｊ
	CASE 12
		数字 = Ｑ
	CASEELSE
		数字 = Ｋ
ENDSELECT
PRINTBUTTON @"%スート%%数字%", ボタン
RESETCOLOR

;カジノフラグリセット
@CLEAR_CASINO_FLAG
キャロル進行フラグ = 0
ことみちゃん進行フラグ = 0
