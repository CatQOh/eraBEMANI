;バカラは裏カジノ専用なのでARGは使わない
@バカラ, ARG
#DIM DYNAMIC ゲーム数 = 16
#DIM DYNAMIC 賭け金上限 = 2000000
#DIM DYNAMIC デッキ数 = 6
#DIM DYNAMIC カード数
#DIMS DYNAMIC ベット
#DIM DYNAMIC 賭け金
#DIM DYNAMIC フェイズ
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 配当


#DIM DYNAMIC B_CARD, 2, 3;カードを記録(0はプレイヤー,1はバンカー)

;LOCAL = 16;残りゲーム数
;LOCAL:1 = 200000;MAXBET
;LOCAL:4 = 6;使用デッキ数
;PLAYSOUND トランプシャッフル
CALL SHUFFLE, 52 * デッキ数

;LOCAL:5 = 0;使用カード数
DO
	;ここからゲーム開始
	IF 8*(52*デッキ数-カード数) / (52*デッキ数) < 1;デッキの残りが1/8を切るとシャッフル
		CALL SHUFFLE, 52*デッキ数
		カード数 = 0;使用カード数
		;PLAYSOUND トランプシャッフル
		PRINTW デッキの残りが少なくなったのでシャッフルしました。
	ENDIF
	VARSET B_CARD, -1
	B_CARD:0:0 = CARD:カード数
	カード数++
	B_CARD:1:0 = CARD:カード数
	カード数++
	B_CARD:0:1 = CARD:カード数
	カード数++
	B_CARD:1:1 = CARD:カード数
	カード数++
	PRINTL 
	DRAWLINE
	CALL PASELIL, @"<残り{ゲーム数}ゲーム MAXBET:{賭け金上限}＄>"
	$BACCARAT_INPUT
	PRINTL 
	PRINTL [1]プレイヤー(2倍) [2]バンカー(1.95倍) [3]タイ(9倍) [9]もうやめる
	DO
		INPUT
		IF RESULT == 9
			PRINTL 
			PRINTL もうやめますか？
			PRINTL [0]やっぱり続ける [9]本当にやめる
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						GOTO BACCARAT_INPUT
					CASE 9
						RETURN
				ENDSELECT
			LOOP 1
		ELSEIF !INRANGE(RESULT, 1, 3)
			GOTO BACCARAT_INPUT
		ENDIF
	LOOP 0
	SELECTCASE RESULT
		CASE 1
			ベット = プレイヤー
		CASE 2
			ベット = バンカー
		CASE 3
			ベット= タイ
	ENDSELECT
	PRINTL 
	CALL STAKE, 賭け金上限, 2
	賭け金 = RESULT
	MONEY -= 賭け金
	フェイズ = 0;プレイの段階
	;PLAYSOUND トランプ配布
	$BACCARAT_PLAY
	PRINTL 
	CALL PASELIL, @"<残り{ゲーム数}ゲーム MAXBET:{賭け金上限}＄>"
	CALL PASELIL, @" ベット:{賭け金}(%ベット%)＄"
	PRINTL 
	

	FOR LCOUNT, 0, 2
		SELECTCASE LCOUNT
			CASE 0
				PRINTFORM プレイヤー:{B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2)} 
			CASE 1
				PRINTFORM 　バンカー:{B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2)} 
		ENDSELECT
		CALL PRTCARD, B_CARD:LCOUNT:0
		PRINT  
		CALL PRTCARD, B_CARD:LCOUNT:1
		IF B_CARD:LCOUNT:2 != -1
			PRINT  
			CALL PRTCARD, B_CARD:LCOUNT:2
		ELSEIF B_SUM(B_CARD:LCOUNT:0, B_CARD:LCOUNT:1, 0) == 8
			PRINT  (ナチュラル8)
		ELSEIF B_SUM(B_CARD:LCOUNT:0, B_CARD:LCOUNT:1, 0) == 9
			PRINT  (ナチュラル9)
		ENDIF
		SELECTCASE LCOUNT
			CASE 0
				PRINTL 
			CASE 1
				WAIT
		ENDSELECT
	NEXT
	;プレイヤーのターン
	IF フェイズ == 0 
		;どちらかが8以上だったらゲーム終了
		IF B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2) >= 8 || B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) >= 8
		;双方が6以上だったらゲーム終了
		ELSEIF B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2) >= 6 && B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) >= 6
		;そうでなければカードを引いてフェイズを進める
		ELSEIF B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2) <= 5
			;PLAYSOUND トランプドロー
			B_CARD:0:2 = CARD:カード数
			カード数++
			フェイズ++
			GOTO BACCARAT_PLAY
		;その他（？）
		ELSE
			フェイズ++
			GOTO BACCARAT_PLAY
		ENDIF
	;バンカーのターン
	ELSEIF フェイズ == 1
		;プレイヤーの3枚目のカードに応じてバンカー側に配られるかどうかが決まる
		IF B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) <= 2
			;PLAYSOUND トランプドロー
			B_CARD:1:2 = CARD:カード数
			カード数++
			フェイズ++
			GOTO BACCARAT_PLAY
		ELSEIF B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) == 3
			IF B_SUM(0,0,B_CARD:0:2) != 8
				;PLAYSOUND トランプドロー
				B_CARD:1:2 = CARD:カード数
				カード数++
				フェイズ++
				GOTO BACCARAT_PLAY
			ENDIF
		ELSEIF B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) == 4
			IF B_CARD:0:2 == -1 || INRANGE(B_SUM(0,0,B_CARD:0:2), 2, 7)
				;PLAYSOUND トランプドロー
				B_CARD:1:2 = CARD:カード数
				カード数++
				フェイズ++
				GOTO BACCARAT_PLAY
			ENDIF
		ELSEIF B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) == 5
			IF B_CARD:0:2 == -1 || INRANGE(B_SUM(0,0,B_CARD:0:2), 4, 7)
				;PLAYSOUND トランプドロー
				B_CARD:1:2 = CARD:カード数
				カード数++
				フェイズ++
				GOTO BACCARAT_PLAY
			ENDIF
		ELSEIF B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2) == 6
			IF INRANGE(B_SUM(0,0,B_CARD:0:2), 6, 7)
				;PLAYSOUND トランプドロー
				B_CARD:1:2 = CARD:カード数
				カード数++
				フェイズ++
				GOTO BACCARAT_PLAY
			ENDIF
		ENDIF
	ENDIF
	配当 = 0
	PRINTL 
	IF B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2) > B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2)
		PRINTL プレイヤーの勝ちです。
		IF ベット == "プレイヤー"
			配当 = 2*賭け金
			;PLAYSOUND カジノコイン
			CALL PASELIL,  @"配当:{賭け金}＄×2＝{配当}＄"
		ENDIF
	ELSEIF B_SUM(B_CARD:0:0, B_CARD:0:1, B_CARD:0:2) < B_SUM(B_CARD:1:0, B_CARD:1:1, B_CARD:1:2)
		PRINTL バンカーの勝ちです。
		IF ベット == "バンカー"
			配当 = 39*賭け金/20
			;PLAYSOUND カジノコイン
			CALL PASELIL, @" 配当:{賭け金}＄×1.95＝{配当}＄"
		ENDIF
	ELSE
		PRINTL タイです。
		IF ベット == "タイ"
			配当 = 9*賭け金
			;PLAYSOUND カジノコイン
			CALL PASELIL, @"配当:{賭け金}＄×9＝{配当}＄"
		ELSE
			;PLAYSOUND カジノコイン
			配当 = 賭け金
			CALL PASELIL, @"配当:{賭け金}＄×1＝{配当}＄"
		ENDIF
	ENDIF
	MONEY += 配当
	WAIT
	ゲーム数--
LOOP ゲーム数 > 0

@B_SUM(ARG, ARG:1, ARG:2)
#FUNCTION
;1枚目
SIF ARG < 0
	ARG = 0
ARG %= 13
SIF ARG > 9
	ARG = 0
;2枚目
SIF ARG:1 < 0
	ARG:1 = 0
ARG:1 %= 13
SIF ARG:1 > 9
	ARG:1 = 0
;3枚目
SIF ARG:2 < 0
	ARG:2 = 0
ARG:2 %= 13
SIF ARG:2 > 9
	ARG:2 = 0
RETURNF (ARG + ARG:1 + ARG:2) % 10

