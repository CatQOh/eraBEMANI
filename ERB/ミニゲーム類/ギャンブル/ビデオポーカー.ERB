@ビデオポーカー, カジノの種類
;※Jacks or Better 9/6の配当を参考にしています

#DIM CONST GAMES_MAX = 30;ゲーム数の設定
#DIM CONST D_UP_MAX = 10;ダブルアップの上限回数
#DIM DYNAMIC MAX_BET;1ベットの上限金額
#DIM DYNAMIC GAMES;現在のゲーム数
#DIM DYNAMIC VP_MODE = 0;ゲームモード……0:Jacks or Better 9/6 | 1:Double Bonus 10/7
#DIM DYNAMIC D_UP_ON = 0;ダブルアップはありか？
#DIM DYNAMIC D_UP_CNT;ダブルアップのゲーム回数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC GET_MONEY
#DIM DYNAMIC VP_CARD, 5
#DIM DYNAMIC HOLD
#DIM DYNAMIC ONEBET
#DIM DYNAMIC BET_MULTI
#DIM DYNAMIC CARD_NO
#DIM DYNAMIC D_UP_ANS
#DIM DYNAMIC カジノの種類

;賭け金上限 表カジノなら3,000、裏カジノなら15,000、ゲーセンなら1コイン
SELECTCASE カジノの種類
	CASE 1
		MAX_BET = 3000
	CASE 2
		MAX_BET = 15000
	CASE 3
		MAX_BET = 1
ENDSELECT

IF MAX_BET > 1
	PRINTL
	PRINTW 1ベット当たりの賭け金を決めます。
	CALL STAKE, MAX_BET, カジノの種類
	ONEBET = RESULT;賭け金の設定
ELSEIF MAX_BET == 1
	ONEBET = 1
ENDIF

FOR GAMES, 0, GAMES_MAX
	$VP_START
	DRAWLINE
	PRINTFORM <残り{GAMES_MAX - GAMES}ゲーム> 
	IF VP_MODE
		PRINT (Double Bonus 10/7    
	ELSE
		PRINT (Jacks or Better 9/6  
	ENDIF
	IF D_UP_ON
		PRINTL ダブルアップ:ON )
	ELSE
		PRINTL ダブルアップ:OFF)
	ENDIF
	PRINT 所持金:
	IF MONEY < 0
		SETCOLORBYNAME red
		PRINT -
	ENDIF
	CALL PASELI, @"{ABS(MONEY)}＄ "
	RESETCOLOR
	CALL PASELIL, @"1BET:{ONEBET}＄ MAXBET:{5*ONEBET}＄"
	PRINTL いくら賭けますか？
	PRINT [0]もうやめる 
	IF VP_MODE
		PRINT [7]Jacks or Better 9/6に変える 
	ELSE
		PRINT [7]Double Bonus 10/7に変える   
	ENDIF
	IF D_UP_ON
		PRINT [8]ダブルアップをOFFにする 
	ELSE
		PRINT [8]ダブルアップをONにする  
	ENDIF
	PRINTL [9]賭け金を変える
	CALL PASELIL, @"[1]{ONEBET}＄ [2]{2*ONEBET}＄ [3]{3*ONEBET}＄ [4]{4*ONEBET}＄ [5]{5*ONEBET}＄"
	INPUT
	IF RESULT == 0
		PRINTL 本当にやめますか？
		PRINTL [0]やっぱり続ける
		PRINTL [99]本当にやめる
		INPUT
		SIF RESULT == 99
			BREAK
		GOTO VP_START
	ELSEIF INRANGE(RESULT, 1, 5)
		BET_MULTI = RESULT;ベット数の設定
		MONEY -= RESULT*ONEBET
	ELSEIF RESULT == 7
		INVERTBIT VP_MODE, 0
		GOTO VP_START
	ELSEIF RESULT == 8
		INVERTBIT D_UP_ON, 0
		GOTO VP_START
	ELSEIF RESULT == 9
		PRINTW 1ベット当たりの賭け金を決めます。
		CALL STAKE, MAX_BET, カジノの種類
		ONEBET = RESULT;賭け金の設定
		GOTO VP_START
	ELSE
		GOTO VP_START
	ENDIF
	CALL SHUFFLE, 52
	FOR LCOUNT, 0, 5
		VP_CARD:LCOUNT = CARD:LCOUNT
	NEXT
	HOLD = 0;ホールドの有無(1bitずつで処理)
	PRINTL
	IF VP_MODE
		PRINTL ワンペア(J以上):1倍 ツーペア:1倍 スリーカード:3倍 ストレート:5倍 フラッシュ:7倍 フルハウス:10倍
	ELSE
		PRINTL ワンペア(J以上):1倍 ツーペア:2倍 スリーカード:3倍 ストレート:4倍 フラッシュ:6倍 フルハウス:9倍
	ENDIF
	IF VP_MODE
		PRINT フォーカード(5-K/2-4/A):50/80/160倍
	ELSE
		PRINT フォーカード:25倍
	ENDIF
	PRINTFORML  ストレートフラッシュ:50倍 ロイヤルフラッシュ:{100*(3+BET_MULTI)}倍
	VARLINE = 0
	DO
		SIF VARLINE
			CLEARLINE LINECOUNT-VARLINE
		VARLINE = LINECOUNT
		PRINTL
		FOR LCOUNT, 0, 5
			CALL PRTCARD(VP_CARD:LCOUNT)
			PRINT   
		NEXT
		PRINTL
		FOR LCOUNT, 0, 5
			IF GETBIT(HOLD, LCOUNT)
				PRINT HOLD 
			ELSE
				PRINT      
			ENDIF
		NEXT
		PRINTL
		PRINTL [1]  [2]  [3]  [4]  [5]  [9]CHANGE
		INPUT
		IF RESULT >= 1
			FOR LCOUNT, 0, 1+LOG10(RESULT)
				INVERTBIT HOLD, (RESULT/POWER(10,LCOUNT))%10-1
			NEXT
		ENDIF
		SIF RESULT % 10 == 9
			BREAK
	LOOP 1
	CARD_NO = 5;使用カード枚数
	FOR LCOUNT, 0, 5
		IF GETBIT(HOLD, LCOUNT) == 0
			VP_CARD:LCOUNT = CARD:(CARD_NO)
			CARD_NO += 1
		ENDIF
	NEXT
	PRINTL
	FOR LCOUNT, 0, 5
		CALL PRTCARD(VP_CARD:LCOUNT)
		PRINT   
	NEXT
	PRINTL
	GET_MONEY = 0;獲得金額
	IF FLUSH(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
		IF STRAIGHT(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4) == 14
			PRINTL ロイヤルフラッシュです。
			GET_MONEY += 100*(3+BET_MULTI)*ONEBET*BET_MULTI
		ELSEIF STRAIGHT(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
			PRINTL ストレートフラッシュです。
			GET_MONEY += 50*ONEBET*BET_MULTI
		ELSE
			PRINTL フラッシュです。
			IF VP_MODE
				GET_MONEY += 7*ONEBET*BET_MULTI
			ELSE
				GET_MONEY += 6*ONEBET*BET_MULTI
			ENDIF
		ENDIF
	ELSEIF STRAIGHT(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
		PRINTL ストレートです。
		IF VP_MODE
			GET_MONEY += 5*ONEBET*BET_MULTI
		ELSE
			GET_MONEY += 4*ONEBET*BET_MULTI
		ENDIF
	ELSEIF FOUR_OF_A_KIND(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
		IF VP_MODE
			IF FOUR_OF_A_KIND(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4) > 1400
				PRINTL Aのフォーカードです。
				GET_MONEY += 160*ONEBET*BET_MULTI
			ELSEIF FOUR_OF_A_KIND(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4) < 500
				PRINTL 2-4のフォーカードです。
				GET_MONEY += 80*ONEBET*BET_MULTI
			ELSE
				PRINTL 5-Kのフォーカードです。
				GET_MONEY += 50*ONEBET*BET_MULTI
			ENDIF
		ELSE
			PRINTL フォーカードです。
			GET_MONEY += 25*ONEBET*BET_MULTI
		ENDIF
	ELSEIF FH_OR_THREE(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4) > 1000000
		PRINTL フルハウスです。
		IF VP_MODE
			GET_MONEY += 10*ONEBET*BET_MULTI
		ELSE
			GET_MONEY += 9*ONEBET*BET_MULTI
		ENDIF
	ELSEIF FH_OR_THREE(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
		PRINTL スリーカードです。
		GET_MONEY += 3*ONEBET*BET_MULTI
	ELSEIF TWO_PAIR(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4)
		PRINTL ツーペアです。
		IF VP_MODE
			GET_MONEY += ONEBET*BET_MULTI
		ELSE
			GET_MONEY += 2*ONEBET*BET_MULTI
		ENDIF
	ELSEIF ONE_PAIR(VP_CARD:0, VP_CARD:1, VP_CARD:2, VP_CARD:3, VP_CARD:4) > 11000000
		PRINTL J以上のワンペアです。
		GET_MONEY += ONEBET*BET_MULTI
	ENDIF
	;ここからダブルアップ
	IF GET_MONEY > 0 && D_UP_ON
		CALL PASELIL, @"配当は{GET_MONEY}＄ですが、ダブルアップに挑戦しますか？"
		PRINT [0]挑戦しない [1]ダブルアップ
		SIF GET_MONEY >= 2
			PRINT  [2]ハーフダブル
		PRINTL
		$VP_DOUBLE
		INPUT
		IF RESULT == 0
			GOTO VP_DOUBLE_END
		ELSEIF RESULT == 1
		ELSEIF RESULT == 2 && GET_MONEY >= 2
			MONEY += GET_MONEY - GET_MONEY/2
			CALL PASELIW, @"${GET_MONEY - GET_MONEY/2}＄を手に入れた。"
			GET_MONEY /= 2
		ELSE
			GOTO VP_DOUBLE
		ENDIF
		CALL SHUFFLE, 52
		;ここでFOR文を+1まで回しているのはダブルアップを完全に成功させたあとにもカードを一度表示するため
		FOR D_UP_CNT, 0, 1+D_UP_MAX
			PRINTL
			FOR LCOUNT, 0, D_UP_CNT
				CALL PRTCARD(CARD:(LCOUNT))
				PRINT  
			NEXT
			;チャレンジ中の場合は次のカードを明るくする
			IF D_UP_CNT < D_UP_MAX
				IF GET_MONEY
					SETCOLORBYNAME greenyellow
				ELSE
					SETCOLORBYNAME lime
				ENDIF
				PRINT ？  
			ENDIF
			SETCOLORBYNAME lime
			FOR LCOUNT, D_UP_CNT+1, D_UP_MAX
				PRINT ？  
			NEXT
			RESETCOLOR
			PRINTL
			IF D_UP_CNT < D_UP_MAX && GET_MONEY > 0
				CALL PASELIL, @"賭け金:{GET_MONEY/POWER(2,D_UP_CNT)}＄×{POWER(2,D_UP_CNT)}＄＝{GET_MONEY}＄"
				PRINTL 赤か黒かを左から順に当ててください。
				PRINTL [0]赤 [1]黒 [2]配当を受け取る
				$VP_DOUBLE_PLAY
				INPUT
				;52で割った余りが14-39が赤なので、38足して0-25が赤になるようにする
				;その後26で割って奇数か偶数か調べる
				D_UP_ANS = (38+CARD:D_UP_CNT) / 26
				D_UP_ANS %= 2
				IF RESULT == D_UP_ANS
					GET_MONEY *= 2
				ELSEIF RESULT == 0 || RESULT == 1
					GET_MONEY = 0
				ELSEIF RESULT == 2
					BREAK
				ELSE
					GOTO VP_DOUBLE_PLAY
				ENDIF
			ELSE
				BREAK
			ENDIF
		NEXT
	ENDIF
	$VP_DOUBLE_END
	IF GET_MONEY > 0
		CALL PASELIW, @"{GET_MONEY}＄を手に入れた。"
		MONEY += GET_MONEY
	ELSE
		PRINTW
	ENDIF
NEXT
