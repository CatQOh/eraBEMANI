@COMP_INTEREST(ARG, ARG:1)
#FUNCTION

#DIM DYNAMIC L_CNT
#DIM DYNAMIC L_CNT1
#DIM DYNAMIC L_SUM

#DIM DYNAMIC 利率
#DIM DYNAMIC 利子
#DIM DYNAMIC 浮動小数点

;(1+X)^Yの二項展開で複利を計算する
;Xには0.001～0.1が代入され、X^11の項まで計算される(一応0.9まで代入はできるようにはするが精度の関係から非推奨)
;また、マイナスの利率も代入できる

;ARGには利率(千分率)、ARG:1には期数が代入される

;利率10割以上は弾き、利率0%は「1倍」と返し関数を終了させる
IF ABS(ARG) >= 1000
	RETURNF 0
ELSEIF ARG == 0
	RETURNF 1000
ENDIF

;利率は利率(千分率)の桁数
;ARGは有効数字1桁にする
;浮動小数点は千分率を小数に直すための補正値(例えば5%なら100で5/100=0.05と計算される)
利率 = LOG10(ABS(ARG))
ARG /= POWER(10, 利率)
浮動小数点 = POWER(10, 3-利率)
L_SUM = 0

FOR L_CNT, 0, 12
	利子 = 1000 * BINOM(ARG:1, L_CNT) * POWER(ARG, L_CNT)
	FOR L_CNT1, 0, L_CNT
		利子 /= 浮動小数点
	NEXT
	L_SUM += 利子
NEXT

RETURNF L_SUM
