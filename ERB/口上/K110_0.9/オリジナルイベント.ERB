@EVENTTURNEND
#PRI
#DIM DYNAMIC リリーゼ
#DIM DYNAMIC モード

;口上表示がオフなら表示しない
SIF !FLAG:口上表示
	RETURN 0

;休憩フラグ立ってないと発生しない
SIF !FLAG:休憩フラグ
	RETURN 0

;どちらかが助手もしくは対象じゃないと発生しない
SIF TARGET == -1 && ASSI == -1
	RETURN 0

;PRIが呼ばれる順番は分からないけどモード口上でのイベントが発生してたら発生しない
IF MAUD:90
	MAUD:90 = 0
	RETURN 0
ENDIF

LOCAL = 0
SELECTCASE CHARANAMEF(TARGET)
	CASE "モード", "リリーゼ"
		LOCAL++
ENDSELECT
SELECTCASE CHARANAMEF(ASSI)
	CASE "モード", "リリーゼ"
		LOCAL++
ENDSELECT

;表示するかはランダム 10％くらい
SIF RAND:10 != 0 || LOCAL == 0
	RETURN 0

LOCAL = 0
LOCAL:1 = 0

REPEAT CHARANUM
	SIF !CFLAG:COUNT:陥落
		CONTINUE
	SELECTCASE CHARANAMEF(COUNT)
		CASE "モード"
			LOCAL++
			モード = COUNT
		CASE "リリーゼ"
			LOCAL:1++
			リリーゼ = COUNT
	ENDSELECT
	SIF LOCAL && LOCAL:1
		BREAK
REND

SIF !CFLAG:モード:陥落 || !CFLAG:リリーゼ:陥落
	RETURN 0

SIF MAUD:6 == 0
	RETURN 0

;陥落済みのリリーゼとモードが居て、休憩フラグが立っていれば日常イベント発生
;まだ分岐が少ないから確率を抑える
SELECTCASE RAND:6
	CASE 0
		DRAWLINE
		LILIEZE:90 = 1
		PRINTFORMW 「あら、今日は何もしないの？ えっ、モードちゃんが熱出した！？」
		DRAWLINE
		PRINTFORMW 「だいじょーぶ？ モードちゃん？ あいつに変わってあたしが看病したげる」
		PRINTFORMW 「あ、リリーゼさん……けほっ、けほっ、何か悪い病気だったらうつっちゃうし、だめですよ」
		PRINTFORMW 「あら、あいつはいいのにあたしはダメなんだ？ はいこれ、ちゃんと水分摂りなさいよ」
		PRINTFORMW 「ありがとう、ございます……別にリリーゼさんが嫌というわけではなく、リリーゼさんまでお身体を悪くするのは、嫌……」
		PRINTFORMW 「何言ってんのよ。今、一番苦しいのはモードちゃんじゃない。そんな時こそあたしが側に居てあげなくちゃ」
		PRINTFORMW 「あたしは風邪ひこうが怪我しようがすぐ治るから大丈夫。病気がうつるくらいどうってことない。モードちゃん、寒くて寂しくてしんどいでしょ？ あたしも一緒に寝てあげる」
		PRINTFORMW 「リリーゼ、さん……」
		PRINTFORMW その後、看病を任せたリリーゼが戻ってこないことに痺れを切らした%CALLNAME:MASTER%は、もう一人の病人を抱える心配をすることになった……しかし、モードの顔色が確かに良くなったのも事実である
		DRAWLINE
	CASE 1
		DRAWLINE
		LILIEZE:90 = 1
		PRINTFORMW モードの誘いで図書室に来たリリーゼだが、いまいち退屈そうにしている
		PRINTFORMW 「モードちゃんは本が好きよねぇ。それで何冊目？」
		PRINTFORMW 「えっと……今日は{RAND(2, 6)}冊目、かな？」
		PRINTFORMW モードの机に積まれる本を手に取ったリリーゼは呆気にとられ、額に手をあてる
		PRINTFORMW 「うわ、しかも、一面に文字がびっしり。あたしはマンガ読んでたほうがいいわ。知恵熱出ちゃいそう」
		PRINTFORMW 「小説も面白いものですよ。手始めに、この短編集はいかがしょうか？」
		DRAWLINE
ENDSELECT


