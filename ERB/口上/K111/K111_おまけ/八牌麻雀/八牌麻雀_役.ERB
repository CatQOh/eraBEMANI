; /* 八牌麻雀の役判定用の関数集 */
;同時成立の関係などから処理をまとめている場合があります
;
;清老頭
;字一色
;清一色+混一色
;純チャン+チャンタ
;混老頭+対々和
;タンヤオ 「ヤオ」はUnicodeでは「%UNICODE(0x4E48)%」
;平和
;役牌
;一盃口
;
;ARG:0…和了したプレイヤー ARG:1…アガリ牌 ARG:2…ツモフラグ
[SKIPSTART]
@CHINROTO(ARG:0, ARG:1)
#FUNCTION

FOR N, 0, 8
	IF N == 7
		LOCAL = ARG:1 %25
	ELSE
		LOCAL = TEHAI:(ARG:0):N %25
	ENDIF
	SIF LOCAL > 17 || (1 <= LOCAL%9 && LOCAL%9 <= 7)
		RETURNF 0
NEXT

RETURNF 1


@SHOSUSHI(ARG:0, ARG:1)
#FUNCTION
#DIM DYNAMIC PEI;北の枚数

IF GARI:(ARG:0):0 && P8HANA & 16
	FOR N, 1, GARI:(ARG:0):0 + 1
		SIF GARI:(ARG:0):N % 25 == 21
			PEI++
	NEXT
ELSE
	RETURNF 0
ENDIF

SIF PEI < 3
	RETURNF 0

FOR N, 0, 8
	IF N == 7
		LOCAL = ARG:1 %25
	ELSE
		LOCAL = TEHAI:(ARG:0):N %25
	ENDIF
	SIF LOCAL < 18 || 20 < LOCAL
		RETURNF 0
NEXT

RETURNF 1


@TSUISO(ARG:0, ARG:1)
#FUNCTION

FOR N, 0, 8
	IF N == 7
		LOCAL = ARG:1 %25
	ELSE
		LOCAL = TEHAI:(ARG:0):N %25
	ENDIF
	SIF LOCAL < 18
		RETURNF 0
NEXT

RETURNF 1


@SOME(ARG:0, ARG:1)
;↑サムじゃなくて染め
;清一色に対して2、混一色に対して1、それ以外には0を返す
#FUNCTION
#DIM DYNAMIC PAI, 3;牌の色ごとの枚数

FOR N, 0, 7
	IF TEHAI:(ARG:0):N %25 /9 == 0
	;萬子
		PAI:0++
	ELSEIF TEHAI:(ARG:0):N %25 /9 == 1
	;筒子
		PAI:1++
	ELSE
	;字牌
		PAI:2++
	ENDIF
NEXT
IF ARG:1 %25 /9 == 0
	PAI:0++
ELSEIF ARG:1 %25 /9 == 1
	PAI:1++
ELSE
	PAI:2++
ENDIF
 
IF PAI:0 && PAI:1
	RETURNF 0
ELSEIF PAI:0 || PAI:1
	IF PAI:2
		RETURNF 1
	ELSE
		RETURNF 2
	ENDIF
ELSE
;字一色
	RETURNF 0
ENDIF


@CHANTA(ARG:0, ARG:1)
;純チャンに対して2、チャンタに対して1、それ以外には0を返す
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC SET;面子と雀頭を数える
#DIM DYNAMIC DET;純チャンかチャンタかの判定

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

FOR N, 0, 18
	IF N%9 == 0 || N%9 == 6
		IF PAI:N >= 2 && PAI:(N+1) >= 2 && PAI:(N+2) >= 2
			PAI:N -= 2
			PAI:(N+1) -= 2
			PAI:(N+2) -= 2
			SET += 2
		ELSEIF PAI:N && PAI:(N+1) && PAI:(N+2)
			PAI:N--
			PAI:(N+1)--
			PAI:(N+2)--
			SET++
		ENDIF
	ENDIF
NEXT

IF SET == 0
	;123or789の順子がなければ0を返す
	RETURNF 0
ELSEIF SET == 1
	IF PAI:0 == 3;一萬
		PAI:0 -= 3
		SET++
	ENDIF
	IF PAI:8 == 3;九萬
		PAI:8 -= 3
		SET++
	ENDIF
	IF PAI:9 == 3;一筒
		PAI:9 -= 3
		SET++
	ENDIF
	IF PAI:17 == 3;九筒
		PAI:9 -= 3
		SET++
	ENDIF
	FOR M, 18, 25;字牌
		IF PAI:M == 3
			PAI:M -= 3
			SET++
			DET++
			BREAK
		ENDIF
	NEXT
	SIF SET == 1
		RETURNF 0
ENDIF

;対子の確認 この時点で必ずSET==2
SIF PAI:0 == 2;一萬
	SET++
SIF PAI:8 == 2;九萬
	SET++
SIF PAI:9 == 2;一筒
	SET++
SIF PAI:17 == 2;九筒
	SET++
FOR M, 18, 25;字牌
	IF PAI:M == 2
		SET++
		DET++
		BREAK
	ENDIF
NEXT

SIF SET != 3
	RETURNF 0
SIF DET
	RETURNF 1
RETURNF 2

@HONTOI(ARG:0, ARG:1)
;対々和なら1を返す混老頭も複合しているのなら2を返す
;清老頭でも2を返すので、この関数は役満ではないときに使う
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC KOTSU;刻子の数のカウント
#DIM DYNAMIC DET;混老頭かどうかの判定に使用(0なら成立,1以上なら不成立)

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

FOR N, 0, 25
	IF PAI:N >= 3
		PAI:N -= 3
		KOTSU++
		DET++
		SIF N%9 == 0 || N%9 == 8 || N >= 18
			DET--
	ENDIF
NEXT

SIF KOTSU != 2
	RETURNF 0

;刻子で2メンツがある状態でヘッドがあれば1を返す
FOR N, 0, 25
	IF PAI:N == 2
		DET++
		SIF N%9 == 0 || N%9 == 8 || N >= 18
			DET--
		SIF DET
			RETURNF 1
		RETURNF 2
	ENDIF
NEXT

RETURNF 0

@TANYAO(ARG:0, ARG:1)
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

SIF PAI:0;一萬
	RETURNF 0
SIF PAI:8;九萬
	RETURNF 0
SIF PAI:9;一筒
	RETURNF 0
FOR M, 17, 25;九筒と字牌
	SIF PAI:M
		RETURNF 0
NEXT
RETURNF 1


@PINFU(ARG:0, ARG:1)
;平和 判定が最強クラスに面倒
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数(最初に固定)
#DIM DYNAMIC PAI2, 25;種類別の牌の枚数(計算用 メンツ切り出しによって減っていく)
#DIM DYNAMIC DET;アガリ牌が面子に含まれているかどうかの判定
#DIM DYNAMIC SET;面子の数

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

FOR L, 0, 16
	IF L%9 < 7
		;面子構成は順子+順子+対子のみ
		VARSET DET
		VARSET SET
		FOR M, 0, 25
			PAI2:M = PAI:M
		NEXT
		FOR N, L, L+18
			IF N%9 < 7
				DO
					IF PAI2:(N%18) && PAI2:(N%18+1) && PAI2:(N%18+2)
						IF DET == 0 && (N%18 == ARG:1 %25 || N%18+2 == ARG:1 %25)
							IF (N%9 == 0 && N%18+2 == ARG:1 %25) || (N%9 == 6 && N%18 == ARG:1 %25)
								BREAK
							ELSE
								PAI2:(N%18)--
								PAI2:(N%18+1)--
								PAI2:(N%18+2)--
								SET++
								DET++
							ENDIF
						ELSEIF DET == 0 && N%18+1 == ARG:1 %25
							BREAK
						ELSE
							PAI2:(N%18)--
							PAI2:(N%18+1)--
							PAI2:(N%18+2)--
							SET++
						ENDIF
					ENDIF
				LOOP PAI2:(N%18) && PAI2:(N%18+1) && PAI2:(N%18+2)
			ENDIF
		NEXT
		FOR N, 0, 22
			IF PAI2:N >= 2 && N != 18 && N != 20
				SET++
				BREAK
			ENDIF
		NEXT
		SIF SET == 3 && DET
			RETURNF 1
	ENDIF
NEXT
RETURNF 0

@YAKUHAI(ARG:0, ARG:1)
#FUNCTION
;処理が同じため、ビットごとに処理
;字牌が暗刻の場合に特定のビットを1にする
;東:1 (南:2) 西:4 (北:8) 白:16 發:32 中:64
#DIM DYNAMIC YAKU
#DIM DYNAMIC TON
#DIM DYNAMIC SHA
#DIM DYNAMIC HAKU
#DIM DYNAMIC HATSU
#DIM DYNAMIC CHUN

FOR M, 0, 7
	IF TEHAI:(ARG:0):M %25 == 18
		TON++
	ELSEIF TEHAI:(ARG:0):M %25 == 20
		SHA++
	ELSEIF TEHAI:(ARG:0):M %25 == 22
		HAKU++
	ELSEIF TEHAI:(ARG:0):M %25 == 23
		HATSU++
	ELSEIF TEHAI:(ARG:0):M %25 == 24
		CHUN++
	ENDIF
NEXT

IF ARG:1 %25 == 18
	TON++
ELSEIF ARG:1 %25 == 20
	SHA++
ELSEIF ARG:1 %25 == 22
	HAKU++
ELSEIF ARG:1 %25 == 23
	HATSU++
ELSEIF ARG:1 %25 == 24
	CHUN++
ENDIF

SIF TON == 3
	YAKU += 1
SIF SHA == 3
	YAKU += 4
SIF HAKU == 3
	YAKU += 16
SIF HATSU == 3
	YAKU += 32
SIF CHUN == 3
	YAKU += 64

RETURNF YAKU


@IPEKO(ARG:0, ARG:1)
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数
#DIM DYNAMIC DET;一盃口かどうかの判別

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

FOR N, 0, 16
	IF N%9 < 7
		IF PAI:N >= 2 && PAI:(N+1) >= 2 && PAI:(N+2) >= 2
			PAI:N -= 2
			PAI:(N+1) -= 2
			PAI:(N+2) -= 2
			DET++
			BREAK
		ENDIF
	ENDIF
NEXT

SIF DET == 0
	RETURNF 0

;一盃口の2メンツがある状態でヘッドがあれば1を返す
FOR N, 0, 25
	SIF PAI:N == 2
		RETURNF 1
NEXT

RETURNF 0


@PAI8SCORE(ARG:0, ARG:1, ARG:2)
;ARG:0はプレイヤー ARG:1は和了り牌 ARG:2はツモフラグ
#DIM DYNAMIC FU;符を数えるための変数
#DIM DYNAMIC HAN;翻数を数えるための変数
#DIM DYNAMIC SCORE;最終的な得点計算のための変数
#DIM DYNAMIC YAKUMAN;役満を数えるための変数
#DIM DYNAMIC PINFU;平和かどうかの判定に使用
#DIM DYNAMIC DORA;ドラを数えるための変数

;ここから役満
PRINTL
PRINT ドラ表示牌:
CALL PRTPAI, YAMA:98
IF RICHI:(ARG:0) >= 0
	PRINT 　裏ドラ表示牌:
	CALL PRTPAI, YAMA:99
ENDIF
PRINTL
PRINTL
IF TURN:(ARG:0) == 0
	IF ARG:2
		IF TURN:((ARG:0+1)%2) == 0
			PRINTL 天和　　　　　役満
			YAKUMAN++
		ELSE
			PRINTL 地和　　　　　役満
			YAKUMAN++
		ENDIF
	ELSE
		PRINTL 人和　　　　　役満
		YAKUMAN++
	ENDIF
ENDIF
IF CHINROTO(ARG:0, ARG:1)
	PRINTL 清老頭　　　　役満
	YAKUMAN++
ENDIF
IF SHOSUSHI(ARG:0, ARG:1)
	PRINTL 小四喜　　　　役満
	YAKUMAN++
ENDIF
IF TSUISO(ARG:0, ARG:1)
	PRINTL 字一色　　　　役満
	YAKUMAN++
ENDIF
;ここまで役満
IF YAKUMAN
	IF TONCHA == ARG:0
		SCORE = YAKUMAN * 8000 * 6
	ELSE
		SCORE = YAKUMAN * 8000 * 4
	ENDIF
	SIF YAKUMAN == 2
		PRINT ダブル
	SIF YAKUMAN == 3
		PRINT トリプル
	PRINTFORMW 役満　{SCORE}点
	RETURN SCORE
ELSE
	SELECTCASE RICHI:(ARG:0)
		CASE 0
			PRINTL ダブル立直　　2翻
			HAN += 2
		CASE IS > 0
			PRINTL 立直　　　　　1翻
			HAN += 1
	ENDSELECT
	IF SOKU:(ARG:0)
		PRINTL 一発　　　　　1翻
		HAN += 1
	ENDIF
	SELECTCASE SOME(ARG:0, ARG:1)
		CASE 2
			PRINTL 清一色　　　　6翻
			HAN += 6
		CASE 1
			PRINTL 混一色　　　　3翻
			HAN += 3
	ENDSELECT
	SELECTCASE CHANTA(ARG:0, ARG:1)
		CASE 2
			PRINTFORML 純全帯%UNICODE(0x4E48)%九　　3翻
			HAN += 3
		CASE 1
			PRINTFORML 混全帯%UNICODE(0x4E48)%九　　2翻
			HAN += 2
	ENDSELECT
	SELECTCASE HONTOI(ARG:0, ARG:1)
		CASE 2
			PRINTL 混老頭　　　　2翻
			PRINTL 対々和　　　　2翻
			HAN += 4
		CASE 1
			PRINTL 対々和　　　　2翻
			HAN += 2
	ENDSELECT
	IF TANYAO(ARG:0, ARG:1)
		PRINTFORML 断%UNICODE(0x4E48)%九　　　　1翻
		HAN += 1
	ENDIF
	IF PINFU(ARG:0, ARG:1)
		PRINTL 平和　　　　　1翻
		PINFU++
		HAN += 1
	ENDIF
	IF ARG:2
		PRINTL 門前清自摸和　1翻
		HAN += 1
	ENDIF
	IF ARG:2 && RINSHAN && P8HANA & 4
		PRINTL 嶺上開花　　　1翻
		HAN += 1
	ENDIF
	LOCAL = YAKUHAI(ARG:0, ARG:1)
	IF LOCAL & 1
		PRINTL 東　　　　　　1翻
		HAN += 1
	ELSEIF LOCAL & 4
		PRINTL 西　　　　　　1翻
		HAN += 1
	ELSEIF LOCAL & 16
		PRINTL 白　　　　　　1翻
		HAN += 1
	ELSEIF LOCAL & 32
		PRINTL 發　　　　　　1翻
		HAN += 1
	ELSEIF LOCAL & 64
		PRINTL 中　　　　　　1翻
		HAN += 1
	ENDIF
	IF IPEKO(ARG:0, ARG:1)
		PRINTL 一盃口　　　　1翻
		HAN += 1
	ENDIF
	IF TURN:(ARG:0) == 17
		IF ARG:2 && TURN:((ARG:0+1)%2) == 18
			PRINTL 海底摸月　　　1翻
			HAN += 1
		ENDIF
	ENDIF
	IF TURN:(ARG:0) == 18
		PRINTL 河底撈魚　　　1翻
		HAN += 1
	ENDIF
	;ここからドラ計上
	;表ドラ
	;花牌がドラ表示牌のとき、花牌をドラとする
	IF YAMA:98 >= 100 && GARI:(ARG:0):0
		FOR N, 1, GARI:(ARG:0):0 + 1
			IF GARI:(ARG:0):N >= 100
				DORA++
				HAN++
			ENDIF
		NEXT
	ELSEIF YAMA:98 < 100
		SELECTCASE YAMA:98 %25
			CASE 8
				LOCAL = 0
			CASE 17
				LOCAL = 9
			CASE 21
				LOCAL = 18
			CASE 24
				LOCAL = 22
			CASEELSE
				LOCAL = YAMA:98 %25 + 1
		ENDSELECT
		DORA = 0
		IF LOCAL == 21 && P8RED & 2
			IF GARI:(ARG:0):0
				FOR N, 1, GARI:(ARG:0):0 + 1
					IF GARI:(ARG:0):N %25 == 21
						DORA++
						HAN++
					ENDIF
				NEXT
			ENDIF
		ELSE
			FOR N, 0, 7
				IF TEHAI:(ARG:0):N %25 == LOCAL
					DORA++
					HAN++
				ENDIF
			NEXT
			IF ARG:1 %25 == LOCAL
				DORA++
				HAN++
			ENDIF
		ENDIF
	ENDIF
	PRINTFORML 表ドラ　　　　{DORA}翻
	;裏ドラ
	IF RICHI:(ARG:0) >= 0
		IF YAMA:99 >= 100 && GARI:(ARG:0):0
			FOR N, 1, GARI:(ARG:0):0 + 1
				IF GARI:(ARG:0):N >= 100
					DORA++
					HAN++
				ENDIF
			NEXT
		ELSEIF YAMA:99 < 100
			SELECTCASE YAMA:99 %25
				CASE 8
					LOCAL = 0
				CASE 17
					LOCAL = 9
				CASE 21
					LOCAL = 18
				CASE 24
					LOCAL = 22
				CASEELSE
					LOCAL = YAMA:99 %25 + 1
			ENDSELECT
			DORA = 0
			IF LOCAL == 21 && P8RED & 2
				IF GARI:(ARG:0):0
					FOR N, 1, GARI:(ARG:0):0 + 1
						IF GARI:(ARG:0):N %25 == 21
							DORA++
							HAN++
						ENDIF
					NEXT
				ENDIF
			ELSE
				FOR N, 0, 7
					IF TEHAI:(ARG:0):N %25 == LOCAL
						DORA++
						HAN++
					ENDIF
				NEXT
				IF ARG:1 %25 == LOCAL
					DORA++
					HAN++
				ENDIF
			ENDIF
		ENDIF
		PRINTFORML 裏ドラ　　　　{DORA}翻
	ENDIF
	DORA = 0
	FOR N, 0, 8
		IF N == 7
			LOCAL:N = ARG:1
		ELSE
			LOCAL:N = TEHAI:(ARG:0):N
		ENDIF
	NEXT
	FOR N, 0, 8
		;赤五萬
		IF (LOCAL:N == 79 && P8RED%4) || (LOCAL:N == 54 && P8RED%4 >= 2) || (LOCAL:N %25 == 4 && P8RED%4 == 3)
			DORA++
			HAN++
		;赤五筒
		ELSEIF (LOCAL:N == 88 && P8RED/4%4) || (LOCAL:N == 63 && P8RED/4%4 >= 2) || (LOCAL:N %25 == 13 && P8RED/4%4 == 3)
			DORA++
			HAN++
		;赤三筒
		ELSEIF (LOCAL:N == 86 && P8RED/16%4) || (LOCAL:N == 61 && P8RED/16%4 == 2)
			DORA++
			HAN++
		;金三筒もといスーパー赤三筒
		ELSEIF (LOCAL:N == 61 && P8RED/16%4 == 3)
			DORA++
			HAN++
		;赤北
		ELSEIF (LOCAL:N == 96 && P8RED/64%2)
			DORA++
			HAN++
		ENDIF
	NEXT
	IF GARI:(ARG:0):0 && P8RED/64%2
		FOR N, 1, GARI:(ARG:0):0 + 1
			IF GARI:(ARG:0):N == 96
				DORA++
				HAN++
			ENDIF
		NEXT
	ENDIF
	SIF DORA
		PRINTFORML 赤ドラ　　　　{DORA}翻
	;抜きドラ
	IF GARI:(ARG:0):0
		HAN += GARI:(ARG:0):0
		PRINTFORML 抜きドラ　　　{GARI:(ARG:0):0}翻
	ENDIF
	;ここまでドラ計上
	IF PINFU
		IF ARG:2
			FU = 20
		ELSE
			FU = 30
		ENDIF
	ELSE
		IF ARG:2
			FU = 30
		ELSE
			FU = 40
		ENDIF
		FU += FUHANE(ARG:0, ARG:1, ARG:2)
	ENDIF
	SELECTCASE HAN
		CASE IS >= 13
			SCORE = 8000
		CASE 11, 12
			SCORE = 6000
		CASE 8, 9, 10
			SCORE = 4000
		CASE 6, 7
			SCORE = 3000
		CASE 5
			SCORE = 2000
		CASEELSE
			SCORE = FU * POWER(2, 2+HAN)
			SIF SCORE > 2000
				SCORE = 2000
	ENDSELECT
	IF TONCHA == ARG:0
		SCORE *= 6
	ELSE
		SCORE *= 4
	ENDIF
	SIF SCORE%100 != 0
		SCORE += 100-SCORE%100
	PRINTFORMW {FU}符{HAN}翻　{SCORE}点
	RETURN SCORE
ENDIF


@FUHANE(ARG:0, ARG:1, ARG:2)
;八牌麻雀においては、
;ロン和了り50符……8符の暗刻+(2符の雀頭+両面以外 or 4符の明刻) OR 4符の暗刻*2+2符の役牌単騎待ち
;ツモ和了り40符……8符の暗刻+(2符の雀頭or両面以外) OR 4符の暗刻*2+2符の雀頭
;対象となる和了りのとき10を返す そうではないときは0
;ARG:0…プレーヤー ARG:1…和了り牌 ARG:2…ツモフラグ
#FUNCTION
#DIM DYNAMIC PAI, 25;種類別の牌の枚数(最初に固定)
#DIM DYNAMIC PAI2, 25;種類別の牌の枚数(計算用 メンツ切り出しによって減っていく)
#DIM DYNAMIC FU;符の計算に使用

FOR M, 0, 25
	FOR N, 0, 7
		SIF TEHAI:(ARG:0):N %25 == M
			PAI:M++
	NEXT
	SIF ARG:1 %25 == M
		PAI:M++
NEXT

;ツモ和了りのとき
IF ARG:2
	;「8符の暗刻+辺張嵌張単騎」以外すべてのケース
	FOR M, 0, 25
		PAI2:M = PAI:M
	NEXT
	FU = 2
	FOR N, 0, 25
		IF PAI2:N >= 3
			PAI2:N -= 3
			FU += 4
			SIF N%9 == 0 || N%9 == 8 || N >= 18
				FU += 4
		ENDIF
	NEXT
	FOR N, 18, 25
		;役牌対子
		IF N != 19 && N != 21
			SIF PAI2:N == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU > 10
		RETURNF 10
	;「8符の暗刻+辺張嵌張単騎」のケースのみ
	FOR M, 0, 25
		PAI2:M = PAI:M
	NEXT
	FU = 0
	FOR N, 0, 25
		IF PAI2:N >= 3
			PAI2:N -= 3
			FU += 4
			SIF N%9 == 0 || N%9 == 8 || N >= 18
				FU += 4
			BREAK
		ENDIF
	NEXT
	SIF FU != 8
		RETURNF 0
	FOR N, 0, 25
		IF PAI2:N >= 2
			PAI2:N -= 2
			SIF N == ARG:1 %25
				RETURNF 10
			BREAK
		ENDIF
		SIF N == 24
			RETURNF 0
	NEXT
	FOR N, 0, 16
		IF N%9 < 7
			IF PAI2:N && PAI2:(N+1) && PAI2:(N+2)
				IF N+1 == ARG:1 %25
					RETURNF 10
				ELSEIF N%9 == 0 && N+2 == ARG:1 %25
					RETURNF 10
				ELSEIF N%9 == 6 && N == ARG:1 %25
					RETURNF 10
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
		ENDIF
	NEXT
	RETURNF 0
;ロン和了りの場合
ELSE
	;「8符の暗刻+2符の雀頭+辺張嵌張」以外すべてのケース
	FOR M, 0, 25
		PAI2:M = PAI:M
	NEXT
	FU = 0
	FOR N, 0, 25
		IF PAI2:N >= 3
			IF N == ARG:1 %25
				PAI2:N -= 3
				FU += 2
				SIF N%9 == 0 || N%9 == 8 || N >= 18
					FU += 2
			ELSE
				PAI2:N -= 3
				FU += 4
				SIF N%9 == 0 || N%9 == 8 || N >= 18
					FU += 4
			ENDIF
		ENDIF
	NEXT
	FOR N, 18, 25
		;役牌対子
		IF N != 19 && N != 21
			SIF PAI2:N == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU > 10
		RETURNF 10
	;「8符の暗刻+2符の雀頭+辺張嵌張」のケースのみ
	FOR M, 0, 25
		PAI2:M = PAI:M
	NEXT
	FU = 0
	FOR N, 0, 25
		IF PAI2:N >= 3
			IF N == ARG:1 %25
				PAI2:N -= 3
				FU += 2
				SIF N%9 == 0 || N%9 == 8 || N >= 18
					FU += 2
			ELSE
				PAI2:N -= 3
				FU += 4
				SIF N%9 == 0 || N%9 == 8 || N >= 18
					FU += 4
			ENDIF
			BREAK
		ENDIF
	NEXT
	SIF FU != 8
		RETURNF 0
	FOR N, 18, 25
		;役牌対子
		IF N != 19 && N != 21
			SIF PAI2:N == 2
				FU += 2
		ENDIF
	NEXT
	SIF FU != 10
		RETURNF 0
	FOR N, 0, 16
		IF N%9 < 7
			IF PAI2:N && PAI2:(N+1) && PAI2:(N+2)
				IF N+1 == ARG:1 %25
					RETURNF 10
				ELSEIF N%9 == 0 && N+2 == ARG:1 %25
					RETURNF 10
				ELSEIF N%9 == 6 && N == ARG:1 %25
					RETURNF 10
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
		ENDIF
	NEXT
	RETURNF 0
ENDIF


@DEAGARI(ARG:0, ARG:1)
;出和了り可能かどうか判定する式中関数
;OKなら1,NGなら0
;ARG:0…プレイヤー ARG:1…和了り牌
#FUNCTION

;和了形になっていないならNG
SIF SHANTEN8(TEHAI:(ARG:0):0, TEHAI:(ARG:0):1, TEHAI:(ARG:0):2, TEHAI:(ARG:0):3, TEHAI:(ARG:0):4, TEHAI:(ARG:0):5, TEHAI:(ARG:0):6, ARG:1, -1) != -1
	RETURNF 0

;河に牌が1枚もないなら人和が成立するのでOK
SIF TURN:(ARG) == 0
	RETURNF 1

;フリテンならNG
FOR LOCAL, 0, TURN:(ARG:0)
	SIF SHANTEN8(TEHAI:(ARG:0):0, TEHAI:(ARG:0):1, TEHAI:(ARG:0):2, TEHAI:(ARG:0):3, TEHAI:(ARG:0):4, TEHAI:(ARG:0):5, TEHAI:(ARG:0):6, KAWA:(ARG:0):LOCAL, -1) == -1
		RETURNF 0
NEXT
SIF MINOGASHI
	RETURNF 0

;あとは何かの役があればOK
SIF RICHI:(ARG:0) >= 0
	RETURNF 1
SIF CHINROTO(ARG:0, ARG:1)
	RETURNF 1
SIF TSUISO(ARG:0, ARG:1)
	RETURNF 1
SIF SOME(ARG:0, ARG:1)
	RETURNF 1
SIF CHANTA(ARG:0, ARG:1)
	RETURNF 1
SIF HONTOI(ARG:0, ARG:1)
	RETURNF 1
SIF TANYAO(ARG:0, ARG:1)
	RETURNF 1
SIF PINFU(ARG:0, ARG:1)
	RETURNF 1
SIF YAKUHAI(ARG:0, ARG:1)
	RETURNF 1
SIF IPEKO(ARG:0, ARG:1)
	RETURNF 1
SIF TURN:(ARG:0) == 18
	RETURNF 1

;赤五筒が役になるとき
IF P8RED & 128
	FOR N, 0, 8
		IF N == 7
			LOCAL:N = ARG:1
		ELSE
			LOCAL:N = TEHAI:(ARG:0):N
		ENDIF
	NEXT
	FOR N, 0, 8
		SIF (LOCAL:N == 88 && P8RED/4%4) || (LOCAL:N == 63 && P8RED/4%4 >= 2) || (LOCAL:N %25 == 13 && P8RED/4%4 == 3)
			RETURNF 1
	NEXT
ENDIF

RETURNF 0
[SKIPEND]
