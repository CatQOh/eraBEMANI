@COMP_INTEREST(ARG, ARG:1)
#FUNCTION

#DIM DYNAMIC L_CNT
#DIM DYNAMIC L_CNT1
#DIM DYNAMIC L_SUM

;(1+X)^Yの二項展開で複利を計算する
;Xには0.001～0.1が代入され、X^11の項まで計算される(一応0.9まで代入はできるようにはするが精度の関係から非推奨)
;また、マイナスの利率も代入できる

;ARGには利率(千分率)、ARG:1には期数が代入される

;利率10割以上は弾き、利率0%は「1倍」と返し関数を終了させる
IF ABS(ARG) >= 1000
	RETURNF 0
ELSEIF ARG == 0
	RETURNF 1000
ENDIF

;LOCALは利率(千分率)の桁数
;ARGは有効数字1桁にする
;LOCAL:1は千分率を小数に直すための補正値(例えば5%なら100で5/100=0.05と計算される)
LOCAL = LOG10(ABS(ARG))
ARG /= POWER(10, LOCAL)
LOCAL:1 = POWER(10, 3-LOCAL)
L_SUM = 0

FOR L_CNT, 0, 12
	LOCAL:2 = 1000 * BINOM(ARG:1, L_CNT) * POWER(ARG, L_CNT)
	FOR L_CNT1, 0, L_CNT
		LOCAL:2 /= LOCAL:1
	NEXT
	L_SUM += LOCAL:2
NEXT

RETURNF L_SUM
