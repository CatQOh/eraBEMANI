@ロケーション選択
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIM DYNAMIC ロケーション判定, 100
#DIMS DYNAMIC 名称抜き取り用, 2
#DIMS DYNAMIC 目的地
#DIM DYNAMIC 会話候補, 1000

VARSIZE ロケーション判定

DRAWLINE
PRINTL □目的地を選択してください

FOR LCOUNT, 0, VARSIZE("ロケーション")
	;条件判定用関数でロケーションごとの出現条件を設定する
	SPLIT ロケーション:LCOUNT, "/", 名称抜き取り用
	;分割されてたらスラッシュより後の部分だけを使用する
	IF RESULT >= 2
		RESULT = 0
		TRYCALLFORM ロケーション条件_%名称抜き取り用:1%
	ELSE
		RESULT = 0
		TRYCALLFORM ロケーション条件_%ロケーション:LCOUNT%
	ENDIF
	;RETURN 1で返されたら条件を満たしている
	SIF !RESULT
		CONTINUE
	PRINTFORMLC 　[{LCOUNT}] - %ロケーション:LCOUNT%
	ロケーション判定:LCOUNT = 1
NEXT

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN 　ロケーション選択　
PRINTL 

DO
	INPUT
	LRESULT = RESULT
	SELECTCASE LRESULT
		CASE 0 TO VARSIZE("ロケーション")-1
			SIF !ロケーション判定:LRESULT
				CONTINUE
			SPLIT ロケーション:LRESULT, "/", 名称抜き取り用
			;分割されてたらスラッシュより後の部分だけを使用する
			IF RESULT >= 2
				目的地 = %名称抜き取り用:1%
			ELSE
				目的地 = %ロケーション:LRESULT%
			ENDIF

			RESULT = 0
			TRYCALLFORM ロケーション条件_%目的地%
			SIF !RESULT
				CONTINUE
			PRINTFORML %CALLNAME:PLAYER%は%目的地%に向かった
			CSTR:PLAYER:現在地 = %目的地%
			BREAK
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 1

;関数でロケーションごとに出現するキャラを設定する
;「出現キャラ」フラグの数値は1～100 出現する確率
VARSET 出現率
TRYCALLFORM 出現キャラ_%目的地%

DRAWLINE
;ロケーション専用のコマンドがあるならそれを優先
TRYCCALLFORM ロケーション_%目的地%
;無ければ会話コマンド
CATCH
	$リロード
	PRINTFORML □%目的地%に着いた 何をしよう？
	VARSET 会話候補
	FOR LCOUNT, 1, CHARANUM
		IF 出現率:LCOUNT
			IF RANDPER(出現率:LCOUNT)
				PRINTFORML 　[{LCOUNT}] - %NAME:LCOUNT%に話しかける
				会話候補:LCOUNT = 1
			ENDIF
		ENDIF
	NEXT
	IF MAXARRAY(会話候補) == 0
		PRINTL 
		PRINTL 　----------誰も居ないようだ----------
		PRINTL 
	ENDIF
	DRAWLINE
	PRINTPLAINFORM 　%目的地%　
	PRINTL [1000] - 帰る [1001] - もう少し周りを見てみる
	DO
		INPUT
		LRESULT = RESULT
		SELECTCASE LRESULT
			CASE 1000
				CSTR:PLAYER:現在地 = 自宅
				RETURN
			CASE 1001
				GOTO リロード
			CASE 1 TO CHARANUM
				SIF !会話候補:LRESULT
					CONTINUE
				;会話の内容はもう少し考える
				PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:LRESULT%にと会話した
				CALL 相性増加, PLAYER, LRESULT
				RETURN
		ENDSELECT
	LOOP 1
ENDCATCH


RETURN








