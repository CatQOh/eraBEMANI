@ロケーション選択, 散策
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LRESULT
#DIM DYNAMIC ロケーション判定, 100
#DIMS DYNAMIC 名称抜き取り用, 2
#DIMS DYNAMIC 目的地
#DIM DYNAMIC 会話候補, 1010
#DIM DYNAMIC 散策

VARSET ロケーション判定

IF 散策
	SPLIT CSTR:PLAYER:現在地, "/", 名称抜き取り用
	;分割されてたらスラッシュより後の部分だけを使用する
	IF RESULT >= 2
		目的地 = %名称抜き取り用:1%
	ELSE
		目的地 = %CSTR:PLAYER:現在地%
	ENDIF
	GOTO 散策
ENDIF

DRAWLINE
PRINTL □目的地を選択してください

CALL ロケーション一覧

DRAWLINE
PRINTPLAIN 　ロケーション選択　
PRINTL [10000] - 戻る

BINPUT

LRESULT = RESULT
SELECTCASE LRESULT
	CASE 0 TO VARSIZE("ロケーション")-1
		SPLIT ロケーション:LRESULT, "/", 名称抜き取り用
		;分割されてたらスラッシュより後の部分だけを使用する
		IF RESULT >= 2
			目的地 = %名称抜き取り用:1%
		ELSE
			目的地 = %ロケーション:LRESULT%
		ENDIF

		PRINTFORML %CALLNAME:PLAYER%は%目的地%に向かった
		CSTR:PLAYER:現在地 = %目的地%
	CASE 10000
		RETURN 0
ENDSELECT

$散策

;関数でロケーションごとに出現するキャラを設定する
;「出現キャラ」フラグの数値は1～100 出現する確率
VARSET 出現率
VARSET 集団行動
TRYCALLFORM 出現キャラ_%目的地%

DRAWLINE
;ロケーション専用のコマンドがあるならそれを優先
TRYCCALLFORM ロケーション_%目的地%
;無ければ会話コマンド
CATCH
	$リロード
	IF 散策
		PRINTFORML □%CALLNAME:PLAYER%は%目的地%を散策してみた
	ELSE
		PRINTFORML □%目的地%に着いた 何をしよう？
	ENDIF
	VARSET 会話候補
	FOR LCOUNT, 1, CHARANUM
		IF 出現率:LCOUNT > 0
			IF RANDPER(出現率:LCOUNT)
				PRINTFORML 　[{LCOUNT}] - %NAME:LCOUNT%に話しかける
				会話候補:LCOUNT = 1
			ENDIF
		ENDIF
	NEXT
	FOR LCOUNT, 0, 10
		IF 集団行動:LCOUNT:0 > 0
			PRINTFORM 　[{1000+LCOUNT}] - 
			FOR LCOUNT2, 0, 5
				PRINTFORM %NAME:(集団行動:LCOUNT:LCOUNT2)%
				IF LCOUNT2 != 4 && 集団行動:LCOUNT:(LCOUNT2+1) > 0
					PRINT 、
				ELSE
					BREAK
				ENDIF
			NEXT
			PRINTL のグループ
			会話候補:(1000+LCOUNT) = 1
		ENDIF
	NEXT
	IF MAXARRAY(会話候補) == 0
		PRINTL 
		PRINTL 　----------誰も居ないようだ----------
		PRINTL 
	ENDIF
	DRAWLINE
	PRINTPLAINFORM 　%目的地%　
	PRINTFORML [0] - 帰る [-1] - %目的地%に留まる [-2] - もう少し周りを見てみる
	DO
		INPUT
		LRESULT = RESULT
		SELECTCASE LRESULT
			CASE 0
				CSTR:PLAYER:現在地 = 自宅
				RETURN 1
			CASE -1
				RETURN 1
			CASE -2
				GOTO リロード
			CASE 1 TO CHARANUM
				SIF !会話候補:LRESULT
					CONTINUE
				CALL 学園モード_汎用会話, LRESULT
				SIF !RESULT
					GOTO リロード
				BREAK
			CASE 1000 TO 1009
				SIF 集団行動:(LRESULT-1000):0 == 0
					CONTINUE
				FOR LCOUNT, 0, 5
					PRINTFORM %NAME:(集団行動:(LRESULT-1000):LCOUNT)%
					IF LCOUNT != 4 && 集団行動:(LRESULT-1000):(LCOUNT+1) > 0
						PRINT 、
					ELSE
						BREAK
					ENDIF
				NEXT
				PRINTL のグループと会話した
				FOR LCOUNT, 0, 5
					SIF 集団行動:(LRESULT-1000):LCOUNT > 0
						CALL 好感度上昇, 集団行動:(LRESULT-1000):LCOUNT, "友情", 10
				NEXT
				BREAK
		ENDSELECT
	LOOP 1
ENDCATCH


RETURN 1

@ロケーション一覧
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 名称抜き取り用, 2

FOR LCOUNT, 0, VARSIZE("ロケーション")
	;条件判定用関数でロケーションごとの出現条件を設定する
	SPLIT ロケーション:LCOUNT, "/", 名称抜き取り用
	;分割されてたらスラッシュより後の部分だけを使用する
	IF RESULT >= 2
		RESULT = 0
		;既に現在地の場合は無効
		SIF CSTR:PLAYER:現在地 == 名称抜き取り用:1
			CONTINUE
		TRYCALLFORM ロケーション条件_%名称抜き取り用:1%
	ELSE
		RESULT = 0
		;既に現在地の場合は無効
		SIF CSTR:PLAYER:現在地 == ロケーション:LCOUNT
			CONTINUE
		TRYCALLFORM ロケーション条件_%ロケーション:LCOUNT%
	ENDIF
	;RETURN 1で返されたら条件を満たしている
	SIF !RESULT
		CONTINUE
	PRINTFORMLC 　[{LCOUNT}] - %ロケーション:LCOUNT%
NEXT
CALL NEWLINE
