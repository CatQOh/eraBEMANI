@ロケーション条件_ゲームセンター
;汎用ロケーション 条件無し
RETURN 1

@出現キャラ_ゲームセンター
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数

;NOではなく登録番号で指定する

FOR LCOUNT, 1, CHARANUM
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != ""
		CONTINUE
	;主に音ゲーマーなどが居る
	SELECTCASE CHARANAMEF(LCOUNT)
		;識と士朗が100%居る
		CASE "識", "神崎 士朗"
			出現率:LCOUNT = 100
		;数が多いので各30%
		{
		CASE "ユーズ", "ナイア", "水城 セリカ", "ニクス", "クジャク", "北見 エリカ", 
			 "ジルチ", "光伊 計名", "雛月 理々奈", "神崎 慧靂", "菱宮 津軽", "刃雪 達磨", 
			 "梅桐 彩葉", "シア", "鉄火", "トラン", "黄シグ", "ハーティ", "夜兎姫", "おいわちゃん", 
			 "ミミ", "ニャミ", "MZD", "パステルくん", "揚羽", "エミ", "ユニ", "フリオ", "リノン", 
			 "オトベア", "BisCo", "Mei", "ToRa", "伊澄 いずみ", "三田 希少", "グリム・ハルト", "芽兎 めう", 
			 "いちか", "クプロ"
		}
			出現率:LCOUNT = 30
		;休日や放課後に集団で来ているグループとかがある
		CASEELSE
			IF RANDPER(2) && グループ数 < 10
				;相性の良いキャラと2～3人で行動している
				;無所属、教師陣のグループは休日のみ現れる
				IF ISHOLIDAY()
					CALL グループキャラ選出, LCOUNT, "相性"
				;あとは放課後等
				ELSEIF CSTR:LCOUNT:在校 != ""
					CALL クラスメイト選出, LCOUNT, "相性"
				ELSE
					CONTINUE
				ENDIF
				;2人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				;2～4人のグループ
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				IF RANDPER(30)
					集団行動:グループ数:2 = RESULT:1
					SIF RANDPER(30)
						集団行動:グループ数:3 = RESULT:2
				ENDIF
				グループ数++
			ENDIF
	ENDSELECT
NEXT
