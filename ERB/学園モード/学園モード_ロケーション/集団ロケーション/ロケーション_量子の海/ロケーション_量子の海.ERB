@ロケーション条件_量子の海
#DIM DYNAMIC LCOUNT
;出現条件のキャラをフルネーム(NAME)で入れる
{
#DIMS DYNAMIC 条件用キャラ = 
"モード",
"リリーゼ",
"比多木 小瑠璃"
}

FOR LCOUNT, 0, VARSIZE("条件用キャラ")
	IF GETCHARANAME(条件用キャラ:LCOUNT) > 0
		SELECTCASE RELATION:GETCHARANAME(条件用キャラ:LCOUNT):(NO:PLAYER)
			;相性120%以上が条件
			CASE IS >= 120
				RETURN 1
		ENDSELECT
	ENDIF
NEXT

RETURN 0

@出現キャラ_量子の海
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC グループ数

;NOではなく登録番号で指定する

FOR LCOUNT, 1, CHARANUM
	;学校などに行ってるキャラは居ない
	SIF CSTR:LCOUNT:現在地 != ""
		CONTINUE
	SELECTCASE CHARANAMEF(LCOUNT)
		;ご存知の彼女たちがいる
		CASE "モード", "リリーゼ", "比多木 小瑠璃"
			出現率:LCOUNT = 100
		;海なら海賊も居るって分かるはずなんだけどなぁ・・・
		CASE "アイリーン", "ライア", "ニョア"
			出現率:LCOUNT = 50
		;海の家の経営者と人魚
		CASE "サタヌ", "マーメイドガール", "パール", "テトラ", "ティア"
			出現率:LCOUNT = 20
		;天気が良い休日はグループで来てる人がいる
		CASEELSE
			IF ISHOLIDAY() && CSTR:LCOUNT:在校 != "" && 天気() == "晴れ" && RANDPER(3) && グループ数 < 10
				;相性の良いキャラと3～5人で行動している
				CALL グループキャラ選出, LCOUNT, "相性"
				;3人以上揃わなかったら無かったことにする
				SIF RESULT:0 == 0
					CONTINUE
				集団行動:グループ数:0 = LCOUNT
				集団行動:グループ数:1 = RESULT:0
				集団行動:グループ数:2 = RESULT:1
				IF RANDPER(50)
					集団行動:グループ数:3 = RESULT:2
					SIF RANDPER(50)
						集団行動:グループ数:4 = RESULT:3
				ENDIF
				グループ数++
			ENDIF
		
	ENDSELECT
NEXT

	


