@医務室
#DIM DYNAMIC 配属人数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 補充費
#DIM DYNAMIC 名前文字数
#DIM DYNAMIC 禁断の知識あり
#DIM DYNAMIC 調合知識あり

SIF TALENT:MASTER:禁断の知識
	禁断の知識あり = 1
SIF TALENT:MASTER:調合知識
	調合知識あり = 1

配属人数 = CMATCH(CSTR:配属, "医務室")
DRAWLINE
PRINTL □医務室
PRINTL 　医療班を配属させたり、医薬品の補充によってキャラクターの健康管理が可能です
PRINTL 　配属中のキャラの能力に応じて体力・気力・ストレス値の回復量が変動します
PRINT 　医薬品の在庫:
SELECTCASE FLAG:医薬品
	CASE 0
		SETCOLORBYNAME gray
	CASE 1 TO 15
		SETCOLORBYNAME red
	CASE 16 TO 50
		SETCOLORBYNAME yellow
	CASE 51 TO 100
		SETCOLORBYNAME lime
ENDSELECT
PRINTFORM {FLAG:医薬品}\%
RESETCOLOR
CALL PASELIL, @" 配属人数:{配属人数}/3 所持金:{MONEY}＄"
PRINTL □担当医
IF CMATCH(CSTR:配属, "医務室")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "医務室"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "医務室"
			PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 
			SIF !CFLAG:LCOUNT:配属完了
				PRINT 【配属準備中】
			CALL ISTALENT, LCOUNT, "調合知識"
			SIF TALENT:LCOUNT:調合知識
				調合知識あり = 1
			CALL ISTALENT, LCOUNT, "治療"
			IF TALENT:LCOUNT:禁断の知識
				PRINT [禁断の知識]
				禁断の知識あり = 1
			ENDIF
			IF TALENT:LCOUNT:謎の魅力
				PRINT [謎の魅力]
			ELSEIF TALENT:LCOUNT:魅力
				PRINT [魅力]
			ELSEIF TALENT:LCOUNT:魅惑
				PRINT [魅惑]
			ELSEIF TALENT:LCOUNT:人気
				PRINT [人気]
			ENDIF
			CALL ISTALENT, LCOUNT, "統率"
			PRINTL 
		ENDIF
	NEXT
ELSE
	PRINTL [無し]
ENDIF
PRINTL 
;医薬品主人を除くキャラ人数*5000
;浪費癖一人につき一人頭1000アップ
補充費 = (CHARANUM-1)*(5000+浪費癖人数("医務室")*1000)
CALL PASELIL, @"　[1] - 医薬品を補充する\@ !ORIGIN ? ({補充費}＄)#\@"
PRINTL 　[2] - キャラを配属させる
PRINTL 　[3] - 利用履歴を確認する（過去5日分）
IF 禁断の知識あり
	PRINTL 　[4] - 身体改造を行う
ELSE
	CALL PRINTCOLORL, "　[-] - 身体改造を行う(主人か配属中のキャラが[禁断の知識]を持っている必要があります)", "gray"
ENDIF
IF 調合知識あり
	PRINTL 　[5] - 薬品系アイテムを強化する
ELSE
	CALL PRINTCOLORL, "　[-] - 薬品系アイテムを強化する(主人か配属中のキャラが[調合知識]を持っている必要があります)", "gray"
ENDIF
IF 禁断の知識あり
	PRINTL 　[6] - 人体錬成をする
ELSE
	CALL PRINTCOLORL, "　[-] - 人体錬成をする(主人か配属中のキャラが[禁断の知識]を持っている必要があります)", "gray"
ENDIF
SIF STSMOD
	PRINTL 　[9] - キャラを解体する

;IF 配属人数
;	PRINTL [3] - 健康診断を行う
;ELSE
;	SETCOLORBYNAME gray
;	PRINTL [-] - 健康診断を行う(キャラを配属する必要があります)
;	RESETCOLOR
;ENDIF
DRAWLINE
PRINTPLAIN 　医務室　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			IF FLAG:医薬品 >= 100
				PRINTW 医薬品は十分に揃っています
			ELSEIF MONEY > 補充費 || ORIGIN
				PRINTW 医薬品を補充しました
				MONEY -= 補充費
				FLAG:医薬品 = 100
			ELSE
				PRINTW 所持金が足りません
			ENDIF
			RESTART
		CASE 2
			CALL STRATEGY_PERSONAL, "医務室"
			RESTART
		CASE 3
			CALL 医務室利用者
			RESTART
		CASE 4
			SIF 禁断の知識あり
				CALL BODY_MOD_1
			RESTART
		CASE 5
			SIF 調合知識あり
				CALL MED_MOD
			RESTART
		CASE 6
			SIF 禁断の知識あり
				CALL 人体錬成
			RESTART
		CASE 9
			SIF STSMOD
				CALL STS_BLOOD
			RESTART
		;CASE 3
		;	DRAWLINE
		;	FOR PAGING, 0, CHARANUM
		;		;色変えしたいのでここだけ特別処理
		;		CALL PAGING, PAGING != 0, "健康診断用", "健康診断"
		;	NEXT
		;	SIF RESULT == -1
		;		RESTART
		;	CALL 健康診断, RESULT
	ENDSELECT
LOOP 1

@健康診断, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC クジャクアイ
;孔雀の瞳を持ってるキャラが主人・助手・担当医に居るかを見る
SIF TALENT:MASTER:孔雀の瞳
	クジャクアイ = 1
SIF ASSI > 0 && TALENT:ASSI:孔雀の瞳
	クジャクアイ = 1
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室" && TALENT:LCOUNT:孔雀の瞳
		クジャクアイ = 1
NEXT

DRAWLINE
PRINTL □患者
PRINTFORML 　名前:%NAME:対象%
PRINTFORML 　体力%BARBASE("体力", 対象, 32)%
PRINTFORML 　気力%BARBASE("気力", 対象, 32)%
PRINT 　ストレス値:
IF クジャクアイ
	SELECTCASE CFLAG:対象:ストレス値
		CASE 1 TO 99
			SETCOLORBYNAME lime
		CASE 100 TO 299
			SETCOLORBYNAME yellow
		CASE 300 TO 499
			SETCOLORBYNAME red
		CASE IS >= 500
			SETCOLORBYNAME darkred
	ENDSELECT
	PRINTFORML {CFLAG:対象:ストレス値}/999
	RESETCOLOR
ELSE
	SELECTCASE CFLAG:対象:ストレス値
		CASE 0
			PRINTL 無
		CASE 1 TO 99
			SETCOLORBYNAME lime
			PRINTL 低
		CASE 100 TO 299
			SETCOLORBYNAME yellow
			PRINTL 中
		CASE 300 TO 499
			SETCOLORBYNAME red
			PRINTL 高
		CASE IS >= 500
			SETCOLORBYNAME darkred
			PRINTL 過多
	ENDSELECT
	RESETCOLOR
ENDIF

PRINTL 

@TURNEND_医務室
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 男性医師
#DIM DYNAMIC 女性医師
#DIM DYNAMIC 医師魅力
#DIM DYNAMIC 利用確率
#DIM DYNAMIC 利用人数
#DIM DYNAMIC 調合知識持ち
#DIM DYNAMIC 利用確率補正
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC 世話好き人数
#DIM DYNAMIC モチベ加算値

医務室体力補正 = 0
医務室気力補正 = 0
医務室ストレス補正 = 0

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "医務室" && !CFLAG:LCOUNT:口上非表示
		TRYCALLFORM TURNEND_医務室_%CSVNAMERPS(NO:LCOUNT)%
NEXT

;以下の処理は日付終了時のみ
SIF TIME == 0
	RETURN

;配属中のキャラを調べる
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		IF CFLAG:LCOUNT:配属完了
			医務室体力補正 += 30
			医務室気力補正 += 30
			医務室ストレス補正++
			;奉仕精神を見る
			IF 統率者("調理室") && !TALENT:LCOUNT:統率
				医務室体力補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/2*10
				医務室気力補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/2*10
				医務室ストレス補正 += LIMIT(ABL:LCOUNT:奉仕精神+1, 0, 10)/3
			ELSE
				医務室体力補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/2*10
				医務室気力補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/2*10
				医務室ストレス補正 += LIMIT(ABL:LCOUNT:奉仕精神, 0, 10)/3
			ENDIF
			SELECTCASE SEX(LCOUNT)
				CASE 1
					男性医師++
				CASE 2
					女性医師++
			ENDSELECT
			;医師の素質を見る
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "生意気", "倒錯的"
						医務室ストレス補正--
					CASE "気丈", "冷静", "自制心"
						医務室体力補正 += 10
						医務室気力補正 += 10
					CASE "衝動的"
						医務室体力補正 -= 10
						医務室気力補正 -= 10
					CASE "プライド高い"
						医務室体力補正 += 20
						医務室気力補正 += 20
						医務室ストレス補正--
					CASE "ハイテンション", "解放", "鼓舞"
						医務室気力補正 += 10
						医務室ストレス補正++
					;サボり魔は25%の確率で上記の基礎補正を打ち消す
					CASE "サボり魔"
						IF RAND:4 == 0 && !ワーカホリックレベル(LCOUNT)
							医務室体力補正 -= 50
							医務室気力補正 -= 50
							医務室ストレス補正--
						ENDIF
					CASE "無関心"
						医務室体力補正 += 10
					CASE "楽観的"
						医務室体力補正 -= 10
						医務室気力補正 += 10
						医務室ストレス補正++
					CASE "悲観的"
						医務室体力補正 += 20
						医務室気力補正 -= 10
						医務室ストレス補正--
					CASE "針さばき", "潔癖症"
						医務室体力補正 += 20
						医務室気力補正 += 20
					CASE "汚れ無視"
						医務室体力補正 -= 20
						医務室気力補正 -= 20
					CASE "魅惑", "魅力", "人気"
						医務室気力補正 += 10
						利用確率補正 += 3
					CASE "謎の魅力"
						医務室気力補正 += 20
						医務室ストレス補正++
						利用確率補正 += 5
					CASE "治療"
						医務室体力補正 += 50
						医務室気力補正 += 50
						医務室ストレス補正 += 2
						利用確率補正 += 10
					CASE "母性"
						医務室体力補正 += 10
						医務室気力補正 += 10
						医務室ストレス補正++
						利用確率補正 += 3
					CASE "調合知識"
						調合知識持ち++
					CASE "世話好き"
						医務室体力補正 += 10
						医務室気力補正 += 20
						医務室ストレス補正++
						利用確率補正 += 3
						世話好き人数++
				ENDSELECT
			NEXT
			
			;モチベ計算用にもう一度見る
			モチベ計算用 = 0
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "プライド高い", "冷静", "世話好き", "自制心", "楽観的", "調合知識", "潔癖症", "サド", "母性", "治療"
						モチベ計算用++
					CASE "衝動的", "弱気", "無関心", "悲観的", "痛みに弱い", "男嫌い", "女嫌い", "同族嫌悪"
						モチベ計算用--
				ENDSELECT
			NEXT

			モチベ加算値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数+(ABL:LCOUNT:奉仕精神/3)
			SIF TALENT:LCOUNT:世話好き
				モチベ加算値 -= 世話好き人数
			;配属先のキャラの相性で計算
			モチベ加算値 += 相性モチベ補正(LCOUNT)
			CFLAG:LCOUNT:モチベーション = LIMIT(モチベ加算値, 1, 100)

			IF ABL:LCOUNT:奉仕精神 >= 5
				医務室体力補正 += 20
				医務室気力補正 += 20
				医務室ストレス補正++
			ENDIF
			;実務経験加算
			EXP:LCOUNT:実務経験 += 2
			;習得早ければ+1
			SIF TALENT:LCOUNT:習得早い
				EXP:LCOUNT:実務経験++
		ENDIF
		;配属完了にする
		CFLAG:LCOUNT:配属完了 = 1
	ENDIF
NEXT

医務室体力補正 = MAX(医務室体力補正, 0)
医務室気力補正 = MAX(医務室気力補正, 0)
医務室ストレス補正 = MAX(医務室ストレス補正, 0)

;医薬品が足りてない場合はそれぞれ/10
IF FLAG:医薬品 <= 0
	医務室体力補正 /= 10
	医務室気力補正 /= 10
	医務室ストレス補正 /= 10
ENDIF

;医務室利用履歴の変数を後ろに詰める 2次元配列ではARRAYSHIFTできないので力技
FOR LCOUNT, 3, -1, -1
	FOR LCOUNT2, 0, 1000
		医務室利用履歴:(LCOUNT+1):LCOUNT2 = %医務室利用履歴:LCOUNT:LCOUNT2%
		SIF LCOUNT == 0
			医務室利用履歴:LCOUNT:LCOUNT2 = 
	NEXT
NEXT

;利用者の判定
FOR LCOUNT, 0, CHARANUM
	;MASTERは除外
	SIF LCOUNT == MASTER
		CONTINUE
	;配属中の医師も除外
	SIF CSTR:LCOUNT:配属 == "医務室"
		CONTINUE
	;残り体力と気力とストレス値から確率を割り出す
	;各値それぞれ最大100%(ストレス値は99%)で、合計を利用確率にする
	利用確率 = 100-(100*BASE:LCOUNT:体力)/MAXBASE:LCOUNT:体力
	利用確率 += 100-(100*BASE:LCOUNT:気力)/MAXBASE:LCOUNT:気力
	利用確率 += CFLAG:LCOUNT:ストレス値/10
	
	;利用確率があるなら素質での補正
	IF 利用確率
		利用確率 += 利用確率補正
		FOR LCOUNT2, 0, VARSIZE("TALENT")
			SIF !TALENT:LCOUNT:LCOUNT2
				CONTINUE
			SELECTCASE TALENTNAME:LCOUNT2
				CASE "気丈", "プライド高い", "強気", "痛みに強い"
					利用確率 -= 5
				CASE "生意気", "反抗的", "楽観的", "恥じらい", "薬毒耐性", "マゾ", "戦士"
					利用確率 -= 3
				CASE "臆病", "素直", "冷静", "悲観的", "寂しがり屋", "中毒しやすい", "暑さに弱い", "寒さに弱い"
					利用確率 += 3
				CASE "弱気", "痛みに弱い", "治療", "妊娠", "育児中"
					利用確率 += 5
			ENDSELECT
		NEXT
		;苦痛刻印*3
		利用確率 += MARK:LCOUNT:苦痛刻印*3
		;二日酔い +5
		SIF CFLAG:LCOUNT:二日酔い
			利用確率 += 5
	ENDIF
	;0以下だったら0に合わせる
	利用確率 = MAX(0, 利用確率)
	
	;男好き、女好きは元気でも来訪して薬を使いやがる
	SIF TALENT:LCOUNT:男好き
		利用確率 += 男性医師*10
	SIF TALENT:LCOUNT:女好き
		利用確率 += 女性医師*10
	
	;利用確率を0～100に合わせる
	利用確率 = LIMIT(利用確率, 0, 100)
	
	;利用するなら利用フラグを立てる
	IF 利用確率 >= RAND(1, 101)
		CFLAG:LCOUNT:医務室利用 = 1
		;利用履歴変数に名前を入れる
		医務室利用履歴:0:利用人数 = %NAME:LCOUNT%
		利用人数++
	ENDIF
NEXT

;利用者の数*30習得を増やす
IF 利用人数 > 0
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "医務室" && CFLAG:LCOUNT:配属完了
			JUEL:LCOUNT:習得 += 利用人数*30
	NEXT
ENDIF

;最後に利用者の割合に応じて医薬品を減らす 1ターンで最大10
;調合知識持ちだけ医薬品の消費を抑える
SIF 利用人数
	FLAG:医薬品 -= MAX((利用人数*10)/CHARANUM-調合知識持ち, 1)

FLAG:医薬品 = LIMIT(FLAG:医薬品, 0, 100)

PRINTFORML {利用人数}人が医務室を利用しました

RETURN

@医務室利用者
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日
#DIM DYNAMIC 利用年, 5
#DIM DYNAMIC 利用月, 5
#DIM DYNAMIC 利用日, 5
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ

;5日遡ってその日時を入れておく
年 = YEAR
月 = MONTH
日 = DAYS
FOR LCOUNT, 0, 5
	DO
		日--
		IF 日 <= 0
			月--
			IF 月 <= 0
				年--
				月 = 12
			ENDIF
			日 = 31
		ENDIF
	LOOP !EXIST_DAY(年, 月, 日)
	利用年:LCOUNT = 年
	利用月:LCOUNT = 月
	利用日:LCOUNT = 日
NEXT

VARLINE = 0

$LOOP
SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

DRAWLINE
PRINTFORML □{利用年:ページ}年{利用月:ページ}月{利用日:ページ}日の医務室利用者
FOR LCOUNT, 0, 1000
	IF 医務室利用履歴:ページ:LCOUNT == ""
		IF LCOUNT == 0
			PRINTL 
			PRINTL 利用者無し
			PRINTL 
		ENDIF
		BREAK
	ENDIF
	PRINTFORML 　%医務室利用履歴:ページ:LCOUNT%
NEXT
DRAWLINE
PRINTPLAIN 　医務室利用履歴　
IF ページ > 0
	PRINT [1] 翌日の履歴 
ELSE
	PRINTPLAIN 　　　　　　　 
ENDIF
PRINT [0] 戻る 
IF ページ < 4
	PRINT [9] 前日の履歴
ELSE
	PRINTPLAIN 　　　　　　　
ENDIF

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			VARLINE = 0
			RETURN
		CASE 1
			SIF ページ > 0
				ページ--
			GOTO LOOP
		CASE 9
			SIF ページ < 4
				ページ++
			GOTO LOOP
	ENDSELECT
LOOP 1

@出産用医務室ランク, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 医務室ランク

;配属中のキャラを調べる
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "医務室"
		IF CFLAG:LCOUNT:配属完了
			;医師の素質を見る
			FOR LCOUNT2, 0, VARSIZE("TALENT")
				SIF !TALENT:LCOUNT:LCOUNT2
					CONTINUE
				SELECTCASE TALENTNAME:LCOUNT2
					CASE "母性", "鼓舞"
						医務室ランク++
					CASE "治療"
						医務室ランク++
					CASE "汚れ無視"
						医務室ランク--
				ENDSELECT
			NEXT
		ENDIF
	ENDIF
NEXT

;3人まで配属できるので最大6
RETURNF 医務室ランク

@STS_BLOOD
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象

DRAWLINE

PRINT □解体するキャラを選んでください。
FONTBOLD
PRINTL 解体したキャラは死亡します
FONTREGULAR

表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF TALENT:LCOUNT:無機物
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+20)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT% 回収できる物:
	IF ISSLIME(LCOUNT)
		IF TALENT:LCOUNT:小柄体型
			PRINT 粘液2500ml 
		ELSE
			PRINT 粘液4000ml 
		ENDIF
	ELSE
		IF TALENT:LCOUNT:小柄体型
			PRINT 血液2500ml 
		ELSE
			PRINT 血液4000ml 
		ENDIF
	ENDIF
	SIF 生殖機能♂(LCOUNT)
		PRINT 精液100ml 
	SIF TALENT:LCOUNT:母乳体質
		PRINT 母乳200ml 
	SIF TALENT:LCOUNT:動物耳
		PRINT ケモミミが付いた頭 
	SIF TALENT:LCOUNT:羽
		PRINT 翼の生えた胴体 
	SIF TALENT:LCOUNT:足使用不可
		PRINT 人魚の下半身 
	SIF TALENT:LCOUNT:尻尾
		PRINT 切断された尻尾 
	SELECTCASE CSTR:LCOUNT:種族
		CASE "悪魔"
			PRINT 悪魔の心臓 
		CASE "天使"
			PRINT 天使の心臓 
		CASE "神"
			PRINT 神の心臓 
		CASE "魔術師"
			PRINT 魔術師の心臓 
	ENDSELECT
	PRINTL 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　解体可能なキャラは居ません
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　解体.{ページ数+1}　
IF ページ数 > 0
	PRINT [-1] - 前ページ
ELSE
	PRINTPLAIN                
ENDIF
PRINT [0] - 戻る
SIF 次ページあり
	PRINT [-9] - 次ページ
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE -1
			SIF ページ数 > 0
				ページ数--
			RESTART
		CASE -9
			SIF 次ページあり
				ページ数++
			RESTART
		CASE 1 TO CHARANUM
			対象 = RESULT
			SIF TALENT:対象:無機物
				CONTINUE
			PRINTFORML %NAME:対象%を解体します。よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			PRINTL [2] - 楽しみながら解体する
			DO
				INPUT
				SIF RESULT == 1
					RESTART
			LOOP LOOPRES(0, 2)
			IF RESULT == 2
				DRAWLINE
				TALENT:MASTER:狂気 = 1
				IF VAGINA(対象)
					CALL EXPUP, "Ｖ経験", RAND(10, 21), 対象
					CALL EXPUP, "膣射経験", RAND(2, 5), 対象
				ENDIF
				CALL EXPUP, "Ａ経験", RAND(10, 21), 対象
				CALL EXPUP, "絶頂経験", RAND(5, 11), 対象
				SIF PENIS(対象)
					CALL EXPUP, "射精経験", RAND(5, 11), 対象
				CALL EXPUP, "放尿経験", RAND(1, 5), 対象
				SIF TALENT:対象:妊娠
					CALL EXPUP, "流産経験", 1, 対象

				CALL EXPUP, "異常経験", 5, 対象
				CALL JUELUP, "苦痛", RAND(100, 1000)*100, 対象
				CALL JUELUP, "恐怖", RAND(100, 1000)*100, 対象
				SIF PENIS(MASTER)
					CALL EXPUP, "射精経験", RAND(5, 11), MASTER
				CALL EXPUP, "嗜虐快楽経験", 10, MASTER
			ENDIF
			SETCOLORBYNAME red
			CUSTOMDRAWLINE ✂----------
			RESETCOLOR
			PRINTFORML %NAME:対象%を解体しました
			IF ISSLIME(対象)
				IF TALENT:対象:小柄体型
					PRINTL 保管済み粘液+2500ml
					FLAG:保管済み粘液 += 2500
				ELSE
					PRINTL 保管済み粘液+4000ml
					FLAG:保管済み粘液 += 4000
				ENDIF
			ELSE
				IF TALENT:対象:小柄体型
					PRINTL 保管済み血液+2500ml
					FLAG:保管済み血液 += 2500
				ELSE
					PRINTL 保管済み血液+4000ml
					FLAG:保管済み血液 += 4000
				ENDIF
			ENDIF
			IF 生殖機能♂(対象)
				PRINTL 保管済み精液+100ml
				FLAG:保管済み精液 += 100
			ENDIF
			IF TALENT:対象:母乳体質
				PRINTL 保管済み母乳+200ml
				FLAG:保管済み母乳 += 200
			ENDIF
			IF TALENT:対象:動物耳
				PRINTL ケモミミが付いた頭+1
				ITEM:ケモミミが付いた頭 += 1
			ENDIF
			IF TALENT:対象:羽
				PRINTL 翼の生えた胴体+1
				ITEM:翼の生えた胴体 += 1
			ENDIF
			IF TALENT:対象:足使用不可
				PRINTL 人魚の下半身+1
				ITEM:人魚の下半身 += 1
			ENDIF
			IF TALENT:対象:尻尾
				PRINTL 切断された尻尾+1
				ITEM:切断された尻尾 += 1
			ENDIF
			SELECTCASE CSTR:対象:種族
				CASE "悪魔"
					PRINTL 悪魔の心臓+1
					ITEM:悪魔の心臓 += 1
				CASE "天使"
					PRINTL 天使の心臓+1
					ITEM:天使の心臓 += 1
				CASE "神"
					PRINTL 神の心臓+1
					ITEM:神の心臓 += 1
				CASE "魔術師"
					PRINTL 魔術師の心臓+1
					ITEM:魔術師の心臓 += 1
			ENDSELECT
			IF NO:対象 < 10000 && !DNAデータ:(NO:対象)
				PRINTFORML %CALLNAME:対象%のDNAデータを解析できるようになりました
				DNAデータ:(NO:対象) = 1
			ENDIF
			WAIT
			CALL CLEAR_CHARA, 対象
			RESTART
	ENDSELECT
LOOP 1


