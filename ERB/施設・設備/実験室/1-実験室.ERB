@実験室
#DIM DYNAMIC 配属人数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 名前文字数
#DIM DYNAMIC 禁断の知識あり
配属人数 = CMATCH(CSTR:配属, "実験室")

禁断の知識あり = 0

SIF TALENT:MASTER:禁断の知識
	禁断の知識あり = 1

CALL 実験室レベル計算
DRAWLINE
PRINTL □実験室
PRINTL 　研究者を配属することによって、アイテムの生産や改造が可能になります
PRINTL 　能力の高いキャラや、いささか常識から外れているキャラの適性が高いようです
PRINTFORML □研究レベル:{基礎研究レベル}/50 欲望レベル:{欲望研究レベル}/50
PRINTFORML 　サディストレベル:{サド研究レベル}/50 マゾヒストレベル:{マゾ研究レベル}/50
PRINTFORML □研究員:{配属人数}/5
IF CMATCH(CSTR:配属, "実験室")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "実験室"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 != "実験室"
			CONTINUE
		PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 工作技能:{ABL:LCOUNT:工作技能} サドっ気:{ABL:LCOUNT:サドっ気} マゾっ気:{ABL:LCOUNT:マゾっ気}
		IF !CFLAG:LCOUNT:配属完了
			PRINT 【配属準備中】
		ELSE
			SIF 統率者("実験室") && !TALENT:LCOUNT:統率
				PRINT  統率者ボーナス+1 
		ENDIF
		SIF TALENT:LCOUNT:禁断の知識
			禁断の知識あり = 1
		CALL ISTALENT, LCOUNT, "工学博士"
		CALL ISTALENT, LCOUNT, "サド"
		CALL ISTALENT, LCOUNT, "マゾ"
		CALL ISTALENT, LCOUNT, "倒錯的"
		CALL ISTALENT, LCOUNT, "調合知識"
		CALL ISTALENT, LCOUNT, "禁断の知識"
		CALL ISTALENT, LCOUNT, "スライム"
		CALL ISTALENT, LCOUNT, "溶ける快"
		CALL ISTALENT, LCOUNT, "統率"
		SIF 特質("ワーカホリック", LCOUNT)
			PRINTFORM [ワーカホリック+{特質("ワーカホリック", LCOUNT)/10}]
		PRINTL 
	NEXT
ELSE
	PRINTL 　[無し]
ENDIF
PRINTL 
PRINTL 　[1] - キャラを配属させる
PRINTL 　[2] - アイテムを改造する
IF 禁断の知識あり
	PRINTL 　[3] - ロボットファクトリーを使う
ELSE
	SETCOLORBYNAME gray
	PRINTL 　[-] - ロボットファクトリーを使う（主人か配属中のキャラに[禁断の知識]が必要です）
	RESETCOLOR
ENDIF
PRINTL 　[4] - 実験室の説明
;PRINTL 　[3] - 実験室ボーナスを確認する
DRAWLINE
PRINT 　実験室　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			CALL STRATEGY_PERSONAL, "実験室"
			RESTART
		CASE 2
			CALL アイテム改造
			RESTART
		CASE 3
			SIF !禁断の知識あり
				CONTINUE
			CALL ロボットファクトリー
			RESTART
		CASE 4
			CALL MANUAL_実験室
			RESTART
	ENDSELECT
LOOP 1

@アイテム改造
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 選択アイテム
#DIMS DYNAMIC 改造前
#DIMS DYNAMIC 改造後
#DIMS DYNAMIC 改造アイテム
#DIM DYNAMIC 費用
#DIM DYNAMIC 必要基礎レベル
#DIM DYNAMIC 必要欲望レベル
#DIM DYNAMIC 必要サドレベル
#DIM DYNAMIC 必要マゾレベル
#DIMS DYNAMIC 必要アイテム
#DIMS DYNAMIC 必要素質

PRINTL □改造するアイテムを選んでください
FOR LCOUNT, 0, 50
	SIF SAVESTRNAME:LCOUNT == ""
		CONTINUE
	IF ITEM:(SAVESTRNAME:LCOUNT)
		PRINTFORML [{LCOUNT, 2}] - %SAVESTRNAME:LCOUNT%
	ELSE
		SETCOLORBYNAME gray
		PRINTFORML [--] - %SAVESTRNAME:LCOUNT%（アイテム未所持）
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTPLAIN 　実験室　
PRINTL [100] - 戻る

DO
	INPUT
	SIF RESULT == 100
		RETURN
	選択アイテム = %SAVESTRNAME:RESULT%
	$改造
	費用 = 0
	必要基礎レベル = 0
	必要欲望レベル = 0
	必要サドレベル = 0
	必要マゾレベル = 0
	必要アイテム =
	必要素質 =
	;アイテムの改造状態 無改造だと「通常の」接頭語が付く
	SIF 選択アイテム != "" && ITEM:選択アイテム
		PRINTFORML □現在の状態 \@ 選択アイテム == SAVESTR:選択アイテム ? 通常の #  \@%SAVESTR:選択アイテム%
	SELECTCASE 選択アイテム
		CASE "ローター"
			改造アイテム = ローター
			PRINTL 　[1] - 通常のローターに戻す
			PRINTL 　[2] - 防水仕様にする(お風呂で使用可能になる)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常のローター
					CASE 2
						改造後 = 防水ローター
						費用 = 30000
						必要基礎レベル = 3
				ENDSELECT
			LOOP LOOPRES(1, 2)
		CASE "電気按摩"
			改造アイテム = 電気按摩
			PRINTL 　[1] - 通常の電気按摩に戻す
			PRINTL 　[2] - 防水仕様にする(お風呂で使用可能になる)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常の電気按摩
					CASE 2
						改造後 = 防水電気按摩
						費用 = 50000
						必要基礎レベル = 6
				ENDSELECT
			LOOP LOOPRES(1, 2)
		CASE "オナホール"
			改造アイテム = オナホール
			PRINTL 　[1] - 通常のオナホールに戻す
			PRINTL 　[2] - 電動オナホールに改造する(快感アップ)
			PRINTL 　[3] - スライムオナホールに改造する(快感アップ、改造には配属中のキャラに[スライム]or[溶ける快]を持つキャラが必要)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常のオナホール
						必要素質 = 
					CASE 2
						改造後 = 電動オナホール
						費用 = 100000
						必要基礎レベル = 5
						必要欲望レベル = 7
						必要素質 = 
					CASE 3
						改造後 = スライムオナホール
						費用 = 50000
						必要基礎レベル = 2
						必要欲望レベル = 7
						必要素質 = スライム
				ENDSELECT
			LOOP LOOPRES(1, 2, 3)
		CASE "バイブ"
			改造アイテム = バイブ
			PRINTL 　[1] - 通常のバイブに戻す
			PRINTL 　[2] - 振動を強くする(快感アップ)
			PRINTL 　[3] - 細くする(苦痛、体力消費量、快感ダウン )
			PRINTL 　[4] - 太くする(苦痛、体力消費量アップ 膣拡張可能になる)
			;PRINTL 　[5] - 両穴バイブに改造する(要アナルバイブ)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常のバイブ
					CASE 2
						改造後 = 強力なバイブ
						費用 = 50000
						必要基礎レベル = 5
						必要欲望レベル = 5
					CASE 3
						改造後 = 細いバイブ
						費用 = 100000
						必要基礎レベル = 10
						必要欲望レベル = 10
					CASE 4
						改造後 = 太いバイブ
						費用 = 100000
						必要基礎レベル = 10
						必要欲望レベル = 10
						必要サドレベル = 10
					;CASE 5
					;	改造後 = 両穴バイブ
					;	費用 = 150000
					;	必要基礎レベル = 10
					;	必要欲望レベル = 15
					;	必要アイテム = アナルバイブ
				ENDSELECT
			LOOP LOOPRES(1, 2, 3, 4)
		CASE "アナルバイブ"
			改造アイテム = アナルバイブ
			PRINTL 　[1] - 通常のアナルバイブに戻す
			PRINTL 　[2] - 振動を強くする(快感アップ)
			PRINTL 　[3] - 細くする(苦痛、体力消費量、快感ダウン)
			PRINTL 　[4] - 太くする(苦痛、体力消費量アップ アナル拡張可能になる)
			PRINTL 　[5] - 電動エネマグラに改造する(男性専用 射精ゲージダウン 快感アップ メスイキ時のボーナスアップ)
			;PRINTL 　[6] - 両穴バイブに改造する(要バイブ)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常のアナルバイブ
					CASE 2
						改造後 = 強力なアナルバイブ
						費用 = 50000
						必要基礎レベル = 5
						必要欲望レベル = 5
					CASE 3
						改造後 = 細いアナルバイブ
						費用 = 100000
						必要基礎レベル = 10
						必要欲望レベル = 10
					CASE 4
						改造後 = 太いアナルバイブ
						費用 = 100000
						必要基礎レベル = 10
						必要欲望レベル = 10
						必要サドレベル = 10
					CASE 5
						改造後 = 電動エネマグラ
						費用 = 150000
						必要基礎レベル = 10
						必要欲望レベル = 20
						必要マゾレベル = 10
					;CASE 6
					;	改造後 = 両穴バイブ
					;	費用 = 150000
					;	必要基礎レベル = 10
					;	必要欲望レベル = 15
					;	必要アイテム = バイブ
				ENDSELECT
			LOOP LOOPRES(1, 2, 3, 4, 5)
		CASE "アナルビーズ"
			改造アイテム = アナルビーズ
			PRINTL 　[1] - 通常のアナルビーズに戻す
			PRINTL 　[2] - 珠を大きくする(苦痛、体力消費量、快感アップ)
			PRINTL 　[3] - 珠の形をいびつにする(引き抜いたときの苦痛、体力消費量、快感大幅アップ)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常のアナルビーズ
					CASE 2
						改造後 = 大きなアナルビーズ
						費用 = 30000
						必要基礎レベル = 3
						必要サドレベル = 15
						必要マゾレベル = 15
					CASE 3
						改造後 = いびつなアナルビーズ
						費用 = 50000
						必要基礎レベル = 5
						必要サドレベル = 20
						必要マゾレベル = 20
				ENDSELECT
			LOOP LOOPRES(1, 2, 3)
		CASE "浣腸器"
			改造アイテム = 浣腸器
			PRINTL 　[1] - 通常の浣腸器に戻す
			PRINTL 　[2] - 媚薬浣腸に改造する(浣腸時の催淫効果大)
			PRINTL 　[3] - 精液浣腸に改造する(保管中の精液の中出しが可能)
			PRINTL 　[4] - スライム浣腸に改造する(保管中の粘液の中出しが可能、改造には配属中のキャラに[スライム]or[溶ける快]を持つキャラが必要)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常の浣腸器
						必要素質 = 
					CASE 2
						改造後 = 媚薬浣腸
						費用 = 300000
						必要基礎レベル = 5
						必要欲望レベル = 10
						必要素質 = 
					CASE 3
						改造後 = 精液浣腸
						費用 = 10000
						必要基礎レベル = 2
						必要欲望レベル = 15
						必要素質 = 
					CASE 4
						改造後 = スライム浣腸
						費用 = 10000
						必要基礎レベル = 2
						必要欲望レベル = 15
						必要素質 = スライム
				ENDSELECT
			LOOP LOOPRES(1, 2, 3, 4)
		CASE "搾乳器"
			改造アイテム = 搾乳器
			PRINTL 　[1] - 通常の搾乳器に戻す
			PRINTL 　[2] - 搾精器に改造する(要オナホール ペニスにセットして精液を吸い上げる)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常の搾乳器
					CASE 2
						改造後 = 搾精器
						費用 = 100000
						必要基礎レベル = 7
						必要欲望レベル = 13
						必要サドレベル = 10
				ENDSELECT
			LOOP LOOPRES(1, 2)
		;CASE "ペニスバンド"
		;	改造アイテム = ペニスバンド
		;	PRINTL 　[1] - 通常のペニスバンドに戻す
		;	PRINTL 　[2] - 細くする(苦痛、体力消費量、快感ダウン)
		;	PRINTL 　[3] - 太くする(苦痛、体力消費量アップ 挿入時に拡張効果あり)
		;	PRINTL 　[4] - 双頭ディルド仕様に改造する(装着者も膣で感じることができる)
		;	PRINTL 　[5] - 疑似射精機能を追加する(白濁液を噴出できるようになる)
		;	PRINTL 　[0] - 戻る
		;	DO
		;		INPUT
		;		SELECTCASE RESULT
		;			CASE 0
		;				RESTART
		;			CASE 1
		;				改造後 = 通常のペニスバンド
		;			CASE 2
		;				改造後 = 細いペニスバンド
		;				費用 = 30000
		;				必要基礎レベル = 5
		;			CASE 3
		;				改造後 = 太いペニスバンド
		;				費用 = 50000
		;				必要基礎レベル = 3
		;				必要サドレベル = 5
		;			CASE 4
		;				改造後 = 双頭ペニスバンド
		;				費用 = 100000
		;				必要基礎レベル = 5
		;			CASE 5
		;				改造後 = 擬似射精ペニスバンド
		;				費用 = 250000
		;				必要基礎レベル = 30
		;				必要欲望レベル = 15
		;		ENDSELECT
		;	LOOP LOOPRES(1, 2, 3, 4, 5)
		CASE "鞭"
			改造アイテム = 鞭
			PRINTL 　[1] - 通常の鞭に戻す
			PRINTL 　[2] - 威力を上げる(苦痛、体力消費量アップ)
			PRINTL 　[3] - 電流を流す(苦痛、体力消費量大幅アップ)
			PRINTL 　[4] - スライム鞭にする(スライムが振るうと威力を発揮、改造には配属中のキャラに[スライム]or[溶ける快]を持つキャラが必要)
			PRINTL 　[5] - ヴァンパイアキラーにする(苦痛、体力消費量アップ、吸血鬼、ゾンビへの効果↑↑、獣人、スライムへの効果↑)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常の鞭
					CASE 2
						改造後 = 強力な鞭
						費用 = 50000
						必要基礎レベル = 3
						必要サドレベル = 15
						必要マゾレベル = 10
					CASE 3
						改造後 = 電流鞭
						費用 = 150000
						必要基礎レベル = 25
						必要サドレベル = 25
						必要マゾレベル = 20
					CASE 4
						改造後 = 粘液鞭
						費用 = 100000
						必要基礎レベル = 5
						必要サドレベル = 12
						必要マゾレベル = 12
						必要素質 = スライム
					CASE 5
						改造後 = ヴァンパイアキラー
						費用 = 120000
						必要基礎レベル = 18
						必要サドレベル = 20
						必要マゾレベル = 15
				ENDSELECT
			LOOP LOOPRES(1, 2, 3, 4, 5)
		;CASE "ボールギャグ"
		;	改造アイテム = ボールギャグ
		;	PRINTL 　[1] - 通常のボールギャグに戻す
		;	PRINTL 　[2] - 強制開口器に改造する(強制イラマチオが可能になる)
		;	PRINTL 　[0] - 戻る
		;	DO
		;		INPUT
		;		SELECTCASE RESULT
		;			CASE 0
		;				RESTART
		;			CASE 1
		;				改造後 = 通常のボールギャグ
		;			CASE 2
		;				改造後 = 強制開口器
		;				費用 = 30000
		;				必要基礎レベル = 5
		;				必要サドレベル = 15
		;		ENDSELECT
		;	LOOP LOOPRES(1, 2)
		CASE "大鏡"
			改造アイテム = 大鏡
			PRINTL 　[1] - 通常の大鏡に戻す
			PRINTL 　[2] - 三面鏡にする(羞恥効果アップ)
			PRINTL 　[3] - カメラ仕込みマジックミラーにする(要ビデオカメラ 撮影を同時に行える)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 通常の大鏡
					CASE 2
						改造後 = 三面鏡
						費用 = 200000
						必要基礎レベル = 5
					CASE 3
						改造後 = マジックミラー
						費用 = 500000
						必要基礎レベル = 40
						必要欲望レベル = 20
						必要アイテム = ビデオカメラ
				ENDSELECT
			LOOP LOOPRES(1, 2, 3)
		CASE "首輪"
			改造アイテム = 首輪
			PRINTL 　[1] - 通常の首輪に戻す
			PRINTL 　[2] - 尻尾プラグのオプションを追加する(快Ａボーナスが付く)
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 普通の首輪
					CASE 2
						改造後 = 尻尾プラグ付き首輪
						費用 = 30000
						必要基礎レベル = 10
						必要欲望レベル = 5
				ENDSELECT
			LOOP LOOPRES(1, 2)
		CASE "エプロン"
			改造アイテム = エプロン
			PRINTL 　[1] - 通常のエプロンに戻す
			PRINTL 　[2] - 玄人志向に改造する（料理の美味しさにボーナス）
			PRINTL 　[0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						改造後 = 普通のエプロン
					CASE 2
						改造後 = 玄人志向エプロン
						費用 = 200000
						必要基礎レベル = 15
				ENDSELECT
			LOOP LOOPRES(1, 2)
		;CASE "ローション"
		;	PRINTL 　[1] - 通常のローションに戻す
		;	PRINTL 　[2] - 媚薬入りローションに改造する
		;	PRINTL 　[0] - 戻る
		CASEELSE
			RESTART
	ENDSELECT
	BREAK
LOOP 1

IF SAVESTR:改造アイテム == 改造アイテム
	改造前 = 通常の%改造アイテム%
ELSE
	改造前 = %SAVESTR:改造アイテム%
ENDIF

IF 改造前 == 改造後
	PRINTFORMW 既に%改造後%に改造されています
	GOTO 改造
ELSE
	DRAWLINE
	;浪費癖一人につき改造費2割増し
	費用 += 費用/5*浪費癖人数("実験室")
	PRINTFORML 「%改造前%」→「%改造後%」に改造します
	SIF !ORIGIN
		CALL PASELIL, @"費用:{費用}＄ 現在の所持金:{MONEY}＄"
	PRINTL 
	PRINTL 必要レベル(現在のレベル)
	PRINTFORML 基礎レベル:{必要基礎レベル}({基礎研究レベル}) 欲望レベル:{必要欲望レベル}({欲望研究レベル})
	PRINTFORML サドレベル:{必要サドレベル}({サド研究レベル}) マゾレベル:{必要マゾレベル}({マゾ研究レベル})
	SIF 必要アイテム != ""
		PRINTFORML 必要アイテム:%必要アイテム%
	SIF 必要素質 == "スライム"
		PRINTFORML 必要素質:スライム または 溶ける快
	IF 費用 > MONEY && !ORIGIN
		PRINTW 費用が足りません
		GOTO 改造
	ELSEIF 必要基礎レベル > 基礎研究レベル || 必要欲望レベル > 欲望研究レベル || 必要サドレベル > サド研究レベル || 必要マゾレベル > マゾ研究レベル
		PRINTW レベルが足りません
		GOTO 改造
	ELSEIF 必要アイテム != "" && !ITEM:必要アイテム
		PRINTFORMW %必要アイテム%を所持していません
		GOTO 改造
	ELSEIF 必要素質 == "スライム" && スライム所属 == 0
		PRINTFORMW 必要素質:スライム または 溶ける快を所持している研究者がいません
		GOTO 改造
	ENDIF
	PRINTL よろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
ENDIF
DO
	INPUT
	SIF RESULT == 1
		GOTO 改造
LOOP LOOPRES(0)

PRINTFORMW 「%改造前%」を「%改造後%」に改造しました
MONEY -= 費用
CALL 現金出納帳記録, @"アイテムの改造/-{費用}"
SIF 必要アイテム != ""
	ITEM:必要アイテム = 0
;「通常の」はそのままアイテム名を入れる
IF SUBSTRINGU(改造後, 0, 3) == "通常の"
	SAVESTR:改造アイテム = %改造アイテム%
ELSE
	SAVESTR:改造アイテム = %改造後%
ENDIF

FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 != "実験室" || !CFLAG:LCOUNT:配属完了
		CONTINUE
	EXP:LCOUNT:工作経験 += (必要基礎レベル+必要欲望レベル+必要サドレベル+必要マゾレベル)/4
	JUEL:LCOUNT:習得 += (必要基礎レベル+必要欲望レベル+必要サドレベル+必要マゾレベル)*100
NEXT

GOTO 改造

@実験室レベル計算
#DIM DYNAMIC LCOUNT

基礎研究レベル = 0
欲望研究レベル = 0
サド研究レベル = 0
マゾ研究レベル = 0
スライム所属 = 0

FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 != "実験室" || !CFLAG:LCOUNT:配属完了
		CONTINUE
	IF 統率者("実験室") && !TALENT:LCOUNT:統率
		;基礎研究レベル まず工作技能を入れる
		基礎研究レベル += ABL:LCOUNT:工作技能+1
		;欲望研究レベル (工作技能+欲望)/2
		欲望研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:欲望+1)/2
		;サド研究レベル (工作技能+サドっ気)/2
		サド研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:サドっ気+1)/2
		;マゾ研究レベル (工作技能+マゾっ気)/2
		マゾ研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:マゾっ気+1)/2
	ELSE
		;基礎研究レベル まず工作技能を入れる
		基礎研究レベル += ABL:LCOUNT:工作技能
		;欲望研究レベル (工作技能+欲望)/2
		欲望研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:欲望)/2
		;サド研究レベル (工作技能+サドっ気)/2
		サド研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:サドっ気)/2
		;マゾ研究レベル (工作技能+マゾっ気)/2
		マゾ研究レベル += (ABL:LCOUNT:工作技能+ABL:LCOUNT:マゾっ気)/2
	ENDIF
	;素質補正
	IF TALENT:LCOUNT:工学博士
		基礎研究レベル += 2
		欲望研究レベル += 2
		サド研究レベル += 2
		マゾ研究レベル += 2
	ENDIF
	SIF TALENT:LCOUNT:淫乱
		欲望研究レベル += 2
	SIF TALENT:LCOUNT:娼婦
		欲望研究レベル += 3
	SIF TALENT:LCOUNT:サド
		サド研究レベル += 2
	SIF TALENT:LCOUNT:マゾ
		マゾ研究レベル += 2
	IF TALENT:LCOUNT:倒錯的
		サド研究レベル++
		マゾ研究レベル++
	ENDIF
	SIF TALENT:LCOUNT:調合知識
		基礎研究レベル++
	IF 特質("ワーカホリック", LCOUNT)
		基礎研究レベル += 特質("ワーカホリック", LCOUNT)/10
		欲望研究レベル += 特質("ワーカホリック", LCOUNT)/10
		サド研究レベル += 特質("ワーカホリック", LCOUNT)/10
		マゾ研究レベル += 特質("ワーカホリック", LCOUNT)/10
	ENDIF
	SIF TALENT:LCOUNT:スライム || TALENT:LCOUNT:溶ける快
		スライム所属 = 1
NEXT

;各値最大50
CALL SETMIN, 基礎研究レベル, 50
CALL SETMIN, 欲望研究レベル, 50
CALL SETMIN, サド研究レベル, 50
CALL SETMIN, マゾ研究レベル, 50

RETURN

@TURNEND_実験室
#DIM DYNAMIC LCOUNT

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "実験室" && !CFLAG:LCOUNT:口上非表示
		TRYCALLFORM TURNEND_実験室_%CSVNAMERPS(NO:LCOUNT)%
NEXT

;以下の処理は日付変更時のみ
SIF TIME == 0
	RETURN

FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "実験室"
		CFLAG:LCOUNT:配属完了 = 1
NEXT

@MANUAL_実験室
DRAWLINE
PRINTL □実験室について
PRINTL 　購入済みのアイテムを改造して効果を高めることができます
PRINTL 　改造したアイテムは名前が変わり、調教中に使用することが可能です
PRINTL 　アイテム改造には相応の人員と費用が必要です。よりアブノーマルな効果を施すには、工作技能の他に、
PRINTL 　より高い[欲望][サドっ気][マゾっ気]の能力が求められます
PRINTL 　まずはアイテムを購入するところから始めてみましょう
DRAWLINE
PRINTL　　実験室
WAIT
