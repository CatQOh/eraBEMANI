@ロボットファクトリー
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET

PRINTL □ここではロボットの制作ができます
PRINT 　
FOR LCOUNT, GETNUM(ITEM, "マテリアル"), GETNUM(ITEM, "マテリアル")+10
	IF ITEM:LCOUNT
		SIF LINEISEMPTY()
			PRINT 　
		PRINTFORM %ITEMNAME:LCOUNT%×{ITEM:LCOUNT} 
	ENDIF
NEXT
SIF !LINEISEMPTY()
	PRINTL 
PRINTL 
CALL 実験室レベル計算
PRINTFORML □研究レベル:{基礎研究レベル}/50 欲望レベル:{欲望研究レベル}/50
PRINTFORML 　サディストレベル:{サド研究レベル}/50 マゾヒストレベル:{マゾ研究レベル}/50

PRINTL [1] - ロボットを制作する
PRINTL [2] - 既存のロボを調整する
SIF STSMOD
	PRINTL [3] - ロボットを廃棄する
DRAWLINE
PRINTPLAIN 　ロボットファクトリー　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			LTARGET = TARGET
			CALL ADD_ランダムキャラ, RAND(1, 3), -1, 1
			TARGET = CHARANUM-1
			CSTR:種族 = 機械
			TALENT:無機物 = 1
			MAXBASE:体力 = 1500
			MAXBASE:気力 = 1500
			VARSET TALENT:0
			CALL ロボット制作
			SIF !RESULT
				TARGET = LTARGET
			RESTART
		CASE 2
			;CALL ロボット調整
		CASE 3
			SIF STSMOD
				CALL STS_MACHINE
			RESTART
	ENDSELECT
LOOP 1

@ロボット制作
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC アイテムあり
;余分に要素数を取っておく
#DIM DYNAMIC 素質変更可能, 300
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 製作コスト

;選択可能な素質とコストを設定する
VARSET 素質変更可能
製作コスト = 0
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		;コスト-1
		CASE "小柄体型"
			素質変更可能 = -1
			SIF TALENT:LCOUNT
				製作コスト -= 1
		;コスト1
		{
		CASE "生意気", "臆病", "素直", "反抗的", "ハイテンション", "大人しい", "世話好き",
			 "浪費癖", "無関心", "好奇心", "痛みに弱い", "習得遅い", "潔癖症", "汚れ無視",
			 "快感の否定", "倒錯的", "男好き", "女好き", "サド", "マゾ", "小悪魔", "遊び人",
			 "Ｃ鈍感", "Ｖ鈍感", "Ａ鈍感", "Ｂ鈍感", "動物耳", "回復遅い", "鼓舞", "幼稚", 
		}
			素質変更可能:LCOUNT = 1
			SIF TALENT:LCOUNT
				製作コスト += 1
		;コスト2
		{
		CASE "痛みに強い", "習得早い", "舌使い", "買い物上手", "献身的", "快感に素直", "魅力", "魅惑",
			 "統率", "Ｃ敏感", "Ｖ敏感", "Ａ敏感", "Ｂ敏感", "ふたなり", "回復早い", "治療", "孔雀の瞳"
		}
			素質変更可能 = 2
			SIF TALENT:LCOUNT
				製作コスト += 2
		;コスト3
		CASE "調合知識", "謎の魅力", "ネクロマンサー", "禁断の知識"
			素質変更可能 = 3
			SIF TALENT:LCOUNT
				製作コスト += 3
	ENDSELECT
NEXT

PRINTL □ロボットを制作します
FOR LCOUNT, GETNUM(ITEM, "マテリアル"), GETNUM(ITEM, "マテリアル")+10
	IF ITEM:LCOUNT
		SIF LINEISEMPTY()
			PRINT 　
		PRINTFORM %ITEMNAME:LCOUNT%×{ITEM:LCOUNT} 
		SIF ITEMNAME:LCOUNT != "マテリアル"
			アイテムあり = 1
	ENDIF
NEXT
SIF !LINEISEMPTY()
	PRINTL 
PRINTL 
PRINTL □完成後の性能
PRINTFORML 性別:\@ TALENT:性別 == 1 ? 男性 # 女性 \@ [8] 変更する
PRINTFORML 体力:{MAXBASE:体力} 気力:{MAXBASE:気力} [-9] -300/コスト-1 [9] +300/コスト+1
;初期値は1500 300ごとにコスト1上がる
製作コスト += (MAXBASE:体力-1500)/300
PRINT 素質:
FOR LCOUNT, 0, VARSIZE("TALENT")
	SELECTCASE TALENTNAME:LCOUNT
		CASE "性別", "恋心", "既婚者", "バストサイズ"
		CASEELSE
			SIF TALENT:LCOUNT
				PRINTFORM [%TALENTNAME:LCOUNT%]
	ENDSELECT
NEXT
PRINTL 
CALL PASELIL, @"必要製作コスト:{製作コスト*30000}＄"
PRINTL 
PRINTL 　[1] - 素質を変更する
PRINTL 　[2] - 素質をプリセットから選ぶ
SIF アイテムあり
	PRINTL 　[3] - 部品を使用する
DRAWLINE
PRINTPLAIN 　ロボットファクトリー　
PRINTL [0] - 戻る [10] - 作成する

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			$TALENT
			FOR LCOUNT, 0, VARSIZE("TALENT")
				SIF 素質変更可能:LCOUNT
					PRINTFORMLC [{LCOUNT, 3}] %TALENTNAME:LCOUNT%(コスト:{素質変更可能:LCOUNT})
			NEXT
			SIF !LINEISEMPTY()
				PRINTL 
			SIF TALENT:性別 == 2
				PRINTFORML [{TALENTF("バストサイズ")}] バストサイズ:%バストサイズ(TALENT:バストサイズ)%
			DRAWLINE
			PRINTPLAIN 　ロボットファクトリー　
			PRINTL [-1] - 戻る
			DO
				INPUT
				SIF RESULT == -1
					RESTART
				LRESULT = RESULT
				IF INRANGE(LRESULT, 0, VARSIZE("TALENT"))
					IF TALENTNAME:LRESULT == "バストサイズ" && TALENT:性別 == 2
						PRINTL [1] 絶壁にする
						PRINTL [2] 貧乳にする
						PRINTL [3] 並乳にする
						PRINTL [4] 巨乳にする
						PRINTL [5] 豊乳にする
						PRINTL [6] 爆乳にする
						INPUT
						DO
						LOOP LOOPRES(1, 2, 3, 4, 5, 6)
						TALENT:バストサイズ = RESULT
						GOTO TALENT
					ELSEIF 素質変更可能:LRESULT
						INVERTBIT TALENT:LRESULT, 0
						GOTO TALENT
					ENDIF
				ENDIF
			LOOP 1
			RESTART
		CASE 2
			PRINTL プリセットを選択してください
			PRINTL [1] - 従順
			PRINTL [2] - 家事手伝い
			PRINTL [3] - セクサロイド
			PRINTL [4] - ポンコツ
			PRINTL [0] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1
						VARSET TALENT:0
						TALENT:素直 = 1
						TALENT:快感に素直 = 1
						TALENT:大人しい = 1
						TALENT:献身的 = 1
						TALENT:汚れ無視 = 1
						RESTART
					CASE 2
						VARSET TALENT:0
						TALENT:世話好き = 1
						TALENT:習得早い = 1
						TALENT:買い物上手 = 1
						TALENT:潔癖症 = 1
						TALENT:鼓舞 = 1
						RESTART
					CASE 3
						VARSET TALENT:0
						TALENT:好奇心 = 1
						TALENT:汚れ無視 = 1
						TALENT:回復早い = 1
						TALENT:男好き = 1
						TALENT:女好き = 1
						TALENT:舌使い = 1
						TALENT:小悪魔 = 1
						TALENT:倒錯的 = 1
						RESTART
					CASE 4
						VARSET TALENT:0
						TALENT:反抗的 = 1
						TALENT:回復遅い = 1
						TALENT:浪費癖 = 1
						TALENT:痛みに弱い = 1
						TALENT:習得遅い = 1
						TALENT:遊び人 = 1
						RESTART
				ENDSELECT
			LOOP 1
		CASE 8
			SELECTCASE TALENT:性別
				CASE 1
					TALENT:性別 = 2
				CASE 2
					TALENT:性別 = 1
			ENDSELECT
		CASE -9
			SIF MAXBASE:体力 <= 1500
				CONTINUE
			MAXBASE:体力 -= 300
			MAXBASE:気力 -= 300
			RESTART
		CASE 9
			MAXBASE:体力 += 300
			MAXBASE:気力 += 300
			RESTART
	ENDSELECT
LOOP 1









@STS_MACHINE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象
#DIM DYNAMIC 入手数

DRAWLINE

PRINT □解体するキャラを選んでください。
FONTBOLD
PRINTL 解体したキャラはロストします
FONTREGULAR

表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF !TALENT:LCOUNT:無機物
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+20)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT% 回収できる物:
	IF TALENT:LCOUNT:小柄体型
		PRINT マテリアル3個 
	ELSE
		PRINT マテリアル5個 
	ENDIF

	入手数 = ABL:LCOUNT:Ｃ感覚+ABL:LCOUNT:Ｖ感覚+ABL:LCOUNT:Ａ感覚+ABL:LCOUNT:Ｂ感覚
	SIF 入手数
		PRINTFORM 性感センサー{入手数}個 

	SELECTCASE FALLTYPE(LCOUNT)
		CASE 1
			PRINT 感情再現デバイス（恋慕） 
		CASE 2
			PRINT 感情再現デバイス（淫乱） 
		CASE 3
			PRINT 感情再現デバイス（服従） 
	ENDSELECT

	入手数 = ABL:LCOUNT:従順+ABL:LCOUNT:欲望+ABL:LCOUNT:技巧+ABL:LCOUNT:奉仕精神
	SIF 入手数
		PRINTFORM 交友インターフェイス{入手数}個 

	入手数 = ABL:LCOUNT:自慰中毒+ABL:LCOUNT:精液中毒+ABL:LCOUNT:レズ中毒+ABL:LCOUNT:ゲイ中毒+ABL:LCOUNT:性交中毒+ABL:LCOUNT:噴乳中毒
	SIF 入手数
		PRINTFORM 制御不能基板{入手数}個 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　解体可能なキャラは居ません
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　解体.{ページ数+1}　
IF ページ数 > 0
	PRINT [-1] - 前ページ
ELSE
	PRINTPLAIN                
ENDIF
PRINT [0] - 戻る
SIF 次ページあり
	PRINT [-9] - 次ページ
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE -1
			SIF ページ数 > 0
				ページ数--
			RESTART
		CASE -9
			SIF 次ページあり
				ページ数++
			RESTART
		CASE 1 TO CHARANUM
			対象 = RESULT
			SIF !TALENT:対象:無機物
				CONTINUE
			PRINTFORML %NAME:対象%を解体します。よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			DO
				INPUT
				SIF RESULT == 1
					RESTART
			LOOP LOOPRES(0)
			DRAWLINE
			RESETCOLOR
			PRINTFORML %NAME:対象%を解体しました
			IF TALENT:対象:小柄体型
				PRINTL マテリアル+3
				ITEM:マテリアル += 3
			ELSE
				PRINTL マテリアル+5
				ITEM:マテリアル += 5
			ENDIF

			入手数 = ABL:対象:Ｃ感覚+ABL:対象:Ｖ感覚+ABL:対象:Ａ感覚+ABL:対象:Ｂ感覚
			IF 入手数
				PRINTFORML 性感センサー+{入手数}
				ITEM:性感センサー += 入手数
			ENDIF

			SELECTCASE FALLTYPE(対象)
				CASE 1
					PRINTL 感情再現デバイス（恋慕）+1
					ITEM:感情再現デバイス（恋慕）++
				CASE 2
					PRINTL 感情再現デバイス（淫乱）+1
					ITEM:感情再現デバイス（淫乱）++
				CASE 3
					PRINTL 感情再現デバイス（服従）+1
					ITEM:感情再現デバイス（服従）++
			ENDSELECT

			入手数 = ABL:対象:従順+ABL:対象:欲望+ABL:対象:技巧+ABL:対象:奉仕精神
			IF 入手数
				PRINTFORML 交友インターフェイス+{入手数}
				ITEM:交友インターフェイス += 入手数
			ENDIF

			入手数 = ABL:対象:自慰中毒+ABL:対象:精液中毒+ABL:対象:レズ中毒+ABL:対象:ゲイ中毒+ABL:対象:性交中毒+ABL:対象:噴乳中毒
			IF 入手数
				PRINTFORML 制御不能基板+{入手数}
				ITEM:制御不能基板 += 入手数
			ENDIF

			IF NO:対象 < 10000 && !分析データ:(NO:対象)
				PRINTFORML %CALLNAME:対象%の構成データを解析できるようになりました
				分析データ:(NO:対象) = 1
			ENDIF
			WAIT
			CALL CLEAR_CHARA, 対象
			RESTART
	ENDSELECT
LOOP 1
