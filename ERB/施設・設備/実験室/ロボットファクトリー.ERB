@ロボットファクトリー

PRINTL □ここではロボットの制作ができます
PRINTFORML 　所持マテリアル:{ITEM:マテリアル} (製作時にお金の代わりに使用可能)
CALL 実験室レベル計算
PRINTFORML □研究レベル:{基礎研究レベル}/50 欲望レベル:{欲望研究レベル}/50
PRINTFORML 　サディストレベル:{サド研究レベル}/50 マゾヒストレベル:{マゾ研究レベル}/50

PRINTL [1] - ロボットを制作する
PRINTL [2] - 既存のロボを調整する
SIF STSMOD
	PRINTL [3] - ロボットを廃棄する
DRAWLINE
PRINTPLAIN 　ロボットファクトリー　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 3
			CALL STS_MACHINE
			RESTART
	ENDSELECT
LOOP 1


@STS_MACHINE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 飛ばした数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 対象
#DIM DYNAMIC 入手数

DRAWLINE

PRINT □解体するキャラを選んでください。
FONTBOLD
PRINTL 解体したキャラはロストします
FONTREGULAR

表示数 = 0
次ページあり = 0
FOR LCOUNT, 1, CHARANUM
	SIF !TALENT:LCOUNT:無機物
		CONTINUE
	表示数++
	IF 表示数 > ページ数*20+20
		次ページあり = 1
		BREAK
	ENDIF
	SIF !INRANGE(表示数, ページ数*20, ページ数*20+20)
		CONTINUE
	PRINTFORM 　[{LCOUNT, 2}] %NAME:LCOUNT, 26, LEFT% 回収できる物:
	IF TALENT:LCOUNT:小柄体型
		PRINT マテリアル3個 
	ELSE
		PRINT マテリアル5個 
	ENDIF

	入手数 = ABL:LCOUNT:Ｃ感覚+ABL:LCOUNT:Ｖ感覚+ABL:LCOUNT:Ａ感覚+ABL:LCOUNT:Ｂ感覚
	SIF 入手数
		PRINTFORM 性感センサー{入手数}個 

	SELECTCASE FALLTYPE(LCOUNT)
		CASE 1
			PRINT 感情再現デバイス（恋慕） 
		CASE 2
			PRINT 感情再現デバイス（淫乱） 
		CASE 3
			PRINT 感情再現デバイス（服従） 
	ENDSELECT

	入手数 = ABL:LCOUNT:従順+ABL:LCOUNT:欲望+ABL:LCOUNT:技巧+ABL:LCOUNT:奉仕精神
	SIF 入手数
		PRINTFORM 交友インターフェイス{入手数}個 

	入手数 = ABL:LCOUNT:自慰中毒+ABL:LCOUNT:精液中毒+ABL:LCOUNT:レズ中毒+ABL:LCOUNT:ゲイ中毒+ABL:LCOUNT:性交中毒+ABL:LCOUNT:噴乳中毒
	SIF 入手数
		PRINTFORM 制御不能基板{入手数}個 
NEXT
IF 表示数 == 0
	PRINTL 
	PRINTL 　解体可能なキャラは居ません
	PRINTL 
ENDIF

DRAWLINE
PRINTPLAINFORM 　解体.{ページ数+1}　
IF ページ数 > 0
	PRINT [-1] - 前ページ
ELSE
	PRINTPLAIN                
ENDIF
PRINT [0] - 戻る
SIF 次ページあり
	PRINT [-9] - 次ページ
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE -1
			SIF ページ数 > 0
				ページ数--
			RESTART
		CASE -9
			SIF 次ページあり
				ページ数++
			RESTART
		CASE 1 TO CHARANUM
			対象 = RESULT
			SIF !TALENT:対象:無機物
				CONTINUE
			PRINTFORML %NAME:対象%を解体します。よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			DO
				INPUT
				SIF RESULT == 1
					RESTART
			LOOP LOOPRES(0)
			DRAWLINE
			RESETCOLOR
			PRINTFORML %NAME:対象%を解体しました
			IF TALENT:対象:小柄体型
				PRINTL マテリアル+3
				ITEM:マテリアル += 3
			ELSE
				PRINTL マテリアル+5
				ITEM:マテリアル += 5
			ENDIF

			入手数 = ABL:対象:Ｃ感覚+ABL:対象:Ｖ感覚+ABL:対象:Ａ感覚+ABL:対象:Ｂ感覚
			IF 入手数
				PRINTFORML 性感センサー+{入手数}
				ITEM:性感センサー += 入手数
			ENDIF

			SELECTCASE FALLTYPE(対象)
				CASE 1
					PRINTL 感情再現デバイス（恋慕）+1
					ITEM:感情再現デバイス（恋慕）++
				CASE 2
					PRINTL 感情再現デバイス（淫乱）+1
					ITEM:感情再現デバイス（淫乱）++
				CASE 3
					PRINTL 感情再現デバイス（服従）+1
					ITEM:感情再現デバイス（服従）++
			ENDSELECT

			入手数 = ABL:対象:従順+ABL:対象:欲望+ABL:対象:技巧+ABL:対象:奉仕精神
			IF 入手数
				PRINTFORML 交友インターフェイス+{入手数}
				ITEM:交友インターフェイス += 入手数
			ENDIF

			入手数 = ABL:対象:自慰中毒+ABL:対象:精液中毒+ABL:対象:レズ中毒+ABL:対象:ゲイ中毒+ABL:対象:性交中毒+ABL:対象:噴乳中毒
			IF 入手数
				PRINTFORML 制御不能基板+{入手数}
				ITEM:制御不能基板 += 入手数
			ENDIF

			IF NO:対象 < 10000 && !分析データ:(NO:対象)
				PRINTFORML %CALLNAME:対象%の構成データを解析できるようになりました
				分析データ:(NO:対象) = 1
			ENDIF
			WAIT
			CALL CLEAR_CHARA, 対象
			RESTART
	ENDSELECT
LOOP 1
