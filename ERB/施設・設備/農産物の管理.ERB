@農産物
DRAWLINE
PRINTL □農産物の管理
IF ゲームモード() == "新婚モード"
	PRINTFORML 　現在の設備 田んぼ収穫日:{ITEM:田んぼ}/{田んぼ上限} 畑収穫日:{ITEM:畑}/{畑上限} プランター収穫日:{ITEM:プランター}/4 栽培レベル:{栽培レベル()}
ELSE
	PRINTFORML 　現在の設備 田んぼ収穫日:{ITEM:田んぼ}/{田んぼ上限} 畑収穫日:{ITEM:畑}/{畑上限} プランター収穫日:{ITEM:プランター}/4
	PRINTFORML 　現在の人員 配属人数:{CMATCH(CSTR:配属, "農業")}/{ITEM:畑+ITEM:田んぼ*3+(ITEM:プランター > 0)} 栽培レベル:{栽培レベル()}
ENDIF
PRINTL 
IF ITEM:田んぼ || ITEM:畑 || ITEM:プランター
	PRINTL 　[1] - 作物を植える
	SIF ゲームモード() != "新婚モード"
		PRINTL 　[2] - 人員を配置する
ELSE
	SETCOLORBYNAME gray
	HTML_PRINT SET_TOOLTIP("　[-] - 作物を植える", "農業用の設備を導入する必要があります")
	HTML_PRINT SET_TOOLTIP("　[-] - 人員を配置する", "農業用の設備を導入する必要があります")
	RESETCOLOR
ENDIF
PRINTL 　[3] - 生産状況を確認する
PRINTL 　[4] - 設備を導入する
PRINTL 　[5] - 農業の説明
DRAWLINE
PRINTPLAIN 　農業　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			SIF ITEM:田んぼ || ITEM:畑 || ITEM:プランター
				CALL 作物を植える
			RESTART
		CASE 2
			SIF (ITEM:田んぼ || ITEM:畑 || ITEM:プランター) && ゲームモード() != "新婚モード"
				CALL STRATEGY_PERSONAL, "農業"
			RESTART
		CASE 3
			CALL 生産状況_農業
			RESTART
		CASE 4
			CALL 農業設備導入
			RESTART
		CASE 5
			CALL MANUAL_農業
			RESTART
		CASE 0
			RETURN
	ENDSELECT
LOOP 1

@作物を植える
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 作物
#DIM DYNAMIC LRESULT

;成長速度補正は栽培技能合計*2+魔法技能合計+工作技能合計
;作物の基礎成長日数-成長速度補正が収穫できるまでの日数。最短で3日

DRAWLINE
PRINTL □栽培に使う土地を選んでください
SETCOLORBYNAME gray
IF ITEM:プランター
	PRINTFORMDL [1] - プランター (現在{ITEM:プランター}個所持)
ELSE
	PRINTL [-] - プランター (未所持)
ENDIF
IF ITEM:畑
	PRINTFORMDL [2] - 畑 (現在{ITEM:畑}枚所持)
ELSE
	PRINTL [-] - 畑 (未所持)
ENDIF
IF ITEM:田んぼ
	PRINTFORMDL [3] - 田んぼ (現在{ITEM:田んぼ}枚所持)
ELSE
	PRINTL [-] - 田んぼ (未所持)
ENDIF
RESETCOLOR
DRAWLINE
PRINTPLAIN 　作物を植える　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			SIF ITEM:プランター
				GOTO プランター
		CASE 2
			SIF ITEM:畑
				GOTO 畑
		CASE 3
			SIF ITEM:田んぼ
				GOTO 田んぼ
	ENDSELECT
LOOP 1

$プランター
DRAWLINE
PRINTL □使用するプランターを選んでください
FOR LCOUNT, 1, 5
	IF ITEM:プランター < LCOUNT
		SETCOLORBYNAME gray
		PRINTL 　[-] - プランター未所持
		RESETCOLOR
	ELSE
		PRINTFORM 　[{LCOUNT}] - 
		IF プランターの作物:LCOUNT == ""
			PRINTL 空のプランター
		ELSE
			PRINTFORML %プランターの作物:LCOUNT%のプランター\@ プランター収穫日:LCOUNT == 0 ? (収穫可能) # (残り{プランター収穫日:LCOUNT}日で収穫可能) \@
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　プランター　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RESTART
		CASE 1 TO 4
			SIF RESULT > ITEM:プランター
				CONTINUE
			LRESULT = RESULT
			IF プランターの作物:RESULT != ""
				PRINTFORML {LRESULT}番のプランターでは%プランターの作物:LRESULT%を栽培しています。新しく別のものを栽培しますか？
				PRINTL [0] - 新しいものに挑戦したい
				PRINTL [1] - 考え直す
				DO
					INPUT
					SIF RESULT == 1
						GOTO プランター
				LOOP LOOPRES(0)
			ENDIF
			DRAWLINE
			PRINTFORML □{LRESULT}番プランターに何を植えますか？
			IF !ITEM:600
				PRINTL 
				PRINTL 【育てられる作物がありません】
				PRINTL 
			ELSE
				FOR LCOUNT, 600, 650
					SIF ITEM:LCOUNT
						PRINTFORML [{LCOUNT-600, 2}] - %ITEMNAME:LCOUNT%(収穫目安 およそ{MAX(ITEMPRICE:LCOUNT-成長速度補正(), 3)}日)
				NEXT
			ENDIF
			DRAWLINE
			PRINTPLAIN 　プランター　
			PRINTL [100] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 100
						GOTO プランター
					CASEELSE
						作物 = RESULT+600
						IF ITEM:作物
							PRINTFORML {LRESULT}番プランターに%ITEMNAME:作物%を植えました
							プランター収穫日:LRESULT = MAX(ITEMPRICE:作物-成長速度補正(), 3)
							プランターの作物:LRESULT = %ITEMNAME:作物%
							GOTO プランター
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1

$畑
DRAWLINE
PRINTL □使用する畑を選んでください
FOR LCOUNT, 1, 9
	IF ITEM:畑 < LCOUNT
		SETCOLORBYNAME gray
		PRINTL 　[-] - 畑未所持
		RESETCOLOR
	ELSE
		PRINTFORM 　[{LCOUNT}] - 
		IF 畑の作物:LCOUNT == ""
			PRINTL 未使用の畑
		ELSE
			PRINTFORML %畑の作物:LCOUNT%の畑\@ 畑収穫日:LCOUNT == 0 ? (収穫可能) # (残り{畑収穫日:LCOUNT}日で収穫可能) \@
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　畑　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RESTART
		CASE 1 TO 8
			SIF RESULT > ITEM:畑
				CONTINUE
			LRESULT = RESULT
			IF 畑の作物:RESULT != ""
				PRINTFORML {LRESULT}番の畑では%畑の作物:LRESULT%を栽培しています。新しく別のものを栽培しますか？
				PRINTL [0] - 新しいものに挑戦したい
				PRINTL [1] - 考え直す
				DO
					INPUT
					SIF RESULT == 1
						GOTO 畑
				LOOP LOOPRES(0)
			ENDIF
			DRAWLINE
			PRINTFORML □{LRESULT}番の畑に何を植えますか？
			IF !ITEM:650
				PRINTL 
				PRINTL 【育てられる作物がありません】
				PRINTL 
			ELSE
				FOR LCOUNT, 650, 700
					SIF ITEM:LCOUNT
						PRINTFORML [{LCOUNT-650, 2}] - %ITEMNAME:LCOUNT%(収穫目安 およそ{MAX(ITEMPRICE:LCOUNT-成長速度補正(), 3)}日)
				NEXT
			ENDIF
			DRAWLINE
			PRINTPLAIN 　畑　
			PRINTL [100] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 100
						GOTO 畑
					CASEELSE
						作物 = RESULT+650
						IF ITEM:作物
							PRINTFORML {LRESULT}番の畑に%ITEMNAME:作物%を植えました
							畑収穫日:LRESULT = MAX(ITEMPRICE:作物-成長速度補正(), 3)
							畑の作物:LRESULT = %ITEMNAME:作物%
							GOTO 畑
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1

$田んぼ
DRAWLINE
PRINTL □使用する田んぼを選んでください
FOR LCOUNT, 1, 3
	IF ITEM:田んぼ < LCOUNT
		SETCOLORBYNAME gray
		PRINTL 　[-] - 田んぼ未所持
		RESETCOLOR
	ELSE
		PRINTFORM 　[{LCOUNT}] - 
		IF 田んぼの作物:LCOUNT == ""
			PRINTL 未使用の田んぼ
		ELSE
			PRINTFORML %田んぼの作物:LCOUNT%の田んぼ\@ 田んぼ収穫日:LCOUNT == 0 ? (収穫可能) # (残り{田んぼ収穫日:LCOUNT}日で収穫可能) \@
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTPLAIN 　田んぼ　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RESTART
		CASE 1 TO 2
			SIF RESULT > ITEM:田んぼ
				CONTINUE
			LRESULT = RESULT
			IF 田んぼの作物:RESULT != ""
				PRINTFORML {LRESULT}番の田んぼでは%田んぼの作物:LRESULT%を栽培しています。新しく別のものを栽培しますか？
				PRINTL [0] - 新しいものに挑戦したい
				PRINTL [1] - 考え直す
				DO
					INPUT
					SIF RESULT == 1
						GOTO 田んぼ
				LOOP LOOPRES(0)
			ENDIF
			DRAWLINE
			PRINTFORML □{LRESULT}番の田んぼに何を植えますか？
			IF !ITEM:700
				PRINTL 
				PRINTL 【育てられる作物がありません】
				PRINTL 
			ELSE
				FOR LCOUNT, 700, 750
					SIF ITEM:LCOUNT
						PRINTFORML [{LCOUNT-700}] - %ITEMNAME:LCOUNT%(収穫目安 およそ{MAX(ITEMPRICE:LCOUNT-成長速度補正(), 3)}日)
				NEXT
			ENDIF
			DRAWLINE
			PRINTPLAIN 　田んぼ　
			PRINTL [100] - 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 100
						GOTO 田んぼ
					CASEELSE
						作物 = RESULT+700
						IF ITEM:作物
							PRINTFORML {LRESULT}番の田んぼに%ITEMNAME:作物%を植えました
							田んぼ収穫日:LRESULT = MAX(ITEMPRICE:作物-成長速度補正(), 3)
							田んぼの作物:LRESULT = %ITEMNAME:作物%
							GOTO 田んぼ
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1


@生産状況_農業
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 名前文字数
#DIM DYNAMIC LTARGET
DRAWLINE

PRINTFORML □農業の人員:{CMATCH(CSTR:配属, "農業")}/{ITEM:畑+ITEM:田んぼ*3+(ITEM:プランター > 0)}

;新婚モード
IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
	CSTR:MASTER:配属 = 農業
	CSTR:FINDCHARA(CFLAG:嫁, 1):配属 = 農業
ENDIF

IF CMATCH(CSTR:配属, "農業")
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "農業"
			CALL SETMAX(名前文字数, STRLENS(NAME:LCOUNT))
	NEXT
	LTARGET = TARGET
	FOR LCOUNT, 0, CHARANUM
		IF CSTR:LCOUNT:配属 == "農業"
			TARGET = LCOUNT
			PRINTFORM 　%NAME:LCOUNT, 名前文字数, LEFT% 栽培技能:{ABL:LCOUNT:栽培技能, 2, LEFT} 魔法技能:{ABL:LCOUNT:魔法技能, 2, LEFT} 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} 
			SIF 統率者("農業") && !TALENT:LCOUNT:統率
				PRINT 統率者ボーナス+1 
			CALL ISTALENT, LCOUNT, "統率"
			CALL ISTALENT, LCOUNT, "植生学"
			SIF 特質("ワーカホリック", LCOUNT)
				PRINTFORM [ワーカホリック+{特質("ワーカホリック", LCOUNT)/10}]
			CALL NEED_JUEL_栽培技能
			CALL CHECK_JUEL_栽培技能
			SIF RESULT && ABL:LCOUNT:栽培技能 < 10
				PRINTBUTTON "[栽培技能上昇可能]", LCOUNT+1
			PRINTL 
		ENDIF
	NEXT
	TARGET = LTARGET
ELSE
	PRINTL 　[無し]
ENDIF
PRINTL 

PRINTL 　[-1] 植物図鑑を見る
PRINTL 

PRINTL □現在の作物
PRINTL └プランター
FOR LCOUNT, 1, 5
	IF ITEM:プランター < LCOUNT
		SETCOLORBYNAME gray
		PRINTFORMLC 　{LCOUNT}番:未所持
		RESETCOLOR
	ELSE
		IF プランターの作物:LCOUNT != ""
			PRINTFORMLC 　{LCOUNT}番:%プランターの作物:LCOUNT%(\@ プランター収穫日:LCOUNT == 0 ? 収穫可能 # 収穫まで{プランター収穫日:LCOUNT}日 \@)
		ELSE
			PRINTFORMLC 　{LCOUNT}番:無し
		ENDIF
	ENDIF
NEXT
PRINTL 

PRINTL └畑
FOR LCOUNT, 1, 9
	SIF LCOUNT == 5
		PRINTL 
	IF ITEM:畑 < LCOUNT
		SETCOLORBYNAME gray
		PRINTFORMLC 　{LCOUNT}番:未所持
		RESETCOLOR
	ELSE
		IF 畑の作物:LCOUNT != ""
			PRINTFORMLC 　{LCOUNT}番:%畑の作物:LCOUNT%(\@ 畑収穫日:LCOUNT == 0 ? 収穫可能 # 収穫まで{畑収穫日:LCOUNT}日 \@)
		ELSE
			PRINTFORMLC 　{LCOUNT}番:無し
		ENDIF
	ENDIF
NEXT
PRINTL 

PRINTL └田んぼ
FOR LCOUNT, 1, 3
	IF ITEM:田んぼ < LCOUNT
		SETCOLORBYNAME gray
		PRINTFORMLC 　{LCOUNT}番:未所持
		RESETCOLOR
	ELSE
		IF 田んぼの作物:LCOUNT != ""
			PRINTFORMLC 　{LCOUNT}番:%田んぼの作物:LCOUNT%(\@ 田んぼ収穫日:LCOUNT == 0 ? 収穫可能 # 収穫まで{田んぼ収穫日:LCOUNT}日 \@)
		ELSE
			PRINTFORMLC 　{LCOUNT}番:無し
		ENDIF
	ENDIF
NEXT

PRINTL 
DRAWLINE
PRINTPLAIN 　生産状況　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			;新婚モード
			IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
				CSTR:MASTER:配属 = 
				CSTR:FINDCHARA(CFLAG:嫁, 1):配属 = 
			ENDIF
			RETURN
		CASE -1
			CALL 植物図鑑
			RESTART
		CASEELSE
			LTARGET = TARGET
			TARGET = RESULT-1
			IF CSTR:配属 == "農業"
				CALL NEED_JUEL_栽培技能
				CALL CHECK_JUEL_栽培技能
				IF RESULT
					CALLFORM ABLUP{GETNUM(ABL, "栽培技能")}
					TARGET = LTARGET
					RESTART
				ENDIF
			ENDIF
			TARGET = LTARGET
	ENDSELECT
LOOP 1

@栽培解禁
#DIM DYNAMIC LCOUNT

IF 栽培レベル() >= 1 && !ITEM:ハーブ
	PRINTL 栽培:ハーブ(プランター)を解禁しました
	ITEM:ハーブ = 1
ENDIF
IF 栽培レベル() >= 2 && !ITEM:もやし
	PRINTL 栽培:もやし(プランター)を解禁しました
	ITEM:もやし = 1
ENDIF
IF 栽培レベル() >= 3 && !ITEM:イチゴ
	PRINTL 栽培:イチゴ(プランター)を解禁しました
	PRINTL 栽培:トマト(畑)を解禁しました
	ITEM:イチゴ = 1
	ITEM:トマト = 1
ENDIF
IF 栽培レベル() >= 4 && !ITEM:唐辛子
	PRINTL 栽培:唐辛子(プランター)を解禁しました
	PRINTL 栽培:きゅうり(畑)を解禁しました
	ITEM:唐辛子 = 1
	ITEM:きゅうり = 1
ENDIF
IF 栽培レベル() >= 5 && !ITEM:じゃがいも
	PRINTL 栽培:じゃがいも(畑)を解禁しました
	PRINTL 栽培:さつまいも(畑)を解禁しました
	ITEM:じゃがいも = 1
	ITEM:さつまいも = 1
ENDIF
IF 栽培レベル() >= 6 && !ITEM:ナス
	PRINTL 栽培:ナス(畑)を解禁しました
	PRINTL 栽培:ピーマン(畑)を解禁しました
	ITEM:ナス = 1
	ITEM:ピーマン = 1
ENDIF
IF 栽培レベル() >= 6 && !ITEM:ほうれん草
	PRINTL 栽培:ほうれん草(畑)を解禁しました
	ITEM:ほうれん草 = 1
ENDIF
IF 栽培レベル() >= 7 && !ITEM:大根
	PRINTL 栽培:大根(畑)を解禁しました
	PRINTL 栽培:人参(畑)を解禁しました
	ITEM:大根 = 1
	ITEM:人参 = 1
ENDIF
IF 栽培レベル() >= 7 && !ITEM:れんこん
	PRINTL 栽培:れんこん(畑)を解禁しました
	ITEM:れんこん = 1
ENDIF
IF 栽培レベル() >= 8 && !ITEM:玉ねぎ
	PRINTL 栽培:玉ねぎ(畑)を解禁しました
	PRINTL 栽培:長ねぎ(畑)を解禁しました
	ITEM:玉ねぎ = 1
	ITEM:長ねぎ = 1
ENDIF
IF 栽培レベル() >= 9 && !ITEM:レモン
	PRINTL 栽培:レモン(プランター)を解禁しました
	PRINTL 栽培:ライム(プランター)を解禁しました
	ITEM:レモン = 1
	ITEM:ライム = 1
ENDIF
IF 栽培レベル() >= 10 && !ITEM:レタス
	PRINTL 栽培:レタス(畑)を解禁しました
	PRINTL 栽培:ごぼう(畑)を解禁しました
	ITEM:レタス = 1
	ITEM:ごぼう = 1
ENDIF
IF 栽培レベル() >= 10 && !ITEM:アルストロメリア
	PRINTL 栽培:アルストロメリア(プランター)を解禁しました
	ITEM:アルストロメリア = 1
ENDIF
IF 栽培レベル() >= 11 && !ITEM:りんご
	PRINTL 栽培:りんご(畑)を解禁しました
	PRINTL 栽培:みかん(畑)を解禁しました
	ITEM:りんご = 1
	ITEM:みかん = 1
ENDIF
IF 栽培レベル() >= 12 && !ITEM:ブルーベリー
	PRINTL 栽培:ブルーベリー(プランター)を解禁しました
	PRINTL 栽培:ラズベリー(プランター)を解禁しました
	ITEM:ブルーベリー = 1
	ITEM:ラズベリー = 1
ENDIF
IF 栽培レベル() >= 12 && !ITEM:カカオ豆
	PRINTL 栽培:カカオ豆(畑)を解禁しました
	ITEM:カカオ豆 = 1
ENDIF

IF 栽培レベル() >= 13 && !ITEM:キャベツ
	PRINTL 栽培:キャベツ(畑)を解禁しました
	PRINTL 栽培:白菜(畑)を解禁しました
	ITEM:キャベツ = 1
	ITEM:白菜 = 1
ENDIF
IF 栽培レベル() >= 14 && !ITEM:とうもろこし
	PRINTL 栽培:とうもろこし(畑)を解禁しました
	PRINTL 栽培:かぼちゃ(畑)を解禁しました
	ITEM:とうもろこし = 1
	ITEM:かぼちゃ = 1
ENDIF
IF 栽培レベル() >= 15 && !ITEM:米
	PRINTL 栽培:米(田んぼ)を解禁しました
	PRINTL 栽培:麦(田んぼ)を解禁しました
	ITEM:米 = 1
	ITEM:麦 = 1
ENDIF
IF 栽培レベル() >= 16 && !ITEM:にんにく
	PRINTL 栽培:にんにく(プランター)を解禁しました
	ITEM:にんにく = 1
ENDIF
IF 栽培レベル() >= 17 && !ITEM:メロン
	PRINTL 栽培:メロン(畑)を解禁しました
	PRINTL 栽培:スイカ(畑)を解禁しました
	ITEM:メロン = 1
	ITEM:スイカ = 1
ENDIF
IF 栽培レベル() >= 18 && !ITEM:薔薇
	PRINTL 栽培:薔薇(プランター)を解禁しました
	ITEM:薔薇 = 1
ENDIF
IF 栽培レベル() >= 20 && !ITEM:ぶどう
	PRINTL 栽培:ぶどう(畑)を解禁しました
	ITEM:ぶどう = 1
ENDIF

IF 栽培レベル() >= 30 && !ITEM:嘆きの樹
	PRINTL 栽培:嘆きの樹(プランター)を解禁しました
	ITEM:嘆きの樹 = 1
	;実績:すべての作物を解禁する
	CALL 実績解除, "Power of Nature"
ENDIF

;畑は栽培レベル3から購入可能
IF 栽培レベル() >= 3 && 畑上限 < 1
	畑上限++
	PRINTL 畑を購入できるようになりました
ENDIF

;以後、栽培レベル3上昇ごとに畑上限が1増える 最大で8枚
FOR LCOUNT, 2, 9
	IF 栽培レベル() >= LCOUNT*3 && 畑上限 < LCOUNT
		畑上限++
		PRINTFORML 畑の所持上限が増えました(現在{畑上限}枚\@ 畑上限 == 8 ? 　畑全解禁！ #  \@)
	ENDIF
NEXT

IF 栽培レベル() >= 15 && 田んぼ上限 < 1
	田んぼ上限++
	PRINTL 田んぼを購入できるようになりました
ENDIF
IF 栽培レベル() >= 30 && 田んぼ上限 < 2
	田んぼ上限++
	PRINTL 田んぼの所持上限が増えました
ENDIF

RETURN

@植物図鑑
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC ページ数
#DIM DYNAMIC 総数
DRAWLINE

表示数 = 0
次ページあり = 0
総数 = 0

FOR LCOUNT, 600, 750
	IF ITEMNAME:LCOUNT != ""
		IF 表示数 == 10
			次ページあり = 1
			BREAK
		ENDIF
		総数++
		SIF 総数 < ページ数*10+1
			CONTINUE
		IF ITEM:LCOUNT
			;1は解禁済み（白文字）、2は栽培収穫したことがある（青文字）
			SIF ITEM:LCOUNT == 2
				SETCOLOR 100, 150, 200
			PRINTFORM 　{総数}.%ITEMNAME:LCOUNT, 14, LEFT% 
			RESETCOLOR
			SELECTCASE ITEMNAME:LCOUNT
				CASE "ハーブ"
					PRINTL 繁殖力がすさまじい薬草たち。ゾンビパンデミックでも重宝する
				CASE "もやし"
					PRINTL 買っても育ててもお手軽。シャキシャキ感がたまらない。カロリーは無いが栄養も無い
				CASE "イチゴ"
					PRINTL 甘くて美味しい。身近な果物の代表格
				CASE "唐辛子"
					PRINTL 辛党の必需品。調理する人の塩梅次第でケンカが起きる
				CASE "レモン"
					PRINTL ビタミンCが豊富な酸っぱい果物。ジュース、スイーツ、揚げ物と用途は幅広い
				CASE "ライム"
					PRINTL レモン同様に酸っぱい果物。近年の勢いはレモンの座を脅かしつつある
				CASE "ブルーベリー"
					PRINTL 目に良い。ジャムにもなる
				CASE "ラズベリー"
					PRINTL 甘酸っぱい。ジャムにもなる
				CASE "にんにく"
					PRINTL 元気の源。においさえ気にしなければ栄養価は最強
				CASE "薔薇"
					PRINTL 愛の花。きれいな花には棘がある
				CASE "嘆きの樹"
					PRINTL 「その嘆きこそ我の糧也」と言いながら愚痴を聞いてくれるイイやつ
				CASE "アルストロメリア"
					PRINTL 3月25日の誕生花。幸せを運ぶ花
				CASE "トマト"
					PRINTL どこにでもある野菜だがその用途は多岐にわたる。生食、煮込み、調味料などなど
				CASE "きゅうり"
					PRINTL 夏の風物詩。採れたてをそのままいただこう
				CASE "じゃがいも"
					PRINTL 畑に生る主食。熱を通せばだいたい美味しく食べられる
				CASE "さつまいも"
					PRINTL 和スイーツには欠かせない野菜。焼いても蒸かしても美味しい
				CASE "ナス"
					PRINTL 瑞々しさと肉々しさが両立した野菜。和洋中問わず何にでも使える
				CASE "ピーマン"
					PRINTL 子供の天敵。これを美味しく食べられるようになったら大人になった証拠
				CASE "ほうれん草"
					PRINTL 葉菜の代表格。おかずに一品、付け合せに一品と縁の下の力持ち
				CASE "大根"
					PRINTL 熱々に煮込まれた大根には根強いファンも多い。根っこだけに
				CASE "人参"
					PRINTL 子供の天敵その2。カレー等の助け舟があるのでピーマンほど嫌われてはいない
				CASE "れんこん"
					PRINTL 切る前も後も面白い見た目の根菜。他では味わえない食感
				CASE "玉ねぎ"
					PRINTL 全米を泣かせた
				CASE "長ねぎ"
					PRINTL 時と場合によっては長柄武器にもなる
				CASE "レタス"
					PRINTL キャベツほど融通が利かない不憫野菜
				CASE "ごぼう"
					PRINTL 煮物、きんぴら、炊き込みご飯など、和食の友
				CASE "りんご"
					PRINTL 皮ごとかじる派と皮をきれいに剥く派に分かれる
				CASE "みかん"
					PRINTL こたつ、猫とよくトリオになっている
				CASE "キャベツ"
					PRINTL 炒めてよし、煮込んでよし、生でもよしの万能野菜
				CASE "白菜"
					PRINTL 冬版キャベツ。炒めてよし、煮込んでよし、生でも以下略
				CASE "とうもろこし"
					PRINTL 調理するとシャキシャキだったりふわふわだったりする謎の食材
				CASE "かぼちゃ"
					PRINTL 甘くて美味しいが栽培難易度は高い
				CASE "メロン"
					PRINTL 農家の夢。高級品は一玉100クレ以上にもなる
				CASE "スイカ"
					PRINTL 麦わら帽子、セミの鳴き声、そして思い出す縁側で食べたこれ
				CASE "ぶどう"
					PRINTL そのまま食べるほか、ワインの命はぶどうの品質にも大きく左右される
				CASE "米"
					PRINTL ピタミンミネラル食物繊維、スーパーエナジーミラクルパワー。おこめおいし♪
				CASE "麦"
					PRINTL 米と対をなす二大穀物。これの有無で食生活が大きく変わる
				CASE "カカオ豆"
					PRINTL コーヒー豆だったりする
			ENDSELECT
		ELSE
			SETCOLORBYNAME gray
			PRINTFORML 　{総数}.[未解禁]
			RESETCOLOR
		ENDIF
		表示数++
	ENDIF
NEXT
DRAWLINE
PRINT 　白文字:解禁済み 
SETCOLOR 100, 150, 200
PRINT 青文字:栽培・収穫経験あり 
RESETCOLOR
SETCOLORBYNAME gray
PRINTL 灰文字:未解禁
RESETCOLOR
DRAWLINE
PRINTPLAINFORM 　植物図鑑 {ページ数+1}ページ　
IF ページ数
	PRINT [1] 前ページ 
ELSE
	PRINT 　　　　　　 
ENDIF
PRINT [0] 戻る 
SIF 次ページあり
	PRINT [9] 次ページ

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			SIF ページ数
				ページ数--
			RESTART
		CASE 9
			SIF 次ページあり
				ページ数++
			RESTART
	ENDSELECT
LOOP 1

@農業設備導入
#DIMS DYNAMIC 購入設備
DRAWLINE
PRINTL □農業用設備の導入
PRINTFORML 　現在の設備 田んぼ収穫日:{ITEM:田んぼ}/{田んぼ上限} 畑収穫日:{ITEM:畑}/{畑上限} プランター収穫日:{ITEM:プランター}/4
CALL PASELIL, @"　所持金:{MONEY}＄"
PRINTL 
IF ITEM:田んぼ < 田んぼ上限
	CALL PASELIL, @"　[0] - 田んぼを購入する({ITEMPRICE:田んぼ}＄)"
ELSE
	SETCOLORBYNAME gray
	PRINTL 　[-] - 田んぼはこれ以上買えません
	RESETCOLOR
ENDIF
IF ITEM:畑 < 畑上限
	CALL PASELIL, @"　[1] - 畑を購入する({ITEMPRICE:畑}＄)"
ELSE
	SETCOLORBYNAME gray
	PRINTL 　[-] - 畑はこれ以上買えません
	RESETCOLOR
ENDIF
IF ITEM:プランター < 4
	CALL PASELIL, @"　[2] - プランターを購入する({ITEMPRICE:プランター}＄)"
ELSE
	SETCOLORBYNAME gray
	PRINTL 　[-] - プランターはこれ以上買えません
	RESETCOLOR
ENDIF
PRINTL 　[9] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			IF ITEM:田んぼ < 田んぼ上限
				購入設備 = 田んぼ
				BREAK
			ENDIF
		CASE 1
			IF ITEM:畑 < 畑上限
				購入設備 = 畑
				BREAK
			ENDIF
		CASE 2
			IF ITEM:プランター < 4
				購入設備 = プランター
				BREAK
			ENDIF
		CASE 9
			RETURN
	ENDSELECT
LOOP 1

IF MONEY < ITEMPRICE:購入設備
	PRINTW 所持金が足りません
	RESTART
ELSE
	CALL PASELIL, @"%購入設備%を購入します。よろしいですか？(所持金:{MONEY}＄→{MONEY-ITEMPRICE:購入設備}＄)"
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW %購入設備%を購入しました
				ITEM:購入設備++
				MONEY -= ITEMPRICE:購入設備
				RESTART
			CASE 1
				RESTART
		ENDSELECT
	LOOP 1
ENDIF

@栽培レベル, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 返り値

;新婚モードなら無条件で50にする
SIF ゲームモード() == "新婚モード"
	RETURNF 50

FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "農業"
		CONTINUE
	CFLAG:LCOUNT:配属完了 = 1
	IF 統率者("農業") && !TALENT:LCOUNT:統率
		返り値 += MIN((ABL:LCOUNT:栽培技能+1+(特質("ワーカホリック", LCOUNT)/10)), 10)
	ELSE
		返り値 += MIN(ABL:LCOUNT:栽培技能+(特質("ワーカホリック", LCOUNT)/10), 10)
	ENDIF
	SIF TALENT:ARG:植生学
		返り値 += 3
NEXT

RETURNF 返り値

@成長速度補正, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 魔法レベル
#DIM DYNAMIC 科学レベル

FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 != "農業"
		CONTINUE
	IF 統率者("農業") && !TALENT:LCOUNT:統率
		魔法レベル += MIN((ABL:LCOUNT:魔法技能+1), 10)
		科学レベル += MIN((ABL:LCOUNT:工作技能+1), 10)
	ELSE
		魔法レベル += MIN(ABL:LCOUNT:魔法技能, 10)
		科学レベル += MIN(ABL:LCOUNT:工作技能, 10)
	ENDIF
NEXT

IF TALENT:ARG:植生学
	RETURNF 栽培レベル()*2+魔法レベル+科学レベル+3
ELSE
	RETURNF 栽培レベル()*2+魔法レベル+科学レベル
ENDIF

;収穫日数の再計算
@農業_ASSIGNON_OPTION, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 上昇値

;配属したキャラが成長速度補正に及ぼす影響を調べる
上昇値 = ABL:ARG:栽培技能*2+ABL:ARG:魔法技能+ABL:ARG:工作技能
SIF TALENT:ARG:植生学
	上昇値 += 3

;成長速度補正が基礎成長日数以下なら上昇値の分収穫を早める 最短で3日まで
FOR LCOUNT, 1, 5
	SIF プランター収穫日:LCOUNT < 3
		CONTINUE
	SIF 成長速度補正()-上昇値 < ITEMPRICE:(プランターの作物:LCOUNT)
		プランター収穫日:LCOUNT = MAX(プランター収穫日:LCOUNT-上昇値, 3)
NEXT

FOR LCOUNT, 1, 9
	SIF 畑収穫日:LCOUNT < 3
		CONTINUE
	SIF 成長速度補正()-上昇値 < ITEMPRICE:(畑の作物:LCOUNT)
		畑収穫日:LCOUNT = MAX(畑収穫日:LCOUNT-上昇値, 3)
NEXT

FOR LCOUNT, 1, 3
	SIF 田んぼ収穫日:LCOUNT < 3
		CONTINUE
	SIF 成長速度補正()-上昇値 < ITEMPRICE:(田んぼの作物:LCOUNT)
		田んぼ収穫日:LCOUNT = MAX(田んぼ収穫日:LCOUNT-上昇値, 3)
NEXT

@農業_ASSIGNOFF_OPTION, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 減少値

;外したキャラが成長速度補正に及ぼしていた影響を調べる
減少値 = ABL:ARG:栽培技能*2+ABL:ARG:魔法技能+ABL:ARG:工作技能
SIF TALENT:ARG:植生学
	減少値 += 3

;成長速度補正が基礎成長日数以下なら減少値の分収穫を先延ばしにする
FOR LCOUNT, 1, 5
	SIF プランター収穫日:LCOUNT == 0
		CONTINUE
	SIF 成長速度補正() < ITEMPRICE:(プランターの作物:LCOUNT)
		プランター収穫日:LCOUNT = MIN(プランター収穫日:LCOUNT+減少値, ITEMPRICE:(プランターの作物:LCOUNT))
NEXT

FOR LCOUNT, 1, 9
	SIF 畑収穫日:LCOUNT == 0
		CONTINUE
	SIF 成長速度補正() < ITEMPRICE:(畑の作物:LCOUNT)
		畑収穫日:LCOUNT = MIN(畑収穫日:LCOUNT+減少値, ITEMPRICE:(畑の作物:LCOUNT))
NEXT

FOR LCOUNT, 1, 3
	SIF 田んぼ収穫日:LCOUNT == 0
		CONTINUE
	SIF 成長速度補正() < ITEMPRICE:(田んぼの作物:LCOUNT)
		田んぼ収穫日:LCOUNT = MIN(田んぼ収穫日:LCOUNT+減少値, ITEMPRICE:(田んぼの作物:LCOUNT))
NEXT


@TURNEND_農業
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 栽培数, 4
CALL 栽培解禁

;新婚モード
IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
	CSTR:MASTER:配属 = 農業
	CSTR:FINDCHARA(CFLAG:嫁, 1):配属 = 農業
ENDIF

;ターン終了時の口上呼び出し
FOR LCOUNT, 0, CHARANUM
	SIF CSTR:LCOUNT:配属 == "農業" && !CFLAG:LCOUNT:口上非表示
		TRYCALLFORM TURNEND_農業_%CSVNAMERPS(NO:LCOUNT)%
NEXT


;作物の成長は日付変更時のみ
IF TIME == 1
	FOR LCOUNT, 1, 5
		SIF プランター収穫日:LCOUNT <= 0
			CONTINUE
		プランター収穫日:LCOUNT--
		IF プランター収穫日:LCOUNT == 0
			PRINTFORML %プランターの作物:LCOUNT%(プランター)が収穫可能になりました
			ITEM:(プランターの作物:LCOUNT) = 2
		ENDIF
		;プランターはいくつ育てても1換算
		栽培数 = 1
		栽培数:1 = 1
	NEXT
	FOR LCOUNT, 1, 9
		SIF 畑収穫日:LCOUNT <= 0
			CONTINUE
		畑収穫日:LCOUNT--
		IF 畑収穫日:LCOUNT == 0
			PRINTFORML %畑の作物:LCOUNT%(畑)が収穫可能になりました
			ITEM:(畑の作物:LCOUNT) = 2
		ENDIF
		栽培数++
		栽培数:2 = 1
	NEXT
	FOR LCOUNT, 1, 3
		SIF 田んぼ収穫日:LCOUNT <= 0
			CONTINUE
		田んぼ収穫日:LCOUNT--
		IF 田んぼ収穫日:LCOUNT == 0
			PRINTFORML %田んぼの作物:LCOUNT%(田んぼ)が収穫可能になりました
			ITEM:(田んぼの作物:LCOUNT) = 2
		ENDIF
		栽培数++
		栽培数:3 = 1
	NEXT
	;栽培経験を加算 栽培数-配属人数 ただしプランター、畑、田んぼで栽培していればその分最低保証
	;栽培数0なら1にする
	栽培数 -= CMATCH(CSTR:配属, "農業")
	CALL SETMAX, 栽培数, 1
	CALL SETMAX, 栽培数, 栽培数:1+栽培数:2+栽培数:3
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "農業"
			EXP:LCOUNT:栽培経験 += 栽培数
	NEXT
ENDIF

CALL CHECKDRAWLINEW

;SIF !メッセージ表示 && !LOCAL
;	PRINTL 農業の方は特に変わったことは無かったようだ

;新婚モード
IF ゲームモード() == "新婚モード" && FINDCHARA(CFLAG:嫁, 1) >= 0
	CSTR:MASTER:配属 = 
	CSTR:FINDCHARA(CFLAG:嫁, 1):配属 = 
ENDIF

@MANUAL_農業
DRAWLINE
PRINTL □農業について
PRINTL 　[1] - 農業のシステム
PRINTL 　[2] - 野菜育てて何の意味があるの？
DRAWLINE
PRINTPLAIN 　農業　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			PRINTL 農業ではプランター、畑、田んぼで野菜や穀物を育てられます
			PRINTL 配属したキャラクターの栽培技能に応じて育てられる作物や使用できる設備が増え、
			PRINTL 配属上限人数も上がります。最初はプランターだけですが、最終的には米や麦などを収穫可能です
			PRINTL プランター、畑、田んぼには数に限りがあるため、一度に育てられる種類にも限りがあります
			PRINTL 農業では設備を買うとき以外には費用がかかりません。農業担当に任せて気長に収穫を待ちましょう
			WAIT
			RESTART
		CASE 2
			PRINTL 育てた作物は調理室で使用できます。農業で収穫可能となった作物を使用して料理を作ります
			PRINTL 収穫可能となった作物はいくらでも使用でき、数の概念はありません。逆に、備蓄もできません
			PRINTL その時その時で収穫可能となっている作物を利用してレシピを考えましょう。
			PRINTL 詳しくは調理室の項も参照
			WAIT
			RESTART
	ENDSELECT
LOOP 1
