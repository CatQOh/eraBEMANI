@模様替え
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LRESULT
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM グリッド表示 = 1
#DIMS DYNAMIC 表示用変数, 100
#DIM DYNAMIC 部屋配置
#DIM DYNAMIC 幅, 2
#DIM DYNAMIC 始点, 2
#DIMS DYNAMIC 部屋情報, 200, 3
#DIM DYNAMIC 設置済み, 200
#DIMS DYNAMIC 部屋名
#DIMS DYNAMIC 表示チップ
#DIM DYNAMIC マップ横幅
#DIM DYNAMIC マップ縦幅
#DIM DYNAMIC 壁無し
#DIM DYNAMIC タイル表示幅
#DIM DYNAMIC タイル始点, 200, 2
#DIM DYNAMIC 生け簀始点, 2
#DIMS DYNAMIC オブジェクト名
#DIM DYNAMIC オブジェクト幅, 2
#DIMS DYNAMIC オブジェクト配置
#DIMS DYNAMIC オブジェクト配置向き
#DIM DYNAMIC オブジェクト表示済み
#DIM DYNAMIC 選択座標, 2
#DIMS DYNAMIC 選択部屋
#DIMS DYNAMIC 選択オブジェクト
#DIM DYNAMIC 畑始点
#DIM DYNAMIC プランター始点
#DIM DYNAMIC 田んぼ始点
#DIM DYNAMIC 居住区設置数
#DIM DYNAMIC 居住区, 100
#DIMS DYNAMIC 住人, 2
#DIM DYNAMIC 部屋番号
#DIM DYNAMIC 居住LCOUNT
#DIM DYNAMIC 住人数

VARLINE = LINECOUNT
CALL マップバージョン管理
CALL オブジェクトデータ

LOCAL = 1
;部屋名, X座標の長さ, Y座標の長さ 1次元目の数値は部屋IDとしても扱う 0は欠番
部屋情報:(LOCAL++):0 '= "レッスン室", "7", "5"
部屋情報:(LOCAL++):0 '= "医務室", "7", "5"
部屋情報:(LOCAL++):0 '= "実験室", "7", "5"
部屋情報:(LOCAL++):0 '= "調理室", "8", "6"
部屋情報:(LOCAL++):0 '= "図書室", "12", "8"
;1から始まるので調整
プランター始点 = LOCAL-1
部屋情報:(LOCAL++):0 '= "プランター", "3", "3"
部屋情報:(LOCAL++):0 '= "プランター", "3", "3"
部屋情報:(LOCAL++):0 '= "プランター", "3", "3"
部屋情報:(LOCAL++):0 '= "プランター", "3", "3"
畑始点 = LOCAL-1
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
部屋情報:(LOCAL++):0 '= "畑", "4", "4"
田んぼ始点 = LOCAL-1
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "田んぼ", "6", "6"
部屋情報:(LOCAL++):0 '= "牧場", "12", "12"
部屋情報:(LOCAL++):0 '= "畜舎", "8", "8"
部屋情報:(LOCAL++):0 '= "生け簀", "8", "8"
部屋情報:(LOCAL++):0 '= "養蜂場", "4", "4"

;100から居住用の部屋
FOR LCOUNT, 101, 200
	部屋情報:LCOUNT:0 '= "居住部屋", "4", "4"
NEXT

部屋配置 = 0

CALL 模様替え用スプライト

VARSET 始点

;SPRITEはここで作成しておく
GDISPOSE 0
GDISPOSE 1
GDISPOSE 2
GDISPOSE 3
GDISPOSE 4
GDISPOSE 5
GDISPOSE 6
GDISPOSE 7
GDISPOSE 8

GCREATEFROMFILE 0, "壁系.png"
SPRITECREATE "GRID", 0, 0, 0, 32, 32

GCREATEFROMFILE 1, "壁系.png"
;α値(不透明)+peru
GCLEAR 1, 0xFF000000+COLOR_FROMNAME("peru")
SPRITECREATE "GRID_OFF", 1, 0, 0, 32, 32

GCREATEFROMFILE 2, "壁系.png"
;α値(不透明)+yellow
GCLEAR 2, 0xFF000000+COLOR_FROMNAME("yellow")
SPRITECREATE "GRID_OVER", 2, 0, 0, 32, 32

GCREATEFROMFILE 3, "壁系.png"
;α値(不透明)+saddlebrow
GCLEAR 3, 0xFF000000+COLOR_FROMNAME("saddlebrown")
SPRITECREATE "GRID_WOOD", 3, 0, 0, 32, 32

GCREATEFROMFILE 4, "壁系.png"
;α値(不透明)+chocolate
GCLEAR 4, 0xFF000000+COLOR_FROMNAME("chocolate")
SPRITECREATE "GRID_GROUND", 4, 0, 0, 32, 32

GCREATEFROMFILE 5, "壁系.png"
;α値(不透明)+silver
GCLEAR 5, 0xFF000000+COLOR_FROMNAME("silver")
SPRITECREATE "GRID_SILVER", 5, 0, 0, 32, 32

GCREATEFROMFILE 8, "壁系.png"
;α値(不透明)+gray
GCLEAR 8, 0xFF000000+COLOR_FROMNAME("gray")
SPRITECREATE "GRID_GRAY", 8, 0, 0, 32, 32

CALL BGM再生, 模様替えBGM

マップ横幅 = 60
マップ縦幅 = 30

DO
	$RESTART
	CLEARLINE LINECOUNT-VARLINE
	VARSET 設置済み
	VARSET タイル始点, -1
	VARSET 居住区
	住人数 = 0
	;マップを表示する
	FOR Y座標, 0, マップ縦幅
		VARSET 表示用変数
		FOR X座標, 0, マップ横幅
			SIF X座標 == 0
				表示用変数:X座標 = <nobr>
			IF 部屋配置
				幅:0 = TOINT(部屋情報:部屋配置:1)
				幅:1 = TOINT(部屋情報:部屋配置:2)
			ELSEIF オブジェクト配置 != ""
				;幅が設定されていないならデフォルトの幅を返す
				IF 幅:0 == 0
					幅:0 = TOINT(オブジェクト情報(オブジェクト配置, "横幅"))
					幅:1 = TOINT(オブジェクト情報(オブジェクト配置, "縦幅"))
				ENDIF
				;向きも設定されてなかったら右向きデフォ
				SIF オブジェクト配置向き == ""
					オブジェクト配置向き = 右向き
			ENDIF
			部屋名 = 
			;重複対策に配置しようとしてる部屋は削除
			SIF 部屋配置 && 土地:Y座標:X座標 == 部屋配置
				土地:Y座標:X座標 = 0
			;始点の座標から計算して配置用の部屋を描画する
			IF (部屋配置 || オブジェクト配置 != "") && X座標 >= 始点:0 && Y座標 >= 始点:1 && INRANGE(X座標-始点:0, 0, 幅:0-1) && INRANGE(Y座標-始点:1, 0, 幅:1-1)
				表示用変数:X座標 += "<img src='GRID_OVER' srcb='' height='100' width='100'>"
			ELSE
				表示チップ =
				部屋名 = %部屋情報:(土地:Y座標:X座標):0%
				;居住部屋の場合は部屋番号と住んでいるキャラをツールチップに加える
				IF 部屋名 == "居住部屋"
					部屋番号 = 土地:Y座標:X座標-100
					住人 = 
					FOR 居住LCOUNT, 1, CHARANUM
						IF CFLAG:居住LCOUNT:居住 == 部屋番号
							IF 住人 == ""
								住人 = %CALLNAME:居住LCOUNT%
							ELSE
								住人 += @"と%CALLNAME:居住LCOUNT%"
							ENDIF
						ENDIF
					NEXT
					表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='居住部屋,{部屋番号} \@ 住人 == "" ? 空き部屋 # %住人%の部屋 \@' pos='{X座標*100}'>"
				ELSE
					表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
				ENDIF
				;まずオブジェクトを表示する
				;IF オブジェクト:Y座標:X座標 != ""
				;	;始点(左上)を記録しておく
				;	IF タイル始点:(オブジェクト:Y座標:X座標):0 == -1
				;		タイル始点:(オブジェクト:Y座標:X座標):0 = X座標
				;		タイル始点:(オブジェクト:Y座標:X座標):1 = Y座標
				;	ENDIF
				;その土地に部屋が設置してあったら対応した色で表示する
				IF 土地:Y座標:X座標 != 0
					;始点(左上)を記録しておく
					IF タイル始点:(土地:Y座標:X座標):0 == -1
						タイル始点:(土地:Y座標:X座標):0 = X座標
						タイル始点:(土地:Y座標:X座標):1 = Y座標
					ENDIF
					;設置済み判定に始点も入れておく
					設置済み:(土地:Y座標:X座標) = Y座標*100+X座標
					SELECTCASE 部屋名
						CASE "居住部屋"
							表示チップ = GRID_WOOD
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
							;部屋のIDから設置済み居住部屋を記録
							居住区:((土地:Y座標:X座標)-100) = 1

							;顔グラ表示の失敗作
							;表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'>"

							;VARSET 住人
							;FOR 居住LCOUNT, 1, CHARANUM
							;	IF CFLAG:居住LCOUNT:居住 == 部屋番号
							;		住人:FINDELEMENT(住人, "", 0, 2, 1) = %NAME:居住LCOUNT%
							;		GDISPOSE 住人数+100
							;		GCREATEFROMFILE 住人数+100, @"%NAME:居住LCOUNT%_FACE.png"
							;		SPRITECREATE @"%NAME:居住LCOUNT%_FACE", 住人数+100
							;		住人数++
							;	ENDIF
							;NEXT

							;IF 住人:0 != "" && 住人:1 != ""
							;	IF SPRITECREATED(@"%住人:0%_FACE") && SPRITECREATED(@"%住人:1%_FACE")
							;		表示用変数:X座標 += @"</button><button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
							;		表示用変数:X座標 += @"<img src='%住人:0%_FACE' srcb='' height='200' width='200' ypos='100'></button>"
							;		表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100+300}'>"
							;		表示用変数:X座標 += @"<img src='%住人:1%_FACE' srcb='' height='200' width='200' ypos='100'>"
							;	ENDIF
							;ELSEIF 住人:0 != ""
							;	IF SPRITECREATED(@"%住人:0%_FACE")
							;		表示用変数:X座標 += @"</button><button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100+100}'>"
							;		表示用変数:X座標 += @"<img src='%住人:0%_FACE' srcb='' height='300' width='300' ypos='100'>"
							;	ENDIF
							;ENDIF

						CASE "レッスン室", "調理室", "図書室"
							表示チップ = GRID_WOOD
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "医務室", "実験室"
							表示チップ = GRID_SILVER
							表示用変数:X座標 += @"<img src='%表示チップ%'>"
						CASE "畑"
							;畑 4*4
							;作物があれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%2 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%2 == 0
								表示チップ = GRID_GROUND
								IF 畑の作物:(土地:Y座標:X座標-畑始点) != ""
									;収穫可能かどうかで画像が変わる
									SELECTCASE 畑の作物:(土地:Y座標:X座標-畑始点)
										CASE "大根", "玉ねぎ", "さつまいも", "長ねぎ", "ごぼう", "ほうれん草", "スイカ", "メロン", "人参", "とうもろこし", "レタス", "白菜", "かぼちゃ", "キャベツ", "じゃがいも"
											表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='200' width='200'></button>"
											表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
											表示用変数:X座標 += "<img src='"
											IF 畑収穫日:(土地:Y座標:X座標-畑始点)
												表示用変数:X座標 += "苗"
											ELSE
												表示用変数:X座標 += @"%畑の作物:(土地:Y座標:X座標-畑始点)%"
											ENDIF
											表示用変数:X座標 += "' srcb='' height='200' width='200'>"
										CASE "トマト", "きゅうり", "ナス", "ピーマン", "りんご", "みかん", "ぶどう", "カカオ"
											IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
												表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
												表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
												表示用変数:X座標 += @"<img src='%畑の作物:(土地:Y座標:X座標-畑始点)%"
												IF 畑収穫日:(土地:Y座標:X座標-畑始点)
													表示用変数:X座標 += "1"
												ELSE
													表示用変数:X座標 += "2"
												ENDIF
												表示用変数:X座標 += "' srcb='' height='400' width='400'>"
											ENDIF
									ENDSELECT
								ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								ENDIF
							ENDIF
						CASE "田んぼ"
							表示チップ = GRID_GROUND
							;田んぼ 6*6
							;作物があれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%3 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%3 == 0
								表示チップ = GRID_GROUND
								IF 田んぼの作物:(土地:Y座標:X座標-田んぼ始点) != ""
									;収穫可能かどうかで画像が変わる
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
									表示用変数:X座標 += "<img src='稲"
									IF 田んぼ収穫日:(土地:Y座標:X座標-田んぼ始点)
										表示用変数:X座標 += "1"
									ELSE
										表示用変数:X座標 += "2"
									ENDIF
									表示用変数:X座標 += "' srcb='' height='300' width='300'>"
								ELSE
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								ENDIF
							ENDIF
						CASE "プランター"
							表示チップ = GRID_OFF
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								;収穫可能かどうかで画像が変わる
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF プランターの作物:(土地:Y座標:X座標-プランター始点) != ""
									IF プランター収穫日:(土地:Y座標:X座標-プランター始点)
										表示用変数:X座標 += "<img src='プランター苗"
									ELSE
										表示用変数:X座標 += @"<img src='%プランターの作物:(土地:Y座標:X座標-プランター始点)%"
									ENDIF
									表示用変数:X座標 += "' srcb='' height='300' width='300'>"
								ELSE
									表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='300' width='300'></button>"
									表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'><img src='プランター容器' srcb='' height='300' width='300'>"
								ENDIF
							ENDIF
						CASE "牧場"
							;牧場 12*12
							;牛か羊がいるならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 6 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 6
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 6 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 6
								LOCAL = 3
							ELSE
								LOCAL = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%6 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%6 == 0
								表示チップ = GRID_GRASS
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='600' width='600'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF LOCAL == 1
									SELECTCASE 牛の状態
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='牛{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='牛' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='牛-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ELSEIF LOCAL == 2
									SELECTCASE 羊の状態
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='羊{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='羊' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='羊-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ELSEIF LOCAL == 3
									SELECTCASE 馬の状態
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='馬{RAND(1, 4)}' srcb='' height='600' width='600'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='馬' srcb='' height='600' width='600'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='馬-{RAND(1, 4)}' srcb='' height='600' width='600'>"
									ENDSELECT
								ENDIF
							ENDIF
						CASE "畜舎"
							;畜舎 8*8
							;豚か鶏がいるならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 1
							ELSE
								LOCAL = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%4 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%4 == 0
								表示チップ = GRID_WOOD
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF LOCAL == 1
									SELECTCASE 豚の状態
										;元気
										CASE 80 TO 100
											表示用変数:X座標 += @"<img src='豚{RAND(1, 4)}' srcb='' height='400' width='400'>"
										;普通
										CASE 30 TO 79
											表示用変数:X座標 += @"<img src='豚' srcb='' height='400' width='400'>"
										;良くない
										CASE 1 TO 29
											表示用変数:X座標 += @"<img src='豚-{RAND(1, 4)}' srcb='' height='400' width='400'>"
									ENDSELECT
								ELSEIF LOCAL == 2 && 鶏の状態
									表示用変数:X座標 += "<img src='鶏' srcb='' height='400' width='400'>"
								ENDIF
							ENDIF
						CASE "生け簀"
							;生け簀は8*8
							;養殖中ならその画像を表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 1
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 0
								LOCAL = 2
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 3
							ELSEIF (X座標-タイル始点:(土地:Y座標:X座標):0) == 4 && (Y座標-タイル始点:(土地:Y座標:X座標):1) == 4
								LOCAL = 4
							ELSE
								LOCAL = 0
							ENDIF
							;始点から4つ区切りで表示
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%4 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%4 == 0
								表示チップ = GRID_WATER
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='400' width='400'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								IF 生け簀の養殖:LOCAL != ""
									SELECTCASE 生け簀の養殖:LOCAL
										CASE "タコ"
											表示用変数:X座標 += "<img src='タコ'"
										CASE "イカ"
											表示用変数:X座標 += "<img src='イカ'"
										CASE "エビ"
											表示用変数:X座標 += "<img src='エビ'"
										CASE "ウナギ"
											表示用変数:X座標 += "<img src='ウナギ'"
										CASE "貝類"
											表示用変数:X座標 += "<img src='貝類'"
										CASE "海藻"
											表示用変数:X座標 += "<img src='海藻'"
										CASE "カニ"
											表示用変数:X座標 += "<img src='カニ'"
										CASEELSE
											表示用変数:X座標 += "<img src='魚'"
									ENDSELECT
									表示用変数:X座標 += " srcb='' height='400' width='400'>"
								ENDIF
							ENDIF
						CASE "養蜂場"
							;養蜂場 4*4
							;蜂がいれば画像を表示する
							IF (X座標-タイル始点:(土地:Y座標:X座標):0)%2 == 0 && (Y座標-タイル始点:(土地:Y座標:X座標):1)%2 == 0
								表示チップ = GRID_BEEHIVE
								表示用変数:X座標 += @"<img src='%表示チップ%' srcb='' height='200' width='200'></button>"
								表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%部屋名%' pos='{X座標*100}'>"
								SELECTCASE 蜂の状態
									;元気
									CASE 80 TO 100
										表示用変数:X座標 += @"<img src='蜂{RAND(1, 4)}' srcb='' height='200' width='200'>"
									;普通
									CASE 30 TO 79
										表示用変数:X座標 += @"<img src='蜂' srcb='' height='200' width='200'>"
									;良くない
									CASE 1 TO 29
										表示用変数:X座標 += @"<img src='蜂-{RAND(1, 4)}' srcb='' height='200' width='200'>"
								ENDSELECT
							ENDIF
					ENDSELECT
				ELSEIF グリッド表示
					表示チップ = GRID
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ELSE
					表示チップ = GRID_OFF
					表示用変数:X座標 += @"<img src='%表示チップ%'>"
				ENDIF
				表示用変数:X座標 += "</button>"
				;壁の確認 端でも壁表示
				IF !壁無し
					;左壁
					IF X座標 == 0 || 土地:Y座標:(X座標-1) != 土地:Y座標:X座標
						;上壁もあれば┌壁
						IF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┌壁'></nonbutton>"
						;下壁もあれば└壁
						ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='└壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='左壁'></nonbutton>"
						ENDIF
					;上壁
					ELSEIF Y座標 == 0 || 土地:(Y座標-1):X座標 != 土地:Y座標:X座標
						;右壁もあれば┐壁
						IF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┐壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='上壁'></nonbutton>"
						ENDIF
					;右壁
					ELSEIF X座標 == マップ横幅-1 || 土地:Y座標:(X座標+1) != 土地:Y座標:X座標
						;下壁もあれば┘壁
						IF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='┘壁'></nonbutton>"
						ELSE
							表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='右壁'></nonbutton>"
						ENDIF
					;下壁
					ELSEIF Y座標 == マップ縦幅-1 || 土地:(Y座標+1):X座標 != 土地:Y座標:X座標
						表示用変数:X座標 += @"<nonbutton pos='{X座標*100}'><img src='下壁'></nonbutton>"
					ENDIF
				ENDIF
			ENDIF
		NEXT
		LOCALS = %STRJOIN(表示用変数, "")%
		VARSET 表示用変数
		FOR X座標, 0, 60
			;配置しようとするオブジェクトも仮表示してみる
			SIF オブジェクト配置 != "" && Y座標 == 始点:1+幅:1-1 && X座標 == 始点:0
				表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト配置%' pos='{X座標*100}'><img src='%オブジェクト配置%' srcb='' height='{幅:1*100}' width='\@ オブジェクト配置向き == "右向き" ? {幅:0*100} # -{幅:0*100} \@' ypos='{(幅:1-1)*(-100)}'></button>"
			IF OBJECTS:Y座標:X座標 != ""
				オブジェクト名 = %オブジェクト(OBJECTS:Y座標:X座標, "名前")%
				オブジェクト幅:0 = TOINT(オブジェクト(OBJECTS:Y座標:X座標, "横幅"))
				オブジェクト幅:1 = TOINT(オブジェクト(OBJECTS:Y座標:X座標, "縦幅"))
				SELECTCASE オブジェクト情報(オブジェクト名, "オブジェクトタイプ")
					;小物は机等に置いて表示するように少しずらす
					CASE "小物"
						表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト名%' pos='{X座標*100}'><img src='%オブジェクト名%' srcb='' height='{オブジェクト幅:1*100}' width='\@ オブジェクト(OBJECTS:Y座標:X座標, "向き") == "右向き" ? {オブジェクト幅:0*100} # -{オブジェクト幅:0*100} \@' ypos='{(オブジェクト幅:1-1)*(-100)+1}'></button>"
					CASEELSE
						表示用変数:X座標 += @"<button value='{(X座標*100)+Y座標}' title='%オブジェクト名%' pos='{X座標*100}'><img src='%オブジェクト名%' srcb='' height='{オブジェクト幅:1*100}' width='\@ オブジェクト(OBJECTS:Y座標:X座標, "向き") == "右向き" ? {オブジェクト幅:0*100} # -{オブジェクト幅:0*100} \@' ypos='{(オブジェクト幅:1-1)*(-100)}'></button>"
				ENDSELECT
			ENDIF
		NEXT
		SIF X座標 == マップ横幅-1
			表示用変数:X座標 += "</nobr>"
		LOCALS += @"%STRJOIN(表示用変数, "")%"
		HTML_PRINT LOCALS
	NEXT
	PRINTL 

	IF 部屋配置
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [7] 左回転　　　　 [9] 右回転
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUT

		SELECTCASE RESULT
			CASE 0
				部屋配置 = 0
			CASE 8
				SIF 始点:1 > 0
					始点:1--
			CASE 4
				SIF 始点:0 > 0
					始点:0--
			CASE 6
				SIF 始点:0+幅:0 < マップ横幅 
					始点:0++
			CASE 2
				SIF 始点:1+幅:1 < マップ縦幅
					始点:1++
			;四角形だから同義
			CASE 7, 9
				SWAP 部屋情報:部屋配置:1, 部屋情報:部屋配置:2
			CASE 5
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					SIF LCOUNT >= マップ縦幅
						GOTO RESTART
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF LCOUNT2 >= マップ横幅
							GOTO RESTART
						SIF 土地:LCOUNT:LCOUNT2 != 0
							GOTO RESTART
					NEXT
				NEXT
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						土地:LCOUNT:LCOUNT2 = 部屋配置
					NEXT
				NEXT
				部屋配置 = 0
				GOTO RESTART
		ENDSELECT

		CONTINUE
	ELSEIF オブジェクト配置 != ""
		PRINTL [4] ← [2] ↓ [8] ↑ [6] →
		PRINTL [7] 横幅を縮小 [9] 横幅を拡大
		PRINTL [1] 縦幅を縮小 [3] 縦幅を拡大
		PRINTBUTTON "[R] 反転", "R"
		PRINTL 
		PRINTL [5] 決定 [0] キャンセル

		ONEINPUTS

		SELECTCASE RESULTS
			CASE "0"
				オブジェクト配置 = 
			CASE "8"
				SIF 始点:1 > 0
					始点:1--
			CASE "4"
				SIF 始点:0 > 0
					始点:0--
			CASE "6"
				SIF 始点:0+幅:0 < マップ横幅 
					始点:0++
			CASE "2"
				SIF 始点:1+幅:1 < マップ縦幅
					始点:1++
			CASE "7"
				SIF 幅:0 > 1
					幅:0--
			CASE "9"
				SIF 幅:0 < 60
					幅:0++
			CASE "1"
				SIF 幅:1 > 1
					幅:1--
			CASE "3"
				SIF 幅:1 < 30
					幅:1++
			CASE "5"
				FOR LCOUNT, 始点:1, 始点:1+幅:1
					SIF LCOUNT >= マップ縦幅
						GOTO RESTART
					FOR LCOUNT2, 始点:0, 始点:0+幅+0
						SIF LCOUNT2 >= マップ横幅
							GOTO RESTART
					NEXT
				NEXT
				OBJECTS:(始点:1+幅:1-1):(始点:0) = %オブジェクト配置%_%オブジェクト配置向き%_{幅:0}_{幅:1}
				オブジェクト配置 = 
				幅:0 = 0
				幅:1 = 0
				GOTO RESTART
			CASE "r", "R"
				IF オブジェクト配置向き == "右向き"
					オブジェクト配置向き = 左向き
				ELSE
					オブジェクト配置向き = 右向き
				ENDIF
				GOTO RESTART
		ENDSELECT

		CONTINUE
	ELSE
		PRINTL [60] - オブジェクトを配置する [61] - 部屋を配置する [62] - オブジェクトを初期化する [63] - 部屋を初期化する
		PRINTFORML [65]  - グリッド表示\@ グリッド表示 ? しない # する \@ [70] 戻る
		INPUT
		SELECTCASE RESULT
			CASE 70
				CALL FADEOUTBGM
				RETURN
			CASE 60
				PRINTL □配置するオブジェクトを選んでください 名前(横幅×縦幅)
				;オブジェクト一覧関数でオブジェクト名入れるところまで処理する
				CALL オブジェクト一覧, オブジェクト配置
				GOTO RESTART
			CASE 61
				PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
				FOR LCOUNT, 1, 102
					SIF 部屋情報:LCOUNT:0 == ""
						CONTINUE
					;現在は部屋名とアイテム名が一致してるからこれでいい
					LOCALS = %部屋情報:LCOUNT:0%
					SELECTCASE LOCALS
						CASE "レッスン室", "調理室", "居住部屋"
						CASEELSE
							SIF ITEM:LOCALS == 0
								CONTINUE
					ENDSELECT
					PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
				NEXT
				SIF !LINEISEMPTY()
					PRINTL 
				DRAWLINE
				PRINTL [0] 戻る
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							GOTO RESTART
						CASE 1 TO VARSIZE("部屋情報", 0)
							;現在は部屋名とアイテム名が一致してるからこれでいい
							LOCALS = %部屋情報:RESULT:0%
							SELECTCASE LOCALS
								CASE "レッスン室", "調理室"
									部屋配置 = RESULT
									IF 設置済み:RESULT
										始点:0 = タイル始点:RESULT:0
										始点:1 = タイル始点:RESULT:1
									ENDIF
								;居住部屋は空いている番号を使う
								CASE "居住部屋"
									部屋配置 = FINDELEMENT(居住区, 0, 1, 100)+100
									IF 部屋配置 >= 200
										PRINTW これ以上居住部屋を置けません
										GOTO RESTART
									ENDIF
								CASEELSE
									SIF ITEM:LOCALS == 0
										CONTINUE
									部屋配置 = RESULT
									IF 設置済み:RESULT
										始点:0 = タイル始点:RESULT:0
										始点:1 = タイル始点:RESULT:1
									ENDIF
							ENDSELECT
							BREAK
					ENDSELECT
				LOOP 1
				CONTINUE
			CASE 62
				PRINTL オブジェクト情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET OBJECTS
				CONTINUE
			CASE 63
				PRINTL 部屋情報を初期化します。よろしいですか？
				PRINTL [0] - はい
				PRINTL [1] - いいえ
				DO
					INPUT
				LOOP LOOPRES(0, 1)
				SIF RESULT == 0
					VARSET 土地
				CONTINUE
			CASE 65
				INVERTBIT グリッド表示, 0
				CONTINUE
			CASEELSE
				;RESULTからX,Y座標を調べる
				;X
				選択座標:0 = RESULT/100
				;Y
				選択座標:1 = RESULT%100
				$部屋オプション
				選択部屋 = %部屋情報:(土地:(選択座標:1):(選択座標:0)):0%
				選択オブジェクト = %OBJECTS:(選択座標:1):(選択座標:0)%
				;座標の情報表示
				部屋番号 = 0
				住人 = 
				IF 選択部屋 == "居住部屋"
					部屋番号 = 土地:(選択座標:1):(選択座標:0)-100
					FOR 居住LCOUNT, 1, CHARANUM
						IF CFLAG:居住LCOUNT:居住 == 部屋番号
							IF 住人 == ""
								住人 = %CALLNAME:居住LCOUNT%
							ELSE
								住人 += @"と%CALLNAME:居住LCOUNT%"
							ENDIF
						ENDIF
					NEXT
				ENDIF
				PRINT □
				SIF 土地:(選択座標:1):(選択座標:0)
					PRINTFORM %選択部屋%\@ 部屋番号 ? ,{部屋番号} #\@ \@ 住人 == "" ? 空き部屋 # %住人%の部屋 \@ 
				SIF 選択オブジェクト != ""
					PRINTFORM %選択オブジェクト%配置中 
				PRINTFORML {選択座標:0}×{選択座標:1}
				;オブジェクトの扱い
				IF 選択オブジェクト != ""
					PRINTFORML 　[1] - %選択オブジェクト%を動かす
					PRINTFORML 　[2] - %選択オブジェクト%を片付ける
				ELSE
					PRINTL 　[3] - オブジェクトを配置する
				ENDIF
				;配置してある部屋の昨日にクイックアクセス
				IF 選択部屋 != ""
					PRINTFORML 　[4] - %選択部屋%\@ 部屋番号 ? ,{部屋番号} #\@を動かす
					PRINTFORML 　[5] - %選択部屋%\@ 部屋番号 ? ,{部屋番号} #\@を取り壊す
					SELECTCASE 選択部屋
						CASE "レッスン室", "医務室", "実験室", "調理室", "図書室"
							SIF GETNUM(ITEM, 選択部屋) == -1 || ITEM:選択部屋
								PRINTFORML 　[6] - %選択部屋%に行く
						CASE "居住部屋"
							SIF 住人 != ""
								PRINTFORML 　[6] - 居住部屋,{部屋番号}に行く
						CASE "プランター", "畑", "田んぼ"
							PRINTL 　[6] - 農作物を管理する
						CASE "牧場", "畜舎", "生け簀", "養蜂場"
							PRINTL 　[6] - 畜産物を管理する
					ENDSELECT
				ELSE
					PRINTL 　[7] - 部屋を配置する
				ENDIF
				SIF 選択部屋 == "居住部屋"
					PRINTL 　[8] - 住人を割り当てる
				DRAWLINE
				PRINTPLAINFORM 　{選択座標:0}×{選択座標:1}　
				PRINTL [0] - 戻る
				始点:0 = 選択座標:0
				始点:1 = 選択座標:1
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0
							VARSET 始点
							GOTO RESTART
						CASE 1
							SIF 選択オブジェクト == ""
								CONTINUE
							オブジェクト配置 = %オブジェクト(OBJECTS:(始点:1):(始点:0), "名前")%
							幅:0 = TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "横幅"))
							幅:1 = TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "縦幅"))
							;縦にずれてる分を調整
							始点:1 -= TOINT(オブジェクト(OBJECTS:(始点:1):(始点:0), "縦幅"))-1
							OBJECTS:(始点:1):(始点:0) = 
							GOTO RESTART
						CASE 2
							SIF 選択オブジェクト == ""
								CONTINUE
							OBJECTS:(始点:1):(始点:0) = 
							GOTO RESTART
						CASE 3
							PRINTL □配置するオブジェクトを選んでください 名前(横幅×縦幅)
							CALL オブジェクト一覧, オブジェクト配置
							GOTO RESTART
						CASE 4
							SIF 選択部屋 == ""
								CONTINUE
							部屋配置 = 土地:(始点:1):(始点:0)
							IF 設置済み:部屋配置
								始点:0 = タイル始点:部屋配置:0
								始点:1 = タイル始点:部屋配置:1
							ENDIF
							GOTO RESTART
						CASE 5
							SIF 選択部屋 == ""
								CONTINUE
							IF 選択部屋 == "居住部屋"
								部屋番号 = 土地:(選択座標:1):(選択座標:0)-100
								PRINTFORML %選択部屋%,{部屋番号}を取り壊します。よろしいですか？(マップ上から消えるだけで機能は失われません)
							ELSE
								PRINTFORML %選択部屋%を取り壊します。よろしいですか？(マップ上から消えるだけで機能は失われません)
							ENDIF
							PRINTL [0] - はい
							PRINTL [1] - いいえ
							LOCAL = 土地:(選択座標:1):(選択座標:0)
							DO
								INPUT
								IF RESULT == 0
									FOR LCOUNT, 0, 30
										FOR LCOUNT2, 0, 60
											SIF 土地:LCOUNT:LCOUNT2 == LOCAL
												土地:LCOUNT:LCOUNT2 = 0
										NEXT
									NEXT
									VARSET 始点
								ENDIF
								IF 選択部屋 == "居住部屋"
									FOR 居住LCOUNT, 0, CHARANUM
										SIF CFLAG:居住LCOUNT:居住 == 部屋番号
											CFLAG:居住LCOUNT:居住 = 0
									NEXT
								ENDIF
								GOTO RESTART
							LOOP 1
						CASE 6
							SELECTCASE 選択部屋
								CASE "レッスン室"
									CALL STRATEGY_IDOL
									GOTO RESTART
								CASE "図書室", "実験室", "調理室", "医務室"
									SIF !ITEM:選択部屋
										CONTINUE
									CALLFORM %選択部屋%
									GOTO RESTART
								CASE "プランター", "畑", "田んぼ"
									CALL 農産物
									GOTO RESTART
								CASE "牧場", "畜舎", "生け簀", "養蜂場"
									CALL 畜産
									GOTO RESTART
								CASE "居住部屋"
									IF 住人 != ""
										CALL 居住部屋, 部屋番号
										SIF FLAG:休憩フラグ
											RETURN
										GOTO RESTART
									ENDIF
							ENDSELECT
						CASE 7
							PRINTL □配置する部屋を選んでください 名前(横幅×縦幅)
							FOR LCOUNT, 1, 102
								SIF 部屋情報:LCOUNT:0 == ""
									CONTINUE
								;現在は部屋名とアイテム名が一致してるからこれでいい
								LOCALS = %部屋情報:LCOUNT:0%
								SELECTCASE LOCALS
									CASE "レッスン室", "調理室", "居住部屋"
									CASEELSE
										SIF ITEM:LOCALS == 0
											CONTINUE
								ENDSELECT
								PRINTFORMLC 　[{LCOUNT}] %部屋情報:LCOUNT:0%({TOINT(部屋情報:LCOUNT:1)}×{TOINT(部屋情報:LCOUNT:2)}\@ 設置済み:LCOUNT ? /設置済み #\@)
							NEXT
							SIF !LINEISEMPTY()
								PRINTL 
							DRAWLINE
							PRINTL [0] 戻る
							DO
								INPUT
								SELECTCASE RESULT
									CASE 0
										GOTO RESTART
									CASE 1 TO VARSIZE("部屋情報", 0)
										;現在は部屋名とアイテム名が一致してるからこれでいい
										LOCALS = %部屋情報:RESULT:0%
										SELECTCASE LOCALS
											CASE "レッスン室", "調理室"
												部屋配置 = RESULT
											;居住部屋は空いている番号を使う
											CASE "居住部屋"
												部屋配置 = FINDELEMENT(居住区, 0, 1, 100)+100
												IF 部屋配置 >= 200
													PRINTW これ以上居住部屋を置けません
													GOTO RESTART
												ENDIF
											CASEELSE
												SIF ITEM:LOCALS == 0
													CONTINUE
												部屋配置 = RESULT
										ENDSELECT
										GOTO RESTART
								ENDSELECT
							LOOP 1
							CONTINUE
						CASE 8
							SIF 選択部屋 != "居住部屋"
								CONTINUE
							CALL 住人配置, 部屋番号
							GOTO 部屋オプション
					ENDSELECT
				LOOP 1
		ENDSELECT
	ENDIF
LOOP 1

;OBJECTS_DATAから目的の情報を抽出して返す
@オブジェクト情報, オブジェクト名, 指定タイプ
#FUNCTIONS
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 配列
#DIMS DYNAMIC オブジェクト名
#DIMS DYNAMIC 指定タイプ

FOR LCOUNT, 1, VARSIZE("OBJECTS_DATA", 0)
	IF OBJECTS_DATA:LCOUNT:0 == オブジェクト名
		配列 = LCOUNT
		BREAK
	ENDIF
NEXT

;#; SIF 配列 == 0
;#; 	THROW %オブジェクト名%のオブジェクト情報が存在しません

SELECTCASE 指定タイプ
	CASE "横幅"
		RETURNF OBJECTS_DATA:配列:1
	CASE "縦幅"
		RETURNF OBJECTS_DATA:配列:2
	CASE "オブジェクトタイプ"
		RETURNF OBJECTS_DATA:配列:3
ENDSELECT

@オブジェクト一覧, オブジェクト配置
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LRESULT
#DIMS REF オブジェクト配置

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;とりあえず上限は1000で設定しておく
FOR LCOUNT, 1, 1000
	SIF OBJECTS_DATA:LCOUNT:0 == ""
		BREAK
	PRINTFORMLC 　[{LCOUNT}] %OBJECTS_DATA:LCOUNT:0%(%OBJECTS_DATA:LCOUNT:1%×%OBJECTS_DATA:LCOUNT:2%)
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			オブジェクト配置 = 
			RETURN
		CASEELSE
			LRESULT = RESULT
			IF OBJECTS_DATA:LRESULT:0 != ""
				オブジェクト配置 = %OBJECTS_DATA:LRESULT:0%
				RETURN
			ELSE
				CONTINUE
			ENDIF
	ENDSELECT
LOOP 1

@オブジェクト, オブジェクト名, タイプ指定
#FUNCTIONS
#DIMS DYNAMIC オブジェクト名
#DIMS DYNAMIC タイプ指定

;OBJECTSの中身は「%オブジェクト名%_%向き%_%横幅%_%縦幅%」となっている
;SPLITで分解して目的の情報を返す

SPLIT オブジェクト名, "_", LOCALS

SELECTCASE タイプ指定
	CASE "名前"
		RETURNF LOCALS:0
	CASE "向き"
		RETURNF LOCALS:1
	CASE "横幅"
		RETURNF LOCALS:2
	CASE "縦幅"
		RETURNF LOCALS:3
	CASEELSE
		;#; THROW タイプ指定が表記ゆれです
ENDSELECT

@マップバージョン管理
#DIMS DYNAMIC オブジェクト情報, 100, 4
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標

LOCAL = 1
オブジェクト情報:(LOCAL++):0 '= "ベッド1_右向き", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "ベッド1_左向き", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "ベッド2_右向き", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "ベッド2_左向き", "2", "1"

LOCAL = 11
オブジェクト情報:(LOCAL++):0 '= "椅子1_右向き", "1", "1"
オブジェクト情報:(LOCAL++):0 '= "椅子1_左向き", "1", "1"
オブジェクト情報:(LOCAL++):0 '= "椅子2_右向き", "1", "1"
オブジェクト情報:(LOCAL++):0 '= "椅子2_左向き", "1", "1"

LOCAL = 21
オブジェクト情報:(LOCAL++):0 '= "テーブル1", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "ランプ1", "1", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "地球儀", "1", "1", "小物"

LOCAL = 31
オブジェクト情報:(LOCAL++):0 '= "本棚1", "2", "2"
オブジェクト情報:(LOCAL++):0 '= "本1", "1", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "カウンター_右向き", "3", "2"
オブジェクト情報:(LOCAL++):0 '= "カウンター_左向き", "3", "2"

LOCAL = 41
オブジェクト情報:(LOCAL++):0 '= "救急箱", "2", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "心電図", "2", "1", "小物"

LOCAL = 51
オブジェクト情報:(LOCAL++):0 '= "シンク", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "コンロ", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "冷蔵庫", "2", "2"

LOCAL = 61
オブジェクト情報:(LOCAL++):0 '= "テレビ", "2", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "鏡", "1", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "タンス", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "クローゼット_右向き", "1", "2"
オブジェクト情報:(LOCAL++):0 '= "クローゼット_左向き", "1", "2"
オブジェクト情報:(LOCAL++):0 '= "ソファ", "2", "1"
オブジェクト情報:(LOCAL++):0 '= "絨毯", "3", "2"

LOCAL = 71
オブジェクト情報:(LOCAL++):0 '= "三角マーク", "1", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "生体ディスプレイ", "2", "2", "小物"
オブジェクト情報:(LOCAL++):0 '= "培養槽", "2", "3"
オブジェクト情報:(LOCAL++):0 '= "フラスコセット", "2", "1", "小物"
オブジェクト情報:(LOCAL++):0 '= "テーブル_白", "2", "1"

;Ver.2.14 2021/05/XX～導入
;変数「オブジェクト」にオブジェクト情報を数値で入れてたのを文字列変数「OBJECTS」に変更
IF マップバージョン == 0
	FOR Y座標, 0, 30
		FOR X座標, 0, 60
			IF オブジェクト:Y座標:X座標
				;テーブル_白だけハイフンが使われてるので別処理
				IF オブジェクト情報:(オブジェクト:Y座標:X座標):0 == "テーブル_白"
					OBJECTS:Y座標:X座標 = テーブル（白）_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
				ELSE
					SPLIT オブジェクト情報:(オブジェクト:Y座標:X座標):0, "_", LOCALS
					IF RESULT >= 2
						OBJECTS:Y座標:X座標 = %オブジェクト情報:(オブジェクト:Y座標:X座標):0%_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
					ELSE
						OBJECTS:Y座標:X座標 = %オブジェクト情報:(オブジェクト:Y座標:X座標):0%_右向き_%オブジェクト情報:(オブジェクト:Y座標:X座標):1%_%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
					ENDIF
				ENDIF
				DEBUGPRINTFORML X({X座標})×Y({Y座標})に%オブジェクト情報:(オブジェクト:Y座標:X座標):0% %オブジェクト情報:(オブジェクト:Y座標:X座標):1%×%オブジェクト情報:(オブジェクト:Y座標:X座標):2%
			ENDIF
		NEXT
	NEXT
	;以降、上記の「オブジェクト情報」は枠が足りなくなるので使わない
	マップバージョン = 21400
ENDIF



