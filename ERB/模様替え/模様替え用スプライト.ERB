@模様替え用スプライト
CALL 動物スプライト
CALL 植物スプライト
CALL 魚スプライト
CALL オブジェクトスプライト

@動物スプライト
;畜産農業は2000台で
#DIM CONST ID = 2000
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
;以下、動物名を「動物.pngの上から順に」書くだけで画像が追加される
{
#DIMS DYNAMIC 動物名 = 
"鶏",
"牛",
"羊",
"豚",
"馬",
"蜂",
}

GCREATEFROMFILE ID, "動物.png"

;LCOUNTは高さ(動物の種類)、LCOUNT2が幅(表情)
;一律100*100
FOR LCOUNT, 0, VARSIZE("動物名")
	;鶏だけ表情差分無いので個別処理
	SELECTCASE 動物名:LCOUNT
		CASE "鶏"
			SPRITECREATE @"%動物名:LCOUNT%", ID, 0, LCOUNT*100, 100, 100
		CASEELSE
			;表情は現在7種類
			FOR LCOUNT2, 0, 7
				SELECTCASE LCOUNT2
					;デフォルト(0)は添え字無し
					CASE 0
						SPRITECREATE @"%動物名:LCOUNT%", ID, 0, LCOUNT*100, 100, 100
					;1～3は正の数が添え字
					CASE 1 TO 3
						SPRITECREATE @"%動物名:LCOUNT%{LCOUNT2}", ID, LCOUNT2*100, LCOUNT*100, 100, 100
					;4～6は負の数が添え字(-1～-3)
					CASE 4 TO 6
						SPRITECREATE @"%動物名:LCOUNT%-{LCOUNT2-3}", ID, LCOUNT2*100, LCOUNT*100, 100, 100
				ENDSELECT
			NEXT
	ENDSELECT
NEXT

@植物スプライト
#DIM CONST ID = 2001
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
;以下、植物名を「植物.pngの左上から順に（左→右→一段下がってまた左→右）」と書けば画像が追加される
;現在では3行目と4行目で成長度合いの2パターンを用意
;空文字は改行
{
#DIMS DYNAMIC 植物名 = 
"プランター",
"プランター苗",
"ハーブ",
"アルストロメリア",
"イチゴ",
"もやし",
"唐辛子",
"レモン",
"ライム",
"ブルーベリー",
"ラズベリー",
"にんにく",
"薔薇",
"嘆きの樹",
"",
"苗",
"大根",
"玉ねぎ",
"さつまいも",
"長ねぎ",
"ごぼう",
"ほうれん草",
"スイカ",
"メロン",
"人参",
"とうもろこし",
"レタス",
"白菜",
"かぼちゃ",
"キャベツ",
"じゃがいも",
"",
"稲",
"トマト",
"きゅうり",
"ナス",
"ピーマン",
"りんご",
"みかん",
"ぶどう",
"カカオ",
}

SIF !GCREATEFROMFILE(ID, "植物.png")
	RETURN 

;一律100*100
FOR LCOUNT, 0, VARSIZE("植物名")
	;空文字列に当たったら改行
	IF 植物名:LCOUNT == ""
		X座標 = 0
		Y座標++
		CONTINUE
	ENDIF
	;Y座標2(画像ファイルでいう3行目)は同名植物に2種類のタイル
	IF Y座標 == 2
		SPRITECREATE @"%植物名:LCOUNT%1", ID, X座標*100, Y座標*100, 100, 100
		SPRITECREATE @"%植物名:LCOUNT%2", ID, X座標*100, Y座標*100+100, 100, 100
	ELSE
		SPRITECREATE @"%植物名:LCOUNT%", ID, X座標*100, Y座標*100, 100, 100
	ENDIF
	X座標++
NEXT

@魚スプライト
#DIM CONST ID = 2002
#DIM DYNAMIC LCOUNT
;魚は左→右 今の所1行しかないのでY座標は使わない
{
#DIMS DYNAMIC 魚名 = 
"サメ",
"タコ",
"エビ",
"イカ",
"魚",
"ウナギ",
"貝類",
"海藻",
"カニ",
}

SIF !GCREATEFROMFILE(ID, "魚.png")
	RETURN 

;一律100*100
FOR LCOUNT, 0, VARSIZE("魚名")
	SPRITECREATE @"%魚名:LCOUNT%", ID, LCOUNT*100, 0, 100, 100
NEXT

@オブジェクトスプライト
#DIM CONST ID = 3000
#DIM DYNAMIC LCOUNT
{
#DIMS DYNAMIC オブジェクト名 = 
"鶏のぬいぐるみ",
"牛のぬいぐるみ",
"羊のぬいぐるみ",
"豚のぬいぐるみ",
"馬のぬいぐるみ",
"蜂のぬいぐるみ",
}

GCREATEFROMFILE ID, "動物.png"

;一律100*100
FOR LCOUNT, 0, VARSIZE("オブジェクト名")
	SPRITECREATE @"%オブジェクト名:LCOUNT%", ID, 0, LCOUNT*100, 100, 100
NEXT
