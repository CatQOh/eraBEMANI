@TEST_CLEAR
#DIM DYNAMIC 換金

PRINTW ………………
PRINTW …………
PRINTW ……

IF FLAG:選択段位 == 1
	PRINTW 段位認定七級に合格しました。おめでとうございます。
	PRINTW 是非次回は六級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 2
	PRINTW 段位認定六級に合格しました。おめでとうございます。
	PRINTW 是非次回は五級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 3
	PRINTW 段位認定五級に合格しました。おめでとうございます。
	PRINTW 是非次回は四級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 4
	PRINTW 段位認定四級に合格しました。おめでとうございます。
	PRINTW 是非次回は三級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 5
	PRINTW 段位認定三級に合格しました。おめでとうございます。
	PRINTW 是非次回は二級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 6
	PRINTW 段位認定二級に合格しました。おめでとうございます。
	PRINTW 是非次回は一級に挑戦してみてください。
ELSEIF FLAG:選択段位 == 7
	PRINTW 段位認定一級に合格しました。おめでとうございます。
	LOADGLOBAL
	SIF 段位 < 7
		PRINTW 次回から青段位が受験可能になりました。
	PRINTW 是非次回は初段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 11
	PRINTW 段位認定初段に合格しました。おめでとうございます。
	PRINTW 是非次回は二段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 12
	PRINTW 段位認定二段に合格しました。おめでとうございます。
	PRINTW 是非次回は三段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 13
	PRINTW 段位認定三段に合格しました。おめでとうございます。
	PRINTW 是非次回は四段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 14
	PRINTW 段位認定四段に合格しました。おめでとうございます。
	PRINTW 是非次回は五段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 15
	PRINTW 段位認定五段に合格しました。おめでとうございます。
	PRINTW 是非次回は六段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 16
	PRINTW 段位認定六段に合格しました。おめでとうございます。
	PRINTW 是非次回は七段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 17
	PRINTW 段位認定七段に合格しました。おめでとうございます。
	PRINTW 是非次回は八段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 18
	PRINTW 段位認定八段に合格しました。おめでとうございます。
	LOADGLOBAL
	SIF 段位 < 18
		PRINTW 次回から赤段位が受験可能になりました。
	PRINTW 是非次回は九段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 19
	PRINTW 段位認定九段に合格しました。おめでとうございます。
	PRINTW 是非次回は十段に挑戦してみてください。
ELSEIF FLAG:選択段位 == 20
	PRINTW 段位認定十段に合格しました。おめでとうございます。
	LOADGLOBAL
	SIF 段位 < 20
		PRINTW 次回から中伝が受験可能になりました。
	PRINTW 是非次回は中伝に挑戦してみてください。
ELSEIF FLAG:選択段位 == 30
	PRINTW 段位認定中伝に合格しました。おめでとうございます。
	LOADGLOBAL
	SIF 段位 < 30
		PRINTW 次回から皆伝が受験可能になりました。
	PRINTW 是非次回は皆伝に挑戦してみてください。
ELSEIF FLAG:選択段位 == 573
	PRINTW 段位認定皆伝に合格しました。おめでとうございます。
	PRINTW あなたにはこのゲームをマスターしたことを名乗る資格があります。
	PRINTW ここまで遊んでいただいてありがとうございます。
	PRINTW これからもeraBEMANIをよろしくお願いします！
	;実績「段位認定皆伝をクリアする」
	CALL 実績解除, "皆伝？これからもよろしくな"
ELSEIF FLAG:選択段位 == 999
	PRINTW 段位認定overjoyに合格しました。おめでとうございます。
	PRINTW 段位認定をここまで攻略したあなたにはもう調教できないものなどありません。
	PRINTW これでも物足りないと思ったあなた、ぜひ新たなアイディアを募集しています。
ELSEIF FLAG:選択段位 == 99
	PRINTW 段位認定EDITに合格しました。
	PRINTW 「このEDIT楽しかったな」って思ったらぜひスレ等に報告してみてください。
ELSE
	PRINTW オリジナルコースに合格しました。おめでとうございます。
ENDIF

PRINTL 

LOADGLOBAL
IF 段位 < FLAG:選択段位 && !INRANGE(FLAG:選択段位, 50, 99)
	PRINTW 段位情報をセーブします。
	段位 = FLAG:選択段位
	SAVEGLOBAL
	PRINTW セーブが完了しました。
ENDIF

PRINTL ゲームを続けますか？
PRINTL 続ける場合、段位ゲージを所持金に換算したあとに、
PRINTL 難易度を選択し全能力を引き継いでPREMIUM FREEに移行します
PRINTL 
PRINTL [0] - 続行する
PRINTL [1] - 終了する

DO
	INPUT
	IF RESULT == 0
		PRINTL 段位ゲージを所持金に換算します
		CALL PASELIL, @"段位ゲージ{FLAG:段位ゲージ*2}\% × 2000 ＝ {FLAG:段位ゲージ * 4000}＄"
		換金 = FLAG:段位ゲージ * 4000
		;オリジナルコースでない場合、ボーナスの計算
		IF !INRANGE(FLAG:選択段位, 50, 90)
			IF CHARANUM == 5
				;ALL JUSTに相当(全員未売却で1人以上陥落)
				IF CFLAG:1:陥落 || CFLAG:2:陥落 || CFLAG:3:陥落 || CFLAG:4:陥落
					;ボーダークリア
					IF FLAG:段位ゲージ == 1
						換金 += 133333
						SETCOLOR 250, 90, 250
						PRINT PERFECT
						RESETCOLOR
						PRINT  + BORDER  
						SETCOLOR 250, 90, 250
						CALL PASELIL, @"133333＄"
						RESETCOLOR
						;実績「段位認定で133333パセリのボーナスを得る」
						CALL 実績解除, "この実績が達成できてしまったあなたは立派なビーマー"
					ELSE
						換金 += 50000
						SETCOLOR 250, 90, 250
						CALL PASELIL, @"PERFECT  50000＄"
						RESETCOLOR
					ENDIF
				;PERFECTに相当(全員未売却)
				ELSE
					;ボーダークリア
					IF FLAG:段位ゲージ == 1
						換金 += 11900
						CALL PASELIL, @"PERFECT + BORDER  11900＄"
					ELSE
						換金 += 3110
						CALL PASELIL, @"PERFECT  3110＄"
					ENDIF
				ENDIF
			;ボーダークリアのみ
			ELSEIF FLAG:段位ゲージ == 1
				換金 += 5730
				CALL PASELIL, @"BORDER  5730＄"
			ENDIF
		ENDIF
		IF ストック
			CALL PASELIL, @"ストック：{ストック}＄"
			換金 += ストック
			ストック = 0
		ENDIF
		CALL PASELIW, @"{換金}＄に換算しました"
		MONEY += 換金
		CALL 現金出納帳記録, @"段位ゲージ換金/{換金}"
		売却禁止 = 0
		PRINTL 難易度を選択してください
		PRINTL 
		PRINTL [0] - NORMAL
		PRINTL [1] - HYPER
		PRINTL [2] - ANOTHER
		PRINTL [3] - LEGGENDARIA
		CALL INPUTF, 0, 1, 2, 3
		FLAG:難易度 = RESULT+1
		PRINTW PREMIUM FREEに移行します
		FLAG:ゲームモード = 9
		CALL プレイログ記録, @"段位認定「%受験段位名%」合格"
		BEGIN SHOP
	ELSEIF RESULT == 1
		PRINTW ゲームを終了します。おつかれさまでした。
		QUIT
	ENDIF
LOOP LOOPRES(0, 1)

RETURN 0

@TEST_FAILED
PRINTW ………………
PRINTW …………
PRINTW ……
PRINTL ゲージが0%になってしまったため、段位認定は不合格です。
PRINTW またの挑戦をお待ちしております。
PRINTL [0] - タイトルに戻る
PRINTL [1] - 諦める
CALL INPUTF, 0, 1
SELECTCASE RESULT
	CASE 0
		CALL RESET_DATA
		;RESETDATAではTARGETが1になってるので-1にする
		TARGET = -1
		BEGIN TITLE
	CASE 1
		CALL THANKYOUFORPLAYING
		QUIT
ENDSELECT
