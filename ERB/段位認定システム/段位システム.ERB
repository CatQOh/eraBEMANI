;段位認定における1日ごとのゲージの変動を管理
@段位ゲージ処理
#DIM DYNAMIC ストレス値合計

IF MONEY >= 4000
	;値を格納
	LOCAL = MONEY
	LOCAL:1 = FLAG:段位ゲージ*2
	LOCAL:2 = ストック
	PRINT 所持金を段位ゲージに換算しました。
	FLAG:段位ゲージ += MONEY/4000
	MONEY = MONEY%4000
	;このあと1減らすためゲージの最大は51(102%)
	IF FLAG:段位ゲージ > 51
		PRINT (※回復量がカンストしています)
		ストック += 4000*(FLAG:段位ゲージ-51)
		ストック -= 3999-MONEY
		FLAG:段位ゲージ = 51
		MONEY = 3999
		PRINTFORM ストック {LOCAL:2} → {ストック}
	ENDIF
	PRINTL 
	PRINTFORML 所持金 {LOCAL} → {MONEY}
	PRINTFORML 段位ゲージ {LOCAL:1} → {FLAG:段位ゲージ*2}
ENDIF

VARSET LOCAL, 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:状態 >= 1
		LOCAL++
	;裏七級の処理(奴隷を崩壊させると即終了)
	SIF 受験段位名 == "裏七級" && TALENT:COUNT:崩壊
		FLAG:段位ゲージ = 0
REND

;クリア判定
;まずは特殊勝利から
IF 受験段位名 == "いちかのアダルトチャンネル"
	IF ストック >= 100000
		CALL FADEOUTBGM
		CALL TEST_CLEAR
	ENDIF
;通常のクリア条件
ELSE
	IF LOCAL >= (CHARANUM-1)
		CALL FADEOUTBGM
		CALL TEST_CLEAR
	ENDIF
ENDIF

;上のクリア判定をスルーした場合、ゲージを減らす処理が入る
IF 受験段位名 == "少女達の芳香は艶やかに、猛き賓客を癒し惑わす。"
;オリジナルコース15では「混乱少女♥そふらんちゃん!!」にちなんでゲージの減りが不規則
;次の日のDAYについて、[約数の個数]%だけゲージが減る(「FLAG:段位ゲージ」では半分の値をとっていることに注意)
;平方数の約数は真ん中の数を二重に数える(例:次が36日目…1,36と2,18と3,12と4,9と6,6で10%減少)
	FLAG:段位ゲージ -= SIGMA(DAY+1, 0)/2
	SIF ISSQUARE(DAY+1)
		FLAG:段位ゲージ -= 1
ELSEIF 受験段位名 == "HELL CHARGE NOTES"
	REPEAT CHARANUM
		ストレス値合計 += CFLAG:COUNT:ストレス値
	REND
	ストレス値合計 /= 2
	SIF ストレス値合計 < 1
		ストレス値合計 = 1
	FLAG:段位ゲージ -= ストレス値合計
ELSEIF 受験段位名 == "BisCoのEX-HARD☆大作戦"
	FLAG:段位ゲージ -= 5
ELSEIF 受験段位名 == "何かを間違えた房中術"
	FLAG:段位ゲージ -= 10
ELSE
	FLAG:段位ゲージ -= 1
ENDIF
IF FLAG:段位ゲージ <= 0
	CALL FADEOUTBGM
	CALL TEST_FAILED
ENDIF

;段位ゲージに応じてBGMが変わる
@段位認定_BGM
SELECTCASE FLAG:段位ゲージ
	CASE 0 TO 11
		IF PLAYINGBGM != "段位_4"
			PLAYBGM 段位_4
			PLAYINGBGM = 段位_4
		ENDIF
	CASE 12 TO 25
		IF PLAYINGBGM != "段位_3"
			PLAYBGM 段位_3
			PLAYINGBGM = 段位_3
		ENDIF
	CASE 26 TO 38
		IF PLAYINGBGM != "段位_2"
			PLAYBGM 段位_2
			PLAYINGBGM = 段位_2
		ENDIF
	CASE 39 TO 50
		IF PLAYINGBGM != "段位_1"
			PLAYBGM 段位_1
			PLAYINGBGM = 段位_1
		ENDIF
ENDSELECT

;段位ゲージの表示
@段位ゲージ表示
#DIM DYNAMIC EXH
SELECTCASE 受験段位名
	CASE "BisCoのEX-HARD☆大作戦", "何かを間違えた房中術"
		EXH = 1
ENDSELECT
PRINTFORM 段位ゲージ {FLAG:段位ゲージ*2}\%[
REPEAT FLAG:段位ゲージ
	IF EXH
		SETCOLOR 220, 255, 0
	ELSE
		SETCOLOR 255, 0, 0
	ENDIF
	PRINT |
	RESETCOLOR
REND
REPEAT 50-FLAG:段位ゲージ
	SETCOLOR 128, 128, 128
	PRINT |
	RESETCOLOR
REND
PRINT ]

@GORILLA_ITEM
DRAWLINE
CALL 段位ゲージ表示
PRINTL 
DRAWLINE
CALL ITEMPRINT
DRAWLINE
PRINTL □段位認定試験では通貨の代わりにゲージを消耗します。
PRINTL 　購入したいアイテムを選んでください。
PRINTL
SIF !ITEM:ローター
	PRINTL 　[0] ローター（ゲージ2%）
SIF !ITEM:オナホール
	PRINTL 　[1] オナホール（ゲージ4%）
SIF !ITEM:バイブ
	PRINTL 　[2] バイブ（ゲージ4%）
SIF !ITEM:アナルバイブ
	PRINTL 　[3] アナルバイブ（ゲージ6%）
SIF !ITEM:アイマスク
	PRINTL 　[4] アイマスク（ゲージ4%）
SIF !ITEM:縄
	PRINTL 　[5] 縄（ゲージ6%）
SIF !ITEM:ビデオカメラ
	PRINTL 　[6] ビデオカメラ テープ×3付属（ゲージ10%）
SIF ITEM:ビデオカメラ
	PRINTL 　[7] ビデオテープ×3（ゲージ6%）
PRINTL 　[8] ローション×5（ゲージ2%）
PRINTL 　[9] 媚薬（ゲージ2%）
DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [10] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			IF ITEM:ローター
				PRINTW ローターは既に所持しています
			ELSEIF FLAG:段位ゲージ > 1
				PRINTW ＜ローターを購入しました＞
				ITEM:ローター = 1
				FLAG:段位ゲージ -= 1
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 1
			IF ITEM:オナホール
				PRINTW オナホールは既に所持しています
			ELSEIF FLAG:段位ゲージ > 2
				PRINTW ＜オナホールを購入しました＞
				ITEM:オナホール = 1
				FLAG:段位ゲージ -= 2
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 2
			IF ITEM:バイブ
				PRINTW バイブは既に所持しています
			ELSEIF FLAG:段位ゲージ > 2
				PRINTW ＜バイブを購入しました＞
				ITEM:バイブ = 1
				FLAG:段位ゲージ -= 2
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 3
			IF ITEM:アナルバイブ
				PRINTW アナルバイブは既に所持しています
			ELSEIF FLAG:段位ゲージ > 3
				PRINTW ＜アナルバイブを購入しました＞
				ITEM:アナルバイブ = 1
				FLAG:段位ゲージ -= 3
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 4
			IF ITEM:アイマスク
				PRINTW アイマスクは既に所持しています
			ELSEIF FLAG:段位ゲージ > 2
				PRINTW ＜アイマスクを購入しました＞
				ITEM:アイマスク = 1
				FLAG:段位ゲージ -= 2
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 5
			IF ITEM:縄
				PRINTW 縄は既に所持しています
			ELSEIF FLAG:段位ゲージ > 3
				PRINTW ＜縄を購入しました＞
				ITEM:縄 = 1
				FLAG:段位ゲージ -= 3
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 6
			IF ITEM:ビデオカメラ
				PRINTW ビデオカメラは既に所持しています
			ELSEIF FLAG:段位ゲージ > 5
				PRINTW ＜ビデオカメラを購入しました＞
				ITEM:ビデオカメラ = 1
				ITEM:ビデオテープ += 3
				FLAG:段位ゲージ -= 5
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 7
			IF ITEM:ビデオテープ >= 97
				PRINTW ビデオテープはこれ以上持てません
			ELSEIF FLAG:段位ゲージ > 3
				PRINTW ＜ビデオテープ×3を購入しました＞
				ITEM:ビデオテープ += 3
				FLAG:段位ゲージ -= 3
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 8
			IF ITEM:ローション >= 95
				PRINTW ローションはこれ以上持てません
			ELSEIF FLAG:段位ゲージ > 1
				PRINTW ＜ローション×5を購入しました＞
				ITEM:ローション += 5
				FLAG:段位ゲージ -= 1
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 9
			IF ITEM:媚薬 >= 99
				PRINTW 媚薬はこれ以上持てません
			ELSEIF FLAG:段位ゲージ > 1
				PRINTW ＜媚薬を購入しました＞
				ITEM:媚薬 += 1
				FLAG:段位ゲージ -= 1
			ELSE
				PRINTW ゲージが足りません
			ENDIF
		CASE 10
			RETURN 0
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6, 7, 8, 9)

RESTART

;数値に応じた段位を色付き文字で表示する関数 色変更するから式中関数化は無理
@GRADE, ARG
SELECTCASE ARG
	;緑段位
	CASE 1 TO 7
		SETCOLOR 140, 240, 140
	;青段位
	CASE 11 TO 18
		SETCOLOR 170, 220, 230
	;赤段位
	CASE 19, 20
		SETCOLOR 250, 150, 150
	;中伝
	CASE 30
		SETCOLOR 240, 240, 240
	;皆伝、裏七級
	CASE 573, 50
		SETCOLOR 250, 180, 0
	;overjoy
	CASE 999
		SETCOLOR 255, 255, 255
ENDSELECT

SELECTCASE ARG
	;緑段位
	CASE 1, 50
		PRINT 七級
	CASE 2
		PRINT 六級
	CASE 3
		PRINT 五級
	CASE 4
		PRINT 四級
	CASE 5
		PRINT 三級
	CASE 6
		PRINT 二級
	CASE 7
		PRINT 一級
	;青段位
	CASE 11
		PRINT 初段
	CASE 12
		PRINT 二段
	CASE 13
		PRINT 三段
	CASE 14
		PRINT 四段
	CASE 15
		PRINT 五段
	CASE 16
		PRINT 六段
	CASE 17
		PRINT 七段
	CASE 18
		PRINT 八段
	;赤段位
	CASE 19
		PRINT 九段
	CASE 20
		PRINT 十段
	;中伝・皆伝
	CASE 30
		PRINT 中伝
	CASE 573
		PRINT 皆伝
	;overjoy
	CASE 999
		PRINT (^^)
	;EDIT,無名
	CASE 99
		PRINT EDIT
	CASE 0
		PRINT 無名
	;オリジナルコース
	CASE 51 TO 90
		PRINT ORIGINAL
ENDSELECT

RESETCOLOR

;ヤンデレ
SIF ARG == 80
	SETCOLORBYNAME red

