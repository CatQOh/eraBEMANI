@STRATEGY
DRAWLINE
IF ITEM:風俗店営業許可証
	PRINTL 　[1] - ゲームセンター経営
	PRINTL 　[2] - 娼館経営
ELSE
	SETCOLORBYNAME gray
	PRINTL 　[-] - ゲームセンター経営（風俗店営業許可証が必要です）
	PRINTL 　[-] - 娼館経営（風俗店営業許可証が必要です）
	RESETCOLOR
ENDIF
PRINTPLAIN 　[3] - ゲーム開発（工事中）
PRINTL 
PRINTL 　[4] - 芸能プロダクション運営
PRINTL 　[5] - 飲食店・ホテル経営
IF STSMOD
	IF ITEM:博物館
		PRINTL 　[6] - 博物館経営
	ELSE
		CALL PRINTCOLORL, "　[-] - 博物館経営（アイテム[博物館]が必要です）", "gray"
	ENDIF
ENDIF
IF ITEM:事業開発オフィス
	PRINTL 　[7] - 出版社経営
ELSE
	CALL PRINTCOLORL, "　[-] - 出版社経営（アイテム[事業開発オフィス]が必要です）", "gray"
ENDIF
DRAWLINE
PRINTPLAIN 　STRATEGY　
PRINT [0] - 戻る
PRINTL 　[100] 各モードの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			CALL プレイ称号計算, "ゲーセン経営"
			CALL STRATEGY_GC
			RESTART
		CASE 2
			CALL プレイ称号計算, "娼館経営"
			CALL STRATEGY_BROTHEL
			RESTART
		;CASE 3
		;	CALL STRATEGY_DEVELOP
		;	RETURN 0
		CASE 4
			CALL プレイ称号計算, "芸プロ運営"
			CALL STRATEGY_IDOL
			;返り値が1ならTURNENDする
			IF RESULT == 1
				BEGIN TURNEND
				RETURN
			ENDIF
			RESTART
		CASE 5
			CALL 飲食店経営
			RESTART
		CASE 6
			IF ITEM:博物館 && STSMOD
				CALL プレイ称号計算, "芸プロ運営"
				CALL 博物館
				;返り値が1ならTURNENDする
				IF RESULT == 1
					BEGIN TURNEND
					RETURN
				ENDIF
			ENDIF
			RESTART
		CASE 7
			SIF !ITEM:事業開発オフィス
				CONTINUE
			CALL 出版社経営
			RESTART
		CASE 100
			CALL STRATEGY_TUTORIAL
			RESTART
	ENDSELECT
LOOP 1

;経営モードチュートリアル
@STRATEGY_TUTORIAL
DRAWLINE
PRINTL 経営モードへようこそ
PRINTL 経営モードは「あなた」がゲーセンなどの施設を運営して収入を得るモードです
PRINTW 予定【ゲーセン経営】【娼館】【ゲーム開発】【芸能プロダクション】などなど

@STRATEGY_INCOME
#DIM DYNAMIC LCOUNT
;休業日に営業開始して収入入るのを防ぐため
IF INRANGE(FLAG:ゲーセン営業中, 1, DAY-1)
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "ゲームセンター" || CSTR:LCOUNT:配属 == "ゲーセン売春"
			CFLAG:LCOUNT:休憩 = 0
	NEXT
	CALL INCOME_GC
	CALL INCOME_GC_BR
ENDIF
CALL CHECKDRAWLINEW

IF INRANGE(FLAG:娼館営業中, 1, DAY-1)
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "娼館"
			CFLAG:LCOUNT:休憩 = 0
	NEXT
	CALL INCOME_BR
ENDIF
CALL CHECKDRAWLINEW

IF INRANGE(FLAG:飲食店営業中, 1, DAY-1)
	FOR LCOUNT, 0, CHARANUM
		SIF CSTR:LCOUNT:配属 == "飲食店"
			CFLAG:LCOUNT:休憩 = 0
	NEXT
	CALL INCOME_飲食店
ENDIF
CALL CHECKDRAWLINEW

CALL INCOME_博物館
CALL CHECKDRAWLINEW

RETURN 0

;経営モード用の変数初期化
@CLEAR_STRATEGY_FLAG
VARSET 設置筐体:0:0, 0
VARSET 空きスペース, 0
GC物件 = 0
GC名声 = 0
GC評判 = 0
基本収入 = 0
GCメンテ = 0
GC広さ = 0
GC分煙 = 0
GC階層 = 0
GC外装 = 0
GCフードコート = 0
GCトイレ = 0

娼館レベル = 1
娼館広さ = 1
娼館部屋数 = 1
娼館方針 = 1
娼館総収入 = 0
娼館レベルアップ必要額 = 1000
娼館前日の収入 = 0

IDOL知名度 = 0

温泉レベル = 0
ホテル部屋数 = 0
飲食店名声 = 0
ホテル名声 = 0
その他業種名声 = 0
業種 = 
飲食店基本料金 = 0
温泉併設 = 0
宿併設 = 0

FLAG:ゲーセン営業中 = -1
FLAG:娼館営業中 = -1
FLAG:飲食店営業中 = -1

VARSET 飲食店メニュー
VARSET 飲食店売上
VARSET 飲食店客数
VARSET メニュー売上
VARSET 飲食店帳簿

出版総利益 = 0
前日の出版利益 = 0
出版社名声 = 0
購読者 = 0

VARSET 雑誌名
VARSET 雑誌内容
VARSET 部数
VARSET 雑誌価格
VARSET 雑誌品質
VARSET 雑誌需要
VARSET 刷数
VARSET 雑誌発売日
VARSET 雑誌出演者
VARSET 総売上部数

VARSET 編集中雑誌名
VARSET 編集中雑誌内容

@サボり判定, 対象
#FUNCTION
#DIM DYNAMIC 対象

SIF !TALENT:対象:サボり魔 || ワーカホリックレベル(対象)
	RETURNF 0
;100-モチベーションの確率で、50%の確率でサボるイベントが発生する モチベ50ならつまり25%で、100ならサボらなくなる
IF RANDPER(100-CFLAG:対象:モチベーション)
	SIF RAND:2
		RETURNF 1
ENDIF

RETURNF 0
