@STRATEGY_GC
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 定休日設定
#DIM DYNAMIC LRESULT

CALL GCバージョン管理
DRAWLINE
PRINTL □現在の規模
SIF GC店名 != "ゲームセンター"
	PRINTFORML 　店名：%GC店名%
;----------------------------------------------------
PRINTFORM 　物件：%物件名(GC物件)%
;名声表示--------------------------------------------
PRINTFORML 　名声：%GC名声グレード(GC名声)%({GC名声})
;評判表示----------------------------------------------
PRINTFORM 　評判：%GC評判グレード(GC評判)%({GC評判})
;収入表示--------------------------------------------
CALL PASELIL, @"　前日の収入：%GC収入グレード(基本収入)%({基本収入}＄)"
;メンテレベル--------------------------------------------
PRINTFORML 　メンテナンス：%GCメンテグレード(GCメンテ)%
;店長・定休日--------------------------------------------
PRINTFORML 　店長：%NAME:LIMIT(FINDCHARA(CFLAG:ゲーセン店長, 1), 0, CHARANUM)%　定休日：\@ ゲーセン定休日 == "" ? 無し # %ゲーセン定休日%曜日 \@
PRINTL 

HTML_PRINT SET_TOOLTIP("　[1]  - 物件購入", "物件を購入します")
IF !GC物件
	HTML_PRINT SET_TOOLTIP("　[-]  - 物件増築", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-]  - 筐体購入", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-]  - 筐体設置", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-]  - 店名変更", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-]  - 通常営業", "物件を購入してください", 0, "gray")
	HTML_PRINT SET_TOOLTIP("　[-]  - 定休日設定", "物件を購入してください", 0, "gray")
ELSE
	HTML_PRINT SET_TOOLTIP("　[2]  - 物件増築", "物件を増築、改装します")
	HTML_PRINT SET_TOOLTIP("　[3]  - 筐体購入", "新しいアーケードゲーム筐体を購入します")
	HTML_PRINT SET_TOOLTIP("　[4]  - 人員配置", "ゲームセンターに人員を配置します")
	HTML_PRINT SET_TOOLTIP("　[5]  - 筐体設置", "ゲームセンターにアーケードゲーム筐体を設置します")
	HTML_PRINT SET_TOOLTIP("　[6]  - 店長任命", "ゲームセンターの人員から店長を任命します")
	HTML_PRINT SET_TOOLTIP("　[7]  - 店名変更", "ゲームセンターの店名を変更します")
	HTML_PRINT SET_TOOLTIP("　[8]  - 定休日設定", "ゲームセンターの定休日を決めます")
	IF FLAG:ゲーセン営業中
		HTML_PRINT SET_TOOLTIP("　[9]  - 一時休業", "ゲームセンターを一時休業し、収入と支出が行われないようにします")
	ELSE
		HTML_PRINT SET_TOOLTIP("　[9]  - 通常営業", "ゲームセンターの営業を再開し、収入と支出が行われるようにします")
	ENDIF
	SIF FINDCHARA(CSTR:配属, "ゲーセン売春") >= 0
		HTML_PRINT SET_TOOLTIP("　[10] - 売春方針", "ゲームセンターで売春させるキャラに方針を命じます")
ENDIF

DRAWLINE
PRINTPLAIN 　GAME CENTER　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 2 TO 5
			SIF !GC物件
				CONTINUE
	ENDSELECT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL GC_PROPERTY
		CASE 2
			CALL GC_RENOVATION
		CASE 3
			CALL GC_ARCADEGAME
		CASE 4
			PRINTL 配属する部署を選んでください
			PRINTL [0] - 経営部(通常のゲームセンター勤務)
			PRINTL [1] - 売春部(ゲームセンターで客を誘惑する)
			CALL INPUTF, 0, 1
			SELECTCASE RESULT
				CASE 0
					CALL STRATEGY_PERSONAL, "ゲームセンター"
				CASE 1
					CALL STRATEGY_PERSONAL, "ゲーセン売春"
			ENDSELECT
		CASE 5
			CALL GC_MAP, 1
		CASE 6
			CALL GC_LEADER
		CASE 7
			PRINT □店名を入力してください
			SIF GC店名 != "ゲームセンター"
				PRINTFORM  現在の店名：%GC店名%
			PRINTL 
			PRINTBUTTON "[変更しない]", "変更しない"
			PRINTL 
			INPUTS
			SIF RESULTS == "変更しない"
				RESTART
			PRINTFORMW 店名を「%RESULTS%」に変更しました
			GC店名 = %RESULTS%
		CASE 8
			VARLINE = 0
			DO
				CALL LOOP_CLEARLINE
				定休日設定 = 
				DRAWLINE
				PRINTL □定休日を選んでください
				PRINTFORML 　[1] - 日曜日（\@ GC定休日("日") ? 定休 # 営業 \@）
				PRINTFORML 　[2] - 月曜日（\@ GC定休日("月") ? 定休 # 営業 \@）
				PRINTFORML 　[3] - 火曜日（\@ GC定休日("火") ? 定休 # 営業 \@）
				PRINTFORML 　[4] - 水曜日（\@ GC定休日("水") ? 定休 # 営業 \@）
				PRINTFORML 　[5] - 木曜日（\@ GC定休日("木") ? 定休 # 営業 \@）
				PRINTFORML 　[6] - 金曜日（\@ GC定休日("金") ? 定休 # 営業 \@）
				PRINTFORML 　[0] - 土曜日（\@ GC定休日("土") ? 定休 # 営業 \@）
				PRINTL 　[9] - 設定終了
				INPUT
				LRESULT = RESULT
				SELECTCASE LRESULT
					CASE 0 TO 6
						FOR LCOUNT, 0, 7
							IF LRESULT == LCOUNT
								IF !GC定休日(DOW(LRESULT))
									ゲーセン定休日 += @"・%DOW(LRESULT)%"
								ELSE
									CONTINUE
								ENDIF
							ENDIF
							IF GC定休日(DOW(LCOUNT))
								IF 定休日設定 == ""
									定休日設定 = %DOW(LCOUNT)%
								ELSE
									定休日設定 += @"・%DOW(LCOUNT)%"
								ENDIF
							ENDIF
						NEXT
						ゲーセン定休日 = %定休日設定%
					CASE 9
						BREAK
				ENDSELECT
			LOOP 1
		CASE 9
			IF FLAG:ゲーセン営業中 > 0
				PRINTFORMW %GC店名%の経営を一時中断しました
				;定休日じゃない休業は-1
				FLAG:ゲーセン営業中 = -1
			ELSE
				PRINTFORMW %GC店名%の経営を開始しました
				FLAG:ゲーセン営業中 = DAY
			ENDIF
		CASE 10
			SIF FINDCHARA(CSTR:配属, "ゲーセン売春") == -1
				CONTINUE
			DO
				DRAWLINE
				PRINTL □方針を変更するキャラを選んでください
				FOR LCOUNT, 0, CHARANUM
					SIF CSTR:LCOUNT:配属 != "ゲーセン売春"
						CONTINUE
					PRINTFORM 　[{LCOUNT, 2}] - %NAME:LCOUNT% 現在の方針:
					SELECTCASE CFLAG:LCOUNT:ゲーセン売春方針
						CASE 0
							PRINTL ホール待機
						CASE 1
							PRINTL ホール待機(清潔)
						CASE 2
							PRINTL トイレ待機
						CASE 3
							PRINTL お持ち帰り待機
					ENDSELECT
				NEXT
				PRINTL 　[ 0] - 戻る
				INPUT
				SIF RESULT == 0
					RESTART
				SIF CSTR:RESULT:配属 != "ゲーセン売春"
					CONTINUE
				PRINTFORML □%CALLNAME:RESULT%の売春方針を指定してください
				LRESULT = RESULT
				HTML_PRINT SET_TOOLTIP("　[0] - ホールで待機させる", "ホールで客引きをさせます")
				HTML_PRINT SET_TOOLTIP("　[1] - ホールで待機させる(清潔)", "ホールで客引きをさせますが、従業員や他の客にばれないように設備を汚させないようにします")
				IF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
					HTML_PRINT SET_TOOLTIP("　[-] - トイレで待機させる", "[淫乱][娼婦]、もしくは50人以上との性経験が必要です", 0, "gray")
				ELSE
					HTML_PRINT SET_TOOLTIP("　[2] - トイレで待機させる", "トイレで肉便器になるように命じます")
				ENDIF
				;IF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
				;	HTML_PRINT SET_TOOLTIP("　[-] - お持ち帰り待機", "[淫乱][娼婦]、もしくは50人以上との性経験が必要です", 0, "gray")
				;ELSEIF !TALENT:LRESULT:音ゲーマー
				;	HTML_PRINT SET_TOOLTIP("　[-] - お持ち帰り待機", "ある程度、客との共通の会話ができるスキル[音ゲーマー]が必要です", 0, "gray")
				;ELSE
				;	HTML_PRINT SET_TOOLTIP("　[3] - お持ち帰り待機", "客と一夜を共にさせます")
				;ENDIF

				CALL INPUTF, 0, 1, 2

				SELECTCASE RESULT
					CASE 0, 1
						CFLAG:LRESULT:ゲーセン売春方針 = RESULT
					CASE 2
						SIF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
							CONTINUE
						CFLAG:LRESULT:ゲーセン売春方針 = 2
					;CASE 3
					;	SIF FALLTYPE(LRESULT) != 2 && 経験人数(LRESULT) < 50
					;		CONTINUE
					;	SIF !TALENT:LRESULT:音ゲーマー
					;		CONTINUE
					;	CFLAG:LRESULT:ゲーセン売春方針 = 3
					;	BREAK
				ENDSELECT
				CONTINUE
			LOOP 1
		CASE 100
			CALL GC_TUTORIAL
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

;----------------------------------------------------
;ゲームセンター経営モードチュートリアル
;----------------------------------------------------
@GC_TUTORIAL
DRAWLINE
PRINTL 当モードはゲームセンターを経営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「物件購入」とは？
PRINTL [2] - 「物件増築」とは？
PRINTL [3] - 「筐体購入」とは？
PRINTL [4] - 「人員配置」とは？
PRINTL [5] - 「筐体設置」とは？
PRINTL [6] - 「店長任命」とは
PRINTL [7] - 「店名変更」とは
PRINTL [8] - 「定休日設定」とは？
PRINTL [9] - 「一時休業/通常営業」とは？
PRINTL [10] - 「売春方針」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 「物件購入」は筐体を置く物件を購入するコマンドです。
			PRINTL これが無いと話にならないので、まずはボロ屋でもいいので購入してみましょう。
			PRINTL 物件は大きく「所有物件」と「テナント」に分けられます。
			PRINTL 「所有物件」は物件購入の際の経費が高いですが、運営費が安く、増築も行えるので汎用性に長けます。
			PRINTW 「テナント」はその逆で、運営費が高く、物件購入の経費が安い他、増築は行なえませんが所有物件よりも収入の安定性が高いのがメリットです。
		CASE 2
			PRINTL 「物件増築」は購入した物件を増築し、筐体を置けるスペースを広げたり、設備を向上させるコマンドです
			PRINTL 物件には「広さ」「分煙施設」「階層」「外装」「フードコート」「トイレ」の設備があり、それぞれ改築することにより様々な面での経済効果がアップします
			PRINTL 分煙施設とフードコートは一部の物件には初期から備えられており、有無で判断するので改築は一回で済みます
			PRINTL 他の施設はいくらでも増築できるので、その分経済効果も尻上がりになります。
			PRINTW しかし増築すればするほど、増築に必要な経費も上がり運営費も上昇するので、計画的な増築が必要になるでしょう
		CASE 3
			PRINTL 「筐体購入」はゲーセンに設置するための筐体を購入するコマンドです。
			PRINTW 当バージョンでは少なく、これからも増やして行く予定……ですが予定は未定。
		CASE 4
			PRINTL 「人員配置」はゲーセン経営に人員を配置するコマンドです。
			PRINTL 性格や適性、能力に応じて名声や評判、メンテナンスレベルから収入まで様々な要素に影響があります。
			PRINTW 特に序盤は名声を上げることが大事なので、配属できるキャラは配属してみるのがいいでしょう。
		CASE 5
			PRINTL 「筐体設置」は所有する物件に筐体を設置するコマンドです。
			PRINTL 筐体は所持するだけなら経費はかかりませんが、設置した当日から経費がかかるので所持金とよく相談して決めましょう。
			PRINTW なお、筐体にはそれぞれ大きさがあり、経済効果の高い筐体ほど大きくなるのでスペースには気を使いましょう。
		CASE 6
			PRINTL 「店長任命」は配属してるキャラの中から店長を任命するコマンドです。
			PRINTL 店長に任命されたキャラはゲーセンの各ステータス・レベルへの影響が大きくなり、より多くの経験が得られるようになります。
			PRINTW 優秀な人材や、積極的に能力を伸ばしていきたいキャラを任命することにより、効率的な運営が可能になります。
		CASE 7
			PRINTL 「店名変更」はゲームセンターの店名を変更するコマンドです。
			PRINTW デフォルトのままでも変更しても収入や名声等への影響はありません。自分の好きな名前を付けましょう。
		CASE 8
			PRINTL 「定休日設定」はゲームセンターの定休曜日を設定するコマンドです。
			PRINTL 配属したキャラたちは能力にもよりますがストレスが溜まりやすくなります。
			PRINTW 適度に休みを設け、キャラたちのメンタルケアを行えばより効率的な運営ができるでしょう。
		CASE 9
			PRINTL 「一時休業/通常営業」はゲームセンターの営業を停止したり再開するコマンドです。
			PRINTW 配属したキャラクターの体調管理などで長期休暇が必要な場合、回復するまで営業を停止するのも良いでしょう。
		CASE 10
			PRINTL 「売春方針」は売春部に配属したキャラクターの売春方針を指定するコマンドです。
			PRINTL 激しく淫らな方針のほうが稼げる額も多くなります。いろいろ試してみましょう。
			PRINTW また、現在ではキャラと客間の妊娠については非対応です。
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

;物件購入処理----------------------------------------------------
@GC_PROPERTY
#DIM DYNAMIC 階層
#DIM DYNAMIC 筐体
#DIM DYNAMIC 総スコア
#DIM DYNAMIC LRESULT

総スコア = GC名声+GC評判
DRAWLINE
PRINTL 【物件購入】
PRINTL 名声や評判によって買える物件も変わります
IF !ORIGIN
	PRINT 「物件名」(金額 - 日毎の基本維持費) 
	CALL PASELIL, @"所持金：{MONEY}＄"
ENDIF
PRINTL 
CALL PASELIL, @"　[1] - ボロ屋\@ !ORIGIN ? (30000＄ - 300＄～)#\@"
CALL PASELIL, @"　[2] - 中小スーパーのテナント\@ !ORIGIN ? (5000＄ - 1000＄～)#\@"
SIF 総スコア >= 5000
	CALL PASELIL, @"　[3] - 商店街の一角\@ !ORIGIN ? (50000＄ - 1200＄～)#\@"
SIF 総スコア >= 10000
	CALL PASELIL, @"　[4] - 駅中テナント\@ !ORIGIN ? (10000＄ - 3000＄～)#\@"
SIF 総スコア >= 30000
	CALL PASELIL, @"　[5] - 街中物件\@ !ORIGIN ? (100000＄ - 2000＄～)#\@"
SIF 総スコア >= 50000
	CALL PASELIL, @"　[6] - 百貨店テナント\@ !ORIGIN ? (30000＄ - 10000＄～)#\@"
SIF 総スコア >= 100000
	CALL PASELIL, @"　[7] - アミューズメント施設\@ !ORIGIN ? (300000＄ - 5000＄～)#\@"
DRAWLINE
PRINTPLAIN 　PROPERTY　
PRINTL [0] - 戻る　

DO
	INPUT
	LRESULT = RESULT
	SELECTCASE LRESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 【ボロ屋】
			PRINTL 
			PRINTL その名の通りボロっちい建物。
			PRINTL 整備すればゲーセンとして使えるかもしれないが……
			PRINTL 狭いため筐体もあまり置けず、高い経済効果は期待できない。
		CASE 2
			PRINTL 【中小スーパーのテナント】
			PRINTL 
			PRINTL 時々スーパーの片隅に申し訳程度の筐体が置いてあるあのスペース。
			PRINTL ゲーム目的で来る人はほとんど居ないと思うが、お買い物に飽きた子どもたちがよく訪れる。
			PRINTL ボロ屋と比べると経済効果はだいぶマシではあるが、こちらはテナントなので維持費が高い。
			PRINTL 他にも改装ができないなど、使い勝手の悪い部分がある。
		CASE 3
			PRINTL 【商店街の一角】
			PRINTL 
			PRINTL 商店街といっても昔ながらの風情のあるような場所ではなく、ちょっとした飲食店やオシャレな店や並ぶいわゆる「アーケード街」
			PRINTL 改築の自由度もそこそこ高く、周囲にもいろいろな店が並ぶので、お客さんが多い。
			PRINTL 維持費も安く、初期設備も平均的なため、経営に慣れてきたらここを使ってみるのもいいだろう。
		CASE 4
			PRINTL 【駅中テナント】
			PRINTL 
			PRINTL 都会の駅中にあるスペースを間借りしたもの。
			PRINTL 最低限の設備は整っており、全体的に水準以上の環境でである。
			PRINTL イニシャルコストは安いが、増築もできずランニングコストも結構かかるため、計画的な経営ができないと破産する可能性も高い。注意しよう。
		CASE 5
			PRINTL 【街中物件】
			PRINTL 
			PRINTL 環境、土地共に優れていて、人通りも多い街中の物件。
			PRINTL 特徴としては分煙施設は無いが、ここは二階建てなので広さの倍の筐体が置ける。
			PRINTL 増築もできるので、買うことさえできればかなり使い勝手のいい物件である。
		CASE 6
			PRINTL 【百貨店テナント】
			PRINTL 
			PRINTL 百貨店内に間借りする物件。
			PRINTL 経済効果が非常に高いが、その分経費も馬鹿にならないため、店を構える際はよく考えよう。
			PRINTL こちらは街中物件とは違い、一階しか無く、増築も行えないが、フードコートと分煙施設は付いている。
		CASE 7
			PRINTL 【アミューズメント】
			PRINTL 
			PRINTL 当バージョンでは最高ランクの施設。
			PRINTL 初期コストが30万、基本経費が5000ととんでもないコストがかかるが、経済効果もコスト同等のものだと思っていいだろう。
			PRINTL 初期から二階建て、所有物件唯一の分煙設備、他高水準の設備もあり、フードコートもある。
			PRINTL 増築も自由にできるため、経営での物件は最終的にここに行き着くだろう。
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 0

IF MONEY < 物件価格(LRESULT) && !ORIGIN
	PRINTW 所持金が足りません
	RESTART
ELSEIF GC物件 == LRESULT
	PRINTW 現在所持している物件です
	RESTART
ENDIF

IF !ORIGIN
	PRINTL 所持金変動
	CALL PASELIL, @"{MONEY}＄ > {MONEY - 物件価格(LRESULT)}＄"
	PRINTL 
ENDIF
PRINTL 購入しますか？
PRINTL [0] - 購入しない
PRINTL [1] - 購入する

CALL INPUTF, 0, 1
SIF RESULT == 0
	RESTART

IF GC物件
	PRINTFORML 現在%物件名(GC物件)%を所持しています。本当に買い替えますか？
	PRINTFORML （編集した%物件名(GC物件)%のマップは初期化されます）
	PRINTL [0] - それは困る
	PRINTL [1] - 大丈夫だ、問題ない
	CALL INPUTF, 0, 1
	SIF RESULT == 0
		RETURN
	VARSET GCオブジェクト
	CALL 設置済み筐体計算
ELSE
	GC店名 = ゲームセンター
ENDIF

PRINTFORMW ＜%物件名(LRESULT)%を購入しました＞
MONEY -= 物件価格(LRESULT)
IF FLAG:パートナー > 0
	SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
		TRYCALLFORM BUY_OBJECT_%CSVNAMERPS(FLAG:パートナー)%, LRESULT
ENDIF
FLAG:ゲーセン営業中 = DAY

;初期値設定-------------------
;物件情報--------------
GC物件 = LRESULT

;広さ初期値------------
GC広さ = LRESULT*2

;分煙施設--------------
SELECTCASE LRESULT
	;テナントとアミューズメントには分煙施設が付く
	CASE 2, 4, 6, 7
		GC分煙 = 1
ENDSELECT

;階層初期値------------
GC階層 = 1
空きスペース:1 = GC広さ
;街中とアミューズメントは2階建て
SELECTCASE LRESULT
	CASE 5, 7
		GC階層 = 2
		空きスペース:2 = GC広さ
ENDSELECT

;外装初期値------------
GC外装 = LRESULT/2+1

;フードコート----------
SELECTCASE LRESULT
	;百貨店とアミューズメントにはフードコートが付く
	CASE 6, 7
		GCフードコート = 1
ENDSELECT

;お手洗い--------------
GCトイレ = LRESULT/2+1

RETURN

;物件増築処理----------------------------------------------------
@GC_RENOVATION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 費用, 7

DRAWLINE
PRINTL 【物件増築】
PRINTL 物件を増築できます。
PRINTL スペースを広げたり設備を強化すると、マップが広がったり評判が上がったりします。
DRAWLINE
PRINTFORMLC 　物件：%物件名(GC物件)%
CALL PASELIL, @"所持金：{MONEY}＄"
SELECTCASE GC分煙
	CASE 0
		PRINTLC 　分煙施設：無
	CASE 1
		PRINTLC 　分煙施設：有
ENDSELECT
PRINTFORMLC 階層：{GC階層}階建

PRINTFORMLC 広さ：{GC広さ}

PRINTL 

PRINTFORMLC 　外装レベル：{GC外装}
SELECTCASE GCフードコート
	CASE 0
		PRINTLC フードコート：無
	CASE 1
		PRINTFORMLC フードコート：有
ENDSELECT
PRINTFORMLC お手洗いレベル：{GCトイレ}

PRINTL 
DRAWLINE

;広さは最大で20まで
費用:1 = GC広さ*30000+50000
費用:2 = 30000
;階層は100まで
費用:3 = (GC階層*GC階層)*1000000
;外装も100まで
費用:4 = GC外装*100000
費用:5 = 100000
費用:6 = GCトイレ*2000+5000

SELECTCASE GC物件
	CASE 2, 4, 6
		PRINTL 
		PRINTL 【増築できません】
		PRINTL 
	CASEELSE
		IF GC広さ >= 20
			PRINTL 　[-] - 広さは最大です
		ELSE
			CALL PASELIL, @"　[1] - 広くする\@ !ORIGIN ? ({費用:1}＄#)\@"
		ENDIF
		SIF !GC分煙
			CALL PASELIL, @"　[2] - 分煙施設を作る\@ !ORIGIN ? ({費用:2}＄)#\@"
		IF GC階層 >= 100
			PRINTL 　[-] - 階層は最大です
		ELSE
			CALL PASELIL, @"　[3] - 階層を増やす\@ !ORIGIN ? ({費用:3}＄)#\@"
		ENDIF
		IF GC外装 >= 100
			PRINTL 　[-] - 外装は最大です
		ELSE
			CALL PASELIL, @"　[4] - 外装強化\@ !ORIGIN ? ({費用:4}＄)#\@"
		ENDIF
		SIF !GCフードコート
			CALL PASELIL, @"　[5] - フードコートを作る\@ !ORIGIN ? ({費用:5}＄)#\@"
		CALL PASELIL, @"　[6] - お手洗い改装\@ !ORIGIN ? ({費用:6}＄)#\@"
ENDSELECT
DRAWLINE
PRINTPLAIN 　RENOVATION　
PRINTL [0] 戻る
;PRINTL [100] マニュアル

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			SIF GC広さ >= 20
				CONTINUE
		CASE 2
			SIF GC分煙
				CONTINUE
		CASE 3
			SIF GC階層 >= 100
				CONTINUE
		CASE 5
			SIF GCフードコート
				CONTINUE
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6)

IF MONEY < 費用:RESULT && !ORIGIN
	PRINTW 所持金が足りません
	RESTART
ENDIF

SELECTCASE RESULT
	CASE 1
		PRINTFORMW %物件名(GC物件)%を改築し、広くしました
		GC広さ++
		FOR LCOUNT, 1, GC階層+1
			空きスペース:LCOUNT++
		NEXT
	CASE 2
		PRINTFORMW 分煙施設を設置しました
		GC分煙 = 1
	CASE 3
		PRINTFORMW {GC階層+1}階建に増築しました
		GC階層++
		空きスペース:GC階層 = GC広さ
	CASE 4
		PRINTFORMW 外装を強化しました
		GC外装++
	CASE 5
		PRINTFORMW フードコート施設を設置しました
		GCフードコート = 1
	CASE 6
		PRINTFORMW トイレを改装しました
		GCトイレ++
ENDSELECT
MONEY -= 費用:RESULT
IF FLAG:パートナー > 0
	SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
		TRYCALLFORM GC_RENOVATION_%CSVNAMERPS(FLAG:パートナー)%, RESULT
ENDIF

RESTART

RETURN

;筐体購入処理----------------------------------------------------
@GC_ARCADEGAME
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ
DRAWLINE
PRINTL 【筐体購入】
PRINTL アーケードゲームを購入できます
PRINTL これが無いとゲーセン経営は話になりません
CALL PRINT_AC_ITEM
PRINTL 　[1] - 音ゲー・アーケードゲーム
PRINTL 　[2] - メダルゲーム
;PRINTL [3] - プライズゲーム
PRINTL 　[4] - 両替機・券売機・その他
DRAWLINE
PRINTPLAIN 　ARCADE　
PRINTL [0] - 戻る

CALL INPUTF, 0, 1, 2, 4

ページ = RESULT
DO
	SELECTCASE ページ
		CASE 0
			RETURN
		CASE 1, 2, 4
			DRAWLINE
			SIF !ORIGIN
				CALL PASELIL, @"所持金:{MONEY}＄"
			PRINTL □購入する筐体を選んでください
			FOR LCOUNT, ページ*100+1000, ページ*100+1100
				SIF ITEMNAME:LCOUNT != ""
					CALL PASELIL, @"　[{LCOUNT}] - %ITEMNAME:LCOUNT%\@ !ORIGIN ? ({ITEMPRICE:LCOUNT}＄)#\@"
			NEXT
			DRAWLINE
			PRINTPLAIN 　ARCADE　
			PRINTL [1000] 戻る
			
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RESTART
					CASEELSE
						IF ITEMNAME:RESULT != ""
							IF MONEY < ITEMPRICE:RESULT && !ORIGIN
								PRINTW 所持金が足りません
								BREAK
							ELSE
								PRINTFORMW ＜%ITEMNAME:RESULT%を購入しました＞
								ITEM:RESULT++
								MONEY -= ITEMPRICE:RESULT
								IF FLAG:パートナー > 0
									SIF FLAG:口上表示 && !CFLAG:GETCHARA(FLAG:パートナー):口上非表示
										TRYCALLFORM BUY_ARCADEGAME_%CSVNAMERPS(FLAG:パートナー)%, RESULT
								ENDIF
								BREAK
							ENDIF
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1

;店長の任命----------------------------------------------------
@GC_LEADER
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 現店長

現店長 = FINDCHARA(CFLAG:ゲーセン店長, 1)
SIF 現店長 == -1
	現店長 = 0

DRAWLINE

PRINT □店長に任命するキャラを選んでください(現在の店長:
IF 現店長
	PRINTFORML %NAME:現店長%)
ELSE
	PRINTL 不在)
ENDIF
FOR LCOUNT, 0, CHARANUM
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		PRINTFORM 　[{LCOUNT, 2, RIGHT}] - %NAME:LCOUNT, 26, LEFT%(家事技能:{ABL:LCOUNT:家事技能, 2, LEFT} 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} 料理技能:{ABL:LCOUNT:料理技能, 2, LEFT}
		SIF TALENT:LCOUNT:料理人
			PRINT  料理人
		SIF TALENT:LCOUNT:工学博士
			PRINT  工学博士
		SIF TALENT:LCOUNT:音ゲーマー
			PRINT  音ゲーマー
		PRINTL )
	ENDIF
NEXT

DRAWLINE
PRINTPLAIN 　LEADER　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASEELSE
			IF CSTR:RESULT:配属 == "ゲームセンター"
				PRINTFORMW %NAME:RESULT%を店長に任命しました
				CFLAG:現店長:ゲーセン店長 = 0
				CFLAG:RESULT:ゲーセン店長 = 1
				SIF FLAG:口上表示 && !CFLAG:RESULT:口上非表示
					TRYCALLFORM GC_ASSIGNLEADER_%CSVNAMERPS(NO:RESULT)%, RESULT, 現店長
				RESTART
			ENDIF
	ENDSELECT
LOOP 1

;----------------------------------------------------
;所持筐体表示処理
;----------------------------------------------------
@PRINT_AC_ITEM
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 所持筐体数

DRAWLINE
FOR LCOUNT, 1100, 1500
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	SIF ITEM:LCOUNT == 0
		CONTINUE

	PRINTFORM [%ITEMNAME:LCOUNT%×{ITEM:LCOUNT}]
	所持筐体数++
NEXT
SIF !所持筐体数
	PRINTFORM [所持筐体無し]
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE

;----------------------------------------------------
;設置済み筐体の確認
;最大で9万回ループのため重くなりそうなら別の処理を考える
;----------------------------------------------------
@設置済み筐体計算, レイアウトロード:0, レイアウトロード:1
#DIM DYNAMIC 階層
#DIM DYNAMIC X座標
#DIM DYNAMIC Y座標
#DIM DYNAMIC 筐体NO
#DIM DYNAMIC レイアウトロード, 2

VARSET 設置済み筐体

;各階層、各マップに設置してある筐体数を計算
;オブジェクトナンバーはそれぞれ上下左右の向きで4つ分ある
;それと1からカウントしてるので式は (オブジェクトナンバー-1)/4
FOR 階層, 1, GC階層+1
	;レイアウトロードフラグが立ってる場合はそのレイアウトで計算する 階層0→1に置き換え
	IF レイアウトロード:0 == 階層
		FOR X座標, 0, 10+GC広さ
			FOR Y座標, 0, 10+GC広さ
				IF GCオブジェクト:(レイアウトロード:1):X座標:Y座標
					筐体NO = (GCオブジェクト:(レイアウトロード:1):X座標:Y座標-1)/4
					設置済み筐体:筐体NO++
				ENDIF
			NEXT
		NEXT
	ELSE
		FOR X座標, 0, 10+GC広さ
			FOR Y座標, 0, 10+GC広さ
				IF GCオブジェクト:階層:X座標:Y座標
					筐体NO = (GCオブジェクト:階層:X座標:Y座標-1)/4
					設置済み筐体:筐体NO++
				ENDIF
			NEXT
		NEXT
	ENDIF
NEXT

;----------------------------------------------------
;所持物件表示処理
;----------------------------------------------------
;@PRINT_PR_ITEM
;REPEAT 100
;	SIF GC100 <= COUNT
;		BREAK
;	PRINTFORM [{COUNT}]
;	SIF COUNT < 10
;		PRINT  
;	PRINTFORML %GC_STR:(GCP:COUNT:0)%
;REND

;----------------------------------------------------
;筐体マニュアル及び筐体購入の処理をします（工事中）
;----------------------------------------------------
;@AC_MANUAL
;IF P == 100
;	PRINTL 【beatmaniaIIDX】
;	PRINTL
;	PRINTL 公式いわく「BEMANI一のガチンコ系」
;	PRINTL ご存知の通り降ってくるノーツに合わせて鍵盤を叩いたり皿を回したりする。
;	PRINTL BEMANI初心者から上級者まで幅広い層に人気があり、メンテや評判、名声が良いと遠くからはるばる遠征に訪れるものも少なくない。
;	PRINTL 15年以上試行錯誤してきた筐体でもあり、BEMANIシリーズの祖であるため、筐体性能は非常にナイーブ。
;	PRINTL メンテナンス能力が重要になってくる筐体だろう。
;IF P == 101
;	PRINTL 【pop`n music】
;	PRINTL
;	PRINTL 

;	PRINTL
;	PRINTL 所持金変動
;	PRINTFORML {MONEY} > {MONEY - ACSI:O}
;	PRINTL [0] - 購入する
;	PRINTL [1] - 購入しない


;----------------------------------------------------
;ゲームセンター経営での収入を計算。日付変更時に呼び出されます
;----------------------------------------------------
@INCOME_GC
#DIM DYNAMIC 収入
#DIM DYNAMIC 名声
#DIM DYNAMIC 評判
#DIM DYNAMIC 支出
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC LTARGET
#DIM DYNAMIC モチベ計算用
#DIM DYNAMIC 世話好き人数
#DIM DYNAMIC 設置済み筐体数
#DIM DYNAMIC 最高期待値
#DIM DYNAMIC 加算値
#DIM DYNAMIC メンテ加算値

CALL 設置済み筐体計算

SIF !GC物件
	RETURN 0
;収入計算--------------------------------------------------------
;良い物件ほど最低額保証
収入 = 300 * GC物件
FOR LCOUNT, 1, GC階層+1
	;一番高い期待値を調べておく
	SIF 最高期待値 < 階層収入期待値(LCOUNT)
		最高期待値 = 階層収入期待値(LCOUNT)
	;期待値--------------------------------------
	SELECTCASE GC物件
		;テナント店なら階層ごとの期待値をそのまま加算
		CASE 2, 4, 6
			収入 += 階層収入期待値(LCOUNT)
		;それ以外ならRAND:階層ごとの収入期待値を加算
		CASEELSE
			;RAND:期待値で出す 外装レベル%の収入が保証される
			加算値 = RAND:階層収入期待値(LCOUNT)
			CALL SETMAX, 加算値, 階層収入期待値(LCOUNT)*GC外装/100
			収入 += 加算値
	ENDSELECT
NEXT
;分煙補正-------------------------------------
;収入には影響なし

;外装補正-------------------------------------
;上で計算済み

;フードコート補正-------------------------
;レベルに応じて支出を抑えられるのでフードコートによる収入は保留
;LOCAL:2 = 0
;IF GCフードコート
;	;階層の数だけ期待値をロール 一番高い値を加算
;	REPEAT GC階層
;		LOCAL:1 = RAND:フードコート収入期待値()
;		SIF LOCAL:2 < LOCAL:1
;			LOCAL:2 = LOCAL:1
;	REND
;ENDIF
;収入 += LOCAL:2

;人事補正---------------------------------
収入 += GC収入人事補正()

;プレミアムフライデー---------------------------------
SIF PREMIUM_FRIDAY(YEAR, MONTH, DAYS)
	TIMES 収入, 1.10

;収入処理--------------------------------------
CALL PASELIL, @"%GC店名%の収入が{収入}＄入りました"
バンめし = 0
MONEY += 収入


;名声計算--------------------------------------------------------
;テナント施設だと名声が上がりやすい
SELECTCASE GC物件
	CASE 2, 4, 6
		名声 += GC物件*5
ENDSELECT

名声 += GCメンテ

;筐体補正--------------------------------------
;単純に筐体数が多ければそれだけで名声が上がる
FOR LCOUNT, 0, 400
	名声 += 名声筐体補正(LCOUNT)*設置済み筐体:LCOUNT
	設置済み筐体数++
NEXT

;お手洗い補正---------------------------------
;レベルに応じて最大筐体設置数と同じまで上がる
名声 += MIN(GCトイレ, 設置済み筐体数)

;広さ補正-------------------------------------
名声 += GC広さ

;分煙補正-------------------------------------
SIF GC分煙
	名声 += GC物件*3

;階層・外装補正-------------------------------------
名声 += GC階層*GC外装*2

;フードコート補正-------------------------
名声 += フードコートレベル()

;お手洗い補正---------------------------------
名声 += GCトイレ*GC物件/10

;人事補正---------------------------------
名声 += 名声人事補正()

;名声処理--------------------------------------
PRINTFORML %GC店名%の名声が{名声}上がりました
GC名声 += 名声


;評判計算--------------------------------------------------------
評判 += GC物件*3

評判 += 評判人事補正()

;飯がマズイと評判下がったりする
SIF GCフードコート
	評判 += (フードコートレベル()-5)*3

評判 += GCメンテ/5

CALL 評判筐体補正
評判 += RESULT

;お手洗い補正---------------------------------
評判 += GCトイレ*GC物件/3

;分煙補正-------------------------------------
IF GC分煙
	SELECTCASE 名声
		CASE IS >= 0
			TIMES 評判 , 1.20
		CASEELSE
			TIMES 評判 , 0.80
	ENDSELECT
ENDIF

;評判処理--------------------------------------
SELECTCASE 評判
	CASE IS > 0
		PRINTFORML %GC店名%の評判が{評判}上がりました
	CASE 0
		PRINTFORML %GC店名%の評判に変化はありませんでした
	CASE IS < 0
		PRINTFORML %GC店名%の評判が{ABS(評判)}下がりました
ENDSELECT
GC評判 += 評判
;0未満にはならない
SIF GC評判 < 0
	GC評判 = 0

;支出計算--------------------------------------------------------
;要調整
SELECTCASE GC物件
	CASE 1
		支出 = 300 
	CASE 2
		支出 = 1000
	CASE 3
		支出 = 1200
	CASE 4
		支出 = 3000
	CASE 5
		支出 = 2000
	CASE 6
		支出 = 10000
	CASE 7
		支出 = 5000
ENDSELECT

;筐体補正--------------------------------------
FOR LCOUNT, 0, 400
	支出 += 筐体支出補正(LCOUNT)*設置済み筐体:LCOUNT
NEXT

;広さ補正-------------------------------------
支出 += GC広さ*500

;階層補正-------------------------------------
支出 += GC階層*10000

;フードコート補正-------------------------
;フードコートレベルに応じて支出を抑えられる
SIF GCフードコート
	支出 = (支出/100)*(100-フードコートレベル())

;人件費---------------------------------
;奴隷にも給料はちゃんと払う
;サボり魔はサボっても給料はちゃんと貰う
FOR LCOUNT, 0, CHARANUM
	;日給500……この世界の金銭価値はいかほどか
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		IF CFLAG:LCOUNT:ゲーセン店長
			支出 += 1000
			;浪費癖には倍額払う
			SIF TALENT:LCOUNT:浪費癖
				支出 += 1000
		ELSE
			支出 += 500
			;浪費癖には倍額払う
			SIF TALENT:LCOUNT:浪費癖
				支出 += 500
		ENDIF
	ENDIF
NEXT

;支出処理--------------------------------------
CALL PASELIL, @"%GC店名%の経費に{支出}＄かかりました"
MONEY -= 支出
基本収入 = 収入-支出

;人事補正計算後に配属中のキャラを配属完了にする
FOR LCOUNT, 1, CHARANUM
	IF CSTR:LCOUNT:配属 == "ゲームセンター"
		IF FLAG:口上表示 && !CFLAG:LCOUNT:口上非表示
			LTARGET = TARGET
			TARGET = LCOUNT
			TRYCALLFORM GC_TURNEND_%CSVNAMERPS(NO:LCOUNT)%, 収入, 支出
			TARGET = LTARGET
		ENDIF
		SIF CFLAG:LCOUNT:配属完了 && TALENT:LCOUNT:世話好き
			世話好き人数++
		CFLAG:LCOUNT:配属完了 = 1
	ENDIF
NEXT

;モチベーションを計算する
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "ゲームセンター"
		CONTINUE
	モチベ計算用 = 0
	FOR LCOUNT2, 0, VARSIZE("TALENT")
		SIF !TALENT:LCOUNT:LCOUNT2
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT2
			CASE "恋慕", "服従"
				モチベ計算用++
			CASE "親愛", "隷属"
				モチベ計算用 += 2
			CASE "素直", "冷静", "強気", "好奇心", "目立ちたがり", "恥薄い", "献身的", "潔癖症", "音ゲーマー"
				モチベ計算用++
			CASE "臆病", "プライド高い", "大人しい", "無関心", "恥じらい", "汚臭敏感", "男嫌い", "女嫌い"
				モチベ計算用 -= 2
		ENDSELECT
	NEXT
	加算値 = CFLAG:LCOUNT:モチベーション+モチベ計算用+世話好き人数
	SIF TALENT:LCOUNT:世話好き
		加算値 -= 世話好き人数
	;配属先のキャラの相性で計算
	加算値 += 相性モチベ補正(LCOUNT)
	CFLAG:LCOUNT:モチベーション = LIMIT(加算値, 1, 100)
NEXT

;最後に現状のメンテレベルを計算
FOR LCOUNT, 1, CHARANUM
	SIF CSTR:LCOUNT:配属 != "ゲームセンター"
		CONTINUE
	IF CFLAG:LCOUNT:サボり
		PRINTFORML %CALLNAME:LCOUNT%はゲームセンターの仕事をサボりました
		CFLAG:LCOUNT:ストレス値 -= 5
		CFLAG:LCOUNT:サボり = 0
		CONTINUE
	ENDIF
	IF FINDCHARA(CFLAG:ゲーセン店長, 1) >= 0 && TALENT:FINDCHARA(CFLAG:ゲーセン店長, 1):統率 && !TALENT:LCOUNT:統率
		加算値 = ABL:LCOUNT:家事技能+2
		加算値 += ABL:LCOUNT:工作技能+2
	ELSEIF 統率者("ゲームセンター") && !TALENT:LCOUNT:統率
		加算値 = ABL:LCOUNT:家事技能+1
		加算値 += ABL:LCOUNT:工作技能+1
	ELSE
		加算値 = ABL:LCOUNT:家事技能
		加算値 += ABL:LCOUNT:工作技能
	ENDIF
	SIF TALENT:LCOUNT:音ゲーマー
		加算値 += 5
	SIF TALENT:LCOUNT:工学博士
		加算値 += 5
	IF CFLAG:LCOUNT:ゲーセン店長
		SIF 加算値 > 20
			加算値 = 20
	ELSE
		加算値 /= 2
		SIF 加算値 > 10
			加算値 = 10
	ENDIF
	メンテ加算値 += 加算値
	;ついでに配属中のキャラには実務経験を加算
	IF CFLAG:LCOUNT:ゲーセン店長
		EXP:LCOUNT:実務経験 += 10
		EXP:LCOUNT:工作経験 += 5
		EXP:LCOUNT:家事経験 += 5
		SIF GCフードコート
			EXP:LCOUNT:料理経験 += 5
		JUEL:LCOUNT:習得 += CFLAG:LCOUNT:モチベーション*20
	ELSE
		EXP:LCOUNT:実務経験++
		EXP:LCOUNT:工作経験++
		EXP:LCOUNT:家事経験++
		SIF GCフードコート
			EXP:LCOUNT:料理経験++
		JUEL:LCOUNT:習得 += CFLAG:LCOUNT:モチベーション*10
	ENDIF
	;人気度も加算
	人気度:(NO:LCOUNT) += 2
NEXT

;店長を2倍で計上して最高値110なので10/11に圧縮(最高値100)
GCメンテ = メンテ加算値*10/11
GCメンテ = LIMIT(GCメンテ, 0, 100)


@GCバージョン管理
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 階層

;マップシステム導入版は20200729
IF GCバージョン < 20200729
	;広さと階層に上限を設けた
	CALL SETMIN, GC広さ, 20
	CALL SETMIN, GC階層, 100
	;設置済みの筐体を全て回収する
	FOR LCOUNT, 1100, 1500
		SIF ITEMNAME:LCOUNT == ""
			CONTINUE
		SIF 設置筐体:階層:LCOUNT == 0
			CONTINUE
		ITEM:LCOUNT += 設置筐体:階層:LCOUNT
		設置筐体:階層:LCOUNT = 0
		空きスペース:階層 = GC広さ
	NEXT
	GCバージョン = 20200729
ENDIF
