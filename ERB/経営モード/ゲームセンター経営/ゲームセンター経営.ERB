@STRATEGY_GC
DRAWLINE
PRINTL 
PRINTL 【ゲームセンター経営】
PRINTL 
DRAWLINE
PRINTL □現在の規模
;----------------------------------------------------
PRINTFORM 　物件：%物件名(GC物件)%
;名声表示--------------------------------------------
PRINTFORML 　名声：%GC名声グレード(GC名声)%({GC名声})
;評判表示----------------------------------------------
PRINTFORM 　評判：%GC評判グレード(GC評判)%({GC評判})
;----------------------------------------------------
PRINTFORML 　前日の収入：%GC収入グレード(基本収入)%(${基本収入})
;----------------------------------------------------
PRINTFORML 　メンテナンス：%GCメンテグレード(GCメンテ)%
DRAWLINE

LOCALS = <button value='1' title='物件を購入します'>　[1] - 物件購入</button>
HTML_PRINT LOCALS
IF !GC物件
	LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 物件増築</nonbutton></font>
	HTML_PRINT LOCALS
	LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 筐体購入</nonbutton></font>
	HTML_PRINT LOCALS
	LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 人員配置</nonbutton></font>
	HTML_PRINT LOCALS
	LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 筐体設置</nonbutton></font>
	HTML_PRINT LOCALS
	IF FLAG:ゲーセン営業中
		LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 一時休業</nonbutton></font>
	ELSE
		LOCALS = <font color='gray'><nonbutton title='物件を購入してください'>　[-] - 通常営業</nonbutton></font>
	ENDIF
	HTML_PRINT LOCALS
ELSE
	LOCALS = <button value='2' title='物件を増築、改装します'>　[2] - 物件増築</button>
	HTML_PRINT LOCALS
	LOCALS = <button value='3' title='新しいアーケードゲーム筐体を購入します'>　[3] - 筐体購入</button>
	HTML_PRINT LOCALS
	LOCALS = <button value='4' title='ゲームセンターに人員を配置します'>　[4] - 人員配置</button>
	HTML_PRINT LOCALS
	LOCALS = <button value='5' title='ゲームセンターにアーケードゲーム筐体を設置します'>　[5] - 筐体設置</button>
	HTML_PRINT LOCALS
	IF FLAG:ゲーセン営業中
		LOCALS = <button value='9' title='ゲームセンターを一時休業し、収入と支出が行われないようにします'>　[9] - 一時休業</button>
	ELSE
		LOCALS = <button value='9' title='ゲームセンターの営業を再開し、収入と支出が行われるようにします'>　[9] - 通常営業</button>
	ENDIF
	HTML_PRINT LOCALS
ENDIF

DRAWLINE
PRINTPLAIN 　GAME CENTER　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 2 TO 5
			SIF !GC物件
				CONTINUE
	ENDSELECT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL GC_PROPERTY
		CASE 2
			CALL GC_RENOVATION
		CASE 3
			CALL GC_ARCADEGAME
		CASE 4
			CALL GC_PERSONAL
		CASE 5
			CALL SELECT_FLOOR
		CASE 9
			IF FLAG:ゲーセン営業中
				PRINTW ゲームセンターの経営を一時中断しました
			ELSE
				PRINTW ゲームセンターの経営を開始しました
			ENDIF
			INVERTBIT FLAG:ゲーセン営業中, 0
		CASE 100
			CALL GC_TUTORIAL
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

;----------------------------------------------------
;ゲームセンター経営モードチュートリアル
;----------------------------------------------------
@GC_TUTORIAL
DRAWLINE
PRINTL 当モードはゲームセンターを経営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「物件購入」とは？
PRINTL [2] - 「物件増築」とは？
PRINTL [3] - 「筐体購入」とは？
PRINTL [4] - 「人員配置」とは？
PRINTL [5] - 「筐体設置」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 「物件購入」は筐体を置く物件を購入するコマンドです。
			PRINTL これが無いと話にならないので、まずはボロ屋でもいいので購入してみましょう。
			PRINTL 物件は大きく「所有物件」と「テナント」に分けられます。
			PRINTL 「所有物件」は物件購入の際の経費が高いですが、運営費が安いので安定した収入を得ることができます。
			PRINTL 「テナント」はその逆で、運営費が高く、物件購入の経費が安いです。
			PRINTW また、テナントは増築ができないので、即席で施設を整えたい場合に使うといいかもしれません
		CASE 2
			PRINTL 「物件増築」は購入した物件を増築し、筐体を置けるスペースを広げたり、設備を向上させるコマンドです
			PRINTL 物件には「広さ」「分煙施設」「階層」「外装」「フードコート」「トイレ」の設備があり、それぞれ改築することにより様々な面での経済効果がアップします
			PRINTL 分煙施設はテナント系物件には初期から備えられており、有無で判断するので改築は一回で済みます
			PRINTL 他の施設はいくらでも増築できるので、その分経済効果も尻上がりになります。
			PRINTW しかし増築すればするほど、増築に必要な経費も上がり運営費も上昇するので、計画的な増築が必要になるでしょう
		CASE 3
			PRINTL 「筐体購入」はゲーセンに設置するための筐体を購入するコマンドです
			PRINTW 当バージョンでは少なく、これからも増やして行きたいと思います
		CASE 4
			PRINTL 「人員配置」はゲーセン経営に人員を配置するコマンドです
			PRINTL 現段階では「物件ごとに配置する」のではなく「経営に配属させる」という形になってるので、奴隷を選ぶだけであとはキャラたちがやってくれます
			PRINTW プラス素質（魅力系や、巨乳や小柄体型などのフェチズム素質など）があるとプラス補正がかかったり、マイナス素質（生意気や悲観的など）があるとマイナス補正がかかります
		CASE 5
			PRINTL 「筐体設置」は所有する物件に筐体を設置するコマンドです
			PRINTL 「物件の広さ」×「物件の階層」と同じ数値の分筐体を置くことができます
			PRINTL 筐体は所持するだけなら経費はかかりませんが、設置した翌日から経費がかかるので所持金とよく相談して決めましょう
			PRINTW ※なお、アーケードゲームは基本一般客には販売しないらしいです
	ENDSELECT
LOOP LOOPRES(1, 2, 3, 4, 5)

RESTART

;物件購入処理----------------------------------------------------
@GC_PROPERTY
#DIM 階層
#DIM 筐体
LOCAL = GC名声+GC評判
DRAWLINE
PRINTL 【物件購入】
PRINTL 名声や評判によって買える物件も変わります
PRINTL 「アイテム名」($金額 - $日毎の基本維持費)
PRINTFORML 所持金：${MONEY}
PRINTL 
PRINTL [1] - ボロ屋($30000 - $300～)
PRINTL [2] - 中小スーパーのテナント($5000 - $1000～)
SIF LOCAL >= 5000
	PRINTL [3] - 商店街の一角($50000 - $1200～)
SIF LOCAL >= 10000
	PRINTL [4] - 駅中テナント($10000 - $3000～)
SIF LOCAL >= 30000
	PRINTL [5] - 街中物件($100000 - $2000～)
SIF LOCAL >= 50000
	PRINTL [6] - 百貨店テナント($30000 - $10000～)
SIF LOCAL >= 100000
	PRINTL [7] - アミューズメント施設($300000 - $5000～)
DRAWLINE
PRINTPLAIN 　PROPERTY　
PRINTL [0] - 戻る　

DO
	INPUT
	LOCAL = RESULT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 【ボロ屋】
			PRINTL 
			PRINTL その名の通りボロっちい建物。
			PRINTL 整備すればゲーセンとして使えるかもしれないが……
			PRINTL 狭いため筐体もあまり置けず、高い経済効果は期待できない。
		CASE 2
			PRINTL 【中小スーパーのテナント】
			PRINTL 
			PRINTL 時々スーパーの片隅に申し分ない程度の筐体が置いてあるあのスペース。
			PRINTL ゲーム目的で来る人はほとんど居ないと思うが、お買い物に飽きた子どもたちがよく訪れる。
			PRINTL ボロ屋と比べると経済効果はだいぶマシではあるが、こちらはテナントなので維持費が高い。
			PRINTL 他にも改装ができないなど、使い勝手の悪い部分がある。
		CASE 3
			PRINTL 【商店街の一角】
			PRINTL 
			PRINTL 商店街といっても昔ながらの風情のあるような場所ではなく、ちょっとした飲食店やオシャレな店や並ぶいわゆる「アーケード街」
			PRINTL 改築の自由度もそこそこ高く、周囲にもいろいろな店が並ぶので、お客さんが多い。
			PRINTL 維持費も安く、初期設備も平均的なため、経営に慣れてきたらここを使ってみるのもいいだろう。
		CASE 4
			PRINTL 【駅中テナント】
			PRINTL 
			PRINTL 都会の駅中にあるスペースを間借りしたもの。
			PRINTL 最低限の設備は整っており、全体的に水準以上の環境でである。
			PRINTL イニシャルコストは安いが、増築もできずランニングコストも結構かかるため、計画的な経営ができないと破産する可能性も高い。注意しよう。
		CASE 5
			PRINTL 【街中物件】
			PRINTL 
			PRINTL 環境、土地共に優れていて、人通りも多い街中の物件。
			PRINTL 特徴としては分煙施設は無いが、ここは二階建てなので広さの倍の筐体が置ける。
			PRINTL 増築もできるので、買うことさえできればかなり使い勝手のいい物件である。
		CASE 6
			PRINTL 【百貨店テナント】
			PRINTL 
			PRINTL 百貨店内に間借りする物件。
			PRINTL 経済効果が非常に高いが、その分経費も馬鹿にならないため、店を構える際はよく考えよう。
			PRINTL こちらは街中物件とは違い、一階しか無く、増築も行えない。
		CASE 7
			PRINTL 【アミューズメント】
			PRINTL 
			PRINTL 当バージョンでは最高ランクの施設。
			PRINTL 初期コストが30万、基本経費が5000ととんでもないコストがかかるが、経済効果もコスト同等のものだと思っていいだろう。
			PRINTL 初期から二階建て、所有物件唯一の分煙設備、他高水準の設備もあり、フードコートもある。
			PRINTL 増築も自由にできるため、経営での物件は最終的にここに行き着くだろう。
	ENDSELECT
LOOP LOOPRES(1, 2, 3, 4, 5, 6, 7)

IF MONEY < 物件価格(RESULT)
	PRINTW 所持金が足りません
	RESTART
ELSEIF GC物件 == RESULT
	PRINTW 現在所持している物件です
	RESTART
ENDIF

PRINTL 所持金変動
PRINTFORML ${MONEY} > ${MONEY - 物件価格(RESULT)}
PRINTL 
PRINTL 購入しますか？
PRINTL [0] - 購入しない
PRINTL [1] - 購入する


DO
	INPUT
	SIF RESULT == 0
		RESTART
LOOP LOOPRES(0, 1)

IF GC物件
	PRINTFORML 現在%物件名(GC物件)%を所持しています。本当に買い替えますか？
	PRINTFORML （%物件名(GC物件)%に設置されている筐体は自動的に回収されます）
	PRINTL [0] - それは困る
	PRINTL [1] - 大丈夫だ、問題ない
	DO
		INPUT
		SELECTCASE RESULT
			CASE 0
				RETURN 0
			CASE 1
				FOR 階層, 1, GC階層+1
					FOR 筐体, 1100, 2000
						SIF ITEMNAME:筐体 == ""
							CONTINUE
						SIF 設置筐体:階層:筐体 == 0
							CONTINUE
						ITEM:筐体 += 設置筐体:階層:筐体
						設置筐体:階層:筐体 = 0
					NEXT
				NEXT
				BREAK
		ENDSELECT
	LOOP 1
ENDIF

PRINTFORMW ＜%物件名(LOCAL)%を購入しました＞
MONEY -= 物件価格(LOCAL)
FLAG:ゲーセン営業中 = 1

;初期値設定-------------------
;物件情報--------------
GC物件 = LOCAL

;広さ初期値------------
GC広さ = (LOCAL * 5)

;分煙施設--------------
SELECTCASE LOCAL
	;テナントとアミューズメントには分煙施設が付く
	CASE 2, 4, 6, 7
		GC分煙 = 1
ENDSELECT

;階層初期値------------
GC階層 = 1
空きスペース:1 = GC広さ
;街中とアミューズメントは2階建て
SELECTCASE LOCAL
	CASE 5, 7
		GC階層 = 2
		空きスペース:2 = GC広さ
ENDSELECT

;外装初期値------------
GC外装 = (LOCAL/2+1)

;フードコート----------
SELECTCASE LOCAL
	;百貨店とアミューズメントにはフードコートが付く
	CASE 6, 7
		GCフードコート = 1
ENDSELECT

;お手洗い--------------
GCトイレ = (LOCAL/2+1)

RETURN 0

;物件増築処理----------------------------------------------------
@GC_RENOVATION
VARSET LOCAL, 0
PRINTL 【物件増築】
PRINTL 物件を増築できます。
PRINTL スペースを広げたり設備を強化すると、置ける筐体の数が増えたり評判が上がったりします。
PRINTL 
DRAWLINE
PRINTFORML 物件：%物件名(GC物件)%
SELECTCASE GC分煙
	CASE 0
		PRINTLC 分煙施設：無
	CASE 1
		PRINTLC 分煙施設：有
ENDSELECT
PRINTFORMLC 階層：{GC階層}階建

PRINTL 

PRINTFORMLC 外装レベル：{GC外装}
SELECTCASE GCフードコート
	CASE 0
		PRINTLC フードコート：無
	CASE 1
		PRINTFORMLC フードコート：有
ENDSELECT
PRINTFORMLC お手洗いレベル：{GCトイレ}

PRINTL 
DRAWLINE

SELECTCASE GC物件
	CASE 2, 4, 6
		PRINTL 
		PRINTL 【増築できません】
		PRINTL 
	CASEELSE
		PRINTFORML [1] - 広くする(${GC広さ*3000+5000})
		SIF !GC分煙
			PRINTFORML [2] - 分煙施設を作る($30000)
		PRINTFORML [3] - 階層を増やす(${GC階層*1000000})
		PRINTFORML [4] - 外装強化(${GC外装*100000})
		SIF !GCフードコート
			PRINTFORML [5] - フードコートを作る($100000)
		PRINTFORML [6] - お手洗い改装(${GCトイレ*2000+5000})
ENDSELECT
DRAWLINE
PRINTPLAIN 　RENOVATION　
PRINTL [0] 戻る
;PRINTL [100] マニュアル

DO
	INPUT
	SELECTCASE RESULT
		CASE 2
			SIF GC分煙
				CONTINUE
		CASE 5
			SIF GCフードコート
				CONTINUE
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 6)

LOCAL = RESULT

SELECTCASE LOCAL
	CASE 0
		RETURN 0
	;ELSEIF RESULT == 100
	;	CALL RENOVATION_MANUAL
	CASE  1 TO 6
		SELECTCASE GC物件
			CASE 2, 4, 6
				RESTART
			CASEELSE
				SELECTCASE LOCAL
					CASE 1
						LOCAL:1 = GC広さ*3000+5000
					CASE 2
						LOCAL:1 = 30000
					CASE 3
						LOCAL:1 = GC階層*1000000
					CASE 4
						LOCAL:1 = GC外装*100000
					CASE 5
						LOCAL:1 = 100000
					CASE 6
						LOCAL:1 = GCトイレ*2000+5000
				ENDSELECT
		ENDSELECT
ENDSELECT

IF MONEY < LOCAL:1
	PRINTW 所持金が足りません
	RESTART
ENDIF
PRINTL 所持金変動
PRINTFORML ${MONEY} > ${MONEY-LOCAL:1}

SELECTCASE LOCAL
	CASE 1
		PRINTL 広さレベル変動
		LOCAL:3 = GC広さ
	CASE 2
		PRINTL 分煙施設設置
	CASE 3
		PRINTL 階層変動
		LOCAL:3 = GC階層
	CASE 4
		PRINTL 外装レベル変動
		LOCAL:3 = GC外装
	CASE 5
		PRINTL フードコートレベル設置
	CASE 6
		PRINTL お手洗いレベル変動
		LOCAL:3 = GCトイレ
ENDSELECT

SIF LOCAL != 2 && LOCAL != 5
	PRINTFORML {LOCAL:3} > {LOCAL:3+1}

PRINTL 
PRINTL 改装しますか？
PRINTL 
PRINTL [0] - 改装しない
PRINTL [1] - 改装する

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RESTART
		CASE 1
			SELECTCASE LOCAL
				CASE 1
					PRINTFORMW %物件名(GC物件)%を改築し、広くしました
					GC広さ++
					FOR COUNT, 1, GC階層+1
						空きスペース:COUNT++
					NEXT
				CASE 2
					PRINTFORMW 分煙施設を設置しました
					GC分煙 = 1
				CASE 3
					PRINTFORMW {GC階層+1}階建に増築しました
					GC階層++
					空きスペース:GC階層 = GC広さ
				CASE 4
					PRINTFORMW 外装を強化しました
					GC外装++
				CASE 5
					PRINTFORMW フードコート施設を設置しました
					GCフードコート = 1
				CASE 6
					PRINTFORMW トイレを改装しました
					GCトイレ++
			ENDSELECT
		MONEY -= LOCAL:1
		RESTART
	ENDSELECT
LOOP 1

RETURN 0

;筐体購入処理----------------------------------------------------
@GC_ARCADEGAME
#DIM LCOUNT
DRAWLINE
PRINTL 【筐体購入】
PRINTL アーケードゲームを購入できます
PRINTL これが無いとゲーセン経営は話になりません
CALL PRINT_AC_ITEM
PRINTL 　[1] - 音ゲー
;PRINTL [2] - メダルゲーム
;PRINTL [3] - プライズゲーム
PRINTL 　[4] - 両替機・券売機・その他
DRAWLINE
PRINTPLAIN 　ARCADE　
PRINTL [0] - 戻る

DO
	INPUT
	LOCAL = RESULT
	$BREAK
	SELECTCASE LOCAL
		CASE 0
			RETURN 0
		CASE 1, 4
			DRAWLINE
			PRINTFORML 所持金:${MONEY}
			PRINTL □購入する筐体を選んでください
			FOR LCOUNT, LOCAL*100+1000, LOCAL*100+1100
				SIF ITEMNAME:LCOUNT != ""
					PRINTFORML 　[{LCOUNT}] - %ITEMNAME:LCOUNT%(${ITEMPRICE:LCOUNT})
			NEXT
			DRAWLINE
			PRINTPLAIN 　ARCADE　
			PRINTL [1000] 戻る
			
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1000
						RESTART
					CASEELSE
						IF ITEMNAME:RESULT != ""
							IF MONEY < ITEMPRICE:RESULT
								PRINTW 所持金が足りません
								GOTO BREAK
							ELSE
								PRINTFORMW ＜%ITEMNAME:RESULT%を購入しました＞
								ITEM:RESULT++
								MONEY -= ITEMPRICE:RESULT
								GOTO BREAK
							ENDIF
						ENDIF
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1

;人員配置処理----------------------------------------------------
@GC_PERSONAL
#DIM DYNAMIC ページ数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 配属可能人数
#DIM DYNAMIC 飛ばした人数
#DIM DYNAMIC 配属人数
VARSET LOCAL, 0
配属人数 = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:配属 == 1
		配属人数++
REND
DRAWLINE
PRINTFORML □ゲーセン経営に配属する人員を選択してください(現在{配属人数}/10)

配属可能人数 = 0

REPEAT CHARANUM
	;主人は無理
	SIF COUNT == MASTER
		CONTINUE
	;助手可能じゃないと無理
	SIF CFLAG:COUNT:状態 < 2
		CONTINUE
	;体力不足だと無理
	SIF BASE:COUNT:体力 < 500
		CONTINUE
	;妊娠中、育児中は無理
	SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中
		CONTINUE
	配属可能人数++
	;配属可能なキャラに対応したLOCALが1になる
	LOCAL:COUNT = 1
REND

飛ばした人数 = 0
表示数 = 0

IF 配属可能人数
	REPEAT CHARANUM
		SIF !LOCAL:COUNT
			CONTINUE
		;2ページ目以降はページ×20まで表示を飛ばす
		IF 飛ばした人数 < ページ数*20
			飛ばした人数++
			CONTINUE
		ENDIF
		PRINTFORM 　[{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(家事技能:{ABL:COUNT:家事技能, 2, LEFT} 工作技能:{ABL:COUNT:工作技能, 2, LEFT} 料理技能:{ABL:COUNT:料理技能, 2, LEFT}
		SIF TALENT:COUNT:料理人
			PRINT  料理人
		SIF TALENT:COUNT:専業主婦
			PRINT  専業主婦
		SIF TALENT:COUNT:工学博士
			PRINT  工学博士
		PRINT )

		SELECTCASE CFLAG:COUNT:配属
			CASE 0
				IF COUNT == TARGET
					PRINTL 【調教中】
				ELSEIF COUNT == ASSI
					PRINTL 【助手中】
				ELSE
					PRINTL 【休憩中】
				ENDIF
			CASE 1
				PRINTL 【配属中】
			CASE 2
				PRINTL 【娼館配属中】
		ENDSELECT
		表示数++
		;20人表示したらページ終わり
		SIF 表示数 == 20
			BREAK
	REND
ELSE
	PRINTL 
	PRINTL 【現在配属できる奴隷はいません】
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数+1}　
SIF ページ数 > 0
	PRINTLC [999]前ページ
PRINTLC [1000] 戻る　
;表示できるキャラがまだ居れば次ページ
SIF 表示数+20*ページ数 < 配属可能人数 
	PRINTLC [1001]次ページ
PRINTL [1002] 全員帰還

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 0
				ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			;表示できるキャラがまだ居れば次ページ
			SIF 表示数+20*ページ数 < 配属可能人数 
				ページ数++
		CASE 1002
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 1
					CFLAG:COUNT:配属完了 = 0
					CFLAG:COUNT:配属 = 0
				ENDIF
			REND
			PRINTW 全員帰宅しました
		CASEELSE
			SIF !LOCAL:RESULT
				CONTINUE
			IF 配属人数 >= 10
				PRINTW これ以上配属できません
				RESTART
			ENDIF
			SELECTCASE CFLAG:RESULT:配属
				CASE 0
					PRINTFORMW %CALLNAME:RESULT%を配属しました
					CFLAG:RESULT:配属 = 1
					SIF TARGET == RESULT
						TARGET = -1
					SIF ASSI == RESULT
						ASSI = -1
					RESTART
				CASE 1
					PRINTFORMW %CALLNAME:RESULT%が帰宅しました
					CFLAG:RESULT:配属 = 0
					CFLAG:RESULT:配属完了 = 0
					RESTART
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(999, 1001, 1002)

RESTART

;階層選択処理----------------------------------------------------
@SELECT_FLOOR
#DIM ページ数
IF GC階層 >= 2
	DRAWLINE
	PRINTFORML □%物件名(GC物件)% {GC階層}階建て
	PRINTL 　階層を選んでください
	FOR COUNT, ページ数*GC階層+1, ページ数*GC階層+11
		SIF COUNT > GC階層
			BREAK
		PRINTFORML 　[{COUNT}] {COUNT}階 {空きスペース:COUNT}/{GC広さ}使用中
	NEXT
	DRAWLINE
	PRINTPLAINFORM 　PAGE:{ページ数+1}　
	SIF ページ数 > 0
		PRINTLC [999] 前ページ
	PRINTLC [1000] 戻る
	SIF GC階層 > ページ数*10
		PRINTLC [1001] 次ページ
	
	DO
		INPUT
		SELECTCASE RESULT
			CASE 999
				SIF ページ数 > 0
					ページ数--
			CASE 1000
				RETURN 0
			CASE 1001
				SIF GC階層 > ページ数*10
					ページ数++
			CASE IS >= 1, IS <= GC階層
				CALL AC_SETUP, RESULT
				RESTART
		ENDSELECT
	LOOP LOOPRES(999, 1001)
ELSE
	CALL AC_SETUP, 1
	RETURN 0
ENDIF

RESTART

;----------------------------------------------------
;筐体設置処理
;----------------------------------------------------
@AC_SETUP, 階層
#DIM 階層
#DIM DYNAMIC 選択筐体
;ページ数は初期値が1
#DIM ページ数 = 1
VARSET LOCAL, 0
DRAWLINE

;階層を選んでここに飛んで来るので、あとは設置する筐体(1100～2000)を選ぶだけ

PRINTFORML □{階層}階 広さ:{GC広さ} 
PRINTFORML 　使用済みスペース:{GC広さ-空きスペース:階層} 使用可能スペース:あと{空きスペース:階層}

FOR COUNT, ページ数*100+1000, ページ数*200+1000
	SIF ITEMNAME:COUNT == ""
		CONTINUE
	
	PRINTFORML ・%ITEMNAME:COUNT%×{ITEM:COUNT}台在庫あり(必要スペース:{筐体コスト(COUNT)} {設置筐体:階層:COUNT}台設置中)
	SIF ITEM:COUNT
		PRINTFORML 　%ITEMNAME:COUNT%を設置[{COUNT*10}]
	SIF 設置筐体:階層:COUNT
		PRINTFORML 　%ITEMNAME:COUNT%を撤去[{COUNT*10+1}]
	PRINTL 
NEXT
PRINTL ・[1002] 全て撤去

DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数}　
SIF ページ数 > 1
	PRINTLC [999] 前ページ
PRINTLC [1000] 戻る
SIF ページ数 < 8
	PRINTLC [1001] 次ページ

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 1
				ページ数 = 1
		CASE 1000
			RETURN 0
		CASE 1001
			SIF ページ数 < 4
				ページ数 = 4
		CASE 1002
			FOR COUNT, 1100, 1500
				SIF ITEMNAME:COUNT == ""
					CONTINUE
				SIF 設置筐体:階層:COUNT == 0
					CONTINUE
				ITEM:COUNT += 設置筐体:階層:COUNT
				設置筐体:階層:COUNT = 0
				空きスペース:階層 = GC広さ
			NEXT
			PRINTW 全筐体を撤去しました
		CASEELSE
			選択筐体 = RESULT/10
			IF RESULT%10 == 0 && 筐体コスト(選択筐体) > 空きスペース:階層
				PRINTW スペースが足りません
				RESTART
			ELSEIF ITEMNAME:選択筐体 != ""
				SELECTCASE RESULT%10
					CASE 0
						IF ITEM:選択筐体
							設置筐体:階層:選択筐体++
							ITEM:選択筐体--
							空きスペース:階層 -= 筐体コスト(選択筐体)
							PRINTFORMW {階層}階に%ITEMNAME:選択筐体%を設置しました(現在{設置筐体:階層:選択筐体}台)
						ENDIF
					CASE 1
						IF 設置筐体:階層:選択筐体
							設置筐体:階層:選択筐体--
							ITEM:選択筐体++
							空きスペース:階層 += 筐体コスト(選択筐体)
							PRINTFORMW {階層}階の%ITEMNAME:選択筐体%を撤去しました(現在{設置筐体:階層:選択筐体}台)
						ENDIF
				ENDSELECT
				RESTART
			ENDIF
	ENDSELECT
LOOP LOOPRES(999, 1001, 1002)

RESTART

;----------------------------------------------------
;所持筐体表示処理
;----------------------------------------------------
@PRINT_AC_ITEM
LOCAL = 0
DRAWLINE
FOR COUNT, 1100, 1500
	SIF ITEMNAME:COUNT == ""
		CONTINUE
	SIF ITEM:COUNT == 0
		CONTINUE

	PRINTFORM [%ITEMNAME:COUNT%×{ITEM:COUNT}]
	LOCAL++
NEXT
SIF !LOCAL
	PRINTFORM [所持筐体無し]
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE

;----------------------------------------------------
;所持物件表示処理
;----------------------------------------------------
;@PRINT_PR_ITEM
;REPEAT 100
;	SIF GC100 <= COUNT
;		BREAK
;	PRINTFORM [{COUNT}]
;	SIF COUNT < 10
;		PRINT  
;	PRINTFORML %GC_STR:(GCP:COUNT:0)%
;REND

;----------------------------------------------------
;筐体マニュアル及び筐体購入の処理をします（工事中）
;----------------------------------------------------
;@AC_MANUAL
;IF P == 100
;	PRINTL 【beatmaniaIIDX】
;	PRINTL
;	PRINTL 公式いわく「BEMANI一のガチンコ系」
;	PRINTL ご存知の通り降ってくるノーツに合わせて鍵盤を叩いたり皿を回したりする。
;	PRINTL BEMANI初心者から上級者まで幅広い層に人気があり、メンテや評判、名声が良いと遠くからはるばる遠征に訪れるものも少なくない。
;	PRINTL 15年以上試行錯誤してきた筐体でもあり、BEMANIシリーズの祖であるため、筐体性能は非常にナイーブ。
;	PRINTL メンテナンス能力が重要になってくる筐体だろう。
;IF P == 101
;	PRINTL 【pop`n music】
;	PRINTL
;	PRINTL 

;	PRINTL
;	PRINTL 所持金変動
;	PRINTFORML {MONEY} > {MONEY - ACSI:O}
;	PRINTL [0] - 購入する
;	PRINTL [1] - 購入しない


;----------------------------------------------------
;ゲームセンター経営での収入を計算。日付変更時に呼び出されます
;----------------------------------------------------
@INCOME_GC
#DIM DYNAMIC 収入
#DIM DYNAMIC 名声
#DIM DYNAMIC 評判
#DIM DYNAMIC メンテ
#DIM DYNAMIC 支出
#DIM DYNAMIC LCOUNT
VARSET LOCAL, 0
SIF !GC物件
	RETURN 0
;収入計算--------------------------------------------------------
;良い物件ほど最低額保証
収入 = 300 * GC物件
FOR LCOUNT, 1, GC階層+1
	;一番高い期待値を調べておく
	SIF LOCAL < 階層収入期待値(LCOUNT)
		LOCAL = 階層収入期待値(LCOUNT)
	;期待値--------------------------------------
	SELECTCASE GC物件
		;テナント店なら階層ごとの期待値をそのまま加算
		CASE 2, 4, 6
			収入 += 階層収入期待値(LCOUNT)
		;それ以外ならRAND:階層ごとの収入期待値を加算
		CASEELSE
			;外装レベル数だけロール 一番高い値を加算
			REPEAT GC外装
				LOCAL:1 = RAND:階層収入期待値(LCOUNT)
				SIF LOCAL:2 < LOCAL:1
					LOCAL:2 = LOCAL:1
			REND
			収入 += LOCAL:2
	ENDSELECT
NEXT
;分煙補正-------------------------------------
;収入には影響なし

;外装補正-------------------------------------
;上で計算済み

;お手洗い補正---------------------------------
;レベル数だけ最大期待値をロール 一番高い値を加算
LOCAL:2 = 0
IF LOCAL > 0
	REPEAT GCトイレ
		LOCAL:1 = RAND:LOCAL
		SIF LOCAL:2 < LOCAL:1
			LOCAL:2 = LOCAL:1
	REND
ENDIF
収入 += LOCAL:2

;フードコート補正-------------------------
;レベルに応じて支出を抑えられるのでフードコートによる収入は保留
;LOCAL:2 = 0
;IF GCフードコート
;	;階層の数だけ期待値をロール 一番高い値を加算
;	REPEAT GC階層
;		LOCAL:1 = RAND:フードコート収入期待値()
;		SIF LOCAL:2 < LOCAL:1
;			LOCAL:2 = LOCAL:1
;	REND
;ENDIF
;収入 += LOCAL:2

;人事補正---------------------------------
収入 += GC収入人事補正()

;収入処理--------------------------------------
PRINTFORML ゲームセンターの収入が${収入}入りました
MONEY += 収入


;名声計算--------------------------------------------------------
;テナント施設だと名声が上がりやすい
SELECTCASE GC物件
	CASE 2, 4, 6
		名声 += GC物件*5
ENDSELECT

名声 += GCメンテ

;筐体補正--------------------------------------
;単純に筐体数が多ければそれだけで名声が上がる
FOR LCOUNT, 1, GC階層+1
	名声 += 名声筐体補正(LCOUNT)
NEXT

;広さ補正-------------------------------------
名声 += GC広さ

;分煙補正-------------------------------------
SIF GC分煙
	名声 += GC物件*3

;階層・外装補正-------------------------------------
名声 += GC階層*GC外装*2

;フードコート補正-------------------------
名声 += フードコートレベル()

;お手洗い補正---------------------------------
名声 += GCトイレ*GC物件/10

;人事補正---------------------------------
名声 += 名声人事補正()

;名声処理--------------------------------------
PRINTFORML ゲームセンターの名声が{名声}上がりました
GC名声 += 名声


;評判計算--------------------------------------------------------
評判 += GC物件*3

評判 += 評判人事補正()

;飯がマズイと評判下がったりする
SIF GCフードコート
	評判 += (フードコートレベル()-5)*3

評判 += GCメンテ/5

FOR LCOUNT, 1, GC階層+1
	;機種が偏ると評判は下がる
	評判 += 評判筐体補正(COUNT)
NEXT

;お手洗い補正---------------------------------
評判 += GCトイレ*GC物件/3

;分煙補正-------------------------------------
IF GC分煙
	SELECTCASE 名声
		CASE IS >= 0
			TIMES 評判 , 1.20
		CASEELSE
			TIMES 評判 , 0.80
	ENDSELECT
ENDIF

;評判処理--------------------------------------
SELECTCASE 評判
	CASE IS > 0
		PRINTFORML ゲームセンターの評判が{評判}上がりました
	CASE 0
		PRINTFORML ゲームセンターの評判に変化はありませんでした
	CASE IS < 0
		PRINTFORML ゲームセンターの評判が{ABS(評判)}下がりました
ENDSELECT
GC評判 += 評判
;0未満にはならない
SIF GC評判 < 0
	GC評判 = 0

;支出計算--------------------------------------------------------
;要調整
SELECTCASE GC物件
	CASE 1
		支出 = 300 
	CASE 2
		支出 = 1000
	CASE 3
		支出 = 1200
	CASE 4
		支出 = 3000
	CASE 5
		支出 = 2000
	CASE 6
		支出 = 10000
	CASE 7
		支出 = 5000
ENDSELECT

;筐体補正--------------------------------------
FOR LCOUNT, 1, GC階層+1
	支出 += 筐体支出補正(LCOUNT)
NEXT

;広さ補正-------------------------------------
支出 += GC広さ*10

;階層補正-------------------------------------
支出 += GC階層*100

;フードコート補正-------------------------
;フードコートレベルに応じて支出を抑えられる
SIF GCフードコート
	支出 = (支出/100)*(100-フードコートレベル())

;人件費---------------------------------
;奴隷にも給料はちゃんと払う
REPEAT CHARANUM
	;日給500……この世界の金銭価値はいかほどか
	SIF CFLAG:COUNT:配属 == 1
		支出 += 500
REND

;支出処理--------------------------------------
PRINTFORMW ゲームセンターの経費に${支出}かかりました
MONEY -= 支出
基本収入 = 収入-支出

VARSET LOCAL, 0

;人事補正計算後に配属中のキャラを配属完了にする
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:配属 == 1
		CFLAG:LCOUNT:配属完了 = 1
NEXT

;最後に現状のメンテレベルを計算
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:配属 != 1
		CONTINUE
	LOCAL += ABL:LCOUNT:家事技能
	LOCAL += ABL:LCOUNT:工作技能
	SIF TALENT:LCOUNT:音ゲーマー
		LOCAL += 5
	SIF TALENT:LCOUNT:工学博士
		LOCAL += 5
	LOCAL /= 2
	SIF LOCAL > 10
		LOCAL = 10
	LOCAL:1 += LOCAL
	;ついでに配属中のキャラには実務経験を加算
	EXP:LCOUNT:実務経験++
NEXT

GCメンテ = LOCAL:1



