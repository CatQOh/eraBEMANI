@博物館
#DIM DYNAMIC 改装費用

SIF 博物館広さ < 10
	博物館広さ = 10

改装費用 = POWER(博物館広さ, 3)*1000

PRINTL □博物館の運営をします
CALL PASELIL, @"　現在の名声:{博物館名声} 昨日の収入:{博物館収入}＄ 現在の所持金:{MONEY}＄"
PRINTL 
PRINTL 　[1] - 作品を展示する
PRINTL 　[2] - 作品を作る
IF 博物館広さ >= 30
	PRINTL 　[-] - 博物館の広さは最大です
ELSE
	CALL PASELIL, @"　[3] - 博物館を改装する（現在の広さ:{博物館広さ} 改装費用:{改装費用}＄）"
ENDIF
DRAWLINE
PRINTPLAIN 　MUSEUM　
PRINTL [0] - 戻る

CALL INPUTF, 0, 1, 2, 3

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		CALL 博物館_MAP
		RESTART
	CASE 2
		CALL 博物館_CREATE
		RESTART
	CASE 3
		IF 博物館広さ >= 30
			PRINTW 博物館の広さは最大です
			RESTART
		ENDIF
		IF MONEY < 改装費用
			PRINTW 改装費用が足りません
			RESTART
		ENDIF
		MONEY -= 改装費用
		博物館広さ++
		RESTART
ENDSELECT

@博物館_MAP
#DIM DYNAMIC ページ数
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 処理数
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC XRES
#DIM DYNAMIC YRES
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIMS DYNAMIC HTML用

#DIM CONST 種類 = 0
#DIM CONST 名前 = 1
#DIM CONST ナンバー = 2
#DIM CONST 価値 = 3
#DIM CONST 色 = 4
#DIM CONST 血縁 = 5
#DIM CONST クローン = 6
#DIM CONST 集客力 = 7
#DIM CONST 汚れ = 8

CALL LOOP_CLEARLINE

VARSET 設置済み
FOR LCOUNTY, 0, 博物館広さ
	HTML用 = 
	FOR LCOUNTX, 0, 博物館広さ
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID > 0
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", @"%MUSEUM:ID:名前% 価値:%MUSEUM:ID:価値% 集客力:%MUSEUM:ID:集客力%\% \@ 博物館射 ? 汚された回数:%MUSEUM:ID:汚れ% #\@", MUSEUM:ID:色)
			設置済み:ID = 1
		ELSE
			HTML用 += SET_TOOLTIPBUTTON("■", @"{LCOUNTX*100+LCOUNTY}", "未設置")
		ENDIF
	NEXT
	HTML_PRINT HTML用
NEXT
DRAWLINE
PRINTPLAIN 　MUSEUM MAP　
PRINTFORML [-1] 戻る [-2] 全て片付ける [-3] 実用の許可/不許可(現在:\@ 博物館射 ? 許可 # 不許可 \@)

INPUT

SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		VARSET 博物館マップ
		RESTART
	CASE -3
		INVERTBIT 博物館射, 0
		RESTART
	CASEELSE
		XRES = RESULT/100
		YRES = RESULT%100
		SIF !INRANGE(XRES, 0, 博物館広さ-1) || !INRANGE(YRES, 0, 博物館広さ-1)
			RESTART
		ID = 博物館マップ:YRES:XRES
		IF ID
			PRINTFORML %MUSEUM:ID:名前%が設置されています。どうしますか？
			PRINTL [0] - 別の物に置き換える
			PRINTL [1] - 片付ける
			PRINTL [2] - 何もしない
			CALL INPUTF, 0, 1, 2
			SELECTCASE RESULT
				CASE 0
					GOTO SELECT_OBJECT
				CASE 1
					博物館マップ:YRES:XRES = 0
					RESTART
				CASE 2
					RESTART
			ENDSELECT
		ENDIF
		$SELECT_OBJECT
		PRINTL □何を設置しますか？
		FOR LCOUNT, 1, 1000
			IF MUSEUM:LCOUNT:種類 != "" && !設置済み:LCOUNT
				IF 処理数 >= ページ数*20+21
					次ページあり = 1
					BREAK
				ENDIF
				処理数++
				SIF ページ数*20 > 処理数
					CONTINUE
				CALL PASELI, @"　[{LCOUNT, 2}] %MUSEUM:LCOUNT:名前% 価値:%MUSEUM:LCOUNT:価値%＄ 集客力:%MUSEUM:LCOUNT:集客力%\% "
				PRINT 色:
				SETCOLOR TOINT(MUSEUM:LCOUNT:色)
				PRINTL ■
				RESETCOLOR
			ENDIF
		NEXT
		DRAWLINE
		PRINTPLAIN 　OBJECT　
		IF ページ数
			PRINTL [-1] 前ページ 
		ELSE
			PRINTPLAINFORM %SPACESLENS("[-1] 前ページ ")%
		ENDIF
		PRINTL [0] 戻る 
		SIF 次ページあり
			PRINTL [-9] 次ページ
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					RESTART
				CASE -1
					SIF ページ数
						ページ数--
					GOTO SELECT_OBJECT
				CASE -9
					SIF 次ページあり
						ページ数++
					GOTO SELECT_OBJECT
				CASEELSE
					ID = RESULT
					IF MUSEUM:ID:種類 != ""
						博物館マップ:YRES:XRES = ID
						RESTART
					ENDIF
					CONTINUE
			ENDSELECT
		LOOP 1
ENDSELECT

@博物館_CREATE


@INCOME_博物館
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNTX
#DIM DYNAMIC LCOUNTY
#DIM DYNAMIC ID
#DIM DYNAMIC 設置済み, 1000
#DIM DYNAMIC 総価値
#DIM DYNAMIC 収入
#DIM DYNAMIC 客数
#DIM DYNAMIC 名声上昇量
#DIM DYNAMIC 入場料
#DIM DYNAMIC 集客力平均
#DIM DYNAMIC 対象
#DIM DYNAMIC 総設置数

#DIM CONST 種類 = 0
#DIM CONST 名前 = 1
#DIM CONST ナンバー = 2
#DIM CONST 価値 = 3
#DIM CONST 色 = 4
#DIM CONST 血縁 = 5
#DIM CONST クローン = 6
#DIM CONST 集客力 = 7
#DIM CONST 汚れ = 8

SIF !ITEM:博物館
	RETURN

FOR LCOUNTY, 1, 50
	FOR LCOUNTX, 1, 50
		ID = 博物館マップ:LCOUNTY:LCOUNTX
		IF ID
			設置済み:ID = ID
			総価値 += TOINT(MUSEUM:ID:価値)*TOINT(MUSEUM:ID:集客力)/100
			集客力平均 = (集客力平均+TOINT(MUSEUM:ID:集客力))/2
			総設置数++
		ENDIF
	NEXT
NEXT

;総価値0=何も設置してなければ計算しない
SIF 総価値 == 0
	RETURN

;入場料は価値の平均+設置数*10 10未満切り上げ
入場料 = 総価値/総設置数
入場料 += 総設置数*10

入場料 = 入場料/10*10

;総価値/100or名声+広さ が一日の来客数上限
客数 = MAX(総価値/100, 博物館名声+博物館広さ)
;さらに集客力平均で下げる
客数 = 客数*集客力平均/100
;そこからさらにRANDでふるいにかける
客数 = RAND:(客数+1)

;客数だけ名声も上がる 一日に上がる名声は博物館広さと同じ値まで
名声上昇量 = MIN(客数, 博物館広さ)
;ただし客数以上の名声にはならない
SIF 博物館名声+名声上昇量 > 客数
	名声上昇量 = MAX(客数-博物館名声, 0)

博物館名声 += 名声上昇量

MONEY += 入場料*客数
博物館収入 = 入場料*客数

CALL PASELIL, @"博物館には{客数}人の客が訪れ、一人あたり入場料{入場料}＄で合計{入場料*客数}＄の収入がありました"
SIF 名声上昇量 > 0
	PRINTFORML 博物館の名声が{名声上昇量}上がりました

;集客力の変動
FOR LCOUNT, 1, 1000
	;設置中なら5下げる 最低1
	IF 設置済み:LCOUNT
		MUSEUM:LCOUNT:集客力 = {MAX(TOINT(MUSEUM:LCOUNT:集客力)-5, 1)}
	;設置してないやつは10上げる 最高150
	ELSE
		MUSEUM:LCOUNT:集客力 = {MIN(TOINT(MUSEUM:LCOUNT:集客力)+10, 150)}
	ENDIF
NEXT

;実用可能なら以下の処理
IF 博物館射
	;並びを適当にバラす
	FOR LCOUNT, 999, 1, -1
		SWAP 設置済み:LCOUNT, 設置済み:(RAND(1, LCOUNT))
	NEXT
	REPEAT 客数
		;ランダムで対象を決める
		対象 = RAND(1, 1000)
		WHILE !設置済み:対象
			対象++
			SIF 対象 == 1000
				対象 = 1
		WEND
		;0～2回汚される
		MUSEUM:(設置済み:対象):汚れ = {TOINT(MUSEUM:(設置済み:対象):汚れ)+RAND:2}
		DEBUGPRINTFORML %MUSEUM:(設置済み:対象):名前%+
	REND
ENDIF
	
	


