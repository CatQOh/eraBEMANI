@STRATEGY_BROTHEL
CALL BR_ITEM_POINT

SIF 娼館広さ == 0
	娼館広さ = 1
SIF 娼館部屋数 == 0
	娼館部屋数 = 1
SIF 娼館方針 == 0
	娼館方針 = 1

DRAWLINE
PRINTL 
PRINTL 【娼館経営】
PRINTL 
DRAWLINE
SIF 娼館店名 != "娼館"
	PRINTFORM 娼館の店名：%娼館店名% 
PRINTFORML 娼館レベル:{娼館レベル}
CALL PASELIL, @"次のランクアップまであと{娼館レベルアップ必要額}＄の売上が必要"
DRAWLINE

LOCALS = <button value='1' title='娼館に道具を配置します'>　[1] - 道具配置</button>
HTML_PRINT LOCALS
LOCALS = <button value='2' title='娼館用の物件を改装します'>　[2] - 物件増築</button>
HTML_PRINT LOCALS
LOCALS = <button value='3' title='娼館の運営方針を決定します'>　[3] - 運営方針</button>
HTML_PRINT LOCALS
LOCALS = <button value='4' title='娼館で働かせるキャラを配属します'>　[4] - 人員配置</button>
HTML_PRINT LOCALS
LOCALS = <button value='5' title='娼館の店名を変更します'>　[5] - 店名変更</button>
HTML_PRINT LOCALS
IF FLAG:娼館営業中
	LOCALS = <button value='9' title='娼館を一時休業し、収入と支出が行われないようにします'>　[9] - 一時休業</button>
ELSE
	LOCALS = <button value='9' title='娼館の営業を再開し、収入と支出が行われるようにします'>　[9] - 通常営業</button>
ENDIF
HTML_PRINT LOCALS

DRAWLINE
PRINTPLAIN 　BROTHEL　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL BR_ITEM
		CASE 2
			CALL BR_RENOVATION
		CASE 3
			CALL BR_PLAN
		CASE 4
			CALL STRATEGY_PERSONAL, "娼館"
		CASE 5
			PRINT □店名を入力してください
			SIF GC店名 != "娼館"
				PRINTFORM  現在の店名：%娼館店名%
			PRINTL 
			PRINTBUTTON "[変更しない]", "変更しない"
			PRINTL 
			INPUTS
			SIF RESULTS == "変更しない"
				RESTART
			PRINTFORMW 店名を「%RESULTS%」に変更しました
			娼館店名 = %RESULTS%
		CASE 9
			IF FLAG:娼館営業中
				PRINTW 娼館の経営を一時中断しました
			ELSE
				PRINTW 娼館の経営を開始しました
			ENDIF
			INVERTBIT FLAG:娼館営業中, 0
		CASE 100
			CALL BR_TUTORIAL
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

RESTART

;----------------------------------------------------
;娼館経営モードチュートリアル
;----------------------------------------------------
@BR_TUTORIAL
DRAWLINE
PRINTL 当モードは娼館を経営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「道具配置」とは？
PRINTL [2] - 「物件増築」とは？
PRINTL [3] - 「経営方針」とは？
PRINTL [4] - 「人員配置」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 道具を配置するコマンドです。
			PRINTL 調教用の道具とはまた別物で、娼館で使用するものは配置分だけ毎日コストを支払う必要があります。
			PRINTL 道具にはそれぞれ用途に合わせたポイントがあり、経営方針に見合った道具を配置することで収入にプラスの補正が入ります。
			PRINTW デフォルトでは1種類しか配置できませんが、増築することによって配置可能な数が増えます。
		CASE 2
			PRINTL 物件を増築し、より多くの道具や奴隷を配置できるようにするコマンドです。
			PRINTL ゲームセンター経営と違い、物件はデフォルトで所持しているので経営自体はすぐに始められます。
			PRINTW 増築には当然お金がかかりますが、序盤は小規模でも特に気になるものではないと思います。
		CASE 3
			PRINTL 運営方針を決めるコマンドです。
			PRINTL 運営には方針が必要であり、これを決めないとほとんど経営放棄も同然です。
			PRINTW 奴隷の身体をよく案じて考えて、適切な方針を定めましょう。
		CASE 4
			PRINTL 奴隷から人員を選出するコマンドです。
			PRINTL 当然身体を売る奴隷がいないと収入はありません。売春を行ったキャラは、売春の回数に応じて売却額が下がっていきます。
			PRINTL 体力が減っている、もしくはストレス過多の奴隷は配置しても売春できないため、奴隷の身体をちゃんと考えてあげましょう。
			PRINTW (数値上1000以上減っていると自動で休憩に入り、回復後に売春を行います)
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

@BR_ITEM
#DIM LCOUNT
#DIM 娼館道具設置数
CALL BR_ITEM_POINT
PRINTL □道具を配置します
DRAWLINE
PRINTL 【現在のポイント】
PRINTFORMLC ソフト - {ソフトポイント}
PRINTFORMLC ハード - {ハードポイント}
PRINTFORMLC SM - {SMポイント}
PRINTFORMLC 狂気 - {狂気ポイント}
PRINTL 
DRAWLINE

娼館道具設置数 = 0
FOR LCOUNT, 1500, 1580
	SIF ITEM:LCOUNT
		娼館道具設置数++
NEXT
PRINT  *配置枠*［
LOCAL = 0
REPEAT 娼館広さ
	IF LOCAL < 娼館道具設置数
		SETCOLOR 0 , 100 , 250
			PRINT ■
		RESETCOLOR
	ELSE
		SETCOLOR 120 , 120 , 120
			PRINT ■
		RESETCOLOR
	ENDIF
	LOCAL++
REND
PRINTL ］
DRAWLINE
LOCAL = 0
FOR LCOUNT, 1500, 1580
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	SIF LOCAL%3 == 0
		PRINT 　
	PRINTFORM [{LCOUNT}] %SUBSTRINGU(ITEMNAME:LCOUNT, 2, -1)%
	CALL PASELI, @" 維持費:{ITEMPRICE:LCOUNT}＄"
	IF ITEM:LCOUNT == 0
		PRINT 【
		SETCOLOR 120 , 120 , 120
			PRINT 未配置
		RESETCOLOR
		PRINT 】
	ELSE
		PRINT 【
		SETCOLOR 0 , 100 , 250
			PRINT 配置中
		RESETCOLOR
		PRINT 】
	ENDIF
	LOCAL++
	SIF LOCAL%3 == 0
		PRINTL 
NEXT
PRINTL 
DRAWLINE
PRINTPLAIN 　PERSONAL　
PRINTL [100] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 100
			RETURN 0
		CASEELSE
			SIF ITEMNAME:RESULT == ""
				CONTINUE
			IF ITEM:RESULT == 0
				IF 娼館道具設置数 >= 娼館広さ
					PRINTW 【これ以上配置できません】
				ELSE
					PRINTFORMW %SUBSTRINGU(ITEMNAME:RESULT, 2, -1)%を配置しました
					ITEM:RESULT = 1
					SIF FLAG:口上表示
						TRYCALLFORM BR_ITEM_K{FLAG:パートナー}, RESULT
				ENDIF
			ELSE
				PRINTFORMW %SUBSTRINGU(ITEMNAME:RESULT, 2, -1)%を回収しました
				ITEM:RESULT = 0
			ENDIF
			RESTART
	ENDSELECT
LOOP 1

@PRINT_BR_ITEM
#DIM LCOUNT
VARSET LOCAL, 0
PRINTFORML 【設備】
FOR LCOUNT, 1580, 1600
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF LOCAL == 0
		PRINT 　
	PRINTFORM [%ITEMNAME:LCOUNT%]
	LOCAL += STRLENS(ITEMNAME:LCOUNT)
	IF LOCAL > 80
		PRINTL 
		LOCAL = 0
	ENDIF
	LOCAL:1++
NEXT
SIF !LOCAL:1
	PRINT 　[設備無し]
SIF !LINEISEMPTY()
	PRINTL 

;娼館増設----------------------------------------------------
@BR_RENOVATION
LOCAL = POWER(娼館広さ, 2)*10000
LOCAL:1 = POWER(2, 娼館部屋数)*10000
PRINTL □物件を増設します
PRINTFORMLC 　広さ:{娼館広さ}
PRINTFORMLC 部屋数:{娼館部屋数}
PRINTL 
DRAWLINE
CALL PRINT_BR_ITEM
DRAWLINE

IF 娼館広さ >= 10
	SETCOLOR 120, 120, 120
	PRINTPLAIN 　[-] - これ以上広く出来ません
	PRINTL 
	RESETCOLOR
ELSE
	CALL PASELIL, @"　[1] - 広くする({LOCAL}＄)"
ENDIF
CALL PASELIL, @"　[2] - 部屋を増やす({LOCAL:1}＄)"
PRINTFORML 　[3] - 設備を導入する
DRAWLINE
PRINTPLAIN 　RENOVATION　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			SIF 娼館広さ >= 10
				CONTINUE
			IF LOCAL > MONEY
				PRINTW 所持金が足りません
				RESTART
			ENDIF
			PRINTL 娼館を広くしますか？
			PRINTFORML 広さ {娼館広さ}→{娼館広さ+1}
			CALL PASELIL, @"増設後の所持金 {MONEY-LOCAL}＄"
			PRINTL 
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 娼館を広くしました
						MONEY -= LOCAL
						娼館広さ++
						SIF FLAG:口上表示
							TRYCALLFORM BR_RENOVATION_K{FLAG:パートナー}, 1
						RESTART
				ENDSELECT
			LOOP LOOPRES(0, 1)
		CASE 2
			IF LOCAL:1 > MONEY
				PRINTW 所持金が足りません
				RESTART
			ENDIF
			PRINTL 娼館の部屋を増やしますか？
			PRINTFORML 部屋数 {娼館部屋数}→{娼館部屋数+1}
			CALL PASELIL, @"増設後の所持金 {MONEY-LOCAL:1}＄"
			PRINTL 
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTW 娼館の部屋を増やしました
						MONEY -= LOCAL:1
						娼館部屋数++
						SIF FLAG:口上表示
							TRYCALLFORM BR_RENOVATION_K{FLAG:パートナー}, 2
						RESTART
				ENDSELECT
			LOOP LOOPRES(0, 1)
		CASE 3
			CALL BR_ITEM_BUY
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

;設備導入----------------------------------------------------
@BR_ITEM_BUY
#DIM DYNAMIC 表示数
#DIM DYNAMIC LCOUNT, 2
#DIM DYNAMIC 単位文字数
表示数 = 0
DRAWLINE
PRINTL □設備を導入します
DRAWLINE
CALL PRINT_BR_ITEM
DRAWLINE

FOR LCOUNT, 1580, 1600
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	SIF 表示数 == 0
		PRINT 　
	IF ITEM:LCOUNT
		SETCOLOR 120, 120, 120
		PRINTFORMLC [導入済み] %ITEMNAME:LCOUNT%
		RESETCOLOR
	ELSE
		LOCALS = [{LCOUNT}] %ITEMNAME:LCOUNT%({ITEMPRICE:LCOUNT}＄)
		CALL PASELI, LOCALS
		;PRINT_IMGでPRINTC系命令使うとエラーになるので疑似再現
		IF FLAG:パセリ表示
			単位文字数 = 2
		ELSEIF 通貨単位 != ""
			単位文字数 = STRLENS(通貨単位)
		ELSE
			単位文字数 = 1
		ENDIF
		;＄の分2文字と通貨単位分
		FOR LCOUNT:1, 0, 28-(STRLENS(LOCALS)-2)-単位文字数
			PRINT  
		NEXT
	ENDIF
	表示数++
	IF 表示数 == 4
		PRINTL 
		表示数 = 0
	ENDIF
NEXT

SIF !LINEISEMPTY()
	PRINTL 

DRAWLINE
PRINTPLAIN 　ITEM　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASEELSE
			SIF ITEMNAME:RESULT == ""
				CONTINUE
			SIF ITEM:RESULT
				CONTINUE
			IF ITEMPRICE:RESULT > MONEY
				PRINTW 所持金が足りません
				RESTART
			ENDIF
			DRAWLINE
			LOCAL = RESULT
			PRINTFORM %ITEMNAME:RESULT%:
			SELECTCASE ITEMNAME:RESULT
				CASE "お風呂場"
					PRINTL 性行為の前には体を洗いましょう
				CASE "高級ベッド"
					PRINTL 広々とした空間で新たなプレイを
				CASE "隠しカメラ"
					PRINTL マジックミラーの後ろに仕込む隠しカメラ
				CASE "換気扇"
					PRINTL 臭いと不快感を減らす
				CASE "ピンクライト"
					PRINTL 売春の雰囲気UP!
				CASE "防音壁"
					PRINTL 外に声が漏れないので遠慮なく楽しめます
				CASE "拷問器具"
					PRINTL 普通のSMプレイにも飽きた人へ
				CASE "わけのわからないもの"
					PRINTL わけのわからない人たちが使う
			ENDSELECT
			CALL PASELIL, @"導入後の所持金 {MONEY}＄→{MONEY-ITEMPRICE:RESULT}＄"
			PRINTL [0] - 導入する
			PRINTL [1] - 導入しない
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						PRINTFORMW %ITEMNAME:LOCAL%を導入しました
						ITEM:LOCAL = 1
						MONEY -= ITEMPRICE:LOCAL
						SIF FLAG:口上表示
							TRYCALLFORM BR_ITEM_K{FLAG:パートナー}, LOCAL
				ENDSELECT
			LOOP LOOPRES(0, 1)
			RESTART
	ENDSELECT
LOOP 1


;運営方針----------------------------------------------------
@BR_PLAN
;CALL BR_ITEM_POINT
DRAWLINE
PRINTL 【現在のポイント】
PRINTFORMLC 　ソフト - {ソフトポイント}
PRINTFORMLC ハード - {ハードポイント}
PRINTFORMLC SM - {SMポイント}
PRINTFORMLC 狂気 - {狂気ポイント}
PRINTL 
DRAWLINE
PRINTFORML □現在の方針：%娼館方針名(娼館方針)%
;PRINTL 　[0] - 方針無し
PRINTL 　[1] - ソフト方針
PRINTL 　[2] - ハード方針
PRINTL 　[3] - SM方針
PRINTL 　[4] - 狂気方針
PRINTFORML 　[5] - プレイ内容を報告させる（現在\@ FLAG:娼館報告 ? オン # オフ \@）
PRINTFORML 　[6] - 上級娼婦を任命する（現在の上級娼婦:\@ FINDCHARA(CFLAG:上級娼婦, 1) >= 0 ? %NAME:(FINDCHARA(CFLAG:上級娼婦, 1))% # 無し\@）
DRAWLINE
PRINTPLAIN 　PLAN　
PRINT [10] 戻る 
PRINTL [20] 各方針の説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 1 TO 4
			娼館方針 = RESULT
			SIF FLAG:口上表示
				TRYCALLFORM BR_PLAN_K{FLAG:パートナー}, RESULT
		CASE 5
			INVERTBIT FLAG:娼館報告, 0
		CASE 6
			LOCAL = FINDCHARA(CFLAG:上級娼婦, 1)
			SIF LOCAL >= 0
				CFLAG:LOCAL:上級娼婦 = 0
			DRAWLINE
			PRINTL □上級娼婦に任命したいキャラを選択してください
			FOR PAGING, 0, CHARANUM
				CALL PAGING, CFLAG:PAGING:配属 == 2 && FALLTYPE(PAGING) == 2, "", "上級娼婦", "任命しない"
			NEXT
			SELECTCASE RESULT
				CASE -1
					PRINTW 上級娼婦を任命しません
				CASEELSE
					PRINTFORMW %CALLNAME:RESULT%を上級娼婦に任命します
					CFLAG:RESULT:上級娼婦 = 1
					SIF !CFLAG:RESULT:上級娼婦に任命された経験
						CFLAG:RESULT:名刺 = 5
					CFLAG:RESULT:上級娼婦に任命された経験++
			ENDSELECT
			BREAK
		CASE 10
			RETURN 0
		CASE 20
			PRINTL 0-方針無し
			PRINTL ├特に方針を指定しません。初期設定でもあります。
			PRINTL ├無計画ということなので方針としての効果は皆無。
			PRINTL └何かしら方針は定めたほうが良いでしょう。
			PRINTL 
			PRINTL 1-ソフト売春
			PRINTL ├本番無しなど店側の制約を多く設けた方針です。
			PRINTL ├体力や気力の消費が軽く経験の浅い奴隷でもそれなりの効果があります。
			PRINTL └しかし基本的に経験が上がりにくく、調教としての効果は薄いかもしれません。
			PRINTL 
			PRINTL 2-ハード方針
			PRINTL ├経験豊富で淫乱な奴隷ほど効果が高まる方針です。
			PRINTL ├各種経験に多めの数値が入り、収入も安定しています。
			PRINTL └経験が浅く適正でない奴隷の場合、収入も低く体力消費も大きいため序盤にはおすすめできません。
			PRINTL 
			PRINTL 3-SM方針
			PRINTL ├いろいろとマニアックなプレイを専門とする方針です。
			PRINTL ├一部素質で強力なプラス補正がかかり、逆にこういったプレイに向かない奴隷の場合は
			PRINTL └多大な体力消費など、マイナスの効果が大きいかもしれません。
			PRINTL 
			PRINTL 4-狂気方針
			PRINTL ├ごく一部の稀有なクライアント向けの方針です。
			PRINTL ├脱法的な経営なので、客は少なく、ランクが低いとまったく収入がありません。
			PRINTL └崩壊した奴隷や特殊な素質を持った奴隷の効果が高く、玄人向けの方針といえるでしょう。
			PRINTL 
			PRINTL 5-プレイ内容を報告させる
			PRINTL ├売春での奉仕内容を収入計算時に報告させます。
			PRINTL └オンオフで収入に違いは無いので奴隷の動向が気になるならオンにしてみましょう。
			PRINTL 
			PRINTL 6-上級娼婦を任命する
			PRINTL ├淫乱、もしくは娼婦の素質を持ったキャラを上級娼婦に任命できます。
			PRINTL ├上級娼婦に任命されたキャラは、売春中にプレイアブル可能になります。
			PRINTL ├運営方針に応じて可能な行為、効果的な行為が変わり、お客様の満足度によって得られる報酬も増減します。
			PRINTW └お客様とのより濃厚な売春を楽しみたいならぜひ任命してみましょう。
	ENDSELECT
LOOP LOOPRES(0, 1, 2, 3, 4, 5, 20)

RESTART

;道具名----------------------------------------------------
@BR_ITEM_POINT
#DIM LCOUNT
ソフトポイント = 0
ハードポイント = 0
SMポイント = 0
狂気ポイント = 0

FOR LCOUNT, 1500, 1580
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	SIF !ITEM:LCOUNT
		CONTINUE
	SELECTCASE ITEMNAME:LCOUNT
		CASE "娼館ローター"
			ソフトポイント += 20
			ハードポイント += 10
			狂気ポイント += 5
		CASE "娼館バイブ"
			ハードポイント += 10
			SMポイント += 20
			狂気ポイント += 15
		CASE "娼館鞭"
			SMポイント += 30
			狂気ポイント += 20
		CASE "娼館縄"
			SMポイント += 25
			狂気ポイント += 35
		CASE "娼館ペニスバンド"
			ハードポイント += 10
			SMポイント += 30
			狂気ポイント += 5
		CASE "娼館コンドーム"
			ソフトポイント += 5
			ハードポイント += 20
			SMポイント += 15
			狂気ポイント += 5
		CASE "娼館ローション"
			ソフトポイント += 15
			ハードポイント += 20
			SMポイント += 10
			狂気ポイント += 5
		CASE "娼館媚薬"
			ソフトポイント += 20
			ハードポイント += 40
			SMポイント += 35
			狂気ポイント += 30
		CASE "娼館睡眠薬"
			ハードポイント += 5
			SMポイント += 10
			狂気ポイント += 30
		CASE "娼館ゴム手袋"
			ソフトポイント += 15
			狂気ポイント += 5
	ENDSELECT
NEXT

SIF ITEM:ピンクライト
	TIMES ソフトポイント , 1.50
SIF ITEM:防音壁
	TIMES ハードポイント , 1.50
SIF ITEM:拷問器具
	TIMES SMポイント , 1.50
SIF ITEM:わけのわからないもの
	TIMES 狂気ポイント , 1.50

;収入計算----------------------------------------------------
@INCOME_BR
#DIM LCOUNT
#DIM DYNAMIC 収入
#DIM DYNAMIC 収入合計
#DIM DYNAMIC 支出
#DIM DYNAMIC 適性
#DIM DYNAMIC 不適性

#DIM DYNAMIC 客の性別
#DIM DYNAMIC 絶頂
#DIM DYNAMIC 射精
#DIM DYNAMIC 客の射精
#DIM DYNAMIC 自慰
#DIM DYNAMIC 自慰で射精
#DIM DYNAMIC フェラ
#DIM DYNAMIC フェラで射精
#DIM DYNAMIC ヴァギナ
#DIM DYNAMIC 中出し
#DIM DYNAMIC アナル
#DIM DYNAMIC アナル中出し
#DIM DYNAMIC 手コキ
#DIM DYNAMIC 手コキで射精
#DIM DYNAMIC 足コキ
#DIM DYNAMIC 足コキで射精
#DIM DYNAMIC 兜合わせ
#DIM DYNAMIC 兜合わせで射精
#DIM DYNAMIC 素股
#DIM DYNAMIC 素股で射精
#DIM DYNAMIC ゲイ
#DIM DYNAMIC レズ
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LMASTER
#DIM DYNAMIC LASSI

収入合計 += 娼館レベル*100

収入合計 += ソフトポイント*10
収入合計 += ハードポイント*10
収入合計 += SMポイント*10
収入合計 += 狂気ポイント*10

DRAWLINE

LOCAL = FINDCHARA(CFLAG:上級娼婦, 1)
IF LOCAL >= 0
	;方針設定してないとダメ
	SIF 娼館方針 == 0
		GOTO 通常営業
	;体力不足
	IF BASE:LOCAL:体力 <= 1000
		PRINTFORML %CALLNAME:LOCAL%は疲労で娼館を休みました
		CFLAG:LOCAL:休憩 = 1
		GOTO 通常営業
	ENDIF
	IF CFLAG:LOCAL:ストレス値 > 90 && !TALENT:LOCAL:崩壊
		PRINTFORML %CALLNAME:LOCAL%はストレス過多のため、娼館を休みました
		CFLAG:LOCAL:休憩 = 1
		GOTO 通常営業
	ENDIF
	LTARGET = TARGET
	LMASTER = MASTER
	LASSI = ASSI
	CALL 上級売春
	TARGET = LTARGET
	MASTER = LMASTER
	ASSI = LASSI
ENDIF

$通常営業

FOR LCOUNT, 1, CHARANUM
	;当然配属してないとダメ
	SIF CFLAG:LCOUNT:配属 != 2
		CONTINUE
	;配属完了してないとダメ
	IF !CFLAG:LCOUNT:配属完了
		CFLAG:LCOUNT:配属完了 = 1
		CONTINUE
	ENDIF
	;上級娼婦は上で処理してるので除外
	SIF CFLAG:LCOUNT:上級娼婦 && 娼館方針
		CONTINUE
	DRAWLINE
	;体力不足
	IF BASE:LCOUNT:体力 <= 1000
		PRINTFORML %CALLNAME:LCOUNT%は疲労で娼館を休みました
		CFLAG:LCOUNT:休憩 = 1
		CONTINUE
	ENDIF
	IF CFLAG:LCOUNT:ストレス値 > 90 && !TALENT:LCOUNT:崩壊
		PRINTFORML %CALLNAME:LCOUNT%はストレス過多のため、娼館を休みました
		CFLAG:LCOUNT:休憩 = 1
		CONTINUE
	ENDIF
	;1000パセリは保証
	収入 = 1000
	適性 = 0
	不適性 = 0
	;方針による補正
	SELECTCASE 娼館方針
		;ソフト
		CASE 1
			;まずはプラス素質から計算
			IF TALENT:LCOUNT:淫乱
				収入 += 300
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:娼婦
				収入 += 500
				適性 += 3
			ENDIF
			IF TALENT:LCOUNT:素直
				収入 += 200
				適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:臆病
				収入 += 100
			
			IF TALENT:LCOUNT:プライド低い
				収入 += 150
				適性++
			ENDIF
			IF TALENT:LCOUNT:大人しい
				収入 += 50
				適性++
			ENDIF
			IF TALENT:LCOUNT:好奇心
				収入 += 150
				適性++
			ENDIF
			SIF TALENT:LCOUNT:恥じらい
				収入 += 50
			SIF TALENT:LCOUNT:恥薄い
				適性++
			SIF TALENT:LCOUNT:痛みに強い
				適性 += 2
			SIF TALENT:LCOUNT:舌使い
				収入 += 200
			IF TALENT:LCOUNT:自慰しやすい
				収入 += 50
				適性++
			ENDIF
			SIF TALENT:LCOUNT:汚臭鈍感
				適性++
			SIF TALENT:LCOUNT:汚れ無視
				適性++
			IF TALENT:LCOUNT:献身的
				収入 += 150
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:快感に素直
				収入 += 100
				適性++
			ENDIF
			IF TALENT:LCOUNT:淫核
				収入 += 150
				適性++
			ENDIF
			IF TALENT:LCOUNT:両刀
				収入 += 50
				適性++
			ENDIF
			IF TALENT:LCOUNT:男好き
				収入 += 100
				適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:小悪魔
				収入 += 50
			SIF TALENT:LCOUNT:魅力
				収入 += 50
			SIF TALENT:LCOUNT:魅惑
				収入 += 50
			SIF TALENT:LCOUNT:謎の魅力
				収入 += 100
			;客がロリコンの確率は半々
			IF TALENT:LCOUNT:小柄体型
				IF RAND:2 == 0
					収入 += 100
				ELSE
					収入 -= 50
				ENDIF
				;どちらにせよ負担が大きいので不適性
				不適性++
			ENDIF
			;大きいのが好きか小さいのが好きかはランダム
			IF RAND:2 == 0
				収入 += (6-TALENT:LCOUNT:バストサイズ)*10
			ELSE
				収入 += TALENT:LCOUNT:バストサイズ*10
			ENDIF
			SIF TALENT:LCOUNT:ファーリー
				収入 += 30
			SIF TALENT:LCOUNT:尻尾
				収入 += 30
			SIF TALENT:LCOUNT:動物耳
				収入 += 30
			;小人も人による
			IF TALENT:LCOUNT:小人体型
				IF RAND:3 == 0
					収入 += 200
				ELSE
					収入 -= 100
				ENDIF
				不適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:ふたなり
				収入 += 50
			IF TALENT:LCOUNT:人気
				収入 += 100
				適性++
			ENDIF

			;ここからマイナス素質
			IF TALENT:LCOUNT:恋慕
				収入 -= 50
				不適性++
			ENDIF
			IF TALENT:LCOUNT:親愛
				収入 -= 100
				不適性 += 2
			ENDIF
			IF TALENT:LCOUNT:生意気
				収入 -= 50
				不適性++
			ENDIF
			IF TALENT:LCOUNT:反抗的
				収入 -= 50
				不適性++
			ENDIF
			IF TALENT:LCOUNT:プライド高い
				収入 -= 30
				不適性++
			ENDIF
			SIF TALENT:LCOUNT:ツンデレ
				収入 -= 30
			SIF TALENT:LCOUNT:無関心
				収入 -= 30
			SIF TALENT:LCOUNT:保守的
				不適性++
			SIF TALENT:LCOUNT:悲観的
				不適性++
			SIF TALENT:LCOUNT:抵抗
				不適性 += 2
			SIF TALENT:LCOUNT:痛みに弱い
				不適性 += 2
			SIF TALENT:LCOUNT:汚臭敏感
				不適性++
			SIF TALENT:LCOUNT:潔癖症
				不適性++
			IF TALENT:LCOUNT:男嫌い
				収入 -= 50
				不適性 += 2
			ENDIF
			IF TALENT:LCOUNT:同族嫌悪
				収入 -= 50
				不適性++
			ENDIF
			;ソフトでは適性-10
			不適性 += 10
			;ここから能力で倍率計算
			収入 += (収入/20)*ABL:LCOUNT:従順
			適性 += ABL:LCOUNT:従順/3

			収入 += (収入/10)*ABL:LCOUNT:欲望
			適性 += ABL:LCOUNT:欲望/3

			収入 += (収入/5)*ABL:LCOUNT:技巧
			適性 += ABL:LCOUNT:技巧/2

			収入 += (収入/10)*ABL:LCOUNT:奉仕精神
			適性 += ABL:LCOUNT:奉仕精神/2
		;ハード
		CASE 2
			;まずはプラス素質から計算
			SIF TALENT:LCOUNT:処女
				収入 += 2000
			IF TALENT:LCOUNT:淫乱
				収入 += 500
				適性++
			ENDIF
			IF TALENT:LCOUNT:娼婦
				収入 += 1000
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:素直
				収入 += 300
				適性++
			ENDIF
			IF TALENT:LCOUNT:プライド低い
				収入 += 200
				適性++
			ENDIF
			IF TALENT:LCOUNT:大人しい
				収入 += 100
				適性++
			ENDIF
			IF TALENT:LCOUNT:好奇心
				収入 += 200
				適性++
			ENDIF
			IF TALENT:LCOUNT:貞操無頓着
				収入 += 100
				適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:恥じらい
				収入 += 150
			SIF TALENT:LCOUNT:恥薄い
				適性++
			SIF TALENT:LCOUNT:痛みに強い
				適性 += 2
			SIF TALENT:LCOUNT:舌使い
				収入 += 100
			SIF TALENT:LCOUNT:汚れ無視
				適性++
			IF TALENT:LCOUNT:献身的
				収入 += 300
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:快感に素直
				収入 += 200
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:淫膣
				収入 += 300
				適性 += 2
			ENDIF
			IF TALENT:LCOUNT:両刀
				収入 += 100
			ENDIF
			IF TALENT:LCOUNT:男好き
				収入 += 100
				適性++
			ENDIF
			SIF TALENT:LCOUNT:魅力
				収入 += 100
			SIF TALENT:LCOUNT:魅惑
				収入 += 100
			SIF TALENT:LCOUNT:謎の魅力
				収入 += 200
			;客がロリコンの確率は半々
			IF TALENT:LCOUNT:小柄体型
				IF RAND:2 == 0
					収入 += 300
				ELSE
					収入 -= 100
				ENDIF
				;どちらにせよ負担が大きいので不適性
				不適性++
			ENDIF
			SIF TALENT:LCOUNT:Ｖ敏感
				適性++
			SIF TALENT:LCOUNT:Ａ敏感
				適性++
			;大きいのが好きか小さいのが好きかはランダム
			IF RAND:2 == 0
				収入 += (6-TALENT:LCOUNT:バストサイズ)*20
			ELSE
				収入 += TALENT:LCOUNT:バストサイズ*20
			ENDIF
			SIF TALENT:LCOUNT:ファーリー
				収入 += 50
			SIF TALENT:LCOUNT:尻尾
				収入 += 50
			SIF TALENT:LCOUNT:動物耳
				収入 += 50
			;小人も人による
			IF TALENT:LCOUNT:小人体型
				IF RAND:3 == 0
					収入 += 500
				ELSE
					収入 -= 300
				ENDIF
				不適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:ふたなり
				収入 += 100
			IF TALENT:LCOUNT:人気
				収入 += 200
				適性++
			ENDIF

			;ここからマイナス素質
			IF TALENT:LCOUNT:恋慕
				収入 -= 150
				不適性++
			ENDIF
			IF TALENT:LCOUNT:親愛
				収入 -= 300
				不適性 += 2
			ENDIF
			IF TALENT:LCOUNT:生意気
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:反抗的
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:プライド高い
				収入 -= 50
				不適性 += 2
			ENDIF
			SIF TALENT:LCOUNT:ツンデレ
				収入 -= 50
			SIF TALENT:LCOUNT:無関心
				収入 -= 50
			SIF TALENT:LCOUNT:保守的
				不適性++
			SIF TALENT:LCOUNT:悲観的
				不適性++
			IF TALENT:LCOUNT:貞操観念
				収入 -= 50
				不適性++
			ENDIF
			SIF TALENT:LCOUNT:抵抗
				不適性 += 2
			SIF TALENT:LCOUNT:痛みに弱い
				不適性 += 2
			SIF TALENT:LCOUNT:潔癖症
				不適性 += 2
			IF TALENT:LCOUNT:男嫌い
				収入 -= 200
				不適性 += 3
			ENDIF
			IF TALENT:LCOUNT:同族嫌悪
				収入 -= 100
				不適性++
			ENDIF
			SIF TALENT:LCOUNT:Ｖ鈍感
				不適性++
			SIF TALENT:LCOUNT:Ａ鈍感
				不適性++
			;ハードは適性-15
			不適性 += 15
			;ここから能力で倍率計算
			収入 += (収入/10)*ABL:LCOUNT:従順
			適性 += ABL:LCOUNT:従順/3

			収入 += (収入/10)*ABL:LCOUNT:欲望
			適性 += ABL:LCOUNT:欲望/2

			収入 += (収入/5)*ABL:LCOUNT:技巧
			適性 += ABL:LCOUNT:技巧/3

			IF VAGINA(LCOUNT)
				収入 += (収入/20)*ABL:LCOUNT:Ｖ感覚
				適性 += ABL:LCOUNT:Ｖ感覚/5
			ENDIF

			収入 += (収入/20)*ABL:LCOUNT:Ａ感覚
			適性 += ABL:LCOUNT:Ａ感覚/5

			収入 += (収入/10)*ABL:LCOUNT:奉仕精神
			適性 += ABL:LCOUNT:奉仕精神/3

			収入 += (収入/5)*ABL:LCOUNT:性交中毒
			適性 += ABL:LCOUNT:性交中毒/2
		;SM
		CASE 3
			;まずはプラス素質から計算
			;SMは我が強い子が結構稼げる 責めに回っても受けに回っても需要がある
			SIF TALENT:LCOUNT:処女
				収入 += 1000
			SIF TALENT:LCOUNT:生意気
				収入 += 200
			SIF TALENT:LCOUNT:反抗的
				収入 += 200
			SIF TALENT:LCOUNT:気丈
				収入 += 100
			SIF TALENT:LCOUNT:衝動的
				収入 += 150
			SIF TALENT:LCOUNT:プライド高い
				収入 += 150
			SIF TALENT:LCOUNT:保守的
				収入 += 100
			SIF TALENT:LCOUNT:一線越えない
				収入 += 100
			SIF TALENT:LCOUNT:貞操観念
				収入 += 150
			SIF TALENT:LCOUNT:抑圧
				収入 += 100
			SIF TALENT:LCOUNT:抵抗
				収入 += 150
			SIF TALENT:LCOUNT:弱味
				収入 += 50
			IF TALENT:LCOUNT:痛みに強い
				収入 += 50
				適性 += 3
			ENDIF
			SIF TALENT:LCOUNT:針さばき
				収入 += 100
			SIF TALENT:LCOUNT:おもらし癖
				収入 += 100
			SIF TALENT:LCOUNT:快感に素直
				適性 += 2
			SIF TALENT:LCOUNT:快感の否定
				収入 += 50
			SIF TALENT:LCOUNT:即落ち
				収入 += 50
			SIF TALENT:LCOUNT:倒錯的
				収入 += 200
			SIF TALENT:LCOUNT:両刀
				収入 += 50
			SIF TALENT:LCOUNT:サド
				収入 += 100
			SIF TALENT:LCOUNT:マゾ
				収入 += 100
			IF TALENT:LCOUNT:戦士
				収入 += 100
				適性++
			ENDIF
			SIF TALENT:LCOUNT:ふたなり
				収入 += 150
			;マイナス
			IF TALENT:LCOUNT:臆病
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:素直
				収入 -= 50
				不適性++
			ENDIF
			IF TALENT:LCOUNT:大人しい
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:無関心
				収入 -= 150
				不適性++
			ENDIF
			IF TALENT:LCOUNT:感情乏しい
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:恥じらい
				収入 -= 50
				不適性 += 2
			ENDIF
			IF TALENT:LCOUNT:寂しがり屋
				収入 -= 100
				不適性++
			ENDIF
			IF TALENT:LCOUNT:痛みに弱い
				収入 -= 50
				不適性 += 5
			ENDIF
			;SMは適性がかなり上がりにくいので適性-5
			不適性 += 5
			;ここから能力で倍率計算
			収入 += (収入/20)*ABL:LCOUNT:従順
			適性 += ABL:LCOUNT:従順/3+1

			収入 += (収入/20)*ABL:LCOUNT:欲望
			適性 += ABL:LCOUNT:欲望/5

			収入 += (収入/20)*ABL:LCOUNT:技巧
			適性 += ABL:LCOUNT:技巧/5

			IF VAGINA(LCOUNT)
				収入 += (収入/20)*ABL:LCOUNT:Ｖ感覚
				適性 += ABL:LCOUNT:Ｖ感覚/5
			ENDIF

			収入 += (収入/20)*ABL:LCOUNT:Ａ感覚
			適性 += ABL:LCOUNT:Ａ感覚/5

			収入 += (収入/10)*ABL:LCOUNT:露出癖
			適性 += ABL:LCOUNT:露出癖/5

			収入 += (収入/5)*ABL:LCOUNT:マゾっ気
			適性 += ABL:LCOUNT:マゾっ気/2

			収入 += (収入/5)*ABL:LCOUNT:サドっ気
			適性 += ABL:LCOUNT:サドっ気/2
		;ルナティック
		CASE 4
			;まずはプラス素質から計算
			IF TALENT:LCOUNT:崩壊
				収入 += 2000
				適性++
			ENDIF
			IF TALENT:LCOUNT:弱味
				収入 += 300
				適性++
			ENDIF
			IF TALENT:LCOUNT:倒錯的
				収入 += 500
				適性++
			ENDIF
			IF TALENT:LCOUNT:妄信
				収入 += 500
				適性++
			ENDIF
			SIF TALENT:LCOUNT:狐
				収入 += 300
			IF TALENT:LCOUNT:ネクロマンサー
				収入 += 300
				適性++
			ENDIF
			SIF TALENT:LCOUNT:バストサイズ == 5
				収入 += 200
			SIF TALENT:LCOUNT:ファーリー
				収入 += 200
			IF TALENT:LCOUNT:無機物
				収入 += 500
				適性++
			ENDIF
			SIF TALENT:LCOUNT:羽
				収入 += 100
			SIF TALENT:LCOUNT:小人体型
				収入 += 1000
			SIF TALENT:LCOUNT:性器破損
				収入 += 500
			IF TALENT:LCOUNT:ふたなり
				収入 += 300
				適性++
			ENDIF
			SIF TALENT:LCOUNT:吸血鬼
				収入 += 500
			SIF TALENT:LCOUNT:幼児／幼児退行
				収入 += 300
			SIF TALENT:LCOUNT:幼稚
				収入 += 300
			IF TALENT:LCOUNT:狂気
				収入 += 1000
				適性++
			ENDIF
			SIF TALENT:LCOUNT:夢喰い
				収入 += 200
			SIF TALENT:LCOUNT:ドラゴン使い
				収入 += 200
			IF TALENT:LCOUNT:リミットバースト
				収入 += 500
				適性++
			ENDIF
			;マイナス
			不適性 = 20
			;娼館レベルに応じて変動
			収入 = 収入*娼館レベル/50
			;ここから能力で倍率計算
			収入 += (収入/20)*ABL:LCOUNT:従順
			適性 += ABL:LCOUNT:従順/3

			収入 += (収入/20)*ABL:LCOUNT:欲望
			適性 += ABL:LCOUNT:欲望/3

			収入 += (収入/20)*ABL:LCOUNT:技巧
			適性 += ABL:LCOUNT:技巧/3
	ENDSELECT
	;体力気力消費計算
	LOCAL = 不適性 - 適性
	SIF LOCAL < 1
		LOCAL = 1
	SELECTCASE 娼館方針
		;ソフト
		CASE 1
			BASE:LCOUNT:体力 -= LOCAL * 50
		;ハード
		CASE 2
			BASE:LCOUNT:体力 -= LOCAL * 100
		;SM
		CASE 3
			BASE:LCOUNT:体力 -= LOCAL * 300
		;狂気
		CASE 4
			BASE:LCOUNT:体力 -= LOCAL * 200
	ENDSELECT
	SIF BASE:LCOUNT:体力 <= 0
		BASE:LCOUNT:体力 = 1
	;プレミアムフライデー
	SIF PREMIUM_FRIDAY(YEAR, MONTH, DAYS)
		TIMES 収入, 1.10

	収入合計 += 収入

	客の性別 = 0
	絶頂 = 0
	射精 = 0
	客の射精 = 0
	自慰 = 0
	自慰で射精 = 0
	フェラ = 0
	フェラで射精 = 0
	ヴァギナ = 0
	中出し = 0
	アナル = 0
	アナル中出し = 0
	手コキ = 0
	手コキで射精 = 0
	足コキ = 0
	足コキで射精 = 0
	兜合わせ = 0
	兜合わせで射精 = 0
	素股 = 0
	素股で射精 = 0
	ゲイ = 0
	レズ = 0


	SELECTCASE 娼館方針
		CASE 1
			絶頂 = RAND:6
			;射精 = RAND:6
			自慰 = RAND:4
			SIF 自慰 && PENIS(LCOUNT)
				自慰で射精 = RAND:2
			フェラ = RAND:8+3
			フェラで射精 = RAND:3
			IF PENIS(LCOUNT)
				兜合わせ = RAND:3
				SIF 兜合わせ
					兜合わせで射精 = RAND:2
				射精 = RAND:3+自慰で射精
			ELSE
				素股 = RAND:3
				SIF 素股
					素股で射精 = RAND:2
			ENDIF
			足コキ = RAND:3
			SIF 足コキ == 2
				足コキで射精 = RAND:2
			手コキ = RAND:3
			SIF 手コキ == 2
				手コキで射精 = RAND:2
			SIF SEX(LCOUNT) == 1
				ゲイ = RAND:4+1
			客の射精 = フェラで射精+手コキで射精+足コキで射精
		CASE 2
			IF VAGINA(LCOUNT)
				ヴァギナ = RAND:11
			ELSE
				アナル = RAND:11
			ENDIF
			絶頂 = RAND:6
			フェラ = RAND:2
			フェラで射精 = フェラ
			アナル中出し = RAND:3
			SIF VAGINA(LCOUNT) && 客の性別 != 2
				中出し = RAND:3
			SIF PENIS(LCOUNT)
				射精 = RAND:6
			SIF SEX(LCOUNT) == 1
				ゲイ = RAND:10+3
			SIF ヴァギナ == 0 && アナル == 0
				アナル++
			客の射精 = 中出し+アナル中出し+フェラで射精
		CASE 3
			客の性別 = RAND:4
			;たまにマゾが来る
			IF 客の性別 != 2 && RAND:5 == 0
				手コキで射精 = RAND:3+1
				手コキ = 手コキで射精
				足コキで射精 = RAND:3+1
				足コキ = 足コキで射精
			ELSE
				アナル = RAND:4
				SIF VAGINA(LCOUNT)
					ヴァギナ = RAND:4
				絶頂 = RAND:4
				自慰 = RAND:4+3
				SIF 自慰 && PENIS(LCOUNT)
					自慰で射精 = RAND:3
				SIF アナル && RAND:4 == 0
					アナル中出し = 1
				SIF VAGINA(LCOUNT) && 客の性別 != 2 && RAND:3 == 0
					中出し = 1
				SIF アナル == 0 && ヴァギナ == 0
					アナル++
				客の射精 = RAND:2
			ENDIF
			SIF PENIS(LCOUNT)
				射精 = RAND:6+自慰で射精
			SIF 客の性別 != 2
				フェラ = RAND:4
			SIF フェラ
				フェラで射精 = RAND:フェラ
			SELECTCASE SEX(LCOUNT)
				CASE 1
					SIF 客の性別 <= 1
						ゲイ = RAND:4+2
				CASE 2
					SIF 客の性別 >= 2
						レズ = RAND:4+2
			ENDSELECT
			客の射精 = 中出し+アナル中出し+手コキで射精+足コキで射精+フェラで射精
		CASE 4
			客の性別 = RAND:4
			SIF VAGINA(LCOUNT)
				ヴァギナ = RAND:26+5
			アナル = RAND:26+5
			SIF 客の性別 != 2
				客の射精 = RAND:2
			SIF PENIS(LCOUNT)
				射精 = RAND:11
			SELECTCASE SEX(LCOUNT)
				CASE 1
					SIF 客の性別 <= 1
						ゲイ = RAND:10+1
				CASE 2
					SIF 客の性別 >= 2
						レズ = RAND:10+1
			ENDSELECT
	ENDSELECT

	IF FLAG:娼館報告 && 娼館方針
		IF FLAG:口上表示
			TRYCCALLFORM BR_PLAY_K{NO:LCOUNT}, 客の性別, 絶頂, 射精, 客の射精, 自慰, 自慰で射精, フェラ, フェラで射精, ヴァギナ, 中出し, アナル, アナル中出し, 手コキ, 手コキで射精, 足コキ, 足コキで射精, 兜合わせ, 兜合わせで射精, 素股, 素股で射精, ゲイ, レズ, 
			CATCH
				$NOTKOJO
				PRINTFORML %CALLNAME:LCOUNT%が今日の仕事を報告してきた。
				PRINTFORM %CALLNAME:LCOUNT%は
				SIF 娼館方針 == 3 && 手コキで射精
					PRINT マゾヒストの
				SELECTCASE 客の性別
					CASE IS <= 1
						PRINT 男性客を相手に
					CASE 2
						PRINT 女性客を相手に
					CASE 3
						PRINT ふたなり女性客を相手に
				ENDSELECT
				IF 客の性別 != 2
					IF フェラ
						PRINT 口で奉仕
						IF フェラで射精
							PRINTFORM して{フェラで射精}回射精させた
						ELSE
							PRINT した
						ENDIF
						IF ヴァギナ || アナル || 手コキ || 足コキ || 素股 || 兜合わせ
							PRINT 後、
						ELSE
							PRINT 。
						ENDIF
					ENDIF
					IF 手コキ || 足コキ
						IF 手コキ && 足コキ
							IF 手コキで射精 && 足コキで射精
								PRINTFORM 手で{手コキで射精}回、足で{足コキで射精}回イかせ
							ELSEIF 手コキで射精 || 足コキで射精
								PRINTFORM 手と足で扱いて{手コキで射精+足コキで射精}回イかせ
							ELSE
								PRINTFORM 手と足で扱
							ENDIF
						ELSEIF 手コキ
							IF 手コキで射精
								PRINTFORM 手で{手コキで射精}回イかせ
							ELSE
								PRINT 手で扱
							ENDIF
						ELSEIF 足コキ
							IF 足コキで射精
								PRINTFORM 足で{足コキで射精}回イかせ
							ELSE
								PRINT 足で扱
							ENDIF
						ENDIF
						IF 手コキで射精 || 足コキで射精
							IF ヴァギナ || アナル || 素股 || 兜合わせ
								PRINT 、
							ELSE
								PRINT た。
							ENDIF
						ELSE
							IF ヴァギナ || アナル || 素股 || 兜合わせ
								PRINT き、
							ELSE
								PRINT いたが、射精させるには至らなかった。
							ENDIF
						ENDIF
					ENDIF
					IF 兜合わせ
						PRINTFORM 客と陰茎を擦り合わせ
						IF 兜合わせで射精
							PRINTFORM て{兜合わせで射精}回射精させた。
						ELSE
							PRINT たが、射精させるには至らなかった。
						ENDIF
					ENDIF
					IF 素股
						PRINTFORM 性行為の代わりに秘部を擦り合わせ
						IF 素股で射精
							PRINTFORM て{素股で射精}回射精させた。
						ELSE
							PRINT たが、射精させるには至らなかった。
						ENDIF
					ENDIF
					IF ヴァギナ || アナル
						SIF ヴァギナ
							PRINT 膣
						SIF ヴァギナ && アナル
							PRINT と
						SIF アナル
							PRINT アナル
						PRINT で性行為に
						IF 中出し || アナル中出し
							PRINT 及び、
							IF 中出し && アナル中出し
								PRINTFORM 膣に{中出し}回、アナルに{アナル中出し}回中出しされた。
							ELSEIF 中出し
								PRINTFORM {中出し}回中出しされた。
							ELSEIF アナル中出し
								PRINTFORM {アナル中出し}回中出しされた。
							ENDIF
						ELSE
							PRINT 及んだが、射精させるには至らなかった。
						ENDIF
					ENDIF
					PRINTL 
				ENDIF
				IF (アナル || ヴァギナ) && (絶頂 || 射精)
					SIF 客の性別 != 2
						PRINTFORM %CALLNAME:LCOUNT%は
					SIF アナル
						PRINT アナル
					SIF アナル && ヴァギナ
						PRINT や
					SIF ヴァギナ
						PRINT 膣内
					PRINT を
					SIF 客の性別 == 2
						PRINT 指や道具で
					SIF ヴァギナ > 7 || アナル > 7
						PRINT 激しく
					PRINT 犯された
					IF 射精 == 0 && 絶頂 == 0
						PRINTL ようだ。
					ELSE
						PRINT 挙句、
						IF 中出し
							PRINT 膣に中出しされながら
						ELSEIF アナル中出し
							PRINT アナルに中出しされながら
						ELSEIF ヴァギナ > アナル
							PRINT ヴァギナへの快感で
						ELSE
							PRINT アナルへの快感で
						ENDIF
						SIF 射精 >= 3 && 絶頂 >= 3
							PRINT 何回も
						IF 射精
							PRINTL 射精してしまったようだ。
						ELSE
							PRINTL 絶頂に達してしまったようだ。
						ENDIF
					ENDIF
				ENDIF
				IF 自慰
					PRINT その後、自慰を命じられ
					IF 自慰で射精
						PRINTFORML 、%CALLNAME:LCOUNT%は上手に自分で射精できたようだ。
					ELSEIF 絶頂
						PRINTFORML 、%CALLNAME:LCOUNT%は上手に自分で絶頂に達せたようだ。
					ELSE
						PRINTFORML たが、一人で絶頂を迎えることはできなかったらしい。
					ENDIF
				ENDIF
				WAIT
			ENDCATCH
		ELSE
			GOTO NOTKOJO
		ENDIF
	ENDIF
	SIF ヴァギナ
		PRINTFORML Ｖ経験+{ヴァギナ}(%CALLNAME:LCOUNT%)
	IF ヴァギナ && TALENT:LCOUNT:処女
		TALENT:LCOUNT:処女 = 0
		IF TALENT:LCOUNT:性転換済
			VIRGIN:LCOUNT:11 = 娼館に訪れた客
		ELSE
			VIRGIN:LCOUNT:1 = 娼館に訪れた客
		ENDIF
	ENDIF
	SIF アナル
		PRINTFORML Ａ経験+{アナル}(%CALLNAME:LCOUNT%)
	SIF 絶頂
		PRINTFORML 絶頂経験+{絶頂}(%CALLNAME:LCOUNT%)
	SIF 射精
		PRINTFORML 射精経験+{射精}(%CALLNAME:LCOUNT%)
	SIF 中出し
		PRINTFORML 膣射経験+{中出し}(%CALLNAME:LCOUNT%)
	SIF 客の射精
		PRINTFORML 精液経験+{客の射精}(%CALLNAME:LCOUNT%)
	SIF 自慰
		PRINTFORML 自慰経験+{自慰}(%CALLNAME:LCOUNT%)
	SIF フェラ
		PRINTFORML フェラ経験+{フェラ}(%CALLNAME:LCOUNT%)
	SIF レズ
		PRINTFORML レズ経験+{レズ}(%CALLNAME:LCOUNT%)
	SIF ゲイ
		PRINTFORML ゲイ経験+{ゲイ}(%CALLNAME:LCOUNT%)

	EXP:LCOUNT:Ｖ経験 += ヴァギナ
	EXP:LCOUNT:Ａ経験 += アナル
	EXP:LCOUNT:膣射経験 += 中出し
	EXP:LCOUNT:フェラ経験 += フェラ
	EXP:LCOUNT:絶頂経験 += 絶頂
	EXP:LCOUNT:射精経験 += 射精
	EXP:LCOUNT:精液経験 += 客の射精
	EXP:LCOUNT:自慰経験 += 自慰
	EXP:LCOUNT:レズ経験 += レズ
	EXP:LCOUNT:ゲイ経験 += ゲイ
	TALENT:LCOUNT:傷物++
	;お風呂場があるとストレス溜まりづらくなる
	IF ITEM:お風呂場
		CFLAG:LCOUNT:ストレス値 += 娼館方針
	ELSE
		CFLAG:LCOUNT:ストレス値 += 娼館方針*2
	ENDIF
	;換気口あるとさらに軽減
	SIF ITEM:換気口
		CFLAG:LCOUNT:ストレス値 -= 1
	;実務経験を加算
	EXP:LCOUNT:実務経験++
NEXT

;方針とポイントに応じて上下する
SELECTCASE 娼館方針
	;ソフト
	CASE 1
		収入合計 += 収入合計*ソフトポイント/100
	;ハード
	CASE 2
		収入合計 += 収入合計*ハードポイント/100
	;SM
	CASE 3
		収入合計 += 収入合計*SMポイント/100
	;狂気
	CASE 4
		収入合計 += 収入合計*狂気ポイント/100
ENDSELECT

;隠し撮りビデオを売却して追加報酬
SIF ITEM:隠しカメラ
	収入合計 += 収入合計/10

IF 収入合計
	CALL PASELIL, @"%娼館店名%の収入が{収入合計}＄入りました"
	MONEY += 収入合計
	娼館前日の収入 = 収入合計
	娼館総収入 += 収入合計
	DO
		LOCAL = 0
		;高級ベッドがあるとレベルが上がりやすくなる
		IF ITEM:高級ベッド
			REPEAT 娼館レベル
				LOCAL = 700*(COUNT*COUNT)
			REND
		ELSE
			REPEAT 娼館レベル
				LOCAL += 1000*(COUNT*COUNT)
			REND
		ENDIF
		SIF 娼館総収入 >= LOCAL
			娼館レベル++
	LOOP 娼館総収入 >= LOCAL

	娼館レベルアップ必要額 = LOCAL-娼館総収入

	FOR LCOUNT, 1500, 1580
		SIF ITEM:LCOUNT
			支出 += ITEMPRICE:LCOUNT
	NEXT
	SIF 支出
		CALL PASELIL, @"%娼館店名%の支出は{支出}＄でした"
	MONEY -= 支出
ENDIF


