@STRATEGY_PERSONAL, 配属先
#DIM DYNAMIC ページ数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 配属可能人数
#DIM DYNAMIC 飛ばした人数
#DIM DYNAMIC 配属人数
#DIMS DYNAMIC 配属先
#DIM DYNAMIC 配属上限
#DIM DYNAMIC LTARGET
VARSET LOCAL, 0
配属人数 = 0

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;分野ごとの配属上限人数を設定しておく
SELECTCASE 配属先
	CASE "ゲームセンター"
		配属上限 = 10
	CASE "娼館"
		配属上限 = 娼館部屋数
	CASE "芸能プロダクション"
		配属上限 = 20
	CASE "医務室"
		配属上限 = 3
	CASE "図書室"
		配属上限 = 3
	CASE "実験室"
		配属上限 = 5
ENDSELECT

;既に配属されている人数を計算
配属人数 = CMATCH(CSTR:配属, 配属先)

DRAWLINE
PRINTFORML □%配属先%に配属する人員を選択してください(現在{配属人数}/{配属上限})

配属可能人数 = 0

;配属可能なキャラを調べる
REPEAT CHARANUM
	;主人は無理
	SIF COUNT == MASTER
		CONTINUE
	SELECTCASE 配属先
		;ゲーセンと芸能と各施設は助手可能が条件
		CASE "ゲームセンター", "芸能プロダクション", "医務室", "図書室"
			SIF CFLAG:COUNT:状態 < 2
				CONTINUE
		;娼館は売却可能で配置可能
		CASE "娼館"
			;売却可能じゃないと無理
			SIF !CFLAG:COUNT:状態
				CONTINUE
	ENDSELECT
	;妊娠中、育児中は無理
	SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中
		CONTINUE
	;使用不可のキャラも無理
	SIF CFLAG:COUNT:使用不可
		CONTINUE
	配属可能人数++
	;配属可能なキャラに対応したLOCALが1になる
	LOCAL:COUNT = 1
REND

飛ばした人数 = 0
表示数 = 0

;配属できるキャラがいる
IF 配属可能人数
	REPEAT CHARANUM
		SIF !LOCAL:COUNT
			CONTINUE
		;2ページ目以降はページ×20まで表示を飛ばす
		IF 飛ばした人数 < ページ数*20
			飛ばした人数++
			CONTINUE
		ENDIF
		;その分野に配属中のキャラには◯マークが付く
		IF CSTR:COUNT:配属 == 配属先
			PRINT ◯
		ELSE
			PRINT 　
		ENDIF
		;配属先によって表示が違う
		SELECTCASE 配属先
			CASE "ゲームセンター"
				PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(家事技能:{ABL:COUNT:家事技能, 2, LEFT} 工作技能:{ABL:COUNT:工作技能, 2, LEFT} 料理技能:{ABL:COUNT:料理技能, 2, LEFT}
				SIF TALENT:COUNT:料理人
					PRINT  料理人
				SIF TALENT:COUNT:工学博士
					PRINT  工学博士
				SIF TALENT:COUNT:音ゲーマー
					PRINT  音ゲーマー
				SIF TALENT:COUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "娼館"
				PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(従順:{ABL:COUNT:従順, 2, LEFT} 欲望:{ABL:COUNT:欲望, 2, LEFT} 技巧:{ABL:COUNT:技巧, 2, LEFT}
				SIF TALENT:COUNT:淫乱
					PRINT  淫乱
				SIF TALENT:COUNT:娼婦
					PRINT  娼婦
				SIF TALENT:COUNT:淫核
					PRINT  淫核
				SIF TALENT:COUNT:淫膣
					PRINT  淫膣
				SIF TALENT:COUNT:淫尻
					PRINT  淫尻
				SIF TALENT:COUNT:淫乳
					PRINT  淫乳
				SIF TALENT:COUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "芸能プロダクション"
				PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
				SIF TALENT:COUNT:魅力
					PRINT  魅力
				SIF TALENT:COUNT:魅惑
					PRINT  魅惑
				SIF TALENT:COUNT:謎の魅力
					PRINT  謎の魅力
				SIF TALENT:COUNT:人気
					PRINT  人気
				SIF TALENT:COUNT:絶対音感
					PRINT  絶対音感
				SIF TALENT:COUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "医務室"
				PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(技巧:{ABL:COUNT:技巧, 2, LEFT}
				SIF TALENT:COUNT:調合知識
					PRINT  調合知識
				SIF TALENT:COUNT:治療
					PRINT  治療
				IF TALENT:COUNT:謎の魅力
					PRINT  謎の魅力
				ELSEIF TALENT:COUNT:魅力
					PRINT  魅力
				ELSEIF TALENT:COUNT:魅惑
					PRINT  魅惑
				ELSEIF TALENT:COUNT:人気
					PRINT  人気
				ENDIF
				PRINT )
			CASE "図書室"
				PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(実務経験:{EXP:COUNT:実務経験, 4, LEFT}
				SIF TALENT:COUNT:買い物上手
					PRINT  買い物上手
				SIF TALENT:COUNT:潔癖症
					PRINT  潔癖症
				SIF TALENT:COUNT:禁断の知識
					PRINT  禁断の知識
				IF TALENT:COUNT:謎の魅力
					PRINT  謎の魅力
				ELSEIF TALENT:COUNT:魅力
					PRINT  魅力
				ENDIF
				PRINT )
			CASE "実験室"
				PRINTFORM 　%NAME:COUNT% 工作技能:{ABL:COUNT:工作技能, 2, LEFT} サドっ気:[ABL:COUNT:サドっ気, 2, LEFT] マゾっ気:{ABL:COUNT:マゾっ気, 2, LEFT}
				SIF TALENT:COUNT:工学博士
					PRINT  工学博士
				SIF TALENT:COUNT:サド
					PRINT  サド
				SIF TALENT:COUNT:マゾ
					PRINT  マゾ
				SIF TALENT:COUNT:倒錯的
					PRINT  倒錯的
				SIF TALENT:COUNT:調合知識
					PRINT  調合知識
				PRINT )
		ENDSELECT
		SELECTCASE CSTR:COUNT:配属
			CASE ""
				IF COUNT == TARGET
					PRINTL 【調教中】
				ELSEIF COUNT == ASSI
					PRINTL 【助手中】
				ELSE
					PRINTL 【休憩中】
				ENDIF
			CASEELSE
				PRINTFORML 【%CSTR:COUNT:配属%配属中】
		ENDSELECT
		表示数++
		;20人表示したらページ終わり
		SIF 表示数 == 20
			BREAK
	REND
ELSE
	PRINTL 
	PRINTL 【現在配属できるキャラはいません】
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数+1}　
IF ページ数 > 0
	PRINTLC [999]前ページ
ELSE
	PRINTLC  
ENDIF
PRINTLC [1000] 戻る　
;表示できるキャラがまだ居れば次ページ
IF 表示数+20*ページ数 < 配属可能人数 
	PRINTLC [1001]次ページ
ELSE
	PRINTLC  
ENDIF
PRINTL [1002] 全員帰還

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 0
				ページ数--
		CASE 1000
			VARLINE = 0
			RETURN 0
		CASE 1001
			;表示できるキャラがまだ居れば次ページ
			SIF 表示数+20*ページ数 < 配属可能人数 
				ページ数++
		CASE 1002
			REPEAT CHARANUM
				SIF CSTR:COUNT:配属 == 配属先
					CALL 配属解除, COUNT
			REND
				SELECTCASE 配属先
					CASE "医務室", "図書室", "実験室"
						PRINTFORMW %配属先%の全員を担当から外しました
					CASEELSE
						PRINTFORMW %配属先%に配属してる人員が全員帰宅しました
				ENDSELECT
		CASEELSE
			SIF !LOCAL:RESULT
				CONTINUE
			SELECTCASE CSTR:RESULT:配属
				CASE 配属先
					SELECTCASE 配属先
						CASE "医務室", "図書室", "実験室"
							PRINTFORMW %CALLNAME:RESULT%を%配属先%担当から外しました
						CASEELSE
							PRINTFORMW %CALLNAME:RESULT%が帰宅しました
					ENDSELECT
					CALL 配属解除, RESULT
					LTARGET = TARGET
					TARGET = RESULT
					;配属解除時の口上
					IF FLAG:口上表示
						SELECTCASE 配属先
							CASE "ゲームセンター"
								TRYCALLFORM GC_ASSIGNOFF_K{NO:RESULT}
							CASE "娼館"
								TRYCALLFORM BR_ASSIGNOFF_K{NO:RESULT}
							CASE "芸能プロダクション"
								TRYCALLFORM IDOL_ASSIGNOFF_K{NO:RESULT}
						ENDSELECT
					ENDIF
					TARGET = LTARGET
					RESTART
				CASEELSE
					IF 配属人数 >= 配属上限
						PRINTW これ以上配属できません
						RESTART
					ENDIF
					PRINTFORMW %CALLNAME:RESULT%を配属しました
					CALL 配属解除, RESULT
					CSTR:RESULT:配属 = %配属先%
					LTARGET = TARGET
					TARGET = RESULT
					;配属時の口上
					IF FLAG:口上表示
						SELECTCASE 配属先
							CASE "ゲームセンター"
								TRYCALLFORM GC_ASSIGNON_K{NO:RESULT}
							CASE "娼館"
								TRYCALLFORM BR_ASSIGNON_K{NO:RESULT}
							CASE "芸能プロダクション"
								TRYCALLFORM IDOL_ASSIGNON_K{NO:RESULT}
						ENDSELECT
					ENDIF
					IF TARGET > 0
						TARGET = -1
					ELSEIF ASSI == TARGET
						ASSI = -1
					ELSE
						TARGET = LTARGET
					ENDIF
					;実績:日向美ビタースイーツ♪のメンバーを全員娼館に配属する
					LOCAL = 0
					IF 配属先 == "娼館"
						REPEAT CHARANUM
							IF CSTR:COUNT:配属 == "娼館"
								SELECTCASE CHARANAMEF(COUNT)
									CASE "山形 まり花", "和泉 一舞", "春日 咲子", "霜月 凛", "芽兎 めう"
										LOCAL++
								ENDSELECT
							ENDIF
						REND
						SIF LOCAL == 5
							CALL 実績解除, "日向美商店街で噂の売春宿"
					ENDIF
					RESTART
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(999, 1001, 1002)

RESTART

@配属解除, ARG
CSTR:ARG:配属 = 
CFLAG:ARG:配属完了 = 0
CFLAG:ARG:ゲーセン店長 = 0
CFLAG:ARG:上級娼婦 = 0
CFLAG:ARG:レッスン = 0

