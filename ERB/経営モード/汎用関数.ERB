@STRATEGY_PERSONAL, 配属先, 配属部屋
#DIM DYNAMIC ページ数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 配属可能人数
#DIM DYNAMIC 飛ばした人数
#DIM DYNAMIC 配属人数
#DIMS DYNAMIC 配属先
#DIM DYNAMIC 配属上限
#DIM DYNAMIC 配属部屋
#DIM DYNAMIC LTARGET
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 配属可能, 10000
VARSET LOCAL, 0
配属人数 = 0

SIF VARLINE
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;分野ごとの配属上限人数を設定しておく
SELECTCASE 配属先
	CASE "ゲームセンター"
		配属上限 = 10
	CASE "娼館"
		配属上限 = 2
	CASE "芸能プロダクション"
		配属上限 = 20
	CASE "医務室"
		配属上限 = 3
	CASE "図書室"
		配属上限 = 3
	CASE "実験室"
		配属上限 = 5
	CASE "農業"
		;プランターで一人、畑は各1人*8、田んぼは各3人*2、最大で合計15人
		配属上限 = ITEM:畑+ITEM:田んぼ*3
		SIF ITEM:プランター
			配属上限++
	CASE "畜産業"
		配属上限 = 10
	CASE "調理室"
		配属上限 = 3
ENDSELECT

;既に配属されている人数を計算
IF 配属先 == "娼館"
	配属人数 = CMATCH(CFLAG:娼館部屋番号, 配属部屋)
ELSE
	配属人数 = CMATCH(CSTR:配属, 配属先)
ENDIF

DRAWLINE
IF 配属先 == "娼館"
	PRINTFORML □娼館の{配属部屋}番ルームに配属するキャラを選んでください
ELSE
	PRINTFORML □%配属先%に配属する人員を選択してください(現在{配属人数}/{配属上限})
ENDIF

配属可能人数 = 0

;配属可能なキャラを調べる
FOR LCOUNT, 0, CHARANUM
	;主人は無理
	SIF LCOUNT == MASTER
		CONTINUE
	;アニマルも当然無理
	SIF TALENT:LCOUNT:アニマル
		CONTINUE
	SELECTCASE 配属先
		;ゲーセンと芸能と各施設は助手可能が条件
		CASE "ゲームセンター", "芸能プロダクション", "医務室", "図書室", "調理室"
			SIF CFLAG:LCOUNT:状態 < 2
				CONTINUE
		;娼館は売却可能で配置可能
		CASE "娼館"
			;売却可能じゃないと無理
			SIF !CFLAG:LCOUNT:状態
				CONTINUE
		;農業と畜産業は状態に関わらず配属可
		CASE "農業", "畜産業"
	ENDSELECT
	;妊娠中、育児中は無理
	SIF TALENT:LCOUNT:妊娠 || TALENT:LCOUNT:育児中
		CONTINUE
	;使用不可のキャラも無理
	SIF CFLAG:LCOUNT:使用不可
		CONTINUE
	配属可能人数++
	;配属可能なキャラに対応したLOCALが1になる
	配属可能:LCOUNT = 1
NEXT

飛ばした人数 = 0
表示数 = 0

;配属できるキャラがいる
IF 配属可能人数
	FOR LCOUNT, 0, CHARANUM
		SIF !配属可能:LCOUNT
			CONTINUE
		;2ページ目以降はページ×20まで表示を飛ばす
		IF 飛ばした人数 < ページ数*20
			飛ばした人数++
			CONTINUE
		ENDIF
		;その分野に配属中のキャラには◯マークが付く 娼館の場合は部屋番号
		IF CSTR:LCOUNT:配属 == 配属先
			IF 配属先 == "娼館"
				PRINTFORM {CFLAG:LCOUNT:娼館部屋番号, 2}
			ELSE
				PRINT ◯
			ENDIF
		ELSE
			PRINT 　
		ENDIF
		;配属先によって表示が違う
		SELECTCASE 配属先
			CASE "ゲームセンター"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(家事技能:{ABL:LCOUNT:家事技能, 2, LEFT} 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} 料理技能:{ABL:LCOUNT:料理技能, 2, LEFT}
				SIF TALENT:LCOUNT:料理人
					PRINT  料理人
				SIF TALENT:LCOUNT:工学博士
					PRINT  工学博士
				SIF TALENT:LCOUNT:音ゲーマー
					PRINT  音ゲーマー
				SIF TALENT:LCOUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "娼館"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(従順:{ABL:LCOUNT:従順, 2, LEFT} 欲望:{ABL:LCOUNT:欲望, 2, LEFT} 技巧:{ABL:LCOUNT:技巧, 2, LEFT}
				SIF TALENT:LCOUNT:淫乱
					PRINT  淫乱
				SIF TALENT:LCOUNT:娼婦
					PRINT  娼婦
				SIF TALENT:LCOUNT:淫核
					PRINT  淫核
				SIF TALENT:LCOUNT:淫膣
					PRINT  淫膣
				SIF TALENT:LCOUNT:淫尻
					PRINT  淫尻
				SIF TALENT:LCOUNT:淫乳
					PRINT  淫乳
				SIF TALENT:LCOUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "芸能プロダクション"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(歌唱技能:{ABL:LCOUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:LCOUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:LCOUNT:舞踊技能, 2, LEFT}
				SIF TALENT:LCOUNT:魅力
					PRINT  魅力
				SIF TALENT:LCOUNT:魅惑
					PRINT  魅惑
				SIF TALENT:LCOUNT:謎の魅力
					PRINT  謎の魅力
				SIF TALENT:LCOUNT:人気
					PRINT  人気
				SIF TALENT:LCOUNT:絶対音感
					PRINT  絶対音感
				SIF TALENT:LCOUNT:サボり魔
					PRINT  サボり魔
				PRINT )
			CASE "医務室"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(奉仕精神:{ABL:LCOUNT:奉仕精神, 2, LEFT}
				SIF TALENT:LCOUNT:調合知識
					PRINT  調合知識
				SIF TALENT:LCOUNT:治療
					PRINT  治療
				IF TALENT:LCOUNT:謎の魅力
					PRINT  謎の魅力
				ELSEIF TALENT:LCOUNT:魅力
					PRINT  魅力
				ELSEIF TALENT:LCOUNT:魅惑
					PRINT  魅惑
				ELSEIF TALENT:LCOUNT:人気
					PRINT  人気
				ENDIF
				PRINT )
			CASE "図書室"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(実務経験:{EXP:LCOUNT:実務経験, 4, LEFT}
				SIF TALENT:LCOUNT:買い物上手
					PRINT  買い物上手
				SIF TALENT:LCOUNT:潔癖症
					PRINT  潔癖症
				SIF TALENT:LCOUNT:禁断の知識
					PRINT  禁断の知識
				IF TALENT:LCOUNT:謎の魅力
					PRINT  謎の魅力
				ELSEIF TALENT:LCOUNT:魅力
					PRINT  魅力
				ENDIF
				PRINT )
			CASE "実験室"
				PRINTFORM 　%NAME:LCOUNT% 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} サドっ気:[ABL:LCOUNT:サドっ気, 2, LEFT] マゾっ気:{ABL:LCOUNT:マゾっ気, 2, LEFT}
				SIF TALENT:LCOUNT:工学博士
					PRINT  工学博士
				SIF TALENT:LCOUNT:サド
					PRINT  サド
				SIF TALENT:LCOUNT:マゾ
					PRINT  マゾ
				SIF TALENT:LCOUNT:倒錯的
					PRINT  倒錯的
				SIF TALENT:LCOUNT:調合知識
					PRINT  調合知識
				PRINT )
			CASE "農業"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(栽培技能:{ABL:LCOUNT:栽培技能, 2, LEFT} 工作技能:{ABL:LCOUNT:工作技能, 2, LEFT} 魔法技能:{ABL:LCOUNT:魔法技能, 2, LEFT}
				;SIF TALENT:LCOUNT:調合知識
				;	PRINT  調合知識
				;SIF TALENT:LCOUNT:工学博士
				;	PRINT  工学博士
				;SIF TALENT:LCOUNT:魔術師
				;	PRINT  魔術師
				;SIF TALENT:LCOUNT:専業主婦
				;	PRINT  専業主婦
				PRINT )
			CASE "畜産業"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(飼育技能:{ABL:LCOUNT:飼育技能, 2, LEFT} 飼育適性:{飼育レベル(LCOUNT), 2, LEFT}
				SIF TALENT:LCOUNT:世話好き
					PRINT  世話好き
				SELECTCASE CSTR:LCOUNT:種族
					CASE "人魚"
						PRINT  人魚
					CASE "獣人"
						PRINT  獣人
					CASE "獣"
						PRINT  獣
					CASE "精霊"
						PRINT  精霊
					CASE "妖精"
						PRINT  妖精
				ENDSELECT
				PRINT )
			CASE "調理室"
				PRINTFORM [{LCOUNT, 2}] - %NAME:LCOUNT, 26, LEFT%(料理技能:{ABL:LCOUNT:料理技能, 2, LEFT}
				SIF TALENT:LCOUNT:料理人
					PRINT  料理人
				SIF TALENT:LCOUNT:買い物上手
					PRINT  買い物上手
				SELECTCASE CSTR:LCOUNT:種族
					CASE "獣人"
						PRINT  獣人
					CASE "獣"
						PRINT  獣
					CASE "ゾンビ"
						PRINT  ゾンビ
				ENDSELECT
				PRINT )
		ENDSELECT
		SELECTCASE CSTR:LCOUNT:配属
			CASE ""
				IF LCOUNT == TARGET
					PRINTL 【調教中】
				ELSEIF LCOUNT == ASSI
					PRINTL 【助手中】
				ELSE
					PRINTL 【休憩中】
				ENDIF
			CASEELSE
				PRINTFORML 【%CSTR:LCOUNT:配属%配属中】
		ENDSELECT
		表示数++
		;20人表示したらページ終わり
		SIF 表示数 == 20
			BREAK
	NEXT
ELSE
	PRINTL 
	PRINTL 【現在配属できるキャラはいません】
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数+1}　
IF ページ数 > 0
	PRINTLC [999]前ページ
ELSE
	PRINTLC  
ENDIF
PRINTLC [1000] 戻る　
;表示できるキャラがまだ居れば次ページ
IF 表示数+20*ページ数 < 配属可能人数 
	PRINTLC [1001]次ページ
ELSE
	PRINTLC  
ENDIF
PRINTL [1002] 全員帰還

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 0
				ページ数--
		CASE 1000
			VARLINE = 0
			RETURN 0
		CASE 1001
			;表示できるキャラがまだ居れば次ページ
			SIF 表示数+20*ページ数 < 配属可能人数 
				ページ数++
		CASE 1002
			REPEAT CHARANUM
				SIF CSTR:COUNT:配属 == 配属先
					CALL 配属解除, COUNT
			REND
			SELECTCASE 配属先
				CASE "医務室", "図書室", "実験室", "調理室"
					PRINTFORMW %配属先%の全員を担当から外しました
				CASEELSE
					PRINTFORMW %配属先%に配属してる人員が全員帰宅しました
			ENDSELECT
			RESTART
		CASEELSE
			SIF !配属可能:RESULT
				CONTINUE
			SELECTCASE CSTR:RESULT:配属
				CASE 配属先
					;娼館の別部屋に配属してる場合はそちらから引っ張ってくる
					SIF 配属先 == "娼館" && CFLAG:RESULT:娼館部屋番号 != 配属部屋
						GOTO 娼館ELSE
					SELECTCASE 配属先
						CASE "医務室", "図書室", "実験室", "調理室"
							PRINTFORMW %CALLNAME:RESULT%を%配属先%担当から外しました
						CASEELSE
							PRINTFORMW %CALLNAME:RESULT%が帰宅しました
					ENDSELECT
					LOCAL = RESULT
					CALL 配属解除, LOCAL
					LTARGET = TARGET
					TARGET = LOCAL
					;配属解除時の口上
					SIF FLAG:口上表示
						TRYCALLFORM %配属先%_ASSIGNOFF_K{NO:LOCAL}
					TARGET = LTARGET
					;配属先ごとに専用の処理がある場合は呼び出す
					TRYCALLFORM %配属先%_ASSIGNOFF_OPTION, LOCAL
					RESTART
				CASEELSE
					$娼館ELSE
					IF 配属人数 >= 配属上限
						PRINTW これ以上配属できません
						RESTART
					ENDIF
					PRINTFORMW %CALLNAME:RESULT%を配属しました
					LOCAL = RESULT
					CALL 配属解除, LOCAL
					CSTR:LOCAL:配属 = %配属先%
					SIF 配属先 == "娼館"
						CFLAG:LOCAL:娼館部屋番号 = 配属部屋
					LTARGET = TARGET
					TARGET = LOCAL
					;配属時の口上
					SIF FLAG:口上表示
						TRYCALLFORM %配属先%_ASSIGNON_K{NO:LOCAL}
					IF ASSI == TARGET
						ASSI = -1
					ELSEIF LTARGET == TARGET
						TARGET = -1
					ELSE
						TARGET = LTARGET
					ENDIF
					;配属先ごとに専用の処理がある場合は呼び出す
					TRYCALLFORM %配属先%_ASSIGNON_OPTION, LOCAL
					;実績:日向美ビタースイーツ♪のメンバーを全員娼館に配属する
					LOCAL = 0
					IF 配属先 == "娼館"
						REPEAT CHARANUM
							IF CSTR:COUNT:配属 == "娼館"
								SELECTCASE CHARANAMEF(COUNT)
									CASE "山形 まり花", "和泉 一舞", "春日 咲子", "霜月 凛", "芽兎 めう"
										LOCAL++
								ENDSELECT
							ENDIF
						REND
						SIF LOCAL == 5
							CALL 実績解除, "日向美商店街で噂の売春宿"
					ENDIF
					;実績:ユーズ、ター子、レイジを調理室に配属する
					LOCALS = 0
					IF 配属先 == "調理室"
						FOR LCOUNT, 0, CHARANUM
							IF CSTR:LCOUNT:配属 == "調理室"
								SELECTCASE CHARANAMEF(LCOUNT)
									CASE "ユーズ", "ター子", "レイジ"
										LOCAL++
								ENDSELECT
							ENDIF
						NEXT
						SIF LOCAL == 3
							CALL 実績解除, "今夜はたこ焼きパーティ"
					ENDIF
					RESTART
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(999, 1001, 1002)

RESTART

@配属解除, ARG
LOCALS = %CSTR:ARG:配属%
CSTR:ARG:配属 = 
CFLAG:ARG:配属完了 = 0
CFLAG:ARG:ゲーセン店長 = 0
CFLAG:ARG:上級娼婦 = 0
CFLAG:ARG:レッスン = 0
CFLAG:ARG:娼館部屋番号 = 0
TRYCALLFORM %LOCALS%_ASSIGNOFF_OPTION, ARG

