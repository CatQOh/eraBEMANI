@STRATEGY
;IF FLAG:700 == 0
;	GC:3 = 50
;	BR:15 = 1
;	CALL STRATEGY_TUTORIAL
;ENDIF
DRAWLINE
PRINTL [1] - ゲームセンター経営
PRINTL [2] - 娼館経営
PRINTPLAIN [3] - ゲーム開発（工事中）
PRINTL 
PRINTL [4] - 芸能プロダクション運営
DRAWLINE
PRINTPLAIN 　STRATEGY　
PRINT [0] - 戻る
PRINTL 　[100] 各モードの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL STRATEGY_GC
			RETURN 0
		CASE 2
			CALL STRATEGY_BROTHEL
			RETURN 0
		;CASE 3
		;	CALL STRATEGY_DEVELOP
		;	RETURN 0
		CASE 4
			CALL STRATEGY_IDOL
			RETURN 0
		CASE 100
			CALL STRATEGY_TUTORIAL
			RESTART
	ENDSELECT
LOOP 1

RESTART


;経営モードチュートリアル
@STRATEGY_TUTORIAL
DRAWLINE
PRINTL 経営モードへようこそ
PRINTL 経営モードは「あなた」がゲーセンなどの施設を運営して収入を得るモードです
PRINTW 予定【ゲーセン経営】【娼館】【ゲーム開発】【芸能プロダクション】などなど

@STRATEGY_INCOME
SIF FLAG:ゲーセン営業中
	TRYCALL INCOME_GC
SIF FLAG:娼館営業中
	TRYCALL INCOME_BR

RETURN 0


;経営モード用の変数初期化
@CLEAR_STRATEGY_FLAG
VARSET 設置筐体:0:0, 0
VARSET 空きスペース, 0
GC物件 = 0
GC名声 = 0
GC評判 = 0
基本収入 = 0
GCメンテ = 0
GC広さ = 0
GC分煙 = 0
GC階層 = 0
GC外装 = 0
GCフードコート = 0
GCトイレ = 0

娼館レベル = 0
娼館広さ = 0
娼館部屋数 = 0
娼館方針 = 0
娼館総収入 = 0
娼館レベルアップ必要額 = 0
娼館前日の収入 = 0

IDOL知名度 = 0


;経営モードの借金の処理
@DEBT
#DIM DYNAMIC 金額
#DIM DYNAMIC 方向

;所持金がマイナスの場合は借金に繰り入れる
IF MONEY < 0
	借金 -= MONEY
	MONEY = 0
ENDIF
LOCAL = 借金

PRINTL
IF 借金 > 0
	PRINTFORM 現在の借金${LOCAL}に
	;利率はNORMAL:5%, HYPER:7%, ANOTHER:10%
	SELECTCASE FLAG:難易度
		CASE 1
			PRINT 5%の利子
			TIMES 借金, 1.05
		CASE 2
			PRINT 7%の利子
			TIMES 借金, 1.07
		CASE 3
			PRINT 10%の利子
			TIMES 借金, 1.10
	ENDSELECT
	PRINTFORMW (${借金-LOCAL})がつき、合計${借金}になりました。
ELSE
	PRINT 新たに借金をすることができます(10日ごとに利子が
	SELECTCASE FLAG:難易度
		CASE 1
			PRINT 5%
		CASE 2
			PRINT 7%
		CASE 3
			PRINT 10%
	ENDSELECT
	PRINTL つきます)
	PRINTL [0] する
	PRINTL [1] しない
	DO
		INPUT
		SIF RESULT == 1
			RETURN 0
	LOOP LOOPRES(0)
ENDIF

;変数「方向」は、1なら返済で0なら借入
SIF 借金 > 0
	方向 = 1

DO
	DRAWLINE
	PRINTL 返済・借入する金額を入力してください
	PRINTL キーボードで直接入力もできます
	PRINTL
	IF 方向
		PRINTFORML 借金……${借金-金額}
	ELSE
		PRINTFORML 借金……${借金+金額}
	ENDIF
	SELECTCASE 方向
		CASE 1
			PRINTL 　　　　↑
		CASEELSE
			PRINTL 　　　　｜
	ENDSELECT
	PRINTFORM 　　　　｜${金額}
	SELECTCASE 方向
		CASE 1
			PRINTL 返済
		CASEELSE
			PRINTL 借入
	ENDSELECT
	SELECTCASE 方向
		CASE 0
			PRINTL 　　　　↓
		CASEELSE
			PRINTL 　　　　｜
	ENDSELECT
	IF 方向
		PRINTFORML 所持金…${MONEY-金額}
	ELSE
		PRINTFORML 所持金…${MONEY+金額}
	ENDIF
	
	PRINTL
	PRINTBUTTON "[CHANGE] 返済・借入の変更", "CHANGE"
	PRINTL
	IF 方向
		PRINTBUTTON "[ MAX ] 持ち金全てを返済にあてる", "MAX"
		PRINTL
	ELSE
		PRINTL
	ENDIF
	PRINTBUTTON "[+5000000]", "+5000000"
	PRINTBUTTON "[+500000]", "+500000"
	PRINTBUTTON "[+50000]", "+50000"
	PRINTBUTTON "[+5000]", "+5000"
	PRINTL
	PRINTBUTTON "[+2000000]", "+2000000"
	PRINTBUTTON "[+200000]", "+200000"
	PRINTBUTTON "[+20000]", "+20000"
	PRINTBUTTON "[+2000]", "+2000"
	PRINTL
	PRINTBUTTON "[+1000000]", "+1000000"
	PRINTBUTTON "[+100000]", "+100000"
	PRINTBUTTON "[+10000]", "+10000"
	PRINTBUTTON "[+1000]", "+1000"
	PRINTL
	PRINTL [                0               ]
	PRINTBUTTON "[-1000000]", "-1000000"
	PRINTBUTTON "[-100000]", "-100000"
	PRINTBUTTON "[-10000]", "-10000"
	PRINTBUTTON "[-1000]", "-1000"
	PRINTL
	PRINTBUTTON "[-2000000]", "-2000000"
	PRINTBUTTON "[-200000]", "-200000"
	PRINTBUTTON "[-20000]", "-20000"
	PRINTBUTTON "[-2000]", "-2000"
	PRINTL
	PRINTBUTTON "[-5000000]", "-5000000"
	PRINTBUTTON "[-500000]", "-500000"
	PRINTBUTTON "[-50000]", "-50000"
	PRINTBUTTON "[-5000]", "-5000"
	PRINTL
	PRINTBUTTON "[ENTER]", "ENTER"
	
	INPUTS
	SELECTCASE RESULTS
		CASE "", "ENTER", "enter", "Enter", "E", "e"
			BREAK
		CASE "CHANGE", "change", "Change", "C", "c"
			IF 方向
				方向 = 0
			ELSE
				方向 = 1
			ENDIF
		CASE "MAX", "max", "Max", "M", "m"
			SIF 方向
				金額 = MIN(借金, MONEY)
		CASE "0"
			金額 = 0
		CASEELSE
			IF CHARATU(RESULTS, 0) == "+"
				SIF ISNUMERIC(SUBSTRING(RESULTS, 1))
					金額 += TOINT(SUBSTRING(RESULTS, 1))
			ELSEIF CHARATU(RESULTS, 0) == "-"
				SIF ISNUMERIC(SUBSTRING(RESULTS, 1))
					金額 -= TOINT(SUBSTRING(RESULTS, 1))
			ELSEIF ISNUMERIC(RESULTS)
				金額 = TOINT(RESULTS)
			ENDIF
	ENDSELECT
	SIF 金額 < 0
		金額 = 0
	;借りている額より多く返せないし、持っている額より多く返せない
	SIF 方向
		金額 = LIMIT(金額, 0, MIN(借金, MONEY))
	CLEARLINE 21
LOOP 1

IF 方向
	MONEY -= 金額
	借金 -= 金額
ELSE
	MONEY += 金額
	借金 += 金額
ENDIF
