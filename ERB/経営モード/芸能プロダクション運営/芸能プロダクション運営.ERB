@STRATEGY_IDOL
#DIM LCOUNT

SIF IDOL知名度 < 10
	IDOL知名度 = 10

FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:配属 == 4
		IDOL所属人数++
NEXT

DRAWLINE
PRINTL 
PRINTL 【芸能プロダクション運営】
PRINTL 
DRAWLINE
PRINTFORML 知名度:{IDOL知名度}
DRAWLINE

IF IDOL所属人数
	LOCALS = <button value='1' title='レッスンを受けさせる予定を入れます'>　[1] - レッスン</button>
	HTML_PRINT LOCALS
	LOCALS = <button value='2' title='ライブなどのイベントを行います'>　[2] - イベント</button>
	HTML_PRINT LOCALS
ELSE
	LOCALS = <font color='gray'><nonbutton title='キャラを配属してください'>　[-] - レッスン</nonbutton></font>
	HTML_PRINT LOCALS
	LOCALS = <font color='gray'><nonbutton title='キャラを配属してください'>　[-] - イベント</nonbutton></font>
	HTML_PRINT LOCALS
ENDIF
LOCALS = <button value='3' title='現金などを送ることにより知名度を強引に上げます'>　[3] - 贈賄する</button>
HTML_PRINT LOCALS
LOCALS = <button value='4' title='芸能プロダクションで働かせるキャラを配属します'>　[4] - 人員配置</button>
HTML_PRINT LOCALS
LOCALS = <button value='5' title='芸能プロダクション運営に関する設定を変更します'>　[5] - 設定変更</button>
HTML_PRINT LOCALS


DRAWLINE
PRINTPLAIN 　IDOL　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 1, 2
			SIF !IDOL所属人数
				CONTINUE
	ENDSELECT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL IDOL_LESSON
		CASE 2
			CALL IDOL_EVENT
		CASE 3
			CALL IDOL_BRIBE
		CASE 4
			CALL IDOL_PERSONAL
		CASE 5
			CALL IDOL_OPTION
		CASE 100
			CALL IDOL_TUTORIAL
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

RESTART

;----------------------------------------------------
;芸能プロダクション運営モードチュートリアル
;----------------------------------------------------
@IDOL_TUTORIAL
DRAWLINE
PRINTL 当モードは芸能プロダクションを運営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「レッスン」とは？
PRINTL [2] - 「イベント」とは？
PRINTL [3] - 「贈賄する」とは？
PRINTL [4] - 「人員配置」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 「レッスン」はこれから受けるレッスンの予定を入れるコマンドです。
			PRINTL お金を払うことによりボーカル・ダンス・楽器演奏のいずれかのレッスンを受けることができます。
			PRINTL ここで予定を入れなくても全ての経験を少しずつ得ていきますが、
			PRINTL 能力をより強くしたり何かの技能に特化させたり際に必要となるでしょう。
			PRINTW 高い効果のレッスンは相応の知名度がないと受けることができません。
		CASE 2
			PRINTL 「イベント」はライブなどの催し物で利益を上げるコマンドです。
			PRINTW このコマンドを選択したときのみ、主人は半日間拘束されます。
		CASE 3
			PRINTL 「贈賄する」は賄賂を送ることなどの裏工作により知名度を上げるコマンドです。
			PRINTL 知名度が高いほど賄賂に必要な金額は増えていきます。
			PRINTW また、枕営業をさせることでも取引ができますが、その効果は奴隷の働き次第です。
		CASE 4
			PRINTL 「人員配置」は芸能プロダクションに人員を配置するコマンドです。
			PRINTW 配属された人数に応じてお金はかかってしまうので、誰を配属するかはよく考えましょう。
	ENDSELECT
LOOP LOOPRES(1, 2, 3, 4)

RESTART


;レッスン----------------------------------------------------
@IDOL_LESSON
;「CFLAG:レッスン」について
;3で割った余りでレッスンの種類、3の倍数に切り上げたときの値でレッスンのレベルを判別
;Vocal:1, 4, 7, 10, 13
;Inst.:2, 5, 8, 11, 14
;Dance:3, 6, 9, 12, 15
;それぞれ左からLv1～Lv5となる

DRAWLINE
PRINTL □レッスンを受けさせるキャラを選んでください

REPEAT CHARANUM
	IF CFLAG:COUNT:配属 == 4
		IF CFLAG:COUNT:レッスン == 0
			PRINT 　　　　　
		ELSEIF CFLAG:COUNT:レッスン%3 == 1
			PRINTFORM Vocal(Lv{(CFLAG:COUNT:レッスン+2)/3})
		ELSEIF CFLAG:COUNT:レッスン%3 == 2
			PRINTFORM Inst.(Lv{(CFLAG:COUNT:レッスン+1)/3})
		ELSE
			PRINTFORM Dance(Lv{CFLAG:COUNT:レッスン/3})
		ENDIF
		PRINTFORM  [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
		SIF TALENT:COUNT:魅力
			PRINT  魅力
		SIF TALENT:COUNT:魅惑
			PRINT  魅惑
		SIF TALENT:COUNT:謎の魅力
			PRINT  謎の魅力
		SIF TALENT:COUNT:人気
			PRINT  人気
		PRINTL )
	ENDIF
REND

DRAWLINE
PRINTPLAIN 　LESSON　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1 TO CHARANUM-1
			IF CFLAG:RESULT:配属 == 4
				LOCAL = RESULT
				PRINTL
				PRINTL レッスンのレベルと種類を選んでください
				PRINTL
				PRINTL [0]レッスンの予定を入れない
				PRINTL 　　　　　　 Vocal Inst. Dance
				PRINTL Lv1($10000)  [ 1]  [ 2]  [ 3]
				IF IDOL知名度 > 40
					PRINTPLAIN Lv2($30000)  
					PRINTL [ 4]  [ 5]  [ 6]
				ENDIF
				IF IDOL知名度 > 160
					PRINTPLAIN Lv3($60000)  
					PRINTL [ 7]  [ 8]  [ 9]
				ENDIF
				IF IDOL知名度 > 640
					PRINTPLAIN Lv4($100000) 
					PRINTL [10]  [11]  [12]
				ENDIF
				IF IDOL知名度 > 2560
					PRINTPLAIN Lv5($150000) 
					PRINTL [13]  [14]  [15]
				ENDIF
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0 TO 3
							CFLAG:LOCAL:レッスン = RESULT
							RESTART
						CASE 4 TO 6
							IF IDOL知名度 > 40
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 7 TO 9
							IF IDOL知名度 > 160
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 10 TO 12
							IF IDOL知名度 > 640
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 13 TO 15
							IF IDOL知名度 > 2560
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
					ENDSELECT
				LOOP 1
			ENDIF
	ENDSELECT
LOOP 1


;イベント----------------------------------------------------
@IDOL_EVENT
#DIM DYNAMIC 分野
#DIM DYNAMIC 技量, 4
#DIM DYNAMIC 人数, 4
#DIM DYNAMIC LMEMORY
#DIM DYNAMIC LCOUNT

DRAWLINE
PRINTL 何のイベントを開催しますか？
PRINTL [1] ライブ
DRAWLINE
PRINTPLAIN 　EVENT　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			VARSET LOCAL
			CALL MUSIC_LIST
			SIF IDOL選曲 == ""
				RESTART
			$MEMBER_LOOP
			;◆◆◆技量の計算◆◆◆
			;	技量 = 技能LVの合計値 / √(担当人数)
			;D&VやI&Vの場合は、それぞれで1人として数えるが技能LVを0.75倍で計算する
			;技量は10000倍にして計算する(表記上は小数第4位まで計算する)
			VARSET 技量
			VARSET 人数
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 4
					SELECTCASE LOCAL:COUNT
						CASE 1
							技量:3 += 10000*ABL:COUNT:舞踊技能
							人数:3++
						CASE 2
							技量:2 += 10000*ABL:COUNT:演奏技能
							人数:2++
						CASE 3
							技量:1 += 10000*ABL:COUNT:歌唱技能
							人数:1++
						CASE 4
							技量:3 += 7500*ABL:COUNT:舞踊技能
							人数:3++
							技量:1 += 7500*ABL:COUNT:歌唱技能
							人数:1++
						CASE 5
							技量:2 += 7500*ABL:COUNT:演奏技能
							人数:2++
							技量:1 += 7500*ABL:COUNT:歌唱技能
							人数:1++
					ENDSELECT
				ENDIF
			REND
			FOR 分野, 1, 4
				SIF 人数:分野 <= 0
					CONTINUE
				技量:分野 *= 10000
				技量:分野 /= SQRT(人数:分野*10000*10000)
			NEXT
			DRAWLINE
			PRINTL ライブに出すメンバーと担当するパートを選んでください
			PRINTL （ボーカル曲の場合、最低でも1人はボーカルを担当する必要があります）
			PRINTL
			PRINTFORML %IDOL選曲%
			PRINT 楽曲難易度　
			FOR 分野, 1, 4
				SELECTCASE 分野
					CASE 1
						PRINT Vocal:
					CASE 2
						PRINT Inst.:
					CASE 3
						PRINT Dance:
				ENDSELECT
				SELECTCASE TOINT(IDOL選曲:分野)
					CASE 0
						PRINT ‐ 
					CASE IS < 10
						PRINTFORM %TOFULL(IDOL選曲:分野)% 
					CASEELSE
						PRINTFORM %IDOL選曲:分野% 
				ENDSELECT
			NEXT
			PRINTL
			PRINT 現在の技量　
			FOR 分野, 1, 4
				PRINTFORM {技量:分野/10000, 3, RIGHT}.
				FOR LCOUNT, 0, 4-STRLENS(TOSTR(技量:分野%10000))
					PRINT 0
				NEXT
				PRINTFORM {技量:分野%10000} 
			NEXT
			PRINTL
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 4
					SELECTCASE LOCAL:COUNT
						CASE 0
							PRINT 　　
						CASE 1
							PRINT Da. 
						CASE 2
							PRINT In. 
						CASE 3
							PRINT Vo. 
						CASE 4
							PRINT D&V 
						CASE 5
							PRINT I&V 
					ENDSELECT
					PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
					SIF TALENT:COUNT:魅力
						PRINT  魅力
					SIF TALENT:COUNT:魅惑
						PRINT  魅惑
					SIF TALENT:COUNT:謎の魅力
						PRINT  謎の魅力
					SIF TALENT:COUNT:人気
						PRINT  人気
					PRINTL )
				ENDIF
			REND
			DO
				INPUT
				IF CFLAG:RESULT:配属 == 4
					LMEMORY = RESULT
					PRINTL
					PRINTL どのパートを担当しますか？
					PRINTL [0] 担当しない
					PRINTL [1] ダンス
					PRINTL [2] 楽器演奏
					IF IDOL選曲:1 != "0"
						PRINTL [3] ボーカル
						PRINTL [4] ダンス&ボーカル
						PRINTL [5] 楽器演奏&ボーカル
					ENDIF
					$PART_INPUT
					INPUT
					SELECTCASE RESULT
						CASE 0, 1, 2
							LOCAL:LMEMORY = RESULT
						CASE 3, 4, 5
							SIF IDOL選曲:1 == "0"
								GOTO PART_INPUT
							LOCAL:LMEMORY = RESULT
						CASEELSE
							GOTO PART_INPUT
					ENDSELECT
					GOTO MEMBER_LOOP
				ENDIF
			LOOP 1
	ENDSELECT
LOOP 1


;人員配置処理----------------------------------------------------
@IDOL_PERSONAL
#DIM DYNAMIC ページ数
#DIM DYNAMIC 表示数
#DIM DYNAMIC 配属可能人数
#DIM DYNAMIC 飛ばした人数
#DIM DYNAMIC 配属人数
VARSET LOCAL, 0
配属人数 = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:配属 == 4
		配属人数++
REND
DRAWLINE
PRINTFORML □芸能プロダクションに配属する人員を選択してください(現在{配属人数}/10)

配属可能人数 = 0

REPEAT CHARANUM
	;主人は無理
	SIF COUNT == MASTER
		CONTINUE
	;助手可能じゃないと無理
	SIF CFLAG:COUNT:状態 < 2
		CONTINUE
	;体力不足だと無理
	SIF BASE:COUNT:体力 < 500
		CONTINUE
	;妊娠中、育児中は無理
	SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中
		CONTINUE
	配属可能人数++
	;配属可能なキャラに対応したLOCALが1になる
	LOCAL:COUNT = 1
REND

飛ばした人数 = 0
表示数 = 0

IF 配属可能人数
	REPEAT CHARANUM
		SIF !LOCAL:COUNT
			CONTINUE
		;2ページ目以降はページ×20まで表示を飛ばす
		IF 飛ばした人数 < ページ数*20
			飛ばした人数++
			CONTINUE
		ENDIF
		PRINTFORM 　[{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
		SIF TALENT:COUNT:魅力
			PRINT  魅力
		SIF TALENT:COUNT:魅惑
			PRINT  魅惑
		SIF TALENT:COUNT:謎の魅力
			PRINT  謎の魅力
		SIF TALENT:COUNT:人気
			PRINT  人気
		PRINT )

		SELECTCASE CFLAG:COUNT:配属
			CASE 0
				IF COUNT == TARGET
					PRINTL 【調教中】
				ELSEIF COUNT == ASSI
					PRINTL 【助手中】
				ELSE
					PRINTL 【休憩中】
				ENDIF
			CASE 1
				PRINTL 【ゲームセンター配属中】
			CASE 2
				PRINTL 【娼館配属中】
			CASE 4
				PRINTL 【配属中】
		ENDSELECT
		表示数++
		;20人表示したらページ終わり
		SIF 表示数 == 20
			BREAK
	REND
ELSE
	PRINTL 
	PRINTL 【現在配属できる奴隷はいません】
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAINFORM 　PAGE.{ページ数+1}　
SIF ページ数 > 0
	PRINTLC [999]前ページ
PRINTLC [1000] 戻る　
;表示できるキャラがまだ居れば次ページ
SIF 表示数+20*ページ数 < 配属可能人数 
	PRINTLC [1001]次ページ
PRINTL [1002] 全員帰還

DO
	INPUT
	SELECTCASE RESULT
		CASE 999
			SIF ページ数 > 0
				ページ数--
		CASE 1000
			RETURN 0
		CASE 1001
			;表示できるキャラがまだ居れば次ページ
			SIF 表示数+20*ページ数 < 配属可能人数 
				ページ数++
		CASE 1002
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 4
					CFLAG:COUNT:配属完了 = 0
					CFLAG:COUNT:配属 = 0
				ENDIF
			REND
			PRINTW 全員帰宅しました
		CASEELSE
			SIF !LOCAL:RESULT
				CONTINUE
			SELECTCASE CFLAG:RESULT:配属
				CASE 1
					PRINTFORMW %CALLNAME:RESULT%が帰宅しました
					CFLAG:RESULT:配属 = 0
					SIF FLAG:口上表示
						TRYCALLFORM IDOL_ASSIGNOFF_K{NO:RESULT}
					RESTART
				CASEELSE
					IF 配属人数 >= 10
						PRINTW これ以上配属できません
						RESTART
					ENDIF
					PRINTFORMW %CALLNAME:RESULT%を配属しました
					CFLAG:RESULT:配属 = 4
					CFLAG:RESULT:配属完了 = 0
					CFLAG:RESULT:ゲーセン店長 = 0
					SIF FLAG:口上表示
						TRYCALLFORM IDOL_ASSIGNON_K{NO:RESULT}
					SIF TARGET == RESULT
						TARGET = -1
					SIF ASSI == RESULT
						ASSI = -1
					RESTART
			ENDSELECT
	ENDSELECT
LOOP LOOPRES(999, 1001, 1002)

RESTART


;オプション----------------------------------------------------
@IDOL_OPTION
PRINTL
PRINTL ターンごとの収支の報告をしますか？
PRINTL [0] する(メッセージWAITあり)
PRINTL [1] する(メッセージWAITなし)
PRINTL [2] しない
DO
	INPUT
	IF INRANGE(RESULT, 0, 2)
		IDOL報告 = IDOL報告/4*4 + RESULT
		PRINTW 設定を変更しました
		RETURN 0
	ENDIF
LOOP 1


;経験の増加・収支の計算など----------------------------------------------------
@TURNEND_IDOL
#DIM DYNAMIC レッスンレベル

VARSET LOCAL

REPEAT CHARANUM
	IF CFLAG:COUNT:配属 == 4
		;1人ごとにかかる基礎費用
		LOCAL:1 += 1000
		;レッスンの内容ごとに場合分け
		;0未満は枕営業中
		IF CFLAG:COUNT:レッスン < 0
			CALL IDOL_MAKURA, COUNT
		;0の場合はレッスンなしで、歌唱・演奏・ダンスの経験がそれぞれ1つずつ増える
		ELSEIF CFLAG:COUNT:レッスン == 0
			EXP:COUNT:歌唱経験++
			EXP:COUNT:演奏経験++
			EXP:COUNT:ダンス経験++
		;1以上の場合は何らかのレッスンを受けており、5*Lvの経験が入る
		ELSE
			;3で割って1余るとき、ボーカルのレッスン
			IF CFLAG:COUNT:レッスン%3 == 1
				レッスンレベル = (CFLAG:COUNT:レッスン+2)/3
				EXP:COUNT:歌唱経験 += 5*レッスンレベル
			;3で割って2余るとき、楽器演奏のレッスン
			ELSEIF CFLAG:COUNT:レッスン%3 == 2
				レッスンレベル = (CFLAG:COUNT:レッスン+1)/3
				EXP:COUNT:演奏経験 += 5*レッスンレベル
			;3で割り切れるとき、ダンスのレッスン
			ELSE
				レッスンレベル = CFLAG:COUNT:レッスン/3
				EXP:COUNT:ダンス経験 += 5*レッスンレベル
			ENDIF
			;レベルNのレッスン費用は
			;	(1+2+3+…+N)*10000 ―①
			;で求まり
			;	 1+2+3+…+N = N(N+1)/2
			;であるから、①は
			;	5000N(N+1)
			;と変形できる
			LOCAL:2 += 5000*レッスンレベル*(レッスンレベル+1)
		ENDIF
	ENDIF
REND

LOCAL = LOCAL:1 + LOCAL:2
MONEY -= LOCAL
IF LOCAL && IDOL報告%4 < 2
	PRINTFORM 事務所の維持費用に${LOCAL:1}
	IF LOCAL:2
		PRINTFORM とレッスン費用に${LOCAL:2}かかったので、合計${LOCAL}を支払いました。
	ELSE
		PRINT かかりました。
	ENDIF
	SIF IDOL報告%4 == 0
		WAIT
ENDIF


;枕営業の処理
@IDOL_MAKURA, ARG

