@STRATEGY_IDOL
#DIM LCOUNT

SIF IDOL知名度 < 0
	IDOL知名度 = 0

FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:配属 == 4
		IDOL所属人数++
NEXT

DRAWLINE
PRINTL 
PRINTL 【芸能プロダクション運営】
PRINTL 
DRAWLINE
PRINTFORML 知名度:{IDOL知名度}
DRAWLINE

IF IDOL所属人数
	LOCALS = <button value='1' title='レッスンを受けさせる予定を入れます'>　[1] - レッスン</button>
	HTML_PRINT LOCALS
	LOCALS = <button value='2' title='ライブなどのイベントを行います'>　[2] - イベント</button>
	HTML_PRINT LOCALS
ELSE
	LOCALS = <font color='gray'><nonbutton title='キャラを配属してください'>　[-] - レッスン</nonbutton></font>
	HTML_PRINT LOCALS
	LOCALS = <font color='gray'><nonbutton title='キャラを配属してください'>　[-] - イベント</nonbutton></font>
	HTML_PRINT LOCALS
ENDIF
LOCALS = <button value='3' title='現金などを送ることにより知名度を強引に上げます'>　[3] - 贈賄する</button>
HTML_PRINT LOCALS
LOCALS = <button value='4' title='芸能プロダクションで働かせるキャラを配属します'>　[4] - 人員配置</button>
HTML_PRINT LOCALS
LOCALS = <button value='5' title='芸能プロダクション運営に関する設定を変更します'>　[5] - 設定変更</button>
HTML_PRINT LOCALS


DRAWLINE
PRINTPLAIN 　IDOL　
PRINT [0] - 戻る
PRINTL 　[100] 各コマンドの説明

DO
	INPUT
	SELECTCASE RESULT
		CASE 1, 2
			SIF !IDOL所属人数
				CONTINUE
	ENDSELECT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			CALL IDOL_LESSON
		CASE 2
			CALL IDOL_EVENT
			SIF RESULT
				BREAK
		CASE 3
			CALL IDOL_BRIBE
		CASE 4
			CALL STRATEGY_PERSONAL, "芸能プロダクション"
		CASE 5
			CALL IDOL_OPTION
		CASE 100
			CALL IDOL_TUTORIAL
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1

;上記のループをBREAKで脱出するには「イベント」をRETURN 1で終了させる必要がある
STRATEGY_TURNEND = 1
BEGIN TURNEND

;----------------------------------------------------
;芸能プロダクション運営モードチュートリアル
;----------------------------------------------------
@IDOL_TUTORIAL
DRAWLINE
PRINTL 当モードは芸能プロダクションを運営して資金を稼ぐコマンドです。
PRINTL 
PRINTL [1] - 「レッスン」とは？
PRINTL [2] - 「イベント」とは？
PRINTL [3] - 「贈賄する」とは？
PRINTL [4] - 「人員配置」とは？
DRAWLINE
PRINTPLAIN 　TUTORIAL　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL 「レッスン」はこれから受けるレッスンの予定を入れるコマンドです。
			PRINTL お金を払うことによりボーカル・ダンス・楽器演奏のいずれかのレッスンを受けることができます。
			PRINTL ここで予定を入れなくても全ての経験を少しずつ得ていきますが、
			PRINTL 能力をより強くしたり何かの技能に特化させたり際に必要となるでしょう。
			PRINTW 高い効果のレッスンは相応の知名度がないと受けることができません。
		CASE 2
			PRINTL 「イベント」はライブなどの催し物で利益を上げるコマンドです。
			PRINTW このコマンドを選択したときのみ、主人は半日間拘束されます。
		CASE 3
			PRINTL 「贈賄する」は賄賂を送ることなどの裏工作により知名度を上げるコマンドです。
			PRINTL 知名度が高いほど賄賂に必要な金額は増えていきます。
			PRINTW また、枕営業をさせることでも取引ができますが、その効果は奴隷の働き次第です。
		CASE 4
			PRINTL 「人員配置」は芸能プロダクションに人員を配置するコマンドです。
			PRINTW 配属された人数に応じてお金はかかってしまうので、誰を配属するかはよく考えましょう。
	ENDSELECT
LOOP LOOPRES(1, 2, 3, 4)

RESTART


;レッスン----------------------------------------------------
@IDOL_LESSON
;「CFLAG:レッスン」について
;3で割った余りでレッスンの種類、3の倍数に切り上げたときの値でレッスンのレベルを判別
;Vocal:1, 4, 7, 10, 13
;Inst.:2, 5, 8, 11, 14
;Dance:3, 6, 9, 12, 15
;それぞれ左からLv1～Lv5となる

DRAWLINE
PRINTL □レッスンを受けさせるキャラを選んでください

REPEAT CHARANUM
	;体力不足だと無理
	SIF BASE:COUNT:体力 < 500
		CONTINUE
	IF CFLAG:COUNT:配属 == 4
		IF CFLAG:COUNT:レッスン == 0
			PRINT 　　　　　
		ELSEIF CFLAG:COUNT:レッスン < 0
			PRINT 枕営業予定
		ELSEIF CFLAG:COUNT:レッスン%3 == 1
			PRINTFORM Vocal(Lv{(CFLAG:COUNT:レッスン+2)/3})
		ELSEIF CFLAG:COUNT:レッスン%3 == 2
			PRINTFORM Inst.(Lv{(CFLAG:COUNT:レッスン+1)/3})
		ELSE
			PRINTFORM Dance(Lv{CFLAG:COUNT:レッスン/3})
		ENDIF
		PRINTFORM  [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
		SIF TALENT:COUNT:魅力
			PRINT  魅力
		SIF TALENT:COUNT:魅惑
			PRINT  魅惑
		SIF TALENT:COUNT:謎の魅力
			PRINT  謎の魅力
		SIF TALENT:COUNT:人気
			PRINT  人気
		PRINTL )
	ENDIF
REND

DRAWLINE
PRINTPLAIN 　LESSON　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1 TO CHARANUM-1
			IF BASE:RESULT:体力 >= 500 && CFLAG:RESULT:配属 == 4
				LOCAL = RESULT
				PRINTL
				PRINTL レッスンのレベルと種類を選んでください
				PRINTL
				PRINTL [0]レッスンの予定を入れない
				PRINTL 　　　　　　 Vocal Inst. Dance
				PRINTL Lv1($10000)  [ 1]  [ 2]  [ 3]
				IF IDOL知名度 > 40
					PRINTPLAIN Lv2($30000)  
					PRINTL [ 4]  [ 5]  [ 6]
				ENDIF
				IF IDOL知名度 > 160
					PRINTPLAIN Lv3($60000)  
					PRINTL [ 7]  [ 8]  [ 9]
				ENDIF
				IF IDOL知名度 > 640
					PRINTPLAIN Lv4($100000) 
					PRINTL [10]  [11]  [12]
				ENDIF
				IF IDOL知名度 > 2560
					PRINTPLAIN Lv5($150000) 
					PRINTL [13]  [14]  [15]
				ENDIF
				DO
					INPUT
					SELECTCASE RESULT
						CASE 0 TO 3
							CFLAG:LOCAL:レッスン = RESULT
							RESTART
						CASE 4 TO 6
							IF IDOL知名度 > 40
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 7 TO 9
							IF IDOL知名度 > 160
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 10 TO 12
							IF IDOL知名度 > 640
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
						CASE 13 TO 15
							IF IDOL知名度 > 2560
								CFLAG:LOCAL:レッスン = RESULT
								RESTART
							ENDIF
					ENDSELECT
				LOOP 1
			ENDIF
	ENDSELECT
LOOP 1


;イベント----------------------------------------------------
@IDOL_EVENT
#DIM DYNAMIC 分野
#DIM DYNAMIC 技量, 4
#DIM DYNAMIC 難度, 4
#DIM DYNAMIC 難度合計
#DIM DYNAMIC 人数, 4
#DIM DYNAMIC LMEMORY
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 収入, 2

DRAWLINE
PRINTL 何のイベントを開催しますか？
PRINTL [1] ライブ
DRAWLINE
PRINTPLAIN 　EVENT　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			VARSET LOCAL
			$MUSIC_SELECT
			CALL MUSIC_LIST
			SIF IDOL選曲 == ""
				RESTART
			IF IDOL選曲:1 == "0"
				REPEAT CHARANUM
					SIF LOCAL:COUNT >= 3
						LOCAL:COUNT -= 3
				REND
			ENDIF
			$MEMBER_LOOP
			;◆◆◆技量の計算◆◆◆
			;	技量 = 技能LVの合計値 / √(担当人数)
			;D&VやI&Vの場合は、それぞれで1人として数えるが技能LVを0.75倍で計算する
			;技量は10000倍にして計算する(表記上は小数第4位まで計算する)
			VARSET 技量
			VARSET 人数
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 4
					SELECTCASE LOCAL:COUNT
						CASE 1
							技量:3 += 10000*ABL:COUNT:舞踊技能
							人数:3++
						CASE 2
							技量:2 += 10000*ABL:COUNT:演奏技能
							人数:2++
						CASE 3
							技量:1 += 10000*ABL:COUNT:歌唱技能
							人数:1++
						CASE 4
							技量:3 += 7500*ABL:COUNT:舞踊技能
							人数:3++
							技量:1 += 7500*ABL:COUNT:歌唱技能
							人数:1++
						CASE 5
							技量:2 += 7500*ABL:COUNT:演奏技能
							人数:2++
							技量:1 += 7500*ABL:COUNT:歌唱技能
							人数:1++
					ENDSELECT
				ENDIF
			REND
			FOR 分野, 1, 4
				SIF 人数:分野 <= 0
					CONTINUE
				技量:分野 *= 10000
				技量:分野 /= SQRT(人数:分野*10000*10000)
			NEXT
			DRAWLINE
			PRINTL ライブに出すメンバーと担当するパートを選んでください
			PRINTL （ボーカル曲の場合、最低でも1人はボーカルを担当する必要があります）
			PRINTL
			PRINTFORML %IDOL選曲%
			PRINT 楽曲難易度　
			FOR 分野, 1, 4
				SELECTCASE 分野
					CASE 1
						PRINT Vocal:
					CASE 2
						PRINT Inst.:
					CASE 3
						PRINT Dance:
				ENDSELECT
				SELECTCASE TOINT(IDOL選曲:分野)
					CASE 0
						PRINT ‐ 
					CASE IS < 10
						PRINTFORM %TOFULL(IDOL選曲:分野)% 
					CASEELSE
						PRINTFORM %IDOL選曲:分野% 
				ENDSELECT
			NEXT
			PRINTL
			PRINT 現在の技量　
			FOR 分野, 1, 4
				PRINTFORM {技量:分野/10000, 3, RIGHT}.
				FOR LCOUNT, 0, 4-STRLENS(TOSTR(技量:分野%10000))
					PRINT 0
				NEXT
				PRINTFORM {技量:分野%10000} 
			NEXT
			PRINTL
			REPEAT CHARANUM
				;体力不足だと無理
				SIF BASE:COUNT:体力 < 500
					CONTINUE
				IF CFLAG:COUNT:配属 == 4
					SELECTCASE LOCAL:COUNT
						CASE 0
							PRINT 　　
						CASE 1
							PRINT Da. 
						CASE 2
							PRINT In. 
						CASE 3
							PRINT Vo. 
						CASE 4
							PRINT D&V 
						CASE 5
							PRINT I&V 
					ENDSELECT
					PRINTFORM [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(歌唱技能:{ABL:COUNT:歌唱技能, 2, LEFT} 演奏技能:{ABL:COUNT:演奏技能, 2, LEFT} 舞踊技能:{ABL:COUNT:舞踊技能, 2, LEFT}
					SIF TALENT:COUNT:魅力
						PRINT  魅力
					SIF TALENT:COUNT:魅惑
						PRINT  魅惑
					SIF TALENT:COUNT:謎の魅力
						PRINT  謎の魅力
					SIF TALENT:COUNT:人気
						PRINT  人気
					PRINTL )
				ENDIF
			REND
			PRINTL 　　 [ 0] - ライブを始める
			PRINTL 　　 [-1] - 選曲画面に戻る
			PRINTL 　　 [-2] - 戻る
			DO
				INPUT
				SIF RESULT == -2
					RESTART
				SIF RESULT == -1
					GOTO MUSIC_SELECT
				IF RESULT == 0
					IF 人数:1 == 0  && 人数:2 == 0 && 人数:3 == 0
						PRINTW メンバーを1人以上選び出してください
						GOTO MEMBER_LOOP
					ENDIF
					IF IDOL選曲:1 != "0" && 人数:1 == 0
						PRINTW ボーカルを1人以上選び出してください
						GOTO MEMBER_LOOP
					ENDIF
					BREAK
				ENDIF
				IF BASE:RESULT:体力 >= 500 && CFLAG:RESULT:配属 == 4
					LMEMORY = RESULT
					PRINTL
					PRINTL どのパートを担当しますか？
					PRINTL [0] 担当しない
					PRINTL [1] ダンス
					PRINTL [2] 楽器演奏
					IF IDOL選曲:1 != "0"
						PRINTL [3] ボーカル
						PRINTL [4] ダンス&ボーカル
						PRINTL [5] 楽器演奏&ボーカル
					ENDIF
					$PART_INPUT
					INPUT
					SELECTCASE RESULT
						CASE 0, 1, 2
							LOCAL:LMEMORY = RESULT
						CASE 3, 4, 5
							SIF IDOL選曲:1 == "0"
								GOTO PART_INPUT
							LOCAL:LMEMORY = RESULT
						CASEELSE
							GOTO PART_INPUT
					ENDSELECT
					GOTO MEMBER_LOOP
				ENDIF
			LOOP 1
			;上記のループを超えるとライブ開始
			難度:1 = 10000*TOINT(IDOL選曲:1)
			難度:2 = 10000*TOINT(IDOL選曲:2)
			難度:3 = 10000*TOINT(IDOL選曲:3)
			難度合計 = (難度:1 + 難度:2 + 難度:3) / 10000
			;魅惑で2%、魅力で3%、謎の魅力で4%、人気で5%技量が上がる(乗算で重複する)
			;ビジュアルによって難度が下がる(※かわいいは正義)ことを表現したいのだが
			;難度を下げると収入も下がってしまうので相対的に技量を上げる
			REPEAT CHARANUM
				IF CFLAG:COUNT:配属 == 4 && LOCAL:COUNT
					CFLAG:COUNT:レッスン = 0
					BASE:COUNT:体力 -= 50*(難度合計)
					IF BASE:COUNT:体力 < 1
						BASE:COUNT:体力 = 1
						CFLAG:COUNT:回復量低下 += 2
					ENDIF
					IF TALENT:COUNT:魅惑
						TIMES 技量:1, 1.02
						TIMES 技量:2, 1.02
						TIMES 技量:3, 1.02
					ENDIF
					IF TALENT:COUNT:魅力
						TIMES 技量:1, 1.03
						TIMES 技量:2, 1.03
						TIMES 技量:3, 1.03
					ENDIF
					IF TALENT:COUNT:謎の魅力
						TIMES 技量:1, 1.04
						TIMES 技量:2, 1.04
						TIMES 技量:3, 1.04
					ENDIF
					IF TALENT:COUNT:人気
						TIMES 技量:1, 1.05
						TIMES 技量:2, 1.05
						TIMES 技量:3, 1.05
					ENDIF
				ENDIF
			REND
			収入 = 0
			;それぞれの分野についての基本収入を出す
			;収入は難度Xに対して技量が同じだとX*10000となり、
			;不足分1あたり12500ずつ減り、超過分1あたり8000ずつ増える
			FOR 分野, 1, 4
				収入 += 難度:分野
				IF 技量:分野 >= 難度:分野
					収入 += (技量:分野 - 難度:分野)*4/5
				ELSE
					収入 += (技量:分野 - 難度:分野)*5/4
				ENDIF
			NEXT
			収入:1 = 収入
			;最後に知名度による補正
			収入:1 *= (1000+IDOL知名度)
			収入:1 /= 1000
			MONEY += 収入:1
			;最終的な収益の計算の後に知名度の増減を行う
			IDOL知名度 += 収入/1000
			PRINTL
			PRINTL ライブが終了しました
			IF 収入:1 >= 0
				PRINTFORM ${収入:1}の利益を出し、
			ELSE
				PRINTFORM ${ABS(収入:1)}の損失を出し、
			ENDIF
			PRINT 知名度が
			IF 収入 >= 0
				PRINTFORMW {収入/1000}増えました
			ELSE
				PRINTFORMW {ABS(収入/1000)}減りました
			ENDIF
			SIF IDOL知名度 < 0
				IDOL知名度 = 0
			;返り値が1であることでTURNENDできる
			RETURN 1
	ENDSELECT
LOOP 1


;賄賂・枕営業----------------------------------------------------
@IDOL_BRIBE
DRAWLINE
PRINTL 何をしますか？
PRINTL [1] 賄賂を送る
PRINTL [2] 枕営業をさせる
DRAWLINE
PRINTPLAIN 　BRIBE　
PRINTL [0] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			;知名度が50上昇したあと1%上昇する
			;贈賄に必要な金額は、知名度をX(ただしXが40未満のとき40にする)と置くと
			;1000*X^1.5
			LOCAL = IDOL知名度
			LOCAL:1 = IDOL知名度
			SIF LOCAL < 40
				LOCAL = 40
			LOCAL *= SQRT(LOCAL*1000*1000)
			;1000の倍数に四捨五入
			LOCAL += 500
			LOCAL /= 1000
			LOCAL *= 1000
			PRINTFORML 賄賂による裏工作をするには${LOCAL}必要です
			IF MONEY < LOCAL
				PRINTW お金が足りません
				RESTART
			ENDIF
			PRINTFORML 支払いますか？(所持金:${MONEY})
			PRINTL [0] はい
			PRINTL [1] いいえ
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						MONEY -= LOCAL
						IDOL知名度 += 50
						TIMES IDOL知名度, 1.01
						PRINTFORMW ${LOCAL}を支払い知名度を{IDOL知名度-LOCAL:1}上昇させました
				ENDSELECT
			LOOP LOOPRES(0, 1)
			RESTART
		CASE 2
			;枕営業とレッスンの切り替えについて
			;CFLAG:レッスンの変数を次のように変換する
			;0と-1、1と-2、2と-3、3と-4……
			;負の数ならば枕営業となる
			;	Y = -X-1
			;のような式になるうえに、この逆関数は
			;	X = -Y-1
			;となるので枕営業の設定・解除のどちらの場合も
			;「-1倍して1引く」という演算で済む
			$MAKURA_LOOP
			DRAWLINE
			PRINTL □枕営業をさせるキャラを選んでください

			REPEAT CHARANUM
				;体力不足だと無理
				SIF BASE:COUNT:体力 < 500
					CONTINUE
				IF CFLAG:COUNT:配属 == 4
					IF CFLAG:COUNT:レッスン == 0
						PRINT 　　　　　
					ELSEIF CFLAG:COUNT:レッスン < 0
						PRINT 枕営業予定
					ELSEIF CFLAG:COUNT:レッスン%3 == 1
						PRINTFORM Vocal(Lv{(CFLAG:COUNT:レッスン+2)/3})
					ELSEIF CFLAG:COUNT:レッスン%3 == 2
						PRINTFORM Inst.(Lv{(CFLAG:COUNT:レッスン+1)/3})
					ELSE
						PRINTFORM Dance(Lv{CFLAG:COUNT:レッスン/3})
					ENDIF
					PRINTFORML  [{COUNT, 2, RIGHT}] - %NAME:COUNT, 26, LEFT%(従順:{ABL:COUNT:従順, 2, LEFT} 技巧:{ABL:COUNT:技巧, 2, LEFT} 奉仕精神:{ABL:COUNT:奉仕精神, 2, LEFT})
				ENDIF
			REND
			DRAWLINE
			PRINTPLAIN 　MAKURA　
			PRINTL [0] 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						RESTART
					CASE 1 TO CHARANUM-1
						IF BASE:RESULT:体力 >= 500 && CFLAG:RESULT:配属 == 4
							CFLAG:RESULT:レッスン *= -1
							CFLAG:RESULT:レッスン--
						ENDIF
						GOTO MAKURA_LOOP
				ENDSELECT
			LOOP 1
	ENDSELECT
LOOP 1

;オプション----------------------------------------------------
;「変数IDOL報告」は4進数で管理されている
;収支の報告……0,1,2 (3は未使用)
;枕営業の報告…0,4,8,12
@IDOL_OPTION
DRAWLINE
PRINTL 設定する項目を選んでください
PRINTL [1] ターンごとの収支報告
PRINTL [2] 枕営業の報告
DRAWLINE
PRINTPLAIN 　IDOL　
PRINT [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			PRINTL
			PRINTL ターンごとの収支の報告をしますか？
			PRINTL [0] する(メッセージWAITあり)
			PRINTL [1] する(メッセージWAITなし)
			PRINTL [2] しない
			DO
				INPUT
				IF INRANGE(RESULT, 0, 2)
					IDOL報告 = IDOL報告/4*4 + RESULT
					PRINTW 設定を変更しました
					RESTART
				ENDIF
			LOOP 1
		CASE 2
			PRINTL
			PRINTL 枕営業の報告をしますか？
			PRINTL [0] プレイ内容まで全て報告する
			PRINTL [1] 経験・知名度の上昇値のみ報告
			PRINTL [2] 知名度の上昇値のみ報告
			PRINTL [3] 一切しない
			DO
				INPUT
				IF INRANGE(RESULT, 0, 3)
					IDOL報告 = IDOL報告%4 + RESULT*4
					PRINTW 設定を変更しました
					RESTART
				ENDIF
			LOOP 1
	ENDSELECT
LOOP 1


;経験の増加・収支の計算など----------------------------------------------------
@TURNEND_IDOL
#DIM DYNAMIC レッスンレベル

VARSET LOCAL

REPEAT CHARANUM
	IF CFLAG:COUNT:配属 == 4
		;1人ごとにかかる基礎費用
		LOCAL:1 += 2000
		;レッスンの内容ごとに場合分け
		;0未満は枕営業中
		IF CFLAG:COUNT:レッスン < 0
			CALL IDOL_MAKURA, COUNT
		;0の場合はレッスンなしで、歌唱・演奏・ダンスの経験がどれか1つ増える
		;増えるものはローテーション
		ELSEIF CFLAG:COUNT:レッスン == 0
			SELECTCASE (2*DAY+TIME)%3
				CASE 2
					EXP:COUNT:歌唱経験++
				CASE 0
					EXP:COUNT:演奏経験++
				CASE 1
					EXP:COUNT:ダンス経験++
			ENDSELECT
		;1以上の場合は何らかのレッスンを受けており、5*Lvの経験が入る
		ELSE
			;3で割って1余るとき、ボーカルのレッスン
			IF CFLAG:COUNT:レッスン%3 == 1
				レッスンレベル = (CFLAG:COUNT:レッスン+2)/3
				EXP:COUNT:歌唱経験 += 5*レッスンレベル
			;3で割って2余るとき、楽器演奏のレッスン
			ELSEIF CFLAG:COUNT:レッスン%3 == 2
				レッスンレベル = (CFLAG:COUNT:レッスン+1)/3
				EXP:COUNT:演奏経験 += 5*レッスンレベル
			;3で割り切れるとき、ダンスのレッスン
			ELSE
				レッスンレベル = CFLAG:COUNT:レッスン/3
				EXP:COUNT:ダンス経験 += 5*レッスンレベル
			ENDIF
			;レベルNのレッスン費用は
			;	(1+2+3+…+N)*10000 ―①
			;で求まり
			;	 1+2+3+…+N = N(N+1)/2
			;であるから、①は
			;	5000N(N+1)
			;と変形できる
			LOCAL:2 += 5000*レッスンレベル*(レッスンレベル+1)
		ENDIF
	ENDIF
REND

LOCAL = LOCAL:1 + LOCAL:2
MONEY -= LOCAL
IF LOCAL && IDOL報告%4 < 2
	PRINTFORM 事務所の維持費用に${LOCAL:1}
	IF LOCAL:2
		PRINTFORML とレッスン費用に${LOCAL:2}かかったので、合計${LOCAL}を支払いました。
	ELSE
		PRINTL かかりました。
	ENDIF
	SIF IDOL報告%4 == 0
		WAIT
ENDIF


;枕営業の処理
;ARGはキャラ登録番号
@IDOL_MAKURA, ARG
#DIM DYNAMIC 満足度
#DIM DYNAMIC ヴァギナ
#DIM DYNAMIC 中出し
#DIM DYNAMIC アナル
#DIM DYNAMIC アナル中出し
#DIM DYNAMIC フェラ
#DIM DYNAMIC 奉仕
#DIM DYNAMIC 奉仕で射精
#DIM DYNAMIC 絶頂
#DIM DYNAMIC 射精

;賄賂と違い、満足度に比例して増えるだけなので
;相対的に知名度が低いときの方が効果が高い
;基本的に言われたことを何でもするような従順な子の方が満足してもらえる

;能力
満足度 = 3*ABL:ARG:従順 + ABL:ARG:技巧 + 3*ABL:ARG:奉仕精神

;陥落素質
SIF TALENT:ARG:淫乱
	満足度 += 6
SIF TALENT:ARG:娼婦
	満足度 += 9
SIF TALENT:ARG:服従
	満足度 += 20
SIF TALENT:ARG:隷属
	満足度 += 30

;性格
SIF TALENT:ARG:生意気
	満足度 -= 10
SIF TALENT:ARG:臆病
	満足度 += 10
SIF TALENT:ARG:素直
	満足度 += 15
SIF TALENT:ARG:反抗的
	満足度 -= 15
SIF TALENT:ARG:プライド高い
	満足度 -= 10
SIF TALENT:ARG:プライド低い
	満足度 += 10
SIF TALENT:ARG:ツンデレ
	満足度 -= 5

;性への関心
SIF TALENT:ARG:自制心
	満足度 -= 7
SIF TALENT:ARG:保守的
	満足度 -= 10
SIF TALENT:ARG:一線越えない
	満足度 -= 10
SIF TALENT:ARG:目立ちたがり
	満足度 += 2

;乙女心
SIF TALENT:ARG:貞操観念
	満足度 -= 20
SIF TALENT:ARG:貞操無頓着
	満足度 += 5
SIF TALENT:ARG:恥じらい
	満足度 -= 2

;体質
SIF TALENT:ARG:濡れにくい
	満足度 -= 1

;技術
SIF TALENT:ARG:舌使い
	満足度 += 5
SIF TALENT:ARG:献身的
	満足度 += 15
SIF TALENT:ARG:潔癖症
	満足度 -= 8

;正直度
SIF TALENT:ARG:自慰狂い || TALENT:ARG:セックス狂 || TALENT:ARG:尻穴狂い || TALENT:ARG:淫乳
	満足度 += 4

;先天的身体特徴
SIF TALENT:ARG:小柄体型 || TALENT:ARG:小人体型
	満足度 += 13
SELECTCASE TALENT:ARG:バストサイズ
	CASE 1
		満足度 += 1
	CASE 2
		満足度 += 2
	CASE 3
		満足度 += 3
	CASE 4
		満足度 += 5
	CASE 5
		満足度 += 8
ENDSELECT

;後天的身体特徴
SIF TALENT:ARG:母乳体質
	満足度 += 3
SIF TALENT:ARG:絶倫
	満足度 += 5

;満足度1を保証
SIF 満足度 < 1
	満足度 = 1

;特別ボーナス
SIF TALENT:ARG:処女
	TIMES 満足度, 1.80
SIF TALENT:ARG:謎の魅力
	TIMES 満足度, 1.10
SIF TALENT:ARG:魅力
	TIMES 満足度, 1.05

;回数のロール
IF VAGINA(ARG)
	ヴァギナ = 5+RAND:3
	中出し = RAND:(1+ヴァギナ)
ENDIF
アナル = 1+RAND:7
アナル中出し = RAND:(1+アナル)

絶頂 = RAND:(1+ヴァギナ+アナル)/3
射精 = RAND:(1+ヴァギナ+アナル)/3

フェラ += 1+RAND:4
SELECTCASE TALENT:ARG:バストサイズ
	CASE 1
		奉仕 += 1
	CASE 2
		奉仕 += 1
	CASE 3
		奉仕 += 2
	CASE 4
		奉仕 += 3
	CASE 5
		奉仕 += 5
ENDSELECT
奉仕 += RAND:( 1+LIMIT(ABL:ARG:奉仕精神, 0, 10) )
奉仕で射精 = (フェラ+奉仕)/3

CFLAG:ARG:レッスン = 0
BASE:ARG:体力 -= 40*(ヴァギナ+アナル+絶頂+射精)
BASE:ARG:体力 -= 20*(フェラ+奉仕)
IF BASE:ARG:体力 < 1
	BASE:ARG:体力 = 1
	CFLAG:ARG:回復量低下 += 2
ENDIF

IF IDOL報告/4 == 0
	PRINTL 今日は営業先の重役を相手した。
	PRINT フェラ
	SIF 奉仕
		PRINT や奉仕
	PRINT でペニスを刺激させ
	SIF 奉仕で射精
		PRINT て射精させ
	PRINT た後、
	SIF VAGINA(ARG)
		PRINTFORM 膣を{ヴァギナ}回
	PRINTFORM アナルを{アナル}回犯され
	SIF 中出し || アナル中出し
		PRINTL 、
	SIF 中出し
		PRINTFORM 膣に{中出し}回
	SIF アナル中出し
		PRINTFORM アナルに{アナル中出し}回
	SIF 中出し || アナル中出し
		PRINT 中出しされ
	PRINTW た……
ENDIF

IF VAGINA(ARG)
	SIF IDOL報告/4 < 2
		PRINTFORML Ｖ経験+{ヴァギナ}
	EXP:ARG:Ｖ経験 += ヴァギナ
	IF TALENT:ARG:処女
		TALENT:ARG:処女 = 0
		IF TALENT:ARG:性転換済
			VIRGIN:ARG:11 = 営業先の重役
		ELSE
			VIRGIN:ARG:1 = 営業先の重役
		ENDIF
	ENDIF
ENDIF

IF IDOL報告/4 < 2
	PRINTFORML Ａ経験+{アナル}
	SIF 絶頂
		PRINTFORML 絶頂経験+{絶頂}
	SIF 射精
		PRINTFORML 射精経験+{射精}
	SIF 中出し
		PRINTFORML 膣射経験+{中出し}
	PRINTFORML フェラ経験+{フェラ}
	SIF 中出し+アナル中出し+奉仕で射精
		PRINTFORML 精液経験+{中出し+アナル中出し+奉仕で射精}
ENDIF

EXP:ARG:Ａ経験 += アナル
EXP:ARG:膣射経験 += 中出し
EXP:ARG:フェラ経験 += フェラ
EXP:ARG:絶頂経験 += 絶頂
EXP:ARG:射精経験 += 射精
IF TALENT:ARG:性別 == 1
	SIF IDOL報告/4 < 2
		PRINTFORML ゲイ経験+{2*(ヴァギナ+アナル+フェラ+奉仕)}
	EXP:ARG:ゲイ経験 += 2*(ヴァギナ+アナル+フェラ+奉仕)
ENDIF
WAIT

SIF IDOL報告 < 3
	PRINTFORMW %CALLNAME:ARG%のおかげで知名度が{満足度}上がった
IDOL知名度 += 満足度
