@飲食店経営
#DIM DYNAMIC LCOUNT

CALL 飲食店ステータス
PRINTL 
PRINTL 　[1] - 物件の管理
PRINTL 　[2] - 経営方針の管理
PRINTL 　[3] - 人員の管理
PRINTFORML 　[4] - \@ FLAG:飲食店営業中 == -1 ? 営業を再開する # 営業を休止する \@
PRINTL 　[5] - 定休日の設定
DRAWLINE
PRINTPLAIN 　飲食店・ホテル　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			CALL 物件管理_飲食店
		CASE 2
			CALL 経営方針_飲食店
		CASE 3
			CALL 人員管理_飲食店
		CASE 4
			IF FLAG:飲食店営業中 > 0
				PRINTFORMW %飲食店名%の経営を一時中断しました
				;定休日じゃない休業は-1
				FLAG:飲食店営業中 = -1
			ELSE
				PRINTFORMW %飲食店名%の経営を開始しました
				FLAG:飲食店営業中 = DAY
			ENDIF
		CASE 5
			VARLINE = 0
			DO
				SIF VARLINE
					CLEARLINE LINECOUNT-VARLINE
				VARLINE = LINECOUNT
				LOCALS = 
				DRAWLINE
				PRINTL □定休日を選んでください
				PRINTFORML 　[1] - 日曜日（\@ INN定休日("日") ? 定休 # 営業 \@）
				PRINTFORML 　[2] - 月曜日（\@ INN定休日("月") ? 定休 # 営業 \@）
				PRINTFORML 　[3] - 火曜日（\@ INN定休日("火") ? 定休 # 営業 \@）
				PRINTFORML 　[4] - 水曜日（\@ INN定休日("水") ? 定休 # 営業 \@）
				PRINTFORML 　[5] - 木曜日（\@ INN定休日("木") ? 定休 # 営業 \@）
				PRINTFORML 　[6] - 金曜日（\@ INN定休日("金") ? 定休 # 営業 \@）
				PRINTFORML 　[0] - 土曜日（\@ INN定休日("土") ? 定休 # 営業 \@）
				PRINTL 　[9] - 設定終了
				INPUT
				LOCAL = RESULT
				SELECTCASE LOCAL
					CASE 0 TO 6
						FOR LCOUNT, 0, 7
							IF LOCAL == LCOUNT
								IF !INN定休日(DOW(LOCAL))
									飲食店定休日 += @"・%DOW(LOCAL)%"
								ELSE
									CONTINUE
								ENDIF
							ENDIF
							IF INN定休日(DOW(LCOUNT))
								IF LOCALS == ""
									LOCALS = %DOW(LCOUNT)%
								ELSE
									LOCALS += @"・%DOW(LCOUNT)%"
								ENDIF
							ENDIF
						NEXT
						飲食店定休日 = %LOCALS%
					CASE 9
						BREAK
				ENDSELECT
			LOOP 1
		CASEELSE
			CONTINUE
	ENDSELECT
	RESTART
LOOP 1


@物件管理_飲食店

PRINTL [1] - 温泉を探す
PRINTL [2] - 宿泊施設を増築する

DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			CALL 温泉探索
			RETURN
	ENDSELECT
LOOP 1

@経営方針_飲食店_

PRINTL [1] - 業種の変更
PRINTFORML [2] - 料理のジャンル(現在のジャンル:\@ 飲食ジャンル == "" ? 無し # %飲食ジャンル% \@)
PRINTL [2] - 

DO
	SELECTCASE RESULT
		CASE 1
			;CALL 業種変更_飲食店
		CASE 2
			PRINTL □ジャンルを選んでください
			PRINTL 　[1] - 和食
			PRINTL 　[2] - 洋食
			PRINTL 　[3] - 中華
			PRINTL 　[4] - スイーツ
			PRINTL 　[5] - ファストフード
			PRINTL 　[6] - お酒
			PRINTL 　[0] - 無し
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						飲食ジャンル = 
					CASE 1
						飲食ジャンル = 和食
					CASE 2
						飲食ジャンル = 洋食
					CASE 3
						飲食ジャンル = 中華
					CASE 4
						飲食ジャンル = スイーツ
					CASE 5
						飲食ジャンル = ファストフード
					CASE 6
						飲食ジャンル = お酒
					CASEELSE
						CONTINUE
				ENDSELECT
			LOOP 0
	ENDSELECT
	RESTART
LOOP 1


@経営方針_飲食店
#DIM DYNAMIC 条件可否, 20
{
#DIMS DYNAMIC 業種名 = 
"飲食店",
"ビジネスホテル",
"リゾートホテル",
"飲食店(裏)",
"リゾートホテル(裏)",
"ラブホテル",
"日帰り温泉",
"バー",
"ピンクサロン",
"ライブハウス",
"ホストクラブ",
"キャバクラ"
}
#DIM DYNAMIC LCOUNT

IF 温泉レベル == 0
	SETCOLORBYNAME gray
	PRINTL [-] - 温泉の併設(温泉設備が無いと併設できません)
	RESETCOLOR
ELSE
	PRINTL [1] - 温泉の併設
ENDIF
IF ホテル部屋数 == 0
	SETCOLORBYNAME gray
	PRINTL [-] - 宿の併設(宿泊施設の増築を行わないと併設できません)
	RESETCOLOR
ELSE
	PRINTL [2] - 宿の併設
ENDIF
IF CMATCH(CSTR:配属, "娼館") == 0
	SETCOLORBYNAME gray
	PRINTL [-] - 娼館との連携(娼館に配属してるキャラが居ないと連携できません)
	RESETCOLOR
ELSE
	PRINTL [3] - 娼館との連携
ENDIF
IF CMATCH(CSTR:配属, "芸能プロダクション") == 0
	SETCOLORBYNAME gray
	PRINTL [-] - 芸能プロダクションとの連携(芸プロの配属してるキャラが居ないと連携できません)
	RESETCOLOR
ELSE
	PRINTL [4] - 芸能プロダクションとの連携
ENDIF
SELECTCASE 業種
	CASE "ラブホテル", "日帰り温泉"
		SETCOLORBYNAME gray
		PRINTL [-] - 料理のジャンル(提供無し)
		RESETCOLOR
	CASE "バー", "ピンクサロン", "ライブハウス", "ホストクラブ", "キャバクラ"
		PRINTL [-] - 料理のジャンル(お酒で固定)
	CASEELSE
		PRINTFORML [5] - 料理のジャンル(現在:%飲食ジャンル%)
ENDSELECT


PRINTL 上記の条件から業種を選択
PRINT 業種候補:

LCOUNT = 0
VARSET 条件可否

FOR LCOUNT, 0, VARSIZE("業種名")
	SELECTCASE 業種名:LCOUNT
		;無条件解放
		CASE "飲食店", "バー", "ホストクラブ", "キャバクラ"
			条件可否:LCOUNT = 1
		;宿泊施設あり
		CASE "ビジネスホテル", "ラブホテル"
			SIF ホテル部屋数
				条件可否:LCOUNT = 1
		;宿泊施設+温泉
		CASE "リゾートホテル"
			SIF ホテル部屋数 && 温泉レベル
				条件可否:LCOUNT = 1
		;娼館との連携必須
		CASE "飲食店(裏)", "ピンクサロン"
			SIF 娼館連携
				条件可否:LCOUNT = 1
		;リゾートホテル+娼館連携
		CASE "リゾートホテル(裏)"
			SIF 娼館連携 && ホテル部屋数 && 温泉レベル
				条件可否:LCOUNT = 1
		;温泉あり
		CASE "日帰り温泉"
			SIF 温泉レベル
				条件可否:LCOUNT = 1
		;芸プロとの連携
		CASE "ライブハウス"
			SIF 芸プロ連携
				条件可否:LCOUNT = 1
	ENDSELECT
	SIF !条件可否
		SETCOLORBYNAME gray
	PRINTFORM [{LCOUNT+20}]%業種名:LCOUNT% 
	RESETCOLOR
NEXT

DO
	INPUT
	SELECTCASE RESULT
		CASE 5
			PRINTL □ジャンルを選んでください
			PRINTL 　[1] - 和食
			PRINTL 　[2] - 洋食
			PRINTL 　[3] - 中華
			PRINTL 　[4] - スイーツ
			PRINTL 　[5] - ファストフード
			PRINTL 　[6] - お酒
			PRINTL 　[0] - 無し
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						飲食ジャンル = 
					CASE 1
						飲食ジャンル = 和食
					CASE 2
						飲食ジャンル = 洋食
					CASE 3
						飲食ジャンル = 中華
					CASE 4
						飲食ジャンル = スイーツ
					CASE 5
						飲食ジャンル = ファストフード
					CASE 6
						飲食ジャンル = お酒
					CASEELSE
						CONTINUE
				ENDSELECT
			LOOP 0
		CASE 20 TO 40
			SIF 業種名:(RESULT-20) == ""
				CONTINUE
			SIF !条件可否:(RESULT-20)
				CONTINUE
			業種 = %業種名:(RESULT-20)%
			;デフォルト名のままなら業種に合わせて店名変更
			SIF MATCH(業種名, 飲食店名) || 飲食店名 == ""
				飲食店名 = %業種%
			;酒がメインのサービスなら酒で固定
			SELECTCASE 業種
				CASE "バー", "ピンクサロン", "ライブハウス", "ホストクラブ", "キャバクラ"
					飲食ジャンル = お酒
			ENDSELECT
	ENDSELECT
	RESTART
LOOP 1


@人員管理_飲食店

DRAWLINE

;連携する業種ではそちらから追加で配属できる
SELECTCASE 業種
	CASE "飲食店(裏)", "リゾートホテル(裏)", "ピンクサロン"
		SIF CMATCH(CSTR:配属, "娼館") == 0
			GOTO ELSE
		PRINTL □人員を配属します
		PRINTFORML 　[1] - 娼館からの配属(現在:{CMATCH(CSTR:転属, "飲食店")}人/3人)
	CASE "ライブハウス"
		SIF CMATCH(CSTR:配属, "芸能プロダクション") == 0
			GOTO ELSE
		PRINTL □人員を配属します
		PRINTFORML 　[1] - 芸プロからの配属(現在:{CMATCH(CSTR:転属, "飲食店")}人/3人)
	CASEELSE
		$ELSE
		CALL STRATEGY_PERSONAL, "飲食店"
		RETURN
ENDSELECT
PRINTFORML 　[2] - 通常の配属(現在:{CMATCH(CSTR:配属, "飲食店")}人/{飲食店配属上限()}人)
PRINTL 　[0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
			SELECTCASE 業種
				CASE "飲食店(裏)", "リゾートホテル(裏)", "ピンクサロン"
					CALL 転属_飲食店, "娼館"
				CASE "ライブハウス"
					CALL 転属_飲食店, "芸プロ"
			ENDSELECT
			RESTART
		CASE 2
			CALL STRATEGY_PERSONAL
			RESTART
	ENDSELECT
LOOP 1

@転属_飲食店, ARGS
#DIM DYNAMIC LCOUNT

DRAWLINE
PRINTFORML □%飲食店名%に転属するキャラを選択してください(本業の配属は解除されません)

FOR LCOUNT, 1, CHARANUM
	SELECTCASE ARGS
		CASE "娼館"
			SIF CSTR:LCOUNT:配属 != "娼館"
				CONTINUE
			PRINTFORML 　[{LCOUNT, 2}] - %NAME:LCOUNT%\@ CSTR:LCOUNT:転属 == "飲食店" ? (転属中) # \@
		CASE "芸プロ"
			SIF CSTR:LCOUNT:配属 != "芸能プロダクション"
				CONTINUE
			PRINTFORML 　[{LCOUNT, 2}] - %NAME:LCOUNT%\@ CSTR:LCOUNT:転属 == "飲食店" ? (転属中) # \@
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAIN 　転属　
PRINTL [0] - 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1 TO CHARANUM-1
			SELECTCASE ARGS
				CASE "娼館"
					SIF CSTR:RESULT:配属 != "娼館"
						CONTINUE
				CASE "芸プロ"
					SIF CSTR:RESULT:配属 != "芸能プロダクション"
						CONTINUE
			ENDSELECT
			IF CSTR:RESULT:転属 == "飲食店"
				CSTR:RESULT:転属 = 
			ELSE
				CSTR:RESULT:転属 = 飲食店
			ENDIF
			RESTART
	ENDSELECT
LOOP 1

@飲食店配属上限
#FUNCTION

;業種によって配属上限人数が違う
SELECTCASE 業種
	CASE "ラブホテル", "日帰り温泉"
		RETURNF 3
	CASE "リゾートホテル", "リゾートホテル(裏)"
		RETURNF 10
	;それ以外は全部5人
	CASEELSE
		RETURNF 5
ENDSELECT

@飲食店ステータス

PRINTFORM □%飲食店名%のステータス
IF FLAG:飲食店営業中
	IF INN定休日()
		PRINTL (本日定休日)
	ELSE
		PRINTL (営業中)
	ENDIF
ELSE
	PRINTL (休業中)
ENDIF
PRINTFORML 　飲食業の名声:{飲食店名声} ホテル業の名声:{ホテル名声}
PRINTFORM 　現在の業種:%業種%
SELECTCASE 業種
	CASE "飲食店"
		PRINTL (食事の提供)
	CASE "ビジネスホテル"
		PRINTL (宿泊施設と軽食の提供)
	CASE "リゾートホテル"
		PRINTL (温泉と宿泊施設及び食事の提供)
	CASE "飲食店(裏)"
		PRINTL (食事とその他のサービスの提供)
	CASE "リゾートホテル(裏)"
		PRINTL (宿泊施設と併設された多数の娯楽及び食事の提供)
	CASE "ラブホテル"
		PRINTL (宿泊施設のみの提供)
	CASE "日帰り温泉"
		PRINTL (入浴を目的とした施設を提供)
	CASE "バー"
		PRINTL (アルコール類と軽食の提供)
	CASE "ピンクサロン"
		PRINTL (アルコール類とその他サービスの提供)
	CASE "ライブハウス"
		PRINTL (アルコール類と軽食、魂を揺さぶるメロディの提供)
	CASE "ホストクラブ", "キャバクラ"
		PRINTL (アルコール類と軽食、至福のひとときを提供)
ENDSELECT
PRINTFORML 　定休日:\@ 飲食店定休日 == "" ? 無し # %飲食店定休日% \@
PRINTL □設備等
IF 温泉レベル == 0
	PRINT 　[温泉無し]
ELSE
	PRINTFORM 　[温泉レベル:{温泉レベル}]
ENDIF
IF ホテル部屋数 == 0
	PRINT [宿泊施設無し]
ELSE
	PRINTFORM [宿泊施設:{ホテル部屋数}室]
ENDIF 
IF 娼館連携
	PRINT [娼館と連携中]
ELSE
	PRINT [娼館との連携無し]
ENDIF
IF 芸プロ連携
	PRINT [芸プロと連携中]
ELSE
	PRINT [芸プロとの連携無し]
ENDIF
PRINTL 

;引数で指定した曜日が定休日かどうか 省略すれば今日の曜日
;書式 INN定休日("木") みたいな
@INN定休日, ARGS
#FUNCTION
VARSET LOCALS

SIF ARGS == ""
	ARGS = %DOW(FLAG:曜日)%

SPLIT 飲食店定休日, "・", LOCALS

IF FINDELEMENT(LOCALS, ARGS) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF







