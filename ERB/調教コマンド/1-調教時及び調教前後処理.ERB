;調教前の処理
;2.04でEVENTTRAINもイベント関数から解放
@EVENTTRAIN
CALL EVENTUSERTRAIN

@EVENTUSERTRAIN
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC コマンドフィルタ変数
#DIM DYNAMIC 最大値
#DIM DYNAMIC アイテム種
#DIM DYNAMIC アイテム数
#DIM DYNAMIC 気温ボーナス

;BEGIN TRAINで行われる変数初期化を行う 上級売春やカジノから飛んでくるときの対策
CALL CVAR_RESET

;トイレ使用中(学園モード)ならオンにする
SIF トイレ使用中
	TEQUIP:トイレ = 1

;初期着衣設定に応じて、服の設定をする
FOR LCOUNT, 100, 107
	SIF CSTR:LCOUNT != ""
		TEQUIP:(LCOUNT-100) = 1
	SIF CSTR:MASTER:LCOUNT != ""
		TEQUIP:MASTER:(LCOUNT-100) = 1
	IF ASSI > 0
		SIF CSTR:ASSI:LCOUNT != ""
			TEQUIP:ASSI:(LCOUNT-100) = 1
	ENDIF
NEXT

SELECTCASE FLAG:着衣設定
	;下着無し
	CASE 1
		TEQUIP:上半身下着 = 0
		TEQUIP:下半身下着 = 0
	;下着姿
	CASE 2
		TEQUIP:上半身上着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	;全裸
	CASE 3
		VARSET TEQUIP:MASTER:0, 0, 0, 7
		VARSET TEQUIP:TARGET:0, 0, 0, 7
		SIF ASSI > 0
			VARSET TEQUIP:ASSI:0, 0, 0, 7
ENDSELECT

;調教対象・助手が特定のキャラの場合、調教開始時に豹変することがある
;紅刃……1/5の確率で豹変
;冷音……天気が雨・雪・雷雨で豹変
IF TARGET >= 0
	IF CHARANAMEF(TARGET) == "紅刃" && RAND:5 == 0
		CALL TRANCE_ON, TARGET
		TRANCE:200 = 1
		CALL CHECKDRAWLINE
		PRINTFORMW ＜紅刃は性格が豹変した＞
	ELSEIF CHARANAMEF(TARGET) == "青雨 冷音" && INRANGE(FLAG:天気, 3, 5)
		CALL TRANCE_ON, TARGET
		TRANCE:200 = 1
		CALL CHECKDRAWLINE
		PRINTFORMW ＜冷音は性格が豹変した＞
	ENDIF
ENDIF
IF ASSI >= 0
	IF CHARANAMEF(ASSI) == "紅刃" && RAND:5 == 0
		CALL TRANCE_ON, ASSI
		TRANCE:ASSI:200 = 1
		CALL CHECKDRAWLINE
		PRINTFORMW ＜紅刃は性格が豹変した＞
	ELSEIF CHARANAMEF(ASSI) == "青雨 冷音" && INRANGE(FLAG:天気, 3, 5)
		CALL TRANCE_ON, ASSI
		TRANCE:ASSI:200 = 1
		CALL CHECKDRAWLINE
		PRINTFORMW ＜冷音は性格が豹変した＞
	ENDIF
ENDIF

;条件を満たしていればユリア天使化
SIF GROUPMATCH("鬼蝮 ユリア", CHARANAMEF(TARGET), CHARANAMEF(ASSI)) && GROUPMATCH("イングリット・オーリック・コーネル", CHARANAMEF(TARGET), CHARANAMEF(ASSI))
	CALL ユリア天使化

;実行コマンド、直前の実行コマンドフラグを初期化
PREVCOMS = 
SELECTCOMS = 

;二日酔い
IF CFLAG:二日酔い
	PRINTFORMW %CALLNAME:TARGET%は二日酔いを起こしたそうだ……
	BASE:体力 /= 2
	BASE:気力 /= 2
ENDIF

;服従系陥落なら主人と助手との相性が良くなったりする
IF TALENT:服従
	SIF 相性値(TARGET, MASTER) < 120
		CALL 相性加算, TARGET, MASTER, 120-相性値(TARGET, MASTER)
	SIF ASSI > 0 && 相性値(TARGET, ASSI) < 120
		CALL 相性加算, TARGET, ASSI, 120-相性値(TARGET, ASSI)
ELSEIF TALENT:隷属
	SIF 相性値(TARGET, MASTER) < 150
		CALL 相性加算, TARGET, MASTER, 150-相性値(TARGET, MASTER)
	SIF ASSI > 0 && 相性値(TARGET, ASSI) < 150
		CALL 相性加算, TARGET, ASSI, 150-相性値(TARGET, ASSI)
ENDIF

;助手もまた然り
IF ASSI > 0
	IF TALENT:ASSI:服従
		SIF 相性値(ASSI, MASTER) < 120
			CALL 相性加算, ASSI, MASTER, 120-相性値(ASSI, MASTER)
		SIF ASSI > 0 && 相性値(ASSI, TARGET) < 120
			CALL 相性加算, ASSI, TARGET, 120-相性値(ASSI, TARGET)
	ELSEIF TALENT:ASSI:隷属
		SIF 相性値(ASSI, MASTER) < 150
			CALL 相性加算, ASSI, MASTER, 150-相性値(ASSI, MASTER)
		SIF ASSI > 0 && 相性値(ASSI, TARGET) < 150
			CALL 相性加算, ASSI, TARGET, 150-相性値(ASSI, TARGET)
	ENDIF
ENDIF

;MAXBASEの設定
最大値 = 10000
;絶倫で半減
SIF TALENT:MASTER:絶倫
	最大値 /= 2
;小柄体型で2倍
SIF TALENT:MASTER:小柄体型
	最大値 *= 2
;女性で2倍
SIF SEX(MASTER) == 2
	最大値 *= 2
;Ｃ鈍感なら1.5倍
SIF TALENT:MASTER:Ｃ鈍感
	TIMES 最大値, 1.50
;ミッドナイトスペシャル使用時は半減
SIF CFLAG:MASTER:ミッドナイトスペシャル
	最大値 /= 2
MAXBASE:MASTER:射精 = 最大値

;以下対象と助手にも同様の処理
最大値 = 10000
SIF TALENT:絶倫
	最大値 /= 2
SIF TALENT:小柄体型
	最大値 *= 2
SIF SEX(TARGET) == 2
	最大値 *= 2
SIF TALENT:Ｃ鈍感
	TIMES 最大値, 1.50
SIF CFLAG:ミッドナイトスペシャル
	最大値 /= 2
MAXBASE:射精 = 最大値

IF ASSI >= 0
	最大値 = 10000
	SIF TALENT:ASSI:絶倫
		最大値 /= 2
	SIF TALENT:ASSI:小柄体型
		最大値 *= 2
	SIF SEX(ASSI) == 2
		最大値 *= 2
	SIF TALENT:ASSI:Ｃ鈍感
		TIMES 最大値, 1.50
	SIF CFLAG:ASSI:ミッドナイトスペシャル
		最大値 /= 2
	MAXBASE:ASSI:射精 = 最大値
ENDIF

IF TALENT:母乳体質
	最大値 = 10000
	SIF TALENT:淫乳
		最大値 /= 2
	SIF TALENT:Ｂ鈍感
		最大値 *= 2
	MAXBASE:母乳 = 最大値
ENDIF

MAXBASE:MASTER:触手射精 = 10000
MAXBASE:MASTER:ドラゴン射精 = 10000
SIF ASSI > 0
	MAXBASE:ASSI:ドラゴン射精 = 10000

;主人と調教対象と助手の射精を0に
BASE:MASTER:射精 = 0
BASE:射精 = 0
BASE:MASTER:ドラゴン射精 = 0
IF ASSI >= 0
	BASE:ASSI:射精 = 0
	BASE:ASSI:ドラゴン射精 = 0
ENDIF
;母乳も0に
BASE:母乳 = 0
;触手の射精も0に
BASE:MASTER:触手射精 = 0

;射精限界数の確保
CFLAG:残数 = 10
SIF TALENT:絶倫
	CFLAG:残数 += 5
SIF !PENIS(TARGET) && !TALENT:去勢済み && !TALENT:スライム && !TALENT:溶ける快
	CFLAG:残数 = 0

CFLAG:MASTER:残数 = 10
SIF TALENT:MASTER:絶倫
	CFLAG:MASTER:残数 += 5
SIF エナジードライブ
	CFLAG:MASTER:残数 += エナジードライブ:1*3
SIF !PENIS(MASTER) && !TALENT:MASTER:去勢済み && !TALENT:MASTER:スライム && !TALENT:溶ける快
	CFLAG:MASTER:残数 = 0

IF ASSI >= 0
	CFLAG:ASSI:残数 = 10
	SIF TALENT:ASSI:絶倫
		CFLAG:ASSI:残数 += 5
	SIF !PENIS(ASSI) && !TALENT:ASSI:去勢済み && !TALENT:ASSI:スライム && !TALENT:溶ける快
		CFLAG:ASSI:残数 = 0
ENDIF

CFLAG:射精飾り値 = 0
CFLAG:MASTER:射精飾り値 = 0
SIF ASSI >= 0
	CFLAG:ASSI:射精飾り値 = 0

;賢者タイムをリセット
CFLAG:MASTER:賢者タイム = 0
CFLAG:賢者タイム = 0
SIF ASSI >= 0
	CFLAG:ASSI:賢者タイム = 0

;TFLAGをリセット
VARSET TFLAG
ハッキング命令 = 

;淫乱なら膣内にヴァギナ汚れを付加
SIF TALENT:淫乱 && VAGINA(TARGET)
	SETBIT STAIN:膣内, 0
SIF ASSI >= 0 && TALENT:ASSI:淫乱 && VAGINA(ASSI)
	SETBIT STAIN:ASSI:膣内, 0
;淫乱ならペニスに先走り付加
SIF TALENT:淫乱 && PENIS(TARGET)
	SETBIT STAIN:ペニス, 1
SIF ASSI >= 0 && TALENT:ASSI:淫乱 && PENIS(ASSI)
	SETBIT STAIN:ASSI:ペニス, 1
;主人は無条件で先走りと愛液付加
SIF VAGINA(MASTER)
	SETBIT STAIN:MASTER:ヴァギナ, 0
SIF PENIS(MASTER)
	SETBIT STAIN:MASTER:ペニス, 1

;媚薬の効果をリセット
CFLAG:MASTER:媚薬 = 0
CFLAG:媚薬 = 0
SIF ASSI > 0
	CFLAG:ASSI:媚薬 = 0

;コンドーム射精フラグをリセット
TFLAG:使用済み避妊具 = 0

;調教者はMASTERにしとく
PLAYER = MASTER

;避妊具設定に応じて避妊具を付ける
IF ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 0) && PENIS(MASTER) && !Ｐ使用不可(MASTER)
	TEQUIP:MASTER:コンドーム = 1
	ITEM:コンドーム--
ENDIF
IF ASSI > 0 && ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 1) && PENIS(ASSI) && !Ｐ使用不可(ASSI)
	TEQUIP:ASSI:コンドーム = 1
	ITEM:コンドーム--
ENDIF
IF ITEM:コンドーム && GETBIT(FLAG:避妊具設定, 2) && PENIS(TARGET) && !Ｐ使用不可(TARGET)
	TEQUIP:コンドーム = 1
	ITEM:コンドーム--
ENDIF
IF ITEM:搾精コンドーム && GETBIT(FLAG:避妊具設定, 3) && PENIS(TARGET) && !Ｐ使用不可(TARGET)
	TEQUIP:搾精コンドーム = 1
	ITEM:搾精コンドーム--
ENDIF

;ビデオボーナスのリセット
VARSET VIDEOBONUS

;写真関連のリセット
VARSET 写真内容
VARSET 写真価格

;ドラゴンの名前が無い場合、名前を「ドラゴン」に
SIF TALENT:MASTER:ドラゴン使い && CSTR:MASTER:ドラゴン == ""
	CSTR:MASTER:ドラゴン = ドラゴン
SIF ASSI >= 0 && TALENT:ASSI:ドラゴン使い && CSTR:ASSI:ドラゴン == ""
	CSTR:ASSI:ドラゴン = ドラゴン

;種族宇宙人の感度変化
IF CSTR:種族 == "宇宙人"
	IF VAGINA(TARGET)
		SELECTCASE RAND:3
			CASE 0
				TALENT:Ｖ敏感 = 0
				TALENT:Ｖ鈍感 = 1
			CASE 1
				TALENT:Ｖ敏感 = 0
				TALENT:Ｖ鈍感 = 0
			CASE 2
				TALENT:Ｖ敏感 = 1
				TALENT:Ｖ鈍感 = 0
		ENDSELECT
	ENDIF
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ｃ敏感 = 0
			TALENT:Ｃ鈍感 = 1
		CASE 1
			TALENT:Ｃ敏感 = 0
			TALENT:Ｃ鈍感 = 0
		CASE 2
			TALENT:Ｃ敏感 = 1
			TALENT:Ｃ鈍感 = 0
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ａ敏感 = 0
			TALENT:Ａ鈍感 = 1
		CASE 1
			TALENT:Ａ敏感 = 0
			TALENT:Ａ鈍感 = 0
		CASE 2
			TALENT:Ａ敏感 = 1
			TALENT:Ａ鈍感 = 0
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ｂ敏感 = 0
			TALENT:Ｂ鈍感 = 1
		CASE 1
			TALENT:Ｂ敏感 = 0
			TALENT:Ｂ鈍感 = 0
		CASE 2
			TALENT:Ｂ敏感 = 1
			TALENT:Ｂ鈍感 = 0
	ENDSELECT
ENDIF

;種族妖精ならランダムで消費アイテム取得
IF CSTR:種族 == "妖精"
	アイテム種 = RAND(100, 110)
	アイテム数 = RAND(1, 4)
	PRINTFORML %CALLNAME:TARGET%の部屋にあった{アイテム数}個の%ITEMNAME:アイテム種%を回収しました
	ITEM:アイテム種 += アイテム数
ENDIF

;鳳凰ならボーナス
IF CSTR:種族 == "鳳凰" && FLAG:気温 > 0
	気温ボーナス = FLAG:気温
	SELECTCASE FLAG:天気
		CASE 2
			TIMES 気温ボーナス , 0.50
		CASE 3 TO 7
			TIMES 気温ボーナス , 0.20
	ENDSELECT
	;気温と天気で計算したボーナスを一部の初期PALAMに付与する
	FOR LCOUNT, 0, VARSIZE("PALAM")
		SELECTCASE PALAMNAME:LCOUNT
			CASE "快Ａ", "快Ｂ", "潤滑", "恭順", "欲情", "屈服", "習得"
				PALAM:LCOUNT += 気温ボーナス
			CASE "快Ｃ"
				SELECTCASE SEX(TARGET)
					CASE 1
						SIF PENIS(TARGET)
							PALAM:LCOUNT += 気温ボーナス
					CASE 2
						PALAM:LCOUNT += 気温ボーナス
				ENDSELECT
			CASE "快Ｖ"
				SIF VAGINA(TARGET)
					PALAM:LCOUNT += 気温ボーナス
		ENDSELECT
	NEXT
ENDIF

;特質
IF 特質("研ぎ澄まされた感覚")
	PALAM:快Ｃ += 特質("研ぎ澄まされた感覚")*100
	PALAM:快Ｖ += 特質("研ぎ澄まされた感覚")*100
	;処女の場合は99まで
	SIF 処女膜(TARGET) && PALAM:快Ｖ >= 100
		PALAM:快Ｖ = 99
	PALAM:快Ａ += 特質("研ぎ澄まされた感覚")*100
	PALAM:快Ｂ += 特質("研ぎ澄まされた感覚")*100
ENDIF

;ミッドナイトスペシャル使用時は欲情を3000にする
IF CFLAG:ミッドナイトスペシャル
	SIF PALAM:欲情 < 3000
		PALAM:欲情 = 3000
ENDIF

;開始前の愛情経験を入れておく
TFLAG:愛情経験計算用 = EXP:愛情経験

;開始前の各種経験を入れておく
嗜虐経験計算用 = EXP:嗜虐快楽経験
主人ＡＶ経験計算用 = EXP:MASTER:Ａ経験+EXP:MASTER:Ｖ経験
ＡＶ経験計算用 = EXP:Ａ経験+EXP:Ｖ経験

;妊娠確率補正を1/1にする
CVARSET TEQUIP, GETNUM(TEQUIP, "妊娠確率分母"), 1
CVARSET TEQUIP, GETNUM(TEQUIP, "妊娠確率分子"), 1

;対象が霊体、ゾンビならネクロ経験の増加
IF TALENT:霊体 || TALENT:ゾンビ
	CFLAG:MASTER:ネクロ経験++
	SIF ASSI > 0
		CFLAG:ASSI:ネクロ経験++
ENDIF

;オナホパフェフラグリセット
オナホパフェ = 0

;ワガママ姫段位
IF 受験段位名 == "ワガママ姫たちの接待調教プログラム"
	;年月日のシード値を用いて乱数を制御するため、同じ日付ならリロードしても同じ結果になる
	;制御するのはコマンドフィルタだけなのでDUMPRANDで保存しておく
	DUMPRAND
	RANDOMIZE (YEAR*100000)+(MONTH*1000)+(DAYS*10)+TIME
	;コマンドフィルタ変数に1000～10000の乱数を入れる
	コマンドフィルタ変数 = RAND(1000, 10000)
	;乱数の状態を戻す
	INITRAND

	;2で割ったときの剰余の有無でフィルタをかける ビット数みたいな感じ
	FLAG:愛撫コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:道具コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:奉仕コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:獣姦コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:Ｖ系コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:Ａ系コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
	FLAG:暴力コマンドフィルタ = コマンドフィルタ変数%2
	コマンドフィルタ変数 /= 2
ENDIF

;実績:アイテムを何も持ってない状態で調教を始める
SIF SUMARRAY(ITEM) == 0
	CALL 実績解除, "そんな装備で大丈夫か？"

;実績:ロッテとラッテを対面させる
IF ASSI > 0
	SIF (CHARANAMEF(TARGET) == "ロッテ" && CHARANAMEF(ASSI) == "ラッテ") || (CHARANAMEF(TARGET) == "ラッテ" && CHARANAMEF(ASSI) == "ロッテ")
		CALL 実績解除, "パラドックス"
ENDIF

;実績;パステルくん主人で奴隷と好相性の助手を使用する
SIF CHARANAMEF(MASTER) == "パステルくん" && ASSI > 0 && 相性値(TARGET, ASSI) > 100
	CALL 実績解除, "「わ～いらくち～ん」"

CALL KOJO_START

;コマンド終了時に呼ばれる ここでは調教終了処理と媚薬処理
@EVENTUSERCOMEND
;SKIPDISP中は無視
SIF ISSKIP()
	RETURN
IF !TEQUIP:媚薬 && CFLAG:媚薬 >= 10
	SELECTCASE CSTR:MASTER:種族
		CASE "獣人", "獣"
			PRINTFORML %CALLNAME:MASTER%のフェロモンで%CALLNAME:TARGET%は媚薬を飲まされたかのように発情してしまった……
		CASEELSE
			PRINTFORML %CALLNAME:ASSI%のフェロモンで%CALLNAME:TARGET%は媚薬を飲まされたかのように発情してしまった……
	ENDSELECT
	TEQUIP:媚薬 = 1
ENDIF
IF !TEQUIP:MASTER:媚薬 && CFLAG:MASTER:媚薬 >= 10
	SELECTCASE CSTR:種族
		CASE "獣人", "獣"
			PRINTFORML %CALLNAME:TARGET%のフェロモンで%CALLNAME:MASTER%は媚薬を飲まされたかのように発情してしまった……
		CASEELSE
			PRINTFORML %CALLNAME:ASSI%のフェロモンで%CALLNAME:MASTER%は媚薬を飲まされたかのように発情してしまった……
	ENDSELECT
	TEQUIP:MASTER:媚薬 = 1
ENDIF
IF ASSI > 0
	IF !TEQUIP:ASSI:媚薬 && CFLAG:ASSI:媚薬 >= 10
		SELECTCASE CSTR:種族
			CASE "獣人", "獣"
				PRINTFORML %CALLNAME:TARGET%のフェロモンで%CALLNAME:ASSI%は媚薬を飲まされたかのように発情してしまった……
			CASEELSE
				PRINTFORML %CALLNAME:MASTER%のフェロモンで%CALLNAME:ASSI%は媚薬を飲まされたかのように発情してしまった……
		ENDSELECT
	ENDIF
	TEQUIP:ASSI:媚薬 = 1
ENDIF

CALL TUTORIAL, "調教終了無効"

IF 上級売春中
	IF BASE:MASTER:体力 <= 500
		PRINTFORML （%CALLNAME:MASTER%の体力が限界のため、売春を終了します）
		;お客様満足度一定以上
		IF 上級売春中 == 2
			上級売春中 = 4
		ELSE
			上級売春中 = 3
		ENDIF
	ENDIF
ELSE
	;裸体盛りでの終了
	IF TFLAG:裸体盛り
		TFLAG:調教終了 = 1
		PRINTFORM %CALLNAME:TARGET%の裸体盛りをみんなに振る舞うことにした
		BEGIN AFTERTRAIN
	;体力切れ
	ELSEIF BASE:体力 <= 0
		TFLAG:調教終了 = 1
		;自爆した場合
		IF ハッキング命令 == "自爆"
			PRINTFORML ★★★%CALLNAME:TARGET%は自爆しました★★★
			自爆キャラ:(NO:TARGET) = 1
			BEGIN AFTERTRAIN
		ELSE
			PRINTFORML （激しい調教で%CALLNAME:TARGET%はひどく消耗したようです　調教を終了します）
			;従順ダウン 陥落済みなら発生しない
			IF ABL:従順 > 0 && !CFLAG:陥落
				ABL:従順--
				PRINTFORML %CALLNAME:TARGET%の従順が1下がった
			ENDIF
			;回復量低下フラグ
			CFLAG:回復量低下 += 2
			CFLAG:ストレス値 += 10
			BASE:体力 = 1
			;条件を満たしていれば吸血イベント発生するか選択できる
			;条件：主人が吸血鬼で、対象が種族[人間]である
			IF (TALENT:MASTER:吸血鬼 && CSTR:種族 == "人間") && !(TALENT:MASTER:スライム || TALENT:MASTER:溶ける快)
				PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
				PRINTFORMW このまま%CALLNAME:TARGET%を……
				PRINTL [0] 眷属に迎え入れようか
				PRINTL [1] 放っておこうか
				DO
					INPUT
					;吸血
					IF RESULT == 0
						CALL KOJO_MESSAGE_EVENT, "主人吸血"
						;吸血鬼化
						IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
							TALENT:吸血鬼 = 1
							CSTR:種族 = ヴァンパイア
							;上位陥落素質ありで妄信イベント
							IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
								PRINTFORML %CALLNAME:TARGET%は[妄信]を得た。
								TALENT:妄信 = 1
							ENDIF
						;ゾンビ化
						ELSE
							TALENT:ゾンビ = 1
							CSTR:種族 = ゾンビ
						ENDIF
						IF CFLAG:被吸血経験 == 0
							EXP:異常経験++
							PRINTL 異常経験+1
						ENDIF
						CFLAG:被吸血経験++
						EXP:MASTER:吸血経験++
					ENDIF
				LOOP LOOPRES(0, 1)
			;助手が陥落して条件を満たしていれば、助手を使った吸血イベント発生するか選択できる
			;条件：助手が居て、助手が吸血鬼で、対象が種族[人間]である
			ELSEIF (ASSI >= 0 && CFLAG:ASSI:陥落 && TALENT:ASSI:吸血鬼 && CSTR:種族 == "人間") && !(TALENT:ASSI:スライム || TALENT:ASSI:溶ける快)
				PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ。
				PRINTFORML %CALLNAME:ASSI%に血を吸わせますか？
				PRINTL [0] - 吸わせる
				PRINTL [1] - 吸わせない
				DO
					INPUT
					IF RESULT == 0
						CALL KOJO_MESSAGE_EVENT, "助手吸血"
						;対象が処女、再生処女、童貞なら吸血鬼化
						IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
							TALENT:吸血鬼 = 1
							CSTR:種族 = ヴァンパイア
						;ゾンビ化
						ELSE
							TALENT:ゾンビ = 1
							CSTR:種族 = ゾンビ
						ENDIF
						;初めて血を吸われたら異常経験追加
						IF CFLAG:被吸血経験 == 0
							EXP:異常経験++ 
							PRINTL 異常経験+1
						ENDIF
						CFLAG:被吸血経験++
						EXP:ASSI:吸血経験++
					ENDIF
				LOOP LOOPRES(0, 1)
			;条件を満たしていればスライム浸食イベント発生するか選択できる
			;条件：主人がスライムで、対象が種族[人間]である
			ELSEIF (TALENT:MASTER:スライム || TALENT:MASTER:溶ける快) && CSTR:種族 == "人間" && !FLAG:抗スライム
				PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
				PRINTFORMW このまま%CALLNAME:TARGET%を……
				PRINTL [0] 少しずつスライム化させていこう
				PRINTL [1] 一気にスライムに変えてしまおう
				PRINTL [2] いや、今は休ませておこう
				DO
					INPUT
					IF RESULT == 0
						;未感染ならスライム感染
						IF CFLAG:スライム度 == 0
							CALL KOJO_MESSAGE_EVENT, "主人スライム感染初回"
							CFLAG:スライム度++
						;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
						ELSEIF CFLAG:スライム度 < 20
							CALL KOJO_MESSAGE_EVENT, "主人スライム感染深化"
							CFLAG:スライム度 += 10
						ELSE
							;感染済みでスライム度が20以上なら完全スライム化
							CALL KOJO_MESSAGE_EVENT, "主人スライム感染完了"
							CFLAG:スライム度 += 10
							TALENT:スライム = 1
							CSTR:種族 = スライム
							;上位陥落素質ありで妄信イベント
							IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
								PRINTFORML %CALLNAME:TARGET%は[妄信]を得た。
								TALENT:妄信 = 1
							ENDIF
						ENDIF
					ELSEIF RESULT == 1
						;深度を無視して一気にスライム化
						CALL KOJO_MESSAGE_EVENT, "主人スライム強制変化"
						IF TALENT:MASTER:溶ける快
							CFLAG:スライム度 += 1500
							TALENT:溶ける快 = 1
							TALENT:妄信 = 1
							CALL 相性加算, TARGET, MASTER, 50
							CSTR:種族 = スライム
						ELSE
							CFLAG:スライム度 += 30
							TALENT:スライム = 1
							TALENT:崩壊 = 1
							CSTR:種族 = スライム
							IF CFLAG:陥落
								PRINTFORMW %CALLNAME:TARGET%が、%CALLNAME:MASTER%に抱いてた感情は、スライムの本能に塗りつぶされてしまった……
								SIF TALENT:恋慕
									PRINTFORMW %CALLNAME:TARGET%は[恋慕]を失った
								TALENT:恋慕 = 0
								SIF TALENT:淫乱
									PRINTFORMW %CALLNAME:TARGET%は[淫乱]を失った
								TALENT:淫乱 = 0
								SIF TALENT:服従
									PRINTFORMW %CALLNAME:TARGET%は[服従]を失った
								TALENT:服従 = 0
								SIF TALENT:親愛
									PRINTFORMW %CALLNAME:TARGET%は[親愛]を失った
								TALENT:親愛 = 0
								SIF TALENT:娼婦
									PRINTFORMW %CALLNAME:TARGET%は[娼婦]を失った
								TALENT:娼婦 = 0
								SIF TALENT:隷属
									PRINTFORMW %CALLNAME:TARGET%は[隷属]を失った
								TALENT:隷属 = 0
								CFLAG:陥落 = 0
							ENDIF
						ENDIF
					ENDIF
				LOOP LOOPRES(0, 2)
			;助手が陥落して条件を満たしていれば、助手を使った吸血イベント発生するか選択できる
			;条件：助手が居て、助手がスライムで、対象が種族[人間]である
			ELSEIF ASSI >= 0 && CFLAG:ASSI:陥落 && (TALENT:ASSI:スライム || TALENT:ASSI:溶ける快) && CSTR:種族 == "人間" && !FLAG:抗スライム
				PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ。
				PRINTFORML %CALLNAME:ASSI%に襲わせますか？
				PRINTL [0] - 襲わせる
				PRINTL [1] - 襲わせない
				DO
					INPUT
					IF RESULT == 0
						;未感染ならスライム感染
						IF CFLAG:スライム度 == 0
							CALL KOJO_MESSAGE_EVENT, "助手スライム感染初回"
							CFLAG:スライム度++
						;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
						ELSEIF CFLAG:スライム度 < 20
							CALL KOJO_MESSAGE_EVENT, "助手スライム感染深化"
							CFLAG:スライム度 += 10
						ELSE
							;感染済みでスライム度が20以上なら完全スライム化
							CALL KOJO_MESSAGE_EVENT, "助手スライム感染完了"
							CFLAG:スライム度 += 10
							TALENT:スライム = 1
							CSTR:種族 = スライム
						ENDIF
					ENDIF
				LOOP LOOPRES(0, 1)
			ENDIF
			;実績:ガッキー博士で奴隷の体力を0にする
			SIF CHARANAMEF(PLAYER) == "ガッキー博士"
				CALL 実績解除, "「科学の発展に犠牲は付き物デース」"
		ENDIF
		BEGIN AFTERTRAIN
	ELSEIF BASE:体力 < 500 && !特質("無理強い")
		TFLAG:調教終了 = 1
		PRINTL （体力が限界に来ています　調教を終了します）
		;条件を満たしていれば吸血イベント発生するか選択できる
		;条件：主人が吸血鬼で、対象が種族[人間]である
		IF (TALENT:MASTER:吸血鬼 && CSTR:種族 == "人間") && !(TALENT:MASTER:スライム || TALENT:MASTER:溶ける快)
			PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
			PRINTFORMW このまま%CALLNAME:TARGET%を……
			PRINTL [0] 眷属に迎え入れようか
			PRINTL [1] 放っておこうか
			DO
				INPUT
				;吸血
				IF RESULT == 0
					CALL KOJO_MESSAGE_EVENT, "主人吸血"
					;吸血鬼化
					IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
						TALENT:吸血鬼 = 1
						CSTR:種族 = ヴァンパイア
						;上位陥落素質ありで妄信イベント
						IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
							PRINTFORML %CALLNAME:TARGET%は[妄信]を得た。
							TALENT:妄信 = 1
						ENDIF
					;ゾンビ化
					ELSE
						TALENT:ゾンビ = 1
						CSTR:種族 = ゾンビ
					ENDIF
					IF CFLAG:被吸血経験 == 0
						EXP:異常経験++
						PRINTL 異常経験+1
					ENDIF
					CFLAG:被吸血経験++
					EXP:MASTER:吸血経験++
				ENDIF
			LOOP LOOPRES(0, 1)
		;助手が陥落して条件を満たしていれば、助手を使った吸血イベント発生するか選択できる
		;条件：助手が居て、助手が吸血鬼で、対象が種族[人間]である
		ELSEIF (ASSI >= 0 && CFLAG:ASSI:陥落 && TALENT:ASSI:吸血鬼 && CSTR:種族 == "人間") && !(TALENT:ASSI:スライム || TALENT:ASSI:溶ける快)
			PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ。
			PRINTFORML %CALLNAME:ASSI%に血を吸わせますか？
			PRINTL [0] - 吸わせる
			PRINTL [1] - 吸わせない
			DO
				INPUT
				IF RESULT == 0
					CALL KOJO_MESSAGE_EVENT, "助手吸血"
					;対象が処女、再生処女、童貞なら吸血鬼化
					IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
						TALENT:吸血鬼 = 1
						CSTR:種族 = ヴァンパイア
					;ゾンビ化
					ELSE
						TALENT:ゾンビ = 1
						CSTR:種族 = ゾンビ
					ENDIF
					;初めて血を吸われたら異常経験追加
					IF CFLAG:被吸血経験 == 0
						EXP:異常経験++ 
						PRINTL 異常経験+1
					ENDIF
					CFLAG:被吸血経験++
					EXP:ASSI:吸血経験++
				ENDIF
			LOOP LOOPRES(0, 1)
		;条件を満たしていればスライム浸食イベント発生するか選択できる
		;条件：主人がスライムで、対象が種族[人間]である
		ELSEIF (TALENT:MASTER:スライム || TALENT:MASTER:溶ける快) && CSTR:種族 == "人間" && !FLAG:抗スライム
			PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
			PRINTFORMW このまま%CALLNAME:TARGET%を……
			PRINTL [0] 少しずつスライム化させていこう
			PRINTL [1] 一気にスライムに変えてしまおう
			PRINTL [2] いや、今は休ませておこう
			DO
				INPUT
				IF RESULT == 0
					;未感染ならスライム感染
					IF CFLAG:スライム度 == 0
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染初回"
						CFLAG:スライム度++
					;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
					ELSEIF CFLAG:スライム度 < 20
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染深化"
						CFLAG:スライム度 += 10
					ELSE
						;感染済みでスライム度が20以上なら完全スライム化
						CALL KOJO_MESSAGE_EVENT, "主人スライム感染完了"
						CFLAG:スライム度 += 10
						TALENT:スライム = 1
						CSTR:種族 = スライム
						;上位陥落素質ありで妄信イベント
						IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
							PRINTFORML %CALLNAME:TARGET%は[妄信]を得た。
							TALENT:妄信 = 1
						ENDIF
					ENDIF
				ELSEIF RESULT == 1
					;深度を無視して一気にスライム化
					CALL KOJO_MESSAGE_EVENT, "主人スライム強制変化"
					IF TALENT:MASTER:溶ける快
						CFLAG:スライム度 += 1500
						TALENT:溶ける快 = 1
						TALENT:妄信 = 1
						CALL 相性加算, TARGET, MASTER, 50
						CSTR:種族 = スライム
					ELSE
						CFLAG:スライム度 += 30
						TALENT:スライム = 1
						TALENT:崩壊 = 1
						CSTR:種族 = スライム
						IF CFLAG:陥落
							PRINTFORMW %CALLNAME:TARGET%が、%CALLNAME:MASTER%に抱いてた感情は、スライムの本能に塗りつぶされてしまった……
							SIF TALENT:恋慕
								PRINTFORMW %CALLNAME:TARGET%は[恋慕]を失った
								TALENT:恋慕 = 0
							SIF TALENT:淫乱
								PRINTFORMW %CALLNAME:TARGET%は[淫乱]を失った
								TALENT:淫乱 = 0
							SIF TALENT:服従
								PRINTFORMW %CALLNAME:TARGET%は[服従]を失った
								TALENT:服従 = 0
							SIF TALENT:親愛
								PRINTFORMW %CALLNAME:TARGET%は[親愛]を失った
								TALENT:親愛 = 0
							SIF TALENT:娼婦
								PRINTFORMW %CALLNAME:TARGET%は[娼婦]を失った
								TALENT:娼婦 = 0
							SIF TALENT:隷属
								PRINTFORMW %CALLNAME:TARGET%は[隷属]を失った
								TALENT:隷属 = 0
							CFLAG:陥落 = 0
						ENDIF
					ENDIF
				ENDIF
			LOOP LOOPRES(0, 2)
		;助手が陥落して条件を満たしていれば、助手を使った吸血イベント発生するか選択できる
		;条件：助手が居て、助手がスライムで、対象が種族[人間]である
		ELSEIF ASSI >= 0 && CFLAG:ASSI:陥落 && (TALENT:ASSI:スライム || TALENT:ASSI:溶ける快) && CSTR:種族 == "人間" && !FLAG:抗スライム
			PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ。
			PRINTFORML %CALLNAME:ASSI%に襲わせますか？
			PRINTL [0] - 襲わせる
			PRINTL [1] - 襲わせない
			DO
				INPUT
				IF RESULT == 0
					;未感染ならスライム感染
					IF CFLAG:スライム度 == 0
						CALL KOJO_MESSAGE_EVENT, "助手スライム感染初回"
						CFLAG:スライム度++
					;感染済みでスライム度が20以下ならスライム度+10（１段階進行）
					ELSEIF CFLAG:スライム度 < 20
						CALL KOJO_MESSAGE_EVENT, "助手スライム感染深化"
						CFLAG:スライム度 += 10
					ELSE
						;感染済みでスライム度が20以上なら完全スライム化
						CALL KOJO_MESSAGE_EVENT, "助手スライム感染完了"
						CFLAG:スライム度 += 10
						TALENT:スライム = 1
						CSTR:種族 = スライム
					ENDIF
				ENDIF
			LOOP LOOPRES(0, 1)
		ENDIF
		BEGIN AFTERTRAIN
	ENDIF
ENDIF

;流産時に体力が削りきれなかった場合は強制的に調教終了
IF TFLAG:流産
	PRINTFORML （%CALLNAME:TARGET%が流産したため、調教を終了します）
	CFLAG:使用不可 += 30
	CFLAG:出産トラウマ += 50
	SIF CFLAG:出産トラウマ >= 100
		CFLAG:出産トラウマ = 100
	BEGIN AFTERTRAIN
ENDIF
;自爆時も同様
IF ハッキング命令 == "自爆"
	PRINTFORML ★★★%CALLNAME:TARGET%は自爆しました★★★
	自爆キャラ:(NO:TARGET) = 1
	BEGIN AFTERTRAIN
ENDIF

;調教で【崩壊】を得た場合のイベント
;@COLLAPSE_MIND_TRAIN
;PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
;PRINTFORMW 激しい調教の影響によって、%CALLNAME:TARGET%の精神に異常をきたしてしまったようだ
;PRINTFORMW %NAME:TARGET%は【崩壊】してしまった
;TALENT:崩壊 = 1
;IF CFLAG:陥落
;	PRINTFORMW 今まで重ねてきた時間も虚しく、%CALLNAME:TARGET%はすっかり心を閉ざしてしまった……
;	SIF TALENT:恋慕
;		PRINTFORMW %CALLNAME:TARGET%は[恋慕]を失った
;	SIF TALENT:淫乱
;		PRINTFORMW %CALLNAME:TARGET%は[淫乱]を失った
;	SIF TALENT:服従
;		PRINTFORMW %CALLNAME:TARGET%は[服従]を失った
;	SIF TALENT:親愛
;		PRINTFORMW %CALLNAME:TARGET%は[親愛]を失った
;	SIF TALENT:娼婦
;		PRINTFORMW %CALLNAME:TARGET%は[娼婦]を失った
;	SIF TALENT:隷属
;		PRINTFORMW %CALLNAME:TARGET%は[隷属]を失った
;	CFLAG:陥落 = 0
;ENDIF

;調教終了時のイベント
@EVENTEND
#LATER
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ストレス低下量
#DIM DYNAMIC 実績計算用

SIF ハッキング命令 != "自爆"
	CALL KOJO_TURNEND
PRINTW 調教を終了しました

CALL 調教称号計算
CALL アイテム修理

;ペニバン解除タイミングが怪しいのでここで外しておく
WHILE FINDCHARA(TEQUIP:ペニスバンド, 1) >= 0
	TEQUIP:FINDCHARA(TEQUIP:ペニスバンド, 1):ペニスバンド = 0
WEND

;自爆したらDELCHARAして全部無視してTURNEND
IF ハッキング命令 == "自爆"
	CALL プレイログ記録, @"%NAME:TARGET%自爆"
	CALL CLEAR_CHARA, TARGET
	TARGET = -1
	BEGIN TURNEND
ENDIF

;屋外プレイで雨、もしくはお風呂場プレイ中に終了した場合、人魚の体力気力を半減させる
IF TEQUIP:お風呂場
	IF CSTR:種族 == "人魚"
		MAXBASE:体力 /= 2
		MAXBASE:気力 /= 2
		BASE:体力 /= 2
		BASE:気力 /= 2
	ENDIF
ELSEIF TEQUIP:野外プレイ
	SELECTCASE FLAG:天気
		CASE 3, 4, 5
			IF CSTR:種族 == "人魚"
				MAXBASE:体力 /= 2
				MAXBASE:気力 /= 2
				BASE:体力 /= 2
				BASE:気力 /= 2
			ENDIF
	ENDSELECT
ENDIF

;女装してたら服を元に戻す
IF TEQUIP:女装
	CSTR:特殊 = %服装保存用:0%
	CSTR:上半身上着 = %服装保存用:1%
	CSTR:下半身上着 = %服装保存用:2%
	CSTR:全身上着 = %服装保存用:3%
	CSTR:上半身下着 = %服装保存用:4%
	CSTR:下半身下着 = %服装保存用:5%
	CSTR:足元 = %服装保存用:6%

	CFLAG:挿入可 = 服装フラグ保存用:0
	CFLAG:着衣併用 = 服装フラグ保存用:1
	CFLAG:上着パイズリ = 服装フラグ保存用:2
	CFLAG:下着パイズリ = 服装フラグ保存用:3

	TEQUIP:女装 = 0
ENDIF

;快感に素直、快感の否定は絶頂回数に応じてストレス増減
IF TALENT:快感に素直
	CFLAG:ストレス値 -= EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
ELSEIF TALENT:快感の否定 && !CFLAG:陥落
	CFLAG:ストレス値 += EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
ENDIF

;恋慕系陥落で流産してなければ愛情経験に応じてストレス低下
ストレス低下量 = EXP:愛情経験-TFLAG:愛情経験計算用
IF ストレス低下量 > 0 && CFLAG:ストレス値 > 0
	IF TALENT:恋慕 && ストレス低下量 >= 2
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の求愛によってストレスが緩和されました
		CFLAG:ストレス値 -= ストレス低下量/2
	ELSEIF TALENT:親愛
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%からの愛を確かめることでことでストレスが緩和されました
		CFLAG:ストレス値 -= ストレス低下量
	ENDIF
ENDIF

;淫乱系陥落で絶頂回数に応じてストレス低下
ストレス低下量 = EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂*EX:Ｂ絶頂+EX:射精
IF ストレス低下量 > 0
	IF TALENT:淫乱
		PRINTFORMW %CALLNAME:TARGET%は欲求を満たされることでストレスが緩和しました
		CFLAG:ストレス値 = ストレス低下量
	ELSEIF TALENT:娼婦
		PRINTFORMW %CALLNAME:TARGET%は欲求を満たされることでストレスが緩和しました
		CFLAG:ストレス値 -= ストレス低下量*2
	ENDIF
ENDIF

;マゾっ気があればPALAM:苦痛のPALAMLVに応じてストレス低下
IF ABL:マゾっ気 && GETPALAMLV(PALAM:苦痛, 10)
	PRINTFORMW %CALLNAME:TARGET%はマゾヒズムを満たされることでストレスが緩和しました
	CFLAG:ストレス値 -= MIN(ABL:マゾっ気, GETPALAMLV(PALAM:苦痛, 10))
ENDIF

;精液中毒 主人と助手の射精回数に応じてストレス低下
ストレス低下量 = EX:MASTER:射精+EX:MASTER:触手射精
SIF ASSI > 0
	ストレス低下量 += EX:ASSI:射精
IF ストレス低下量 > 4 && ABL:精液中毒
	PRINTFORMW %CALLNAME:TARGET%は精液への渇望を満たされることでストレスが緩和しました
	CFLAG:ストレス値 -= MIN(ABL:精液中毒, ストレス低下量/5)
ENDIF

;採血
IF TFLAG:採血量
	PRINTFORMW 今回の調教で計{TFLAG:採血量}mlの血液を採取しました
	FLAG:保管済み血液 += TFLAG:採血量
ENDIF

;搾乳した母乳の売却
CALL SELL_MILK

;搾精した精液の売却
CALL SELL_SAMEN

;調教時に録画したビデオを売却
CALL SELL_VIDEO

;調教時に撮影した写真を売却
VARLINE = 0
CALL SELL_PICTURE

;調教後行為のチェック 気絶、流産時、裸体盛り時はスルー
SIF !TFLAG:気絶 && !TFLAG:流産 && !CFLAG:使用不可 && !TFLAG:裸体盛り
	CALL SELF_CHECK

;コース「THE 房中術」「何かを間違えた房中術」での処理
SELECTCASE 受験段位名
	CASE "THE 房中術"
		FLAG:段位ゲージ -= EX:MASTER:射精
	CASE "何かを間違えた房中術"
		MONEY += 2000*EX:MASTER:射精
		CALL 現金出納帳記録, @"房中術パワー/{2000*EX:MASTER:射精}"
ENDSELECT

;実績「1回の調教でフローラに10回以上中出しする」
IF CHARANAMEF(TARGET) == "フローラ"
	実績計算用 = 0
	実績計算用 += PREG:0
	SIF ASSI > -1
		実績計算用 += PREG:1
	IF 実績計算用 >= 10
		CALL 実績解除, "生命の環を紡いで"
	ENDIF
ELSEIF ASSI > -1 && CHARANAMEF(ASSI) == "フローラ"
	実績計算用 = 0
	実績計算用 += PREG:ASSI:0
	実績計算用 += PREG:ASSI:2
	IF 実績計算用 >= 10
		CALL 実績解除, "生命の環を紡いで"
	ENDIF
ENDIF

;全体の妊娠判定の処理
CALL IN_VAGINA

;何の珠を得られたか
CALL JUEL_CHECK

;否定の珠の打ち消しチェック
CALL DENIAL_CHECK

;淫乱or娼婦奴隷の相性増加
IF TALENT:淫乱
	IF RAND:3 == 0
		CALL 相性加算, TARGET, MASTER, 1, 150
		SIF ASSI > 0
			CALL 相性加算, TARGET, ASSI, 1, 150
	ENDIF
ELSEIF TALENT:娼婦
	IF RAND:2 == 0
		CALL 相性加算, TARGET, MASTER, 1, 200
		SIF ASSI > 0
			CALL 相性加算, TARGET, ASSI, 1, 200
	ENDIF
ENDIF

;絶倫：調教中の射精回数10回以上（出し尽くす）
IF CFLAG:残数 <= 0 && !TALENT:絶倫 && PENIS(TARGET)
	CALL KOJO_MESSAGE_EVENT, "絶倫"
	TALENT:絶倫 = 1
ENDIF

VARLINE = 0

;BEGIN ABLUP
JUMP USER_ABLUP

@アイテム修理
#DIM DYNAMIC LCOUNT
;壊れたアイテムを修理する
FOR LCOUNT, 0, VARSIZE("ITEM")
	IF ITEM:LCOUNT == -1
		PRINTFORML 破損した%ITEMNAME:LCOUNT%を修理しました
		ITEM:LCOUNT = 1
	ENDIF
NEXT

@JUEL_CHECK
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 取得量

FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE GETPALAMLV(PALAM:LCOUNT, 10)
		CASE 0
			取得量 = 0
		CASE 1
			IF PALAM:LCOUNT < PALAMLV:1*3
				取得量 = 1
			ELSE
				取得量 = 2
			ENDIF
		CASE 2
			IF PALAM:LCOUNT < PALAMLV:2*3
				取得量 = 10
			ELSE
				取得量 = 20
			ENDIF
		CASE 3
			IF PALAM:LCOUNT < PALAMLV:3*2
				取得量 = 100
			ELSE
				取得量 = 200
			ENDIF
		CASE 4
			取得量 = 500
		CASE 5
			取得量 = 1000
		CASE 6
			取得量 = 2000
		CASE 7
			取得量 = 3000
		CASE 8
			取得量 = 5000
		CASE 9
			取得量 = 8000
		CASEELSE
			取得量 = PALAM:LCOUNT/20
	ENDSELECT

	SELECTCASE PALAMNAME:LCOUNT
		CASE "快Ｃ"
			IF PENIS(TARGET)
				GOTJUEL:LCOUNT = 取得量+(EX:射精*2000)+(EX:潮噴き*3000)
			ELSE
				GOTJUEL:LCOUNT = 取得量+EX:Ｃ絶頂*1000
			ENDIF
		CASE "快Ｖ"
			GOTJUEL:LCOUNT = 取得量+EX:Ｖ絶頂*1000
		CASE "快Ａ"
			GOTJUEL:LCOUNT = 取得量+EX:Ａ絶頂*1000
		CASE "快Ｂ"
			GOTJUEL:LCOUNT = 取得量+EX:Ｂ絶頂*1000
		CASE "反感", "不快", "抑鬱", "否定"
			GOTJUEL:否定 += 取得量
		CASEELSE
			GOTJUEL:LCOUNT = 取得量
	ENDSELECT
NEXT

CALL CHECKDRAWLINE
PRINTL 調教の結果：
FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE PALAMNAME:LCOUNT
		CASE "反感"
			PRINTFORML 否定の珠×({JUEL:否定}+{GOTJUEL:否定})
			JUEL:否定 += GOTJUEL:否定
			BREAK
		CASE "潤滑", "勃起"
		CASEELSE
			PRINTFORML %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT}+{GOTJUEL:LCOUNT})
			JUEL:LCOUNT += GOTJUEL:LCOUNT
	ENDSELECT
NEXT

PRINTW 以上の珠を得ました

;上限は999,999,999,999
FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE PALAMNAME:LCOUNT
		CASE "反感"
			SIF JUEL:否定 > 999999999999
				JUEL:否定 = 999999999999
			BREAK
		CASEELSE
			SIF JUEL:LCOUNT > 999999999999
				JUEL:LCOUNT = 999999999999
	ENDSELECT
NEXT

@DENIAL_CHECK
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 相殺量

;欲情、屈服、恭順の珠から順に打ち消される
SIF JUEL:否定 == 0
	RETURN 0

CALL CHECKDRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています

IF JUEL:否定 > 0 && (JUEL:恭順+JUEL:欲情+JUEL:屈服) > 0
	相殺量 = JUEL:否定
	SIF JUEL:欲情 < JUEL:否定
		相殺量 = JUEL:欲情

	JUEL:欲情 -= 相殺量
	JUEL:否定 -= 相殺量

	SIF 相殺量 > 0
		PRINTFORML 欲情の珠×{相殺量}減少

	相殺量 = JUEL:否定
	SIF JUEL:屈服 < JUEL:否定
		相殺量 = JUEL:屈服

	SIF 相殺量 > 0
		PRINTFORML 屈服の珠×{相殺量}減少

	JUEL:屈服 -= 相殺量
	JUEL:否定 -= 相殺量

	相殺量 = JUEL:否定
	SIF JUEL:恭順 < JUEL:否定
		相殺量 = JUEL:恭順

	JUEL:恭順 -= 相殺量
	JUEL:否定 -= 相殺量

	SIF 相殺量 > 0
		PRINTFORML 恭順の珠×{相殺量}減少
ENDIF

PRINTL その結果、
FOR LCOUNT, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE PALAMNAME:LCOUNT
		CASE "反感"
			PRINTFORML 否定の珠×({JUEL:100})
			BREAK
		CASE "潤滑", "勃起"
		CASEELSE
			PRINTFORML %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT})
	ENDSELECT
NEXT

PRINTW 以上のように変化しました

@CVAR_RESET
;調教時のキャラ変数を初期化する BEGIN TRAINで行われる処理と同等
;STAINは独自仕様なので初期化しない
ASSIPLAY = 0
;PREVCOM = -1 使用禁止変数
;NEXTCOM = -1 使用禁止変数
VARSET TFLAG
IF TARGET >= 0
	VARSET GOTJUEL
	VARSET TEQUIP
	VARSET EX
	VARSET PALAM
	VARSET SOURCE
ENDIF
IF ASSI >= 0
	VARSET TEQUIP:ASSI:0
	VARSET EX:ASSI:0
ENDIF
VARSET TEQUIP:MASTER:0
VARSET EX:MASTER:0
PREVCOMS = 
SELECTCOMS = 
SELECTCOM = -1

@TVAR_RESET
;コマンド実行時の変数初期化 NOWEXとTFLAG:100以下
#DIM DYNAMIC LCOUNT

VARSET NOWEX, 0
VARSET NOWEX:MASTER:0, 0
SIF ASSI >= 0
	VARSET NOWEX:ASSI:0, 0
VARSET TFLAG, 0, 0, 100
;寸止めフラグをリセット
TFLAG:寸止め = 0
