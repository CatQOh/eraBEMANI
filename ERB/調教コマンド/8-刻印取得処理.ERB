;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
@MARK_GOT
;-------------------------------------------------
;苦痛刻印
;-------------------------------------------------
;学園モードでは刻印取得は無い
SIF ゲームモード() == "学園モード"
	RETURN
SELECTCASE UP:苦痛
	CASE 2000 TO 4000
		IF MARK:苦痛刻印 < 1; && MARK:苦痛刻印履歴 < 1
			MARK:苦痛刻印 = 1
			MARK:苦痛刻印履歴 = 1
			CALL KOJO_MESSAGE_MARK, 1
			PRINTFORMW %CALLNAME:TARGET%は苦痛刻印LV1を得た
		ENDIF
	CASE 4000 TO 8000
		IF MARK:苦痛刻印 < 2; && MARK:苦痛刻印履歴 < 2
			MARK:苦痛刻印 = 2
			MARK:苦痛刻印履歴 = 2
			CALL KOJO_MESSAGE_MARK, 1
			PRINTFORMW %CALLNAME:TARGET%は苦痛刻印LV2を得た
			;従順上昇 [気丈][感情乏しい][痛みに強い][抵抗]だと無効
			IF !TALENT:気丈 && !TALENT:感情乏しい && !TALENT:痛みに強い && !TALENT:抵抗 && ABL:従順 < 1
				;さらに[痛みに弱い]だと上昇値+1
				IF TALENT:痛みに弱い && ABL:従順 < 2
					PRINTW そして従順がLV2になった
					ABL:従順 = 2
				ELSE
					PRINTW そして従順がLV1になった
					ABL:従順 = 1
				ENDIF
			ENDIF
		ENDIF
	CASE IS >= 8000
		IF MARK:苦痛刻印 < 3; && MARK:苦痛刻印履歴 < 3
			MARK:苦痛刻印 = 3
			CALL KOJO_MESSAGE_MARK, 1
			PRINTFORML %CALLNAME:TARGET%は苦痛刻印LV3を得た
			IF MARK:苦痛刻印履歴 < 3
				IF TALENT:PLAYER:サド
					PRINTFORML 【%CALLNAME:PLAYER%がサド】
					PRINTFORMW 異常経験+2
					EXP:異常経験 += 2
				ELSEIF TALENT:PLAYER:針さばき && SELECTCOMS == "針"
					PRINTFORML 【%CALLNAME:PLAYER%が針さばき】
					PRINTFORMW 異常経験+2
					EXP:異常経験 += 2
				ELSE
					PRINTFORMW 異常経験+1
					EXP:異常経験++
				ENDIF
			ENDIF
			;従順上昇 [気丈][感情乏しい][痛みに強い][抵抗]だと無効
			IF !TALENT:気丈 && !TALENT:感情乏しい && !TALENT:痛みに強い && !TALENT:抵抗 && ABL:従順 < 2
				;さらに[痛みに弱い]だと上昇値+1
				IF TALENT:痛みに弱い && ABL:従順 < 3
					PRINTW そして従順がLV3になった
					ABL:従順 = 3
				ELSE
					PRINTW そして従順がLV2になった
					ABL:従順 = 2
				ENDIF
			ENDIF
			MARK:苦痛刻印履歴 = 3
		ENDIF
ENDSELECT

;-------------------------------------------------
;快楽刻印
;-------------------------------------------------
SELECTCASE TFLAG:快楽刻印フラグ
	CASE 1
		IF MARK:快楽刻印 < 1
			MARK:快楽刻印 = 1
			CALL KOJO_MESSAGE_MARK, 2
			PRINTFORMW %CALLNAME:TARGET%は快楽刻印LV1を得た
		ENDIF
	CASE 2
		IF MARK:快楽刻印 < 2
			MARK:快楽刻印 = 2
			CALL KOJO_MESSAGE_MARK, 2
			PRINTFORMW %CALLNAME:TARGET%は快楽刻印LV2を得た
			;欲望上昇 [自制心][一線越えない][抑圧]だと無効
			IF !TALENT:自制心 && !TALENT:一線越えない && !TALENT:抑圧 && ABL:欲望 < 1
				;さらに[好奇心]だと上昇値+1
				IF TALENT:好奇心 && ABL:欲望 < 2
					PRINTW そして欲望がLV2になった
					ABL:欲望 = 2
				ELSE
					PRINTW そして欲望がLV1になった
					ABL:欲望 = 1
				ENDIF
			ENDIF
		ENDIF
	CASE 3
		IF MARK:快楽刻印 < 3
			MARK:快楽刻印 = 3
			CALL KOJO_MESSAGE_MARK, 2
			PRINTFORMW %CALLNAME:TARGET%は快楽刻印LV3を得た
			;欲望上昇 [自制心][一線越えない][抑圧]だと無効
			IF !TALENT:自制心 && !TALENT:一線越えない && !TALENT:抑圧 && ABL:欲望 < 2
				;さらに[好奇心]だと上昇値+1
				IF TALENT:好奇心 && ABL:欲望 < 3
					PRINTW そして欲望がLV3になった
					ABL:欲望 = 3
				ELSE
					PRINTW そして欲望がLV2になった
					ABL:欲望 = 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT

;-------------------------------------------------
;屈服刻印
;-------------------------------------------------
;快楽によるLvUP 快楽刻印＞屈服刻印じゃないと発生しない
IF MARK:快楽刻印 > MARK:屈服刻印
	SELECTCASE UP:快Ｃ+UP:快Ｖ+UP:快Ａ+UP:快Ｂ
		CASE 5000 TO 10000
			IF MARK:屈服刻印 < 1
				MARK:屈服刻印 = 1
				CALL KOJO_MESSAGE_MARK, 3
				PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV1を得た
			ENDIF
		CASE 10000 TO 25000
			IF MARK:屈服刻印 < 2
				MARK:屈服刻印 = 2
				CALL KOJO_MESSAGE_MARK, 3
				PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV2を得た
				;奉仕精神上昇 [保守的][一線越えない][抑圧]だと無効
				IF !TALENT:保守的 && !TALENT:一線越えない && !TALENT:抑圧 && ABL:奉仕精神 < 1
					;さらに[献身的]だと上昇値+1
					IF TALENT:献身的 && ABL:奉仕精神 < 2
						PRINTW そして奉仕精神がLV2になった
						ABL:奉仕精神 = 2
					ELSE
						PRINTW そして奉仕精神がLV1になった
						ABL:奉仕精神 = 1
					ENDIF
				ENDIF
			ENDIF
		CASE IS >= 25000
			IF MARK:屈服刻印 < 3
				MARK:屈服刻印 = 3
				CALL KOJO_MESSAGE_MARK, 3
				PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV3を得た
				;奉仕精神上昇 [保守的][一線越えない][抑圧]だと無効
				IF !TALENT:保守的 && !TALENT:一線越えない && !TALENT:抑圧 && ABL:奉仕精神 < 2
					;さらに[献身的]だと上昇値+1
					IF TALENT:献身的 && ABL:奉仕精神 < 3
						PRINTW そして奉仕精神がLV3になった
						ABL:奉仕精神 = 3
					ELSE
						PRINTW そして奉仕精神がLV2になった
						ABL:奉仕精神 = 2
					ENDIF
				ENDIF
			ENDIF
	ENDSELECT
ENDIF

;苦痛と恐怖によるLvUP
SELECTCASE UP:苦痛+UP:恐怖
	CASE 1000 TO 30000
		IF MARK:屈服刻印 < 1
			MARK:屈服刻印 = 1
			CALL KOJO_MESSAGE_MARK, 4
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV1を得た
		ENDIF
	CASE 30000 TO 50000
		IF MARK:屈服刻印 < 2
			MARK:屈服刻印 = 2
			CALL KOJO_MESSAGE_MARK, 4
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV2を得た
			;奉仕精神上昇 [プライド高い][痛みに強い][感情乏しい]だと無効
			IF !TALENT:プライド高い && !TALENT:痛みに強い && !TALENT:感情乏しい && ABL:奉仕精神 < 1
				;さらに[臆病]だと上昇値+1
				IF TALENT:臆病 && ABL:奉仕精神 < 2
					PRINTW そして奉仕精神がLV2になった
					ABL:奉仕精神 = 2
				ELSE
					PRINTW そして奉仕精神がLV1になった
					ABL:奉仕精神 = 1
				ENDIF
			ENDIF
		ENDIF
	CASE IS >= 50000
		IF MARK:屈服刻印 < 3
			MARK:屈服刻印 = 3
			CALL KOJO_MESSAGE_MARK, 4
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV3を得た
			;奉仕精神上昇 [プライド高い][痛みに強い][感情乏しい]だと無効
			IF !TALENT:プライド高い && !TALENT:痛みに強い && !TALENT:感情乏しい && ABL:奉仕精神 < 2
				;さらに[臆病]だと上昇値+1
				IF TALENT:臆病 && ABL:奉仕精神 < 3
					PRINTW そして奉仕精神がLV3になった
					ABL:奉仕精神 = 3
				ELSE
					PRINTW そして奉仕精神がLV2になった
					ABL:奉仕精神 = 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT

;恭順と屈服によるLvUP
SELECTCASE UP:恭順+UP:屈服
	CASE 4000 TO 8000
		IF MARK:屈服刻印 < 1
			MARK:屈服刻印 = 1
			CALL KOJO_MESSAGE_MARK, 5
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV1を得た
		ENDIF
	CASE 8000 TO 15000
		IF MARK:屈服刻印 < 2
			MARK:屈服刻印 = 2
			CALL KOJO_MESSAGE_MARK, 5
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV2を得た
			;奉仕精神上昇 [生意気][反抗的][無関心]だと無効
			IF !TALENT:生意気 && !TALENT:反抗的 && !TALENT:無関心 && ABL:奉仕精神 < 1
				;さらに[寂しがり屋]だと上昇値+1
				IF TALENT:寂しがり屋 && ABL:奉仕精神 < 2
					PRINTW そして奉仕精神がLV2になった
					ABL:奉仕精神 = 2
				ELSE
					PRINTW そして奉仕精神がLV1になった
					ABL:奉仕精神 = 1
				ENDIF
			ENDIF
		ENDIF
	CASE IS >= 15000
		IF MARK:屈服刻印 < 3
			MARK:屈服刻印 = 3
			CALL KOJO_MESSAGE_MARK, 5
			PRINTFORMW %CALLNAME:TARGET%は屈服刻印LV3を得た
			;奉仕精神上昇 [生意気][反抗的][無関心]だと無効
			IF !TALENT:生意気 && !TALENT:反抗的 && !TALENT:無関心 && ABL:奉仕精神 < 2
				;さらに[寂しがり屋]だと上昇値+1
				IF TALENT:寂しがり屋 && ABL:奉仕精神 < 3
					PRINTW そして奉仕精神がLV2になった
					ABL:奉仕精神 = 3
				ELSE
					PRINTW そして奉仕精神がLV1になった
					ABL:奉仕精神 = 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT

;-------------------------------------------------
;反発刻印
;------------------------------------------------- 
SELECTCASE UP:反感+UP:不快
	CASE 1000 TO 2000
		IF MARK:反発刻印履歴 < 1 && !TFLAG:反発刻印回避
			MARK:反発刻印 = 1
			MARK:反発刻印履歴 = 1
			CALL KOJO_MESSAGE_MARK, 10
			PRINTFORMW %CALLNAME:TARGET%は反発刻印LV1を得た
		ENDIF
	CASE 2000 TO 4000
		IF MARK:反発刻印履歴 < 2 && !TFLAG:反発刻印回避
			MARK:反発刻印 = 2
			MARK:反発刻印履歴 = 2
			CALL KOJO_MESSAGE_MARK, 10
			PRINTFORML %CALLNAME:TARGET%は反発刻印LV2を得た
			;従順低下 陥落済みの場合は無効
			IF !CFLAG:陥落 && ABL:従順 != 0
				IF ABL:従順-1 <= 0
					PRINTW そして従順がLV0に下がった
					ABL:従順 = 0
				ELSE
					PRINTFORMW そして従順がLV{ABL:従順-1}に下がった
					ABL:従順 -= 1
				ENDIF
			ENDIF
		ENDIF
	CASE IS >= 4000
		IF MARK:反発刻印履歴 < 3 && !TFLAG:反発刻印回避
			MARK:反発刻印 = 3
			MARK:反発刻印履歴 = 3
			CALL KOJO_MESSAGE_MARK, 10
			PRINTFORML %CALLNAME:TARGET%は反発刻印LV3を得た
			;従順低下 陥落済みの場合は無効
			IF !CFLAG:陥落 && ABL:従順 != 0
				IF ABL:従順-2 <= 0
					PRINTW そして従順がLV0に下がった
					ABL:従順 = 0
				ELSE
					PRINTFORMW そして従順がLV{ABL:従順-2}に下がった
					ABL:従順 -= 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT
TFLAG:反発刻印回避 = 0
