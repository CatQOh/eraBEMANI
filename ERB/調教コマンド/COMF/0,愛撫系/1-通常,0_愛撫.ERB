;-------------------------------------------------
;愛撫
;-------------------------------------------------
@USERCOM_ABLE_愛撫, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;ペニスバンド装着中はダメ
SIF TEQUIP:ペニスバンド
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_愛撫
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;前回のコマンド
	SELECTCASE PREVCOMS
		;後背位、騎乗位、後背位アナル、騎乗位アナルだとセックス愛撫系に派生
		CASE "後背位手淫", "騎乗位手淫", "後背位アナル手淫", "騎乗位アナル手淫"
			JUMPFORM COM_%PREVCOMS%
		CASE "後背位"
			SIF USERCOM_ABLE_後背位手淫()
				JUMP COM_後背位手淫
		CASE "騎乗位"
			SIF USERCOM_ABLE_騎乗位手淫()
				JUMP COM_騎乗位手淫
		CASE "後背位アナル"
			SIF USERCOM_ABLE_後背位アナル手淫()
				JUMP COM_後背位アナル手淫
		CASE "騎乗位アナル"
			SIF USERCOM_ABLE_騎乗位アナル手淫()
				JUMP COM_騎乗位アナル手淫
	ENDSELECT
ENDIF

PRINTL 愛撫
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 30
SOURCE:快B = 30
SOURCE:情愛 = 30
SOURCE:性行動 = 30
SOURCE:露出 = 10
SOURCE:不潔 = 30
SOURCE:反感 = 10

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;汚臭鈍感
SIF TALENT:汚臭鈍感
	SOURCE:不潔 /= 4
;汚臭敏感
SIF TALENT:汚臭敏感
	SOURCE:不潔 *= 3
;プライド高い
SIF TALENT:プライド高い
	SOURCE:不潔 *= 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

