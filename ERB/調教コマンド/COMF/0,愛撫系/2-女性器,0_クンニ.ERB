;-------------------------------------------------
;クンニ
;-------------------------------------------------
@USERCOM_ABLE_クンニ, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;性器が汚れている場合（愛液、母乳の汚れ以外）
FOR LCOUNT, 0, 8
	IF GETBIT(STAIN:ヴァギナ, LCOUNT)
		SELECTCASE LCOUNT
			CASE 1, 2, 3, 5, 6, 7
				;実行者
				SELECTCASE PLAYER
					;主人
					CASE MASTER
						;汚れ無視を持っていない場合はクンニできない
						SIF !TALENT:MASTER:汚れ無視
							RETURNF 0
					;助手
					CASE ASSI
						;汚臭敏感で従順3以下の場合クンニはできない（汚れ無視なら可能）
						SIF ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
							RETURNF 0
				ENDSELECT
				BREAK
		ENDSELECT
	ENDIF
NEXT

;ペニスバンド装着中はダメ
SIF TEQUIP:ペニスバンド
	RETURNF 0
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURNF 0
;対象がヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_クンニ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じで、縄とボールギャグを使ってない。ただしゲームセンター中はダメ
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:縄 && !TEQUIP:ボールギャグ && !TEQUIP:ゲームセンター
	;前コマンド
	SELECTCASE PREVCOMS
		;フェラチオ、クンニさせる、シックスナインだとシックスナインに
		CASE "フェラチオ", "クンニさせる", "シックスナイン"
			SIF USERCOM_ABLE_シックスナイン()
				JUMP COM_シックスナイン
		;前回の調教がパイズリか縦パイズリかパイズリフェラか69パイズリだと69パイズリに
		CASE "パイズリ", "縦パイズリ", "パイズリフェラ", "69パイズリ"
			SIF USERCOM_ABLE_69パイズリ()
				JUMP COM_69パイズリ
	ENDSELECT
ENDIF

;羞恥プレイ中は岩清水に
SIF TEQUIP:羞恥プレイ
	JUMP COM_岩清水

PRINTL クンニ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 100

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧/5

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.40
	CASE 1
		TIMES LOCAL , 0.60
	CASE 2
		TIMES LOCAL , 0.60
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.20
ENDSELECT

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｖ感覚-3)
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += LOCAL*ABL:マゾっ気/5

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 8
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 80
SOURCE:液体 = 60
SOURCE:露出 = 30
SOURCE:不潔 = 20
SOURCE:反感 = 25

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快C , 0.75

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶの汚れは舐め取る
STAIN:ヴァギナ = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 1

;魔力回復
IF TALENT:PLAYER:魔術師
	LOCAL = MAXBASE:PLAYER:魔力/200
	BASE:PLAYER:魔力 = LIMIT(BASE:PLAYER:魔力+LOCAL, 0, MAXBASE:PLAYER:魔力)
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	PRINTFORML 嗜虐快楽経験(%CALLNAME:PLAYER%)+1
	EXP:PLAYER:嗜虐快楽経験++
ENDIF

RETURN 1

