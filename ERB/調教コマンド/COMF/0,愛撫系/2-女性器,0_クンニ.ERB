;-------------------------------------------------
;クンニ
;-------------------------------------------------
@USERCOM_ABLE_クンニ
#DIM LCOUNT
;クンニ実行判定
;性器が汚れている場合（愛液、母乳の汚れ以外）
FOR LCOUNT, 0, 8
	IF GETBIT(STAIN:ヴァギナ, LCOUNT)
		SELECTCASE LCOUNT
			CASE 1, 2, 3, 5, 6, 7
				;実行者
				SELECTCASE PLAYER
					;主人
					CASE MASTER
						;汚れ無視を持っていない場合はクンニできない
						SIF !TALENT:MASTER:汚れ無視
							RETURN 0
					;助手
					CASE ASSI
						;汚臭敏感で従順3以下の場合クンニはできない（汚れ無視なら可能）
						SIF ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
							RETURN 0
				ENDSELECT
				BREAK
		ENDSELECT
	ENDIF
NEXT

;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0
;対象がヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0

RETURN 1

@COM_クンニ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じで、縄とボールギャグを使ってない
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:縄 && !TEQUIP:ボールギャグ
	;前コマンド
	SELECTCASE PREVCOMS
		;フェラチオ、クンニ強制、シックスナインだとシックスナインに
		CASE "フェラチオ", "クンニ強制", "シックスナイン"
			CALL USERCOM_ABLE_シックスナイン
			SIF RESULT
				JUMP COM_シックスナイン
		;前回の調教がパイズリか縦パイズリかパイズリフェラか69パイズリだと69パイズリに
		CASE "パイズリ", "縦パイズリ", "パイズリフェラ", "69パイズリ"
			CALL USERCOM_ABLE_69パイズリ
			SIF RESULT
				JUMP COM_69パイズリ
	ENDSELECT
ENDIF

;羞恥プレイ中は岩清水に
SIF TEQUIP:羞恥プレイ
	JUMP COM_岩清水

PRINTL クンニ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 100

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧/5

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.40
	CASE 1
		TIMES LOCAL , 0.60
	CASE 2
		TIMES LOCAL , 0.60
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.20
ENDSELECT

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｖ感覚-3)
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += LOCAL*ABL:マゾっ気/5

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 8
LOSEBASE:気力 += 50

SOURCE:快C = 80
SOURCE:液体 = 60
SOURCE:露出 = 30
SOURCE:不潔 = 20
SOURCE:反感 = 25

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快C , 0.75

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶの汚れは舐め取る
STAIN:ヴァギナ = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 1

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	PRINTFORML 嗜虐快楽経験(%CALLNAME:PLAYER%)+1
	EXP:PLAYER:嗜虐快楽経験++
ENDIF

RETURN 1

