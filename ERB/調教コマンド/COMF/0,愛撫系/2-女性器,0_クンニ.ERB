;-------------------------------------------------
;クンニ
;-------------------------------------------------
@USERCOM_ABLE_クンニ, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;性器が汚れている場合（愛液、母乳の汚れ以外）
コマンド実行条件 += "性器が汚れていると実行できない(素質[汚れ無視]なら可)/"
FOR LCOUNT, 0, 8
	IF GETBIT(STAIN:ヴァギナ, LCOUNT)
		SELECTCASE LCOUNT
			CASE 1, 2, 3, 5, 6, 7
				;実行者
				SELECTCASE PLAYER
					;主人
					CASE MASTER
						;汚れ無視を持っていない場合はクンニできない
						SIF !TALENT:MASTER:汚れ無視
							COMRESULT = 0
					;助手
					CASE ASSI
						;汚臭敏感で従順3以下の場合クンニはできない（汚れ無視なら可能）
						SIF ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
							COMRESULT = 0
				ENDSELECT
				BREAK
		ENDSELECT
	ENDIF
NEXT

コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;対象がヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0

RETURNF COMRESULT

@COM_クンニ
PRINTL クンニ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_クンニ射精(100), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 8
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 80
SOURCE:液体 = 60
SOURCE:露出 = 30
SOURCE:不潔 = 20
SOURCE:反感 = 25

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快C , 0.75

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶの汚れは舐め取る
STAIN:ヴァギナ = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1, PLAYER

CALL 魔力回復, 3, PLAYER
CALL 獣人催淫, 1, PLAYER

RETURN 1

