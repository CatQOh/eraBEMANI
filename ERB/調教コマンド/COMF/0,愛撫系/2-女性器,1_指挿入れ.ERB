;-------------------------------------------------
;指挿入れ
;-------------------------------------------------
@USERCOM_ABLE_指挿入れ, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;プレイヤーが助手の場合、助手が女性で従順3以下orレズっ気3以下、対象が潤滑不足の場合指挿入れはできない（サドならＯＫ）
コマンド実行条件 += "実行者が助手で、対象が潤滑不足の場合、助手の従順LV4以上orレズっ気LV4以上必要/"
IF PLAYER == ASSI && !TALENT:ASSI:サド
	SIF SEX(ASSI) == 2 && (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && PALAM:潤滑 < PALAMLV:2
		COMRESULT = 0
ENDIF
;ペニスバンド装着中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0
;異物挿入中はダメ
コマンド実行条件 += "V異物挿入中は実行できない/"
SIF TEQUIP:Ｖ異物
	COMRESULT = 0
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
RETURNF COMRESULT

@COM_指挿入れ
#DIM DYNAMIC 増加量

PRINTL 指挿入れ
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ
SIF !TALENT:処女 && !TALENT:再生処女
	TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ責め(100)

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

IF !TALENT:処女 && !TALENT:再生処女
	SOURCE:快V = 100
	SOURCE:苦痛 = 20
	SOURCE:露出 = 10
	SOURCE:不潔 = 30
	SOURCE:反感 = 30
;処女及び再生処女の場合
;V経験が0の時はV経験を伴わない物に派生
;Vの珠は入らないが、快Vが高いと慣らしてあるため処女喪失時に影響
ELSE
	SIF PALAM:快Ｖ < 100
		SOURCE:快V = 60
	SOURCE:苦痛 = 30
	SOURCE:恐怖 = 40
	SOURCE:露出 = 20
	SOURCE:不潔 = 10
	SOURCE:反感 = 50
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
IF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20
	;処女
	SIF TALENT:処女
		SOURCE:鬱屈 += 10
ENDIF

CALL 特殊ソース処理_Ｖ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の指の汚れが移動
STAIN:膣内 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 1

IF !TALENT:処女 && !TALENT:再生処女
	SIF !Ｖ使用中(TARGET)
		CALL EXPUP, "Ｖ経験", 1
ENDIF

RETURN 1

