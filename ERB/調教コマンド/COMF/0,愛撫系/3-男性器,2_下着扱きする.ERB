;-------------------------------------------------
;下着扱きする
;-------------------------------------------------
@USERCOM_ABLE_下着扱きする, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;対象がペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0
;実行者がなんらかの下着を着ていないとダメ
コマンド実行条件 += "実行者が下着が設定されていないと実行できない/"
SIF CSTR:PLAYER:下半身下着 == ""
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_下着扱きする
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 増加量

PRINTL 下着扱きする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:PLAYER:下半身下着
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｐ責め(300)

;下着が濡れてると増加
SIF GET_STAIN(STAIN:PLAYER:下着, "愛液", "精液", "粘液", "尿")
	TIMES 増加量 , 1.20

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 200
SOURCE:達成 = 50
SOURCE:液体 = 50
SOURCE:露出 = 100
SOURCE:屈従 = 200
SOURCE:逸脱 = 300
SOURCE:反感 = 300
;下着の汚れで不潔ソース増加
SIF GET_STAIN(STAIN:PLAYER:下着, "愛液")
	SOURCE:不潔 += 30
SIF GET_STAIN(STAIN:PLAYER:下着, "先走り")
	SOURCE:不潔 += 60
SIF GET_STAIN(STAIN:PLAYER:下着, "精液")
	SOURCE:不潔 += 90
SIF GET_STAIN(STAIN:PLAYER:下着, "アナル")
	SOURCE:不潔 += 120
SIF GET_STAIN(STAIN:PLAYER:下着, "母乳")
	SOURCE:不潔 += 30
SIF GET_STAIN(STAIN:PLAYER:下着, "尿")
	SOURCE:不潔 += 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者の下着の汚れが移動
STAIN:ペニス |= STAIN:PLAYER:下着
STAIN:PLAYER:下着 |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

RETURN 1

