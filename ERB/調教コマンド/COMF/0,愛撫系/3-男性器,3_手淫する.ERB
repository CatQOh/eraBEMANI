;-------------------------------------------------
;手淫する
;-------------------------------------------------
@USERCOM_ABLE_手淫する, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;貞操帯装備中はダメ
SIF CFLAG:貞操帯
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_手淫する
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;前回のコマンド
	SELECTCASE PREVCOMS
		;後背位、騎乗位、後背位アナル、騎乗位アナルだとセックス愛撫系に派生
		CASE "後背位手淫", "騎乗位手淫", "後背位アナル手淫", "騎乗位アナル手淫"
			JUMPFORM COM_%PREVCOMS%
		CASE "後背位"
			SIF USERCOM_ABLE_後背位手淫()
				JUMP COM_後背位手淫
		CASE "騎乗位"
			SIF USERCOM_ABLE_騎乗位手淫()
				JUMP COM_騎乗位手淫
		CASE "後背位アナル"
			SIF USERCOM_ABLE_後背位アナル手淫()
				JUMP COM_後背位アナル手淫
		CASE "騎乗位アナル"
			SIF USERCOM_ABLE_騎乗位アナル手淫()
				JUMP COM_騎乗位アナル手淫
	ENDSELECT
ENDIF

PRINTL 手淫する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 250

;ABL:調教者の技巧をみる
LOCAL += ((LOCAL/10)*8)*ABL:PLAYER:技巧

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 200
SOURCE:達成 = 50
SOURCE:液体 = 50
SOURCE:露出 = 50
SOURCE:屈従 = 200
SOURCE:逸脱 = 200
SOURCE:反感 = 250
SOURCE:不潔 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者の手の汚れが移動
STAIN:ペニス |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

