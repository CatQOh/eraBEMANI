;-------------------------------------------------
;素股する
;-------------------------------------------------
@USERCOM_ABLE_素股する, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;実行者がペニスあるとダメ
SIF PENIS(PLAYER)
	RETURNF 0
;Ｐ使用中はダメ
SIF Ｐ使用中(TARGET)
	RETURNF 0
;PLAYERがＶ使用中はダメ
SIF Ｖ使用中(PLAYER)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_素股する
PRINTL 素股する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ｐ責め(200)

;素肌、下着なら汚れ次第で増加
IF 性器露出(PLAYER)
	SIF GET_STAIN(STAIN:PLAYER:ヴァギナ, "愛液", "精液")
		TIMES LOCAL , 1.50
ELSEIF (TEQUIP:PLAYER:下半身下着 && CFLAG:PLAYER:挿入可) || OVER_GENITAL(PLAYER) == CSTR:PLAYER:下半身下着
	SIF GET_STAIN(STAIN:PLAYER:下着, "愛液", "精液")
		TIMES LOCAL , 1.20
ENDIF

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 200
SOURCE:達成 = 100
SOURCE:液体 = 50
SOURCE:露出 = 50
SOURCE:屈従 = 100
SOURCE:逸脱 = 200
SOURCE:反感 = 200
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF (TEQUIP:PLAYER:下半身下着 && CFLAG:PLAYER:挿入可) || OVER_GENITAL(PLAYER) == CSTR:PLAYER:下半身下着
	;下着に擦りつけた場合は下着が汚れる
	STAIN:PLAYER:下着 |= STAIN:ペニス
	STAIN:ペニス |= STAIN:PLAYER:下着
ELSEIF OVER_GENITAL(TARGET) == ""
	;実行者のヴァギナ⇔対象のペニスの汚れが移動
	STAIN:PLAYER:ヴァギナ |= STAIN:ペニス
	STAIN:ペニス |= STAIN:PLAYER:ヴァギナ
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

SIF !避妊具(TARGET)
	CALL 魔力回復, 1, PLAYER

RETURN 1

