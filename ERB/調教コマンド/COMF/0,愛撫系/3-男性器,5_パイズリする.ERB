;-------------------------------------------------
;パイズリする
;-------------------------------------------------
@USERCOM_ABLE_パイズリする, ARG
#FUNCTION
;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;オナホール使用中はダメ
SIF TEQUIP:オナホール
	RETURNF 0

;パイズリ実行判定
SELECTCASE TALENT:PLAYER:バストサイズ
	;男or絶壁だとダメ
	CASE 0, 1
		RETURNF 0
	;貧乳なら技巧4以上必要
	CASE 2
		LOCAL = 4
	;普通なら技巧3以上必要
	CASE 3
		LOCAL = 3
	;巨乳なら技巧LV2以上必要
	CASE 4
		LOCAL = 2
	;爆乳なら技巧LV1以上必要
	CASE 5
		LOCAL = 1
ENDSELECT
SIF ABL:PLAYER:技巧 < LOCAL
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_パイズリする
PRINTL パイズリする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 400

;ABL:調教者の技巧をみる
LOCAL += ((LOCAL/10)*8)*ABL:PLAYER:技巧

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

;TALENT:バストサイズをみる
SELECTCASE TALENT:PLAYER:バストサイズ
	CASE 1
		TIMES LOCAL , 0.70
	CASE 2
		TIMES LOCAL , 0.85
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.15
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT
;対象が小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES LOCAL , 1.15

;機械・無機物の場合、ある程度減らす
IF CSTR:PLAYER:種族 == "機械" && TALENT:PLAYER:無機物
	TIMES LOCAL , 0.60
ELSEIF CSTR:PLAYER:種族 == "機械" || TALENT:PLAYER:無機物
	TIMES LOCAL , 0.80
ENDIF

;実行者が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES LOCAL , 1.25

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 300
SOURCE:達成 = 200
SOURCE:液体 = 50
SOURCE:露出 = 100
SOURCE:屈従 = 200
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;実行者の胸⇔対象のペニスの汚れが移動
STAIN:PLAYER:胸 |= STAIN:ペニス
STAIN:ペニス |= STAIN:PLAYER:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 2

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

