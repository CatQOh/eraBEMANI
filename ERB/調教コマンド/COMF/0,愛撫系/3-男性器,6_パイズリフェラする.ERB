;-------------------------------------------------
;パイズリフェラする
;-------------------------------------------------
@USERCOM_ABLE_パイズリフェラする, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;Ｐ使用中はダメ
SIF Ｐ使用中(TARGET)
	RETURNF 0

;パイズリ実行判定
SELECTCASE TALENT:PLAYER:バストサイズ
	;男or絶壁だとダメ
	CASE 0, 1
		RETURNF 0
	;貧乳なら技巧LV5以上必要
	CASE 2
		LOCAL = 5
	;普通なら技巧LV4以上必要
	CASE 3
		LOCAL = 4
	;巨乳なら技巧LV3以上必要
	CASE 4
		LOCAL = 3
	;爆乳なら技巧LV2以上必要
	CASE 5
		LOCAL = 2
ENDSELECT
SIF ABL:PLAYER:技巧 < LOCAL
	RETURNF 0

;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
IF TEQUIP:PLAYER:上半身上着 || (TEQUIP:PLAYER:全身上着 && CFLAG:PLAYER:着衣併用!=1)
	SELECTCASE CFLAG:PLAYER:上着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF
IF TEQUIP:PLAYER:上半身下着
	SELECTCASE CFLAG:PLAYER:下着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF

COMRESULT = 1
RETURNF 1

@COM_パイズリフェラする
PRINTL パイズリフェラする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 600

;ABL:調教者の技巧をみる
LOCAL += ((LOCAL/10)*8)*ABL:PLAYER:技巧

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

;TALENT:バストサイズをみる
SELECTCASE TALENT:PLAYER:バストサイズ
	CASE 1
		TIMES LOCAL , 0.70
	CASE 2
		TIMES LOCAL , 0.85
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.15
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT
;対象が小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES LOCAL , 1.15

;機械・無機物の場合、ある程度減らす
IF CSTR:PLAYER:種族 == "機械" && TALENT:PLAYER:無機物
	TIMES LOCAL , 0.60
ELSEIF CSTR:PLAYER:種族 == "機械" || TALENT:PLAYER:無機物
	TIMES LOCAL , 0.80
ENDIF

;実行者が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES LOCAL , 1.25

;TALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 500
SOURCE:達成 = 300
SOURCE:液体 = 50
SOURCE:露出 = 150
SOURCE:屈従 = 300
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;実行者の胸⇔対象のペニスの汚れが移動
STAIN:PLAYER:胸 |= STAIN:ペニス
STAIN:ペニス |= STAIN:PLAYER:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 3, 2

PRINTFORML フェラ経験+1(%CALLNAME:PLAYER%)
EXP:PLAYER:フェラ経験++

;魔力回復
IF TALENT:PLAYER:魔術師 && !TEQUIP:コンドーム && !TEQUIP:搾精コンドーム
	LOCAL = MAXBASE:PLAYER:魔力/200
	BASE:PLAYER:魔力 = LIMIT(BASE:PLAYER:魔力+LOCAL, 0, MAXBASE:PLAYER:魔力)
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;実績:山吹でフェラさせる。もしくは山吹でフェラをする
SIF CHARANAMEF(PLAYER) == "山吹"
	CALL 実績解除, "殿方の笛を鳴らすのもお得意で？"

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

