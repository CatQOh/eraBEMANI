;-------------------------------------------------
;アナル舐め
;-------------------------------------------------
@USERCOM_ABLE_アナル舐め, ARG
#FUNCTION
#DIM LCOUNT
;アナル舐め実行判定
;アナルが汚れている場合（先走り、精液、粘液、血、尿）
FOR LCOUNT, 0, 8
	IF GETBIT(STAIN:アナル, LCOUNT)
		SELECTCASE LCOUNT
			CASE 1, 2, 5, 6, 7
				;プレイヤーが助手の場合、汚臭敏感で従順3以下ではアナル舐めはできない（汚れ無視ならできる）
				SIF PLAYER == ASSI && ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
					RETURNF 0
				BREAK
		ENDSELECT
	ENDIF
NEXT
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;アナル仕様中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_アナル舐め
PRINTL アナル舐め
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 10

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧/5

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.40
	CASE 1
		TIMES LOCAL , 0.60
	CASE 2
		TIMES LOCAL , 0.60
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.20
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES LOCAL , 0.20
	CASE 1
		TIMES LOCAL , 0.50
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 2.00
	CASE 4
		TIMES LOCAL , 3.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ａ感覚-1)
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += LOCAL*ABL:マゾっ気/5

CALL 射精ゲージ増加, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 60

SOURCE:快A = 50
SOURCE:液体 = 50
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 50

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快A , 1.50
	
;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快A , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル汚れを舐め取る
STAIN:アナル = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
PRINTL Ａ経験+1
EXP:Ａ経験++

CALL EXPUP, 2, 1

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	PRINTFORML 嗜虐快楽経験(%CALLNAME:PLAYER%)+1
	EXP:PLAYER:嗜虐快楽経験++
ENDIF

RETURN 1

