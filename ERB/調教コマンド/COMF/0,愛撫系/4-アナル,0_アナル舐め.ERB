;-------------------------------------------------
;アナル舐め
;-------------------------------------------------
@USERCOM_ABLE_アナル舐め, ARG
#FUNCTION
#DIM DYNAMIC LCOUNT
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;アナルが汚れている場合（先走り、精液、粘液、血、尿）
コマンド実行条件 += "アナルが汚れている場合、従順LV3または汚れに敏感な助手は実行できない/"
FOR LCOUNT, 0, 8
	IF GETBIT(STAIN:アナル, LCOUNT)
		SELECTCASE LCOUNT
			CASE 1, 2, 5, 6, 7
				;プレイヤーが助手の場合、汚臭敏感で従順3以下ではアナル舐めはできない（汚れ無視ならできる）
				SIF PLAYER == ASSI && ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭敏感 && !TALENT:ASSI:汚れ無視
					COMRESULT = 0
				BREAK
		ENDSELECT
	ENDIF
NEXT
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;アナル仕様中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0

RETURNF COMRESULT

@COM_アナル舐め
#DIM DYNAMIC 増加量

PRINTL アナル舐め
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ責め(10)

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES 増加量 , 1.50
	
;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES 増加量 , 0.75

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 60

SOURCE:快A = 50
SOURCE:液体 = 50
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 50

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快A , 1.50
	
;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快A , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル汚れを舐め取る
STAIN:アナル = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1
CALL EXPUP, "Ａ経験", 1

CALL 魔力回復, 3, PLAYER
CALL 獣人催淫, 1, PLAYER

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1, PLAYER

RETURN 1

