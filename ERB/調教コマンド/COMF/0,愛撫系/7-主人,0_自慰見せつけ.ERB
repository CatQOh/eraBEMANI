;-------------------------------------------------
;自慰見せつけ
;-------------------------------------------------
@USERCOM_ABLE_自慰見せつけ, ARG
#FUNCTION
#DIM DYNAMIC 必要露出癖
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;助手だと露出癖2以上必要 恥じらいだと3以上
コマンド実行条件 += "実行者が助手の場合は露出癖LV2以上必要 [恥じらい]持ちだとLV3以上必要/"
IF PLAYER == ASSI
	必要露出癖 = 2
	SIF TALENT:ASSI:恥じらい
		必要露出癖++
	SIF ABL:ASSI:露出癖 < 必要露出癖
		COMRESULT = 0
ENDIF
;ペニスバンド装着中はダメ
コマンド実行条件 += "実行者がペニスバンド装着中は実行できない/"
SIF TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0
;Ｐ使用中はダメ
コマンド実行条件 += "実行者の男性器に道具使用中は実行できない/"
SIF Ｐ使用中(PLAYER)
	COMRESULT = 0

RETURNF COMRESULT

@COM_自慰見せつけ
#DIM DYNAMIC 増加量

PRINTL 自慰見せつけ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 200

;ABL:PLAYERの技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:性行動 = 40
SOURCE:欲情 = 20
SOURCE:恭順 = 20
SOURCE:露出 = 5
SOURCE:不潔 = 50
SOURCE:反感 = 30

;恋心に効果が高い
IF TALENT:恋心 == NO:PLAYER
	SOURCE:欲情 *= 2
	SOURCE:恭順 *= 2
	SOURCE:露出 *= 2
	SOURCE:不潔 /= 2
	SOURCE:反感 /= 2
;恥じらいにも効果的
ELSEIF TALENT:恥じらい
	SOURCE:露出 *= 5
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 0
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 0, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者の手⇔調教者のペニスの汚れが移動
STAIN:PLAYER:手 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:PLAYER:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

