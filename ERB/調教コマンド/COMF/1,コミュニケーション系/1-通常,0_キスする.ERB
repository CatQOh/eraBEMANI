;-------------------------------------------------
;キスする
;-------------------------------------------------
@USERCOM_ABLE_キスする, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_キスする
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_キス
実効値 += RESULT

;15以上で実行
CALL 実効値判定, 実効値, 15

RETURN RESULT

@COM_キスする
#DIM DYNAMIC 増加量

PRINTL キスする

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（実行者）
;-------------------------------------------------
増加量 = 射精ゲージ計算_キス実行者(20)

IF TALENT:PLAYER:淫乱
	増加量 *= 2
ELSEIF TALENT:PLAYER:娼婦
	増加量 *= 3
ENDIF

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_キス(20)

;TALENT:陥落系素質をみる
;はずだったけど、既に相性で計算してるから消してみた
;IF PLAYER == MASTER
;	SIF TALENT:淫乱 || TALENT:服従
;		TIMES 増加量 , 1.50
;	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
;		TIMES 増加量 , 2.00
;	SIF TALENT:親愛
;		TIMES 増加量 , 3.00
;ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 20

SOURCE:情愛 = 100
SOURCE:性行動 = 10
SOURCE:恭順 = 30
SOURCE:屈従 = 10
SOURCE:不潔 = 30
SOURCE:反感 = 10
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:情愛 , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:情愛 , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

;愛情経験
SIF FALLTYPE(TARGET) == 1 && !ASSIPLAY
	CALL EXPUP, "愛情経験", 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

;魔力回復 相性が良いと1.0%、そうでもないと0.5%回復
IF 相性値(TARGET, PLAYER) > 150
	増加量 = 10
ELSE
	増加量 = 5
ENDIF
CALL 魔力回復, 増加量
CALL 魔力回復, 増加量, PLAYER

CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

SIF CHARANAMEF(PLAYER) == "刃雪 達磨" && CHARANAMEF(TARGET) == "菱宮 津軽"
	CALL 実績解除, "「このスケべ！ 順序ってものが……もうっ！」"

RETURN 1

