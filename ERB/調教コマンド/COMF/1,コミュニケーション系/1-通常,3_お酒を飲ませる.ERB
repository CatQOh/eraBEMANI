;-------------------------------------------------
;お酒を飲ませる
;-------------------------------------------------
@USERCOM_ABLE_お酒を飲ませる
#DIM LCOUNT
;ボールギャグ付いてるとダメ
SIF TEQUIP:ボールギャグ
	RETURN 0
;縄もダメ
SIF TEQUIP:縄
	RETURN 0

;妊娠中は飲ませされない
SIF TALENT:妊娠
	RETURN 0

;機械と無機物は飲めない
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURN 0

;お酒持ってないとダメ
LOCAL = 0
FOR LCOUNT, 150, 200
	IF ITEM:LCOUNT > 0
		LOCAL = 1
		BREAK
	ENDIF
NEXT

RETURN LOCAL

@COM_お酒を飲ませる
#DIM DYNAMIC ALC
#DIM DYNAMIC お酒
#DIMS DYNAMIC 製法
#DIM DYNAMIC LCOUNT
PRINTL 何を飲ませますか？

SIF ITEM:スーパードライ || ITEM:一番搾り || ITEM:プレミアムモルツ || ITEM:ヱビス || ITEM:ほろよい
	PRINTL [1] 缶飲料類
SIF ITEM:皆伝
	PRINTL [2] 皆伝(日本酒)
SIF ITEM:アルパカ
	PRINTL [3] アルパカ(ワイン)
SIF ITEM:魔王
	PRINTL [4] 魔王(焼酎)
SIF ITEM:倉吉
	PRINTL [5] 倉吉(ウイスキー)
SIF ITEM:スペリオール
	PRINTL [6] スペリオール(ラム)
SIF ITEM:ボンベイサファイア
	PRINTL [7] ボンベイサファイア(ジン)
SIF ITEM:アブソルート
	PRINTL [8] アブソルート(ウォッカ)
SIF ITEM:カルーア
	PRINTL [9] カルーア
SIF ITEM:スピリタス
	PRINTL [10] スピリタス
PRINTL [100] やっぱりやめる

DO
	INPUT
	SELECTCASE RESULT
		CASE 1
			SIF !ITEM:スーパードライ && !ITEM:一番搾り && !ITEM:プレミアムモルツ && !ITEM:ヱビス && !ITEM:ほろよい
				CONTINUE
			PRINTL 何を飲ませますか？
			SIF ITEM:スーパードライ
				PRINTL [0] スーパードライ(Alc.5%)
			SIF ITEM:一番搾り
				PRINTL [1] 一番搾り(Alc.5%)
			SIF ITEM:プレミアムモルツ
				PRINTL [2] プレミアムモルツ(Alc.5%)
			SIF ITEM:ヱビス
				PRINTL [3] ヱビス(Alc.5%)
			SIF ITEM:ほろよい
				PRINTL [4] ほろよい(Alc.3%)
			SIF ITEM:ストロングゼロ
				PRINTL [5] ストロングゼロ(Alc.9%)
			DO
				INPUT
				SIF !ITEM:(RESULT+150)
					CONTINUE
				SELECTCASE RESULT
					CASE 0 TO 3
						お酒 = RESULT+150
						IF ITEM:ジンジャーエール || ITEM:レモンジュース || ITEM:アルパカ
							PRINTFORML %ITEMNAME:お酒%をどのように飲ませますか？
							PRINTL [1] そのまま(Alc.5%)
							SIF ITEM:ジンジャーエール
								PRINTL [2] シャンディガフ(Alc.3%)
							SIF ITEM:レモンジュース
								PRINTL [3] パナシェ(Alc.3%)
							SIF ITEM:アルパカ
								PRINTL [4] ビアスプリッツァー(Alc.8%)
							DO
								INPUT
								SELECTCASE RESULT
									CASE 1
										酒名 = ITEMNAME:(RESULT+150)
										製法 = ビール
										ALC = 5
										BREAK
									CASE 2
										SIF !ITEM:ジンジャーエール
											CONTINUE
										製法 = ジンジャー割り
										酒名 = シャンディガフ
										ALC = 3
										BREAK
									CASE 3
										SIF !ITEM:レモンジュース
											CONTINUE
										製法 = レモン割り
										酒名 = パナシェ
										ALC = 3
										BREAK
									CASE 4
										SIF !ITEM:アルパカ
											CONTINUE
										製法 = カクテル
										酒名 = ビアスプリッツァー
										ALC = 8
										BREAK
								ENDSELECT
							LOOP 1
						ELSE
							酒名 = ITEMNAME:(RESULT+150)
							製法 = ビール
							ALC = 5
						ENDIF
						BREAK
					CASE 4
						お酒 = RESULT+150
						酒名 = ほろよい
						ALC = 3
						BREAK
					CASE 5
						お酒 = RESULT+150
						酒名 = ストロングゼロ
						ALC = 9
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 2
			SIF !ITEM:皆伝
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "皆伝"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			IF ITEM:ライムジュース
				PRINTL 皆伝をどのように飲ませますか？
				PRINTL [1] そのまま(Alc.12%)
				PRINTL [2] サムライロック(Alc.3～10%)
				DO
					INPUT
					SELECTCASE RESULT
						CASE 1
							酒名 = 皆伝
							ALC = 12
							BREAK
						CASE 2
							SIF !ITEM:ライムジュース
								CONTINUE
							製法 = ライム割り
							酒名 = サムライロック
							PRINTL アルコール度数を決めてください(3～10%)
							CALL TWO_DIGIT, 10, "%", 3
							ALC = RESULT
							BREAK
					ENDSELECT
				LOOP 1
			ELSE
				酒名 = 皆伝
				ALC = 12
			ENDIF
			BREAK
		CASE 3
			SIF !ITEM:アルパカ
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "アルパカ"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			酒名 = アルパカ
			ALC = 12
			BREAK
		CASE 4
			SIF !ITEM:魔王
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "魔王"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			PRINTL 魔王をどのように飲ませますか？
			PRINTL [1] 水割り(Alc.5～20%)
			PRINTL [2] お湯割り(Alc.5～20%)
			PRINTL [3] ロック(Alc.22%)
			PRINTL [4] ストレート(Alc.25%)
			SIF ITEM:炭酸水
				PRINTL [5] 焼酎ハイボール(Alc.5～20%)
			SIF ITEM:コーラ
				PRINTL [6] 焼酎コーク
			SIF ITEM:ジンジャーエール
				PRINTL [7] 焼酎ジンジャー
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1, 2, 5, 6, 7
						SELECTCASE RESULT
							CASE 1
								製法 = 水割り
								酒名 = 焼酎の水割り
							CASE 2
								製法 = お湯割り
								酒名 = 焼酎のお湯割り
							CASE 5
								SIF !ITEM:炭酸水
									CONTINUE
								製法 = ハイボール
								酒名 = 焼酎ハイボール
							CASE 6
								SIF !ITEM:コーラ
									CONTINUE
								製法 = コーラ割り
								酒名 = 焼酎コーク
							CASE 7
								SIF !ITEM:ジンジャーエール
									CONTINUE
								製法 = ジンジャー割り
								酒名 = 焼酎ジンジャー
						ENDSELECT
						PRINTL アルコール度数を決めてください(5～20%)
						CALL TWO_DIGIT, 20, "%", 5
						ALC = RESULT
						BREAK
					CASE 3
						酒名 = 焼酎のロック
						ALC = 22
						BREAK
					CASE 4
						酒名 = 焼酎のストレート
						ALC = 25
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 5
			SIF !ITEM:倉吉
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "倉吉"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			PRINTL 倉吉をどのように飲ませますか？
			PRINTL [1] 水割り(Alc.9～35%)
			PRINTL [2] お湯割り(Alc.9～35%)
			PRINTL [3] ロック(Alc.37%)
			PRINTL [4] ストレート(Alc.40%)
			SIF ITEM:炭酸水
				PRINTL [5] ハイボール(Alc.9～35%)
			SIF ITEM:コーラ
				PRINTL [6] コークハイ(Alc.9～35%)
			SIF ITEM:ジンジャーエール
				PRINTL [7] ジンジャーハイ(Alc.9～35%)
				
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1, 2, 5, 6, 7
						SELECTCASE RESULT
							CASE 1
								製法 = 水割り
								酒名 = ウイスキーの水割り
							CASE 2
								製法 = お湯割り
								酒名 = ウイスキーのお湯割り
							CASE 5
								SIF !ITEM:炭酸水
									CONTINUE
								製法 = ハイボール
								酒名 = ハイボール
							CASE 6
								SIF !ITEM:コーラ
									CONTINUE
								製法 = コーラ割り
								酒名 = コークハイ
							CASE 7
								SIF !ITEM:ジンジャーエール
									CONTINUE
								製法 = ジンジャー割り
								酒名 = ジンジャーハイ
						ENDSELECT
						PRINTL アルコール度数を決めてください(9～35%)
						CALL TWO_DIGIT, 35, "%", 9
						ALC = RESULT
						BREAK
					CASE 3
						酒名 = 倉吉のロック
						ALC = 37
						BREAK
					CASE 4
						酒名 = 倉吉のストレート
						ALC = 40
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 6
			SIF !ITEM:スペリオール
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "スペリオール"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			PRINTL スペリオールをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.40%)
			PRINTL [2] ロック(Alc.37%)
			SIF ITEM:コーラ
				PRINTL [3] キューバリバー(Alc.9～35%)
			SIF ITEM:ライムジュース
				PRINTL [4] ダイキリ(Alc.20～35%)
			SIF ITEM:トニックウォーター
				PRINTL [5] ラムトニック(Alc.9～35%)
			SIF ITEM:レモンジュース && ITEM:炭酸水
				PRINTL [6] ラムフィズ(Alc.9～35%)
			SIF ITEM:ライムジュース && ITEM:炭酸水
				PRINTL [7] モヒート(Alc.9～35%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = スペリオールのストレート
						ALC = 40
						BREAK
					CASE 2
						酒名 = スペリオールのロック
						ALC = 37
						BREAK
					CASE 3, 4, 5, 6, 7
						SELECTCASE RESULT
							CASE 3
								SIF !ITEM:コーラ
									CONTINUE
								製法 = コーラ割り
								酒名 = キューバリバー
							CASE 4
								SIF !ITEM:ライムジュース
									CONTINUE
								製法 = ライム割り
								酒名 = ダイキリ
							CASE 5
								SIF !ITEM:トニックウォーター
									CONTINUE
								製法 = トニック割り
								酒名 = ラムトニック
							CASE 6
								SIF !ITEM:レモンジュース || !ITEM:炭酸水
									CONTINUE
								製法 = カクテル
								酒名 = ラムフィズ
							CASE 7
								SIF !ITEM:ライムジュース || !ITEM:炭酸水
									CONTINUE
								製法 = カクテル
								酒名 = モヒート
						ENDSELECT
						IF 酒名 == "ダイキリ"
							PRINTL アルコール度数を決めてください(20～35%)
							CALL TWO_DIGIT, 35, "%", 20
						ELSE
							PRINTL アルコール度数を決めてください(9～35%)
							CALL TWO_DIGIT, 35, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 7
			SIF !ITEM:ボンベイサファイア
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "ボンベイサファイア"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			PRINTL ボンベイサファイアをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.47%)
			PRINTL [2] ロック(Alc.44%)
			SIF ITEM:トニックウォーター
				PRINTL [3] ジントニック(Alc.9～42%)
			SIF ITEM:炭酸水
				PRINTL [4] ジンリッキー(Alc.9～42%)
			SIF ITEM:ライムジュース
				PRINTL [5] ギムレット(Alc.25～42%)
			SIF ITEM:レモンジュース && ITEM:炭酸水
				PRINTL [6] ジンフィズ(Alc.9～42%)
			SIF ITEM:レモンジュース && ITEM:ジンジャーエール
				PRINTL [7] ジンバック(Alc.9～42%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = ボンベイサファイアのストレート
						ALC = 47
						BREAK
					CASE 2
						酒名 = ボンベイサファイアのロック
						ALC = 44
						BREAK
					CASE 3, 4, 5, 6, 7
						SELECTCASE RESULT
							CASE 3
								SIF !ITEM:トニックウォーター
									CONTINUE
								製法 = トニック割り
								酒名 = ジントニック
							CASE 4
								SIF !ITEM:炭酸水
									CONTINUE
								製法 = ハイボール
								酒名 = ジンリッキー
							CASE 5
								SIF !ITEM:ライムジュース
									CONTINUE
								製法 = ライム割り
								酒名 = ギムレット
							CASE 6
								SIF !ITEM:レモンジュース || !ITEM:炭酸水
									CONTINUE
								製法 = カクテル
								酒名 = ジンフィズ
							CASE 7
								SIF !ITEM:レモンジュース || !ITEM:ジンジャーエール
									CONTINUE
								製法 = カクテル
								酒名 = ジンバック
						ENDSELECT
						IF 酒名 == "ギムレット"
							PRINTL アルコール度数を決めてください(25～42%)
							CALL TWO_DIGIT, 42, "%", 25
						ELSE
							PRINTL アルコール度数を決めてください(9～42%)
							CALL TWO_DIGIT, 42, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 8
			SIF !ITEM:アブソルート
				CONTINUE
			FOR LCOUNT, 150, 200
				IF ITEMNAME:LCOUNT == "アブソルート"
					お酒 = LCOUNT
					BREAK
				ENDIF
			NEXT
			PRINTL アブソルートをどのように飲ませますか？
			PRINTL [1] ストレート(Alc.40%)
			PRINTL [2] ロック(Alc.37%)
			SIF ITEM:トニックウォーター
				PRINTL [3] ウォッカトニック(Alc.9～35%)
			SIF ITEM:炭酸水
				PRINTL [4] ウォッカリッキー(Alc.9～35%)
			SIF ITEM:カルーア
				PRINTL [5] ブラックルシアン(Alc.25～37%)
			SIF ITEM:ライムジュース && ITEM:ジンジャーエール
				PRINTL [6] モスコミュール(Alc.9～35%)
			DO
				INPUT
				SELECTCASE RESULT
					CASE 1
						酒名 = アブソルートのストレート
						ALC = 40
						BREAK
					CASE 2
						酒名 = アブソルートのロック
						ALC = 37
						BREAK
					CASE 3, 4, 5, 6, 7
						SELECTCASE RESULT
							CASE 3
								SIF !ITEM:トニックウォーター
									CONTINUE
								製法 = トニック割り
								酒名 = ウォッカトニック
							CASE 4
								SIF !ITEM:炭酸水
									CONTINUE
								製法 = ハイボール
								酒名 = ウォッカリッキー
							CASE 5
								SIF !ITEM:カルーア
									CONTINUE
								製法 = カクテル
								酒名 = ブラックルシアン
							CASE 6
								SIF !ITEM:ライムジュース || !ITEM:ジンジャーエール
									CONTINUE
								製法 = カクテル
								酒名 = モスコミュール
						ENDSELECT
						IF 酒名 == "ブラックルシアン"
							PRINTL アルコール度数を決めてください(25～37%)
							CALL TWO_DIGIT, 37, "%", 25
						ELSE
							PRINTL アルコール度数を決めてください(9～35%)
							CALL TWO_DIGIT, 35, "%", 9
						ENDIF
						ALC = RESULT
						BREAK
				ENDSELECT
			LOOP 1
			BREAK
		CASE 100
			PRINTFORMW お酒を飲ませるのを止めました
			RETURN 0
	ENDSELECT
LOOP 1

CALL ALC_ORDER, ALC, 製法

PRINTFORM  = {RESULT}
SELECTCASE RESULT
	CASE IS < 0
		PRINT  < 
	CASE IS > 0
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINT 実効値0
WAIT

TFLAG:お酒 = お酒
TFLAG:アルコール度数 = ALC

;実行できない お酒は特殊の地の文
IF RESULT < 0
	CALL KOJO_MESSAGE_EVENT, 101
	RETURN 0
ELSE
	ITEM:お酒--
	SIF 製法 == "ハイボール"
		ITEM:炭酸水--
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

SOURCE:中毒 = 500
SOURCE:露出 = 50
SOURCE:欲情 = 100
SOURCE:恭順 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
IF TALENT:下戸
	CFLAG:酔 += ALC*2
ELSEIF TALENT:酒豪
	CFLAG:酔 += ALC/2
ELSE
	CFLAG:酔 += ALC
ENDIF

SIF CFLAG:酔 > 100
	CFLAG:酔 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;お酒を飲ませるコマンドのCOM_ORDERは特殊
;-------------------------------------------------
@ALC_ORDER, ALC, 製法
#DIM DYNAMIC ALC
#DIMS DYNAMIC 製法
#DIM DYNAMIC LCOUNT

LOCAL = 0

IF TALENT:下戸
	PRINTFORM -アルコール度数{ALC}\%({ALC*2})
	LOCAL -= ALC*2
ELSEIF !TALENT:酒豪
	PRINTFORM -アルコール度数{ALC}\%({ALC})
	LOCAL -= ALC
ENDIF

LOCAL:1 = 0

IF TALENT:快感に素直
	LOCAL:1 = CFLAG:酔*3/2
ELSEIF TALENT:快感の否定
	LOCAL:1 = CFLAG:酔/2
ELSE
	LOCAL:1 = CFLAG:酔
ENDIF

IF LOCAL:1 >= 5 && TALENT:下戸
	PRINTFORM +酒が回っている({LOCAL:1/5})
	LOCAL += LOCAL:1/5
ELSEIF LOCAL:1 >= 3
	PRINTFORM +酒が回っている({LOCAL:1/3})
	LOCAL += LOCAL:1/3
ENDIF

IF CFLAG:二日酔い
	PRINT -二日酔い(5)
	LOCAL -= 5
ENDIF

IF CFLAG:陥落
	PRINT +陥落(5)
	LOCAL += 5
ENDIF

LOCAL:2 = 0
FOR LCOUNT, 1, 10
	SIF PALAMLV:LCOUNT <= PALAM:恭順
		LOCAL:2 = LCOUNT
NEXT

IF LOCAL:2
	PRINTFORM +恭順({LOCAL:2})
	LOCAL += LOCAL:2
ENDIF

IF 製法 == "ビール" && !TALENT:酒豪 && (TALENT:小柄体型 || TALENT:下戸)
	PRINT -ビールは苦くて無理(10)
	LOCAL -= 10
ENDIF

SELECTCASE 製法
	CASE "水割り", "ハイボール"
		IF FLAG:気温 >= 200
			PRINTFORM +高気温&冷飲料({(FLAG:気温-190)/10})
			LOCAL += (FLAG:気温-190)/10
		ENDIF
	CASE "お湯割り"
		IF FLAG:気温 <= 100
			PRINTFORM +低気温&温飲料({(110-FLAG:気温)/10})
			LOCAL += (110-FLAG:気温)/10
		ENDIF
ENDSELECT

IF 製法 == "ハイボール"
	PRINT +ハイボールは美味しい(5)
	LOCAL += 5
ENDIF

IF TALENT:衝動的
	PRINT +衝動的(3)
	LOCAL += 3
ENDIF

IF TALENT:好奇心
	PRINT +好奇心(3)
	LOCAL += 3
ENDIF

IF TALENT:無関心
	PRINT -無関心(2)
	LOCAL -= 2
ENDIF

IF TALENT:目立ちたがり
	PRINT +目立ちたがり(3)
	LOCAL += 3
ENDIF

IF 製法 == "お湯割り" && TALENT:猫舌
	PRINT -猫舌(3)
	LOCAL -= 2
ENDIF

IF TALENT:小柄体型 && !TALENT:酒豪
	PRINT -子供(3)
	LOCAL -= 3
ENDIF

RETURN LOCAL

