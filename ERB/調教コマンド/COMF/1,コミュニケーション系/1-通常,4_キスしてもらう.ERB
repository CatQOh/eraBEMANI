;-------------------------------------------------
;キスしてもらう
;-------------------------------------------------
@USERCOM_ABLE_キスしてもらう, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;拘束中はダメ
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	RETURNF 0
;猫舌の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_キスしてもらう
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
;アニマルは無条件で可
SIF TALENT:アニマル
	RETURN 1

CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_キス
LOCAL += RESULT

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	PRINTFORM -恥じらい(3)
ENDIF

;25以上で実行
CALL 実効値判定, LOCAL, 25

RETURN RESULT

@COM_キスしてもらう
PRINTL キスしてもらう

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（実行者）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_キス実行者(20)

IF TALENT:PLAYER:淫乱
	LOCAL *= 2
ELSEIF TALENT:PLAYER:娼婦
	LOCAL *= 3
ENDIF

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_キス(20)

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES LOCAL , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES LOCAL , 2.00
	SIF TALENT:親愛
		TIMES LOCAL , 3.00
ENDIF

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

SOURCE:情愛 = 200
SOURCE:性行動 = 10
SOURCE:達成 = 30
SOURCE:恭順 = 30
SOURCE:屈従 = 30
SOURCE:不潔 = 30
SOURCE:反感 = 10
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:情愛 , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:情愛 , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

;愛情経験
SIF FALLTYPE(TARGET) == 1 && !ASSIPLAY
	CALL EXPUP, "愛情経験", 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

;魔力回復 相性良いと1.0%、そうでもないと0.5%
IF RELATION:(NO:MASTER) > 150
	LOCAL = 10
ELSE
	LOCAL = 5
ENDIF
CALL 魔力回復, LOCAL
CALL 魔力回復, LOCAL, PLAYER

CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

RETURN 1

