;-------------------------------------------------
;キスしてもらう
;-------------------------------------------------
@USERCOM_ABLE_キスしてもらう, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;拘束中はダメ
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	RETURNF 0
;猫舌の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_キスしてもらう
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF

IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

;PALAM:欲情
LOCAL:1 = GETPALAMLV(PALAM:欲情, 5)
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	TIMES LOCAL:2 , 1.50
IF LOCAL:2
	LOCAL += LOCAL:2*3
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2*3})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	PRINTFORM -恥じらい(3)
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	LOCAL += 1
	PRINTFORM +汚臭鈍感(1)
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	LOCAL -= 1
	PRINTFORM -汚臭敏感(1)
ENDIF
;献身的
IF TALENT:献身的
	LOCAL += 3
	PRINTFORM +献身的(3)
ENDIF
;快感の否定 淫乱or娼婦で無効
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	LOCAL -= 2
	PRINTFORM -快感の否定(2)
ENDIF
;恋慕
IF TALENT:恋慕 && !ASSIPLAY
	LOCAL += 10
	PRINTFORM +恋慕(10)
ENDIF
;親愛
IF TALENT:親愛 && !ASSIPLAY
	LOCAL += 15
	PRINTFORM +親愛(15)
ENDIF

;合計を表示(25以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 25
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}

WAIT

;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_キスしてもらう
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF TFLAG:前コマンド実行者 == PLAYER
	;前回のコマンドがセックス系だったら対応するキスセックス派生に
	SELECTCASE PREVCOMS
		CASE "正常位"
			SIF USERCOM_ABLE_正常位キス()
				JUMP COM_正常位キス
		CASE "騎乗位"
			SIF USERCOM_ABLE_騎乗位キス()
				JUMP COM_騎乗位キス
		CASE "対面座位"
			SIF USERCOM_ABLE_対面座位キス()
				JUMP COM_対面座位キス
		CASE "正常位アナル"
			SIF USERCOM_ABLE_正常位アナルキス()
				JUMP COM_正常位アナルキス
		CASE "騎乗位アナル"
			SIF USERCOM_ABLE_騎乗位アナルキス()
				JUMP COM_騎乗位アナルキス
		CASE "対面座位アナル"
			SIF USERCOM_ABLE_対面座位アナルキス()
				JUMP COM_対面座位アナルキス
	ENDSELECT
ENDIF

PRINTL キスしてもらう

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（実行者）
;-------------------------------------------------
LOCAL = 20

;ABL:技巧をみる
LOCAL += LOCAL*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:奉仕精神
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;相性を見る
LOCAL *= RELATION:PLAYER:(NO:TARGET)
LOCAL /= 100

IF TALENT:PLAYER:淫乱
	LOCAL *= 2
ELSEIF TALENT:PLAYER:娼婦
	LOCAL += 3
ENDIF

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 20

;依存度を見る
LOCAL += CFLAG:依存度/10

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:奉仕精神
ENDSELECT

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 2.00

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES LOCAL , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES LOCAL , 2.00
	SIF TALENT:親愛
		TIMES LOCAL , 3.00
ENDIF

;相性を見る
LOCAL *= RELATION:TARGET:(NO:PLAYER)
LOCAL /= 100

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

SOURCE:情愛 = 200
SOURCE:性行動 = 10
SOURCE:達成 = 30
SOURCE:恭順 = 30
SOURCE:屈従 = 30
SOURCE:不潔 = 30
SOURCE:反感 = 10

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:情愛 , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:情愛 , 0.75

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 1

;愛情経験
IF FALLTYPE(TARGET) == 1 && !ASSIPLAY
	PRINTL 愛情経験+1
	EXP:愛情経験++
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛 && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

SELECTCASE CSTR:PLAYER:種族
	CASE "獣人", "獣"
		CFLAG:媚薬++
		SIF CFLAG:媚薬%5 == 0
			CFLAG:残数++
ENDSELECT

RETURN 1

