@USERCOM_ABLE_魔法詠唱, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中も不可能（暫定）
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;PLAYERが魔術師じゃないとできない
コマンド実行条件 += "実行者が[魔術師]じゃないと実行できない/"
SIF !TALENT:PLAYER:魔術師
	COMRESULT = 0

RETURNF COMRESULT

@COM_魔法詠唱
#DIM DYNAMIC スキル
#DIM DYNAMIC 消費量

スキル = ABL:PLAYER:魔法技能
DRAWLINE
PRINTFORML □魔法一覧 詠唱者:%NAME:PLAYER% 魔法技能LV{スキル}
PRINTFORML 　%NAME:PLAYER%の魔力%BARSTR(BASE:PLAYER:魔力, MAXBASE:PLAYER:魔力, 32)%({BASE:PLAYER:魔力}/{MAXBASE:PLAYER:魔力})
IF TALENT:魔術師
	SIF 相性値(PLAYER, TARGET) >= 150 && 相性値(TARGET, PLAYER) >= 150
		PRINTFORML 　[ 1] - 魔力吸収 唇を介して魔力を受け取る 相性が良くないと実行不可（150％）
	PRINTFORML 　[ 2] - 魔力譲与 唇を介して魔力を与える 相性が良いほど効率が良い（最高200％）
ENDIF
消費量 = 魔力消費量(3, スキル)/3
;Heatstroke
PRINTFORML 　[ 3] - ヒートストローク（魔力消費量 {消費量}）
PRINTL 　　　　 気温が高いほど実効値上昇
;WHITE OUT
PRINTFORML 　[ 4] - ホワイトアウト（魔力消費量 {消費量}）
PRINTL 　　　　 気温が低いほど実効値上昇
IF スキル >= 4
	消費量 = 魔力消費量(4, スキル)/3
	;リカーシブファンクション
	PRINTFORML 　[ 5] - リカーシブファンクション（魔力消費量 {消費量}）
	PRINTL 　　　　 コマンド連続実行によるデメリットが発生しなくなる
	;魚氷に上り 耀よひて
	PRINTFORML 　[ 6] - ニコニコサンフラワーキッス（魔力消費量 {消費量}）
	PRINTL 　　　　 キス系コマンドに恐怖が伴うようになる
ENDIF
IF スキル >= 5
	消費量 = 魔力消費量(5, スキル)/3
	;METALLIC MIND
	PRINTFORML 　[ 7] - メタリックマインド（魔力消費量 {消費量}）
	PRINTL 　　　　 実効値が低下する代わりにマイナスソースを軽減する
	;Go Berzerk
	PRINTFORML 　[ 8] - ゴー・バーサーク（魔力消費量 {消費量}）
	PRINTL 　　　　 欲情、恭順が上がりやすくなり、気力0でのソース低下が発生しなくなるが、体力消費量が増える
ENDIF
IF スキル >= 6
	消費量 = 魔力消費量(6, スキル)/3
	;Honey Party
	PRINTFORML 　[ 9] - ハニーパーティ（魔力消費量 {消費量}）
	PRINTL 　　　　 浴槽が媚薬ローション風呂になる
	;LUV CAN SAVE YOU
	IF TFLAG:ラブキャンセーブユー == 2
		PRINTL 　[10] - ラブキャンセーブユー解除
		PRINTL 　　　　 当魔法の効果を解除する
	ELSEIF TFLAG:ラブキャンセーブユー == 0
		PRINTFORML 　[10] - ラブキャンセーブユー（魔力消費量 {消費量}）
		PRINTL 　　　　 対象が絶頂に達しなくなる
	ENDIF
ENDIF
消費量 = 魔力消費量(7, スキル)/3
IF スキル >= 7
	;Chain of Pain
	PRINTFORML 　[11] - チェイン・オブ・ペイン（魔力消費量 {消費量}）
	PRINTL 　　　　 体力減少が苦痛ソースに上乗せされる
ENDIF
消費量 = 魔力消費量(8, スキル)/3
IF スキル >= 8
	;DEADLOCK
	PRINTFORML 　[12] - デッドロック（魔力消費量 {消費量}）
	PRINTL 　　　　 3ターン、実行判定を無視する
ENDIF
消費量 = 5000
IF スキル >= 10
	;終極+焔獄OVERKILL
	PRINTFORML 　[13] - 終極オーバーキル（魔力消費量 {消費量}）
	PRINTL 　　　　 常軌を逸した恐怖、苦痛、不快を与え、即時調教を終了する 対象は30ターン使用不可になる
ENDIF
DRAWLINE
PRINTPLAIN 　魔法詠唱　
PRINTL [ 0] - 戻る

DO
	SIF ISSKIP()
		BREAK
	INPUT
	SELECTCASE RESULT
		CASE 0
			RETURN 0
		CASE 1
			SIF !TALENT:魔術師
				CONTINUE
			SIF 相性値(PLAYER, TARGET) < 150 || 相性値(TARGET, PLAYER) < 150
				CONTINUE
			詠唱魔法 = 魔力吸収
			CALL KOJO_MESSAGE_COM
			;対象の魔力半減
			PRINTFORML %CALLNAME:TARGET%の魔力-{BASE:魔力-BASE:魔力/2}
			消費量 = BASE:魔力-BASE:魔力/2
			BASE:魔力 /= 2
			;PLAYERの魔力は相性に応じて貰った分だけ増加 200%でそのまま加算
			消費量 = 消費量*(LIMIT(相性値(TARGET, PLAYER)-100, 0, 100))/100
			PRINTFORML %CALLNAME:PLAYER%の魔力+{消費量}
			BASE:PLAYER:魔力 += 消費量
			CALL SETBASE, PLAYER
			LOSEBASE:体力 += 5
			LOSEBASE:気力 += 50
			BREAK
		CASE 2
			SIF !TALENT:魔術師
				CONTINUE
			詠唱魔法 = 魔力譲与
			CALL KOJO_MESSAGE_COM
			;PLAYERの魔力半減
			PRINTFORML %CALLNAME:PLAYER%の魔力-{BASE:PLAYER:魔力-BASE:PLAYER:魔力/2}
			消費量 = BASE:PLAYER:魔力-BASE:PLAYER:魔力/2
			BASE:PLAYER:魔力 /= 2
			;魔力譲与は苦痛が伴う 相性によって効率が上がり、苦痛も軽減される
			;効率=相性-100%
			消費量 = 消費量*(LIMIT(相性値(TARGET, PLAYER)-100, 0, 100))/100
			PRINTFORML %CALLNAME:TARGET%の魔力+{消費量}
			BASE:魔力 += 消費量
			CALL SETBASE, PLAYER
			;相性が100%以下だと最大体力の半分のダメージを受ける
			LOSEBASE:体力 += MAXBASE:体力/2/LIMIT(相性値(TARGET, PLAYER)-100, 1, 100)
			BREAK
		CASE 3
			消費量 = 魔力消費量(3, スキル)/3
			SIF スキル < 3
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = ヒートストローク
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 3, PLAYER
			TFLAG:ヒートストローク = 1
			BREAK
		CASE 4
			消費量 = 魔力消費量(3, スキル)/3
			SIF スキル < 3
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = ホワイトアウト
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 3, PLAYER
			TFLAG:ホワイトアウト = 1
			BREAK
		CASE 5
			消費量 = 魔力消費量(4, スキル)/3
			SIF スキル < 4
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = リカーシブファンクション
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 4, PLAYER
			TFLAG:リカーシブファンクション = 1
			BREAK
		CASE 6
			消費量 = 魔力消費量(4, スキル)/3
			SIF スキル < 4
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = ニコニコサンフラワーキッス
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 4, PLAYER
			TFLAG:ニコニコサンフラワーキッス = 1
			BREAK
		CASE 7
			消費量 = 魔力消費量(5, スキル)/3
			SIF スキル < 5
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = メタリックマインド
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 5, PLAYER
			TFLAG:メタリックマインド = 1
			BREAK
		CASE 8
			消費量 = 魔力消費量(5, スキル)/3
			SIF スキル < 5
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = ゴー・バーサーク
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 5, PLAYER
			TFLAG:ゴーバーサーク = 1
			BREAK
		CASE 9
			消費量 = 魔力消費量(6, スキル)/3
			SIF スキル < 6
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = ハニーパーティ
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 6, PLAYER
			TFLAG:ハニーパーティ = 1
			BREAK
		CASE 10
			IF !TFLAG:ラブキャンセーブユー
				消費量 = 魔力消費量(6, スキル)/3
				SIF スキル < 6
					CONTINUE
				SIF BASE:PLAYER:魔力 < 消費量
					CONTINUE
			ENDIF
			詠唱魔法 = ラブキャンセーブユー
			CALL KOJO_MESSAGE_COM
			IF !TFLAG:ラブキャンセーブユー
				PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
				BASE:PLAYER:魔力 -= 消費量
				CALL EXPUP, "詠唱経験", 6, PLAYER
				TFLAG:ラブキャンセーブユー = 2
			ELSE
				TFLAG:ラブキャンセーブユー = 1
			ENDIF
			BREAK
		CASE 11
			消費量 = 魔力消費量(7, スキル)/3
			SIF スキル < 7
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = チェイン・オブ・ペイン
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 7, PLAYER
			TFLAG:チェインオブペイン = 1
			BREAK
		CASE 12
			消費量 = 魔力消費量(8, スキル)/3
			SIF スキル < 8
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = デッドロック
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 8, PLAYER
			;このターンで-1されるので4
			TFLAG:実行判定無視 = 4
			BREAK
		CASE 13
			消費量 = 5000
			SIF スキル < 10
				CONTINUE
			SIF BASE:PLAYER:魔力 < 消費量
				CONTINUE
			詠唱魔法 = 終極オーバーキル
			CALL KOJO_MESSAGE_COM
			PRINTFORML %CALLNAME:PLAYER%の魔力-{消費量}
			BASE:PLAYER:魔力 -= 消費量
			CALL EXPUP, "詠唱経験", 10, PLAYER
			LOSEBASE:体力 = BASE:体力
			LOSEBASE:気力 = BASE:気力
			UP:恐怖 = RAND(200, 300)*RAND(2000, 3000)
			UP:苦痛 = RAND(200, 300)*RAND(2000, 3000)
			UP:不快 = RAND(200, 300)*RAND(2000, 3000)
			CFLAG:使用不可 = 31
			BREAK
	ENDSELECT
LOOP 1

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1
