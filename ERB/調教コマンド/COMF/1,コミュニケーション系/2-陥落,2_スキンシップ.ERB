;-------------------------------------------------
;スキンシップ
;-------------------------------------------------
@USERCOM_ABLE_スキンシップ, ARG
#FUNCTION
;奴隷が恋慕か親愛で実行者が主人でないとダメ
コマンド実行条件 += "[恋慕][親愛]かつ実行者が主人じゃないと実行できない/"
SIF FALLTYPE(TARGET) != 1 || PLAYER != MASTER
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0

RETURNF COMRESULT

@COM_スキンシップ
#DIM DYNAMIC 増加量

PRINTL スキンシップ
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
;何かのはずみで触れてしまったら
IF RAND:2 == 0 && !CFLAG:貞操帯
	増加量 = 50
ELSE
	増加量 = 0
ENDIF

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:PLAYER:技巧

;ABL:欲望をみる
増加量 += (増加量/5)*ABL:欲望

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 20

SOURCE:情愛 = 500
SOURCE:性行動 = 500
SOURCE:逸脱 = 10
SOURCE:達成 = 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "愛情経験", 1

RETURN 1

