;-------------------------------------------------
;吸血する
;-------------------------------------------------
@USERCOM_ABLE_吸血する, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;奴隷が陥落済みじゃないとダメ
SIF !CFLAG:陥落
	RETURNF 0
;実行者が主人でない場合は、相性が良くないとダメ
SIF PLAYER != MASTER && RELATION:PLAYER <= 100
	RETURNF 0
;対象が無機物、もしくは種族:機械だとダメ
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURNF 0
;対象がゾンビだとダメ
SIF TALENT:ゾンビ
	RETURNF 0
;実行者が吸血鬼じゃないとダメ
SIF !TALENT:PLAYER:吸血鬼
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_吸血する
PRINTL 吸血する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 50

SOURCE:情愛 = 1000
SOURCE:恭順 = 2000
SOURCE:屈従 = 1000
SOURCE:恐怖 = 500
SOURCE:逸脱 = 10

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
IF PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ENDIF
IF !CFLAG:被吸血経験
	PRINTL 異常経験+1
	EXP:異常経験++
ENDIF
CFLAG:被吸血経験++
PRINTFORML 吸血経験+1(%CALLNAME:PLAYER%)
EXP:PLAYER:吸血経験++

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

