;-------------------------------------------------
;吸血する
;-------------------------------------------------
@USERCOM_ABLE_吸血する, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;奴隷が陥落済みじゃないとダメ
コマンド実行条件 += "対象が陥落済みじゃないと実行できない/"
SIF !CFLAG:陥落
	COMRESULT = 0
;実行者が主人でない場合は、相性が良くないとダメ
コマンド実行条件 += "実行者が助手の場合は相性が101%以上必要/"
SIF PLAYER != MASTER && RELATION:PLAYER <= 100
	COMRESULT = 0
;対象が無機物、もしくは種族:機械だとダメ
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
;対象がゾンビだとダメ
コマンド実行条件 += "対象が[ゾンビ]or[スライム]だと実行できない/"
SIF TALENT:ゾンビ || CSTR:種族 == "スライム"
	COMRESULT = 0
;実行者が吸血鬼じゃないとダメ
コマンド実行条件 += "実行者に[吸血鬼]が必要/"
SIF !TALENT:PLAYER:吸血鬼
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_吸血する
PRINTL 吸血する
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 50

SOURCE:情愛 = 1000
SOURCE:恭順 = 2000
SOURCE:屈従 = 1000
SOURCE:恐怖 = 500
SOURCE:逸脱 = 100

;実行者がスライムで、相手がスライム以外の場合はスライム感染が発生
SIF CSTR:PLAYER:種族 == "スライム" && CSTR:種族 != "スライム"
	CFLAG:スライム度 += RAND(1, 3)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
SIF PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF !CFLAG:被吸血経験
	CALL EXPUP, "異常経験", 1

CFLAG:被吸血経験++
CALL EXPUP, "吸血経験", 1, PLAYER

;魔力2.0%回復 処女の血なら3倍の6.0%回復
IF TALENT:処女
	LOCAL = 60
ELSE
	LOCAL = 20
ENDIF
CALL 魔力回復, LOCAL, PLAYER
CALL 獣人催淫, 1, PLAYER

RETURN 1

