;-------------------------------------------------
;吸血させる
;-------------------------------------------------
@USERCOM_ABLE_吸血させる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;その調教中に吸血させてると使えない
コマンド実行条件 += "1回の調教で1回まで/"
SIF TFLAG:吸血済み
	COMRESULT = 0
;奴隷が陥落済みじゃないとダメ
コマンド実行条件 += "対象が陥落済みじゃないと実行できない/"
SIF !CFLAG:陥落
	COMRESULT = 0
;実行者が主人でない場合は、相性が良くないとダメ
コマンド実行条件 += "実行者が助手の場合は相性が101%以上必要/"
SIF PLAYER != MASTER && 相性値(TARGET, PLAYER) <= 100
	COMRESULT = 0
;実行者が無機物、もしくは種族:機械だとダメ
コマンド実行条件 += "実行者が[機械]or[無機物]だと実行できない/"
SIF TALENT:PLAYER:無機物 || CSTR:PLAYER:種族 == "機械"
	COMRESULT = 0
;実行者がゾンビ、もしくはスライムだとダメ
コマンド実行条件 += "実行者が[ゾンビ]or[スライム]だと実行できない/"
SIF TALENT:PLAYER:ゾンビ || CSTR:PLAYER:種族 == "スライム"
	COMRESULT = 0
;奴隷が吸血鬼じゃないとダメ
コマンド実行条件 += "対象に[吸血鬼]が必要/"
SIF !TALENT:吸血鬼
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:アイマスク || TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:触手
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_吸血させる
#DIM DYNAMIC 増加量

PRINTL 吸血させる
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 0

SOURCE:情愛 = 500
SOURCE:恭順 = 500
SOURCE:逸脱 = 100

;体力回復
PRINTFORML %CALLNAME:TARGET%の体力+500
BASE:体力 += 500

;奴隷がスライムで、実行者がスライム以外の場合はスライム感染が発生
SIF CSTR:種族 == "スライム" && CSTR:PLAYER:種族 != "スライム"
	CFLAG:PLAYER:スライム度 += RAND(1, 3)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
SIF PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

CALL EXPUP, "吸血経験", 1

;吸血済みフラグ
TFLAG:吸血済み = 1

;魔力2.0%回復 処女の血なら3倍の6.0%回復
IF TALENT:PLAYER:処女
	増加量 = 60
ELSE
	増加量 = 20
ENDIF
CALL 魔力回復, 増加量
CALL 獣人催淫, 1

RETURN 1
