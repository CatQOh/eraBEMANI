;-------------------------------------------------
;母乳飲み
;-------------------------------------------------
@USERCOM_ABLE_母乳飲み, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;母乳体質じゃなきゃダメ
SIF !TALENT:母乳体質
	RETURNF 0
;陥落済みじゃないとダメ
SIF !CFLAG:陥落
	RETURNF 0
;乳房が露出してないとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0
;胸使用中は不可
SIF Ｂ使用中(TARGET)
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_母乳飲み
;前回と今回の調教者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;前回のコマンドが手淫なら授乳手コキに派生
	SELECTCASE PREVCOMS
		CASE "手淫"
			SIF USERCOM_ABLE_授乳手コキ()
				JUMP COM_授乳手コキ
	ENDSELECT
ENDIF

PRINTL 母乳飲み
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 50

SOURCE:情愛 = 1000
SOURCE:達成 = 1000
SOURCE:恭順 = 2000
SOURCE:屈従 = 1000
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸の汚れは舐め取る
STAIN:胸 = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

;愛情経験
IF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ENDIF

;魔力回復
IF TALENT:PLAYER:魔術師
	LOCAL = MAXBASE:PLAYER:魔力/100
	BASE:PLAYER:魔力 = LIMIT(BASE:PLAYER:魔力+LOCAL, 0, MAXBASE:PLAYER:魔力)
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;依存度補正値(主人かつ恋慕で1、親愛で2）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 4
ENDIF

RETURN 1

