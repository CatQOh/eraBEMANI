;-------------------------------------------------
;料理を作る
;-------------------------------------------------
@USERCOM_ABLE_料理を作る
;助手可能もしくは陥落済みじゃないとダメ
SIF !CFLAG:陥落 && CFLAG:状態 != 2
	RETURN 0
;材料費が無いとダメ
SIF MONEY < 1000
	RETURN 0
;拘束中はダメ
SIF TEQUIP:縄 || TEQUIP:ボールギャグ
	RETURN 0
;お風呂場でもダメ
SIF TEQUIP:お風呂場
	RETURN 0
;屋外でもダメ
SIF TEQUIP:野外プレイ
	RETURN 0
;一回の調教で一回まで
SIF TFLAG:料理
	RETURN 0

RETURN 1

@COM_料理を作る
PRINTL 料理を作る
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;料理レベルを入れる 調教後に料理を振る舞う際に使う
TFLAG:料理 = ABL:料理技能
SIF TFLAG:料理 == 0
	TFLAG:料理 = -1

;材料費
MONEY -= 1000

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 300

SOURCE:習得 = 2000
SOURCE:恭順 = 100
SOURCE:屈従 = 100
SOURCE:逸脱 = 50
SOURCE:反感 = 50
SOURCE:達成 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;経験に応じてボーナス 低いと上がりやすい
SOURCE:習得 += (SOURCE:習得/100)*(100-EXP:料理経験/10)

;料理技能は先に処理しておく
;料理技能が低いと苦痛が入る おそらく怪我してる
SELECTCASE ABL:料理技能
	CASE 0
		TIMES SOURCE:達成 , 0.50
		SOURCE:苦痛 = 100
	CASE 1
		TIMES SOURCE:達成 , 0.70
		SOURCE:苦痛 = 50
	CASE 2
		TIMES SOURCE:達成 , 0.90
		SOURCE:苦痛 = 10
	CASE 3
		TIMES SOURCE:達成 , 1.10
	CASE 4
		TIMES SOURCE:達成 , 1.30
	CASE 5
		TIMES SOURCE:達成 , 1.50
	CASE 6
		TIMES SOURCE:達成 , 1.70
	CASE 7
		TIMES SOURCE:達成 , 1.90
	CASE 8
		TIMES SOURCE:達成 , 2.30
	CASE 9
		TIMES SOURCE:達成 , 2.70
	CASE IS >= 10
		TIMES SOURCE:達成 , 4.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;歌唱経験
PRINTFORML 料理経験+10
EXP:料理経験 += 10

;実績:ゾンビに料理を作らせる
SIF CSTR:種族 == "ゾンビ"
	CALL 実績解除, "ポイズンクッキング"

RETURN 1

