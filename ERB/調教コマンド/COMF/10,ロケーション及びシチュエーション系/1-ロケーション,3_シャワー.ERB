;-------------------------------------------------
;シャワー
;-------------------------------------------------
@USERCOM_ABLE_シャワー, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;解除はいつでも可能
SIF TEQUIP:シャワー
	条件確定 = 1

;お風呂中じゃないとダメ
コマンド実行条件 += "お風呂場でのみ実行可能/"
SIF !TEQUIP:お風呂場
	COMRESULT = 0
;クリキャップ使用中は不可
コマンド実行条件 += "クリキャップ使用中は実行できない/"
SIF TEQUIP:クリキャップ
	COMRESULT = 0
;ニプルキャップ使用中は不可
コマンド実行条件 += "ニプルキャップ使用中は実行できない/"
SIF TEQUIP:ニプルキャップ
	COMRESULT = 0
;搾乳器使用中は不可
コマンド実行条件 += "搾乳機使用中は実行できない/"
SIF TEQUIP:搾乳器
	COMRESULT = 0
;乳房電極装着中はダメ
コマンド実行条件 += "乳房電極使用中は実行できない/"
SIF TEQUIP:乳房電極
	COMRESULT = 0
;ヴァンパイアや獣人、獣には従順4がないと使用不可（プレイヤーがサドならＯＫ）
コマンド実行条件 += "対象が種族[ヴァンパイア][獣人][獣]の場合は対象の従順LV4以上or実行者に[サド]が必要/"
SELECTCASE CSTR:種族
	CASE "ヴァンパイア", "獣人", "獣"
		SIF ABL:従順 < 4 && !TALENT:PLAYER:サド
			COMRESULT = 0
ENDSELECT

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_シャワー
PRINTL シャワー
CALL KOJO_MESSAGE_COM

SIF TEQUIP:粘液ぶっかけ
	TEQUIP:粘液ぶっかけ = 0

;着脱の処理
INVERTBIT TEQUIP:シャワー, 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_シャワー
PRINTL ＜シャワー使用中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 10

DOWNBASE:MASTER:体力 += 5
DOWNBASE:MASTER:気力 += 10

SOURCE:恭順 += 100
SOURCE:鬱屈 += 50
SOURCE:反感 += 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
SELECTCASE CSTR:種族
	;ヴァンパイア（流水の中では動けないため、いろいろ追加）
	CASE "ヴァンパイア"
		SOURCE:恐怖 += 200
		SOURCE:屈従 += 200
		SOURCE:不潔 += 500
		SOURCE:逸脱 += 500
		SOURCE:反感 += 500
	;獣人、獣の場合（まぁ基本洗われるのは嫌と解釈）
	CASE "獣人", "獣"
		SOURCE:恐怖 += 100
		SOURCE:屈従 += 100
		SOURCE:逸脱 += 500
		SOURCE:反感 += 500
	;人魚はLOSEBASEを軽減する
	CASE "人魚"
		LOSEBASE:体力 -= 10
		LOSEBASE:気力 -= 10
		SOURCE:鬱屈 -= 50
		SOURCE:反感 -= 50
ENDSELECT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;シャワーで汚れは常にリセット
RESET_STAIN TARGET

;潤滑を1000減らす
IF PALAM:潤滑 > 1000
	PALAM:潤滑 -= 1000
ELSE
	PALAM:潤滑 = 0
ENDIF

RETURN 1

