;-------------------------------------------------
;ゲームセンター
;-------------------------------------------------
@USERCOM_ABLE_ゲームセンター, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;陥落済みじゃないとダメ
コマンド実行条件 += "対象が陥落済みじゃないと実行できない/"
SIF !CFLAG:陥落
	COMRESULT = 0
;助手が居るなら助手も陥落してないとダメ
コマンド実行条件 += "助手が居る場合は助手も陥落済みじゃないと実行できない/"
SIF ASSI > 0 && !CFLAG:ASSI:陥落
	COMRESULT = 0
;ゲーセン経営してないとダメ
コマンド実行条件 += "自物件のゲーセンが営業していないと実行できない/"
SIF !GC物件 || FLAG:ゲーセン営業中 <= 0
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:ゲームセンター && COMRESULT
	条件確定 = 1

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0
;野外プレイ中はダメ
コマンド実行条件 += "野外プレイ中は実行できない/"
SIF TEQUIP:野外プレイ
	COMRESULT = 0
;搾乳器使用中はダメ
コマンド実行条件 += "搾乳機使用中は実行できない/"
SIF TEQUIP:搾乳器
	COMRESULT = 0
;拘束中はダメ
コマンド実行条件 += "拘束中は実行できない/"
SIF TEQUIP:縄 || TEQUIP:ボールギャグ || TEQUIP:アイマスク
	COMRESULT = 0
;服着てないとダメ
コマンド実行条件 += "各々が服を着ていないと実行できない/"
SIF !((TEQUIP:全身上着 && CFLAG:着衣併用 != 1) || (TEQUIP:上半身上着 && TEQUIP:下半身上着))
	COMRESULT = 0
;主人も
SIF !((TEQUIP:MASTER:全身上着 && CFLAG:MASTER:着衣併用 != 1) || (TEQUIP:MASTER:上半身上着 && TEQUIP:MASTER:下半身上着))
	COMRESULT = 0
;助手が居るなら助手も
SIF ASSI > 0 && !((TEQUIP:ASSI:全身上着 && CFLAG:ASSI:着衣併用 != 1) || (TEQUIP:ASSI:上半身上着 && TEQUIP:ASSI:下半身上着))
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "助手が[アニマル]の場合は実行できない/"
SIF ASSI > 0 && TALENT:ASSI:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF TEQUIP:屍姦
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_ゲームセンター
#DIM ゲーセン出費

IF TEQUIP:ゲームセンター
	PRINTL ゲームセンターを後にする
ELSE
	PRINTL ゲームセンターに行く
ENDIF
CALL KOJO_MESSAGE_COM

;異物だけはここで解除しておく
TEQUIP:Ｖ異物 = 0
TEQUIP:Ａ異物 = 0

INVERTBIT TEQUIP:ゲームセンター, 0

IF !TEQUIP:ゲームセンター
	ゲーセン出費 -= MONEY
	SIF ゲーセン出費 > 0
		CALL 現金出納帳記録, @"ゲームセンターで遊んだ費用/-{ゲーセン出費}"

	TEQUIP:トイレ = 0
	RETURN 1
ELSE
	ゲーセン出費 = MONEY
	RESET_STAIN MASTER
	RESET_STAIN TARGET
	SIF ASSI > 0
		RESET_STAIN ASSI
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 30
SIF TALENT:音ゲーマー
	LOSEBASE:気力 -= 30

SOURCE:恭順 = 300
SOURCE:露出 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
;CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;CALL SAMEN_CHECK_T, 0

;CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_ゲームセンター
LOSEBASE:体力 += 0
LOSEBASE:気力 += 10
SIF TALENT:音ゲーマー
	LOSEBASE:気力 -= 10

SOURCE:恭順 += 100
SOURCE:露出 += 30

RETURN 1
