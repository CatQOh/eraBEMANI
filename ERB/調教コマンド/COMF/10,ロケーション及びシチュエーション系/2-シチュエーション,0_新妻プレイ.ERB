;-------------------------------------------------
;新妻プレイ
;-------------------------------------------------
@USERCOM_ABLE_新妻プレイ, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;奴隷が恋慕か親愛でないとダメ
SIF !TALENT:恋慕 && !TALENT:親愛
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:エプロン
	COMRESULT = 1
	RETURNF 1
ENDIF
;アイテム｢エプロン｣を持っていないとダメ
SIF !ITEM:エプロン
	RETURNF 0
;助手じゃダメ
SIF PLAYER != MASTER
	RETURNF 0
;従順が3以上ないとダメ
SIF ABL:従順 < 3
	RETURNF 0
;野外プレイ中はダメ
SIF TEQUIP:野外プレイ
	RETURNF 0
;羞恥プレイ中はダメ
SIF TEQUIP:羞恥プレイ
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_新妻プレイ
PRINTL 新妻プレイ
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:エプロン, 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_新妻プレイ
;助手調教時は、エプロンを解除して関数終了
IF PLAYER != MASTER
	TEQUIP:エプロン = 0
	RETURN 0
ENDIF

PRINTL ＜新妻プレイ中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 0

SOURCE:情愛 += 1000
SOURCE:恭順 += 5000
SOURCE:逸脱 += 500

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
PRINTL 愛情経験+1
EXP:愛情経験++

;依存度補正値(主人かつ恋慕で2、親愛で4）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 4
ENDIF

RETURN 1

