;-------------------------------------------------
;DDRをプレイする
;-------------------------------------------------
@USERCOM_ABLE_DDRをプレイする, ARG
#FUNCTION
;ゲーセンに居ないとダメ
SIF !TEQUIP:ゲームセンター
	COMRESULT = 0
;トイレに居るとダメ
SIF TEQUIP:トイレ
	COMRESULT = 0
;足使用不可だとできない
SIF TALENT:足使用不可
	COMRESULT = 0
;筐体あるか確認
SIF !EXISTGAMES("Dance Dance Revolution")
	COMRESULT = 0
;(ゲーセン経営してるなら特に問題は無いと思うけど)所持金無いとダメ
IF ASSI > 0
	SIF MONEY < 300
		COMRESULT = 0
ELSE
	SIF MONEY < 200
		COMRESULT = 0
ENDIF

;恥ずかしがり屋さんは露出癖2以上必要
SIF TALENT:恥じらい && ABL:露出癖 < 2
	COMRESULT = 0

RETURNF COMRESULT

@COM_DDRをプレイする
PRINTL DDRをプレイする
CALL KOJO_MESSAGE_COM

IF ASSI > 0
	MONEY -= 300
ELSE
	MONEY -= 200
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 50
SIF TALENT:音ゲーマー
	LOSEBASE:気力 -= 20

SOURCE:恭順 = 100
SOURCE:中毒 = 100
SOURCE:習得 = 100
SOURCE:露出 = 300
SOURCE:苦痛 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TALENT:習得遅い
	LOCAL = 1
ELSEIF TALENT:習得早い
	LOCAL = 3
ELSE
	LOCAL = 2
ENDIF
CALL EXPUP, "音ゲー経験", LOCAL
CALL EXPUP, "ダンス経験", LOCAL

IF TALENT:MASTER:習得遅い
	LOCAL = 1
ELSEIF TALENT:MASTER:習得早い
	LOCAL = 3
ELSE
	LOCAL = 2
ENDIF
CALL EXPUP, "音ゲー経験", LOCAL, MASTER
CALL EXPUP, "ダンス経験", LOCAL, MASTER

IF ASSI > 0
	IF TALENT:ASSI:習得遅い
		LOCAL = 1
	ELSEIF TALENT:ASSI:習得早い
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
	CALL EXPUP, "音ゲー経験", LOCAL, ASSI
	CALL EXPUP, "ダンス経験", LOCAL, ASSI
ENDIF

RETURN 1

