?;-------------------------------------------------
;ギタドラをプレイする
;-------------------------------------------------
@USERCOM_ABLE_ギタドラをプレイする, ARG
#FUNCTION
;ゲーセンに居ないとダメ
コマンド実行条件 += "ゲームセンターでのみ実行可能/"
SIF !TEQUIP:ゲームセンター
	COMRESULT = 0
;トイレに居るとダメ
コマンド実行条件 += "トイレに居る場合は実行できない/"
SIF TEQUIP:トイレ
	COMRESULT = 0
;足使用不可だとできない
コマンド実行条件 += "対象が[足使用不可]だと実行できない/"
SIF TALENT:足使用不可
	COMRESULT = 0
;筐体あるか確認
コマンド実行条件 += "ゲーセンにギタドラ筐体を置いてないと実行できない/"
SIF !EXISTGAMES("GITADORA")
	COMRESULT = 0
;(ゲーセン経営してるなら特に問題は無いと思うけど)所持金無いとダメ
コマンド実行条件 += "2クレジット(200$)が必要 助手が居れば3クレジット(300$)必要/"
IF ASSI > 0
	SIF MONEY < 300
		COMRESULT = 0
ELSE
	SIF MONEY < 200
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_ギタドラをプレイする
PRINTL ギタドラをプレイする
CALL KOJO_MESSAGE_COM

IF ASSI > 0
	MONEY -= 300
ELSE
	MONEY -= 200
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 50
SIF TALENT:音ゲーマー
	LOSEBASE:気力 -= 20

SOURCE:恭順 = 100
SOURCE:中毒 = 100
SOURCE:習得 = 100
SOURCE:露出 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TALENT:習得遅い
	LOCAL = 1
ELSEIF TALENT:習得早い
	LOCAL = 3
ELSE
	LOCAL = 2
ENDIF
CALL EXPUP, "音ゲー経験", LOCAL
CALL EXPUP, "演奏経験", LOCAL

IF TALENT:MASTER:習得遅い
	LOCAL = 1
ELSEIF TALENT:MASTER:習得早い
	LOCAL = 3
ELSE
	LOCAL = 2
ENDIF
CALL EXPUP, "音ゲー経験", LOCAL, MASTER
CALL EXPUP, "演奏経験", LOCAL, MASTER

IF ASSI > 0
	IF TALENT:ASSI:習得遅い
		LOCAL = 1
	ELSEIF TALENT:ASSI:習得早い
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
	CALL EXPUP, "音ゲー経験", LOCAL, ASSI
	CALL EXPUP, "演奏経験", LOCAL, ASSI
ENDIF

RETURN 1

