;-------------------------------------------------
;乱れ牡丹
;-------------------------------------------------
@USERCOM_ABLE_乱れ牡丹
;乱れ牡丹実行判定
;羞恥プレイ中じゃないとダメ
SIF !TEQUIP:羞恥プレイ
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:バイブ
	RETURN 0
;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;調教者がペニス持ち、もしくはペニスバンドがないとダメ
SIF !PENIS(PLAYER) && !ITEM:ペニスバンド
	RETURN 0
;潤滑不足で、従順3以下のASSIの場合乱れ牡丹はできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && PLAYER == ASSI
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		RETURN 0
ENDIF

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

RETURN 1

@COM_乱れ牡丹
PRINTL 乱れ牡丹
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	VARSET LOCAL, 0
	LOCAL = RESULT

	;ABL:欲望
	IF ABL:欲望
		LOCAL += ABL:欲望*3
		PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		LOCAL += ABL:Ｖ感覚*2
		PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
	ENDIF
	;ABL:性交中毒
	IF ABL:性交中毒
		LOCAL += ABL:性交中毒
		PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		LOCAL += MARK:快楽刻印*3
		PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		LOCAL -= 5
		PRINT -潤滑不足(5)
	ENDIF
	;PALAM:欲情
	IF PALAM:欲情 < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:欲情 < PALAMLV:2
		LOCAL:1 = 1
	ELSEIF PALAM:欲情 < PALAMLV:3
		LOCAL:1 = 2
	ELSEIF PALAM:欲情 < PALAMLV:4
		LOCAL:1 = 3
	ELSEIF PALAM:欲情 < PALAMLV:5
		LOCAL:1 = 4
	ELSE
		LOCAL:1 = 5
	ENDIF
	LOCAL:2 = LOCAL:1
	SIF TALENT:衝動的
		TIMES LOCAL:2 , 1.50
	IF LOCAL:2
		LOCAL += LOCAL:2*3
		PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2*3})
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		LOCAL -= 5
		PRINT -恥じらい(5)
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		LOCAL -= 5
		PRINT -快感の否定(5)
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:男嫌い && SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
		LOCAL -= 12
		PRINT -男嫌い(12)
	ENDIF
	;羽
	IF TALENT:羽
		LOCAL -= 5
		PRINT -有翼(5)
	ENDIF
	;尻尾
	IF TALENT:尻尾
		LOCAL -= 5
		PRINT -尻尾(5)
	ENDIF
	;恋慕
	IF TALENT:恋慕 && PLAYER == MASTER
		LOCAL += 15
		PRINT +恋慕(15)
	ENDIF
	;親愛
	IF TALENT:親愛 && PLAYER == MASTER
		LOCAL += 20
		PRINT +親愛(20)
	ENDIF
	;処女
	IF TALENT:処女
		LOCAL -= 40
		PRINT -処女(40)
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		LOCAL -= 15
		PRINT -Ｖ経験不足(15)
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		LOCAL += 6
		PRINT +媚薬(6)
	ENDIF

	LOCAL:1 = 0
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 0)
		LOCAL:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		LOCAL:1 += 3
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		LOCAL:1 += 5
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		LOCAL:1 += 5

	SIF TALENT:汚れ無視
		LOCAL:1 /= 3
	SIF TALENT:潔癖症
		LOCAL:1 *= 2

	;汚れあり
	IF LOCAL:1
		LOCAL -= LOCAL:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({LOCAL:1})
	ENDIF

	;合計を表示(47以上で実行)
	PRINTFORM  = {LOCAL}

	LOCAL:2 = 47
	SIF LOCAL < LOCAL:2
		PRINT  < 
	SIF LOCAL == LOCAL:2
		PRINT  = 
	SIF LOCAL > LOCAL:2
		PRINT  > 
	PRINTFORM 実行値{LOCAL:2}
	WAIT
	;実行できない
	IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, 100
		RETURN 0
	ENDIF
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
SELECTCOMS = 乱れ牡丹
SELECTCOM = 504
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		VIRGIN:PLAYER:10 = %NAME:TARGET%
	ELSE
		VIRGIN:PLAYER:0 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF !CFLAG:挿入可
	TEQUIP:下半身上着 = 0
	TEQUIP:全身上着 = 0
ENDIF
TEQUIP:下半身下着 = 0

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 1000

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.80
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 1.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;処女だと増える
SIF TALENT:処女
	TIMES LOCAL , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:賢者タイム > 0
	LOCAL /= 20

BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 800

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.00
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 1.20
ENDIF

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快C = 400
SOURCE:快V = 400
SOURCE:情愛 = 400
SOURCE:苦痛 = 400
SOURCE:中毒 = 700
SOURCE:露出 = 1000
SOURCE:逸脱 = 700
SOURCE:反感 = 800
SOURCE:恭順 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*40+60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) == 1 && PLAYER != MASTER
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 0.80
	TIMES SOURCE:反感 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 0.10
ELSEIF EXP:Ｖ経験 >= EXPLV:4
	TIMES SOURCE:苦痛 , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 0.70
	TIMES SOURCE:反感 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 0.05
ENDIF

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
SELECTCASE ABL:性交中毒
	CASE IS <= 1
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 7
;奴隷の膣内⇔調教者のＰの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
PRINTL Ｖ経験+1
EXP:Ｖ経験++

;性交経験
PRINTL 性交経験+1
EXP:性交経験++

CALL EXPUP, 3, 2

;愛情経験
IF TALENT:恋慕 && PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 && PLAYER == MASTER
	PRINTL 愛情経験+4
	EXP:愛情経験 += 4
ENDIF

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;実績:恋慕、もしくは親愛の牡丹と乱れ牡丹を行う
SIF PLAYER == MASTER && CHARANAMEF(TARGET) == "牡丹" && FALLTYPE(TARGET) == 1
	CALL 実績解除, "返り咲く紅牡丹"

RETURN 1

