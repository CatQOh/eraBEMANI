;-------------------------------------------------
;ダブル素股
;-------------------------------------------------
@USERCOM_ABLE_ダブル素股
;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

RETURN 1

@COM_ダブル素股
PRINTL ダブル素股
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*2
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	LOCAL:1 = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	LOCAL:1 = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	LOCAL:1 = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	LOCAL:1 = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	LOCAL:1 = 4
ELSE
	LOCAL:1 = 5
ENDIF
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 *= 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	PRINTFORM -恥じらい(3)
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	LOCAL -= 3
	PRINTFORM -快感の否定(3)
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:男嫌い && SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	LOCAL -= 7
	PRINTFORM -男嫌い(7)
ENDIF
;恋慕
IF TALENT:恋慕 && PLAYER == MASTER
	LOCAL += 5
	PRINTFORM +恋慕(5)
ENDIF
;親愛
IF TALENT:親愛 && PLAYER == MASTER
	LOCAL += 10
	PRINTFORM +親愛(10)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	PRINT +媚薬(6)
ENDIF

LOCAL:1 = 0
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 5
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 5

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;素股では汚れがそれほど影響しない
LOCAL:1 /= 3

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(25以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 25
SIF LOCAL < LOCAL:2
	PRINT  < 
SIF LOCAL == LOCAL:2
	PRINT  = 
SIF LOCAL > LOCAL:2
	PRINT  > 
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
	CALL KOJO_MESSAGE_EVENT, 100
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
SELECTCOMS = ダブル素股
SELECTCOM = 510
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 300

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;ABL:助手の技巧をみる
LOCAL += (LOCAL/5)*ABL:ASSI:技巧

;ABL:助手の従順をみる
LOCAL += (LOCAL/10)*ABL:ASSI:従順 - (LOCAL/10)*2

;ABL:助手の奉仕精神をみる
SELECTCASE ABL:ASSI:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:賢者タイム > 0
	LOCAL /= 20

BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 100

SOURCE:快C = 200
SOURCE:情愛 = 100
SOURCE:性行動 = 600
SOURCE:達成 = 300
SOURCE:屈従 = 600
SOURCE:逸脱 = 200
SOURCE:反感 = 200

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*30+60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のヴァギナ⇔調教者のペニスの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ
;助手のヴァギナ⇔調教者のペニスの汚れが移動
STAIN:ASSI:3 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ASSI:3
;奴隷のヴァギナ⇔助手のヴァギナの汚れが移動
STAIN:ヴァギナ |= STAIN:ASSI:3
STAIN:ASSI:3 |= STAIN:ヴァギナ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 4, 1

LOCAL = RELATION:(NO:ASSI)

;依存度補正値(相性100↑で淫乱で2、娼婦で4）
IF TALENT:淫乱 && LOCAL > 100
	TFLAG:依存度補正 += 2
ELSEIF TALENT:娼婦 && LOCAL > 100
	TFLAG:依存度補正 += 4
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

