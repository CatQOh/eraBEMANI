;-------------------------------------------------
;ローター
;-------------------------------------------------
@USERCOM_ABLE_ローター, ARG
#FUNCTION
;ローター実行判定
;上級売春中はローター配置してないとダメ
IF 上級売春中
	SIF !ITEM:娼館ローター
		RETURNF 0
ELSE
	;アイテムを持っていないとダメ
	SIF !ITEM:ローター
		RETURNF 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_ローター
PRINTL ローター
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 200

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.30
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 250
SOURCE:露出 = 20
SOURCE:逸脱 = 30
SOURCE:反感 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

RETURN 1

