;-------------------------------------------------
;ローター
;-------------------------------------------------
@USERCOM_ABLE_ローター, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
コマンド実行条件 += "アイテム[ローター]が必要/"
;上級売春中はローター配置してないとダメ
IF 上級売春中
	SIF !ITEM:娼館ローター
		COMRESULT = 0
ELSE
	;アイテムを持っていないとダメ
	SIF ITEM:ローター <= 0
		COMRESULT = 0
ENDIF
;ペニスバンド装着中はダメ
コマンド実行条件 += "対象がペニスバンド装着中は実行不可/"
SIF TEQUIP:ペニスバンド
	COMRESULT = 0

;お風呂場中は防水ローターじゃないとダメ
コマンド実行条件 += "お風呂場では防水加工が必要/"
SIF TEQUIP:お風呂場 && SAVESTR:ローター != "防水ローター"
	COMRESULT = 0
;Ｐ使用中はだめ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0

RETURNF COMRESULT

@COM_ローター
PRINTL ローター
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ｐ道具(200)

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 250
SOURCE:露出 = 20
SOURCE:逸脱 = 30
SOURCE:反感 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:ローター = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

