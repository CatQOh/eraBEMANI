;-------------------------------------------------
;電気按摩
;-------------------------------------------------
@USERCOM_ABLE_電気按摩, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;アイテムを持っていないとダメ
SIF ITEM:電気按摩 <= 0
	RETURNF 0
;ASSIの場合、技巧3以上かサドじゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF
;お風呂場中は防水じゃないとダメ
SIF TEQUIP:お風呂場 && SAVESTR:電気按摩 != "防水電気按摩"
	RETURNF 0
;ペニスバンド装着中はダメ
SIF TEQUIP:ペニスバンド
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_電気按摩
PRINTL 電気按摩
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 250

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.30
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
LOCAL += (LOCAL/2)*ABL:Ｃ感覚

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快C = 500
SOURCE:露出 = 40
SOURCE:逸脱 = 60
SOURCE:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 60

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電気按摩 = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

