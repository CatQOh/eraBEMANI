?;-------------------------------------------------
;バイブ
;-------------------------------------------------
@USERCOM_ABLE_バイブ, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中はアイテム無いとダメ
IF 上級売春中
	SIF !ITEM:娼館バイブ
		COMRESULT = 0
ELSE
	;アイテムを持っていないとダメ
	SIF ITEM:バイブ <= 0
		COMRESULT = 0
ENDIF
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
IF TEQUIP:バイブ
	COMRESULT = 1
	RETURNF 1
ENDIF
;アイテム使用中は不可
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;ヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0

;処女で、従順4以下・レズっ気4以下の女性ASSIの場合バイブはできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF SEX(ASSI) == 2 && (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF
;潤滑不足で、従順3以下のASSIの場合バイブはできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_バイブ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:触手
	PRINTL 触手挿入
	SELECTCOM = GETNUM(TRAINNAME, "触手挿入")
ELSE
	PRINTL バイブ
ENDIF
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;着脱の処理
INVERTBIT TEQUIP:バイブ, 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 150
;改造済みだと変化
IF !TEQUIP:触手
	SELECTCASE SAVESTR:バイブ
		CASE "強力なバイブ"
			LOCAL += 100
		CASE "細いバイブ"
			LOCAL -= 100
	ENDSELECT
ENDIF

CALL 射精ゲージ増加, 射精ゲージ計算_Ｖ道具(LOCAL), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快V = 300
SOURCE:苦痛 = 50
SOURCE:露出 = 20
SOURCE:逸脱 = 40
SOURCE:反感 = 100

IF !TEQUIP:触手
	SELECTCASE SAVESTR:バイブ
		CASE "強力なバイブ"
			SOURCE:快V += 100
		CASE "細いバイブ"
			LOSEBASE:体力 -= 30
			LOSEBASE:気力 -= 70
			SOURCE:快V -= 200
			SOURCE:苦痛 -= 40
			SOURCE:逸脱 -= 30
			SOURCE:反感 -= 70
		CASE "太いバイブ"
			SOURCE:V拡張 = 200
	ENDSELECT
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
SIF SOURCE:V拡張
	CALL 特殊ソース処理_Ｖ拡張

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:バイブ = -1
	TEQUIP:バイブ = 0
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手挿入なら、触手体液が膣内に付く
IF TEQUIP:触手
	SETBIT STAIN:膣内, 5
	SETBIT STAIN:ヴァギナ, 5
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

;異常経験
IF TALENT:処女 && TEQUIP:触手
	PRINTL ＜触手処女喪失＞
	CALL EXPUP, "異常経験", 2
ELSEIF TALENT:処女
	PRINTL ＜バイブ処女喪失＞
	CALL EXPUP, "異常経験", 1
ENDIF

IF !TEQUIP:触手 && SAVESTR:バイブ == "太いバイブ"
	IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
		PRINTL ＜初Ｖ拡張＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CALL EXPUP, "Ｖ拡張経験", 2
ENDIF


RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_バイブ
;バイブ挿入中
;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

IF TEQUIP:触手
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

LOSEBASE:体力 += 15
LOSEBASE:気力 += 30

SOURCE:快V += 150
SOURCE:露出 += 10
SOURCE:逸脱 += 20
SOURCE:反感 += 30

IF !TEQUIP:触手
	SELECTCASE SAVESTR:バイブ
		CASE "強力なバイブ"
			SOURCE:快V += 50
		CASE "細いバイブ"
			LOSEBASE:体力 -= 10
			LOSEBASE:気力 -= 20
			SOURCE:快V -= 100
			SOURCE:逸脱 -= 10
			SOURCE:反感 -= 20
		CASE "太いバイブ"
			SOURCE:V拡張 += 50
	ENDSELECT
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 70
IF !TEQUIP:触手
	SELECTCASE SAVESTR:バイブ
		CASE "強力なバイブ"
			LOCAL += 30
		CASE "細いバイブ"
			LOCAL -= 40
	ENDSELECT
ENDIF

CALL 射精ゲージ増加, 射精ゲージ計算_Ｖ道具(LOCAL), TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

CALL 特殊ソース処理_Ｖ道具, 25, 20
SIF SOURCE:V拡張
	CALL 特殊ソース処理_Ｖ道具拡張, 30, 20, 10

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;バイブ出し入れ時は経験+1
SELECTCASE SELECTCOMS
	CASE "自慰", "指挿入れ"
		CALL EXPUP, "Ｖ経験", 2
	CASE IS != "バイブ"
		CALL EXPUP, "Ｖ経験", 1
ENDSELECT

SIF SAVESTR:バイブ == "太いバイブ" && !TEQUIP:触手
	CALL EXPUP, "Ｖ拡張経験", 1

;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1

