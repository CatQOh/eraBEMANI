;-------------------------------------------------
;オナホール
;-------------------------------------------------
@USERCOM_ABLE_オナホール, ARG
#FUNCTION
#DIM DYNAMIC 条件確定
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:オナホール && COMRESULT
	条件確定 = 1

;小人体型持ちは、実行者と対象双方が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合は、実行者の体格が同じか、主人に[禁断の知識]が必要/"
IF TALENT:小人体型
	SIF !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
		COMRESULT = 0
ENDIF
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[オナホール]が必要"
SIF ITEM:オナホール <= 0
	COMRESULT = 0
;ペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;少なくとも半立ち状態じゃないとダメ
コマンド実行条件 += @"対象の勃起{PALAMLV:2}未満だと実行できない/"
SIF PALAM:勃起 < PALAMLV:2
	COMRESULT = 0

;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_オナホール
#DIM DYNAMIC 増加量
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:触手
	PRINTL 触手搾精
	SELECTCOM = GETNUM(TRAINNAME, "触手搾精")
ELSE
	IF TEQUIP:搾精コンドーム
		PRINTL オナホ搾精
	ELSE
		PRINTL オナホール
	ENDIF
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:オナホール, 0
SIF TEQUIP:触手
	INVERTBIT TEQUIP:搾精コンドーム, 0

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 500
;電動オナホなら快感アップ
SIF SAVESTR:オナホール == "電動オナホール" && !TEQUIP:触手
	増加量 += 300
;スライムオナホだとより快感アップ
SIF SAVESTR:オナホール == "スライムオナホール" && !TEQUIP:触手
	増加量 += 500

CALL 射精ゲージ増加, 射精ゲージ計算_オナホール(増加量), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 80

SOURCE:快C = 300
;電動オナホなら快感アップ
SIF SAVESTR:オナホール == "電動オナホール" && !TEQUIP:触手
	SOURCE:快C += 200

;スライムオナホなら快感アップに加えて僅かに負担軽減
IF SAVESTR:オナホール == "スライムオナホール" && !TEQUIP:触手
	SOURCE:快C += 300
	LOSEBASE:体力 -= 5
	LOSEBASE:気力 -= 10
ENDIF

SOURCE:露出 = 20
SOURCE:不潔 = 50
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:オナホール = -1
	TEQUIP:オナホール = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手ペニス責めなら、触手体液がペニスに付く
SIF TEQUIP:触手
	SETBIT STAIN:ペニス, 5

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_オナホール
#DIM DYNAMIC 増加量
;オナホール装備中
IF TEQUIP:触手
	PRINTL ＜触手ペニス責め中＞
ELSEIF TEQUIP:触手 && TEQUIP:搾精コンドーム
	PRINTL ＜触手搾精中＞
ELSE
	PRINTL ＜オナホール装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 20

SOURCE:快C += 50
;電動オナホなら快感アップ
SIF SAVESTR:オナホール == "電動オナホール" && !TEQUIP:触手
	SOURCE:快C += 50

;スライムオナホなら快感アップに加えて僅かに負担軽減、ただしスライム感染のオマケ付き
IF SAVESTR:オナホール == "スライムオナホール" && !TEQUIP:触手
	SOURCE:快C += 100
	LOSEBASE:気力 -= 10
	IF !FLAG:抗スライム
		IF TALENT:スライム
			CFLAG:スライム度 += RAND(5, 20)
		ELSE
			CFLAG:スライム度++
		ENDIF
	ENDIF
ENDIF

SOURCE:露出 += 10
SOURCE:不潔 += 10
SOURCE:逸脱 += 15
SOURCE:反感 += 15

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 100
;電動オナホなら快感アップ
SIF SAVESTR:オナホール == "電動オナホール" && !TEQUIP:触手
	増加量 += 100
SIF SAVESTR:オナホール == "スライムオナホール" && !TEQUIP:触手
	増加量 += 200

CALL 射精ゲージ増加, 射精ゲージ計算_オナホール(増加量), TARGET

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1
