;-------------------------------------------------
;搾精コンドーム
;-------------------------------------------------
@USERCOM_ABLE_搾精コンドーム, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:搾精コンドーム && COMRESULT
	条件確定 = 1

;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
コマンド実行条件 += "アイテム[搾精コンドーム]が必要/"
;アイテムを持っていないとダメ
SIF !ITEM:搾精コンドーム
	COMRESULT = 0

;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手使用中は不可
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0

;オナホール及びコンドーム使用中は不可
コマンド実行条件 += "対象の男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET) || TEQUIP:コンドーム
	COMRESULT = 0
;ペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;少なくとも半立ち状態じゃないとダメ
コマンド実行条件 += @"対象の勃起{PALAMLV:2}未満だと実行できない/"
SIF PALAM:勃起 < PALAMLV:2
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_搾精コンドーム
IF TEQUIP:触手
	JUMP COM_オナホール
ELSE
	PRINTL 搾精コンドーム
ENDIF

CALL KOJO_MESSAGE_COM

;着脱の処理
SIF !TEQUIP:触手 && !TEQUIP:搾精コンドーム
	ITEM:搾精コンドーム--
INVERTBIT TEQUIP:搾精コンドーム, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 3
LOSEBASE:気力 += 10

SOURCE:快C = 10
SOURCE:中毒 = 5
SOURCE:液体 = 5
SOURCE:欲情 = 10
SOURCE:屈従 = 5
SOURCE:反感 = 10

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 0

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_搾精コンドーム
IF TEQUIP:触手
	RETURN 1
ELSE
	PRINTL ＜搾精コンドーム装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 0
LOSEBASE:気力 += 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF TEQUIP:触手
	TENTACLE++

RETURN 1

