?;-------------------------------------------------
;コンドーム精飲（奴隷）
;-------------------------------------------------
@USERCOM_ABLE_コンドーム精飲（奴隷）, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;コンドーム精飲（奴隷）
;それまでの調教でゴム射してないとダメ
コマンド実行条件 += "使用済みの避妊具が必要/"
SIF !TFLAG:使用済み避妊具
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_コンドーム精飲（奴隷）
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

CALL COM_ORDER_欲情, 1
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 5
		CASE "娼婦", "隷属"
			LOCAL:1 = 10
		CASE "淫乱", "服従"
			LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -2
		CASE "献身的"
			LOCAL:1 = 6
		CASE "汚臭敏感"
			LOCAL:1 = -7
		CASE "汚臭鈍感"
			LOCAL:1 = 2
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;男嫌い
IF TALENT:男嫌い
	LOCAL -= 12
	PRINT -男嫌い(12)
ENDIF
;男好き
IF TALENT:男好き
	LOCAL += 3
	PRINT +男好き(3)
ENDIF
;男（両刀・男好きなら無効）
IF SEX(TARGET) == 1 && !TALENT:両刀 && !TALENT:男好き
	LOCAL -= 5
	PRINT -男(5)
ENDIF

;24以上で実行
CALL 実効値判定, LOCAL, 25

RETURN RESULT

@COM_コンドーム精飲（奴隷）
PRINTL コンドーム精飲（奴隷）

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 70

SOURCE:性行動 = 100
SOURCE:達成 = 150
SOURCE:屈従 = 500
SOURCE:逸脱 = 500
SOURCE:反感 = 500
SOURCE:不潔 = 500

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;コンドームの精液を初期化
TFLAG:使用済み避妊具 = 0

CALL EXPUP, "精液経験", 1

CALL 魔力回復, 20

RETURN 1

