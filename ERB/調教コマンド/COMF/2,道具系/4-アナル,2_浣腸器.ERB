;-------------------------------------------------
;浣腸器
;-------------------------------------------------
@USERCOM_ABLE_浣腸器, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0

;その調教で既に排便済みなら出来ない
SIF TFLAG:腸洗浄済み
	RETURNF 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:浣腸器
	COMRESULT = 1
	RETURNF 1
ENDIF

;精液浣腸は保管済み精液が100mlから必要
SIF SAVESTR:浣腸器 == "精液浣腸" && FLAG:保管済み精液 < 100
	RETURNF 0

;無機物はダメ
SIF TALENT:無機物
	RETURNF 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURNF 0
;アイテムを持っていないとダメ
SIF !ITEM:浣腸器
	RETURNF 0
;Ａ経験が100未満じゃできない
SIF EXP:Ａ経験 < 100
	RETURNF 0
;従順、欲望の合計が6ないとできない
SIF ABL:従順+ABL:欲望 < 6
	RETURNF 0

;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_浣腸器
IF TEQUIP:触手
	PRINTL 触手浣腸
	SELECTCOM = GETNUM(TRAINNAME, "触手浣腸")
ELSE
	PRINTFORML %SAVESTR:浣腸器%
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:浣腸器, 0

;触手浣腸は触手の汚れを追加
SIF TEQUIP:触手
	SETBIT STAIN:アナル, 5
;精液浣腸は精液汚れ追加
IF SAVESTR:浣腸器 == "精液浣腸"
	SETBIT STAIN:アナル, 2
	FLAG:保管済み精液 -= 100
ENDIF
;解除時
IF !TEQUIP:浣腸器
	IF !CFLAG:浣腸器で脱糞した経験
		PRINTL ＜初浣腸調教＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CFLAG:浣腸器で脱糞した経験++
	SIF TEQUIP:ビデオカメラ
		CFLAG:脱糞を撮影された経験++
	SETBIT STAIN:アナル, 3
	TFLAG:腸洗浄済み = 1
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 150

SOURCE:快A = 300
SOURCE:苦痛 = 500
SOURCE:露出 = 1000
SOURCE:屈従 = 200
SOURCE:不潔 = 800
SOURCE:逸脱 = 800
SOURCE:反感 = 3000
;媚薬浣腸
IF SAVESTR:浣腸器 == "媚薬浣腸" && !TEQUIP:触手
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 50
	SOURCE:欲情 = 500
	CFLAG:媚薬 += 3
	TEQUIP:媚薬 = 1
ENDIF
;精液浣腸
IF SAVESTR:浣腸器 == "精液浣腸" && !TEQUIP:触手
	SOURCE:露出 += 3000
	SOURCE:反感 += 3000
	SOURCE:不潔 += 700
ENDIF
;解除時
IF !TEQUIP:浣腸器
	SOURCE:露出 += 1000
	SOURCE:反感 += 2000
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

CALL 特殊ソース処理_Ａ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

SIF SAVESTR:浣腸器 == "精液浣腸" && !TEQUIP:触手
	CALL EXPUP, "精液経験", 3

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_浣腸器
;浣腸+アナルプラグ挿入中
IF TEQUIP:触手
	PRINTL ＜浣腸触手挿入中＞
ELSE
	PRINTL ＜浣腸器挿入中＞
ENDIF

LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快A += 150
SOURCE:屈従 += 100
SOURCE:露出 += 300
SOURCE:不潔 += 300
SOURCE:逸脱 += 250

IF SAVESTR:浣腸器 == "媚薬浣腸" && !TEQUIP:触手
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 50

	SOURCE:欲情 += 100
	CFLAG:媚薬++
ENDIF
IF SAVESTR:浣腸器 == "精液浣腸" && !TEQUIP:触手
	SOURCE:露出 += 1000
	SOURCE:反感 += 500
	SOURCE:不潔 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 250

CALL 特殊ソース処理_Ａ道具, 250, 250

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
SELECTCASE SELECTCOMS
	CASE "浣腸器"
		CALL EXPUP, "Ａ経験", 3
	CASEELSE
		CALL EXPUP, "Ａ経験", 1
ENDSELECT

SIF SAVESTR:浣腸器 == "精液浣腸" && !TEQUIP:触手 && SELECTCOMS != "浣腸器"
	CALL EXPUP, "精液経験", 1

RETURN 1

