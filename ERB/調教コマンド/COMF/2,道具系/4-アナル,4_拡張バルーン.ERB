;-------------------------------------------------
;拡張バルーン
;-------------------------------------------------
@USERCOM_ABLE_拡張バルーン
;拡張バルーン実行判定

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:拡張バルーン
	RETURN 1
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURN 0
;アイテムを持っていないとダメ
SIF !ITEM:拡張バルーン
	RETURN 0
;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURN 0

RETURN 1

@COM_拡張バルーン
PRINTL 拡張バルーン
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:拡張バルーン, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 200

SOURCE:快A = 200
SOURCE:苦痛 = 800
SOURCE:露出 = 300
SOURCE:屈従 = 400
SOURCE:逸脱 = 700
SOURCE:反感 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 3.00
	TIMES SOURCE:反感 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 0.80
	TIMES SOURCE:反感 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 0.60
	TIMES SOURCE:反感 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:苦痛 , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:苦痛 , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 1.00
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 0.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;装備で既に計算されてるので1なら異常経験+
IF !EXP:Ｖ拡張経験 && EXP:Ａ拡張経験 == 1
	PRINTL 異常経験+1
	EXP:異常経験++
ENDIF

CALL EXPUP, 1, 4

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_拡張バルーン
PRINTL ＜拡張バルーン挿入中＞

LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快A += 100
SOURCE:苦痛 += 50
SOURCE:露出 += 150
SOURCE:屈従 += 200
SOURCE:逸脱 += 350

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 350

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
LOCAL = 400
LOCAL:1 = 350
IF EXP:Ａ経験 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.20
ENDIF
SOURCE:苦痛 += LOCAL
SOURCE:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SELECTCASE SELECTCOMS
	CASE "拡張バルーン"
		PRINTL Ａ経験+5
		EXP:Ａ経験 += 5
	CASEELSE
		PRINTL Ａ経験+3
		EXP:Ａ経験 += 3
ENDSELECT

PRINTL Ａ拡張経験+1
EXP:Ａ拡張経験++

