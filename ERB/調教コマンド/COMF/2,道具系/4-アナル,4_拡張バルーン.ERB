?;-------------------------------------------------
;拡張バルーン
;-------------------------------------------------
@USERCOM_ABLE_拡張バルーン, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0

;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
IF TEQUIP:拡張バルーン
	COMRESULT = 1
	RETURNF 1
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
SIF ITEM:拡張バルーン <= 0
	COMRESULT = 0
;アナル使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
;ある程度開発してないとだめ
SIF ABL:Ａ感覚 < 5
	COMRESULT = 0

RETURNF COMRESULT

@COM_拡張バルーン
PRINTL 拡張バルーン
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:拡張バルーン, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 200

SOURCE:快A = 200
SOURCE:苦痛 = 800
SOURCE:露出 = 300
SOURCE:屈従 = 400
SOURCE:逸脱 = 700
SOURCE:反感 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

CALL 特殊ソース処理_Ａ

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:拡張バルーン = -1
	TEQUIP:拡張バルーン = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

;異常経験+
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	PRINTL ＜初Ａ拡張＞
	CALL EXPUP, "異常経験", 1
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_拡張バルーン
PRINTL ＜拡張バルーン挿入中＞

LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快A += 100
SOURCE:苦痛 += 50
SOURCE:露出 += 150
SOURCE:屈従 += 200
SOURCE:逸脱 += 350

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 350

CALL 特殊ソース処理_Ａ道具, 400, 350

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SELECTCASE SELECTCOMS
	CASE "拡張バルーン"
		CALL EXPUP, "Ａ経験", 5
	CASEELSE
		CALL EXPUP, "Ａ経験", 3
		CALL EXPUP, "Ａ拡張経験", 1
ENDSELECT
