;-------------------------------------------------
;アナル電極
;-------------------------------------------------
@USERCOM_ABLE_アナル電極, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;奴隷が服従か隷属でないとダメ
IF FALLTYPE(TARGET) != 3
	;ただしPLAYERがサドならおＫ
	SIF !TALENT:PLAYER:サド
		RETURNF 0
ENDIF

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:アナル電極
	COMRESULT = 1
	RETURNF 1
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURNF 0
;アイテムを持っていないとダメ
SIF ITEM:電極プラグ <= 0
	RETURNF 0
;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_アナル電極
PRINTL アナル電極
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:アナル電極, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 200

SOURCE:快A = 500
SOURCE:苦痛 = 1500
SOURCE:露出 = 300
SOURCE:屈従 = 800
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

CALL 特殊ソース処理_Ａ強

SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 8.00

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電極プラグ = -1
	TEQUIP:アナル電極 = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

;初めてなら異常経験追加
IF !CFLAG:電極で責められた経験
	PRINTL ＜初電極調教＞
	CALL EXPUP, "異常経験", 1
ENDIF
CFLAG:電極で責められた経験++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_アナル電極
PRINTL ＜アナル電極挿入中＞

LOSEBASE:体力 += 90
LOSEBASE:気力 += 150

SOURCE:快A += 250
SOURCE:屈従 += 400
SOURCE:逸脱 += 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

CALL 特殊ソース処理_Ａ道具強, 750, 600

SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 8.00

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "Ａ経験", 5

;実績:ファウラに電極を使う
SIF CHARANAMEF(TARGET) == "ファウラ"
	CALL 実績解除, "雷龍が如く"

RETURN 1

