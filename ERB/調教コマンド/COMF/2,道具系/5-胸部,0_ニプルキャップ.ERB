;-------------------------------------------------
;ニプルキャップ
;-------------------------------------------------
@USERCOM_ABLE_ニプルキャップ, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;乳房が露出してないとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:ニプルキャップ
	COMRESULT = 1
	RETURNF 1
ENDIF
;アイテムを持っていないとダメ
SIF ITEM:ニプルキャップ <= 0
	RETURNF 0
;胸を使ってる場合はダメ
SIF Ｂ使用中(TARGET)
	RETURNF 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURNF 0
ENDIF
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_ニプルキャップ
IF TEQUIP:触手
	PRINTL 触手乳首責め
	SELECTCOM = GETNUM(TRAINNAME, "触手乳首責め")
ELSE
	PRINTL ニプルキャップ
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:ニプルキャップ, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

SOURCE:快B = 200
SOURCE:露出 = 20
SOURCE:逸脱 = 30
SOURCE:反感 = 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:ニプルキャップ = -1
	TEQUIP:ニプルキャップ = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手乳首責めなら、触手体液が胸に付く
SIF TEQUIP:触手
	SETBIT STAIN:胸, 5

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_ニプルキャップ
;ニプルキャップ装備中
IF TEQUIP:触手
	PRINTL ＜触手乳首責め中＞
ELSE
	PRINTL ＜ニプルキャップ装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 30

SOURCE:快B += 100
SOURCE:露出 += 10
SOURCE:逸脱 += 20
SOURCE:反感 += 20

RETURN 1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

