;-------------------------------------------------
;搾乳器
;-------------------------------------------------
@USERCOM_ABLE_搾乳器, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;胸が露出してないとダメ
コマンド実行条件 += "対象の乳房が露出していないと実行できない/"
SIF OVER_BUST(TARGET) != ""
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:搾乳器 && COMRESULT
	条件確定 = 1

;小人体型持ちは主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[搾乳機]が必要/"
SIF ITEM:搾乳器 <= 0
	COMRESULT = 0
;搾精器に改造されてるとダメ
SIF SAVESTR:搾乳器 == "搾精器"
	COMRESULT = 0
;母乳体質でないと使えない
コマンド実行条件 += "対象に[母乳体質]が必要/"
SIF !TALENT:母乳体質
	COMRESULT = 0
;胸に道具ついてるとダメ
コマンド実行条件 += "B道具使用中は実行できない/"
SIF Ｂ使用中(TARGET)
	COMRESULT = 0
;Ｂ感覚2以下はダメ
コマンド実行条件 += "対象のB感覚LV2以上が必要/"
SIF ABL:TARGET:Ｂ感覚 <= 2
	COMRESULT = 0
;シャワー中はダメ
コマンド実行条件 += "シャワー中は実行できない/"
SIF TEQUIP:シャワー
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_搾乳器
IF TEQUIP:触手
	PRINTL 触手搾乳
	SELECTCOM = GETNUM(TRAINNAME, "触手搾乳")
ELSE
	PRINTL 搾乳器
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:搾乳器, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快B = 200
SOURCE:苦痛 = 100
SOURCE:中毒 = 100
SOURCE:露出 = 30
SOURCE:不潔 = 50
SOURCE:屈従 = 100
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;中毒による中毒充足のソースは先に処理しておく
;噴乳中毒を見る
SELECTCASE ABL:噴乳中毒
	CASE 1
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF ((TALENT:衝動的 && !特質("しつけ")) || (TALENT:PLAYER:衝動的 && !特質("しつけ", PLAYER))) && RAND:3 == 0 && !TEQUIP:触手
	TFLAG:アイテム破壊 = 1
	ITEM:搾乳器 = -1
	TEQUIP:搾乳器 = 0
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手搾乳なら、触手体液が胸に付く
SIF TEQUIP:触手
	SETBIT STAIN:胸, 5

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_搾乳器
;搾乳器装備中
IF TEQUIP:触手
	PRINTL ＜触手搾乳中＞
ELSE
	PRINTL ＜搾乳器装備中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 25
LOSEBASE:気力 += 50

SOURCE:快B += 100
SOURCE:苦痛 += 50
SOURCE:中毒 += 50
SOURCE:露出 += 20
SOURCE:屈従 += 50
SOURCE:逸脱 += 50
SOURCE:反感 += 50

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1
