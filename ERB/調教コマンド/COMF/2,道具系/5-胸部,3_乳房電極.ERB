;-------------------------------------------------
;乳房電極
;-------------------------------------------------
@USERCOM_ABLE_乳房電極, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;奴隷が服従か隷属でないとダメ
SIF !TALENT:服従 && !TALENT:隷属
	RETURNF 0

;胸が露出してないとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:乳房電極
	COMRESULT = 1
	RETURNF 1
ENDIF
;アイテムを持っていないとダメ
SIF ITEM:電極プラグ <= 0
	RETURNF 0
;胸に道具ついてるとダメ
SIF Ｂ使用中(TARGET)
	RETURNF 0
;搾乳器使用中は不可
SIF TEQUIP:搾乳器
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF PLAYER == ASSI
	SIF ABL:ASSI:技巧 < 3
		RETURNF 0
ENDIF

COMRESULT = 1
RETURNF 1

@COM_乳房電極
PRINTL 乳房電極
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:乳房電極, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 180

SOURCE:快B = 500
SOURCE:苦痛 = 1500
SOURCE:露出 = 300
SOURCE:屈従 = 800
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF (TALENT:衝動的 || TALENT:PLAYER:衝動的) && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電極プラグ = -1
	TEQUIP:乳房電極 = 0
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;異常経験が5未満のとき、異常経験追加
IF EXP:異常経験 < 5
	EXP:異常経験 += 1
	PRINTL 異常経験+1
ENDIF

CALL EXPUP, 1, 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_乳房電極
PRINTL ＜乳房電極装着中＞

LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快B += 250
SOURCE:苦痛 += 750
SOURCE:屈従 += 400
SOURCE:逸脱 += 600
SOURCE:反感 += 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

;実績:ファウラに電極を使う
SIF CHARANAMEF(TARGET) == "ファウラ"
	CALL 実績解除, "雷龍が如く"

RETURN 1
