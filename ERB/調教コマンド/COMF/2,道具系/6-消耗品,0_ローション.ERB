;-------------------------------------------------
;ローション
;-------------------------------------------------
@USERCOM_ABLE_ローション, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中はアイテム無いとダメ
コマンド実行条件 += "アイテム[ローション]が必要"
IF 上級売春中
	SIF !ITEM:娼館ローション
		COMRESULT = 0
ELSE
	;アイテムを持っていないとダメ
	SIF !ITEM:ローション
		COMRESULT = 0
ENDIF
;全裸じゃないとダメ
コマンド実行条件 += "対象が服を着てると実行できない/"
SIF OVER_GENITAL(TARGET) != "" || OVER_BUST(TARGET) != ""
	COMRESULT = 0
;シャワー中はダメ
コマンド実行条件 += "シャワー中は実行できない/"
SIF TEQUIP:シャワー
	COMRESULT = 0

RETURNF COMRESULT

@COM_ローション
PRINTL ローション
CALL KOJO_MESSAGE_COM

SIF !上級売春中
	ITEM:ローション--

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 3
LOSEBASE:気力 += 3

DOWNBASE:MASTER:体力 += 10
DOWNBASE:MASTER:気力 += 10

;一応強化値が乗る 天井があるのでそんなに効果は実感できない
IF ローション強化値
	SOURCE:液体 = ローション強化値*1000
ELSE
	SOURCE:液体 = 10000
ENDIF
SOURCE:露出 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

;実績:レーヴァスレーターにローションを使う
SIF CHARANAMEF(TARGET) == "レーヴァスレーター"
	CALL 実績解除, "ピピピピピ…ボカーン"

RETURN 1

