;-------------------------------------------------
;ローション注入
;-------------------------------------------------
@USERCOM_ABLE_ローション注入, ARG
#FUNCTION
;ローション実行判定
;男じゃないとダメ
SIF TALENT:性別 == 2
	RETURNF 0
;上級売春中はアイテム無いとダメ
IF 上級売春中
	SIF !ITEM:娼館ローション
		RETURNF 0
ELSE
	;アイテムを持っていないとダメ
	SIF !ITEM:ローション
		RETURNF 0
ENDIF
;下半身が開いてないとダメ
SIF !性器露出(TARGET)
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURNF 0
;アナルが使用中だとダメ
SIF Ａ使用中(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_ローション注入
PRINTL ローション注入
CALL KOJO_MESSAGE_COM

SIF !上級売春中
	ITEM:ローション--

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 10

UP:潤滑 += 10000
SOURCE:液体 = 5000
SOURCE:露出 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

;実績:レーヴァスレーターにローションを使う
SIF CHARANAMEF(TARGET) == "レーヴァスレーター"
	CALL 実績解除, "ピピピピピ…ボカーン"

RETURN 1

