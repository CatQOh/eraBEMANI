;-------------------------------------------------
;媚薬
;-------------------------------------------------
@USERCOM_ABLE_媚薬, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中はアイテム無いとダメ
IF 上級売春中
	SIF !ITEM:娼館媚薬
		RETURNF 0
ELSE
	;アイテムを持っているか、触手調教中でないとダメ
	SIF !ITEM:媚薬 && !TEQUIP:触手
		RETURNF 0
ENDIF
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	RETURNF 0
;[無機物]か種族:機械だとダメ
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_媚薬
PRINTL 媚薬
CALL KOJO_MESSAGE_COM

SIF !TEQUIP:触手 && !上級売春中
	ITEM:媚薬--

CFLAG:媚薬 += 10
TEQUIP:媚薬 = 1

CFLAG:PLAYER:媚薬 += 5
TEQUIP:PLAYER:媚薬 = 1

SIF PENIS(TARGET)
	CFLAG:残数 += 2
SIF PENIS(PLAYER)
	CFLAG:PLAYER:残数++

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
;調教者が調合知識を持っていれば消費を抑えられる
IF TALENT:PLAYER:調合知識
	LOSEBASE:体力 += 150
	LOSEBASE:気力 += 150
ELSE
	LOSEBASE:体力 += 300
	LOSEBASE:気力 += 300
ENDIF

DOWNBASE:MASTER:体力 += 100
DOWNBASE:MASTER:気力 += 100

;媚薬効果で欲情のアップが2倍になるので実際には+5000
UP:欲情 = 2500
SOURCE:逸脱 = 1000

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

SELECTCASE CHARANAMEF(TARGET)
	CASE "ユーズ"
		媚薬実績用:0 = 1
	CASE "士朗"
		媚薬実績用:1 = 1
	CASE "エレキ"
		媚薬実績用:2 = 1
ENDSELECT
SIF 媚薬実績用:0 && 媚薬実績用:1 && 媚薬実績用:2
	CALL 実績解除, "「美しい……」"

SIF CHARANAMEF(TARGET) == "梅桐 彩葉" && CHARANAMEF(PLAYER) == "雛月 理々奈"
	CALL 実績解除, "「新薬の効果は……いかがデスか？」"

RETURN 1

