;-------------------------------------------------
;利尿剤
;-------------------------------------------------
@USERCOM_ABLE_利尿剤, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;アイテムを持っているか、触手調教中でないとダメ
SIF !ITEM:利尿剤 && !TEQUIP:触手
	RETURNF 0
;薬毒耐性の場合、直接注入以外ダメ
SIF TALENT:薬毒耐性 && !TEQUIP:触手
	RETURNF 0
;[無機物]か種族:機械だとダメ
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_利尿剤
PRINTL 利尿剤
CALL KOJO_MESSAGE_COM

SIF !TEQUIP:触手
	ITEM:利尿剤--

;利尿剤強化を行っている場合は強化値と同値になる
IF 利尿剤強化値
	TEQUIP:利尿剤 = 利尿剤強化値
ELSE
	TEQUIP:利尿剤 = 10
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
;調教者が調合知識を持っていれば消費を抑えられる
IF TALENT:PLAYER:調合知識
	LOSEBASE:体力 += 100
	LOSEBASE:気力 += 100
ELSE
	LOSEBASE:体力 += 200
	LOSEBASE:気力 += 200
ENDIF

SOURCE:露出 = 200
SOURCE:逸脱 = 800
;利尿剤効果で軽く変動
SOURCE:反感 = 150

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

RETURN 1

