;-------------------------------------------------
;強精神薬
;-------------------------------------------------
@USERCOM_ABLE_強精神薬, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[強精神薬]が必要/"
SIF !ITEM:強精神薬
	COMRESULT = 0
;薬毒耐性だとダメ
コマンド実行条件 += "対象が[薬毒耐性]を持っていると実行できない/"
SIF TALENT:薬毒耐性
	COMRESULT = 0
;[無機物]もしくは種族:機械だとダメ
コマンド実行条件 += "対象が[無機物]or[機械]だと実行できない/"
SIF TALENT:無機物 || CSTR:種族 == "機械"
	COMRESULT = 0
;効果発揮中・反動中はダメ
コマンド実行条件 += "対象に強精神薬の効果が発動中は実行できない/"
SIF TEQUIP:強精神薬
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_強精神薬
PRINTL 強精神薬
CALL KOJO_MESSAGE_COM

ITEM:強精神薬--
TEQUIP:強精神薬 = 7
SIF 特質("薬効強化")
	TEQUIP:強精神薬 *= 特質("薬効強化")/5

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_強精神薬
IF TEQUIP:強精神薬 > 2
	PRINTL ＜強精神薬効果発揮中＞
	TEQUIP:強精神薬--
ELSEIF TEQUIP:強精神薬 == 2
	TEQUIP:強精神薬 = 1
	PRINTFORML %CALLNAME:TARGET%の強精神剤の効果が切れたようだ
	PRINTL ＜強精神薬反動中＞
ELSEIF TEQUIP:強精神薬 == 1
	PRINTL ＜強精神薬反動中＞
ENDIF

RETURN 1

