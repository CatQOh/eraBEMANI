;-------------------------------------------------
;写真撮影
;-------------------------------------------------
@USERCOM_ABLE_写真撮影, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;カメラを持っていないとダメ
コマンド実行条件 += "アイテム[カメラ]が必要/"
SIF !ITEM:カメラ
	COMRESULT = 0
;撮影上限突破してるとダメ
コマンド実行条件 += "1回の調教で撮影できるのは10枚まで/"
SIF TFLAG:カメラ撮影回数 > ABL:PLAYER:撮影技能+2 || TFLAG:カメラ撮影回数 >= 10
	COMRESULT = 0
;調教の始めには実行できない
コマンド実行条件 += "初手には実行できない/"
SIF PREVCOMS == ""
	COMRESULT = 0
;前回コマンドが撮影向きじゃないと実行できない
コマンド実行条件 += "一部コマンド利用直後には実行できない/"
SELECTCASE PREVCOMS
	{
	CASE "コンドーム", "触手召喚", "ローション", "媚薬", "利尿剤", "ビデオカメラ", "野外プレイ", "助手を犯す",
		 "羞恥プレイ", "お風呂場プレイ", "シャワー", "新妻プレイ", "写真撮影", "強精神薬", "コンドーム精飲（助手）",
		 "料理を作る", "お菓子を作る", "ふたなり化", "ペニスバンド", "ミッドナイトスペシャル", "ゲームセンター",
		 "バストサイズ変更"
	}
		COMRESULT = 0
ENDSELECT

RETURNF COMRESULT

@COM_写真撮影
PRINTL 写真撮影
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

SOURCE:恐怖 = 300
SOURCE:液体 = 100
SOURCE:露出 = 300
SOURCE:逸脱 = 500
SOURCE:反感 = 500

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;苦痛を伴わないマゾっ気及びマゾの処理は先にしておく
;マゾっ気を見る
SELECTCASE ABL:マゾっ気
	CASE 0
		TIMES SOURCE:露出 , 0.80
		SOURCE:液体 += 0
		SOURCE:欲情 += 0
	CASE 1
		TIMES SOURCE:露出 , 1.00
		SOURCE:液体 += 20
		SOURCE:欲情 += 30
	CASE 2
		TIMES SOURCE:露出 , 1.20
		SOURCE:液体 += 40
		SOURCE:欲情 += 70
	CASE 3
		TIMES SOURCE:露出 , 1.40
		SOURCE:液体 += 70
		SOURCE:欲情 += 120
	CASE 4
		TIMES SOURCE:露出 , 1.50
		SOURCE:液体 += 110
		SOURCE:欲情 += 180
	CASE 5
		TIMES SOURCE:露出 , 1.70
		SOURCE:液体 += 150
		SOURCE:欲情 += 250
	CASE 6
		TIMES SOURCE:露出 , 1.90
		SOURCE:液体 += 200
		SOURCE:欲情 += 300
	CASE 7
		TIMES SOURCE:露出 , 2.10
		SOURCE:液体 += 250
		SOURCE:欲情 += 350
	CASE 8
		TIMES SOURCE:露出 , 2.30
		SOURCE:液体 += 300
		SOURCE:欲情 += 400
	CASE 9
		TIMES SOURCE:露出 , 2.50
		SOURCE:液体 += 350
		SOURCE:欲情 += 450
	CASE IS >= 10
		TIMES SOURCE:露出 , 3.00
		SOURCE:液体 += 500
		SOURCE:欲情 += 500
ENDSELECT

;マゾを見る
IF TALENT:マゾ
	TIMES SOURCE:露出 , 2.50
	TIMES SOURCE:液体 , 1.20
	TIMES SOURCE:欲情 , 1.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;予め計算しておいた状況と価格を代入
写真内容:(TFLAG:カメラ撮影回数) = %写真内容記録用%
写真価格:(TFLAG:カメラ撮影回数) = 写真価格記録用
TFLAG:カメラ撮影回数++

CALL EXPUP, "撮影経験", 1, PLAYER
CALL EXPUP, "被写経験", 1

;実績:レピカのあんな写真やこんな写真を撮影する
SIF CHARANAMEF(TARGET) == "青のレピカ"
	CALL 実績解除, "お前が被写体になるんだよ！！"

RETURN 1

