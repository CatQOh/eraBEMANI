;-------------------------------------------------
;騎乗位
;-------------------------------------------------
@USERCOM_ABLE_騎乗位, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;縄使用中だとダメ
SIF TEQUIP:縄
	RETURNF 0
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	RETURNF 0
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0

;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_性交()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_騎乗位
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	LOCAL += ABL:Ｖ感覚*2
	PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

;ABL:性交中毒
IF ABL:性交中毒
	LOCAL += ABL:性交中毒
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF
;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	LOCAL -= 15
	PRINT -潤滑不足(15)
ENDIF

CALL COM_ORDER_欲情, 0
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 20
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 15
		CASE "恥じらい"
			LOCAL:1 = -10
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -5
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -12
		CASE "処女"
			LOCAL:1 = -40
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

IF !TALENT:処女 && EXP:Ｖ経験 < EXPLV:2
	LOCAL -= 15
	PRINT -Ｖ経験不足(15)
ENDIF

;出産トラウマ
IF CFLAG:出産トラウマ
	LOCAL -= CFLAG:出産トラウマ
	PRINTFORM -妊娠出産トラウマ({CFLAG:出産トラウマ})
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	PRINT +媚薬(6)
ENDIF

LOCAL:1 = 0
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 5
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 7
;母乳
SIF GETBIT(STAIN:PLAYER:ペニス, 4)
	LOCAL:1 += 2
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 5

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(35以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 35
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_騎乗位
PRINTL 騎乗位

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		VIRGIN:PLAYER:10 = %NAME:TARGET%
	ELSE
		VIRGIN:PLAYER:0 = %NAME:TARGET%
	ENDIF
ENDIF

;実行者が奴隷のフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 1000

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)

;PALAM:欲情
SELECTCASE GETPALAMLV(PALAM:欲情, 5)
	CASE 0
		TIMES LOCAL , 0.60
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.20
	CASE 4
		TIMES LOCAL , 1.40
	CASE 5
		TIMES LOCAL , 1.70
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/5*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;処女だと増える
SIF TALENT:処女
	TIMES LOCAL , 1.50

;子供は締まりが良いので増える
SIF TALENT:小柄体型
	TIMES LOCAL , 1.20

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 700

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:サドっ気をみる
LOCAL += LOCAL/5*ABL:サドっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 50

SOURCE:快V = 500
SOURCE:情愛 = 500
SOURCE:性行動 = 700
SOURCE:達成 = 700
SOURCE:恭順 = 500
SOURCE:苦痛 = 500
SOURCE:中毒 = 300
SOURCE:露出 = 200
SOURCE:屈従 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*40+100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験++
PRINTL Ｖ経験+1

;性交経験
EXP:性交経験++
EXP:PLAYER:性交経験++
PRINTL 性交経験+1

CALL EXPUP, 4, 3

;嗜虐快楽経験
IF ABL:サドっ気 >= 7
	PRINTFORML 嗜虐快楽経験+{ABL:サドっ気/7}
	EXP:嗜虐快楽経験 += ABL:サドっ気/7
ENDIF

;愛情経験
IF TALENT:恋慕 && !ASSIPLAY
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 && !ASSIPLAY
	PRINTL 愛情経験+4
	EXP:愛情経験 += 4
ENDIF

;魔力回復
IF TALENT:魔術師 && !TEQUIP:PLAYER:コンドーム && !TEQUIP:PLAYER:搾精コンドーム
	LOCAL = MAXBASE:魔力/300
	BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
ENDIF

;依存度補正値(恋慕で2、親愛で4）
IF TALENT:恋慕 && !ASSIPLAY
	TFLAG:依存度補正 += 2
ELSEIF TALENT:親愛 && !ASSIPLAY
	TFLAG:依存度補正 += 4
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

