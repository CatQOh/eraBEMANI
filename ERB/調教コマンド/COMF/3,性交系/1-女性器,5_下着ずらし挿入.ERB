;-------------------------------------------------
;下着ずらし挿入
;-------------------------------------------------
@USERCOM_ABLE_下着ずらし挿入, ARG
#FUNCTION
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

;下半身下着をつけていないとダメ(下着一体型全身上着は例外)
コマンド実行条件 += "下半身をずらせる服を着てないと実行できない/"
SIF !TEQUIP:下半身下着 && CFLAG:着衣併用 != 2
	COMRESULT = 0
;下着一体型全身上着は着ていないとダメ
SIF !TEQUIP:全身上着 && CFLAG:着衣併用 == 2
	COMRESULT = 0
;下の空いてない上着を着ているとダメ
IF (TEQUIP:下半身上着 || TEQUIP:全身上着) && CFLAG:着衣併用 != 2
	SIF !CFLAG:挿入可
		COMRESULT = 0
ENDIF

;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_性交()
	COMRESULT = 0
SIF !USERCOM_ABLE_性交助手()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_下着ずらし挿入
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理（正常位のものを使用）
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
SIF TEQUIP:縄
	RETURN 1

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_セックス
実効値 += RESULT

IF TALENT:羽
	実効値 -= 5
	PRINT -羽(5)
ENDIF

;20以上で実行
CALL 実効値判定, 実効値, 20

RETURN RESULT

@COM_下着ずらし挿入
#DIM DYNAMIC 増加量

PRINTL 下着ずらし挿入

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		CSTR:PLAYER:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:PLAYER:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入実行者(700)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入(500)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

;奴隷のABL:マゾっ気をみる
増加量 += (増加量/5)*ABL:マゾっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 120

SOURCE:快V = 400
SOURCE:情愛 = 300
SOURCE:恭順 = 200
SOURCE:苦痛 = 200
SOURCE:中毒 = 200
SOURCE:露出 = 150
SOURCE:反感 = 100

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "精液", "アナル", "尿")
	SOURCE:不潔 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 2

CALL EXPUP, "Ｖ経験", 1
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;愛情経験
SIF TALENT:恋慕 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF TALENT:親愛 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 4

;魔力回復
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

