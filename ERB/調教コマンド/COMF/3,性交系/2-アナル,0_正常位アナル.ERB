;-------------------------------------------------
;正常位アナル
;-------------------------------------------------
@USERCOM_ABLE_正常位アナル, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針がハード、SMじゃないとダメ
SIF 上級売春中 && !GROUPMATCH(娼館方針, 2, 3)
	RETURNF 0
;調教者がペニス有り、もしくはペニスバンドがないとダメ
IF !PENIS(PLAYER)
	IF 上級売春中
		SIF !ITEM:娼館ペニスバンド
			RETURNF 0
	ELSE
		SIF !ITEM:ペニスバンド
			RETURNF 0
	ENDIF
ENDIF
;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0
;小人体型持ちは、調教者と調教対象が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
	RETURNF 0
;潤滑不足で、従順3以下のASSIの場合正常位アナルはできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:お風呂場
	SIF !ITEM:プレイマット
		RETURNF 0
ENDIF

COMRESULT = 1
RETURNF 1

@実行判定_正常位アナル
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	VARSET LOCAL, 0
	LOCAL = RESULT

	;ABL:欲望
	IF ABL:欲望
		LOCAL += ABL:欲望*3
		PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	ENDIF
	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		LOCAL += ABL:Ａ感覚*2
		PRINTFORM +Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚*2})
	ENDIF
	;ABL:性交中毒
	IF ABL:性交中毒
		LOCAL += ABL:性交中毒
		PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		LOCAL += MARK:快楽刻印*3
		PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		LOCAL -= 10
		PRINT -潤滑不足(10)
	ENDIF

	CALL COM_ORDER_欲情, 0
	LOCAL += RESULT

	REPEAT VARSIZE("TALENT")
		LOCAL:1 = 0
		SIF !TALENT:COUNT
			CONTINUE
		SELECTCASE TALENTNAME:COUNT
			CASE "娼婦"
				SIF PLAYER == MASTER
					LOCAL:1 = 20
			CASE "淫乱"
				SIF PLAYER == MASTER
					LOCAL:1 = 15
			CASE "恥じらい"
				LOCAL:1 = -7
			CASE "快感の否定"
				SIF FALLTYPE(TARGET) != 2
					LOCAL:1 = -5
			CASE "羽"
				LOCAL:1 = -5
			CASE "男嫌い"
				SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
					LOCAL:1 = -12
		ENDSELECT
		IF LOCAL:1
			PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
			LOCAL += LOCAL:1
		ENDIF
	REND

	;A経験
	IF EXP:Ａ経験 < EXPLV:3
		LOCAL -= 30
		PRINT -Ａ経験不足(30)
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		LOCAL += 6
		PRINT +媚薬(6)
	ENDIF

	LOCAL:1 = 0
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 0)
		LOCAL:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		LOCAL:1 += 3
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		LOCAL:1 += 3
	;母乳
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		LOCAL:1 += 2
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		LOCAL:1 += 7

	SIF TALENT:汚れ無視
		LOCAL:1 /= 3
	SIF TALENT:潔癖症
		LOCAL:1 *= 2

	;汚れあり
	IF LOCAL:1
		LOCAL -= LOCAL:1
		IF TALENT:汚れ無視
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:潔癖症
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({LOCAL:1})
	ENDIF

	;合計を表示(30以上で実行)
	PRINTFORM  = {LOCAL}

	LOCAL:2 = 30
	SELECTCASE LOCAL
		CASE IS < LOCAL:2
			PRINT  < 
		CASE IS > LOCAL:2
			PRINT  > 
		CASEELSE
			PRINT  = 
	ENDSELECT
	PRINTFORM 実行値{LOCAL:2}
	WAIT
	;実行できない
	IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 1
ENDIF

@COM_正常位アナル
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
TFLAG:乱交継続中 = 0
IF PREVCOMS == "3P"
	IF USERCOM_ABLE_3P()
		TFLAG:乱交継続中 = 1
		JUMP COM_3P
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF TFLAG:前コマンド実行者 != PLAYER
	;前回の調教が正常位か後背位かフェラチオかイラマチオのとき3Pへ
	SELECTCASE PREVCOMS
		CASE "正常位", "後背位", "フェラチオ", "イラマチオ"
			SIF USERCOM_ABLE_3P()
				JUMP COM_3P
		;前回の調教が助手を犯すのとき（すなわち、今回の実行者は助手）
		CASE "助手を犯す"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			TFLAG:数珠繋ぎ = 123
			IF VAGINA(ASSI)
				TFLAG:主人挿入箇所 = 1
			ELSE
				TFLAG:主人挿入箇所 = 2
			ENDIF
			TFLAG:助手挿入箇所 = 2
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
ENDIF

PRINTL 正常位アナル

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		VIRGIN:PLAYER:10 = %NAME:TARGET%
	ELSE
		VIRGIN:PLAYER:0 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 700

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 400

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
	CASE 0
		TIMES LOCAL , 0.20
	CASE 1
		TIMES LOCAL , 0.50
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.50
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

;奴隷が男の場合はホモっ気を見る
IF SEX(TARGET) == 1
	SELECTCASE ABL:ホモっ気
		CASE 0
			TIMES LOCAL , 0.15
		CASE 1
			TIMES LOCAL , 0.30
		CASE 2
			TIMES LOCAL , 0.50
		CASE 3
			TIMES LOCAL , 0.75
		CASE 4
			TIMES LOCAL , 1.00
		CASEELSE
			LOCAL += (LOCAL/10)*ABL:ホモっ気-3
	ENDSELECT
;女なら無条件で半減
ELSE
	TIMES LOCAL , 0.50
ENDIF

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 80
DOWNBASE:MASTER:気力 += 60

SOURCE:快A = 450
SOURCE:情愛 = 400
SOURCE:苦痛 = 800
SOURCE:中毒 = 200
SOURCE:露出 = 100
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:恭順 = 400

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*60+90

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者のペニスにアナル汚れ
SIF PENIS(PLAYER)
	SETBIT STAIN:PLAYER:ペニス, 3
;奴隷のアナル⇔調教者のペニスの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;A経験
EXP:Ａ経験 += 2
PRINTL Ａ経験+2

;性交経験
EXP:性交経験 += 1
PRINTL 性交経験+1

CALL EXPUP, 3, 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

