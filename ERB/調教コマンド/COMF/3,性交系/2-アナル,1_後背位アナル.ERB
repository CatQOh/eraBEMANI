;-------------------------------------------------
;後背位アナル
;-------------------------------------------------
@USERCOM_ABLE_後背位アナル, ARG
#FUNCTION
;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_Ａ性交()
	RETURNF 0
SIF !USERCOM_ABLE_Ａ性交助手()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_後背位アナル
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER
	LOCAL = RESULT

	CALL COM_ORDER_アナルセックス
	LOCAL += RESULT

	SELECTCASE CSTR:種族
		CASE "獣"
			LOCAL += 2
			PRINT +獣(2)
		CASE "獣人"
			LOCAL += 1
			PRINT +獣人(1)
	ENDSELECT

	;32以上で実行
	CALL 実効値判定, LOCAL, 32

	RETURN RESULT
ELSE
	RETURN 1
ENDIF

@COM_後背位アナル
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
TFLAG:乱交継続中 = 0
IF PREVCOMS == "3P"
	IF USERCOM_ABLE_3P()
		TFLAG:乱交継続中 = 1
		JUMP COM_3P
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF TFLAG:前コマンド実行者 != PLAYER
	SELECTCASE PREVCOMS
		;前回の調教が正常位か後背位かフェラチオかイラマチオのとき3Pへ
		CASE "正常位", "後背位", "フェラチオ", "イラマチオ"
			SIF USERCOM_ABLE_3P()
				JUMP COM_3P
		;前回の調教が"正常位させる"か"後背位させる"か"正常位アナルさせる"か"後背位アナルさせる"のとき数珠繋ぎへ
		CASE "正常位させる", "後背位させる", "正常位アナルさせる", "後背位アナルさせる"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			IF TFLAG:前コマンド実行者 == MASTER
				TFLAG:数珠繋ぎ = 231
				SELECTCASE PREVCOMS
					CASE "正常位させる", "後背位させる"
						TFLAG:奴隷挿入箇所 = 1
					CASE "正常位アナルさせる", "後背位アナルさせる"
						TFLAG:奴隷挿入箇所 = 2
				ENDSELECT
				TFLAG:助手挿入箇所 = 2
			ELSEIF TFLAG:前コマンド実行者 == ASSI
				TFLAG:数珠繋ぎ = 132
				SELECTCASE PREVCOMS
					CASE "正常位させる", "後背位させる"
						TFLAG:奴隷挿入箇所 = 1
					CASE "正常位アナルさせる", "後背位アナルさせる"
						TFLAG:奴隷挿入箇所 = 2
				ENDSELECT
				TFLAG:主人挿入箇所 = 2
			ENDIF
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
		;前回の調教が助手を犯すのとき（すなわち、今回の実行者は助手）
		CASE "助手を犯す"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			TFLAG:数珠繋ぎ = 123
			IF VAGINA(ASSI)
				TFLAG:主人挿入箇所 = 1
			ELSE
				TFLAG:主人挿入箇所 = 2
			ENDIF
			TFLAG:助手挿入箇所 = 2
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
;前回、今回共にの調教者が主人で前回の調教が助手を犯させるのとき
ELSEIF TFLAG:前コマンド実行者 == PLAYER && PREVCOMS == "助手を犯させる"
	TFLAG:数珠繋ぎ = 132
	IF VAGINA(ASSI)
		TFLAG:奴隷挿入箇所 = 1
	ELSE
		TFLAG:奴隷挿入箇所 = 2
	ENDIF
	TFLAG:主人挿入箇所 = 2
	IF USERCOM_ABLE_数珠繋ぎ()
		JUMP COM_数珠繋ぎ
	ELSE
		TFLAG:奴隷挿入箇所 = 0
		TFLAG:主人挿入箇所 = 0
	ENDIF
ENDIF

PRINTL 後背位アナル

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		VIRGIN:PLAYER:10 = %NAME:TARGET%
	ELSE
		VIRGIN:PLAYER:0 = %NAME:TARGET%
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ａ挿入実行者(900)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ａ挿入(600)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 170

DOWNBASE:MASTER:体力 += 80
DOWNBASE:MASTER:気力 += 60

SOURCE:快A = 500
SOURCE:情愛 = 200
SOURCE:苦痛 = 900
SOURCE:中毒 = 250
SOURCE:露出 = 200
SOURCE:逸脱 = 200
SOURCE:反感 = 300
SOURCE:恭順 = 200

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "精液", "尿")
	SOURCE:不潔 = 150

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者のペニスにアナル汚れ
SIF PENIS(PLAYER)
	SETBIT STAIN:PLAYER:ペニス, 3
;奴隷のアナル⇔調教者のペニスの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "Ａ経験", 2
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;魔力回復
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

