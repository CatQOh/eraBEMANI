;-------------------------------------------------
;騎乗位アナル
;-------------------------------------------------
@USERCOM_ABLE_騎乗位アナル, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;縄使用中はダメ
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;足使用不可だとできない
コマンド実行条件 += "対象が[足使用不可]だと実行できない/"
SIF TALENT:足使用不可
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;汎用COMABLEが通らなかったらダメ
SIF !USERCOM_ABLE_Ａ性交()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_騎乗位アナル
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_アナルセックス
実効値 += RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

;露出癖
IF ABL:露出癖-3 != 0
	実効値 += ABL:露出癖-3
	PRINTFORM \@ SIGN(ABL:露出癖-3) == 1 ? + # - \@露出癖LV{ABL:露出癖}({ABS(ABL:露出癖-3)})
ENDIF

;45以上で実行
CALL 実効値判定, 実効値, 45

RETURN RESULT

@COM_騎乗位アナル
#DIM DYNAMIC 増加量

PRINTL 騎乗位アナル

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;童貞喪失
IF TALENT:PLAYER:童貞
	TALENT:PLAYER:童貞 = 0
	IF TALENT:PLAYER:性転換済
		CSTR:PLAYER:性転換童貞 = %NAME:TARGET%
	ELSE
		CSTR:PLAYER:童貞 = %NAME:TARGET%
	ENDIF
ENDIF

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ挿入実行者(800)

;ABL:調教者の技巧をみる
増加量 += (増加量/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;PALAM:欲情
SELECTCASE GETPALAMLV(PALAM:欲情, 5)
	CASE 0
		TIMES 増加量 , 0.60
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.20
	CASE 4
		TIMES 増加量 , 1.40
	CASE 5
		TIMES 増加量 , 1.70
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		増加量 += 増加量/3*ABL:奉仕精神
ENDSELECT

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ａ挿入(600)

;ABL:調教者の技巧をみる
増加量 += (増加量/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 200

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 60

SOURCE:快A = 600
SOURCE:情愛 = 400
SOURCE:性行動 = 700
SOURCE:達成 = 700
SOURCE:恭順 = 400
SOURCE:苦痛 = 800
SOURCE:中毒 = 300
SOURCE:露出 = 250
SOURCE:逸脱 = 300
SOURCE:反感 = 550

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "精液", "尿")
	SOURCE:不潔 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 900

CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者のペニスにアナル汚れ
SETBIT STAIN:PLAYER:ペニス, 3
;奴隷のアナル⇔調教者のペニスの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 5, 3

CALL EXPUP, "Ａ経験", 3
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;嗜虐快楽経験
SIF ABL:サドっ気 >= 7
	CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/7

;魔力回復
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1
