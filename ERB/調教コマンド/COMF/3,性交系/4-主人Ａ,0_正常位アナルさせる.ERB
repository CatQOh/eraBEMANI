;-------------------------------------------------
;正常位アナルさせる
;-------------------------------------------------
@USERCOM_ABLE_正常位アナルさせる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;足使用不可だとできない
SIF TALENT:PLAYER:足使用不可
	RETURNF 0
;上級売春中は方針がハード、SMじゃないとダメ
SIF 上級売春中 && !GROUPMATCH(娼館方針, 2, 3)
	RETURNF 0
;上級売春中で方針がSM、それでもって客がマゾだと実行できない
SIF 上級売春中 && TALENT:マゾ
	RETURNF 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_性交させる()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_正常位アナルさせる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_セックスさせる

;実行者が羽
IF TALENT:PLAYER:羽
	LOCAL -= 5
	PRINTFORM -%CALLNAME:PLAYER%が有翼(5)
ENDIF

;52以上で実行
CALL 実効値判定, LOCAL, 52

RETURN RESULT

@COM_正常位アナルさせる
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
IF TFLAG:前コマンド実行者 != PLAYER
	SELECTCASE PREVCOMS
		;前回の調教が後背位か後背位アナルのとき
		CASE "後背位", "後背位アナル"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			IF TFLAG:前コマンド実行者 == MASTER
				TFLAG:数珠繋ぎ = 132
				SELECTCASE PREVCOMS
					CASE "後背位"
						TFLAG:主人挿入箇所 = 1
					CASE "後背位アナル"
						TFLAG:主人挿入箇所 = 2
				ENDSELECT
				TFLAG:奴隷挿入箇所 = 2
			ELSEIF TFLAG:前コマンド実行者 == ASSI
				TFLAG:数珠繋ぎ = 231
				SELECTCASE PREVCOMS
					CASE "後背位"
						TFLAG:助手挿入箇所 = 1
					CASE "後背位アナル"
						TFLAG:助手挿入箇所 = 2
				ENDSELECT
				TFLAG:奴隷挿入箇所 = 2
			ENDIF
			IF USERCOM_ABLE_数珠繋ぎ()
				TFLAG:数珠繋ぎ = 132
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
			ENDIF
	ENDSELECT
ENDIF

PRINTL 正常位アナルさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷フラグ
TFLAG:奴隷実行 = 1
TFLAG:奴隷挿入箇所 = 2

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_実行者受け(400)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += LOCAL/10*4*ABL:技巧

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_奴隷挿入(700)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += LOCAL/10*4*ABL:技巧

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 90

DOWNBASE:MASTER:体力 += 110
DOWNBASE:MASTER:気力 += 80

SOURCE:快C = 400
SOURCE:恐怖 = 100
SOURCE:性行動 = 600
SOURCE:屈従 = 900
SOURCE:逸脱 = 700
SOURCE:反感 = 700
SOURCE:露出 = 100
SOURCE:恭順 = 400
SOURCE:不潔 = 100
;汚れ
SIF GET_STAIN(STAIN:PLAYER:アナル, "尿")
	SOURCE:不潔 += 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精時の処理
IF PLAYER == MASTER
	LOCAL = 6
ELSEIF PLAYER == ASSI
	LOCAL = 7
ENDIF

CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, LOCAL

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, LOCAL, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者のアナルの汚れが移動
IF PENIS(TARGET)
	SETBIT STAIN:ペニス, 3
	STAIN:ペニス |= STAIN:PLAYER:アナル
	STAIN:PLAYER:アナル |= STAIN:ペニス
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 2

CALL EXPUP, "Ａ経験", 2, PLAYER
CALL EXPUP, "苦痛快楽経験", 2, PLAYER
CALL EXPUP, "嗜虐快楽経験", 2
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;恋慕or親愛なら愛情経験+ 中出しでさらに+
IF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	IF NOWEX:射精 && !GETBIT(TFLAG:避妊具射精, 2)
		CALL EXPUP, "愛情経験", 2
	ELSE
		CALL EXPUP, "愛情経験", 1
	ENDIF
ENDIF

SIF !避妊具(TARGET)
	CALL 魔力回復, 3, PLAYER

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

