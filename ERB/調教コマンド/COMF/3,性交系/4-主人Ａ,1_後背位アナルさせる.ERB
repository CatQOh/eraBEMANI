;-------------------------------------------------
;後背位アナルさせる
;-------------------------------------------------
@USERCOM_ABLE_後背位アナルさせる, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は方針がハード、SMじゃないとダメ
SIF 上級売春中 && !GROUPMATCH(娼館方針, 2, 3)
	RETURNF 0
;上級売春中で方針がSM、それでもって客がマゾだと実行できない
SIF 上級売春中 && TALENT:マゾ
	RETURNF 0
;対象がペニス有り、もしくはペニスバンドがないとダメ
IF !PENIS(TARGET)
	IF 上級売春中
		SIF !ITEM:娼館ペニスバンド
			RETURNF 0
	ELSE
		SIF !ITEM:ペニスバンド
			RETURNF 0
	ENDIF
ENDIF
;調教者が小人体型のとき主人が禁断の知識持ちじゃないとダメ
SIF TALENT:PLAYER:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:小人体型
	RETURNF 0
;助手の場合は助手が陥落していないとダメ
SIF ASSIPLAY && !CFLAG:ASSI:陥落
	RETURNF 0
;オナホール装備中はダメ
SIF TEQUIP:オナホール
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0
;欲情が一定以上じゃないとダメ
SIF PALAM:欲情 <= PALAMLV:3
	RETURNF 0
;縄使用中は出来ない
SIF TEQUIP:縄
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_後背位アナルさせる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
;ABL:性交中毒
IF ABL:性交中毒
	LOCAL += ABL:性交中毒*2
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒*2})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF

CALL COM_ORDER_欲情, 0
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "恥じらい"
			LOCAL:1 = -5
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -10
		CASE "貞操観念"
			SIF FALLTYPE(TARGET) != 1 || PLAYER != MASTER
				LOCAL:1 = -10
		CASE "貞操無頓着"
			LOCAL:1 = 3
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	PRINT +媚薬(6)
ENDIF

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
	LOCAL:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
	LOCAL:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
	LOCAL:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(52以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 52
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_後背位アナルさせる
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回、今回共にの調教者が主人で前回の調教が助手を犯すのとき
IF TFLAG:前コマンド実行者 == PLAYER
	SELECTCASE PREVCOMS
		CASE "助手を犯す"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			TFLAG:数珠繋ぎ = 312
			IF VAGINA(ASSI)
				TFLAG:主人挿入箇所 = 1
			ELSE
				TFLAG:主人挿入箇所 = 2
			ENDIF
			TFLAG:奴隷挿入箇所 = 2
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF TFLAG:前コマンド実行者 != PLAYER
	SELECTCASE PREVCOMS
		CASE "後背位", "後背位アナル"
			TFLAG:奴隷挿入箇所 = 0
			TFLAG:助手挿入箇所 = 0
			TFLAG:主人挿入箇所 = 0
			IF TFLAG:前コマンド実行者 == MASTER
				TFLAG:数珠繋ぎ = 132
				SELECTCASE PREVCOMS
					CASE "後背位"
						TFLAG:主人挿入箇所 = 1
					CASE "後背位アナル"
						TFLAG:主人挿入箇所 = 2
				ENDSELECT
				TFLAG:奴隷挿入箇所 = 2
			ELSEIF TFLAG:前コマンド実行者 == ASSI
				TFLAG:数珠繋ぎ = 231
				SELECTCASE PREVCOMS
					CASE "後背位"
						TFLAG:助手挿入箇所 = 1
					CASE "後背位アナル"
						TFLAG:助手挿入箇所 = 2
				ENDSELECT
				TFLAG:奴隷挿入箇所 = 2
			ENDIF
			IF USERCOM_ABLE_数珠繋ぎ()
				JUMP COM_数珠繋ぎ
			ELSE
				TFLAG:奴隷挿入箇所 = 0
				TFLAG:助手挿入箇所 = 0
				TFLAG:主人挿入箇所 = 0
			ENDIF
	ENDSELECT
ENDIF

PRINTL 後背位アナルさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷フラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 600

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += LOCAL/10*4*ABL:技巧

;ABL:奉仕精神をみる
LOCAL += (LOCAL/10)*ABL:奉仕精神

;プレイヤーのABL:Ａ感覚をみる
SELECTCASE ABL:PLAYER:Ａ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ａ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 900

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += LOCAL/10*4*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;ABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.40
	CASE 2
		TIMES LOCAL , 1.80
	CASE 3
		TIMES LOCAL , 2.20
	CASE 4
		TIMES LOCAL , 2.60
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｃ感覚
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 90

DOWNBASE:MASTER:体力 += 110
DOWNBASE:MASTER:気力 += 80

SOURCE:快C = 400
SOURCE:性行動 = 500
SOURCE:屈従 = 1000
SOURCE:逸脱 = 700
SOURCE:反感 = 700
SOURCE:露出 = 200
SOURCE:恭順 = 200
;上の方で計算した汚れ
SOURCE:不潔 = LOCAL:1*60+100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精時の処理
IF PLAYER == MASTER
	LOCAL = 6
ELSEIF PLAYER == ASSI
	LOCAL = 7
ENDIF

CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, LOCAL

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, LOCAL, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者のアナルの汚れが移動
IF PENIS(TARGET)
	SETBIT STAIN:ペニス, 3
	STAIN:ペニス |= STAIN:PLAYER:アナル
	STAIN:PLAYER:アナル |= STAIN:ペニス
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;調教者のＡ経験
PRINTFORML Ａ経験+2(%CALLNAME:PLAYER%)
EXP:PLAYER:Ａ経験 += 2
PRINTFORML 苦痛快楽経験+2(%CALLNAME:PLAYER%)
EXP:PLAYER:苦痛快楽経験 += 2
PRINTL 嗜虐快楽経験+2
EXP:嗜虐快楽経験 += 2
PRINTL 性交経験+1
EXP:性交経験++

CALL EXPUP, 3, 2

;恋慕or親愛なら愛情経験+ 中出しでさらに+
IF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	IF NOWEX:射精 && !GETBIT(TFLAG:避妊具射精, 2)
		PRINTL 愛情経験+2
		EXP:愛情経験 += 2
	ELSE
		PRINTL 愛情経験+1
		EXP:愛情経験++
	ENDIF
ENDIF

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

