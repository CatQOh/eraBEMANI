;-------------------------------------------------
;手淫
;-------------------------------------------------
@USERCOM_ABLE_手淫, ARG
#FUNCTION
;アニマルだと無理
SIF TALENT:アニマル
	RETURNF 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_手淫
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_手淫
LOCAL += RESULT

;14以上で実行
CALL 実効値判定, LOCAL, 14

RETURN RESULT

@COM_手淫
;前回と今回の調教者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;前回のコマンドが母乳飲みなら授乳手コキに派生
	SELECTCASE PREVCOMS
		CASE "母乳飲み"
			SIF USERCOM_ABLE_授乳手コキ()
				JUMP COM_授乳手コキ
	ENDSELECT
ENDIF

PRINTL 手淫

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_奉仕(300), PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:情愛 = 50
SOURCE:性行動 = 400
SOURCE:達成 = 50
SOURCE:屈従 = 350
SOURCE:逸脱 = 100
SOURCE:反感 = 100
SOURCE:不潔 = 60

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 3
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 3, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷の手⇔調教者のペニスの汚れが移動
STAIN:手 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

