;-------------------------------------------------
;フェラチオ
;-------------------------------------------------
@USERCOM_ABLE_フェラチオ, ARG
#FUNCTION
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_フェラチオ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_フェラ
LOCAL += RESULT

;24以上で実行
CALL 実効値判定, LOCAL, 24

RETURN RESULT

@COM_フェラチオ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
TFLAG:乱交継続中 = 0
IF PREVCOMS == "3P"
	IF USERCOM_ABLE_3P()
		TFLAG:乱交継続中 = 1
		JUMP COM_3P
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のときでゲームセンターじゃない
ELSEIF TFLAG:前コマンド実行者 != PLAYER && !TEQUIP:ゲームセンター
	;前回の調教が正常位か後背位か背面座位かアナルセックスのときは3Pへ
	SELECTCASE PREVCOMS
		CASE "正常位", "後背位", "正常位アナル", "後背位アナル", "背面座位", "背面座位アナル"
			SIF USERCOM_ABLE_3P()
				JUMP COM_3P
	ENDSELECT
ENDIF

;前回の調教が二本フェラのとき二本フェラへ
IF PREVCOMS == "二本フェラ"
	SIF USERCOM_ABLE_二本フェラ()
		JUMP COM_二本フェラ
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF TFLAG:前コマンド実行者 != PLAYER
	;前回の調教がフェラチオだと二本フェラに
	IF PREVCOMS == "フェラチオ"
		SIF USERCOM_ABLE_二本フェラ()
			JUMP COM_二本フェラ
	ENDIF
ENDIF

;前回と今回の調教者が同じでゲームセンターじゃない
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:ゲームセンター
	;前回の調教がクンニかフェラするかシックスナインだとシックスナインに
	SELECTCASE PREVCOMS
		CASE "クンニ", "フェラする", "シックスナイン" 
			SIF USERCOM_ABLE_シックスナイン()
				JUMP COM_シックスナイン
	ENDSELECT
ENDIF

;前回と今回の調教者が同じ
IF PLAYER == TFLAG:前コマンド実行者
	;自慰との組み合わせでオナフェラに派生
	SELECTCASE PREVCOMS
		CASE "自慰", "胸自慰", "オナフェラ"
			SIF USERCOM_ABLE_オナフェラ()
				JUMP COM_オナフェラ
	ENDSELECT
ENDIF

PRINTL フェラチオ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 射精ゲージ計算_奉仕(500)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:情愛 = 100
SOURCE:性行動 = 800
SOURCE:達成 = 150
SOURCE:屈従 = 700
SOURCE:逸脱 = 200
SOURCE:反感 = 200
SOURCE:不潔 = 80

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 4
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 4, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "フェラ経験", 1
;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

IF !避妊具(PLAYER)
	CALL 魔力回復, 5
	CALL 獣人催淫, 1
ENDIF

TFLAG:奉仕快楽 = 1

;実績:山吹でフェラさせる。もしくは山吹でフェラをする
SIF CHARANAMEF(TARGET) == "山吹"
	CALL 実績解除, "殿方の笛を鳴らすのもお得意で？"

RETURN 1

