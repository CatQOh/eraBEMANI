;-------------------------------------------------
;足扱き
;-------------------------------------------------
@USERCOM_ABLE_足扱き, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;足使用不可だとできない
コマンド実行条件 += "対象が[足使用不可]だと実行できない/"
SIF TALENT:足使用不可
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_足扱き
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_手淫
実効値 += RESULT

IF TALENT:倒錯的
	実効値 += 5
	PRINT +倒錯的(5)
ENDIF
IF TALENT:サド
	実効値 += 5
	PRINT +サド(5)
ENDIF
IF TALENT:小悪魔
	実効値 += 5
	PRINT +小悪魔(5)
ENDIF

;22以上で実行
CALL 実効値判定, 実効値, 22

RETURN RESULT

@COM_足扱き
#DIM DYNAMIC 増加量

PRINTL 足扱き

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(400)

;ABL:サドっ気をみる
増加量 += (増加量/10)*ABL:サドっ気

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 60

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 40

SOURCE:性行動 = 500
SOURCE:達成 = 200
SOURCE:屈従 = 100
SOURCE:逸脱 = 100
SOURCE:反感 = 100
SOURCE:不潔 = 40

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/3+1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
