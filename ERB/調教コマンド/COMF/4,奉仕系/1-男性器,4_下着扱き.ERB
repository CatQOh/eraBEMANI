;-------------------------------------------------
;下着扱き
;-------------------------------------------------
@USERCOM_ABLE_下着扱き, ARG
#FUNCTION
;なんらかの下着を着ていないとダメ
コマンド実行条件 += "対象に下半身下着が設定されていないと実行できない/"
SIF CSTR:下半身下着 == ""
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF TEQUIP:屍姦
		COMRESULT = 0
ENDIF

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_下着扱き
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_手淫
実効値 += RESULT

IF TALENT:貞操観念
	実効値 -= 3
	PRINT -貞操観念(3)
ENDIF

;24以上で実行
CALL 実効値判定, 実効値, 24

RETURN RESULT

@COM_下着扱き
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
PRINTL 下着扱き

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:下半身下着
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_奉仕(300), PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

SOURCE:性行動 = 500
SOURCE:達成 = 100
SOURCE:屈従 = 400
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 60

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の下着⇔調教者のペニスの汚れが移動
STAIN:下着 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:下着

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;嗜虐快楽経験
SIF ABL:サドっ気 >= 4
	CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/4

;愛情経験
SIF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
