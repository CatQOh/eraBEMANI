?;-------------------------------------------------
;パイズリ
;-------------------------------------------------
@USERCOM_ABLE_パイズリ, ARG
#FUNCTION

コマンド実行条件 += "対象が貧乳なら技巧LV4以上必要/"
コマンド実行条件 += "対象が並乳なら技巧LV3以上必要/"
コマンド実行条件 += "対象が巨乳、豊乳なら技巧LV2以上必要/"
コマンド実行条件 += "対象が爆乳なら技巧LV1以上必要/"
SELECTCASE TALENT:バストサイズ
	;絶壁だとダメ
	CASE 0, 1
		COMRESULT = 0
	;貧乳なら技巧4以上必要
	CASE 2
		LOCAL = 4
	;普通なら技巧3以上必要
	CASE 3
		LOCAL = 3
	;巨乳、豊乳なら技巧LV2以上必要
	CASE 4, 5
		LOCAL = 2
	;爆乳なら技巧LV1以上必要
	CASE 6
		LOCAL = 1
ENDSELECT
SIF ABL:技巧 < LOCAL
	COMRESULT = 0

;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
コマンド実行条件 += "対象の服装によっては実行できない場合あり/"
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 0, 2
			COMRESULT = 0
	ENDSELECT
ENDIF
IF TEQUIP:上半身下着
	SELECTCASE CFLAG:下着パイズリ
		CASE 0, 2
			COMRESULT = 0
	ENDSELECT
ENDIF
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_パイズリ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_フェラ
LOCAL += RESULT

;30以上で実行
CALL 実効値判定, LOCAL, 30

RETURN RESULT

@COM_パイズリ
SIF OVER_BUST(TARGET) != ""
	PRINT 着衣
PRINTL パイズリ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 射精ゲージ計算_奉仕(700)

;TALENT:バストサイズをみる
SELECTCASE TALENT:バストサイズ
	CASE 1
		TIMES LOCAL , 0.70
	CASE 2
		TIMES LOCAL , 0.85
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4, 5
		TIMES LOCAL , 1.15
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT

;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES LOCAL , 1.15

;機械・無機物の場合、ある程度減らす
IF CSTR:種族 == "機械" && TALENT:無機物
	TIMES LOCAL , 0.60
ELSEIF CSTR:種族 == "機械" || TALENT:無機物
	TIMES LOCAL , 0.80
ENDIF

;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES LOCAL , 1.25

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 80

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:快B = 150
SOURCE:情愛 = 400
SOURCE:恭順 = 400
SOURCE:性行動 = 1000
SOURCE:達成 = 350
SOURCE:屈従 = 700
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 80

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 5
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 5, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "パイズリ経験", 1

;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

TFLAG:奉仕快楽 = 1

RETURN 1

