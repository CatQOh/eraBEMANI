;-------------------------------------------------
;パイズリフェラ
;-------------------------------------------------
@USERCOM_ABLE_パイズリフェラ, ARG
#FUNCTION
;パイズリ実行判定
SELECTCASE TALENT:バストサイズ
	;絶壁だとダメ
	CASE 0, 1
		RETURNF 0
	;貧乳なら技巧6以上必要
	CASE 2
		LOCAL = 6
	;普通なら技巧5以上必要
	CASE 3
		LOCAL = 5
	;巨乳なら技巧LV4以上必要
	CASE 4
		LOCAL = 4
	;爆乳なら技巧LV3以上必要
	CASE 5
		LOCAL = 3
ENDSELECT
SIF ABL:技巧 < LOCAL
	RETURNF 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0
;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF
IF TEQUIP:上半身下着
	SELECTCASE CFLAG:下着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF
;アニマルだと無理
SIF TALENT:アニマル
	RETURNF 0

;汎用COMABLE通らなかったらダメ
SIF !USERCOM_ABLE_Ｐ奉仕()
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_パイズリフェラ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_フェラ
LOCAL += RESULT

;42以上で実行
CALL 実効値判定, LOCAL, 42

RETURN RESULT

@COM_パイズリフェラ
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じでゲームセンターじゃない
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:ゲームセンター
	;前回の調教がクンニかフェラするかシックスナインか69パイズリだと69パイズリに
	SELECTCASE PREVCOMS
		CASE "クンニ", "フェラする", "シックスナイン", "69パイズリ"
			SIF USERCOM_ABLE_69パイズリ()
				JUMP COM_69パイズリ
	ENDSELECT
ENDIF

SIF OVER_BUST(TARGET) != ""
	PRINT 着衣
PRINTL パイズリフェラ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 射精ゲージ計算_奉仕(900)

;TALENT:バストサイズをみる
SELECTCASE TALENT:バストサイズ
	CASE 1
		TIMES LOCAL , 0.70
	CASE 2
		TIMES LOCAL , 0.85
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.15
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT

;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES LOCAL , 1.15

;機械・無機物の場合、ある程度減らす
IF CSTR:種族 == "機械" && TALENT:無機物
	TIMES LOCAL , 0.60
ELSEIF CSTR:種族 == "機械" || TALENT:無機物
	TIMES LOCAL , 0.80
ENDIF

;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES LOCAL , 1.25

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:快B = 150
SOURCE:情愛 = 500
SOURCE:恭順 = 500
SOURCE:性行動 = 1500
SOURCE:達成 = 800
SOURCE:屈従 = 1200
SOURCE:逸脱 = 400
SOURCE:反感 = 400
SOURCE:不潔 = 100

;汚れ
SIF GET_STAIN(STAIN:PLAYER:ペニス, "アナル", "尿")
	SOURCE:不潔 += 80

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 4
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 4, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 3

CALL EXPUP, "フェラ経験", 1
CALL EXPUP, "フェラ経験", 1

;愛情経験
SIF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奴隷が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

IF !避妊具(PLAYER)
	CALL 魔力回復, 5
	CALL 獣人催淫, 1
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

