;-------------------------------------------------
;乳ファック
;-------------------------------------------------
@USERCOM_ABLE_乳ファック, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;男だとダメ
コマンド実行条件 += "対象が女性じゃないと実行できない/"
SELECTCASE TALENT:バストサイズ
	CASE 0
		COMRESULT = 0
ENDSELECT
;調教者がペニス無いとダメ
コマンド実行条件 += "実行者に男性器が必要/"
SIF !PENIS(PLAYER) || Ｐ使用中(PLAYER)
	COMRESULT = 0
;Ｂ使用中は不可
コマンド実行条件 += "B道具使用中は実行できない/"
SIF Ｂ使用中(TARGET)
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;服を着ている場合下もしくは正面から挿入できる構造じゃないとダメ
コマンド実行条件 += "対象の服装によっては実行できない場合がある/"
;下からの場合
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 1, 3, 4, 5
			$ELSE1
			IF TEQUIP:上半身下着
				SELECTCASE CFLAG:下着パイズリ
					CASE 1, 3, 4
						GOTO TRUE
				ENDSELECT
			ELSE
				GOTO TRUE
			ENDIF
	ENDSELECT
ELSE
	GOTO ELSE1
ENDIF
;正面からの場合
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 2, 5
			$ELSE2
			IF TEQUIP:上半身下着
				SELECTCASE CFLAG:下着パイズリ
					CASE 2
						GOTO TRUE
				ENDSELECT
			ELSE
				GOTO TRUE
			ENDIF
	ENDSELECT
ELSE
	GOTO ELSE2
ENDIF
;「TRUE」にたどり着けない場合、0を返してしまう
COMRESULT = 0

$TRUE

RETURNF COMRESULT

@実行判定_乳ファック
#DIM DYNAMIC LCOUNT
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	VARSET LOCAL, 0
	LOCAL = RESULT

	;ABL:欲望
	IF ABL:欲望
		LOCAL += ABL:欲望*3
		PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
	ENDIF
	;ABL:Ｂ感覚
	IF ABL:Ｂ感覚
		LOCAL += ABL:Ｂ感覚*2
		PRINTFORM +Ｂ感覚LV{ABL:Ｂ感覚}({ABL:Ｂ感覚*2})
	ENDIF
	;ABL:性交中毒
	IF ABL:性交中毒
		LOCAL += ABL:性交中毒
		PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		LOCAL += MARK:快楽刻印*3
		PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		LOCAL -= 5
		PRINT -潤滑不足(5)
	ENDIF

	CALL COM_ORDER_欲情, 0
	LOCAL += RESULT

	FOR LCOUNT, 0, VARSIZE("TALENT")
		LOCAL:1 = 0
		SIF !TALENT:LCOUNT
			CONTINUE
		SELECTCASE TALENTNAME:LCOUNT
			CASE "親愛"
				SIF PLAYER == MASTER
					LOCAL:1 = 10
			CASE "恋慕"
				SIF PLAYER == MASTER
					LOCAL:1 = 5
			CASE "恥じらい"
				LOCAL:1 = -5
			CASE "献身的"
				LOCAL:1 = 6
			CASE "快感の否定"
				SIF FALLTYPE(TARGET) != 2
					LOCAL:1 = -3
			CASE "男嫌い"
				SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
					LOCAL:1 = -12
		ENDSELECT
		IF LOCAL:1
			PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(LOCAL:1)})
			LOCAL += LOCAL:1
		ENDIF
	NEXT

	;媚薬
	IF TEQUIP:媚薬
		LOCAL += 6
		PRINT +媚薬(6)
	ENDIF

	LOCAL:1 = 0
	;愛液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 0)
		LOCAL:1 += 1
	;精液の汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 2)
		LOCAL:1 += 3
	;アナルの汚れ
	SIF GETBIT(STAIN:PLAYER:ペニス, 3)
		LOCAL:1 += 7
	;母乳
	SIF GETBIT(STAIN:PLAYER:ペニス, 4)
		LOCAL:1 += 2
	;尿
	SIF GETBIT(STAIN:PLAYER:ペニス, 7)
		LOCAL:1 += 5

	SIF TALENT:汚れ無視
		LOCAL:1 /= 3
	SIF TALENT:潔癖症
		LOCAL:1 *= 2

	;汚れあり
	IF LOCAL:1
		LOCAL -= LOCAL:1
		IF TALENT:汚臭鈍感
			PRINT -汚れあり、汚れ無視
		ELSEIF TALENT:汚臭敏感
			PRINT -汚れあり、潔癖症
		ELSE
			PRINT -汚れあり
		ENDIF
		PRINTFORM ({LOCAL:1})
	ENDIF

	;15以上で実行
	CALL 実効値判定, LOCAL, 15

	RETURN RESULT
ELSE
	RETURN 1
ENDIF

@COM_乳ファック
PRINTL 乳ファック

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 500

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;調教対象のバストサイズを見る
SELECTCASE TALENT:バストサイズ
	;爆乳
	CASE 6
		TIMES LOCAL , 1.50
	;巨乳、豊乳
	CASE 4, 5
		TIMES LOCAL , 1.20
	;普通
	CASE 3
		TIMES LOCAL , 1.00
	;貧乳
	CASE 2
		TIMES LOCAL , 0.90
	;絶壁
	CASE 1
		TIMES LOCAL , 0.70
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快B = 400
SOURCE:性行動 = 200
SOURCE:苦痛 = 200
SOURCE:恐怖 = 200
SOURCE:中毒 = 150
SOURCE:露出 = 100
SOURCE:逸脱 = 150
SOURCE:反感 = 200
SOURCE:不潔 = 100

;上のほうで計算した汚れデータ
SOURCE:不潔 += LOCAL:1*50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 5
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 5, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
;射精に関係なく行われる処理はここから
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "パイズリ経験", 1

RETURN 1

