;-------------------------------------------------
;アナル奉仕強制
;-------------------------------------------------
@USERCOM_ABLE_アナル奉仕強制
;アナル奉仕強制実行判定
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0

RETURN 1

@実行判定_アナル奉仕強制
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*3
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*3})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

;反抗的（服従、隷属で無効）
IF TALENT:反抗的 && FALLTYPE(TARGET) != 3
	LOCAL -= 5
	PRINTFORM -反抗的(5)
ENDIF
;プライド高い
IF TALENT:プライド高い
	LOCAL -= 12
	PRINTFORM -プライド高い(12)
ENDIF
;プライド低い
IF TALENT:プライド低い
	LOCAL += 2
	PRINTFORM +プライド低い(2)
ENDIF
;抵抗
IF TALENT:抵抗
	LOCAL -= 20
	PRINTFORM -抵抗(20)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	PRINTFORM -恥じらい(3)
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	LOCAL += 3
	PRINTFORM +汚臭鈍感(3)
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	LOCAL -= 5
	PRINTFORM -汚臭敏感(5)
ENDIF
;献身的
IF TALENT:献身的
	LOCAL += 6
	PRINTFORM +献身的(6)
ENDIF

;汚れ無視
IF TALENT:汚れ無視
	LOCAL += 3
	PRINTFORM +汚れ無視(3)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 3
	PRINTFORM +倒錯的(3)
ENDIF
;PLAYERがサド
IF TALENT:サド
	LOCAL += 3
	PRINTFORM +サド(3)
ENDIF
;恋慕
IF TALENT:恋慕 && PLAYER == MASTER
	LOCAL += 5
	PRINTFORM +恋慕(5)
ENDIF
;親愛
IF TALENT:親愛 && PLAYER == MASTER
	LOCAL += 10
	PRINTFORM +親愛(10)
ENDIF

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:アナル, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:アナル, 2)
	LOCAL:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:アナル, 3)
	LOCAL:1 += 5
;尿
SIF GETBIT(STAIN:PLAYER:アナル, 7)
	LOCAL:1 += 5

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(30以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 30
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, 100
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_アナル奉仕強制
PRINTL アナル奉仕強制

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF !CFLAG:PLAYER:挿入可
	TEQUIP:PLAYER:下半身上着 = 0
	TEQUIP:PLAYER:全身上着 = 0
ENDIF
TEQUIP:PLAYER:下半身下着 = 0

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 100

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ａ感覚をみる
SELECTCASE ABL:PLAYER:Ａ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ａ感覚-1)
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:賢者タイム > 0
	LOCAL /= 20

BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:性行動 = 400
SOURCE:達成 = 300
SOURCE:屈従 = 1500
SOURCE:逸脱 = 1200
SOURCE:反感 = 2000

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*100+300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 3

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTL 嗜虐快楽経験+1
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:PLAYER:種族
	CASE "獣人", "獣"
		CFLAG:媚薬++
		SIF CFLAG:媚薬%5 == 0
			CFLAG:残数++
ENDSELECT

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

