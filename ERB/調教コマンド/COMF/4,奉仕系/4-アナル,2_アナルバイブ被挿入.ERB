;-------------------------------------------------
;アナルバイブ被挿入
;-------------------------------------------------
@USERCOM_ABLE_アナルバイブ被挿入, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[アナルバイブ]が必要/"
IF 上級売春中
	SIF !ITEM:娼館アナルバイブ
		COMRESULT = 0
ELSE
	SIF ITEM:アナルバイブ <= 0
		COMRESULT = 0
ENDIF
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_アナルバイブ被挿入
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 実効値, 3
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

実効値 = RESULT

;ABL:欲望
IF ABL:欲望
	実効値 += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	実効値 += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF

CALL COM_ORDER_欲情, 1
実効値 += RESULT

FOR LCOUNT, 0, VARSIZE("TALENT")
	実効値:1 = 0
	SIF !TALENT:LCOUNT
		CONTINUE
	SELECTCASE TALENTNAME:LCOUNT
		CASE "恥じらい"
			実効値:1 = -3
		CASE "献身的"
			実効値:1 = 4
		CASE "汚臭敏感"
			実効値:1 = -5
		CASE "汚臭鈍感"
			実効値:1 = 3
	ENDSELECT
	IF 実効値:1
		PRINTFORM \@SIGN(実効値:1) == 1 ? + # - \@%TALENTNAME:LCOUNT%({ABS(実効値:1)})
		実効値 += 実効値:1
	ENDIF
NEXT

;合計を表示(22以上で実行)
PRINTFORM  = {実効値}

実効値:2 = 22
SIF 実効値 < 実効値:2
	PRINT  < 
SIF 実効値 == 実効値:2
	PRINT  = 
SIF 実効値 > 実効値:2
	PRINT  > 
PRINTFORM 実行値{実効値:2}
WAIT
;実行できない
IF 実効値 < 実効値:2
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_アナルバイブ被挿入
#DIM DYNAMIC 増加量

PRINTL アナルバイブ被挿入

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF CFLAG:挿入可
	TEQUIP:下半身下着 = 0
ELSE
	TEQUIP:下半身下着 = 0
	TEQUIP:下半身上着 = 0
	TEQUIP:全身上着 = 0
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
TFLAG:奴隷挿入箇所 = 2
増加量 = 射精ゲージ計算_実行者受け(300)

;ABL:技巧をみる
増加量 += 増加量/10*4*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 25
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 50

SOURCE:性行動 = 400
SOURCE:達成 = 200
SOURCE:屈従 = 200
SOURCE:恭順 = 150
SOURCE:逸脱 = 300
SOURCE:反感 = 300
SOURCE:不潔 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF TALENT:衝動的 && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:アナルバイブ = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

CALL EXPUP, "Ａ経験", 1, PLAYER

;嗜虐快楽経験
SIF ABL:サドっ気 >= 5
	CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/5

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
