;-------------------------------------------------
;泡踊り
;-------------------------------------------------
@USERCOM_ABLE_泡踊り, ARG
#FUNCTION
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;お風呂場プレイ中でプレイマットが無いとダメ
SIF !TEQUIP:お風呂場 || !ITEM:プレイマット
	RETURNF 0
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURNF 0
;Ｖ仕様中はダメ
SIF Ｖ使用中(TARGET)
	RETURNF 0
;Ａ使用中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0
;Ｂ使用中は不可
SIF Ｂ使用中(TARGET)
	RETURNF 0
;オナホール使用中はダメ
SIF TEQUIP:オナホール
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;アイマスク使用中は不可
SIF TEQUIP:アイマスク
	RETURNF 0
;アニマルだと無理
SIF TALENT:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_泡踊り
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

CALL COM_ORDER_欲情, 1
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -3
		CASE "献身的"
			LOCAL:1 = 6
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -3
		CASE "汚臭敏感"
			LOCAL:1 = -3
		CASE "汚臭鈍感"
			LOCAL:1 = 3
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -5
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 3
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(40以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 40
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_泡踊り
PRINTL 泡踊り

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
IF !CFLAG:PLAYER:貞操帯
	LOCAL = 0
ELSE
	LOCAL = 300
ENDIF

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;種族:人魚なら水の扱いが得意なので増やす
SIF CSTR:種族 == "人魚"
	TIMES LOCAL , 1.50

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快C = 200
SOURCE:快B = 200
SOURCE:情愛 = 900
SOURCE:恭順 = 900
SOURCE:性行動 = 1300
SOURCE:達成 = 700
SOURCE:屈従 = 900
SOURCE:逸脱 = 400
SOURCE:反感 = 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の汚れ
RESET_STAIN TARGET

;調教者の汚れ
IF PLAYER == MASTER
	RESET_STAIN MASTER
ELSE
	RESET_STAIN ASSI
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;愛情経験
IF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	PRINTL 愛情経験+1
	EXP:愛情経験++
ENDIF

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:恋慕 || TALENT:淫乱 || TALENT:隷属) && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

