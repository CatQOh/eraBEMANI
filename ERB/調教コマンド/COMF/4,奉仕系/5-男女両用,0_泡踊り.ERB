;-------------------------------------------------
;泡踊り
;-------------------------------------------------
@USERCOM_ABLE_泡踊り, ARG
#FUNCTION
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;お風呂場プレイ中でプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が必要/"
SIF !TEQUIP:お風呂場 || !ITEM:プレイマット
	COMRESULT = 0
;シャワー中はダメ
コマンド実行条件 += "シャワー中は実行できない/"
SIF TEQUIP:シャワー
	COMRESULT = 0
;Ｖ仕様中はダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;Ａ使用中はダメ
コマンド実行条件 += "対象がA道具使用中は実行できない/"
SIF Ａ使用中(TARGET)
	COMRESULT = 0
;Ｂ使用中は不可
コマンド実行条件 += "B道具使用中は実行できない/"
SIF Ｂ使用中(TARGET)
	COMRESULT = 0
;オナホール使用中はダメ
コマンド実行条件 += "対象がオナホール使用中は実行できない/"
SIF TEQUIP:オナホール
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;アイマスク使用中は不可
コマンド実行条件 += "アイマスク装着中は実行できない/"
SIF TEQUIP:アイマスク
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF TEQUIP:屍姦
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@実行判定_泡踊り
#DIM DYNAMIC 実効値
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_奉仕
実効値 += RESULT

;40以上で実行
CALL 実効値判定, 実効値, 40

RETURN RESULT

@COM_泡踊り
#DIM DYNAMIC 増加量

PRINTL 泡踊り

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
IF CFLAG:PLAYER:貞操帯
	増加量 = 0
ELSE
	増加量 = 300
ENDIF

増加量 = 射精ゲージ計算_奉仕(増加量)

;種族:人魚なら水の扱いが得意なので増やす
SIF CSTR:種族 == "人魚"
	TIMES 増加量 , 1.50

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快C = 200
SOURCE:快B = 200
SOURCE:情愛 = 900
SOURCE:恭順 = 900
SOURCE:性行動 = 1300
SOURCE:達成 = 700
SOURCE:屈従 = 900
SOURCE:逸脱 = 400
SOURCE:反感 = 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の汚れ
RESET_STAIN TARGET

;調教者の汚れ
IF PLAYER == MASTER
	RESET_STAIN MASTER
ELSE
	RESET_STAIN ASSI
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 2

;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

TFLAG:奉仕快楽 = 1

RETURN 1
