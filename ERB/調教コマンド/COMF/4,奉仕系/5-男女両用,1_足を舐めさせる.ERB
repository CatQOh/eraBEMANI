;-------------------------------------------------
;足を舐めさせる
;-------------------------------------------------
@USERCOM_ABLE_足を舐めさせる, ARG
#FUNCTION
;上級売春中は客がマゾじゃないとできない
SIF 上級売春中 && !TALENT:マゾ
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;調教者が足に何か履いてる場合はダメ
SIF TEQUIP:PLAYER:足元
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_足を舐めさせる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

;PALAM:欲情
LOCAL:1 = GETPALAMLV(PALAM:欲情, 5)
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 *= 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "隷属"
			SIF PLAYER == MASTER
				LOCAL:1 = 10
		CASE "服従"
			SIF PLAYER == MASTER
				LOCAL:1 = 5
		CASE "プライド高い"
			LOCAL:1 = -12
		CASE "プライド低い"
			LOCAL:1 = 2
		CASE "抵抗"
			LOCAL:1 = -20
		CASE "恥じらい"
			LOCAL:1 = -1
		CASE "汚臭鈍感", "汚れ無視", "倒錯的", "サド"
			LOCAL:1 = 2
		CASE "献身的"
			LOCAL:1 = 6
		CASE "汚臭敏感", "潔癖症"
			LOCAL:1 = -7
		CASE "反抗的"
			SIF FALLTYPE(TARGET) != 3
				LOCAL:1 = -10
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;合計を表示(39以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 39
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_足を舐めさせる
PRINTL 足を舐めさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:性行動 = 50
SOURCE:達成 = 100
SOURCE:屈従 = 1500
SOURCE:不潔 = 500
SOURCE:逸脱 = 1000
SOURCE:反感 = 1000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 3

;依存度補正値(服従で2、隷属で4）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 4
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

