;-------------------------------------------------
;ローター愛撫させる
;-------------------------------------------------
@USERCOM_ABLE_ローター愛撫させる, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;アイテム無いとダメ
IF 上級売春中
	SIF !ITEM:娼館ローター
		RETURNF 0
ELSE
	SIF ITEM:ローター <= 0
		RETURNF 0
ENDIF
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_ローター愛撫させる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

CALL COM_ORDER_欲情, 1
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 5
		CASE "献身的"
			LOCAL:1 = 3
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;合計を表示(12以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 12
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_ローター愛撫させる
PRINTL ローター愛撫させる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 150

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:情愛 = 100
SOURCE:性行動 = 200
SOURCE:達成 = 100
SOURCE:屈従 = 300
SOURCE:逸脱 = 100
SOURCE:反感 = 200
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF TALENT:衝動的 && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:ローター = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

