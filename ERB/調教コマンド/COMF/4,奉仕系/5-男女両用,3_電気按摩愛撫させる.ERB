?;-------------------------------------------------
;電気按摩愛撫させる
;-------------------------------------------------
@USERCOM_ABLE_電気按摩愛撫させる, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;アイテム無いとダメ
コマンド実行条件 += "アイテム[電気按摩]が必要/"
SIF ITEM:電気按摩 <= 0
	COMRESULT = 0
;貞操帯装備中は不可
コマンド実行条件 += "実行者が貞操帯装着中は実行できない/"
SIF CFLAG:PLAYER:貞操帯
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
;お風呂場中は防水じゃないとダメ
コマンド実行条件 += "電気按摩に防水加工が必要/"
SIF TEQUIP:お風呂場 && SAVESTR:電気按摩 != "防水電気按摩"
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_電気按摩愛撫させる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER
LOCAL = RESULT

CALL COM_ORDER_道具奉仕
LOCAL += RESULT

;14以上で実行
CALL 実効値判定, LOCAL, 14

RETURN RESULT

@COM_電気按摩愛撫させる
PRINTL 電気按摩愛撫させる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_奉仕(200), PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 30

SOURCE:情愛 = 100
SOURCE:性行動 = 250
SOURCE:達成 = 120
SOURCE:屈従 = 200
SOURCE:逸脱 = 100
SOURCE:反感 = 200
SOURCE:不潔 = 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;[衝動的]持ちは3分の1でアイテム破壊する（調教終了後に修復）
IF TALENT:衝動的 && RAND:3 == 0
	TFLAG:アイテム破壊 = 1
	ITEM:電気按摩 = -1
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 1

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

