;-------------------------------------------------
;スパンキング
;-------------------------------------------------
@USERCOM_ABLE_スパンキング, ARG
#FUNCTION
;上級売春中は方針がSMで客がマゾじゃないとダメ
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	RETURNF 0
;従順3未満のASSIの場合スパンキングはできない（サドならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_スパンキング
PRINTL スパンキング
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 60
LOSEBASE:気力 += 20

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:苦痛 = 450
SOURCE:恐怖 = 400
SOURCE:逸脱 = 50
SOURCE:反感 = 300
SOURCE:露出 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

;実績:午前中から既にやる気の無いマモニスおじさんをひっぱたく
SIF CHARANAMEF(TARGET) == "マモニス" && TIME == 0
	CALL 実績解除, "「あ、私午後から畑行きますんで」"

RETURN 1

