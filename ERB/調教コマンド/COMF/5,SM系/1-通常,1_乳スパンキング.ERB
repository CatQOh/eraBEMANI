;-------------------------------------------------
;乳スパンキング
;-------------------------------------------------
@USERCOM_ABLE_乳スパンキング, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針がSMで客がマゾじゃないとダメ
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	RETURNF 0
;従順3未満のASSIの場合スパンキングはできない（サドならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_乳スパンキング
PRINTL 乳スパンキング
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 30

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:苦痛 = 300
SOURCE:恐怖 = 300
SOURCE:露出 = 100
SOURCE:逸脱 = 50
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;マゾっ気に応じて快Bのソースを追加する
SOURCE:快B += 100*ABL:マゾっ気

;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の手の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;実績:レイシスのち首を弾く
SIF CHARANAMEF(TARGET) == "レイシス"
	CALL 実績解除, "はじけっ！ギュイ～ン！"

RETURN 1

