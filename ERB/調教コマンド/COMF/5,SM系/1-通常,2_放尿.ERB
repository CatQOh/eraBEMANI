;-------------------------------------------------
;放尿
;-------------------------------------------------
@USERCOM_ABLE_放尿, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;Ｐ使用中
コマンド実行条件 += "対象の男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET) || 避妊具(TARGET)
	COMRESULT = 0
;尿道使用中は無理
コマンド実行条件 += "尿道にアイテム使用中は実行できない/"
SIF Ｕ使用中(TARGET)
	COMRESULT = 0
;利尿剤使用中かおもらし癖ありでないとダメ
コマンド実行条件 += "[利尿剤]効果発動中か[おもらし癖]を持ってないと実行できない/"
SIF !TEQUIP:利尿剤 && !TALENT:おもらし癖
	COMRESULT = 0
;従順3以上でないとダメ（お風呂場ならＯＫ）
コマンド実行条件 += "対象の従順LV3以上必要/"
コマンド実行条件 += "　お風呂場なら無視できる/"
SIF !TEQUIP:お風呂場 && ABL:従順 < 3
	COMRESULT = 0
;連続実行はできない
コマンド実行条件 += "連続実行は不可/"
SIF PREVCOMS == "放尿"
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF MOD:STS
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF CFLAG:死亡
		COMRESULT = 0
ENDIF

RETURNF COMRESULT

@COM_放尿
PRINTL 放尿
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 200

SOURCE:恐怖 = 200
SOURCE:露出 = 1000
SOURCE:屈従 = 500
SOURCE:逸脱 = 700
SOURCE:反感 = 700
SOURCE:不潔 = 500

IF TALENT:アニマル
	SOURCE:恐怖 = 0
	SOURCE:露出 = 0
	SOURCE:不潔 = 0
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;場所によって逸脱のソースが上下
;野外
IF TEQUIP:野外プレイ
	TIMES SOURCE:逸脱 , 2.50
	TIMES SOURCE:露出 , 2.50
;鏡の前（羞恥プレイ）
ELSEIF TEQUIP:羞恥プレイ
	TIMES SOURCE:逸脱 , 1.50
	TIMES SOURCE:露出 , 1.50
;風呂場
ELSEIF TEQUIP:お風呂場
	TIMES SOURCE:逸脱 , 0.50
	TIMES SOURCE:露出 , 0.50
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;放尿コマンド時は射精チェックを行わない
;-------------------------------------------------
;CALL SAMEN_CHECK_T, 0

;CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF PENIS(TARGET)
	SETBIT STAIN:ペニス, 7
ELSEIF VAGINA(TARGET)
	SETBIT STAIN:ヴァギナ, 7
ENDIF
SIF TEQUIP:下半身下着 && !TEQUIP:トイレ
	SETBIT STAIN:下着, 7

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;放尿経験
CALL EXPUP_BASE, 1, 3

CALL EXPUP, "放尿経験", 2
TEQUIP:利尿剤 -= 2
SIF TEQUIP:利尿剤 <= 0
	TEQUIP:利尿剤 = 0

SIF 撮影中()
	CFLAG:放尿を撮影された経験++
SIF TEQUIP:野外プレイ
	CFLAG:野外放尿経験++
SIF TEQUIP:お風呂場
	CFLAG:風呂場放尿経験++

CALL 獣人催淫, 1, PLAYER

RETURN 1
