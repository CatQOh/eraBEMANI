;-------------------------------------------------
;鞭
;-------------------------------------------------
@USERCOM_ABLE_鞭, ARG
#FUNCTION
;上級売春中は方針がSMで客がマゾじゃないとダメ
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	RETURNF 0
;アイテムを持っていないとダメ
IF 上級売春中
	SIF !ITEM:娼館鞭
		RETURNF 0
ELSE
	SIF !ITEM:鞭
		RETURNF 0
ENDIF
;従順4未満のASSIの場合鞭はできない（サドならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_鞭
PRINTL 鞭
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 50

SOURCE:苦痛 = 1000
SOURCE:恐怖 = 800
SOURCE:逸脱 = 100
SOURCE:反感 = 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 4

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

