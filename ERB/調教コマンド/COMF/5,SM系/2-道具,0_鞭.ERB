;-------------------------------------------------
;鞭
;-------------------------------------------------
@USERCOM_ABLE_鞭, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針がSMで客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がSMで客に[マゾ]が必要/"
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[鞭]が必要/"
IF 上級売春中
	SIF !ITEM:娼館鞭
		COMRESULT = 0
ELSE
	SIF !ITEM:鞭
		COMRESULT = 0
ENDIF
;従順4未満のASSIの場合鞭はできない（サドならＯＫ）
コマンド実行条件 += "実行者が助手の場合、助手に[サド]or従順LV4以上必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		COMRESULT = 0
ENDIF
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

RETURNF COMRESULT

@COM_鞭
PRINTL 鞭
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:苦痛 = 1000
SOURCE:恐怖 = 800
SOURCE:逸脱 = 100
SOURCE:反感 = 600

SELECTCASE SAVESTR:鞭
	CASE "強力な鞭"
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 150
		SOURCE:苦痛 += 2000
		SOURCE:恐怖 += 1000
		SOURCE:逸脱 += 400
		SOURCE:反感 += 1400
	CASE "電流鞭"
		LOSEBASE:体力 += 400
		LOSEBASE:気力 += 250
		SOURCE:苦痛 += 7000
		SOURCE:恐怖 += 4200
		SOURCE:逸脱 += 1900
		SOURCE:反感 += 3000
		SIF CSTR:種族 == "機械"
			TIMES SOURCE:苦痛 , 8.00
	CASE "粘液鞭"
		IF CSTR:PLAYER:種族 == "スライム"
			LOSEBASE:体力 += 100
			LOSEBASE:気力 += 150
			SOURCE:苦痛 += 4000
			SOURCE:恐怖 += 2000
			SOURCE:逸脱 += 1100
			SOURCE:反感 += 1900
			CFLAG:PLAYER:スライム度 += RAND(10, 25)
		ELSE
			LOSEBASE:体力 -= 50
			LOSEBASE:気力 -= 30
			SOURCE:苦痛 -= 900
			SOURCE:恐怖 -= 300
			SOURCE:逸脱 += 50
			SOURCE:反感 -= 100
			CFLAG:PLAYER:スライム度 += RAND(1, 3)
		ENDIF
	CASE "ヴァンパイアキラー"
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 150
		SOURCE:苦痛 += 2000
		SOURCE:恐怖 += 1000
		SOURCE:逸脱 += 400
		SOURCE:反感 += 1400
		IF CSTR:種族 == "吸血鬼"
			LOSEBASE:体力 += 500
			LOSEBASE:気力 += 250
			TIMES SOURCE:苦痛 , 10.00
			TIMES SOURCE:恐怖 , 2.00
		ELSEIF CSTR:種族 == "ゾンビ"
			LOSEBASE:体力 += 300
			LOSEBASE:気力 += 200
			TIMES SOURCE:苦痛 , 10.00
			TIMES SOURCE:恐怖 , 2.00
		ELSEIF CSTR:種族 == "獣人" || CSTR:種族 == "スライム"
			LOSEBASE:体力 += 100
			LOSEBASE:気力 += 50
			TIMES SOURCE:苦痛 , 3.00
		ENDIF
ENDSELECT

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

RETURN 1

