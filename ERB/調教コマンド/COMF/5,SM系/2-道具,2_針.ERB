;-------------------------------------------------
;針
;-------------------------------------------------
@USERCOM_ABLE_針, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;アイテムを持っているか、アイテムなし設定でないとダメ
コマンド実行条件 += "アイテム[針]が必要/"
SIF !ITEM:針
	COMRESULT = 0
;助手の場合従順5未満だとでないとダメ（サド、針さばきならＯＫ）
コマンド実行条件 += "実行者が助手の場合、[サド]or[針さばき]or従順LV6以上必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 5 && !TALENT:ASSI:サド && !TALENT:ASSI:針さばき
		COMRESULT = 0
ENDIF
;全裸でないとダメ（足元、特殊は除く）
コマンド実行条件 += "対象が服を着ていると実行できない/"
SIF OVER_BUST(TARGET) != "" || OVER_GENITAL(TARGET) != ""
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

RETURNF COMRESULT

@COM_針
PRINTL 針
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 100

SOURCE:苦痛 = 2000
SOURCE:恐怖 = 1500
SOURCE:逸脱 = 500
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

RETURN 1

