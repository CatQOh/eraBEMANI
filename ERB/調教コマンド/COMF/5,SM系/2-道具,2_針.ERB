;-------------------------------------------------
;針
;-------------------------------------------------
@USERCOM_ABLE_針, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF !ITEM:針
	RETURNF 0
;助手の場合従順5未満だとでないとダメ（サド、針さばきならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 5 && !TALENT:ASSI:サド && !TALENT:ASSI:針さばき
		RETURNF 0
ENDIF
;全裸でないとダメ（足元、特殊は除く）
SIF OVER_BUST(TARGET) != "" || OVER_GENITAL(TARGET) != ""
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_針
PRINTL 針
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 100

SOURCE:苦痛 = 2000
SOURCE:恐怖 = 1500
SOURCE:逸脱 = 500
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 4

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

