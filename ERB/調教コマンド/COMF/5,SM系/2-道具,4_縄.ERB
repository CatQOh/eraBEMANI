;-------------------------------------------------
;縄
;-------------------------------------------------
@USERCOM_ABLE_縄, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針がSMで客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がSMで客に[マゾ]が必要/"
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:縄 && COMRESULT
	条件確定 = 1

;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[縄]が必要/"
IF 上級売春中
	SIF !ITEM:娼館縄
		COMRESULT = 0
ELSE
	SIF !ITEM:縄
		COMRESULT = 0
ENDIF
;調教者の技巧が1無いとできない
コマンド実行条件 += "実行者に技巧LV1以上必要/"
SIF ABL:PLAYER:技巧 < 1
	COMRESULT = 0
;助手は技巧3でないとダメ
コマンド実行条件 += "実行者が助手の場合、技巧LV3以上必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:技巧 < 3
		COMRESULT = 0
ENDIF

;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_縄
IF TEQUIP:触手
	PRINTL 触手緊縛
	SELECTCOM = 166
ELSE
	PRINTL 縄
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:縄, 0
;触手緊縛は触手の汚れを追加
IF TEQUIP:触手
	SETBIT STAIN:手, 5
	SETBIT STAIN:胸, 5
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 80
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 50

SOURCE:苦痛 = 300
SOURCE:恐怖 = 900
SOURCE:露出 = 300
SOURCE:屈従 = 250
SOURCE:逸脱 = 300
SOURCE:反感 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES LOSEBASE:体力 , 1.00
		TIMES LOSEBASE:気力 , 1.00
	CASE 3
		TIMES LOSEBASE:体力 , 0.90
		TIMES LOSEBASE:気力 , 0.90
	CASE 4
		TIMES LOSEBASE:体力 , 0.70
		TIMES LOSEBASE:気力 , 0.70
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

CALL EXPUP, "緊縛経験", 5

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_縄
#DIM DYNAMIC 消費量

IF TEQUIP:触手
	PRINTL ＜触手緊縛中＞
ELSE
	PRINTL ＜緊縛中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30

SOURCE:苦痛 += 100
SOURCE:恐怖 += 400
SOURCE:露出 += 100
SOURCE:屈従 += 150
SOURCE:逸脱 += 150
SOURCE:反感 += 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
消費量 = 60
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES 消費量 , 1.00
	CASE 3
		TIMES 消費量 , 0.90
	CASE 4
		TIMES 消費量 , 0.70
ENDSELECT
LOSEBASE:気力 += 消費量

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF SELECTCOMS != "縄"
	CALL EXPUP, "緊縛経験", 1

;触手経験加算
SIF TEQUIP:触手
	TENTACLE++

RETURN 1
