?;-------------------------------------------------
;ボールギャグ
;-------------------------------------------------
@USERCOM_ABLE_ボールギャグ, ARG
#FUNCTION
#DIM DYNAMIC 条件確定

;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は不可
コマンド実行条件 += "上級売春中は実行不可/"
SIF 上級売春中
	COMRESULT = 0
;触手口辱中だとダメ
コマンド実行条件 += "触手口辱実行中は実行できない/"
SIF TEQUIP:触手口辱
	COMRESULT = 0
;解除はいつでも可能
SIF TEQUIP:ボールギャグ && COMRESULT
	条件確定 = 1

;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[ボールギャグ]が必要/"
SIF !ITEM:ボールギャグ
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_ボールギャグ
PRINTL ボールギャグ
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:ボールギャグ, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 50

SOURCE:恐怖 = 50
SOURCE:露出 = 30
SOURCE:屈従 = 50
SOURCE:逸脱 = 70
SOURCE:反感 = 150

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES LOSEBASE:体力 , 1.00
		TIMES LOSEBASE:気力 , 1.00
	CASE 3
		TIMES LOSEBASE:体力 , 0.90
		TIMES LOSEBASE:気力 , 0.90
	CASE 4
		TIMES LOSEBASE:体力 , 0.70
		TIMES LOSEBASE:気力 , 0.70
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 3

CALL EXPUP, "緊縛経験", 2

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_ボールギャグ
PRINTL ＜ボールギャグ装備中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5

SOURCE:恐怖 += 150
SOURCE:露出 += 150
SOURCE:屈従 += 75
SOURCE:逸脱 += 50
SOURCE:反感 += 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
LOCAL = 25
SELECTCASE GETEXPLV(EXP:緊縛経験, 4)
	CASE IS <= 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 0.90
	CASE 4
		TIMES LOCAL , 0.70
ENDSELECT
LOSEBASE:気力 += LOCAL

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF SELECTCOMS != "ボールギャグ"
	CALL EXPUP, "緊縛経験", 1

RETURN 1

