;-------------------------------------------------
;尿道カテーテル
;-------------------------------------------------
@USERCOM_ABLE_尿道カテーテル, ARG
#FUNCTION
;上級売春中は不可
SIF 上級売春中
	RETURNF 0
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;アイテムないとダメ
SIF !ITEM:尿道カテーテル
	RETURNF 0
;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0
;解除はいつでも可能
IF TEQUIP:尿道カテーテル
	COMRESULT = 1
	RETURNF 1
ENDIF
;ペニバン使用中はダメ
SIF TEQUIP:ペニスバンド
	RETURNF 0
;オナホ使用中もダメ
SIF TEQUIP:オナホール
	RETURNF 0
;搾精機
SIF TEQUIP:搾精器
	RETURNF 0
;避妊具
SIF TEQUIP:コンドーム || TEQUIP:搾精コンドーム
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_尿道カテーテル
PRINTL 尿道カテーテル
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:尿道カテーテル, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 50
LOSEBASE:気力 += 80

SOURCE:屈従 = 200
SOURCE:苦痛 = 1000
SOURCE:恐怖 = 500
SOURCE:露出 = 300
SOURCE:逸脱 = 1000
SOURCE:反感 = 800
SOURCE:不潔 = 500

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, "尿道経験", 2
SIF PENIS(TARGET)
	CALL EXPUP, "射精管理経験", 1

;利尿剤効果は0になる
SIF TEQUIP:尿道カテーテル
	TEQUIP:利尿剤 = 0

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_尿道カテーテル
PRINTL ＜尿道カテーテル挿入中＞

LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

SOURCE:屈従 = 100
SOURCE:苦痛 = 700
SOURCE:恐怖 = 300
SOURCE:露出 = 100
SOURCE:逸脱 = 300
SOURCE:反感 = 500
SOURCE:不潔 = 200

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF SELECTCOMS != "尿道カテーテル"
	CALL EXPUP, "尿道経験", 1

RETURN 1

