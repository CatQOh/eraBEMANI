?;-------------------------------------------------
;腹パン
;-------------------------------------------------
@USERCOM_ABLE_腹パン, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針がSMで客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は経営方針がSMで客に[マゾ]が必要/"
SIF 上級売春中 && (娼館方針 != 3 || !TALENT:マゾ)
	COMRESULT = 0
;妊娠中は調教者がサドもしくは禁断の知識持ちじゃないとできない
コマンド実行条件 += "対象が妊娠中の場合、実行者が主人で[サド]or[禁断の知識]が必要/"
IF TALENT:妊娠
	SIF ASSIPLAY
		COMRESULT = 0
	SIF !TALENT:MASTER:サド && !TALENT:MASTER:禁断の知識
		COMRESULT = 0
ENDIF
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_腹パン
PRINTL 腹パン
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF TALENT:妊娠 && RAND:4 == 0
	;消費体力、気力
	LOSEBASE:体力 += 3500
	LOSEBASE:気力 += 5000
	SOURCE:苦痛 = 30000
	SOURCE:恐怖 = 6000
	SOURCE:屈従 = 500
	SOURCE:逸脱 = 10000
	SOURCE:反感 = 10000
	SETCOLOR 255 , 0 , 0
		FONTBOLD
			PRINTW 流産
		FONTREGULAR
	RESETCOLOR
	TFLAG:流産 = 1
	TALENT:妊娠 = 0
ELSE
	;消費体力、気力
	LOSEBASE:体力 += 150
	LOSEBASE:気力 += 100

	SIF CFLAG:陥落
		SOURCE:情愛 = 100
	SOURCE:苦痛 = 1000
	SOURCE:恐怖 = 1000
	SOURCE:屈従 = 150
	SOURCE:逸脱 = 500
	SOURCE:反感 = 1500
ENDIF

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

;実績:大犬のお姉さんに暴力を振るう
SIF CHARANAMEF(TARGET) == "大犬のお姉さん"
	CALL 実績解除, "何をするだァーッ！"


RETURN 1

