;-------------------------------------------------
;フィストファック
;-------------------------------------------------
@USERCOM_ABLE_フィストファック, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針が狂気じゃないとダメ
SIF 上級売春中 && 娼館方針 != 4
	RETURNF 0
;奴隷が淫乱系陥落か服従系陥落じゃないとダメ
SIF !上級売春中 && !GROUPMATCH(FALLTYPE(TARGET), 2, 3)
	RETURNF 0
;一定のＶ経験が必要 PLAYERが小柄なら手も小さいので緩和
IF TALENT:PLAYER:小柄体型
	SIF EXP:Ｖ経験 < 100
		RETURNF 0
ELSE
	SIF EXP:Ｖ経験 < 150
		RETURNF 0
ENDIF
;膣に何か入ってるとダメ
SIF Ｖ使用中(TARGET)
	RETURNF 0
;ヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURNF 0
;ASSIの場合、従順4以上かサドじゃないとできない。
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
	RETURNF 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURNF 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURNF 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_フィストファック
PRINTL フィストファック
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 200

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 300
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 30

SOURCE:快V = 200
SOURCE:苦痛 = 1500
SOURCE:恐怖 = 500
SOURCE:露出 = 100
SOURCE:屈従 = 1200
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;フィストなどのVへの大きな苦痛は先に処理しておく
CALL 特殊ソース処理_Ｖ強

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の手の汚れが移動
STAIN:膣内 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ｖ経験
EXP:Ｖ経験 += 25
PRINTL Ｖ経験+25

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	EXP:異常経験 += 1
	PRINTL 異常経験+1
ENDIF

EXP:Ｖ拡張経験 += 5
PRINTL Ｖ拡張経験+5

CALL EXPUP_BASE, 2, 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

