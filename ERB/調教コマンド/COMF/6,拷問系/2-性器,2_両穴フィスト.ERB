;-------------------------------------------------
;両穴フィスト
;-------------------------------------------------
@USERCOM_ABLE_両穴フィスト, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針が狂気じゃないとダメ
SIF 上級売春中 && 娼館方針 != 4
	RETURNF 0
;奴隷が淫乱系陥落か服従系陥落じゃないとダメ
SIF !上級売春中 && !GROUPMATCH(FALLTYPE(TARGET), 2, 3)
	RETURNF 0
;膣に何か入ってるとダメ
SIF Ｖ使用中(TARGET)
	RETURNF 0
;アナルに何か入ってるとダメ
SIF Ａ使用中(TARGET)
	RETURNF 0
;ヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURNF 0
;300以上のＶ・Ａ経験が必要 PLAYERが小柄なら手も小さいので緩和
IF TALENT:PLAYER:小柄体型
	SIF EXP:Ｖ経験 < 150 || EXP:Ａ経験 < 150
		RETURNF 0
ELSE
	SIF EXP:Ｖ経験 < 300 || EXP:Ａ経験 < 300
		RETURNF 0
ENDIF
;ASSIの場合、従順4以上かサドじゃないとできない。
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上かつ拡張経験10以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4 || EXP:Ｖ拡張経験 < 10 || EXP:Ａ拡張経験 < 10
		RETURNF 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURNF 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_両穴フィスト
PRINTL 両穴フィスト
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 250

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 500
LOSEBASE:気力 += 300

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 30

SOURCE:快V = 200
SOURCE:快A = 200
SOURCE:苦痛 = 2500
SOURCE:恐怖 = 1500
SOURCE:露出 = 200
SOURCE:屈従 = 2000
SOURCE:逸脱 = 2000
SOURCE:反感 = 2000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
CALL 特殊ソース処理_Ｖ強
CALL 特殊ソース処理_Ａ強

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔調教者の手の汚れが移動
;奴隷の膣内⇔調教者の手の汚れが移動
SETBIT STAIN:PLAYER:手, 3
STAIN:PLAYER:手 |= STAIN:膣内
STAIN:PLAYER:手 |= STAIN:アナル
STAIN:アナル |= STAIN:PLAYER:手
STAIN:膣内 |= STAIN:PLAYER:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 6

CALL EXPUP, "Ｖ経験", 15
CALL EXPUP, "Ａ経験", 15

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	PRINTL ＜初Ｖ&Ａ拡張＞
	CALL EXPUP, "異常経験", 2
ENDIF

CALL EXPUP, "Ｖ拡張経験", 5
CALL EXPUP, "Ａ拡張経験", 5

RETURN 1

