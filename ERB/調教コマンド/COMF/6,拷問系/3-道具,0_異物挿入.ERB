;-------------------------------------------------
;異物挿入
;-------------------------------------------------
@USERCOM_ABLE_異物挿入, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;上級売春中は方針が狂気じゃないとダメ
コマンド実行条件 += "上級売春中は経営方針が狂気じゃないと実行できない/"
SIF 上級売春中 && 娼館方針 != 4
	COMRESULT = 0
;触手召喚中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;V感覚もしくはA感覚が6以上、もしくは拡張経験200以上必要
コマンド実行条件 += "対象にＶ感覚LV6以上orＡ感覚6以上orＶ拡張経験200以上orＡ拡張経験200以上が必要/"
SIF ABL:Ｖ感覚 < 6 && ABL:Ａ感覚 < 6 && EXP:Ｖ拡張経験 < 200 && EXP:Ａ拡張経験 < 200
	COMRESULT = 0
;両穴がふさがってると実行できない
コマンド実行条件 += "挿入可能な穴が使用中だと実行できない/"
SIF Ｖ使用中(TARGET) && Ａ使用中(TARGET)
	COMRESULT = 0
;挿入できる穴がふさがってると実行できない
IF ABL:Ｖ感覚 >= 6 || EXP:Ｖ拡張経験 >= 200
	SIF ABL:Ａ感覚 < 6 && EXP:Ａ拡張経験 < 200 && Ｖ使用中(TARGET)
		COMRESULT = 0
ENDIF
IF ABL:Ａ感覚 >= 6 || EXP:Ａ拡張経験 >= 200
	SIF ABL:Ｖ感覚 < 6 && EXP:Ｖ拡張経験 < 200 && Ａ使用中(TARGET)
		COMRESULT = 0
ENDIF

;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0

RETURNF COMRESULT

@COM_異物挿入
#DIM DYNAMIC LABL
#DIM DYNAMIC LEXP
#DIM DYNAMIC LRESULT
#DIM DYNAMIC 増加量
#DIM DYNAMIC 挿入箇所

PRINTL 異物挿入

IF VAGINA(TARGET)
	IF (ABL:Ｖ感覚 >= 6 || EXP:Ｖ拡張経験 >= 200) && ((ABL:Ａ感覚 < 6 && EXP:Ａ拡張経験 < 200) || TEQUIP:Ａ異物)
		LABL = GETNUM(ABL, "Ｖ感覚")
		LEXP = GETNUM(EXP, "Ｖ拡張経験")
	ELSEIF (ABL:Ａ感覚 >= 6 || EXP:Ａ拡張経験 >= 200) && ((ABL:Ｖ感覚 < 6 && EXP:Ｖ拡張経験 < 200) || TEQUIP:Ｖ異物)
		LABL = GETNUM(ABL, "Ａ感覚")
		LEXP = GETNUM(EXP, "Ａ拡張経験")
	ELSE
		PRINTL どちらの穴に挿入しますか？
		PRINTL [0] - 膣穴
		PRINTL [1] - 尻穴
		PRINTL [2] - やめる
		DO
			SIF ISSKIP()
				BREAK
			INPUT
		LOOP LOOPRES(0, 1, 2)
		
		SELECTCASE RESULT
			CASE 0
				LABL = GETNUM(ABL, "Ｖ感覚")
				LEXP = GETNUM(EXP, "Ｖ拡張経験")
			CASE 1
				LABL = GETNUM(ABL, "Ａ感覚")
				LEXP = GETNUM(EXP, "Ａ拡張経験")
			CASE 2
				RETURN 0
		ENDSELECT
	ENDIF
ELSE
	LABL = GETNUM(ABL, "Ａ感覚")
	LEXP = GETNUM(EXP, "Ａ拡張経験")
ENDIF

IF LABL == GETNUM(ABL, "Ｖ感覚")
	PRINT 膣穴
ELSEIF LABL == GETNUM(ABL, "Ａ感覚")
	PRINT 尻穴
ENDIF
PRINTL に何を挿入しますか？
SIF ABL:LABL >= 6 || EXP:LEXP >= 200
	PRINTL [1] - コーヒー缶
SIF ABL:LABL >= 7 || EXP:LEXP >= 250
	PRINTL [2] - ペットボトル
SIF ABL:LABL >= 8 || EXP:LEXP >= 300
	PRINTL [3] - 空きビン
PRINTL [4] - やめる

DO
	SIF ISSKIP()
		BREAK
	INPUT
	SELECTCASE RESULT
		CASE 1 TO 3
			SIF RESULT+5 > ABL:LABL && RESULT*50+150 > EXP:LEXP
				CONTINUE
		CASE 4
			RETURN 0
	ENDSELECT
LOOP LOOPRES(1, 2, 3)

SELECTCASE LABL
	CASE GETNUM(ABL, "Ｖ感覚")
		TEQUIP:Ｖ異物 = RESULT
		TFLAG:膣コマンド = 1
	CASE GETNUM(ABL, "Ａ感覚")
		TEQUIP:Ａ異物 = RESULT
ENDSELECT

TFLAG:異物挿入 = LABL
LRESULT = RESULT

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
IF TFLAG:異物挿入 == GETNUM(ABL, "Ｖ感覚")
	増加量 = 射精ゲージ計算_Ｖ道具(50)
	;PALAM:潤滑をみる
	増加量 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(増加量/5)
	;奴隷のABL:マゾっ気をみる
	増加量 += (増加量/5)*ABL:マゾっ気
ELSEIF TFLAG:異物挿入 == GETNUM(ABL, "Ａ感覚")
	増加量 = 射精ゲージ計算_Ａ挿入(50)
	;ABL:調教者の技巧をみる
	増加量 += (増加量/5)*ABL:PLAYER:技巧
	;ABL:自慰中毒をみる
	SELECTCASE ABL:自慰中毒
		CASE 0
			TIMES 増加量 , 1.00
		CASE 1
			TIMES 増加量 , 1.10
		CASE 2
			TIMES 増加量 , 1.30
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.00
		CASEELSE
			増加量 += (増加量/3)*ABL:自慰中毒
	ENDSELECT
ENDIF

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力 SOURCE:V拡張、A拡張で増加する
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50
;快V、快A、苦痛、恐怖、反感は拡張のソースで上乗せする
SELECTCASE TFLAG:異物挿入
	CASE GETNUM(ABL, "Ｖ感覚")
		SOURCE:快V = 100
	CASE GETNUM(ABL, "Ａ感覚")
		SOURCE:快A = 100
ENDSELECT
SOURCE:苦痛 = 500
SOURCE:恐怖 = 100
SOURCE:反感 = 100

SELECTCASE LRESULT
	CASE 1
		SELECTCASE TFLAG:異物挿入
			CASE GETNUM(ABL, "Ｖ感覚")
				SOURCE:V拡張 = 50
			CASE GETNUM(ABL, "Ａ感覚")
				SOURCE:A拡張 = 50
		ENDSELECT
		SOURCE:露出 = 50
		SOURCE:逸脱 = 100
	CASE 2
		SELECTCASE TFLAG:異物挿入
			CASE GETNUM(ABL, "Ｖ感覚")
				SOURCE:V拡張 = 100
			CASE GETNUM(ABL, "Ａ感覚")
				SOURCE:A拡張 = 100
		ENDSELECT
		SOURCE:露出 = 50
		SOURCE:逸脱 = 150
	CASE 3
		SELECTCASE TFLAG:異物挿入
			CASE GETNUM(ABL, "Ｖ感覚")
				SOURCE:V拡張 = 200
			CASE GETNUM(ABL, "Ａ感覚")
				SOURCE:A拡張 = 200
		ENDSELECT
		SOURCE:露出 = 50
		SOURCE:逸脱 = 200
	CASE 4
		SELECTCASE TFLAG:異物挿入
			CASE GETNUM(ABL, "Ｖ感覚")
				SOURCE:V拡張 = 300
			CASE GETNUM(ABL, "Ａ感覚")
				SOURCE:V拡張 = 300
		ENDSELECT
		SOURCE:露出 = 50
		SOURCE:逸脱 = 250
ENDSELECT

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TFLAG:異物挿入 == GETNUM(ABL, "Ｖ感覚")
	IF TALENT:処女
		SOURCE:苦痛 += 2000
		SOURCE:反感 += 3000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:再生処女
		SOURCE:苦痛 += 500
		SOURCE:反感 += 1000
	ENDIF
ENDIF

;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 += GETPALAMLV(PALAM:苦痛, 4)*(SOURCE:苦痛/2)

;拡張系のソース処理
SELECTCASE TFLAG:異物挿入
	CASE GETNUM(ABL, "Ｖ感覚")
		CALL 特殊ソース処理_Ｖ拡張
	CASE GETNUM(ABL, "Ａ感覚")
		CALL 特殊ソース処理_Ａ拡張
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF TEQUIP:Ｖ異物
	挿入箇所 = GETNUM(ABL, "Ｖ感覚")
ELSEIF TEQUIP:Ａ異物
	挿入箇所 = GETNUM(ABL, "Ａ感覚")
ENDIF

IF TEQUIP:Ｖ異物 && TEQUIP:Ａ異物
	IF ABL:Ｖ感覚 > ABL:Ａ感覚
		挿入箇所 = GETNUM(ABL, "Ｖ感覚")
	ELSEIF ABL:Ａ感覚 > ABL:Ｖ感覚
		挿入箇所 = GETNUM(ABL, "Ａ感覚")
	ELSE
		挿入箇所 = GETNUM(ABL, "Ｖ感覚")
	ENDIF
ENDIF

CALL SAMEN_CHECK_T, 挿入箇所
CALL SAMEN_SHOOT_T, 挿入箇所, RESULT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 4

SELECTCASE TFLAG:異物挿入
	CASE GETNUM(ABL, "Ｖ感覚")
		PRINTFORML Ｖ経験+{TEQUIP:Ｖ異物*2}
		EXP:Ｖ経験 += (TEQUIP:Ｖ異物*2)
	CASE GETNUM(ABL, "Ａ感覚")
		PRINTFORML Ａ経験+{TEQUIP:Ａ異物*2}
		EXP:Ａ経験 += (TEQUIP:Ａ異物*2)
ENDSELECT

IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	SELECTCASE TFLAG:異物挿入
		CASE GETNUM(ABL, "Ｖ感覚")
			PRINTL ＜初Ｖ拡張＞
		CASE GETNUM(ABL, "Ａ感覚")
			PRINTL ＜初Ａ拡張＞
	ENDSELECT
	CALL EXPUP, "異常経験", 1
ENDIF

SELECTCASE TFLAG:異物挿入
	CASE GETNUM(ABL, "Ｖ感覚")
		CALL EXPUP, "Ｖ拡張経験", TEQUIP:Ｖ異物+2
	CASE GETNUM(ABL, "Ａ感覚")
		CALL EXPUP, "Ａ拡張経験", TEQUIP:Ａ異物+2
ENDSELECT

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_異物挿入
#DIM DYNAMIC 増加量

;異物挿入中
;V経験を伴うコマンドのフラグ(処女膜再生対応)
IF TEQUIP:Ｖ異物
	PRINTL ＜ヴァギナ異物挿入中＞
	TFLAG:膣コマンド = 1
ENDIF
SIF TEQUIP:Ａ異物
	PRINTL ＜アナル異物挿入中＞

IF TEQUIP:Ｖ異物
	SOURCE:V拡張 += (TEQUIP:Ｖ異物*50)
	SOURCE:露出 += 30
	SOURCE:逸脱 += (TEQUIP:Ｖ異物*20)
	
	CALL 特殊ソース処理_Ｖ道具拡張, 50, 30, 10
ENDIF
IF TEQUIP:Ａ異物
	SOURCE:A拡張 += (TEQUIP:Ａ異物*50)
	SOURCE:露出 += 30
	SOURCE:逸脱 += (TEQUIP:Ａ異物*20)
	
	CALL 特殊ソース処理_Ａ道具拡張, 50, 30, 10
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 20

;ABL:調教者の技巧をみる
SELECTCASE ABL:PLAYER:技巧
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.20
	CASE 2
		TIMES 増加量 , 1.40
	CASE 3
		TIMES 増加量 , 1.60
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		TIMES 増加量 , 2.00
ENDSELECT

;ABL:従順をみる
SELECTCASE ABL:従順
	CASE 0
		TIMES 増加量 , 0.80
	CASE 1
		TIMES 増加量 , 0.90
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.10
	CASE 4
		TIMES 増加量 , 1.20
	CASEELSE
		TIMES 増加量 , 1.30
ENDSELECT

;ABL:欲望をみる
SELECTCASE ABL:欲望
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.10
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.30
	CASE 4
		TIMES 増加量 , 1.40
	CASEELSE
		TIMES 増加量 , 1.50
ENDSELECT

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(増加量/5)

;奴隷のABL:感覚をみる
IF TEQUIP:Ｖ異物
	SELECTCASE ABL:Ｖ感覚
		CASE 0
			TIMES 増加量 , 0.50
		CASE 1
			TIMES 増加量 , 0.80
		CASE 2
			TIMES 増加量 , 1.00
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.00
		CASEELSE
			TIMES 増加量 , 3.00
	ENDSELECT
ENDIF
IF TEQUIP:Ａ異物
	SELECTCASE ABL:Ａ感覚
		CASE 0
			TIMES 増加量 , 0.50
		CASE 1
			TIMES 増加量 , 0.80
		CASE 2
			TIMES 増加量 , 1.00
		CASE 3
			TIMES 増加量 , 1.50
		CASE 4
			TIMES 増加量 , 2.00
		CASEELSE
			TIMES 増加量 , 3.00
	ENDSELECT
ENDIF

;奴隷のABL:マゾっ気をみる
SELECTCASE ABL:マゾっ気
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.20
	CASE 2
		TIMES 増加量 , 1.40
	CASE 3
		TIMES 増加量 , 1.60
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		TIMES 増加量 , 2.00
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	増加量 /= 20

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験+1
SELECTCASE SELECTCOMS
	CASE "自慰"
		SIF TEQUIP:Ｖ異物
			CALL EXPUP, "Ｖ経験", 3
		SIF TEQUIP:Ａ異物
			CALL EXPUP, "Ａ異物", 3
	CASEELSE
		SIF TEQUIP:Ｖ異物 && TFLAG:異物挿入 != GETNUM(ABL, "Ｖ感覚")
			CALL EXPUP, "Ｖ経験", 2
		SIF TEQUIP:Ａ異物 && TFLAG:異物挿入 != GETNUM(ABL, "Ａ感覚")
			CALL EXPUP, "Ａ経験", 2
ENDSELECT

SIF TEQUIP:Ｖ異物 && TFLAG:異物挿入 != GETNUM(ABL, "Ｖ感覚")
	CALL EXPUP, "Ｖ拡張経験", 1
SIF TEQUIP:Ａ異物 && TFLAG:異物挿入 != GETNUM(ABL, "Ａ感覚")
	CALL EXPUP, "Ａ拡張経験", 1

RETURN 1

