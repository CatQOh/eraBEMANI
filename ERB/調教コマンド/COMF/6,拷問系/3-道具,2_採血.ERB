;-------------------------------------------------
;採血
;-------------------------------------------------
@USERCOM_ABLE_採血, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中はダメ
SIF 上級売春中
	RETURNF 0
;触手召喚中は別コマンド
SIF TEQUIP:触手
	RETURNF 0
;解除はいつでも可能
SIF TEQUIP:採血
	RETURNF 1

;血液パックが必要
SIF !ITEM:血液パック
	RETURNF 0
;対象が無機物、もしくは種族:機械だとダメ
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURNF 0
;対象がゾンビだとダメ
SIF TALENT:ゾンビ
	RETURNF 0
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0
;風呂場では実行できない
SIF TEQUIP:お風呂場
	RETURNF 0
;技巧4以上、針さばき、治療、禁断の知識いずれかが必要
SIF ABL:PLAYER:技巧 < 4 && !TALENT:PLAYER:針さばき && !TALENT:PLAYER:治療 && !TALENT:PLAYER:禁断の知識
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_採血
PRINTL 採血

INVERTBIT TEQUIP:採血, 0

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力 継続処理関数で行う
IF TEQUIP:採血
	SOURCE:苦痛 = 1000
	SOURCE:恐怖 = 1500
	SOURCE:反感 = 1500
	SOURCE:不潔 = 500
	SOURCE:鬱屈 = 500

	DOWNBASE:MASTER:体力 += 30
	DOWNBASE:MASTER:気力 += 50
	ITEM:血液パック--
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 3

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM_採血
PRINTL ＜採血中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;採血のLOSEBASE:体力だけは軽減不可の特殊処理を行う(ソース計算を参照)
LOSEBASE:気力 += 100

SOURCE:苦痛 = 300
SOURCE:恐怖 = 1000
SOURCE:反感 = 1000
SOURCE:不潔 = 300
SOURCE:鬱屈 = 300

;1ターンで30ml
TFLAG:採血量 += 30

RETURN 1

