;-------------------------------------------------
;フィストファックさせる
;-------------------------------------------------
@USERCOM_ABLE_フィストファックさせる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針がSM、狂気じゃないとダメ
SIF 上級売春中 && !GROUPMATCH(娼館方針, 3, 4)
	RETURNF 0
;サド、もしくはサドっ気3以上必要(娼館方針が狂気ならスルー)
SIF !(上級売春中 && 娼館方針 == 4) && !TALENT:サド && ABL:サドっ気 < 3
	RETURNF 0

;実行者のＶ経験100以上orＶ感覚3以上
SIF EXP:PLAYER:Ｖ経験 < 100 && ABL:Ｖ感覚 <= 3
	RETURNF 0

;実行者がヴァギナ無いとダメ
SIF !VAGINA(PLAYER)
	RETURNF 0
;助手の場合は助手が陥落していないとダメ
SIF ASSIPLAY && !CFLAG:ASSI:陥落
	RETURNF 0
;調教者が小人体型のとき主人が禁断の知識持ちじゃないとダメ
SIF TALENT:PLAYER:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:小人体型
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;縄使用中は出来ない
SIF TEQUIP:縄
	RETURNF 0
;PLAYERがＶ使用中はダメ
SIF Ｖ使用中(PLAYER)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_フィストファックさせる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF

CALL COM_ORDER_欲情, 0
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "女嫌い"
			SIF SEX(PLAYER) == 2 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -12
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

LOCAL:1 = 0
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
	LOCAL:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
	LOCAL:1 += 6
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
	LOCAL:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(32以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 32
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_フィストファックさせる
PRINTL フィストファックさせる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷フラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(PLAYER) != ""
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 400

;ABL:技巧をみる
LOCAL += LOCAL/10*4*ABL:技巧

;ABL:奉仕精神をみる
LOCAL += (LOCAL/10)*ABL:奉仕精神

;プレイヤーのABL:Ｖ感覚をみる
SELECTCASE ABL:PLAYER:Ｖ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｖ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 70

DOWNBASE:MASTER:体力 += 120
DOWNBASE:MASTER:気力 += 100

SOURCE:恐怖 = 50
SOURCE:性行動 = 500
SOURCE:屈従 = 300
SOURCE:逸脱 = 1000
SOURCE:反感 = 500
SOURCE:中毒 = 200
;上の方で計算した汚れ
SOURCE:不潔 = LOCAL:1*60+40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

IF TALENT:PLAYER:処女
	IF TALENT:PLAYER:性転換済
		IF ASSIPLAY
			IF CFLAG:ASSI:陥落
				CFLAG:ASSI:性転換処女の奪われ方 = 2
			ELSEIF RELATION:ASSI:(NO:TARGET)
				CFLAG:ASSI:性転換処女の奪われ方 = 3
			ELSE
				CFLAG:ASSI:性転換処女の奪われ方 = 1
			ENDIF
		ENDIF
		VIRGIN:PLAYER:11 = %NAME:TARGET%
	ELSE
		IF ASSIPLAY
			IF CFLAG:ASSI:陥落
				CFLAG:ASSI:処女の奪われ方 = 2
			ELSEIF RELATION:ASSI:(NO:TARGET)
				CFLAG:ASSI:処女の奪われ方 = 3
			ELSE
				CFLAG:ASSI:処女の奪われ方 = 1
			ENDIF
		ENDIF
		VIRGIN:PLAYER:1 = %NAME:TARGET%
	ENDIF
ENDIF

IF TALENT:PLAYER:処女 || TALENT:PLAYER:再生処女
	PRINTFORMW %CALLNAME:PLAYER%処女喪失
	SETBIT STAIN:PLAYER:膣内, 6
ENDIF

TALENT:PLAYER:処女 = 0
TALENT:PLAYER:再生処女 = 0

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精時の処理
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手⇔調教者の膣内の汚れが移動
STAIN:手 |= STAIN:PLAYER:膣内
STAIN:PLAYER:膣内 |= STAIN:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;調教者のＶ経験
PRINTFORML Ｖ経験+4(%CALLNAME:PLAYER%)
EXP:PLAYER:Ｖ経験 += 4
PRINTFORML Ｖ拡張経験+3(%CALLNAME:PLAYER%)
EXP:PLAYER:Ｖ拡張経験 += 3
PRINTFORML 苦痛快楽経験+2(%CALLNAME:PLAYER%)
EXP:PLAYER:苦痛快楽経験 += 2

CALL EXPUP, 2, 2

PRINTL 嗜虐快楽経験+3
EXP:嗜虐快楽経験 += 3

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && !ASSIPLAY
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !ASSIPLAY
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

