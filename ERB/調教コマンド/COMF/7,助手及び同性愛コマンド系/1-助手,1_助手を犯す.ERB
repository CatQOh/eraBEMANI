;-------------------------------------------------
;助手を犯す
;-------------------------------------------------
@USERCOM_ABLE_助手を犯す, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;助手がいないとダメ
SIF ASSI < 1
	RETURNF 0
;主人じゃないとダメ
SIF PLAYER != MASTER
	RETURNF 0
;助手陥落していないとダメ
SIF !CFLAG:ASSI:陥落
	RETURNF 0
;助手が男ならＡ感覚3以上必要
SIF SEX(ASSI) == 1 && ABL:ASSI:Ａ感覚 < 3
	RETURNF 0
;小人体型持ちは、双方が小人体型ならOK
;それ以外の場合は助手が小人体型のとき主人が禁断の知識持ちじゃないとダメ
SIF TALENT:ASSI:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:MASTER:小人体型
	RETURNF 0
;主人がペニス有り、もしくはペニスバンドがないとダメ
SIF !PENIS(MASTER) && !TEQUIP:MASTER:ペニスバンド
	RETURNF 0
SIF Ｐ使用不可(MASTER)
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURNF 0
;助手がＶ使用中はダメ
SIF Ｖ使用中(ASSI)
	RETURNF 0

COMRESULT = 1
RETURNF 1

;ダミー関数
@実行判定_助手を犯す
RETURN 1

@COM_助手を犯す
IF VAGINA(ASSI)
	PRINTL 膣とアナルのどちらを犯しますか？
	PRINTL [1] - 膣
	PRINTL [2] - アナル
	DO
		SIF ISSKIP()
			BREAK
		INPUT
	LOOP LOOPRES(1, 2)
	TFLAG:主人挿入箇所 = RESULT
ELSE
	TFLAG:主人挿入箇所 = 2
ENDIF

PRINTL 助手を犯す
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 700

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:助手の技巧をみる
LOCAL += (LOCAL/10)*ABL:ASSI:技巧

;ABL:助手の従順をみる
LOCAL += (LOCAL/10)*ABL:ASSI:従順 - (LOCAL/10)*2

;ABL:助手の欲望をみる
LOCAL += (LOCAL/10)*ABL:ASSI:欲望

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;子供は締まりが良いので増える
SIF TALENT:ASSI:小柄体型
	TIMES LOCAL , 1.20

CALL 射精ゲージ増加, LOCAL, MASTER

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
LOCAL = 500

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:助手の技巧をみる
LOCAL += (LOCAL/10)*ABL:ASSI:技巧

;ABL:助手の従順をみる
LOCAL += (LOCAL/10)*ABL:ASSI:従順

;ABL:助手の欲望をみる
LOCAL += (LOCAL/5)*ABL:ASSI:欲望

;Ｖ感覚、Ａ感覚を見る
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		SELECTCASE ABL:PLAYER:Ｖ感覚
			CASE 0
				TIMES LOCAL , 1.00
			CASE 1
				TIMES LOCAL , 1.50
			CASE 2
				TIMES LOCAL , 2.00
			CASE 3
				TIMES LOCAL , 2.50
			CASE 4
				TIMES LOCAL , 3.50
			CASEELSE
				LOCAL += (LOCAL/2)*ABL:Ｖ感覚
		ENDSELECT
	CASE 2
		SELECTCASE ABL:PLAYER:Ａ感覚
			CASE 0
				TIMES LOCAL , 1.00
			CASE 1
				TIMES LOCAL , 1.50
			CASE 2
				TIMES LOCAL , 2.00
			CASE 3
				TIMES LOCAL , 2.50
			CASE 4
				TIMES LOCAL , 3.50
			CASEELSE
				LOCAL += (LOCAL/2)*ABL:Ａ感覚
		ENDSELECT
ENDSELECT

CALL 射精ゲージ増加, LOCAL, ASSI

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 150

SOURCE:情愛 = 100
SOURCE:液体 = 100
SOURCE:屈従 = 500
SOURCE:欲情 = 200
SOURCE:逸脱 = 700
SOURCE:反感 = 1000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抵抗 || TALENT:抑圧
	SOURCE:鬱屈 += 700

;助手が処女喪失をした場合、屈服と逸脱を増加
IF TALENT:ASSI:処女
	TIMES SOURCE:逸脱 , 20.00
	TIMES SOURCE:屈従 , 3.00
ENDIF

;助手が気持ちよさそうにしてれば欲情増加
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		SOURCE:欲情 += MIN(ABL:ASSI:Ｖ感覚, 10)*SOURCE:欲情/5
	CASE 2
		SOURCE:欲情 += MIN(ABL:ASSI:Ａ感覚, 10)*SOURCE:欲情/5
ENDSELECT
SIF CFLAG:ASSI:陥落
	TIMES SOURCE:欲情, 1.50

;助手と奴隷の相性によって、全体のソースが変化 欲情は例外
LOCAL = RELATION:(NO:ASSI)
SOURCE:情愛 *= LOCAL
SOURCE:液体 *= LOCAL
SOURCE:屈従 *= LOCAL
SOURCE:鬱屈 /= LOCAL
SOURCE:逸脱 /= LOCAL
SOURCE:反感 /= LOCAL
SOURCE:情愛 /= 100
SOURCE:液体 /= 100
SOURCE:屈従 /= 100
SOURCE:鬱屈 *= 100
SOURCE:逸脱 *= 100
SOURCE:反感 *= 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_M, 7
CALL SAMEN_CHECK_A, 6

CALL SAMEN_SHOOT, 7, NOWEX:PLAYER:射精, MASTER, ASSI
CALL SAMEN_SHOOT, 6, NOWEX:ASSI:射精, ASSI, MASTER

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
IF PENIS(MASTER)
	SELECTCASE TFLAG:主人挿入箇所
		CASE 1
			;主人のペニス⇔助手の膣内の汚れが移動
			STAIN:MASTER:ペニス |= STAIN:ASSI:膣内
			STAIN:ASSI:膣内 |= STAIN:MASTER:ペニス
		CASE 2
			;主人のペニスにアナル汚れ
			SETBIT STAIN:MASTER:ペニス, 3
			;主人のペニス⇔助手のアナルの汚れが移動
			STAIN:MASTER:ペニス |= STAIN:ASSI:アナル
			STAIN:ASSI:アナル |= STAIN:MASTER:ペニス
	ENDSELECT
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		;助手が処女の場合、助手と奴隷に異常経験付加
		IF TALENT:ASSI:処女
			TALENT:ASSI:処女 = 0
			PRINTL ＜助手処女喪失＞
			IF !CFLAG:助手の処女喪失を見せつけられた経験
				PRINTL ＜公開処女喪失＞
				IF RELATION:(NO:ASSI) > 100
					CALL EXPUP, "異常経験", 2
				ELSE
					CALL EXPUP, "異常経験", 1
				ENDIF
			ENDIF
			CFLAG:助手の処女喪失を見せつけられた経験++
			CALL EXPUP, "異常経験", 1, ASSI
			IF TALENT:ASSI:性転換済
				CFLAG:ASSI:性転換処女の奪われ方 = 2
				CSTR:ASSI:性転換処女 = %NAME:MASTER%
			ELSE
				CFLAG:ASSI:処女の奪われ方 = 2
				CSTR:ASSI:処女 = %NAME:MASTER%
			ENDIF
		ENDIF

		IF TALENT:MASTER:童貞
			IF TALENT:MASTER:性転換済
				CSTR:MASTER:性転換童貞 = %NAME:ASSI%
			ELSE
				CSTR:MASTER:童貞 = %NAME:ASSI%
			ENDIF
		ENDIF

		;助手のＶ経験
		CALL EXPUP, "Ｖ経験", 2, ASSI
		CALL EXPUP, "性交経験", 1, ASSI
		CALL EXPUP, "性交経験", 1, MASTER
	CASE 2
		;助手のＡ経験
		CALL EXPUP, "Ａ経験", 2, ASSI
		CALL EXPUP, "性交経験", 1, ASSI
		CALL EXPUP, "性交経験", 1, MASTER
ENDSELECT

;助手の同性愛経験
IF SEX(MASTER) == SEX(ASSI)
	SELECTCASE SEX(ASSI)
		CASE 1
			CALL EXPUP, "ゲイ経験", 3, ASSI
			CALL EXPUP, "ゲイ経験", 3, MASTER
		CASE 2
			CALL EXPUP, "レズ経験", 3, ASSI
			CALL EXPUP, "レズ経験", 3, MASTER
	ENDSELECT
ENDIF

;魔力回復
IF !避妊具(MASTER)
	CALL 魔力回復, 3, ASSI
	CALL 獣人催淫, 1, ASSI, MASTER
ENDIF

経験回数:MASTER:(NO:ASSI) += 1
経験回数:ASSI:(NO:MASTER) += 1

RETURN 1

