;-------------------------------------------------
;ダブルフェラ
;-------------------------------------------------
@USERCOM_ABLE_ダブルフェラ, ARG
#FUNCTION
;ダブルフェラ実行判定
;助手じゃないとダメ
SIF PLAYER != ASSI
	RETURNF 0
;主人がペニス無いとダメ
SIF !PENIS(MASTER)
	RETURNF 0
;主人が[魅惑]か[謎の魅力]、もしくは助手と奴隷が陥落していないとダメ
SIF !TALENT:MASTER:魅惑 && !TALENT:MASTER:謎の魅力 && (!CFLAG:陥落 || !CFLAG:ASSI:陥落)
	RETURNF 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURNF 0
;猫舌の助手の場合は技巧Lv2以上必要
SIF TALENT:ASSI:猫舌 && ABL:ASSI:技巧 < 2
	RETURNF 0
;助手が奉仕精神LV3以上、技巧LV3以上じゃないとダメ
SIF ABL:ASSI:技巧 < 3 || ABL:ASSI:奉仕精神 < 3
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_ダブルフェラ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

;PALAM:欲情
LOCAL:1 = GETPALAMLV(PALAM:欲情, 5)
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 *= 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "娼婦"
			LOCAL:1 = 10
		CASE "淫乱"
			LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -3
		CASE "献身的"
			LOCAL:1 = 6
		CASE "汚臭敏感"
			LOCAL:1 = -5
		CASE "汚臭鈍感"
			LOCAL:1 = 3
		CASE "男嫌い"
			SIF SEX(MASTER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -12
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 4)
	LOCAL:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(34以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 34
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, コマンド拒否時
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_ダブルフェラ
PRINTL ダブルフェラ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 400

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 1.50

;ABL:助手の技巧をみる
LOCAL += (LOCAL/5)*ABL:ASSI:技巧

;ABL:助手の従順をみる
LOCAL += (LOCAL/10)*ABL:ASSI:従順 - (LOCAL/10)*2

;ABL:助手の奉仕精神をみる
SELECTCASE ABL:ASSI:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;助手のTALENT:舌使い、猫舌をみる
SIF TALENT:ASSI:舌使い || TALENT:猫舌
	TIMES LOCAL , 1.50

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, MASTER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 100

SOURCE:情愛 = 200
SOURCE:性行動 = 700
SOURCE:達成 = 200
SOURCE:屈従 = 600
SOURCE:逸脱 = 200
SOURCE:反感 = 200

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*30+100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精するのは主人なので専用の関数を呼ぶ
CALL SAMEN_CHECK_M, 4
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 4, NOWEX:MASTER:射精, MASTER
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
PRINTL フェラ経験+1
EXP:フェラ経験++
PRINTFORML フェラ経験+1(%CALLNAME:ASSI%)
EXP:ASSI:フェラ経験++

;同性愛経験
IF SEX(TARGET) == SEX(MASTER) && SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			PRINTL ゲイ経験+7
			EXP:ゲイ経験 += 7
		CASE 2
			PRINTL レズ経験+7
			EXP:レズ経験 += 7
	ENDSELECT
ELSEIF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			PRINTL ゲイ経験+4
			EXP:ゲイ経験 += 4
		CASE 2
			PRINTL レズ経験+4
			EXP:レズ経験 += 4
	ENDSELECT
ENDIF

;調教経験
PRINTL 主人調教経験+2
EXP:主人調教経験 += 2
PRINTL 助手調教経験+1
EXP:助手調教経験 += 1

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTL 嗜虐快楽経験+1
ENDIF
;助手が猫舌持ちの場合はry
IF TALENT:ASSI:猫舌
	EXP:ASSI:嗜虐快楽経験 += 1
	PRINTL 嗜虐快楽経験+1
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:PLAYER:種族
	CASE "獣人", "獣"
		CFLAG:媚薬++
		CFLAG:ASSI:媚薬++
		SIF CFLAG:媚薬%5 == 0
			CFLAG:残数++
		SIF CFLAG:ASSI:媚薬%5 == 0
			CFLAG:ASSI:残数++
ENDSELECT

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

