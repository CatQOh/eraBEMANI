;-------------------------------------------------
;兜合わせ
;-------------------------------------------------
@USERCOM_ABLE_兜合わせ, ARG
#FUNCTION
;ゲーセンに居る場合はトイレじゃないとできない
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	RETURNF 0
;上級売春中は客が男嫌いだとダメ
SIF 上級売春中 && TALENT:男嫌い
	RETURNF 0
;対象にペニス無いとダメ
SIF !PENIS(TARGET)
	RETURNF 0
;調教者にもペニス無いとダメ
SIF !PENIS(PLAYER)
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURNF 0

COMRESULT = 1
RETURNF 1

@実行判定_兜合わせ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*2
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

;PALAM:欲情
LOCAL:1 = GETPALAMLV(PALAM:欲情, 5)
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 *= 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "娼婦"
			LOCAL:1 = 10
		CASE "淫乱"
			LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -1
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -3
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -7
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	PRINT +媚薬(6)
ENDIF

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 2
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 4)
	LOCAL:1 += 2
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 20
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM_兜合わせ
PRINTL 兜合わせ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:下半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用 != 1)
	IF !CFLAG:挿入可
		TEQUIP:全身上着 = 0
		TEQUIP:下半身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 500

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 500

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 300
SOURCE:情愛 = 500
SOURCE:性行動 = 500
SOURCE:恭順 = 300
SOURCE:達成 = 500
SOURCE:屈従 = 300
SOURCE:露出 = 50
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*40+60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のペニス⇔調教者のペニスの汚れが移動
STAIN:ペニス |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 3, 2

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

