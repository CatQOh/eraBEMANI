;-------------------------------------------------
;貝あわせ
;-------------------------------------------------
@USERCOM_ABLE_貝あわせ, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;対象にヴァギナ無いとダメ
コマンド実行条件 += "対象に女性器が必要/"
SIF !VAGINA(TARGET)
	COMRESULT = 0
;調教者にもヴァギナ無いとダメ
コマンド実行条件 += "実行者に女性器が必要/"
SIF !VAGINA(PLAYER)
	COMRESULT = 0
;欲望3以下の助手の場合貝あわせはできない（小悪魔、相性良好ならＯＫ）
コマンド実行条件 += "実行者が助手の場合、欲望4以上or[小悪魔]or双方間の相性101%以上が必要/"
IF PLAYER == ASSI
	SIF ABL:ASSI:欲望 < 4 && !TALENT:ASSI:小悪魔 && (RELATION:(NO:ASSI) <= 100 && RELATION:ASSI:(NO:TARGET) <= 100)
		COMRESULT = 0
ENDIF
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0

;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;膣に何か入ってるとダメ
コマンド実行条件 += "対象がV使用中は実行できない/"
SIF Ｖ使用中(TARGET)
	COMRESULT = 0
;クリキャップ使用中はダメ
コマンド実行条件 += "対象がクリキャップ使用中は実行できない/"
SIF TEQUIP:クリキャップ
	COMRESULT = 0
;PLAYERがＶ使用中はダメ
コマンド実行条件 += "実行者がV使用中は実行できない/"
SIF Ｖ使用中(PLAYER)
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_貝あわせ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望*2
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*2
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*2})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*2
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*2})
ENDIF

CALL COM_ORDER_欲情, 1
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "娼婦"
			LOCAL:1 = 10
		CASE "淫乱"
			LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -5
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -3
		CASE "献身的"
			LOCAL:1 = 6
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 2)
	LOCAL:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 3)
	LOCAL:1 += 5
;母乳汚れ
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 4)
	LOCAL:1 += 1
;尿
SIF GETBIT(STAIN:PLAYER:ヴァギナ, 7)
	LOCAL:1 += 3

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;30以上で実行
CALL 実効値判定, LOCAL, 30

RETURN RESULT

@COM_貝あわせ
PRINTL 貝あわせ

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF !CFLAG:PLAYER:挿入可
	TEQUIP:PLAYER:下半身上着 = 0
	TEQUIP:PLAYER:全身上着 = 0
ENDIF
TEQUIP:PLAYER:下半身下着 = 0

IF !CFLAG:挿入可
	TEQUIP:全身上着 = 0
	TEQUIP:下半身上着 = 0
ENDIF
TEQUIP:下半身下着 = 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 90

DOWNBASE:MASTER:体力 += 40
DOWNBASE:MASTER:気力 += 40

SOURCE:快C = 200
SOURCE:性行動 = 300
SOURCE:達成 = 300
SOURCE:恭順 = 300
SOURCE:露出 = 200
SOURCE:屈従 = 400
SOURCE:逸脱 = 100
SOURCE:反感 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のヴァギナ⇔調教者のヴァギナの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ヴァギナ
STAIN:PLAYER:ヴァギナ |= STAIN:ヴァギナ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 1

CALL 魔力回復, 7
CALL 魔力回復, 7, PLAYER
CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

TFLAG:奉仕快楽 = 1

RETURN 1

