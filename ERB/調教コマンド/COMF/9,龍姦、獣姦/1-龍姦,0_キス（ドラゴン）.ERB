;-------------------------------------------------
;キス（ドラゴン）
;-------------------------------------------------
@USERCOM_ABLE_キス（ドラゴン）, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;上級売春中は方針が狂気じゃないと不可
SIF 上級売春中 && 娼館方針 != 4
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;風呂場だとダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;PLAYERがドラゴン使い持ってないとダメ
SIF !TALENT:PLAYER:ドラゴン使い
	RETURNF 0
;PLAYERが助手で未陥落だとダメ
SIF PLAYER == ASSI && !CFLAG:PLAYER:陥落
	RETURNF 0
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_キス（ドラゴン）
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;技巧が高いとキス・オブ・ザ・ドラゴンに派生
IF ABL:PLAYER:技巧 >= 3
	PRINTL キス・オブ・ザ・ドラゴン
	;このターンで-1されるので2
	TFLAG:実行判定無視 = 2
ELSE
	PRINTL キス（ドラゴン）
ENDIF

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 50

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:奉仕精神
ENDSELECT

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES LOCAL , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES LOCAL , 2.00
	SIF TALENT:親愛
		TIMES LOCAL , 3.00
ENDIF

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 25

DOWNBASE:MASTER:体力 += 10
DOWNBASE:MASTER:気力 += 10

SOURCE:情愛 = 50
SOURCE:性行動 = 10
SOURCE:恭順 = 10
SOURCE:屈従 = 30
SOURCE:恐怖 = 200
SOURCE:不潔 = 100
SOURCE:反感 = 50
SOURCE:逸脱 = 30
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 += 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 6
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 6, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 0, 1

;魔力回復
IF TALENT:魔術師
	LOCAL = MAXBASE:魔力/100
	SIF TALENT:ドラゴン使い
		LOCAL *= 2
	BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
ENDIF

RETURN 1

