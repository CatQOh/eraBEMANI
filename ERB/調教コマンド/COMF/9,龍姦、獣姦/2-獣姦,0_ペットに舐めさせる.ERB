;-------------------------------------------------
;ペットに舐めさせる
;-------------------------------------------------
@USERCOM_ABLE_ペットに舐めさせる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
SIF TEQUIP:ゲームセンター
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURNF 0
;ASSIがペットじゃないとダメ
SIF ASSI < 0 || !TALENT:ASSI:アニマル
	RETURNF 0
;アニマルは無理
SIF TALENT:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_ペットに舐めさせる
PRINTL ペットに舐めさせる
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
;なつき度が低いと消極的になる

LOCAL = 30

LOCAL += CFLAG:ASSI:依存度/10

;ABL:助手の技巧をみる
LOCAL += LOCAL*ABL:ASSI:技巧/2

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

;助手のTALENT:舌使いをみる
SIF TALENT:ASSI:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 15

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 30*MIN(ABL:ASSI:技巧+1, 10)
;なつき度に応じて他の場所も舐める
SIF CFLAG:ASSI:依存度 >= 1000
	SOURCE:快B = 30*MIN(ABL:ASSI:技巧+1, 10)
SIF CFLAG:ASSI:依存度 >= 1500
	SOURCE:快A = 30*MIN(ABL:ASSI:技巧+1, 10)
SOURCE:液体 = 500
SOURCE:屈従 = 100
SOURCE:露出 = 100
SOURCE:恐怖 = 300
SOURCE:不潔 = 300
SOURCE:逸脱 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 2

;飼育経験
EXP:飼育経験++
PRINTL 飼育経験+1

;魔力回復
IF TALENT:魔術師
	LOCAL = MAXBASE:魔力/150
	BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
ENDIF

RETURN 1

