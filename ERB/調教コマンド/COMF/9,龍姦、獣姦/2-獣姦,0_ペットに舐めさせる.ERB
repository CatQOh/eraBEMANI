;-------------------------------------------------
;ペットに舐めさせる
;-------------------------------------------------
@USERCOM_ABLE_ペットに舐めさせる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;ASSIがペットじゃないとダメ
コマンド実行条件 += "助手に[アニマル]が必要/"
SIF ASSI < 0 || !TALENT:ASSI:アニマル
	COMRESULT = 0
;アニマルは無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@COM_ペットに舐めさせる
#DIM DYNAMIC 増加量

PRINTL ペットに舐めさせる
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
;なつき度が低いと消極的になる

増加量 = 30

増加量 += CFLAG:ASSI:依存度/10

;ABL:助手の技巧をみる
増加量 += 増加量*ABL:ASSI:技巧/2

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 2.00
	CASEELSE
		増加量 += 増加量*(ABL:Ｃ感覚-3)
ENDSELECT

;助手のTALENT:舌使いをみる
SIF TALENT:ASSI:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 15

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SOURCE:快C = 30*MIN(ABL:ASSI:技巧+1, 10)
;なつき度に応じて他の場所も舐める
SIF CFLAG:ASSI:依存度 >= 1000
	SOURCE:快B = 30*MIN(ABL:ASSI:技巧+1, 10)
SIF CFLAG:ASSI:依存度 >= 1500
	SOURCE:快A = 30*MIN(ABL:ASSI:技巧+1, 10)
SOURCE:液体 = 500
SOURCE:屈従 = 100
SOURCE:露出 = 100
SOURCE:恐怖 = 300
SOURCE:不潔 = 300
SOURCE:逸脱 = 100

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 2

;飼育経験
CALL EXPUP, "飼育経験", 1

;魔力回復
CALL 魔力回復, 7

RETURN 1

