?;-------------------------------------------------
;ペットに犯させる
;-------------------------------------------------
@USERCOM_ABLE_ペットに犯させる, ARG
#FUNCTION
;ゲーセンに居る場合はできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
;両穴使用中はダメ
SIF Ｖ使用中(TARGET) && Ａ使用中(TARGET)
	COMRESULT = 0
;Ａ使用中はヴァギナ無いとダメ
SIF Ａ使用中(TARGET) && !VAGINA(TARGET)
	COMRESULT = 0
;小人体型持ちは物理的に無理
SIF TALENT:小人体型
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はダメ
コマンド実行条件 += "お風呂場では実行できない/"
SIF TEQUIP:お風呂場
	COMRESULT = 0
;ASSIがアニマルじゃないとダメ
SIF ASSI < 0 || !TALENT:ASSI:アニマル
	COMRESULT = 0
;ペットがペニス無いとダメ
SIF !PENIS(ASSI)
	COMRESULT = 0
;アニマルは無理(暫定)
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

RETURNF COMRESULT

@実行判定_ペットに犯させる
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER
	LOCAL = RESULT

	CALL COM_ORDER_セックス
	LOCAL += RESULT

	IF TALENT:娼婦
		LOCAL = 20
		PRINT +娼婦(20)
	ENDIF
	IF TALENT:淫乱
		LOCAL = 15
		PRINT +淫乱(10)
	ENDIF
	IF TALENT:小柄体型
		LOCAL = -8
		PRINT -小柄体型(8)
	ENDIF
	IF TALENT:潔癖症
		LOCAL = -8
		PRINT -潔癖症(8)
	ENDIF

	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		LOCAL += ABL:Ａ感覚*2
		PRINTFORM +Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚*2})
	ENDIF

	;Ａ経験
	IF EXP:Ａ経験 < EXPLV:3
		LOCAL -= 30
		PRINT -Ａ経験不足(30)
	ENDIF

	;46以上で実行
	CALL 実効値判定, LOCAL, 46

	RETURN RESULT
ELSE
	RETURN 1
ENDIF

@COM_ペットに犯させる
PRINTL ペットに犯させる

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;どっちに入れるかはペットの気分次第
TFLAG:助手挿入箇所 = RAND(1, 3)
;膣無ければアナル固定
SIF !VAGINA(TARGET)
	TFLAG:助手挿入箇所 = 2

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
IF TFLAG:助手挿入箇所 == 1
	TFLAG:膣コマンド = 1
	;処女なら異常経験追加
	IF TALENT:処女
		PRINTL ＜異種姦処女姦通＞
		CALL EXPUP, "異常経験", 1
	ENDIF
ENDIF

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF OVER_GENITAL(TARGET) != ""
	IF CFLAG:挿入可
		TEQUIP:下半身下着 = 0
	ELSE
		TEQUIP:下半身下着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
LOCAL = 1000

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)

;処女だと増える
SIF TALENT:処女
	TIMES LOCAL , 1.50

CALL 射精ゲージ増加, LOCAL, ASSI

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 500

SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		LOCAL = 射精ゲージ計算_Ｖ挿入(500)
		;ABL:助手の技巧をみる
		LOCAL += (LOCAL/5)*ABL:ASSI:技巧

		;ABL:技巧をみる
		LOCAL += (LOCAL/10)*ABL:技巧
	CASE 2
		LOCAL = 射精ゲージ計算_Ａ挿入(500)
		;ABL:助手の技巧をみる
		LOCAL += (LOCAL/5)*ABL:ASSI:技巧

		;ABL:技巧をみる
		LOCAL += (LOCAL/10)*ABL:技巧

		;ABL:従順をみる
		LOCAL += (LOCAL/10)*ABL:従順
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 75
LOSEBASE:気力 += 150

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 20

SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		SOURCE:快V = 500
	CASE 2
		SOURCE:快A = 500
ENDSELECT
SOURCE:苦痛 = 500
SOURCE:中毒 = 100
SOURCE:露出 = 200
SOURCE:反感 = 500
SOURCE:液体 = 300
SOURCE:恐怖 = 500
SOURCE:逸脱 = 300
SOURCE:不潔 = 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで淫乱、娼婦持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 2
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 && TFLAG:助手挿入箇所 == 1
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 && TFLAG:助手挿入箇所 == 1
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

SIF TFLAG:助手挿入箇所 == 1
	CALL 特殊ソース処理_Ｖ
SIF TFLAG:助手挿入箇所 == 2
	CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		CALL SAMEN_CHECK_A, 1
		CALL SAMEN_CHECK_T, 4

		CALL SAMEN_SHOOT, 1, NOWEX:ASSI:射精, ASSI, TARGET
		CALL SAMEN_SHOOT_T, 4, NOWEX:射精
	CASE 2
		CALL SAMEN_CHECK_A, 2
		CALL SAMEN_CHECK_T, 5

		CALL SAMEN_SHOOT, 2, NOWEX:ASSI:射精, ASSI, TARGET
		CALL SAMEN_SHOOT_T, 5, NOWEX:射精
ENDSELECT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 || TALENT:再生処女
	SETBIT STAIN:膣内, 6

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 0, 3

SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		CALL EXPUP, "Ｖ経験", 2
	CASE 2
		CALL EXPUP, "Ａ経験", 2
ENDSELECT

CALL EXPUP, "性交経験", 1
CALL EXPUP, "飼育経験", 2

IF CFLAG:異種姦経験 == 0
	PRINTL ＜初異種姦＞
	CALL EXPUP, "異常経験", 1
ENDIF

CFLAG:異種姦経験++

CALL 魔力回復, 10

RETURN 1

