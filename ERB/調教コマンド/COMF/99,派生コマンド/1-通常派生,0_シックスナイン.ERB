;-------------------------------------------------
;シックスナイン
;-------------------------------------------------
@USERCOM_ABLE_シックスナイン, ARG
#FUNCTION
;シックスナイン実行判定
コマンド実行条件 += "実行者が助手の場合、助手に[小悪魔]or欲望LV3以上が必要/"
IF ASSIPLAY
	;欲望3以下のASSIの場合シックスナインはできない（小悪魔ならＯＫ）
	SIF ABL:ASSI:欲望 <= 3 && !TALENT:ASSI:小悪魔
		COMRESULT = 0
ENDIF

;従順3以上、奉仕精神3以上必要
コマンド実行条件 += "対象に従順と奉仕精神LV3以上が必要/"
SIF ABL:従順 < 3 || ABL:奉仕精神 < 3
	COMRESULT = 0

;小人体型持ちは、双方が小人体型ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
コマンド実行条件 += "対象が[小人体型]の場合、実行者に[小人体型]か、主人に[禁断の知識]が必要/"
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識 && !TALENT:PLAYER:小人体型
	COMRESULT = 0
;猫舌の調教者の場合は技巧Lv2以上必要
コマンド実行条件 += "実行者が[猫舌]持ちの場合は技巧LV2以上必要/"
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	COMRESULT = 0
;猫舌の奴隷の場合は技巧Lv2以上必要
コマンド実行条件 += "対象が[猫舌]の場合は技巧LV2以上必要/"
SIF TALENT:猫舌 && ABL:技巧 < 2
	COMRESULT = 0
;ペニスバンド使用中はダメ
コマンド実行条件 += "対象か実行者がペニスバンド装着中は実行できない/"
SIF TEQUIP:ペニスバンド || TEQUIP:PLAYER:ペニスバンド
	COMRESULT = 0

;オナホール使用中はダメ
コマンド実行条件 += "対象がオナホール装着中は実行できない/"
SIF TEQUIP:オナホール
	COMRESULT = 0
;ボールギャグ使用中は不可
コマンド実行条件 += "対象がボールギャグ装着中は実行できない/"
SIF TEQUIP:ボールギャグ
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;お風呂場プレイ中はプレイマットが無いとダメ
コマンド実行条件 += "お風呂場ではプレイマット(アイテム)が無いと実行できない/"
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	COMRESULT = 0
;ゲームセンターでもできない
コマンド実行条件 += "ゲーセンでは実行不可/"
SIF TEQUIP:ゲームセンター
	COMRESULT = 0
コマンド実行条件 += "睡眠中は実行できない/"
SIF TEQUIP:睡眠
	COMRESULT = 0
IF STSMOD
	コマンド実行条件 += "屍姦中は実行できない/"
	SIF TEQUIP:屍姦
		COMRESULT = 0
ENDIF

;派生確認
;前回と今回の調教者が同じで縄、ボールギャグ使用中でない場合
コマンド実行条件 += "前回と今回の実行者が同じで、[縄][ボールギャグ]を使用していない場合、/"
コマンド実行条件 += "フェラチオ、クンニ、クンニさせる、フェラする、シックスナイン、69パイズリから派生可能/"
IF PLAYER == TFLAG:前コマンド実行者 && !TEQUIP:縄 && !TEQUIP:ボールギャグ
	;前回のコマンド
	SELECTCASE PREVCOMS
		;フェラチオ、クンニ、シックスナイン系だと派生可能
		CASE "フェラチオ", "クンニ", "クンニさせる", "フェラする", "シックスナイン", "69パイズリ"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@実行判定_シックスナイン
#DIM DYNAMIC 実効値

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_シックスナイン
実効値 += RESULT

;33以上で実行
CALL 実効値判定, 実効値, 33

RETURN RESULT

@COM_シックスナイン
#DIM DYNAMIC 増加量

PRINTL シックスナイン

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_奉仕(600)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES 増加量 , 2.00

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_フェラチオ(500), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 500
SOURCE:情愛 = 500
SOURCE:性行動 = 900
SOURCE:達成 = 600
SOURCE:恭順 = 400
SOURCE:液体 = 100
SOURCE:露出 = 100
SOURCE:屈従 = 800
SOURCE:逸脱 = 500
SOURCE:反感 = 500
SOURCE:不潔 = 80

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 4
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT, 4, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のヴァギナ、ペニス汚れは舐め取る
SIF PENIS(TARGET)
	STAIN:ペニス = 0
SIF VAGINA(TARGET)
	STAIN:ヴァギナ = 0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 4, 2

;フェラ経験
SIF PENIS(PLAYER)
	CALL EXPUP, "フェラ経験", 1
SIF PENIS(TARGET)
	CALL EXPUP, "フェラ経験", PLAYER

;愛情経験
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1

;奴隷が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1

;調教者が猫舌持ちの場合は嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	CALL EXPUP, "嗜虐快楽経験", 1, PLAYER


IF !避妊具(PLAYER)
	CALL 魔力回復, 5
	CALL 獣人催淫, 1
ENDIF

IF !避妊具(TARGET)
	CALL 魔力回復, 5, PLAYER
	CALL 獣人催淫, 1, PLAYER
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1
