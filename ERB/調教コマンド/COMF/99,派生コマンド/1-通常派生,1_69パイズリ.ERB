;-------------------------------------------------
;69パイズリ
;-------------------------------------------------
@USERCOM_ABLE_69パイズリ, ARG
#FUNCTION
;69パイズリ
IF ASSIPLAY
	;欲望3以下のASSIの場合シックスナインはできない（小悪魔ならＯＫ）
	SIF ABL:ASSI:欲望 <= 3 && !TALENT:ASSI:小悪魔
		RETURNF 0
ENDIF

;従順3以上、奉仕精神3以上必要
SIF ABL:従順 < 3 || ABL:奉仕精神 < 3
	RETURNF 0

SELECTCASE TALENT:バストサイズ
	;男or絶壁だとダメ
	CASE 0, 1
		RETURNF 0
	;貧乳だと技巧LV6以上必要
	CASE 2
		LOCAL = 6
	;普通だと技巧LV5以上必要
	CASE 3
		LOCAL = 5
	;巨乳なら技巧LV4以上必要
	CASE 4
		LOCAL = 4
	;爆乳なら技巧LV3以上必要
	CASE 5
		LOCAL = 3
ENDSELECT
SIF ABL:技巧 < LOCAL
	RETURNF 0
;調教者がペニス無いとダメ
SIF !PENIS(PLAYER)
	RETURNF 0
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;服を着ている場合ペニスの出入り口がある構造じゃないとダメ
IF TEQUIP:上半身上着 || (TEQUIP:全身上着 && CFLAG:着衣併用!=1)
	SELECTCASE CFLAG:上着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF
IF TEQUIP:上半身下着
	SELECTCASE CFLAG:下着パイズリ
		CASE 0, 2, 4
			RETURNF 0
	ENDSELECT
ENDIF

COMRESULT = 1
RETURNF 1

@COM_69パイズリ
PRINTL 69パイズリ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

CALL COM_ORDER_欲情, 1
LOCAL += RESULT

REPEAT VARSIZE("TALENT")
	LOCAL:1 = 0
	SIF !TALENT:COUNT
		CONTINUE
	SELECTCASE TALENTNAME:COUNT
		CASE "親愛"
			SIF PLAYER == MASTER
				LOCAL:1 = 10
		CASE "恋慕"
			SIF PLAYER == MASTER
				LOCAL:1 = 5
		CASE "恥じらい"
			LOCAL:1 = -3
		CASE "快感の否定"
			SIF FALLTYPE(TARGET) != 2
				LOCAL:1 = -3
		CASE "献身的"
			LOCAL:1 = 6
		CASE "汚臭敏感"
			LOCAL:1 = -6
		CASE "汚臭鈍感"
			LOCAL:1 = 3
		CASE "男嫌い"
			SIF SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
				LOCAL:1 = -12
	ENDSELECT
	IF LOCAL:1
		PRINTFORM \@SIGN(LOCAL:1) == 1 ? + # - \@%TALENTNAME:COUNT%({ABS(LOCAL:1)})
		LOCAL += LOCAL:1
	ENDIF
REND

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 3
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 5
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 5

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2
;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(45以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 45
SIF LOCAL < LOCAL:2
	PRINT  < 
SIF LOCAL == LOCAL:2
	PRINT  = 
SIF LOCAL > LOCAL:2
	PRINT  > 
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
	CALL KOJO_MESSAGE_EVENT, "コマンド拒否時"
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
SELECTCOMS = 69パイズリ
SELECTCOM = 508
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 1000

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;TALENT:バストサイズをみる
SELECTCASE TALENT:バストサイズ
	CASE 1
		TIMES LOCAL , 0.70
	CASE 2
		TIMES LOCAL , 0.85
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.15
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT
;PLAYERが小柄体型・小人体型なら相対的に胸が大きくなるので少し増やす
SIF TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型
	TIMES LOCAL , 1.15

;機械・無機物の場合、ある程度減らす
IF CSTR:種族 == "機械" && TALENT:無機物
	TIMES LOCAL , 0.60
ELSEIF CSTR:種族 == "機械" || TALENT:無機物
	TIMES LOCAL , 0.80
ENDIF

;奴隷が小柄体型・小人体型なら身体が柔らかいので増やす
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES LOCAL , 1.25

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 700

;ABL:調教者の技巧をみる
LOCAL += LOCAL*ABL:PLAYER:技巧/2

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-1)
ENDSELECT

;TALENT:実行者の舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 2.00

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 120

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 30

SOURCE:快C = 500
SOURCE:快B = 200
SOURCE:情愛 = 800
SOURCE:性行動 = 1200
SOURCE:達成 = 500
SOURCE:恭順 = 700
SOURCE:液体 = 100
SOURCE:露出 = 100
SOURCE:屈従 = 1200
SOURCE:逸脱 = 700
SOURCE:反感 = 700

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*50+60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快C , 1.50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 5
CALL SAMEN_CHECK_T, 3

CALL SAMEN_SHOOT, 5, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 3, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸
;奴隷のヴァギナ、ペニス汚れを舐め取る
SIF VAGINA(TARGET)
	STAIN:ヴァギナ = 0
SIF PENIS(TARGET)
	STAIN:ペニス = 0

;奉仕精神LV2以上、技巧LV2以上なら汚れをなめ取る
SIF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:ペニス = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
PRINTL フェラ経験+1
EXP:フェラ経験 += 1
CALL EXPUP, 4, 2

;愛情経験
IF FALLTYPE(TARGET) == 1 && PLAYER == MASTER
	PRINTL 愛情経験+1
	EXP:愛情経験++
ENDIF

;奴隷が猫舌持ちの場合は、嗜虐快楽経験を追加
IF TALENT:猫舌
	PRINTL 嗜虐快楽経験+1
	EXP:嗜虐快楽経験++
ENDIF

;魔力回復
IF TALENT:魔術師
	IF VAGINA(PLAYER) || !TEQUIP:PLAYER:コンドーム
		LOCAL = MAXBASE:魔力/200
		BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
	ENDIF
ENDIF
IF TALENT:PLAYER:魔術師
	IF VAGINA(TARGET) || (!TEQUIP:コンドーム && !TEQUIP:搾精コンドーム)
		LOCAL = MAXBASE:PLAYER:魔力/200
		BASE:PLAYER:魔力 = LIMIT(BASE:PLAYER:魔力+LOCAL, 0, MAXBASE:PLAYER:魔力)
	ENDIF
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

