;-------------------------------------------------
;搾乳
;-------------------------------------------------
@USERCOM_ABLE_搾乳, ARG
#FUNCTION
;搾乳
;上半身下着または上半身上着または全身上着をつけているとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_搾乳
PRINTL 搾乳
SELECTCOMS = 搾乳
SELECTCOM = 509
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快B = 200
SOURCE:情愛 = 80
SOURCE:露出 = 100
SOURCE:不潔 = 50
SOURCE:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;調教者が［幼稚］か［幼児退行］
IF TALENT:PLAYER:幼稚 || TALENT:PLAYER:幼児／幼児退行
	TIMES SOURCE:快B , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の手の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 2

;愛情経験
IF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	PRINTL 愛情経験+1
	EXP:愛情経験++
ENDIF

;実績:ラピカの乳を搾る
SIF CHARANAMEF(TARGET) == "黄のラピカ"
	CALL 実績解除, "地上に舞い降りたミルクサーバー"

RETURN 1

