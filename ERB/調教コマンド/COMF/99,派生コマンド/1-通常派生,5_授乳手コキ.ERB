;-------------------------------------------------
;授乳手コキ
;-------------------------------------------------
@USERCOM_ABLE_授乳手コキ, ARG
#FUNCTION
;授乳手コキ
;大体の条件は派生元の母乳飲み判定と共通なため省略
;乳房が露出してないとダメ
SIF OVER_BUST(TARGET) != ""
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_授乳手コキ
PRINTL 授乳手コキ
SELECTCOMS = 授乳手コキ
SELECTCOM = GETNUM(TRAINNAME, "授乳手コキ")
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 500
;対象が母性持ちだと気持ち良い
SIF TALENT:母性
	LOCAL += 200

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 50

SOURCE:快B = 400
SOURCE:性行動 = 500
SOURCE:達成 = 200
SOURCE:情愛 = 1000
SOURCE:露出 = 1000
SOURCE:恭順 = 3000
SOURCE:屈従 = 500
SOURCE:不潔 = 50
SOURCE:反感 = 100

;対象が母性持ちならいろいろボーナス
IF TALENT:母性
	LOSEBASE:体力 -= 10
	LOSEBASE:気力 -= 20
	SOURCE:快B += 100
	SOURCE:情愛 += 500
	SOURCE:恭順 += 2000
	SOURCE:不潔 -= 50
	SOURCE:反感 -= 100
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;対象が母性持ちならいろいろボーナス
IF TALENT:母性
	LOSEBASE:体力 -= 10
	LOSEBASE:気力 -= 20
	SOURCE:快B += 100
	SOURCE:情愛 += 500
	SOURCE:恭順 += 2000
	SOURCE:不潔 -= 50
	SOURCE:反感 -= 100
ENDIF

;調教者が［幼稚］か［幼児退行］
IF TALENT:PLAYER:幼稚 || TALENT:PLAYER:幼児／幼児退行
	TIMES SOURCE:快B , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 3
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 3, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手⇔調教者のペニスの汚れが移動
STAIN:手 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 3

;愛情経験
IF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER
	PRINTL 愛情経験+2
	EXP:愛情経験 += 2
ENDIF

;魔力回復
IF TALENT:PLAYER:魔術師
	LOCAL = MAXBASE:PLAYER:魔力/200
	BASE:PLAYER:魔力 = LIMIT(BASE:PLAYER:魔力+LOCAL, 0, MAXBASE:PLAYER:魔力)
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

RETURN 1

