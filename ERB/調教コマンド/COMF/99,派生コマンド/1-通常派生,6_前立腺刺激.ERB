;-------------------------------------------------
;前立腺刺激
;-------------------------------------------------
@USERCOM_ABLE_前立腺刺激, ARG
#FUNCTION
;主人の技巧が3以下はだめ
コマンド実行条件 += "実行者の技巧LV3以上が必要/"
SIF ABL:PLAYER:技巧 < 3
	COMRESULT = 0
;男じゃないとダメ
コマンド実行条件 += "対象が男性じゃないと実行できない/"
SIF SEX(TARGET) != 1
	COMRESULT = 0
;異物挿入中はダメ
コマンド実行条件 += "A異物挿入中は実行できない/"
SIF TEQUIP:Ａ異物
	COMRESULT = 0
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0

;前コマンドと今回の実行者が同じ
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "アナル愛撫、前立腺刺激から派生可能/"
IF PLAYER == TFLAG:前コマンド実行者
	;前コマンド
	SELECTCASE PREVCOMS
		;前回の調教がアナル愛撫かつ実行者の技巧3以上だと可能
		CASE "アナル愛撫"
			SIF ABL:PLAYER:技巧 >= 3
				RETURNF COMRESULT
		;前回の調教が前立腺刺激なら続行可能
		CASE "前立腺刺激"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@COM_前立腺刺激
#DIM DYNAMIC 増加量

PRINTL 前立腺刺激
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

DOWNBASE:MASTER:体力 += 30
DOWNBASE:MASTER:気力 += 20

SOURCE:快A = 500
SOURCE:苦痛 = 50
SOURCE:液体 = 50
SOURCE:露出 = 100
SOURCE:逸脱 = 100
SOURCE:反感 = 100

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 300

;ABL:調教者の技巧をみる
増加量 += 増加量*ABL:PLAYER:技巧/5

;ABL:従順をみる
増加量 += (増加量/10)*ABL:従順 - (増加量/10)*2

;ABL:欲望をみる
増加量 += (増加量/10)*ABL:欲望

;PALAM:潤滑をみる
増加量 += (GETPALAMLV(PALAM:潤滑, 4)-3)*(増加量/5)

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.50
	CASE 3
		TIMES 増加量 , 4.00
	CASE 4
		TIMES 増加量 , 6.00
	CASEELSE
		増加量 += 増加量*(ABL:Ａ感覚+2)
ENDSELECT

;奴隷のABL:マゾっ気をみる
増加量 += 増加量*ABL:マゾっ気/5

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

CALL 特殊ソース処理_Ａ

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔調教者の手の汚れが移動
STAIN:アナル |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 1, 2

CALL EXPUP, "Ａ経験", 1

RETURN 1

