;-------------------------------------------------
;騎乗乳房自慰
;-------------------------------------------------
@USERCOM_ABLE_騎乗乳房自慰, ARG
#FUNCTION
;騎乗乳房自慰
;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
コマンド実行条件 += "対象の乳房が露出していないと実行できない/"
SIF OVER_BUST(TARGET) != ""
	COMRESULT = 0
;上級売春中は客がマゾじゃないとダメ
コマンド実行条件 += "上級売春中は客に素質[マゾ]が必要/"
SIF 上級売春中 && !TALENT:マゾ
	COMRESULT = 0
;縄使用中は不可
コマンド実行条件 += "縄で縛ってるときは実行できない/"
SIF TEQUIP:縄
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;アニマルだと無理
コマンド実行条件 += "対象が[アニマル]の場合は実行できない/"
SIF TALENT:アニマル
	COMRESULT = 0

;特殊派生確認
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "騎乗位、騎乗位アナルから派生可能/"
IF PLAYER == TFLAG:前コマンド実行者
	;前コマンド
	SELECTCASE PREVCOMS
		;騎乗位系の場合は可能
		CASE "騎乗位", "騎乗位アナル"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@実行判定_騎乗乳房自慰
#DIM DYNAMIC 実効値, 2

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実効値 = RESULT

CALL COM_ORDER_自慰
実効値 += RESULT

SELECTCASE PREVCOMS
	CASE "騎乗位"
		;ABL:Ｖ感覚
		IF ABL:Ｖ感覚
			実効値 += ABL:Ｖ感覚*2
			PRINTFORM +Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚*2})
		ENDIF
	CASE "騎乗位アナル"
		;ABL:Ａ感覚
		IF ABL:Ａ感覚
			実効値 += ABL:Ａ感覚*2
			PRINTFORM +Ａ感覚LV{ABL:Ａ感覚}({ABL:Ａ感覚*2})
		ENDIF
ENDSELECT
;ABL:奉仕精神
IF ABL:奉仕精神
	実効値 += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF

;ABL:性交中毒
IF ABL:性交中毒
	実効値 += ABL:性交中毒
	PRINTFORM +性交中毒LV{ABL:性交中毒}({ABL:性交中毒})
ENDIF

実効値:1 = 50

;難易度上昇
;公開で+10、搾乳器で+5
SIF 撮影中()
	実効値:1 += 10
SIF TEQUIP:搾乳器
	実効値:1 += 5

CALL 実効値判定, 実効値, 実効値:1

RETURN RESULT

@COM_騎乗乳房自慰
#DIM DYNAMIC 増加量
#DIM DYNAMIC 射精先, 2

PRINTL 騎乗乳房自慰

SIF !TFLAG:実行判定無視
	CALLFORM 実行判定_%SELECTCOMS%
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;派生元を見る
;騎乗位からの派生の場合
IF PREVCOMS == "騎乗位"
	TFLAG:主人挿入箇所 = 1
	;V経験を伴うコマンドのフラグ(処女膜再生対応)
	TFLAG:膣コマンド = 1
	SIF (TALENT:恋慕 || TALENT:親愛) && PLAYER == MASTER && EXP:Ｖ経験 == 0
		TFLAG:恋慕処女 = 1
;騎乗位アナルからの派生の場合
ELSEIF PREVCOMS == "騎乗位アナル"
	TFLAG:主人挿入箇所 = 2
ENDIF

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		増加量 = 射精ゲージ計算_Ｖ挿入実行者(800)
		;処女だと増える
		SIF TALENT:処女
			TIMES 増加量 , 1.50
	CASE 2
		増加量 = 射精ゲージ計算_Ａ挿入実行者(800)
ENDSELECT

;ABL:調教者の技巧をみる
増加量 += (増加量/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/5)*ABL:技巧

;PALAM:欲情
SELECTCASE GETPALAMLV(PALAM:欲情, 5)
	CASE 0
		TIMES 増加量 , 0.60
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.00
	CASE 3
		TIMES 増加量 , 1.20
	CASE 4
		TIMES 増加量 , 1.40
	CASE 5
		TIMES 増加量 , 1.70
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES 増加量 , 0.50
	CASE 1
		TIMES 増加量 , 0.80
	CASE 2
		TIMES 増加量 , 1.20
	CASE 3
		TIMES 増加量 , 1.50
	CASE 4
		TIMES 増加量 , 1.80
	CASEELSE
		増加量 += 増加量/3*ABL:奉仕精神
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES 増加量 , 1.00
	CASE 1
		TIMES 増加量 , 1.50
	CASE 2
		TIMES 増加量 , 2.00
	CASE 3
		TIMES 増加量 , 2.50
	CASE 4
		TIMES 増加量 , 3.50
	CASEELSE
		増加量 += 増加量*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT


;子供は締まりが良いので増える
SIF TALENT:小柄体型
	TIMES 増加量 , 1.20

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 1200

SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		増加量 = 射精ゲージ計算_Ｖ挿入(1200)
	CASE 2
		増加量 = 射精ゲージ計算_Ａ挿入(1200)

		;ABL:従順をみる
		増加量 += (増加量/10)*ABL:従順
ENDSELECT

;ABL:調教者の技巧をみる
増加量 += (増加量/10)*ABL:PLAYER:技巧

;奴隷のABL:Ｂ感覚を見る
SELECTCASE ABL:Ｂ感覚
	CASE 0
		TIMES 増加量 , 0.90
	CASE 1
		TIMES 増加量 , 1.00
	CASE 2
		TIMES 増加量 , 1.10
	CASE 3
		TIMES 増加量 , 1.30
	CASE 4
		TIMES 増加量 , 1.60
	CASEELSE
		増加量 += (増加量/5)*ABL:Ｂ感覚
ENDSELECT

;奴隷のABL:サドっ気をみる
増加量 += (増加量/5)*ABL:サドっ気

CALL 射精ゲージ増加, 増加量, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力は分岐後に代入

SOURCE:快B = 300
SOURCE:性行動 = 900
SOURCE:達成 = 800
SOURCE:中毒 = 450
SOURCE:露出 = 300
SOURCE:逸脱 = 400
SOURCE:反感 = 600
SOURCE:不潔 = 400
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 100
		SOURCE:快V = 500
		SOURCE:情愛 = 500
		SOURCE:恭順 = 500
		SOURCE:苦痛 = 500
	;騎乗位アナルからの派生の場合
	CASE 2
		LOSEBASE:体力 += 150
		LOSEBASE:気力 += 150
		SOURCE:快A = 600
		SOURCE:情愛 = 400
		SOURCE:恭順 = 400
		SOURCE:苦痛 = 800
ENDSELECT

DOWNBASE:MASTER:体力 += 60
DOWNBASE:MASTER:気力 += 70

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 900

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 1 && TFLAG:主人挿入箇所 == 1 &&  PLAYER != MASTER
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 && TFLAG:主人挿入箇所 == 1
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 && TFLAG:主人挿入箇所 == 1
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

;騎乗位からの派生の場合
SIF TFLAG:主人挿入箇所 == 1
	CALL 特殊ソース処理_Ｖ

;騎乗位アナルからの派生の場合
SIF TFLAG:主人挿入箇所 == 2
	CALL 特殊ソース処理_Ａ

CALL 特殊ソース処理_セックス

;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE IS <= 1
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		射精先 = 1
	;騎乗位アナルからの派生の場合
	CASE 2
		射精先 = 2
ENDSELECT

CALL SAMEN_CHECK, 射精先

SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		射精先:1 = 4
	;騎乗位アナルからの派生の場合
	CASE 2
		射精先:1 = 5
ENDSELECT

CALL SAMEN_CHECK_T, 射精先:1

CALL SAMEN_SHOOT, 射精先, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 射精先:1, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔調教者のペニスの汚れが移動
		STAIN:膣内 |= STAIN:PLAYER:ペニス
		STAIN:PLAYER:ペニス |= STAIN:膣内
	;騎乗位アナルからの派生の場合
	CASE 2
		;奴隷のアナル⇔調教者のペニスの汚れが移動
		SETBIT STAIN:PLAYER:ペニス, 3
		STAIN:アナル |= STAIN:PLAYER:ペニス
		STAIN:PLAYER:ペニス |= STAIN:アナル
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 5, 3

;自慰経験
;ビデオ撮影時は経験にプラス
IF 撮影中()
	CALL EXPUP, "自慰経験", 2
ELSE
	CALL EXPUP, "自慰経験", 1
ENDIF
;公開自慰経験
IF 撮影中()
	IF !CFLAG:自慰を撮影された経験
		PRINTL ＜初自慰撮影＞
		CALL EXPUP, "異常経験", 1
	ENDIF
	CFLAG:自慰を撮影された経験++
ENDIF

SELECTCASE TFLAG:主人挿入箇所
	;騎乗位からの派生の場合
	CASE 1
		CALL EXPUP, "Ｖ経験", 1
	;騎乗位アナルからの派生の場合
	CASE 2
		CALL EXPUP, "Ａ経験", 1
ENDSELECT

CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;嗜虐快楽経験
SIF ABL:サドっ気 >= 7
	CALL EXPUP, "嗜虐快楽経験", ABL:サドっ気/7

;愛情経験
SIF TALENT:恋慕 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF TALENT:親愛 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 4

IF !避妊具(PLAYER)
	CALL 魔力回復, 3
	CALL 獣人催淫, 1
ENDIF

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1
