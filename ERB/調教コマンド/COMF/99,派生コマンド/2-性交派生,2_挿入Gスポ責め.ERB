;-------------------------------------------------
;挿入Gスポ責め
;-------------------------------------------------
@USERCOM_ABLE_挿入Gスポ責め, ARG
#FUNCTION
;服着てるとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;技巧3必要
コマンド実行条件 += "実行者の技巧LV3以上が必要/"
SIF ABL:PLAYER:技巧 < 3
	COMRESULT = 0
;正常位のABLE
SIF !USERCOM_ABLE_正常位()
	COMRESULT = 0

;前回と今回の実行者が同じ
コマンド実行条件 += "前回と今回の実行者が同じで、/"
コマンド実行条件 += "正常位、挿入Gスポ責めから派生可能/"
IF TFLAG:前コマンド実行者 == PLAYER
	;正常位から同コマンドなら可能
	SELECTCASE PREVCOMS
		CASE "正常位", "挿入Gスポ責め"
			RETURNF COMRESULT
	ENDSELECT
ENDIF

COMRESULT = 0
RETURNF 0

@COM_挿入Gスポ責め
#DIM DYNAMIC 増加量

PRINTL 挿入Gスポ責め
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
増加量 = 射精ゲージ計算_Ｖ挿入実行者(900)

;ABL:調教者の技巧をみる
増加量 += (増加量/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
増加量 += (増加量/10)*ABL:技巧

CALL 射精ゲージ増加, 増加量, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
CALL 射精ゲージ増加, 射精ゲージ計算_Ｖ挿入強(700), TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 60
LOSEBASE:気力 += 120

DOWNBASE:MASTER:体力 += 80
DOWNBASE:MASTER:気力 += 60

SOURCE:快V = 800
SOURCE:情愛 = 400
SOURCE:苦痛 = 300
SOURCE:中毒 = 300
SOURCE:露出 = 100
SOURCE:反感 = 500
SOURCE:恭順 = 400

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:恋慕
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

CALL 特殊ソース処理_Ｖ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 3, 1

CALL EXPUP, "Ｖ経験", 2
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;愛情経験
SIF TALENT:恋慕 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 1
SIF TALENT:親愛 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2

IF !避妊具(PLAYER)
	CALL 魔力回復, 7
	CALL 獣人催淫, 1
ENDIF

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

