;-------------------------------------------------
;騎乗位アナルキス
;-------------------------------------------------
@USERCOM_ABLE_騎乗位アナルキス, ARG
#FUNCTION
;騎乗位アナルキス
;陥落していないとダメ
SIF !CFLAG:陥落
	RETURNF 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_騎乗位アナルキス
PRINTL 騎乗位アナルキス
SELECTCOMS = 騎乗位アナルキス
SELECTCOM = GETNUM(TRAINNAME, "騎乗位アナルキス")
CALL KOJO_MESSAGE_COM

;実行者が奴隷のフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ａ挿入実行者(800)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;PALAM:欲情
SELECTCASE GETPALAMLV(PALAM:欲情, 5)
	CASE 0
		TIMES LOCAL , 0.60
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.20
	CASE 4
		TIMES LOCAL , 1.40
	CASE 5
		TIMES LOCAL , 1.70
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

;相性を見る
LOCAL *= RELATION:PLAYER:(NO:TARGET)
LOCAL /= 100

SIF CFLAG:嫁 || CFLAG:専属娼婦 || CFLAG:傀儡
	LOCAL *= 3

CALL 射精ゲージ増加, LOCAL, PLAYER

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 射精ゲージ計算_Ａ挿入(600)

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/10)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:奉仕精神
ENDSELECT

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 2.00

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES LOCAL , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES LOCAL , 2.00
	SIF TALENT:親愛
		TIMES LOCAL , 3.00
ENDIF

;相性を見る
LOCAL *= RELATION:TARGET:(NO:PLAYER)
LOCAL /= 100

CALL 射精ゲージ増加, LOCAL, TARGET

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 70
LOSEBASE:気力 += 120

SOURCE:快A = 800
SOURCE:情愛 = 1000
SOURCE:性行動 = 700
SOURCE:達成 = 700
SOURCE:恭順 = 800
SOURCE:苦痛 = 500
SOURCE:中毒 = 300
SOURCE:露出 = 200
SOURCE:逸脱 = 300
SOURCE:反感 = 500
SIF TFLAG:ニコニコサンフラワーキッス
	SOURCE:恐怖 = 500

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

CALL 特殊ソース処理_Ａ
CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 2
CALL SAMEN_CHECK_T, 5

CALL SAMEN_SHOOT, 2, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 5, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者のペニスにアナル汚れ
SETBIT STAIN:PLAYER:ペニス, 3
;奴隷のアナル⇔調教者のペニスの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 5, 3

CALL EXPUP, "Ａ経験", 2
CALL EXPUP, "性交経験", 1
CALL EXPUP, "性交経験", 1, PLAYER

;愛情経験
SIF TALENT:恋慕 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 2
SIF TALENT:親愛 && PLAYER == MASTER
	CALL EXPUP, "愛情経験", 4

CALL 魔力回復, 5
SIF !避妊具(PLAYER)
	CALL 魔力回復, 3
CALL 魔力回復, 5, PLAYER

CALL 獣人催淫, 1
CALL 獣人催淫, 1, PLAYER

;奉仕快楽フラグ
TFLAG:奉仕快楽 = 1

経験回数:PLAYER:(NO:TARGET) += 1
経験回数:TARGET:(NO:PLAYER) += 1

RETURN 1

