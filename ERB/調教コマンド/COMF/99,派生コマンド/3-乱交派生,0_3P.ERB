;-------------------------------------------------
;3P
;セックス系派生コマンド
;-------------------------------------------------
@USERCOM_ABLE_3P, ARG
#FUNCTION
;3P実行判定
;助手がいないとダメ
SIF ASSI < 1
	RETURNF 0
;処女で、従順4以下・レズっ気4以下のASSIの場合3Pはできない（男もしくはサドならＯＫ）
IF TALENT:処女
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && !TALENT:ASSI:サド && SEX(ASSI) == 2
		RETURNF 0
;潤滑不足で、従順3以下のASSIの場合3Pはできない（サドならＯＫ）
ELSEIF PALAM:潤滑 < PALAMLV:2
	SIF ABL:ASSI:従順 <= 3 && !TALENT:ASSI:サド
		RETURNF 0
ENDIF

;性器が露出してないとはダメ
SIF !性器露出(TARGET)
	RETURNF 0

LOCAL = 0
;MASTERがペニス持ち
SIF PENIS(MASTER) || TEQUIP:MASTER:ペニスバンド
	LOCAL++
;ASSIがペニス持ち
SIF (PENIS(ASSI) || TEQUIP:ASSI:ペニスバンド) && !Ｐ使用不可(ASSI)
	LOCAL++
;ペニスバンドとあわせて二本ないとだめ
SIF LOCAL < 2
	RETURNF 0
;膣使用中はダメ
SIF Ｖ使用中(TARGET)
	RETURNF 0
;アナル使用中はダメ
SIF Ａ使用中(TARGET)
	RETURNF 0
;主人、調教対象、助手のいずれかに小人体型がいる場合、主人が禁断の知識を持っていないとダメ
IF TALENT:MASTER:小人体型 || TALENT:ASSI:小人体型 || TALENT:小人体型
	SIF !TALENT:MASTER:禁断の知識
		RETURNF 0
ENDIF
;一定のＡ経験が必要
SIF EXP:Ａ経験 < EXPLV:3
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURNF 0
;アニマルだと無理
SIF TALENT:アニマル
	RETURNF 0
SIF TALENT:ASSI:アニマル
	RETURNF 0

COMRESULT = 1
RETURNF 1

@COM_3P

;-------------------------------------------------
;挿入部位管理
;-------------------------------------------------
;1 = ヴァギナ
;2 = アナル
;3 = 口
;3Pは今回の実行者(PLAYER)と前回の実行者(TFLAG:前コマンド実行者)が不一致でないと発生しない
;派生条件は正常位等の各コマンド内で行っているので、膣に挿入しながら膣に挿入して3Pなんてことにはならないはず
;前回のコマンドが3Pなら挿入部位は変わらず
IF PREVCOMS != "3P"
	SELECTCASE TRAINNAME:SELECTCOM
		CASE "正常位", "後背位"
			LOCAL = 1
		CASE "正常位アナル", "後背位アナル"
			LOCAL = 2
		CASE "フェラチオ", "イラマチオ"
			LOCAL = 3
	ENDSELECT
	SELECTCASE PREVCOMS
		CASE "正常位", "後背位"
			LOCAL:1 = 1
		CASE "正常位アナル", "後背位アナル"
			LOCAL:1 = 2
		CASE "フェラチオ", "イラマチオ"
			LOCAL:1 = 3
	ENDSELECT

	IF PLAYER == MASTER
		TFLAG:主人挿入箇所 = LOCAL
		TFLAG:助手挿入箇所 = LOCAL:1
	ELSEIF PLAYER == ASSI
		TFLAG:助手挿入箇所 = LOCAL
		TFLAG:主人挿入箇所 = LOCAL:1
	ENDIF
ENDIF

PRINTL 3P
SELECTCOMS = 3P
SELECTCOM = GETNUM(TRAINNAME, "3P")
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
SIF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	TFLAG:膣コマンド = 1

SIF FALLTYPE(TARGET) == 1 && TALENT:処女 && TFLAG:主人挿入箇所 == 1
	TFLAG:恋慕処女 = 1

;童貞喪失
SELECTCASE TFLAG:主人挿入箇所
	CASE 1, 2
		IF TALENT:MASTER:童貞
			TALENT:MASTER:童貞 = 0
			IF TALENT:MASTER:性転換済
				VIRGIN:MASTER:10 = %NAME:TARGET%
			ELSE
				VIRGIN:MASTER:0 = %NAME:TARGET%
			ENDIF
		ENDIF
ENDSELECT

SELECTCASE TFLAG:助手挿入箇所
	CASE 1, 2
		IF TALENT:ASSI:童貞
			TALENT:ASSI:童貞 = 0
			IF TALENT:ASSI:性転換済
				VIRGIN:ASSI:10 = %NAME:TARGET%
			ELSE
				VIRGIN:ASSI:0 = %NAME:TARGET%
			ENDIF
		ENDIF
ENDSELECT

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 1000

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;VとAに挿入時、PALAM:潤滑をみる
SELECTCASE TFLAG:主人挿入箇所
	CASE 1, 2
		LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)
		;子供は締まりが良いので増える
		SIF TALENT:小柄体型
			TIMES LOCAL , 1.20
ENDSELECT

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:MASTER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:MASTER:Ｃ感覚-1)
ENDSELECT

;EXP:Ｖ経験をみる
;処女だと増える
SIF TFLAG:主人挿入箇所 == 1 && TALENT:処女
	TIMES LOCAL , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:賢者タイム > 0
	LOCAL /= 20

BASE:MASTER:射精 += LOCAL

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
LOCAL = 1000

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;VとAに挿入時、PALAM:潤滑をみる
SELECTCASE TFLAG:助手挿入箇所
	CASE 1, 2
		LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-2)*(LOCAL/5)
		;子供は締まりが良いので増える
		SIF TALENT:小柄体型
			TIMES LOCAL , 1.20
ENDSELECT

;助手のABL:Ｃ感覚をみる
SELECTCASE ABL:ASSI:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:ASSI:Ｃ感覚-1)
ENDSELECT

;EXP:Ｖ経験をみる
;処女だと増える
SIF TFLAG:助手挿入箇所 == 1 && TALENT:処女
	TIMES LOCAL , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:賢者タイム > 0
	LOCAL /= 20

BASE:ASSI:射精 += LOCAL

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 600

;ABL:主人の技巧をみる
LOCAL += (LOCAL/5)*ABL:MASTER:技巧

;ABL:助手の技巧をみる
LOCAL += (LOCAL/5)*ABL:ASSI:技巧

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;奴隷のABL:Ｖ感覚、Ａ感覚をみる
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	LOCAL += (GETPALAMLV(PALAM:潤滑, 4)-3)*(LOCAL/5)

	SELECTCASE ABL:Ｖ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += (LOCAL/2)*ABL:Ｖ感覚
	ENDSELECT
ENDIF
IF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	;PALAM:潤滑をみる
	SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
		CASE 0
			TIMES LOCAL , 0.20
		CASE 1
			TIMES LOCAL , 0.50
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.50
	ENDSELECT

	SELECTCASE ABL:Ａ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += (LOCAL/2)*ABL:Ａ感覚
	ENDSELECT
ENDIF

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/2)*ABL:マゾっ気

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 300

SOURCE:情愛 = 300
SOURCE:性行動 = 700
SOURCE:中毒 = 500
SOURCE:恐怖 = 300
SOURCE:屈従 = 500
SOURCE:逸脱 = 500
SOURCE:反感 = 500

;挿入している部位によって、ソースの数値が変化する
;Vに挿入している場合
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	SOURCE:快V += 400
	SOURCE:性行動 += 100
	SOURCE:苦痛 += 500
ENDIF
;Aに挿入している場合
IF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	SOURCE:快A += 400
	SOURCE:性行動 += 100
	SOURCE:苦痛 += 800
ENDIF
;口に挿入している場合
IF TFLAG:主人挿入箇所 == 3 || TFLAG:助手挿入箇所 == 3
	SOURCE:性行動 += 420
	SOURCE:達成 += 150
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && FALLTYPE(TARGET) != 1
	SOURCE:反感 += 1000

;Vに挿入している場合
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女
		SOURCE:苦痛 += 5500
		SOURCE:反感 += 3000
	ENDIF
	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:再生処女
		SOURCE:苦痛 += 5500
		SOURCE:反感 += 1000
	ENDIF
	CALL 特殊ソース処理_Ｖ
ENDIF

;Aに挿入している場合
SIF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	CALL 特殊ソース処理_Ａ

CALL 特殊ソース処理_セックス

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック（主人）
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		LOCAL = 1
	CASE 2
		LOCAL = 2
	CASE 3
		LOCAL = 4
ENDSELECT
SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		LOCAL:1 = 1
	CASE 2
		LOCAL:1 = 2
	CASE 3
		LOCAL:1 = 4
ENDSELECT
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	LOCAL:2 = 4
ELSEIF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	LOCAL:2 = 5
ELSE
	LOCAL:2 = 0
ENDIF

CALL SAMEN_CHECK_M, LOCAL
CALL SAMEN_CHECK_A, LOCAL:1
CALL SAMEN_CHECK_T, LOCAL:2

CALL SAMEN_SHOOT, LOCAL, NOWEX:MASTER:射精, MASTER
CALL SAMEN_SHOOT, LOCAL:1, NOWEX:ASSI:射精, ASSI
CALL SAMEN_SHOOT_T, LOCAL:2, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
SELECTCASE TFLAG:主人挿入箇所
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔主人のペニスの汚れが移動
		IF PENIS(MASTER)
			STAIN:膣内 |= STAIN:MASTER:ペニス
			STAIN:MASTER:ペニス |= STAIN:膣内
		ENDIF
	CASE 2
		;奴隷のアナル⇔主人のペニスの汚れが移動
		IF PENIS(MASTER)
			SETBIT STAIN:MASTER:ペニス, 3
			STAIN:アナル |= STAIN:MASTER:ペニス
			STAIN:MASTER:ペニス |= STAIN:アナル
		ENDIF
ENDSELECT

SELECTCASE TFLAG:助手挿入箇所
	CASE 1
		;膣内に破瓜の血の汚れを付加
		SIF TALENT:処女 || TALENT:再生処女
			SETBIT STAIN:膣内, 6
		;奴隷の膣内⇔助手のペニスの汚れが移動
		IF PENIS(ASSI)
			STAIN:膣内 |= STAIN:ASSI:ペニス
			STAIN:ASSI:ペニス |= STAIN:膣内
		ENDIF
	CASE 2
		;奴隷のアナル⇔助手のペニスの汚れが移動
		IF PENIS(ASSI)
			SETBIT STAIN:ASSI:ペニス, 3
			STAIN:アナル |= STAIN:ASSI:ペニス
			STAIN:ASSI:ペニス |= STAIN:アナル
		ENDIF
ENDSELECT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Vに挿入している場合
IF TFLAG:主人挿入箇所 == 1 || TFLAG:助手挿入箇所 == 1
	EXP:Ｖ経験 += 2
	PRINTL Ｖ経験+2
ENDIF

;Aに挿入している場合
IF TFLAG:主人挿入箇所 == 2 || TFLAG:助手挿入箇所 == 2
	EXP:Ａ経験 += 2
	PRINTL Ａ経験+2
ENDIF

;性交経験
;VとAに挿入している場合（口に挿入していない場合）
IF TFLAG:主人挿入箇所 != 3 && TFLAG:助手挿入箇所 != 3
	EXP:性交経験 += 2
	EXP:MASTER:性交経験++
	EXP:ASSI:性交経験++
	PRINTL 性交経験+2
	経験回数:MASTER:(NO:TARGET) += 1
	経験回数:ASSI:(NO:TARGET) += 1
	経験回数:TARGET:(NO:MASTER) += 1
	経験回数:TARGET:(NO:ASSI) += 1
;主人だけ挿入している場合
ELSEIF TFLAG:主人挿入箇所 != 3
	EXP:性交経験++
	EXP:MASTER:性交経験++
	PRINTL 性交経験+1
	経験回数:MASTER:(NO:TARGET) += 1
	経験回数:TARGET:(NO:MASTER) += 1
;助手だけ挿入している場合
ELSE
	EXP:性交経験++
	EXP:ASSI:性交経験++
	PRINTL 性交経験+1
	経験回数:ASSI:(NO:TARGET) += 1
	経験回数:TARGET:(NO:ASSI) += 1
ENDIF

;同性愛経験
IF SEX(TARGET) == SEX(MASTER) && SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			PRINTL ゲイ経験+6
			EXP:ゲイ経験 += 6
		CASE 2
			PRINTL レズ経験+6
			EXP:レズ経験 += 6
	ENDSELECT
ELSEIF SEX(TARGET) == SEX(MASTER) || SEX(TARGET) == SEX(ASSI)
	SELECTCASE SEX(TARGET)
		CASE 1
			PRINTL ゲイ経験+3
			EXP:ゲイ経験 += 3
		CASE 2
			PRINTL レズ経験+3
			EXP:レズ経験 += 3
	ENDSELECT
ENDIF

PRINTL 主人調教経験+2
PRINTL 助手調教経験+2
EXP:主人調教経験 += 2
EXP:助手調教経験 += 2

;魔力回復
IF TALENT:魔術師
	IF !TEQUIP:ASSI:コンドーム
		LOCAL = MAXBASE:魔力/300
		BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
	ENDIF
	IF !TEQUIP:PLAYER:コンドーム
		LOCAL = MAXBASE:魔力/300
		BASE:魔力 = LIMIT(BASE:魔力+LOCAL, 0, MAXBASE:魔力)
	ENDIF
ENDIF

;依存度補正値(主人かつ恋慕で1、親愛で2）
IF TALENT:恋慕
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

