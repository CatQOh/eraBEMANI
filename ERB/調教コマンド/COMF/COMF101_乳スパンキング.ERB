;-------------------------------------------------
;乳スパンキング
;SM系コマンド、レベル1
;-------------------------------------------------
@USERCOM_ABLE101
;乳スパンキング実行判定
;従順3未満のASSIの場合スパンキングはできない（サドならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 3 && !TALENT:ASSI:サド
		RETURN 0
ENDIF

RETURN 1

@COM101
PRINTL 乳スパンキング
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 30

SOURCE:苦痛 = 300
SOURCE:恐怖 = 300
SOURCE:露出 = 100
SOURCE:逸脱 = 50
SOURCE:反感 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;マゾっ気に応じて快Bのソースを追加する
SOURCE:快B += 100*ABL:マゾっ気

;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
IF PALAM:苦痛 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.00
ELSEIF PALAM:苦痛 < PALAMLV:2
	TIMES SOURCE:苦痛 , 1.50
ELSEIF PALAM:苦痛 < PALAMLV:3
	TIMES SOURCE:苦痛 , 2.00
ELSEIF PALAM:苦痛 < PALAMLV:4
	TIMES SOURCE:苦痛 , 2.50
ELSEIF PALAM:苦痛 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 3.00
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の手の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

;実績:レイシスのち首を弾く
SIF NO:TARGET != 0 && NAME:TARGET == "レイシス"
	CALL 実績解除, "はじけっ！ギュイ～ン！"

RETURN 1

