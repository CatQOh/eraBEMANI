;-------------------------------------------------
;蝋燭
;SM系コマンド、レベル3
;-------------------------------------------------
@USERCOM_ABLE103
;蝋燭実行判定
;アイテムを持っていないとダメ
SIF !ITEM:低温蝋燭
	RETURN 0
;従順4未満のASSIの場合蝋燭はできない（サドならＯＫ）
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		RETURN 0
ENDIF
;全裸でないとダメ（足元、特殊は除く）
SIF TEQUIP:上半身下着 || TEQUIP:下半身下着 || TEQUIP:上半身上着 || TEQUIP:下半身上着 || TEQUIP:全身上着
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURN 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURN 0

RETURN 1

@COM103
PRINTL 蝋燭
CALL KOJO_MESSAGE_COM

ITEM:低温蝋燭--

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 80
LOSEBASE:気力 += 30

SOURCE:苦痛 = 1000
SOURCE:恐怖 = 1500
SOURCE:逸脱 = 150
SOURCE:反感 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
IF PALAM:苦痛 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.00
ELSEIF PALAM:苦痛 < PALAMLV:2
	TIMES SOURCE:苦痛 , 1.50
ELSEIF PALAM:苦痛 < PALAMLV:3
	TIMES SOURCE:苦痛 , 2.00
ELSEIF PALAM:苦痛 < PALAMLV:4
	TIMES SOURCE:苦痛 , 2.50
ELSEIF PALAM:苦痛 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 3.00
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 4

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

