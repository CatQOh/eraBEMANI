;-------------------------------------------------
;異物挿入解除
;-------------------------------------------------
@USERCOM_ABLE_異物挿入解除
;異物挿入解除
;異物が挿入されてないと実行できない
SIF !TEQUIP:Ｖ異物 && !TEQUIP:Ａ異物
	RETURN 0

RETURN 1

@COM_異物挿入解除
PRINTL 異物挿入解除

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 20

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.30
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:自慰中毒
ENDSELECT

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

IF TEQUIP:Ｖ異物
	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES LOCAL , 0.40
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES LOCAL , 0.60
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES LOCAL , 0.60
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES LOCAL , 1.00
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES LOCAL , 1.20
	ENDIF

	;奴隷のABL:感覚をみる
	SELECTCASE ABL:Ｖ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += LOCAL*(ABL:Ｖ感覚-2)
	ENDSELECT
ENDIF

IF TEQUIP:Ａ異物
	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES LOCAL , 0.20
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES LOCAL , 0.50
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES LOCAL , 1.00
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES LOCAL , 1.50
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES LOCAL , 2.50
	ENDIF

	SELECTCASE ABL:Ａ感覚
		CASE 0
			TIMES LOCAL , 0.50
		CASE 1
			TIMES LOCAL , 0.80
		CASE 2
			TIMES LOCAL , 1.00
		CASE 3
			TIMES LOCAL , 1.50
		CASE 4
			TIMES LOCAL , 2.00
		CASEELSE
			LOCAL += LOCAL*(ABL:Ａ感覚-2)
	ENDSELECT

	;奴隷が男の場合はホモっ気を見る
	IF SEX(TARGET) == 1
		SELECTCASE ABL:ホモっ気
			CASE 0
				TIMES LOCAL , 0.15
			CASE 1
				TIMES LOCAL , 0.30
			CASE 2
				TIMES LOCAL , 0.50
			CASE 3
				TIMES LOCAL , 0.75
			CASE 4
				TIMES LOCAL , 1.00
			CASEELSE
				LOCAL += (LOCAL/10)*ABL:ホモっ気-3
		ENDSELECT
	;女なら無条件で半減
	ELSE
		TIMES LOCAL , 0.50
	ENDIF
ENDIF

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;解除は口上表示、射精ゲージ処理を終えてから
;-------------------------------------------------
TEQUIP:Ｖ異物 = 0
TEQUIP:Ａ異物 = 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 90

SOURCE:苦痛 = 100
SOURCE:恐怖 = 100
SOURCE:露出 = 50
SOURCE:逸脱 = 100
SOURCE:反感 = 50

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 40

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 0.80
	TIMES SOURCE:反感 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 0.40
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 0.20
ENDIF

RETURN 1

