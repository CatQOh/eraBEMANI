;-------------------------------------------------
;自慰
;愛撫系コマンド
;-------------------------------------------------
@USERCOM_ABLE11
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0

RETURN 1

@実行判定11
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

IF ABL:欲望
	LOCAL += ABL:欲望*3
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望*3})
ENDIF
IF ABL:露出癖
	LOCAL += ABL:露出癖*4
	PRINTFORM +露出癖LV{ABL:露出癖}({ABL:露出癖*4})
ENDIF
IF ABL:自慰中毒
	LOCAL += ABL:自慰中毒*3
	PRINTFORM +自慰中毒LV{ABL:自慰中毒}({ABL:自慰中毒*3})
ENDIF
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印*3
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印*3})
ENDIF
;PALAM:欲情
SELECTCASE PALAM:欲情
	CASE IS < PALAMLV:1
		LOCAL:1 = 0
	CASE IS < PALAMLV:2
		LOCAL:1 = 1
	CASE IS < PALAMLV:3
		LOCAL:1 = 2
	CASE IS < PALAMLV:4
		LOCAL:1 = 3
	CASE IS < PALAMLV:5
		LOCAL:1 = 4
	CASEELSE
		LOCAL:1 = 5
ENDSELECT
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	TIMES LOCAL:2 , 1.50
IF LOCAL:2
	LOCAL += LOCAL:2*3
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2*3})
ENDIF

;自制心
IF TALENT:自制心
	LOCAL -= 5
	PRINTFORM -自制心(5)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 5
	PRINTFORM -恥じらい(5)
ENDIF
;恥薄い
IF TALENT:恥薄い
	LOCAL += 2
	PRINTFORM +恥薄い(2)
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	LOCAL += 5
	PRINTFORM +自慰しやすい(5)
ENDIF
;快感に素直
IF TALENT:快感に素直
	LOCAL += 5
	PRINTFORM +快感に素直(5)
ENDIF
;快感の否定 淫乱or娼婦で無効
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	LOCAL -= 5
	PRINTFORM -快感の否定(5)
ENDIF
;媚薬
IF TEQUIP:媚薬
	LOCAL += 8
	PRINTFORM +媚薬(8)
ENDIF

;合計を表示(33以上で実行)
PRINTFORM  = {LOCAL}

;難易度上昇
;公開で+10、バイブで+5、アナルバイブで+5、シャワーで＋3
LOCAL:1 = 33
SIF TEQUIP:ビデオカメラ
	LOCAL:1 += 10
SIF TEQUIP:バイブ
	LOCAL:1 += 5
SIF TEQUIP:アナルバイブ
	LOCAL:1 += 5
SIF TEQUIP:シャワー
	LOCAL:1 += 3

SELECTCASE LOCAL
	CASE IS < LOCAL:1
		PRINT  < 
	CASE LOCAL:1
		PRINT  = 
	CASE IS > LOCAL:1
		PRINT  > 
ENDSELECT
PRINTFORM 実行値{LOCAL:1}

WAIT

;実行できない
IF LOCAL < LOCAL:1
		CALL KOJO_MESSAGE_EVENT, 100
	RETURN 0
ELSE
	RETURN 1
ENDIF


@COM11
;自慰
;バイブ、アナルバイブ、ビデオ撮影の時に内容変化

;ビデオ撮影中なら公開オナニー
;シャワー使用中ならシャワーオナニー
;バイブ、アナルバイブ、或いは両方装備でバイブオナニー
;異物あれば異物オナニー
SIF TEQUIP:ビデオカメラ
	PRINT 公開
SIF TEQUIP:シャワー
	PRINT シャワー
IF TEQUIP:バイブ && TEQUIP:アナルバイブ
	PRINT 二穴バイブ
ELSEIF TEQUIP:バイブ
	PRINT バイブ
ELSEIF TEQUIP:アナルバイブ
	PRINT アナルバイブ
ELSEIF TEQUIP:Ｖ異物 && TEQUIP:Ａ異物
	PRINT 両穴異物挿入
ELSEIF TEQUIP:Ｖ異物
	PRINT 異物
ELSEIF TEQUIP:Ａ異物
	PRINT アナル異物
ENDIF

IF TEQUIP:ビデオカメラ || TEQUIP:シャワー || TEQUIP:バイブ || TEQUIP:アナルバイブ || TEQUIP:Ｖ異物 || TEQUIP:Ａ異物
	PRINTL オナニー
ELSE
	PRINTL 自慰
ENDIF

SIF !TFLAG:実行判定無視
	CALL 実行判定11
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 200

;ABL:技巧をみる
LOCAL += LOCAL*ABL:技巧/5

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += LOCAL*(ABL:Ｃ感覚-3)
ENDSELECT

;奴隷のABL:露出癖をみる
SELECTCASE ABL:露出癖
	CASE 0
		TIMES LOCAL , 0.20
	CASE 1
		TIMES LOCAL , 0.40
	CASE 2
		TIMES LOCAL , 0.80
	CASE 3
		TIMES LOCAL , 1.00
	CASE 4
		TIMES LOCAL , 1.10
	CASEELSE
		LOCAL += (LOCAL/10)*ABL:露出癖-2
ENDSELECT

;奴隷のABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES LOCAL , 0.80
	CASE 1
		TIMES LOCAL , 1.00
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.40
	CASE 4
		TIMES LOCAL , 1.60
	CASEELSE
		LOCAL += (LOCAL/5)*ABL:自慰中毒
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 80

SOURCE:快C = 80
SOURCE:快B = 80
SOURCE:性行動 = 150
SOURCE:恐怖 = 150
SOURCE:中毒 = 100
SOURCE:露出 = 300
SOURCE:屈従 = 200
SOURCE:逸脱 = 150
SOURCE:反感 = 300
;バイブ、Ｖ異物
IF TEQUIP:バイブ || TEQUIP:Ｖ異物
	SOURCE:快V += 100
	SOURCE:苦痛 += 50
	SOURCE:液体 += 100
	SOURCE:屈従 += 100
	SOURCE:反感 += 100
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;アナルバイブ、Ａ異物
IF TEQUIP:アナルバイブ || TEQUIP:Ａ異物
	SOURCE:快A += 100
	SOURCE:苦痛 += 150
	SOURCE:液体 += 30
	SOURCE:屈従 += 150
	SOURCE:反感 += 150
	TIMES SOURCE:快C , 0.70
	TIMES SOURCE:快B , 0.70
ENDIF
;シャワー
IF TEQUIP:シャワー
	SOURCE:快C += 50
	SOURCE:性行動 += 50
	SOURCE:露出 += 100
	SOURCE:屈従 += 50
	SOURCE:反感 += 80
ENDIF
;ビデオ
IF TEQUIP:ビデオカメラ
	SOURCE:恐怖 += 100
	SOURCE:液体 += 50
	SOURCE:露出 += 200
	SOURCE:欲情 += 100
	SOURCE:反感 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;中毒による中毒充足のソースは先に処理しておく
;自慰中毒を見る
SELECTCASE ABL:自慰中毒
	CASE 0, 1
		TIMES SOURCE:快C , 1.00
		TIMES SOURCE:快V , 1.00
		TIMES SOURCE:快A , 1.00
		TIMES SOURCE:快B , 1.00
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:快C , 1.10
		TIMES SOURCE:快V , 1.10
		TIMES SOURCE:快A , 1.10
		TIMES SOURCE:快B , 1.10
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:快C , 1.20
		TIMES SOURCE:快V , 1.20
		TIMES SOURCE:快A , 1.20
		TIMES SOURCE:快B , 1.20
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:快C , 1.30
		TIMES SOURCE:快V , 1.30
		TIMES SOURCE:快A , 1.30
		TIMES SOURCE:快B , 1.30
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:快C , 1.50
		TIMES SOURCE:快V , 1.50
		TIMES SOURCE:快A , 1.50
		TIMES SOURCE:快B , 1.50
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:快C , 1.70
		TIMES SOURCE:快V , 1.70
		TIMES SOURCE:快A , 1.50
		TIMES SOURCE:快B , 1.50
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:快C , 2.00
		TIMES SOURCE:快V , 2.00
		TIMES SOURCE:快A , 1.70
		TIMES SOURCE:快B , 1.70
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:快C , 2.30
		TIMES SOURCE:快V , 2.30
		TIMES SOURCE:快A , 2.00
		TIMES SOURCE:快B , 2.00
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:快C , 2.50
		TIMES SOURCE:快V , 2.50
		TIMES SOURCE:快A , 2.50
		TIMES SOURCE:快B , 2.50
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:快C , 3.50
		TIMES SOURCE:快V , 3.50
		TIMES SOURCE:快A , 3.50
		TIMES SOURCE:快B , 3.50
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手⇔奴隷の胸の汚れが移動
STAIN:手 |= STAIN:胸
STAIN:胸 |= STAIN:手

;奴隷の指⇔奴隷のヴァギナの汚れが移動
IF VAGINA(TARGET)
	STAIN:手 |= STAIN:ヴァギナ
	STAIN:ヴァギナ |= STAIN:手
ENDIF
;奴隷の指⇔奴隷のペニスの汚れが移動
IF PENIS(TARGET)
	STAIN:手 |= STAIN:ペニス
	STAIN:ペニス |= STAIN:手
ENDIF

;シャワーオナニーの場合汚れをリセット、更に潤滑半分。
IF TEQUIP:シャワー
	STAIN:手 = 0
	STAIN:ヴァギナ = 0
	STAIN:ペニス = 0
	STAIN:胸 = 0
	PALAM:潤滑 /= 2
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオカメラ
	EXP:自慰経験 += 2
	PRINTL 自慰経験+2
ELSE
	EXP:自慰経験 += 1
	PRINTL 自慰経験+1
ENDIF
;公開オナニー時は異常経験付加
IF TEQUIP:ビデオカメラ
	IF !CFLAG:自慰を撮影された経験
		EXP:異常経験++
		PRINTL 異常経験+1
	ENDIF
	CFLAG:自慰を撮影された経験++
ENDIF

;装備中関数で計算してるのでコメントアウト
;挿入物による経験増加
;IF TEQUIP:バイブ
;	EXP:Ｖ経験 += 1
;	PRINTL Ｖ経験+1
;ENDIF
;IF TEQUIP:アナルバイブ
;	EXP:Ａ経験 += 1
;	PRINTL Ａ経験+1
;ENDIF
;IF TEQUIP:Ｖ異物
;	EXP:Ｖ経験 += TEQUIP:Ｖ異物
;	PRINTFORML Ｖ経験+{TEQUIP:Ｖ異物}
;ENDIF
;IF TEQUIP:Ａ異物
;	EXP:Ａ経験 += TEQUIP:Ａ異物
;	PRINTFORML Ａ経験+{TEQUIP:Ａ異物}
;ENDIF

;撮影時の調教内容チェック フロー図的には上書きしてるので問題なし
IF TEQUIP:バイブ && TEQUIP:アナルバイブ
	ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = 二穴バイブオナニー
ELSEIF TEQUIP:バイブ
	ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = バイブオナニー
ELSEIF TEQUIP:アナルバイブ
	ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = アナルバイブオナニー
ELSEIF TEQUIP:シャワー
	ビデオ撮影内容:(TFLAG:ビデオ撮影回数) = シャワーオナニー
ENDIF

LOCAL:1 = 1
SIF TEQUIP:ビデオカメラ
	LOCAL:1++
SIF TEQUIP:シャワー
	LOCAL:1++
SIF TEQUIP:バイブ
	LOCAL:1++
SIF TEQUIP:アナルバイブ
	LOCAL:1++
SIF TEQUIP:Ｖ異物
	LOCAL:1++
SIF TEQUIP:Ａ異物
	LOCAL:1++

CALL EXPUP, 2, LOCAL:1

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属
	TFLAG:依存度補正 += 2
ENDIF


LOCAL = 0
;不要なループを避けるための処理 CFLAGでやったほうがいいかもしれない
SIF 男性に自慰させた人数:(NO:TARGET)
	LOCAL = 1
男性に自慰させた人数:(NO:TARGET)++
IF LOCAL
	LOCAL = 0
	REPEAT 10000
		SIF 男性に自慰させた人数:COUNT
			LOCAL++
	REND
	SIF LOCAL >= 10
		CALL 実績解除, "シコり続けるのだ……"
ENDIF


RETURN 1

