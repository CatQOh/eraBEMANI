;-------------------------------------------------
;ダブルパイズリ
;助手によるコマンド、及びレズプレイ、レベル3
;-------------------------------------------------
@USERCOM_ABLE125
;ダブルパイズリ
;助手じゃないとダメ
SIF PLAYER != ASSI
	RETURN 0
;助手、奴隷共に女性じゃないとダメ
SIF SEX(TARGET) == 1 || SEX(ASSI) == 1
	RETURN 0
;主人がペニス無いとダメ
SIF !PENIS(MASTER)
	RETURN 0
;主人が[魅惑]か[謎の魅力]、もしくは奴隷と助手両方陥落してないとダメ
SIF !TALENT:MASTER:魅惑 && !TALENT:MASTER:謎の魅力 && (!CFLAG:陥落 || !CFLAG:ASSI:陥落)
	RETURN 0
;助手が奉仕精神LV3以上、技巧LV4以上じゃないとダメ
SIF ABL:ASSI:技巧 < 4 || ABL:ASSI:奉仕精神 < 3
	RETURN 0
;上半身下着、上半身上着、全身上着をつけているとダメ
SIF TEQUIP:上半身下着 || TEQUIP:上半身上着 || TEQUIP:全身上着
	RETURN 0
;助手も服着てるとダメ
SIF TEQUIP:ASSI:上半身下着 || TEQUIP:ASSI:上半身上着 || TEQUIP:ASSI:全身上着
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0

RETURN 1

@実行判定125
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

;PALAM:欲情
LOCAL:1 = 0
IF PALAM:欲情 < PALAMLV:1
	LOCAL:1 = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	LOCAL:1 = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	LOCAL:1 = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	LOCAL:1 = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	LOCAL:1 = 4
ELSE
	LOCAL:1 = 5
ENDIF
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 += 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	PRINTFORM -恥じらい(3)
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	LOCAL += 2
	PRINTFORM +汚臭鈍感(2)
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	LOCAL -= 6
	PRINTFORM -汚臭敏感(6)
ENDIF
;献身的
IF TALENT:献身的
	LOCAL += 6
	PRINTFORM +献身的(6)
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感の否定 && FALLTYPE(TARGET) != 2
	LOCAL -= 2
	PRINTFORM -快感の否定(2)
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:男嫌い && SEX(MASTER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	LOCAL -= 12
	PRINTFORM -男嫌い(12)
ENDIF
;淫乱
IF TALENT:淫乱
	LOCAL += 5
	PRINTFORM +淫乱(5)
ENDIF
;娼婦
IF TALENT:娼婦
	LOCAL += 10
	PRINTFORM +娼婦(10)
ENDIF

LOCAL:1 = 0
;愛液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 0)
	LOCAL:1 += 1
;精液の汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 2)
	LOCAL:1 += 4
;アナルの汚れ
SIF GETBIT(STAIN:PLAYER:ペニス, 3)
	LOCAL:1 += 6
;尿
SIF GETBIT(STAIN:PLAYER:ペニス, 7)
	LOCAL:1 += 6

SIF TALENT:汚れ無視
	LOCAL:1 /= 3
SIF TALENT:潔癖症
	LOCAL:1 *= 2

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	IF TALENT:汚れ無視
		PRINT -汚れあり、汚れ無視
	ELSEIF TALENT:潔癖症
		PRINT -汚れあり、潔癖症
	ELSE
		PRINT -汚れあり
	ENDIF
	PRINTFORM ({LOCAL:1})
ENDIF

;合計を表示(38以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 38
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, 100
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM125
PRINTL ダブルパイズリ

SIF !TFLAG:実行判定無視
	CALL 実行判定125
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実績「クロノスとカイロスにダブルパイズリをさせる」
IF NAME:TARGET == "クロノス" || NAME:TARGET == "カイロス"
	IF ASSI > -1
		IF NAME:ASSI == "クロノス" || NAME:ASSI == "カイロス"
			CALL 実績解除, "竿、包み込む。弐つの双丘"
		ENDIF
	ENDIF
ENDIF

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 1000

;ABL:技巧をみる
SELECTCASE ABL:技巧
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.20
	CASE 2
		TIMES LOCAL , 1.40
	CASE 3
		TIMES LOCAL , 1.60
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		TIMES LOCAL , 2.00
ENDSELECT

;ABL:従順をみる
SELECTCASE ABL:従順
	CASE 0
		TIMES LOCAL , 0.80
	CASE 1
		TIMES LOCAL , 0.90
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.10
	CASE 4
		TIMES LOCAL , 1.20
	CASEELSE
		TIMES LOCAL , 1.30
ENDSELECT

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		TIMES LOCAL , 2.40
ENDSELECT

;助手のABL:技巧をみる
IF ABL:ASSI:技巧 >= 2
	TIMES LOCAL , 1.00
ELSEIF ABL:ASSI:技巧 == 3
	TIMES LOCAL , 1.20
ELSEIF ABL:ASSI:技巧 == 4
	TIMES LOCAL , 1.50
ELSE
	TIMES LOCAL , 1.80
ENDIF

;主人のABL:Ｃ感覚をみる
SELECTCASE ABL:MASTER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		TIMES LOCAL , 5.00
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:賢者タイム > 0
	LOCAL /= 20

BASE:MASTER:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快B = 200
SOURCE:情愛 = 300
SOURCE:性行動 = 600
SOURCE:達成 = 450
SOURCE:屈従 = 900
SOURCE:逸脱 = 400
SOURCE:反感 = 400

;上のほうで計算した汚れデータ
SOURCE:不潔 = LOCAL:1*50+100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精するのは主人なので専用の関数を呼ぶ
CALL SAMEN_CHECK_M, 5
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT, 5, NOWEX:MASTER:射精, MASTER
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者のペニスの汚れが移動
STAIN:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:胸

;助手の胸⇔調教者のペニスの汚れが移動
STAIN:ASSI:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:ASSI:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛経験
;助手と奴隷が女性じゃないと実行できないのでレズ経験のみ
IF SEX(MASTER) == 2
	PRINTL レズ経験+7
	EXP:レズ経験 += 7
ELSE
	PRINTL レズ経験+4
	EXP:レズ経験 += 4
ENDIF

;調教経験
PRINTL 主人調教経験+3
EXP:主人調教経験 += 3
PRINTL 助手調教経験+1
EXP:助手調教経験 += 1

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

