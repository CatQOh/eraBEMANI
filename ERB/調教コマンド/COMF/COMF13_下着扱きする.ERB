;-------------------------------------------------
;下着扱きする
;愛撫系コマンド
;-------------------------------------------------
@USERCOM_ABLE13
;下着扱きする実行判定
;対象がペニス無いとダメ
SIF !PENIS(TARGET)
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;実行者がなんらかの下着を着ていないとダメ
SIF CSTR:PLAYER:下半身下着 == ""
	RETURN 0

RETURN 1

@COM13
PRINTL 下着扱きする
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;脱衣
;-------------------------------------------------
IF TEQUIP:PLAYER:下半身下着
	IF CFLAG:PLAYER:挿入可
		TEQUIP:PLAYER:下半身下着 = 0
	ELSE
		TEQUIP:PLAYER:下半身下着 = 0
		TEQUIP:PLAYER:下半身上着 = 0
		TEQUIP:PLAYER:全身上着 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 300

;ABL:調教者の技巧をみる
SELECTCASE ABL:PLAYER:技巧
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.80
	CASE 2
		TIMES LOCAL , 2.60
	CASE 3
		TIMES LOCAL , 3.40
	CASE 4
		TIMES LOCAL , 4.20
	CASEELSE
		TIMES LOCAL , 5.00
ENDSELECT

;ABL:欲望をみる
SELECTCASE ABL:欲望
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.30
	CASE 4
		TIMES LOCAL , 1.40
	CASEELSE
		TIMES LOCAL , 1.50
ENDSELECT

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		TIMES LOCAL , 3.00
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 70

SOURCE:快C = 200
SOURCE:達成 = 50
SOURCE:液体 = 50
SOURCE:露出 = 100
SOURCE:屈従 = 200
SOURCE:逸脱 = 300
SOURCE:反感 = 300
;下着の汚れで不潔ソース増加
;愛液
SIF GETBIT(STAIN:PLAYER:下着, 0)
	SOURCE:不潔 += 30
;先走り
SIF GETBIT(STAIN:PLAYER:下着, 1)
	SOURCE:不潔 += 60
;精液
SIF GETBIT(STAIN:PLAYER:下着, 2)
	SOURCE:不潔 += 90
;アナル
SIF GETBIT(STAIN:PLAYER:下着, 3)
	SOURCE:不潔 += 120
;母乳
SIF GETBIT(STAIN:PLAYER:下着, 4)
	SOURCE:不潔 += 30
;尿
SIF GETBIT(STAIN:PLAYER:下着, 7)
	SOURCE:不潔 += 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のペニス⇔調教者の下着の汚れが移動
STAIN:ペニス |= STAIN:PLAYER:下着
STAIN:PLAYER:下着 |= STAIN:ペニス

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 2, 2

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 1
ELSEIF TALENT:娼婦 && RELATION:(NO:PLAYER) > 100
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

