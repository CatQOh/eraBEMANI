;-------------------------------------------------
;両穴フィスト
;-------------------------------------------------
@USERCOM_ABLE143
;両穴フィスト
;奴隷が服従か隷属でないとダメ
SIF !TALENT:服従 && !TALENT:隷属
	RETURN 0
;バイブ使用中は不可
SIF TEQUIP:バイブ
	RETURN 0
;アナルバイブ使用中は不可
SIF TEQUIP:アナルバイブ
	RETURN 0
;浣腸器使用中は不可
SIF TEQUIP:浣腸器
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:アナルビーズ
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:拡張バルーン
	RETURN 0
;電極使用中は不可
SIF TEQUIP:アナル電極
	RETURN 0
;異物挿入中は不可
SIF TEQUIP:Ｖ異物
	RETURN 0
SIF TEQUIP:Ａ異物
	RETURN 0
;ヴァギナ無いとダメ
SIF !VAGINA(TARGET)
	RETURN 0
;300以上のＶ・Ａ経験が必要
SIF EXP:Ｖ経験 < 300 || EXP:Ａ経験 < 300
	RETURN 0
;ASSIの場合、従順4以上かサドじゃないとできない。
IF PLAYER == ASSI
	SIF ABL:ASSI:従順 < 4 && !TALENT:ASSI:サド
		RETURN 0
ENDIF
;小柄体型なら従順が4以上かつマゾっ気が4以上かつ拡張経験10以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4 || EXP:Ｖ拡張経験 < 10 || EXP:Ａ拡張経験 < 10
		RETURN 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURN 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場 && !ITEM:プレイマット
	RETURN 0
;新妻プレイ中はダメ
SIF TEQUIP:エプロン
	RETURN 0

RETURN 1

@COM_両穴フィスト
PRINTL 両穴フィスト
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 250

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.00
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 1.20
ENDIF

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:Ａ感覚をみる
SELECTCASE ABL:Ａ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ａ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 500
LOSEBASE:気力 += 300

SOURCE:快V = 200
SOURCE:快A = 200
SOURCE:苦痛 = 2500
SOURCE:恐怖 = 1500
SOURCE:露出 = 200
SOURCE:屈従 = 2000
SOURCE:逸脱 = 2000
SOURCE:反感 = 2000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 2.50
	TIMES SOURCE:反感 , 4.00
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 2.00
	TIMES SOURCE:反感 , 3.50
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 1.80
	TIMES SOURCE:反感 , 3.00
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 1.60
	TIMES SOURCE:反感 , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:苦痛 , 1.40
	TIMES SOURCE:反感 , 2.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 2.00
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 3.00
	TIMES SOURCE:反感 , 4.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 2.50
	TIMES SOURCE:反感 , 3.50
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 2.40
	TIMES SOURCE:反感 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 2.00
	TIMES SOURCE:反感 , 2.50
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:苦痛 , 1.80
	TIMES SOURCE:反感 , 2.00
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:苦痛 , 1.60
	TIMES SOURCE:反感 , 1.50
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 3.00
	TIMES SOURCE:反感 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 2.50
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 2.00
	TIMES SOURCE:反感 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 1.50
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 1.00
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のアナル⇔調教者の手の汚れが移動
;奴隷の膣内⇔調教者の手の汚れが移動
SETBIT STAIN:PLAYER:手, 3
STAIN:PLAYER:手 |= STAIN:膣内
STAIN:PLAYER:手 |= STAIN:アナル
STAIN:アナル |= STAIN:PLAYER:手
STAIN:膣内 |= STAIN:PLAYER:手

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ｖ経験
EXP:Ｖ経験 += 25
PRINTL Ｖ経験+25

;Ａ経験
EXP:Ａ経験 += 25
PRINTL Ａ経験+25

;異常経験
IF !EXP:Ｖ拡張経験 && !EXP:Ａ拡張経験
	EXP:異常経験 += 2
	PRINTL 異常経験+2
ENDIF

EXP:Ｖ拡張経験 += 5
PRINTL Ｖ拡張経験+5

EXP:Ａ拡張経験 += 5
PRINTL Ａ拡張経験+5

CALL EXPUP, 3, 6

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

