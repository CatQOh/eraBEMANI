;-------------------------------------------------
;空気浣腸
;-------------------------------------------------
@USERCOM_ABLE_空気浣腸
;空気浣腸実行判定

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

;無機物はダメ
SIF TALENT:無機物
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && !TALENT:MASTER:禁断の知識
	RETURN 0
;アイテムを持っていないとダメ
SIF !ITEM:空気ポンプ
	RETURN 0
;アナルバイブ使用中は不可
SIF TEQUIP:アナルバイブ
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:アナルビーズ
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:浣腸器
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:拡張バルーン
	RETURN 0
;電極使用中は不可
SIF TEQUIP:アナル電極
	RETURN 0
;異物挿入中は不可
SIF TEQUIP:Ａ異物
	RETURN 0
;Ａ経験が50未満じゃできない
SIF EXP:Ａ経験 < 50
	RETURN 0
;露出癖、欲望、従順の合計が10ないとできない
SIF ABL:露出癖+ABL:欲望+ABL:従順 < 10
	RETURN 0

RETURN 1

@COM_空気浣腸
PRINTL 空気浣腸
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 40
LOSEBASE:気力 += 100

SOURCE:快A = 100
SOURCE:苦痛 = 150
SOURCE:露出 = 700
SOURCE:屈従 = 50
SOURCE:逸脱 = 300
SOURCE:反感 = 400

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 100

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 3.00
	TIMES SOURCE:反感 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 0.80
	TIMES SOURCE:反感 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 0.60
	TIMES SOURCE:反感 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:苦痛 , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:苦痛 , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 1.00
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 0.20
ENDIF

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
PRINTL Ａ経験+1
EXP:Ａ経験++

CALL EXPUP, 1, 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

