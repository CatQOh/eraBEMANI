;-------------------------------------------------
;乳房電極
;ハードな調教コマンド、レベル4
;-------------------------------------------------
@USERCOM_ABLE150
;乳房電極実行判定
;奴隷が服従か隷属でないとダメ
SIF !TALENT:服従 && !TALENT:隷属
	RETURN 0
;上半身下着または上半身上着または全身上着をつけているとダメ
SIF TEQUIP:上半身下着 || TEQUIP:上半身上着 || TEQUIP:全身上着
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:乳房電極
	RETURN 1
;アイテムを持っていないとダメ
SIF !ITEM:電極プラグ
	RETURN 0
;ニプルキャップ使用中は不可
SIF TEQUIP:ニプルキャップ
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:搾乳器
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場
	RETURN 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF PLAYER == ASSI
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF

RETURN 1

@COM150
PRINTL 乳房電極
CALL KOJO_MESSAGE_COM

;着脱の処理
INVERTBIT TEQUIP:乳房電極, 0

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 180

SOURCE:快B = 500
SOURCE:苦痛 = 1500
SOURCE:露出 = 300
SOURCE:屈従 = 800
SOURCE:逸脱 = 1200
SOURCE:反感 = 1200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;異常経験が5未満のとき、異常経験追加
IF EXP:異常経験 < 5
	EXP:異常経験 += 1
	PRINTL 異常経験+1
ENDIF

CALL EXPUP, 1, 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:隷属 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM150
PRINTL ＜乳房電極装着中＞

LOSEBASE:体力 += 70
LOSEBASE:気力 += 100

SOURCE:快B += 250
SOURCE:苦痛 += 750
SOURCE:屈従 += 400
SOURCE:逸脱 += 600
SOURCE:反感 += 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;機械には効果が高い
SIF CSTR:種族 == "機械"
	TIMES SOURCE:苦痛 , 4.00

RETURN 1
