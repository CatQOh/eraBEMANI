;-------------------------------------------------
;強精神薬
;特殊コマンド
;-------------------------------------------------
@USERCOM_ABLE191
;強精神薬実行判定
;アイテムを持っていないとダメ
SIF !ITEM:強精神薬
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	RETURN 0
;[無機物]もしくは種族:機械だとダメ
SIF TALENT:無機物 || CSTR:種族 == "機械"
	RETURN 0
;効果発揮中・反動中はダメ
SIF TEQUIP:強精神薬
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0

RETURN 1

@COM191
PRINTL 強精神薬
CALL KOJO_MESSAGE_COM

ITEM:強精神薬--
TEQUIP:強精神薬 = 7

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM191
IF TEQUIP:強精神薬 > 2
	PRINTL ＜強精神薬効果発揮中＞
	TEQUIP:強精神薬--
ELSEIF TEQUIP:強精神薬 == 2
	TEQUIP:強精神薬 = 1
	PRINTFORML %CALLNAME:TARGET%の強精神剤の効果が切れたようだ
	PRINTL ＜強精神薬反動中＞
ELSEIF TEQUIP:強精神薬 == 1
	PRINTL ＜強精神薬反動中＞
ENDIF

SIF TEQUIP:媚薬 && TEQUIP:強精神薬 && TEQUIP:利尿剤
	CALL 実績解除, "バッドトリップ"

RETURN 1

