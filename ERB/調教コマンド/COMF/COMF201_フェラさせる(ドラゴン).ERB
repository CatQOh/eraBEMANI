;-------------------------------------------------
;フェラさせる(ドラゴン)
;龍姦コマンド
;-------------------------------------------------
@USERCOM_ABLE201
;フェラさせる(ドラゴン)実行判定
;ボールギャグ使用中は不可
SIF TEQUIP:ボールギャグ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;風呂場だとダメ
SIF TEQUIP:お風呂場
	RETURN 0
;PLAYERがドラゴン使い持ってないとダメ
SIF !TALENT:PLAYER:ドラゴン使い
	RETURN 0
;PLAYERが助手で未陥落だとダメ
SIF PLAYER == ASSI && !CFLAG:PLAYER:陥落
	RETURN 0

RETURN 1

@実行判定201
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

VARSET LOCAL, 0
LOCAL = RESULT

;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望
	PRINTFORM +欲望LV{ABL:欲望}({ABL:欲望})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神*4
	PRINTFORM +奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神*4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒*3
	PRINTFORM +精液中毒LV{ABL:精液中毒}({ABL:精液中毒*3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	LOCAL += MARK:快楽刻印
	PRINTFORM +快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印})
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	LOCAL:1 = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	LOCAL:1 = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	LOCAL:1 = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	LOCAL:1 = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	LOCAL:1 = 4
ELSE
	LOCAL:1 = 5
ENDIF
LOCAL:2 = LOCAL:1
SIF TALENT:衝動的
	LOCAL:2 *= 2
IF LOCAL:2
	LOCAL += LOCAL:2
	PRINTFORM +欲情LV{LOCAL:1}({LOCAL:2})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 2
	PRINTFORM -恥じらい(2)
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	LOCAL += 2
	PRINTFORM +汚臭鈍感(2)
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	LOCAL -= 10
	PRINTFORM -汚臭敏感(10)
ENDIF
;献身的
IF TALENT:献身的
	LOCAL += 6
	PRINTFORM +献身的(6)
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:男嫌い && SEX(PLAYER) == 1 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	LOCAL -= 12
	PRINTFORM -男嫌い(12)
ENDIF
;恋慕
IF TALENT:恋慕 && PLAYER == MASTER
	LOCAL += 5
	PRINTFORM +恋慕(5)
ENDIF
;親愛
IF TALENT:親愛 && PLAYER == MASTER
	LOCAL += 10
	PRINTFORM +親愛(10)
ENDIF

;合計を表示(42以上で実行)
PRINTFORM  = {LOCAL}

LOCAL:2 = 42
SELECTCASE LOCAL
	CASE IS < LOCAL:2
		PRINT  < 
	CASE IS > LOCAL:2
		PRINT  > 
	CASEELSE
		PRINT  = 
ENDSELECT
PRINTFORM 実行値{LOCAL:2}
WAIT
;実行できない
IF LOCAL < LOCAL:2
		CALL KOJO_MESSAGE_EVENT, 100
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COM201
PRINTL フェラさせる(ドラゴン)

SIF !TFLAG:実行判定無視
	CALL 実行判定201
SIF !RESULT && !TFLAG:実行判定無視
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:奴隷実行 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 500

;ABL:技巧をみる
LOCAL += (LOCAL/5)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += LOCAL/3*ABL:奉仕精神
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

BASE:PLAYER:ドラゴン射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 80

SOURCE:情愛 = 50
SOURCE:性行動 = 800
SOURCE:達成 = 200
SOURCE:屈従 = 1000
SOURCE:恐怖 = 500
SOURCE:逸脱 = 500
SOURCE:反感 = 300
SOURCE:不潔 = 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_DRAGON
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_DRAGON, NOWEX:PLAYER:ドラゴン射精
CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
PRINTL フェラ経験+2
EXP:フェラ経験 += 2

CALL EXPUP, 2, 2

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:猫舌
	EXP:嗜虐快楽経験++
	PRINTL 嗜虐快楽経験+1
ENDIF

TFLAG:奉仕快楽 = 1

RETURN 1

