;-------------------------------------------------
;電気按摩
;道具使用コマンド、レベル2
;-------------------------------------------------
@USERCOM_ABLE41
;電気按摩実行判定
;アイテムを持っていないとダメ
SIF !ITEM:電気按摩
	RETURN 0
;ASSIの場合、技巧3以上かサドじゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3 && !TALENT:ASSI:サド
		RETURN 0
ENDIF

;触手調教中はダメ
SIF TEQUIP:触手
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワー
	RETURN 0

RETURN 1

@COM41
PRINTL 電気按摩
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 250

;ABL:調教者の技巧をみる
SELECTCASE ABL:PLAYER:技巧
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.20
	CASE 2
		TIMES LOCAL , 1.40
	CASE 3
		TIMES LOCAL , 1.60
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		TIMES LOCAL , 2.00
ENDSELECT

;ABL:自慰中毒をみる
SELECTCASE ABL:自慰中毒
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.30
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		TIMES LOCAL , 2.50
ENDSELECT

;ABL:欲望をみる
SELECTCASE ABL:欲望
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.10
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.30
	CASE 4
		TIMES LOCAL , 1.40
	CASEELSE
		TIMES LOCAL , 1.50
ENDSELECT

;奴隷のABL:Ｃ感覚をみる
SELECTCASE ABL:Ｃ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		TIMES LOCAL , 3.00
ENDSELECT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快C = 500
SOURCE:露出 = 40
SOURCE:逸脱 = 60
SOURCE:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 60

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

RETURN 1

