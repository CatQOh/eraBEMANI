;-------------------------------------------------
;正常位キス
;-------------------------------------------------
@USERCOM_ABLE_正常位キス
;正常位キス
;陥落していないとダメ
SIF !CFLAG:陥落
	RETURN 0

;服着てるとダメ
SIF !性器露出(TARGET)
	RETURN 0

RETURN 1

@COM_正常位キス
PRINTL 正常位キス
SELECTCOMS = 正常位キス
SELECTCOM = 530
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:膣コマンド = 1
SIF FALLTYPE(TARGET) == 1 && PLAYER == MASTER && TALENT:処女
	TFLAG:恋慕処女 = 1

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 800

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順 - (LOCAL/10)*2

;ABL:欲望をみる
LOCAL += (LOCAL/10)*ABL:欲望

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.80
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 1.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
SELECTCASE ABL:PLAYER:Ｃ感覚
	CASE 0
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 2.00
	CASE 3
		TIMES LOCAL , 2.50
	CASE 4
		TIMES LOCAL , 3.50
	CASEELSE
		LOCAL += LOCAL*(ABL:PLAYER:Ｃ感覚-1)
ENDSELECT

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

;相性を見る
LOCAL *= RELATION:PLAYER:(NO:TARGET)
LOCAL /= 100

SIF CFLAG:嫁 || CFLAG:専属娼婦 || CFLAG:傀儡
	LOCAL += 3

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:賢者タイム > 0
	LOCAL /= 20

BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
LOCAL = 700

;ABL:調教者の技巧をみる
LOCAL += (LOCAL/5)*ABL:PLAYER:技巧

;ABL:技巧をみる
LOCAL += (LOCAL/10)*ABL:技巧

;ABL:従順をみる
LOCAL += (LOCAL/10)*ABL:従順

;ABL:欲望をみる
LOCAL += (LOCAL/5)*ABL:欲望

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.00
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES LOCAL , 1.20
ENDIF

;奴隷のABL:Ｖ感覚をみる
SELECTCASE ABL:Ｖ感覚
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 2.00
	CASEELSE
		LOCAL += (LOCAL/2)*ABL:Ｖ感覚
ENDSELECT

;奴隷のABL:マゾっ気をみる
LOCAL += (LOCAL/5)*ABL:マゾっ気

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
	CASE 0
		TIMES LOCAL , 0.50
	CASE 1
		TIMES LOCAL , 0.80
	CASE 2
		TIMES LOCAL , 1.20
	CASE 3
		TIMES LOCAL , 1.50
	CASE 4
		TIMES LOCAL , 1.80
	CASEELSE
		LOCAL += (LOCAL/3)*ABL:奉仕精神
ENDSELECT

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 2.00

;TALENT:陥落系素質をみる
IF PLAYER == MASTER
	SIF TALENT:淫乱 || TALENT:服従
		TIMES LOCAL , 1.50
	SIF TALENT:恋慕 || TALENT:娼婦 || TALENT:隷属
		TIMES LOCAL , 2.00
	SIF TALENT:親愛
		TIMES LOCAL , 3.00
ENDIF

;相性を見る
LOCAL *= RELATION:TARGET:(NO:PLAYER)
LOCAL /= 100

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:賢者タイム > 0
	LOCAL /= 20

BASE:射精 += LOCAL

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 49
LOSEBASE:気力 += 77

SOURCE:快V = 700
SOURCE:情愛 = 1250
SOURCE:苦痛 = 300
SOURCE:中毒 = 400
SOURCE:露出 = 50
SOURCE:反感 = 80
SOURCE:恭順 = 1000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (FALLTYPE(TARGET) != 1 || PLAYER != MASTER)
	SOURCE:反感 += 1000

;Vへの苦痛は先に処理しておく
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:苦痛 , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:苦痛 , 0.80
	TIMES SOURCE:反感 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:苦痛 , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:苦痛 , 0.10
ELSEIF EXP:Ｖ経験 >= EXPLV:4
	TIMES SOURCE:苦痛 , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:苦痛 , 1.20
	TIMES SOURCE:反感 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:苦痛 , 0.70
	TIMES SOURCE:反感 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:苦痛 , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:苦痛 , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:苦痛 , 0.05
ENDIF

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
SELECTCASE ABL:性交中毒
	CASE IS <= 1
		TIMES SOURCE:中毒 , 0.00
	CASE 2
		TIMES SOURCE:中毒 , 0.30
	CASE 3
		TIMES SOURCE:中毒 , 0.60
	CASE 4
		TIMES SOURCE:中毒 , 0.90
	CASE 5
		TIMES SOURCE:中毒 , 1.20
	CASE 6
		TIMES SOURCE:中毒 , 1.50
	CASE 7
		TIMES SOURCE:中毒 , 1.80
	CASE 8
		TIMES SOURCE:中毒 , 2.30
	CASE 9
		TIMES SOURCE:中毒 , 3.00
	CASE IS >= 10
		TIMES SOURCE:中毒 , 5.00
ENDSELECT

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK, 1
CALL SAMEN_CHECK_T, 4

CALL SAMEN_SHOOT, 1, NOWEX:PLAYER:射精
CALL SAMEN_SHOOT_T, 4, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者のペニスの汚れが移動
STAIN:膣内 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験++
PRINTL Ｖ経験+1

;性交経験
EXP:性交経験++
PRINTL 性交経験+1

CALL EXPUP, 3, 2

;愛情経験
IF TALENT:恋慕 && PLAYER == MASTER
	PRINTL 愛情経験+3
	EXP:愛情経験 += 3
ELSEIF TALENT:親愛 && PLAYER == MASTER
	PRINTL 愛情経験+6
	EXP:愛情経験 += 6
ENDIF

;獣人、獣による催淫
SELECTCASE CSTR:種族
	CASE "獣人", "獣"
		CFLAG:PLAYER:媚薬++
		SIF CFLAG:PLAYER:媚薬%5 == 0
			CFLAG:PLAYER:残数++
ENDSELECT

SELECTCASE CSTR:PLAYER:種族
	CASE "獣人", "獣"
		CFLAG:媚薬++
		SIF CFLAG:媚薬%5 == 0
			CFLAG:残数++
ENDSELECT

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && PLAYER == MASTER
	TFLAG:依存度補正 += 1
ELSEIF TALENT:親愛 && PLAYER == MASTER
	TFLAG:依存度補正 += 2
ENDIF

RETURN 1

