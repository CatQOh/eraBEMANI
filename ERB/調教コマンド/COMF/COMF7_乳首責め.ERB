;-------------------------------------------------
;乳首責め
;愛撫系コマンド
;-------------------------------------------------
@USERCOM_ABLE7
;乳首責め実行判定
;ニプルキャップ使用中は不可
SIF TEQUIP:ニプルキャップ
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:搾乳器
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:乳房電極
	RETURN 0
RETURN 1

@COM7
PRINTL 乳首責め
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 40

SOURCE:快B = 120
SOURCE:情愛 = 30
SOURCE:苦痛 = 10
SOURCE:露出 = 10
SOURCE:不潔 = 10
SOURCE:反感 = 10

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;絶頂時のウェイト
;-------------------------------------------------
;SIF NOWEX:Ｃ絶頂 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂 || NOWEX:Ｂ絶頂 || NOWEX:射精 || NOWEX:放尿 || NOWEX:噴乳 || NOWEX:MASTER:射精 || (ASSI > 0 && NOWEX:ASSI:射精)
;	WAIT

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸⇔調教者の手の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP, 1, 1

RETURN 1

