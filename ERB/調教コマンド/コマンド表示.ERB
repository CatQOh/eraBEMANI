@SHOW_USERCOM
#DIM DYNAMIC LCOUNT

LOCAL = 0

VARSET LOCALS, 

FOR LCOUNT, 0, 800
	;コマンドが無ければCONTINUE
	SIF TRAINNAME:LCOUNT == ""
		CONTINUE
	;フィルタでの表示/非表示はここで一元管理する
	SELECTCASE TRAINNAME:LCOUNT
		;Ｖ系コマンドフィルタの対象
		{
		CASE "指挿入れ", "バイブ", "正常位", "後背位", "騎乗位", "対面座位", "背面座位", "下着ずらし挿入", "フィストファック",
			 "両穴フィスト", "異物挿入", "触手挿入", "膣を犯す（ドラゴン）", "ペットに犯させる",
		}
			SIF FLAG:Ｖ系コマンドフィルタ
				CONTINUE
		;Ａ系コマンドフィルタの対象
		{
		CASE "アナル舐め", "アナル愛撫", "アナルバイブ", "アナルビーズ", "浣腸器", "空気浣腸", "拡張バルーン", "アナル電極",
			 "ローション注入", "正常位アナル", "後背位アナル", "騎乗位アナル", "対面座位アナル", "背面座位アナル", "下着ずらし挿入アナル",
			 "アナルフィスト", "両穴フィスト", "異物挿入", "アナル触手", "触手浣腸", "アナルを犯す（ドラゴン）", "ペットに犯させる",
			 "腸洗浄",
		}
			 SIF FLAG:Ａ系コマンドフィルタ
			 	CONTINUE
		;暴力コマンドフィルタの対象
		{
		CASE "スパンキング", "乳スパンキング", "鞭", "蝋燭", "針", "アイマスク", "縄", "ボールギャグ", "叩いてもらう",
			 "踏んでもらう", "鞭打ちしてもらう", "腹パン", "踏む", "裸締め", "首を絞める", "ビンタ", "フィストファック",
			 "アナルフィスト", "両穴フィスト", "異物挿入", "フィストファックさせる", "アナルフィストさせる",
		}
			SIF FLAG:暴力コマンドフィルタ
				CONTINUE
	ENDSELECT
	;CALLF系命令では返り値が無いのでCOMRESULTに結果を入れている
	COMRESULT = 0
	CALLFORMF USERCOM_ABLE_%TRAINNAME:LCOUNT%()
	;口上側でのコマンド制御
	IF FLAG:口上表示 && !CFLAG:口上非表示
		TRYCCALLFORM EXIST_KOJO_K{NO:TARGET}
			TRYCALLFORM DISABLE_%TRAINNAME:LCOUNT%_K{NO:TARGET}
		CATCH
			TRYCCALLFORM EXIST_MASTER_K{NO:MASTER}
				TRYCALLFORM DISABLE_MASTER_%TRAINNAME:LCOUNT%_K{NO:MASTER}
			CATCH
			ENDCATCH
		ENDCATCH
	ENDIF
	;Girls vs Tentacles in eraBEMANI挑戦中は触手と一部コマンドのみ可能
	IF 受験段位名 == "Girls vs Tentacles in eraBEMANI"
		SELECTCASE TRAINNAME:LCOUNT
			{
			CASE "愛撫", "クンニ", "指挿入れ", "アナル舐め", "アナル愛撫", "胸揉み", "乳首責め", "胸愛撫", "自慰見せつけ",
				 "キスする", "何もしない", "情欲を煽る", "媚薬", "利尿剤",
				 "触手召喚", "触手挿入", "アナル触手", "触手クリ責め", "触手乳首責め", "触手搾乳","触手緊縛",
				 "触手浣腸", "触手口辱", "触手搾精", "触手射精止め", "触手強制搾精", "触手強制放尿",
			}
			CASEELSE
				COMRESULT = 0
		ENDSELECT
	ENDIF

	IF COMRESULT
		;実行判定の表示をSKIPDISPで飛ばして結果だけ出す
		SKIPDISP 1
		TRYCCALLFORM 実行判定_%TRAINNAME:LCOUNT%
			;実行不可なら灰色表示、可能ならデフォ色表示
			IF RESULT == 0 && TFLAG:実行判定無視 == 0
				SETCOLORBYNAME gray
			ELSE
				SETCOLORBYNAME silver
			ENDIF
		CATCH
			;そもそも実行判定が不要ならデフォ色表示
			SETCOLORBYNAME silver
		ENDCATCH
		SKIPDISP 0

		;TEQUIP系コマンドは使用時と解除時でコマンド名を変える
		SELECTCASE TRAINNAME:LCOUNT
			CASE "触手召喚"
				LOCALS '= @"%\@ TEQUIP:触手 ? 触手送還 # 触手召喚 \@, 23%"
			CASE "ゲームセンター"
				IF TEQUIP:ゲームセンター
					LOCALS = %"ゲームセンターを後にする", 23%
				ELSE
					LOCALS = %"ゲームセンターに行く", 23%
				ENDIF
			CASE "トイレに行く"
				IF TEQUIP:トイレ
					LOCALS = %"トイレを出る", 23%
				ELSE
					LOCALS = %"トイレに行く", 23%
				ENDIF
			CASEELSE
				LOCALS = %TRAINNAME:LCOUNT%
		ENDSELECT

		SELECTCASE TRAINNAME:LCOUNT
			CASE "クリキャップ", "オナホール", "ニプルキャップ", "搾乳器", "搾精コンドーム", "アイマスク", "アナル電極", "乳房電極"
				LOCALS += @"\@ TEQUIP:(TRAINNAME:LCOUNT) ? (解除) # (装着) \@"
			CASE "バイブ", "アナルバイブ", "アナルビーズ"
				LOCALS += @"\@ TEQUIP:(TRAINNAME:LCOUNT) ? (解除) # (挿入) \@"
			CASE "ペニスバンド"
				LOCALS += @"\@ FINDCHARA(TEQUIP:ペニスバンド, 1) >= 0 ? (解除) # (装着) \@"
			CASE "触手クリ責め"
				LOCALS += @"\@ TEQUIP:クリキャップ ? (解除) # (実行) \@"
			CASE "触手ペニス責め"
				LOCALS += @"\@ TEQUIP:オナホール ? (解除) # (実行) \@"
			CASE "触手挿入"
				LOCALS += @"\@ TEQUIP:バイブ ? (解除) # (実行) \@"
			CASE "アナル触手"
				LOCALS += @"\@ TEQUIP:アナルバイブ ? (解除) # (実行) \@"
			CASE "触手乳首責め"
				LOCALS += @"\@ TEQUIP:ニプルキャップ ? (解除) # (実行) \@"
			CASE "触手搾精"
				LOCALS += @"\@ TEQUIP:搾精コンドーム ? (解除) # (実行) \@"
			CASE "縄", "触手緊縛"
				LOCALS += @"\@ TEQUIP:縄 ? (解除) # (実行) \@"
			CASE "ボールギャグ"
				LOCALS += @"\@ TEQUIP:ボールギャグ ? (解除) # (実行) \@"
			CASE "浣腸器"
				LOCALS += @"\@ TEQUIP:浣腸器 ? (解除) # (使用) \@"
			CASE "拡張バルーン"
				LOCALS += @"\@ TEQUIP:拡張バルーン ? (解除) # (使用) \@"
			CASE "触手口辱"
				LOCALS += @"\@ TEQUIP:触手口辱 ? (解除) # (実行) \@"
			CASE "ビデオカメラ", "野外プレイ", "羞恥プレイ"
				LOCALS += @"\@ TEQUIP:(TRAINNAME:LCOUNT) ? (終了) # (開始) \@"
			CASE "お風呂場プレイ"
				LOCALS += @"\@ TEQUIP:お風呂場 ? (終了) # (開始) \@"
			CASE "シャワー"
				LOCALS += @"\@ TEQUIP:シャワー ? (停止) # (使用) \@"
			CASE "新妻プレイ"
				LOCALS += @"\@ TEQUIP:エプロン ? (終了) # (開始) \@"
			CASE "触手射精止め"
				LOCALS += @"\@ TEQUIP:射精止め ? (解放) # (実行) \@"
			CASE "料理を作る"
				IF FLAG:パセリ表示
					LOCALS += "(1000パセリ)"
				ELSEIF 通貨単位 != ""
					LOCALS += @"(1000%通貨単位%)"
				ELSE
					LOCALS += "(1000$)"
				ENDIF
			CASE "お菓子を作る"
				IF FLAG:パセリ表示
					LOCALS += "(200パセリ)"
				ELSEIF 通貨単位 != ""
					LOCALS += @"(200%通貨単位%)"
				ELSE
					LOCALS += "(200$)"
				ENDIF
		ENDSELECT

		LOCALS:(LOCAL%4+1) = %SET_TOOLTIP(@"%LOCALS, 23%[{LCOUNT, 3}]", TRAIN_COMMENT(TRAINNAME:LCOUNT))%

		LOCAL++
		IF LOCAL%4 == 0
			HTML_PRINT LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4
			VARSET LOCALS, 
			LOCAL = 0
		ENDIF
	ENDIF
NEXT

SIF LOCAL
	HTML_PRINT LOCALS:1+LOCALS:2+LOCALS:3+LOCALS:4

SIF !LINEISEMPTY()
	PRINTL 

RESETCOLOR

DRAWLINE
PRINTC 履歴表示[800]
PRINTC 汚れ表示[801]
PRINTC 能力表示[802]

;新妻プレイ、お風呂場プレイ時、触手召喚時は除く
SIF !TEQUIP:エプロン && !TEQUIP:お風呂場 && !TEQUIP:触手
	PRINTC 着衣設定[803]
;お風呂場プレイ時は除く
IF !TEQUIP:お風呂場
	IF FLAG:ゲームモード == 7
		PRINTFORMC 着衣設定(%CALLNAME:MASTER%)[804]
	ELSE
		PRINTC 着衣設定(調教者)[804]
	ENDIF
ENDIF

IF PENIS(PLAYER) || PENIS(TARGET)
	SIF ITEM:コンドーム > 0 || (ITEM:搾精コンドーム && PENIS(TARGET))
		PRINTC コンドーム設定[805]
ENDIF
IF ASSIPLAY
	PRINTC 能力表示(助手)[806]
ELSE
	IF FLAG:ゲームモード == 7
		PRINTFORMC 能力表示(%CALLNAME:MASTER%)[806]
	ELSE
		PRINTC 能力表示(主人)[806]
	ENDIF
ENDIF
IF ASSI > 0 && !TEQUIP:触手 && !TALENT:ASSI:アニマル
	IF ASSIPLAY
		PRINTC 主人と交代[900]
	ELSE
		PRINTC 助手と交代[900]
	ENDIF
ENDIF
PRINTC コマンドセット1の設定[901]
SIF SELECTCOM:1 >= 0
	PRINTC コマンドセット1の実行[902]
PRINTC コマンドセット2の設定[903]
SIF SELECTCOM:11 >= 0
	PRINTC コマンドセット2の実行[904]
PRINTC コマンドフィルタの設定[950]
IF 上級売春中
	PRINTC 売春終了[999]
ELSEIF FLAG:ゲームモード == 7
	PRINTC 情事終了[999]
ELSE
	PRINTC 調教終了[999]
ENDIF
IF PREVCOMS != ""
	PRINTL 
	PRINTFORM ＜前回調教コマンド:%PREVCOMS%
	SIF TFLAG:前コマンド実行者 == ASSI
		PRINT (助手)
	PRINTL ＞
ENDIF

;DOWNBASEの初期化はあくまでここ Emueraのフローに則って
VARSET DOWNBASE:MASTER:0, 0
SIF ASSI >= 0
	VARSET DOWNBASE:ASSI:0

@USERCOM
SELECTCASE RESULT
	CASE 0 TO 799
		LOCAL = RESULT
		VARSET NOWEX, 0
		VARSET NOWEX:MASTER:0, 0
		SIF ASSI >= 0
			VARSET NOWEX:ASSI:0, 0
		TRYCALL EVENTUSERCOM
		IF TRAINNAME:LOCAL == ""
			CLEARLINE 1
			REUSELASTLINE 存在しないコマンドです
			RETURN 1
		ENDIF
		COMRESULT = 0
		CALLFORMF USERCOM_ABLE_%TRAINNAME:LOCAL%
		SELECTCASE COMRESULT
			CASE 0
				CLEARLINE 1
				REUSELASTLINE 実行できないコマンドです
				RETURN 1
			CASE 1
				;天使の技巧ボーナス
				;欲情と屈服のPALAMLVだけ技巧に加算する
				IF CSTR:種族 == "天使"
					LOCAL:10 = GETPALAMLV(PALAM:欲情, 10)
					LOCAL:11 = GETPALAMLV(PALAM:屈服, 10)
					LOCAL:12 = ABL:技巧
					ABL:技巧 = LOCAL:10*LOCAL:11+LOCAL:12
				ENDIF
				SELECTCOM = LOCAL
				SELECTCOMS = %TRAINNAME:SELECTCOM%
				TRYCALLFORM COM_%SELECTCOMS%
				IF RESULT == 0
					;終わったら技巧を元に戻す
					SIF CSTR:種族 == "天使"
						ABL:技巧 = LOCAL:12
					RETURN 1
				ELSE
					CALL SOURCE_CHECK
					VARSET SOURCE, 0
					TRYCALL EVENTUSERCOMEND
					WAIT
					;終わったら技巧を元に戻す
					SIF CSTR:種族 == "天使"
						ABL:技巧 = LOCAL:12
				ENDIF
		ENDSELECT
	;履歴表示
	CASE 800
		CALL SHOW_LOG
		RETURN 1
	;汚れ表示
	CASE 801
		CALL STAIN_INFO
		RETURN 1
	;能力表示
	CASE 802
		CALL SHOW_INFO, TARGET
		WAIT
		RETURN 1
	;着衣設定
	CASE 803
		IF !TEQUIP:エプロン && !TEQUIP:お風呂場 && !(TEQUIP:ゲームセンター && !TEQUIP:トイレ)
			CALL CLOTHES_CHANGE
			RETURN 1
		ENDIF
	;着衣設定(PLAYER)
	CASE 804
		IF !TEQUIP:お風呂場 && !(TEQUIP:ゲームセンター && !TEQUIP:トイレ)
			CALL CLOTHES_CHANGE_PLAYER
			RETURN 1
		ENDIF
	;避妊具の設定
	CASE 805
		IF PENIS(TARGET) || PENIS(PLAYER)
			CALL CONDOM_SETTING
			RETURN 1
		ENDIF
	;PLAYERの能力表示
	CASE 806
		CALL SHOW_INFO, PLAYER
		WAIT
		RETURN 1
	;助手と交代
	CASE 900
		IF ASSI > 0 && !TEQUIP:触手
			IF ASSIPLAY
				ASSIPLAY = 0
				PLAYER = MASTER
			ELSE
				ASSIPLAY = 1
				PLAYER = ASSI
				;実績:淫乱な天土に緋浮美を襲わせる
				IF NO:TARGET != 0 && NO:PLAYER != 0
					SIF CHARANAMEF(TARGET) == "梅桐 緋浮美" && CHARANAMEF(PLAYER) == "梅桐 天土" && FALLTYPE(PLAYER) == 2
						CALL 実績解除, "「ヒーちゃん見っけ♥」"
				ENDIF
			ENDIF
			CALL KOJO_MESSAGE_USERCOM, 900
			RETURN 1
		ENDIF
	;コマンドセットは引数増やすだけで簡単に拡張可能
	CASE 901
		CALL COMMENU_SETTING, 0
		RETURN 1
	CASE 902
		IF SELECTCOM:1 >= 0
			CALL COMMENU_START, 0
			RETURN 1
		ENDIF
	CASE 903
		CALL COMMENU_SETTING, 1
		RETURN 1
	CASE 904
		IF SELECTCOM:11 >= 0
			CALL COMMENU_START, 1
			RETURN 1
		ENDIF
	;コマンドフィルタ設定
	CASE 950
		$コマンドフィルタ
		PRINTFORML [1] Ｖ系コマンドを表示\@ FLAG:Ｖ系コマンドフィルタ ? する # しない \@
		PRINTFORML [2] Ａ系コマンドを表示\@ FLAG:Ａ系コマンドフィルタ ? する # しない \@
		PRINTFORML [3] 暴力系コマンドを表示\@ FLAG:暴力コマンドフィルタ ? する # しない \@
		PRINTL [0] 戻る
		DO
			INPUT
			SELECTCASE RESULT
				CASE 0
					RETURN 1
				CASE 1
					INVERTBIT FLAG:Ｖ系コマンドフィルタ, 0
				CASE 2
					INVERTBIT FLAG:Ａ系コマンドフィルタ, 0
				CASE 3
					INVERTBIT FLAG:暴力コマンドフィルタ, 0
			ENDSELECT
			GOTO コマンドフィルタ
		LOOP 1
	;調教終了
	CASE 999
		IF 上級売春中
			IF 上級売春中 == 2
				上級売春中 = 4
			ELSE
				上級売春中 = 3
			ENDIF
			RETURN 1
		ELSE
			BEGIN AFTERTRAIN
			RETURN 1
		ENDIF
ENDSELECT
RETURN 0

@SHOW_LOG
DRAWLINE
PRINTL □ 珠
CALL SHOW_INFO_JUEL, TARGET
PRINTL 

IF PREVCOMS != ""
	PRINTL □ 前回調教コマンド
	PRINTFORM 　%PREVCOMS%
	SIF TFLAG:前コマンド実行者 == ASSI
		PRINT (助手)
	PRINTL 
ENDIF
PRINTL □ 状態
CALL SHOW_STATE

PRINTL □ 調教道具
CALL SHOW_EQUIP
PRINTL 

PRINTL □ 服装
IF !TEQUIP:特殊 && !TEQUIP:上半身上着 && !TEQUIP:下半身上着 && TEQUIP:全身上着 && !TEQUIP:上半身下着 && !TEQUIP:下半身下着 && !TEQUIP:足元
	PRINTL 　全裸
ELSE
	IF CSTR:特殊 != "" && CFLAG:特殊 != 100
		PRINT 　特殊　　　:
		IF !TEQUIP:特殊
			PRINTFORML 脱
		ELSE
			PRINTFORML %CSTR:特殊%
		ENDIF
	ENDIF

	IF CSTR:上半身上着 != "" && CFLAG:上半身上着 != 100
		PRINT 　上半身上着:
		IF !TEQUIP:上半身上着
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:上半身上着%
		ENDIF
	ENDIF

	IF CSTR:下半身上着 != "" && CFLAG:下半身上着 != 100
		PRINT 　下半身上着:
		IF !TEQUIP:下半身上着
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:下半身上着%
		ENDIF
	ENDIF

	IF CSTR:全身上着 != "" && CFLAG:全身上着 != 100
		PRINT 　全身上着　:
		IF !TEQUIP:全身上着
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:全身上着%
		ENDIF
	ENDIF

	IF CSTR:上半身下着 != "" && CFLAG:上半身下着 != 100
		PRINT 　上半身下着:
		IF !TEQUIP:上半身下着
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:上半身下着%
		ENDIF
	ENDIF

	IF CSTR:下半身下着 != "" && CFLAG:下半身下着 != 100
		PRINT 　下半身下着:
		IF !TEQUIP:下半身下着
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:下半身下着%
		ENDIF
	ENDIF

	IF CSTR:足元 != "" && CFLAG:足元 != 100
		PRINT 　足元　　　:
		IF !TEQUIP:足元
			PRINTL 脱
		ELSE
			PRINTFORML %CSTR:足元%
		ENDIF
	ENDIF
ENDIF
PRINTL 

PRINTL □絶頂回数
IF PENIS(TARGET)
	PRINTFORML 　潮噴き:{EX:潮噴き}回
ELSE
	PRINTFORML 　Ｃ絶頂:{EX:Ｃ絶頂}回
ENDIF
SIF VAGINA(TARGET)
	PRINTFORML 　Ｖ絶頂:{EX:Ｖ絶頂}回
PRINTFORML 　Ａ絶頂:{EX:Ａ絶頂}回
PRINTFORML 　Ｂ絶頂:{EX:Ｂ絶頂}回
SIF TALENT:母乳体質
	PRINTFORML 　噴乳:{EX:噴乳}回
SIF PENIS(TARGET)
	PRINTFORML 　射精:{EX:射精}回
PRINTFORML 　放尿:{EX:放尿}回
PRINTL 
SIF PENIS(MASTER)
	PRINTFORML 　%CALLNAME:MASTER%の射精:{EX:MASTER:射精}回
SIF ASSI > 0 && PENIS(ASSI)
	PRINTFORML 　%CALLNAME:ASSI%の射精:{EX:ASSI:射精}回
SIF TALENT:MASTER:ドラゴン使い
	PRINTFORML 　%CSTR:MASTER:ドラゴン%の射精:{EX:MASTER:ドラゴン射精}回
SIF ASSI > 0 && TALENT:ASSI:ドラゴン使い
	PRINTFORML 　%CSTR:ASSI:ドラゴン%の射精:{EX:ASSI:ドラゴン射精}回

WAIT

;-------------------------------------------------
;調教中の着衣変更
;-------------------------------------------------
@CLOTHES_CHANGE
#DIM DYNAMIC LCOUNT
DRAWLINE
PRINTL 着衣について設定します

;現在来ている服を表示、PRINTLは改行用
PRINTFORML □%CALLNAME:TARGET%の着衣
REPEAT 7
	LOCAL = COUNT+100
	IF CSTR:LOCAL != ""
		PRINTFORM 　%CSTR:LOCAL%：
		IF TEQUIP:COUNT
			PRINTL 着
		ELSEIF !TEQUIP:COUNT
			PRINTL 脱
		ENDIF
	ENDIF
REND
DRAWLINE

;着脱メッセージ表示
REPEAT 7
	LOCAL = COUNT+100
	SIF CSTR:LOCAL == ""
		CONTINUE
	PRINTFORM [{COUNT, 2}] %CSTR:LOCAL%を
	IF TEQUIP:COUNT
		SELECTCASE COUNT
			CASE 0
				PRINTL 奪う
			CASEELSE
				PRINTL 脱がす
		ENDSELECT
	ELSEIF !TEQUIP:COUNT
		SELECTCASE COUNT
			CASE 0
				PRINTL 返す
			CASE 5, 6
				PRINTL 穿かせる
			CASEELSE
				PRINTL 着せる
		ENDSELECT
	ENDIF
REND

;何か着てたらすべて脱がすコマンド表示
REPEAT 7
	IF TEQUIP:COUNT
		PRINTL [10] すべて脱がす
		BREAK
	ENDIF
REND

;何か着てないものがあったらすべて着せるコマンド表示
REPEAT 7
	IF !TEQUIP:COUNT && CSTR:(COUNT+100) != ""
		PRINTL [11] すべて着せる
		BREAK
	ENDIF
REND
DRAWLINE
PRINTPLAIN 　COORDINATE　
PRINTL [100] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 6
			LOCAL = RESULT
			IF CSTR:(LOCAL+100) != ""
				IF TEQUIP:LOCAL
					;脱がせるテキストを呼ぶ
					CALL CLOTHES_MESSAGE_TAKE, RESULT
					TEQUIP:LOCAL = 0
				ELSEIF !TEQUIP:LOCAL
					;着せるテキストを呼ぶ
					CALL CLOTHES_MESSAGE_PUT, RESULT
					TEQUIP:LOCAL = 1
				ENDIF
				BREAK
			ENDIF
		;服を脱がせる場合
		CASE 10
			REPEAT 7
				TEQUIP:COUNT = 0
			REND
		;服を着せる場合
		CASE 11
			REPEAT 7
				SIF CSTR:(COUNT+100) != ""
					TEQUIP:COUNT = 1
			REND
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(10, 11, 100)

TFLAG:服着脱 = RESULT
CALL KOJO_MESSAGE_USERCOM, 803

RESTART

;-------------------------------------------------
;きせかえ時のメッセージ処理
;-------------------------------------------------
@CLOTHES_MESSAGE_TAKE, ARG
PRINTFORM %CSTR:(ARG+100)%を
SELECTCASE ARG
	CASE 0
		PRINTW 奪った
	CASEELSE
		PRINTW 脱がした
ENDSELECT

@CLOTHES_MESSAGE_PUT, ARG
PRINTFORM %CSTR:(ARG+100)%を
SELECTCASE ARG
	CASE 0
		PRINTW 返した
	CASE 5, 6
		PRINTW 穿かせた
	CASEELSE
		PRINTW 着せた
ENDSELECT

;-------------------------------------------------
;調教中の着衣変更
;-------------------------------------------------
@CLOTHES_CHANGE_PLAYER
#DIM DYNAMIC LCOUNT
DRAWLINE
PRINTL 着衣について設定します

;現在来ている服を表示、PRINTLは改行用
PRINTFORML □%CALLNAME:PLAYER%の着衣
REPEAT 7
	LOCAL = COUNT+100
	IF CSTR:PLAYER:LOCAL != ""
		PRINTFORM 　%CSTR:PLAYER:LOCAL%：
		IF TEQUIP:PLAYER:COUNT
			PRINTL 着
		ELSEIF !TEQUIP:PLAYER:COUNT
			PRINTL 脱
		ENDIF
	ENDIF
REND
DRAWLINE

;着脱メッセージ表示
REPEAT 7
	LOCAL = COUNT+100
	SIF CSTR:PLAYER:LOCAL == ""
		CONTINUE
	PRINTFORM [{COUNT, 2}] %CSTR:PLAYER:LOCAL%を
	IF TEQUIP:PLAYER:COUNT
		SELECTCASE COUNT
			CASE 0
				PRINTL 置く
			CASEELSE
				PRINTL 脱ぐ
		ENDSELECT
	ELSEIF !TEQUIP:PLAYER:COUNT
		SELECTCASE COUNT
			CASE 0
				PRINTL 使う
			CASE 5, 6
				PRINTL 穿く
			CASEELSE
				PRINTL 着る
		ENDSELECT
	ENDIF
REND

;何か着てたらすべて脱がすコマンド表示
REPEAT 7
	IF TEQUIP:PLAYER:COUNT
		PRINTL [10] すべて脱ぐ
		BREAK
	ENDIF
REND

;何か着てないものがあったらすべて着せるコマンド表示
REPEAT 7
	IF !TEQUIP:PLAYER:COUNT && CSTR:PLAYER:(COUNT+100) != ""
		PRINTL [11] すべて着る
		BREAK
	ENDIF
REND
DRAWLINE
PRINTPLAIN 　COORDINATE　
PRINTL [100] 戻る

DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 6
			LOCAL = RESULT
			IF CSTR:PLAYER:(LOCAL+100) != ""
				INVERTBIT TEQUIP:PLAYER:LOCAL, 0
				BREAK
			ENDIF
		;服を脱がせる場合
		CASE 10
			REPEAT 7
				TEQUIP:PLAYER:COUNT = 0
			REND
		;服を着せる場合
		CASE 11
			REPEAT 7
				SIF CSTR:PLAYER:(100+COUNT) != ""
					TEQUIP:PLAYER:COUNT = 1
			REND
		CASE 100
			RETURN 0
	ENDSELECT
LOOP LOOPRES(10, 11, 100)

TFLAG:服着脱 = RESULT
CALL KOJO_MESSAGE_USERCOM, 804

RESTART

;-------------------------------------------------
;着衣の状態によって、調教中のソースが若干変動する
;-------------------------------------------------
@EQUIP_CLOTHES
;着衣状態によるソースを付加（１箇所ごとに露出+10、未陥落時反感+10）
REPEAT 7
	;特殊部位と足は計算しない
	SIF COUNT == 0 || COUNT == 6
		CONTINUE
	;上着・下半身上着は全身上着をつけているとき計算しない
	SIF (COUNT == 1 || COUNT == 2) && TEQUIP:全身上着
		CONTINUE
	;全身上着は上着・下半身上着をつけているとき計算しない
	SIF COUNT == 3 && (TEQUIP:上半身上着 || TEQUIP:下半身上着)
		CONTINUE
	;男は上半身下着に関する計算はしない
	SIF COUNT == 4 && SEX(TARGET) == 1
		CONTINUE
	IF !TEQUIP:COUNT
		SOURCE:露出 += 10
		;陥落時は反感を加算しない
		SIF CFLAG:陥落
			CONTINUE
		SOURCE:反感 += 10
	ENDIF
REND

RETURN 1

@CONDOM_SETTING
PRINTL コンドームの設定を行う対象を選択してください
PRINTL  
IF PENIS(MASTER)
	PRINTFORM [0] - %CALLNAME:MASTER%
	IF TEQUIP:MASTER:コンドーム && GETBIT(FLAG:避妊具設定, 0)
		PRINTL (装着済み、常備)
	ELSEIF TEQUIP:MASTER:コンドーム
		PRINTL (装着済み)
	ELSE
		PRINTL (未装着)
	ENDIF
ENDIF

IF ASSI >= 0
	IF PENIS(ASSI)
		PRINTFORM [1] - %CALLNAME:ASSI%
		IF TEQUIP:ASSI:コンドーム && GETBIT(FLAG:避妊具設定, 1)
			PRINTL (装着済み、常備)
		ELSEIF TEQUIP:ASSI:コンドーム
			PRINTL (装着済み)
		ELSE
			PRINTL (未装着)
		ENDIF
	ENDIF
ENDIF

IF PENIS(TARGET)
	PRINTFORM [2] - %CALLNAME:TARGET%
	IF TEQUIP:コンドーム && GETBIT(FLAG:避妊具設定, 2)
		PRINTL (装着済み、常備)
	ELSEIF TEQUIP:コンドーム
		PRINTL (装着済み)
	ELSEIF TEQUIP:搾精コンドーム && GETBIT(FLAG:避妊具設定, 3)
		PRINTL (搾精コンドーム装着済み、常備)
	ELSEIF TEQUIP:搾精コンドーム
		PRINTL (搾精コンドーム装着済み)
	ELSE
		PRINTL (未装着)
	ENDIF
ENDIF

PRINTL [3] - 戻る
DO
	INPUT
	SELECTCASE RESULT
		CASE 0
			PRINTL 避妊具の設定をしてください
			PRINTL  
			IF GETBIT(FLAG:避妊具設定, 0)
				PRINTL [0] 常に避妊しない
			ELSE
				PRINTL [0] 常に避妊する
			ENDIF
			PRINTL [1] 戻る
			DO
				INPUT
				SIF RESULT == 0
					INVERTBIT FLAG:避妊具設定, 0
			LOOP LOOPRES(0, 1)
			RESTART
		CASE 1
			PRINTL 避妊具の設定をしてください
			PRINTL  
			IF GETBIT(FLAG:避妊具設定, 1)
				PRINTL [0] 常に避妊しない
			ELSE
				PRINTL [0] 常に避妊する
			ENDIF
			PRINTL [1] 戻る
			DO
				INPUT
				SIF RESULT == 0
					INVERTBIT FLAG:避妊具設定, 1
			LOOP LOOPRES(0, 1)
			RESTART
		CASE 2
			PRINTL 避妊具の設定をしてください
			PRINTL  
			IF GETBIT(FLAG:避妊具設定, 2) || GETBIT(FLAG:避妊具設定, 3)
				PRINTL [0] 常に何も付けない
			ELSE 
				PRINTL [0] 常に避妊する
				PRINTL [1] 常に搾精コンドーム付ける
			ENDIF
			PRINTL [2] 戻る
			DO
				INPUT
				SELECTCASE RESULT
					CASE 0
						IF GETBIT(FLAG:避妊具設定, 2) || GETBIT(FLAG:避妊具設定, 3)
							CLEARBIT FLAG:避妊具設定, 2, 3
						ELSE
							SETBIT FLAG:避妊具設定, 2
						ENDIF
					CASE 1
						SIF !GETBIT(FLAG:避妊具設定, 2) && !GETBIT(FLAG:避妊具設定, 3)
							SETBIT FLAG:避妊具設定, 3
				ENDSELECT
			LOOP LOOPRES(0, 1, 2)
			RESTART
		CASE 3
			RETURN 0
	ENDSELECT
LOOP 1

RETURN 0

@COMMENU_SETTING, ARG
#DIM DYNAMIC LCOUNT
DRAWLINE
LOCAL = 0
FOR COUNT, ARG*10+1, ARG*10+11
	SIF SELECTCOM:COUNT >= 0
		LOCAL++
NEXT

IF LOCAL < 10
	FOR LCOUNT, 0, 799
		COMRESULT = 0
		SIF TRAINNAME:LCOUNT == ""
			CONTINUE
		CALLFORMF USERCOM_ABLE_%TRAINNAME:LCOUNT%
		;Girls vs Tentacles in eraBEMANI挑戦中は触手と一部コマンドのみ可能
		IF 受験段位名 == "Girls vs Tentacles in eraBEMANI"
			SELECTCASE TRAINNAME:LCOUNT
				{
				CASE "愛撫", "クンニ", "指挿入れ", "アナル舐め", "アナル愛撫", "胸揉み", "乳首責め", "胸愛撫", "自慰見せつけ",
					 "キスする", "何もしない", "情欲を煽る", "媚薬", "利尿剤",
					 "触手召喚", "触手挿入", "アナル触手", "触手クリ責め", "触手乳首責め", "触手搾乳","触手緊縛",
					 "触手浣腸", "触手口辱", "触手搾精", "触手射精止め", "触手強制搾精", "触手強制放尿",
				}
				CASEELSE
					COMRESULT = 0
			ENDSELECT
		ENDIF
		SIF COMRESULT == 1
			PRINTFORMC %TRAINNAME:(LCOUNT)%[{LCOUNT, 3, RIGHT}]
	NEXT
ELSE
	PRINTL これ以上追加できません
ENDIF

SIF !LINEISEMPTY()
	PRINTL 

IF SELECTCOM:(ARG*10+1) >= 0
	DRAWLINE
	PRINTL 設定されている連続実行は以下の通りです
	FOR COUNT, ARG*10+1, ARG*10+11
		IF SELECTCOM:COUNT >= 0
			SIF COUNT%10 != 1
				PRINTL ↓
			PRINTFORM 　{COUNT-ARG*10}.%TRAINNAME:(SELECTCOM:COUNT)%
		ELSE
			BREAK
		ENDIF
	NEXT
	PRINTL 
ENDIF
DRAWLINE
PRINTPLAIN 　COM MENU　
PRINT [998] 繰り返し 
PRINT [999] 初期化 
PRINTL [1000] 戻る

INPUT

SELECTCASE RESULT
	CASE 0 TO 799
		SIF LOCAL >= 10
			RESTART
		SIF TRAINNAME:RESULT == ""
			RESTART
		COMRESULT = 0
		LOCAL:1 = RESULT
		CALLFORMF USERCOM_ABLE_%TRAINNAME:RESULT%
		SIF COMRESULT == 0
			RESTART
		SELECTCOM:(LOCAL+(ARG*10+1)) = LOCAL:1
		RESTART
	CASE 998
		SIF !LOCAL
			RETURN 0
		FOR COUNT, ARG*10+1, ARG*10+11
			IF COUNT > LOCAL+ARG*10
				IF (COUNT-ARG*10)%LOCAL == 0
					SELECTCOM:COUNT = SELECTCOM:(LOCAL+ARG*10)
				ELSE
					SELECTCOM:COUNT = SELECTCOM:((COUNT-ARG*10)%LOCAL+ARG*10)
				ENDIF
			ENDIF
		NEXT
	CASE 999
		FOR COUNT, ARG*10+1, ARG*10+11
			SELECTCOM:COUNT = -1
		NEXT
	CASE 1000
		RETURN 0
ENDSELECT

RESTART

@COMMENU_START, ARG
#DIM DYNAMIC COM_GO
#DIM DYNAMIC LCOUNT
LOCAL = 0
FOR LCOUNT, ARG*10+1, ARG*10+11
	SIF SELECTCOM:LCOUNT >= 0
		LOCAL++
NEXT

FOR LCOUNT, ARG*10+1, (LOCAL+ARG*10+1)
	COMRESULT = 0
	CALLFORMF USERCOM_ABLE_%TRAINNAME:(SELECTCOM:LCOUNT)%
	;Girls vs Tentacles in eraBEMANI挑戦中は触手と一部コマンドのみ可能
	IF 受験段位名 == "Girls vs Tentacles in eraBEMANI"
		SELECTCASE TRAINNAME:LCOUNT
			{
			CASE "愛撫", "クンニ", "指挿入れ", "アナル舐め", "アナル愛撫", "胸揉み", "乳首責め", "胸愛撫", "自慰見せつけ",
				 "キスする", "何もしない", "情欲を煽る", "媚薬", "利尿剤",
				 "触手召喚", "触手挿入", "アナル触手", "触手クリ責め", "触手乳首責め", "触手搾乳","触手緊縛",
				 "触手浣腸", "触手口辱", "触手搾精", "触手射精止め", "触手強制搾精", "触手強制放尿",
			}
			CASEELSE
				COMRESULT = 0
		ENDSELECT
	ENDIF
	IF COMRESULT == 0
		PRINTFORMW {LCOUNT}.%TRAINNAME:(SELECTCOM:LCOUNT)%が実行できないため、連続実行を開始できません
		BREAK
	ELSEIF COMRESULT == 1
		SIF LCOUNT == LOCAL+ARG*10
			COM_GO = 1
	ENDIF
NEXT


IF COM_GO == 1
	FOR LCOUNT, ARG*10+1, (LOCAL+ARG*10+1)
		DRAWLINE
		COMRESULT = 0
		CALLFORMF USERCOM_ABLE_%TRAINNAME:(SELECTCOM:LCOUNT)%
		IF COMRESULT == 0
			PRINTFORMW {LCOUNT}.%TRAINNAME:(SELECTCOM:LCOUNT)%が実行できないため、連続実行を中止しました
			BREAK
		ENDIF
		PRINTFORML コマンド連続実行({LCOUNT-ARG*10}/{LOCAL})
		PRINTFORML □%CALLNAME:TARGET%のステータス
		SIF !上級売春中
			CALL SHOW_INFO_HEALTH
		CALL SHOW_INFO_PALAM
		IF PENIS(TARGET)
			PRINT 　射精　　　
			SIF CFLAG:残数 == 0
				PRINT ※打ち止め※
			BAR BASE:射精, MAXBASE:射精, 32
			PRINTFORML ({BASE:射精}/{MAXBASE:射精})
		ENDIF
		SIF PENIS(MASTER) || PENIS(ASSI) || TEQUIP:触手
			PRINTL □主人と助手のステータス
		IF 上級売春中
			CALL SHOW_INFO_HEALTH, MASTER
		ENDIF
		IF PENIS(MASTER)
			PRINT 　射精(主人)
			SIF CFLAG:MASTER:残数 == 0
				PRINT ※打ち止め※
			BAR BASE:MASTER:射精, MAXBASE:MASTER:射精, 32
			PRINTFORML ({BASE:MASTER:射精}/{MAXBASE:MASTER:射精})
		ENDIF
		IF ASSI >= 0 && PENIS(ASSI)
			PRINT 　射精(助手)
			SIF CFLAG:ASSI:残数 == 0
				PRINT ※打ち止め※
			BAR BASE:ASSI:射精, MAXBASE:ASSI:射精, 32
			PRINTFORML ({BASE:ASSI:射精}/{MAXBASE:ASSI:射精})
		ENDIF
		IF TEQUIP:触手
			PRINT 　射精(触手)
			BAR BASE:MASTER:触手射精, MAXBASE:MASTER:触手射精, 32
			PRINTFORML ({BASE:MASTER:触手射精}/{MAXBASE:MASTER:触手射精})
		ENDIF
		IF TALENT:MASTER:ドラゴン使い
			PRINTFORML □%CSTR:MASTER:ドラゴン%のステータス
			PRINT 　射精　　　
			BAR BASE:MASTER:ドラゴン射精, MAXBASE:MASTER:ドラゴン射精, 32
			PRINTFORML ({BASE:MASTER:ドラゴン射精}/{MAXBASE:MASTER:ドラゴン射精})
		ENDIF
		IF ASSI > 0 && TALENT:ASSI:ドラゴン使い
			PRINTFORML □%CSTR:ASSI:ドラゴン%のステータス
			PRINT 　射精　　　
			BAR BASE:ASSI:ドラゴン射精, MAXBASE:ASSI:ドラゴン射精, 32
			PRINTFORML ({BASE:ASSI:ドラゴン射精}/{MAXBASE:ASSI:ドラゴン射精})
		ENDIF
		DRAWLINE
		VARSET NOWEX, 0
		VARSET NOWEX:MASTER:0, 0
		SIF ASSI >= 0
			VARSET NOWEX:ASSI:0, 0
		CALL EVENTUSERCOM
		SELECTCOM = SELECTCOM:LCOUNT
		SELECTCOMS = %TRAINNAME:SELECTCOM%
		TRYCALLFORM COM_%TRAINNAME:(SELECTCOM:LCOUNT)%
		IF RESULT == 0
			CONTINUE
		ELSE
			CALL SOURCE_CHECK
			VARSET SOURCE, 0
			TRYCALL EVENTUSERCOMEND
			VARSET LOSEBASE, 0
			VARSET DOWNBASE:MASTER:0, 0
			IF 上級売春中
				CALL 上級売春コマンド結果
				IF TFLAG:満足度 >= 1000 && 上級売春中 == 1
					DRAWLINE
					PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%との行為に十分満足したようだ
					PRINTFORML このまま帰ろうとする%CALLNAME:TARGET%を……
					PRINTL [0] - 愛想良く見送る
					PRINTL [1] - 行為を続けるべく引き止める
					INPUT
					DO
						SELECTCASE RESULT
							CASE 0
								上級売春中 = 4
								BREAK
							CASE 1
								上級売春中 = 2
						ENDSELECT
					LOOP LOOPRES(0, 1)
				ENDIF
				SIF BASE:MASTER:体力 < 500
					RETURN 0
			ELSE
				SIF BASE:体力 < 500 || TFLAG:流産
					RETURN 0
			ENDIF
			WAIT
			;RESULT = 0
			;PRINTL [9] - 連続実行を終了する
			;WAITANYKEY
			;SIF RESULT == 9
			;	BREAK
		ENDIF
	NEXT
ELSE
	RETURN 0
ENDIF

