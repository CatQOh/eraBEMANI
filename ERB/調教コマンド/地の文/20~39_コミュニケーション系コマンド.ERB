;-------------------------------------------------
;20,キスする
;-------------------------------------------------
@MESSAGE_COM20
SIF !CFLAG:陥落 && ABL:従順 < 3
	PRINT 抵抗する
PRINTFORM %CALLNAME:TARGET%と
SIF !CFLAG:陥落 && ABL:従順 < 3
	PRINT 半ば強引に
PRINT 唇を重ね
;従順LV3以上+同性愛経験500以上+調教者と同性
IF ABL:従順 >= 3 && (EXP:レズ経験 >= 500 || EXP:ゲイ経験 >= 500) && SEX(PLAYER) == SEX(TARGET)
	PRINTFORMW 、ねっとりと舌を絡めあいながら唾液を交換した……
;陥落済み
ELSEIF CFLAG:陥落 && PLAYER == MASTER
	PRINTFORMW ると、%CALLNAME:TARGET%も%CALLNAME:PLAYER%を求めて舌を絡めてきた……
;従順LV3以上
ELSEIF ABL:従順 >= 3
	PRINTFORMW ると、%CALLNAME:TARGET%も積極的に舌を絡めてきた……
ELSE
	PRINTW た……
ENDIF

;-------------------------------------------------
;21,何もしない
;-------------------------------------------------
@MESSAGE_COM21
PRINTFORML %CALLNAME:PLAYER%は何もせずに%CALLNAME:TARGET%を放置している
SIF TEQUIP:媚薬
	PRINTFORML %CALLNAME:TARGET%は媚薬でこみ上げる欲情に顔を赤くしながら見つめてくる……

;-------------------------------------------------
;22,会話
;-------------------------------------------------
@MESSAGE_COM22
PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と楽しげに会話をした……

;-------------------------------------------------
;23,プレゼント
;-------------------------------------------------
@MESSAGE_COM23
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に
SELECTCASE TFLAG:プレゼント
	;スマイルプリキュア
	CASE 0
		PRINTFORM 爽やかな笑顔
	;今時カードでパセリ買うやつ
	CASE 1
		PRINTFORM パセリカード
	;日用品(ご想像にお任せします)
	CASE 2
		PRINTFORM 軽くて身近な日用品
	;アクセサリ(指輪ではない)
	CASE 3
		PRINTFORM 綺麗なアクセサリ
	;服 数パターンあり 需要があれば口上側で参照できるようにする
	CASE 4
		SELECTCASE RAND:4
			CASE 0
				PRINTFORM 可愛らしい服
			CASE 1
				PRINTFORM 煌びやかな服
			CASE 2
				PRINTFORM 格好良い服
			CASE 3
				PRINTFORM 着心地の良い服
		ENDSELECT
	;指輪
	CASE 5
		PRINTFORM 名前の刻印入りのリング
	;IIDX筐体
	CASE 6
		PRINTFORM IIDX筐体
ENDSELECT
PRINTFORML をプレゼントした……
SELECTCASE TFLAG:プレゼント
	;指輪プレゼントした場合
	CASE 5
		;親愛なら愛情経験ボーナス
		IF TALENT:親愛
			PRINTFORML 愛情経験ボーナス+50
		;恋慕でもボーナス
		ELSEIF TALENT:恋慕
			PRINTFORML 愛情経験ボーナス+25
		ENDIF
	;IIDX筐体をプレセントした場合
	CASE 6
		PRINTFORML 突然のとんでもない振る舞いに%CALLNAME:TARGET%は気が動転している……
ENDSELECT
WAIT

;-------------------------------------------------
;24,スキンシップ
;-------------------------------------------------
@MESSAGE_COM24
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%とのスキンシップを図った
SELECTCASE RAND:3
	CASE 0
		PRINTFORMW %CALLNAME:TARGET%は何だか嬉しそうにしている……
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%は少しくすぐったそうにしているが、まんざらでもなさそうだ……
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%もじゃれ合わんとばかりに%CALLNAME:PLAYER%の体をまさぐっている……
ENDSELECT

;-------------------------------------------------
;25,まったりする
;まったりとはいったい
;-------------------------------------------------
@MESSAGE_COM25
PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と一緒にまったりしていた……

;-------------------------------------------------
;26,家事
;-------------------------------------------------
@MESSAGE_COM26
PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と一緒に家事を済ませることにした……

;-------------------------------------------------
;27,歌の練習
;主人は楽器も演奏できるんだよ！
;-------------------------------------------------
@MESSAGE_COM27
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に歌の練習をさせた
PRINTFORM %CALLNAME:PLAYER%の演奏に合わせて、%CALLNAME:TARGET%の
;歌唱技能
SELECTCASE ABL:歌唱技能
	;LV0
	CASE 0
		PRINTFORMW 聞くに堪えない歌声が響いていた……
	;LV1
	CASE 1
		PRINTFORMW 歌声が響いていた……
	;LV2
	CASE 2
		PRINTFORMW そこそこ聞き心地のよい歌声が響いていた……
	;LV3
	CASE 3
		PRINTFORMW かなり上手い歌声が響いていた……
	;LV4
	CASE 4
		PRINTFORMW 思わず聞き惚れる歌声が響いていた……
	;LV5以上
	CASE IS >= 5
		PRINTFORMW 心奪われる歌声が響き渡っていた……
ENDSELECT

;-------------------------------------------------
;28,罵倒
;-------------------------------------------------
@MESSAGE_COM28
;調教者が男
IF SEX(PLAYER) == 1
	PRINTFORML %CALLNAME:PLAYER%は思いつく限りの罵詈雑言を%CALLNAME:TARGET%に浴びせた
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を罵倒した
ENDIF
PRINTFORM %CALLNAME:TARGET%は
;ボールギャグ装着中
IF TEQUIP:ボールギャグ
	;マゾっ気
	SELECTCASE ABL:マゾっ気
		;LV0
		CASE 0
			PRINTW 何か言い返そうとしているが口枷によって言葉は封じられている
		;LV1,2
		CASE 1, 2
			PRINTW 一瞬何か言い返したいような仕草を見せるが自分の口が塞がっているのを思い出したのかおとなしくなった
		;LV3,4
		CASE 3, 4
			PRINTW 罵られることに快感を感じているようだ……
		;LV5以上
		CASE IS >= 5
			PRINTL 顔を紅潮させて鼻息を荒げている、どうやら罵られることにすっかり快感を感じているようだ
			;以下女性のみ NOT無機物
			SIF SEX(TARGET) == 2 && !TALENT:無機物
				PRINTFORML %CALLNAME:TARGET%は\@ OVER_GENITAL(TARGET) != "" ? 衣服を愛液で湿らせながら # 足元を下品に漏れだす体液で水たまりを作り \@、さらなる罵倒を期待するかの様に淫靡な視線で見上げている……
	ENDSELECT
ELSE
	;マゾっ気
	SELECTCASE ABL:マゾっ気
		;LV0
		CASE 0
			;反抗的orプライド高いor生意気
			IF TALENT:反抗的 || TALENT:プライド高い || TALENT:生意気
				PRINTFORMW ぎりぎりと歯を軋ませながら%CALLNAME:PLAYER%を睨みつけている
			;素直orプライド低いor大人しい(上が優先)
			ELSEIF TALENT:素直 || TALENT:プライド低い || TALENT:大人しい
				PRINTFORMW 目にうっすらと涙を浮かべながら%CALLNAME:PLAYER%の罵詈雑言を堪えている
			ELSE
				PRINTFORMW 怒りと屈辱に満ちた顔で%CALLNAME:PLAYER%を睨んだ
			ENDIF
		;LV1,2
		CASE 1, 2
			PRINTFORMW %CALLNAME:PLAYER%を睨んでいるもののその表情には僅かな快感がちらついている
		;LV3,4
		CASE 3, 4
			PRINTW 罵られることに快感を感じているようだ……
		;LV5以上
		CASE IS <= 5
			PRINTL 罵られることにすっかり快感を感じているようだ
			PRINTFORM %CALLNAME:TARGET%は顔を紅潮させて媚びるような眼差しで%CALLNAME:PLAYER%を見上げ、
			;気丈かプライド高いがある
			IF TALENT:気丈 || TALENT:プライド高い
				PRINTL 言葉責めをねだるその姿にはかつての面影は既に無かった……
			ELSE
				PRINTL 哀願のこもった声音でさらなる言葉責めをおねだりしている……
			ENDIF
	ENDSELECT
ENDIF
WAIT

;-------------------------------------------------
;29,ビデオ鑑賞
;-------------------------------------------------
@MESSAGE_COM29
PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%と一緒に%CALLNAME:TARGET%の痴態を写したビデオを見た
;露出癖LV5
IF ABL:露出癖 >= 5
	PRINTFORMW %CALLNAME:TARGET%は自らの痴態に興奮したのか、%CALLNAME:PLAYER%に淫靡な視線を送っていた……
ELSE
	PRINTFORMW %CALLNAME:TARGET%は画面の向こうの淫らな自身の姿を見て顔を赤くしている……
ENDIF

;-------------------------------------------------
;30,吸血する
;-------------------------------------------------
@MESSAGE_COM30
PRINTFORML %CALLNAME:TARGET%が緊張と期待に少し震えた様子で首筋をさらけ出すと、%CALLNAME:PLAYER%は歯を立てて血を吸い始めた
PRINTFORM 生き血を捧げることに%CALLNAME:TARGET%は
SIF TALENT:痛みに弱い
	PRINT 涙を浮かべながら
PRINTW 悲鳴のような喘ぎ声を上げている……

;-------------------------------------------------
;31,吸血させる
;-------------------------------------------------
@MESSAGE_COM31
PRINTFORML %CALLNAME:TARGET%に血を吸わせることにした
PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の首筋に唇を寄せ、一思いに齧り付くと血を啜り始めた……

;-------------------------------------------------
;32,料理を作る
;-------------------------------------------------
@MESSAGE_COM32
PRINTFORML %CALLNAME:TARGET%に料理を作らせた
SELECTCASE ABL:料理技能
	CASE 0
		SELECTCASE RAND:4
			CASE 0
				PRINTW ……キッチンのほうがなんだか焦げ臭い
			CASE 1
				PRINTW 食欲をそそらない微妙な匂いが漂ってくる……
			CASE 2
				PRINTW どうやら料理中に軽い怪我をしたようだ。顔をしかめている……
			CASE 3
				PRINTW 素材を判別できないほどグロテスクな料理が出来上がった……
		ENDSELECT
	CASE 1
		SELECTCASE RAND:3
			CASE 0
				PRINTW 盛り付けはいまいちだが無難な仕上がりだ
			CASE 1
				PRINTW 普通に美味しそうな料理ができたようだ
			CASE 2
				PRINTW 味見してみると何かが足りない気もするが食べられないことはない
		ENDSELECT
	CASE 2 TO 4
		SELECTCASE RAND:3
			CASE 0
				PRINTFORMW キッチンから漂ってくる食欲をそそる香りが%CALLNAME:MASTER%の空腹を促す……
			CASE 1
				PRINTFORMW %CALLNAME:TARGET%は美味しそうな料理を手際よく作っている
			CASE 2
				PRINTW 素材をふんだんに活かした美味しそうな料理が出来上がった
		ENDSELECT
	CASE IS >= 5
		SELECTCASE RAND:3
			CASE 0
				PRINTW 今までに見たことがないほど美味しそうな料理が出来上がった
			CASE 1
				PRINTW 元の食材からは想像できないような立派な料理が出来上がった
			CASE 2
				PRINTW プロ並みの豪華な料理があっという間に食卓に並んだ
		ENDSELECT
ENDSELECT

;-------------------------------------------------
;33,お菓子を作る
;-------------------------------------------------
@MESSAGE_COM33
PRINTFORML %CALLNAME:TARGET%にお菓子を作らせた
SELECTCASE ABL:料理技能
	CASE 0
		SELECTCASE RAND:2
			CASE 0
				PRINTW ……電子レンジの中で何かが爆発したようだ
			CASE 1
				PRINTW 台所を粉まみれにしつつもなんとか完成したようだ……
		ENDSELECT
	CASE 1
		SELECTCASE RAND:2
			CASE 0
				PRINTW 香りは悪くないが見た目の悪さが際立つ……
			CASE 1
				PRINTFORMW 味付けの分量を間違えたようで、少々味が\@ RAND:2 ? 濃い # 薄い \@……
		ENDSELECT
	CASE 2 TO 4
		SELECTCASE RAND:2
			CASE 0
				PRINTW 美味しそうな甘い香りが家中に広がっている……
			CASE 1
				PRINTFORMW 申し分ない出来栄えに%CALLNAME:TARGET%自信も満足しているようだ
		ENDSELECT
	CASE IS >= 5
		SELECTCASE RAND:2
			CASE 0
				PRINTW お店に並んでいるような本格的なお菓子が出来上がった
			CASE 1
				PRINTW 見た目にも趣向を凝らした逸品は食べるのすらもったいないほどだ
		ENDSELECT
ENDSELECT

;-------------------------------------------------
;34,楽器の練習
;-------------------------------------------------
@MESSAGE_COM34
PRINTFORMW %CALLNAME:TARGET%に楽器の練習をさせた

;-------------------------------------------------
;35,機械の勉強
;-------------------------------------------------
@MESSAGE_COM35
PRINTFORMW %CALLNAME:TARGET%に機械の勉強を教えた

;-------------------------------------------------
;36,プログラミング演習
;-------------------------------------------------
@MESSAGE_COM36
PRINTFORMW %CALLNAME:TARGET%にプログラミングの勉強を教えた

;-------------------------------------------------
;37,お酒を飲ませる
;-------------------------------------------------
@MESSAGE_COM37
PRINTFORML %CALLNAME:TARGET%に%酒名%を飲ませることにした
IF CFLAG:酔
	IF TALENT:下戸
		IF CFLAG:酔 >= 20
			PRINTFORMW %CALLNAME:TARGET%は既に呂律が回らなくなっている。少し苦しそうだ……
		ELSE
			IF CFLAG:陥落
				PRINTFORMW %CALLNAME:TARGET%は酔って%CALLNAME:MASTER%に抱きついたりしながらアルコールに溺れている
			ELSE
				PRINTFORMW 既に酔いが回っていた%CALLNAME:TARGET%は顔を紅くしながら少しづつ%酒名%を飲んでいる
			ENDIF
		ENDIF
	ELSEIF TALENT:酒豪
		IF CFLAG:陥落
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%にも酌をしながらお酒を楽しんでいる
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は、自分はまだ飲めると言わんばかりに豪快に%酒名%を飲み干した
		ENDIF
	ELSE
		IF CFLAG:酔 >= 50
			PRINTFORMW %CALLNAME:TARGET%は既に呂律が回らなくなっている。少し苦しそうだ……
		ELSE
			IF CFLAG:陥落
				PRINTFORMW 勧められるままにお酒を飲む%CALLNAME:TARGET%は、酔いが回って積極的になっている
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は不本意に酒に溺れることを不安がっている……
			ENDIF
		ENDIF
	ENDIF
ELSE
	IF CFLAG:二日酔い
		PRINTFORMW 二日酔い真っ只中の%CALLNAME:TARGET%にとっては辛いようだが、迎え酒という名目で飲ませた……
	ELSEIF TALENT:下戸
		PRINTFORMW %CALLNAME:TARGET%はあまり乗り気ではないようだが、少しだけということでお酒を口にした
	ELSEIF TALENT:酒豪
		IF CFLAG:陥落
			PRINTFORMW %CALLNAME:TARGET%はお酒を飲めてご満悦な様子だ
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は酒が唯一の救いだといった様子で呑んでいる
		ENDIF
	ELSE
		IF CFLAG:陥落
			PRINTFORMW %CALLNAME:TARGET%は一人で飲むのも悪いということで、%CALLNAME:MASTER%と盃を交わすことにした
		ELSE
			PRINTFORMW %CALLNAME:TARGET%はあまり乗り気ではないようだが、少しだけということでお酒を口にした
		ENDIF
	ENDIF
ENDIF

