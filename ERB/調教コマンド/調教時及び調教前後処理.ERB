;調教前の処理
;2.04でEVENTTRAINもイベント関数から解放
@EVENTTRAIN
CALL EVENTUSERTRAIN

@EVENTUSERTRAIN
#DIM DYNAMIC LCOUNT

;初期着衣設定に応じて、服の設定をする
FOR LCOUNT, 100, 107
	SIF CSTR:LCOUNT != ""
		TEQUIP:(LCOUNT-100) = 1
	SIF CSTR:MASTER:LCOUNT != ""
		TEQUIP:MASTER:(LCOUNT-100) = 1
	IF ASSI > 0
		SIF CSTR:ASSI:LCOUNT != ""
			TEQUIP:ASSI:(LCOUNT-100) = 1
	ENDIF
NEXT

SELECTCASE FLAG:着衣設定
	;下着無し
	CASE 1
		TEQUIP:上半身下着 = 0
		TEQUIP:下半身下着 = 0
	;下着姿
	CASE 2
		TEQUIP:上半身上着 = 0
		TEQUIP:下半身上着 = 0
		TEQUIP:全身上着 = 0
	;全裸
	CASE 3
		REPEAT 7
			TEQUIP:COUNT = 0
			TEQUIP:MASTER:COUNT = 0
			SIF ASSI > 0
				TEQUIP:ASSI:COUNT = 0
		REND
ENDSELECT

;調教対象・助手が特定のキャラの場合、調教開始時に豹変することがある
;紅刃……1/5の確率で豹変
;冷音……天気が雨・雪・雷雨で豹変
IF TARGET >= 0
	IF CHARANAMEF(TARGET) == "紅刃" && RAND:5 == 0
		CALL TRANCE_ON, TARGET
		TRANCE:200 = 1
		DRAWLINE
		PRINTFORMW ＜紅刃は性格が豹変した＞
	ELSEIF CHARANAMEF(TARGET) == "春雨 冷音" && INRANGE(FLAG:天気, 3, 5)
		CALL TRANCE_ON, TARGET
		TRANCE:200 = 1
		DRAWLINE
		PRINTFORMW ＜冷音は性格が豹変した＞
	ENDIF
ENDIF
IF ASSI >= 0
	IF CHARANAMEF(ASSI) == "紅刃" && RAND:5 == 0
		CALL TRANCE_ON, ASSI
		TRANCE:ASSI:200 = 1
		DRAWLINE
		PRINTFORMW ＜紅刃は性格が豹変した＞
	ELSEIF CHARANAMEF(ASSI) == "春雨 冷音" && INRANGE(FLAG:天気, 3, 5)
		CALL TRANCE_ON, ASSI
		TRANCE:ASSI:200 = 1
		DRAWLINE
		PRINTFORMW ＜冷音は性格が豹変した＞
	ENDIF
ENDIF

;実行コマンド、直前の実行コマンドフラグを初期化
PREVCOMS = 
SELECTCOMS = 

;二日酔い
IF CFLAG:二日酔い
	PRINTFORMW %CALLNAME:TARGET%は二日酔いを起こしたそうだ……
	BASE:体力 /= 2
	BASE:気力 /= 2
ENDIF

;服従系陥落なら主人と助手との相性が良くなったりする
IF TALENT:服従
	SIF RELATION:(NO:MASTER) < 120
		RELATION:(NO:MASTER) = 120
	SIF ASSI > 0 && RELATION:(NO:ASSI) < 120
		RELATION:(NO:ASSI) = 120
ELSEIF TALENT:隷属
	SIF RELATION:(NO:MASTER) < 150
		RELATION:(NO:MASTER) = 150
	SIF ASSI > 0 && RELATION:(NO:ASSI) < 150
		RELATION:(NO:ASSI) = 150
ENDIF

;助手もまた然り
IF ASSI > 0
	IF TALENT:ASSI:服従
		SIF RELATION:ASSI:(NO:MASTER) < 120
			RELATION:ASSI:(NO:MASTER) = 120
		SIF RELATION:ASSI:(NO:TARGET) < 120
			RELATION:ASSI:(NO:TARGET) = 120
	ELSEIF TALENT:ASSI:隷属
		SIF RELATION:ASSI:(NO:MASTER) < 150
			RELATION:ASSI:(NO:MASTER) = 150
		SIF RELATION:ASSI:(NO:TARGET) < 150
			RELATION:ASSI:(NO:TARGET) = 150
	ENDIF
ENDIF

;MAXBASEの設定
LOCAL = 10000
;絶倫で半減
SIF TALENT:MASTER:絶倫
	LOCAL /= 2
;小柄体型で2倍
SIF TALENT:MASTER:小柄体型
	LOCAL *= 2
;女性で2倍
SIF SEX(MASTER) == 2
	LOCAL *= 2
;Ｃ鈍感なら1.5倍
SIF TALENT:MASTER:Ｃ鈍感
	TIMES LOCAL, 1.50
MAXBASE:MASTER:射精 = LOCAL

;以下対象と助手にも同様の処理
LOCAL = 10000
SIF TALENT:絶倫
	LOCAL /= 2
SIF TALENT:小柄体型
	LOCAL *= 2
SIF SEX(TARGET) == 2
	LOCAL *= 2
SIF TALENT:Ｃ鈍感
	TIMES LOCAL, 1.50
MAXBASE:射精 = LOCAL

IF ASSI >= 0
	LOCAL = 10000
	SIF TALENT:ASSI:絶倫
		LOCAL /= 2
	SIF TALENT:ASSI:小柄体型
		LOCAL *= 2
	SIF SEX(ASSI) == 2
		LOCAL *= 2
	SIF TALENT:ASSI:Ｃ鈍感
		TIMES LOCAL, 1.50
	MAXBASE:ASSI:射精 = LOCAL
ENDIF

IF TALENT:母乳体質
	LOCAL = 10000
	SIF TALENT:淫乳
		LOCAL /= 2
	SIF TALENT:Ｂ鈍感
		LOCAL *= 2
	MAXBASE:母乳 = LOCAL
ENDIF

MAXBASE:MASTER:触手射精 = 10000
MAXBASE:MASTER:ドラゴン射精 = 10000
SIF ASSI > 0
	MAXBASE:ASSI:ドラゴン射精 = 10000

;主人と調教対象と助手の射精を0に
BASE:MASTER:射精 = 0
BASE:射精 = 0
BASE:MASTER:ドラゴン射精 = 0
IF ASSI >= 0
	BASE:ASSI:射精 = 0
	BASE:ASSI:ドラゴン射精 = 0
ENDIF
;母乳も0に
BASE:母乳 = 0
;触手の射精も0に
BASE:MASTER:触手射精 = 0

;射精限界数の確保
CFLAG:残数 = 10
SIF TALENT:絶倫
	CFLAG:残数 += 5
SIF !PENIS(TARGET)
	CFLAG:残数 = 0

CFLAG:MASTER:残数 = 10
SIF TALENT:MASTER:絶倫
	CFLAG:MASTER:残数 += 5
SIF エナジードライブ
	CFLAG:MASTER:残数 += エナジードライブ:1*3
SIF !PENIS(MASTER)
	CFLAG:MASTER:残数 = 0

IF ASSI >= 0
	CFLAG:ASSI:残数 = 10
	SIF TALENT:ASSI:絶倫
		CFLAG:ASSI:残数 += 5
	SIF !PENIS(ASSI)
		CFLAG:ASSI:残数 = 0
ENDIF

CFLAG:射精飾り値 = 0
CFLAG:MASTER:射精飾り値 = 0
SIF ASSI >= 0
	CFLAG:ASSI:射精飾り値 = 0

;賢者タイムをリセット
CFLAG:MASTER:賢者タイム = 0
CFLAG:賢者タイム = 0
SIF ASSI >= 0
	CFLAG:ASSI:賢者タイム = 0

;TFLAGをリセット
VARSET TFLAG

;淫乱なら膣内にヴァギナ汚れを付加
SIF TALENT:淫乱 && VAGINA(TARGET)
	SETBIT STAIN:膣内, 0
SIF ASSI >= 0 && TALENT:ASSI:淫乱 && VAGINA(ASSI)
	SETBIT STAIN:ASSI:膣内, 0
;淫乱ならペニスに先走り付加
SIF TALENT:淫乱 && PENIS(TARGET)
	SETBIT STAIN:ペニス, 1
SIF ASSI >= 0 && TALENT:ASSI:淫乱 && PENIS(ASSI)
	SETBIT STAIN:ASSI:ペニス, 1
;主人は無条件で先走りと愛液付加
SIF VAGINA(MASTER)
	SETBIT STAIN:MASTER:ヴァギナ, 0
SIF PENIS(MASTER)
	SETBIT STAIN:MASTER:ペニス, 1

;媚薬の効果をリセット
CFLAG:MASTER:媚薬 = 0
CFLAG:媚薬 = 0
SIF ASSI > 0
	CFLAG:ASSI:媚薬 = 0

;コンドーム射精フラグをリセット
TFLAG:使用済み避妊具 = 0

;調教者はMASTERにしとく
PLAYER = MASTER

;ビデオボーナスのリセット
VARSET VIDEOBONUS

;写真関連のリセット
VARSET 写真内容
VARSET 写真価格

;ドラゴンの名前が無い場合、名前を「ドラゴン」に
SIF TALENT:MASTER:ドラゴン使い && CSTR:MASTER:ドラゴン == ""
	CSTR:MASTER:ドラゴン = ドラゴン
SIF ASSI >= 0 && TALENT:ASSI:ドラゴン使い && CSTR:ASSI:ドラゴン == ""
	CSTR:ASSI:ドラゴン = ドラゴン

;種族宇宙人の感度変化
IF CSTR:種族 == "宇宙人"
	IF VAGINA(TARGET)
		SELECTCASE RAND:3
			CASE 0
				TALENT:Ｖ敏感 = 0
				TALENT:Ｖ鈍感 = 1
			CASE 1
				TALENT:Ｖ敏感 = 0
				TALENT:Ｖ鈍感 = 0
			CASE 2
				TALENT:Ｖ敏感 = 1
				TALENT:Ｖ鈍感 = 0
		ENDSELECT
	ENDIF
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ｃ敏感 = 0
			TALENT:Ｃ鈍感 = 1
		CASE 1
			TALENT:Ｃ敏感 = 0
			TALENT:Ｃ鈍感 = 0
		CASE 2
			TALENT:Ｃ敏感 = 1
			TALENT:Ｃ鈍感 = 0
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ａ敏感 = 0
			TALENT:Ａ鈍感 = 1
		CASE 1
			TALENT:Ａ敏感 = 0
			TALENT:Ａ鈍感 = 0
		CASE 2
			TALENT:Ａ敏感 = 1
			TALENT:Ａ鈍感 = 0
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			TALENT:Ｂ敏感 = 0
			TALENT:Ｂ鈍感 = 1
		CASE 1
			TALENT:Ｂ敏感 = 0
			TALENT:Ｂ鈍感 = 0
		CASE 2
			TALENT:Ｂ敏感 = 1
			TALENT:Ｂ鈍感 = 0
	ENDSELECT
ENDIF

;種族妖精ならランダムで消費アイテム取得
IF CSTR:種族 == "妖精"
	LOCAL = RAND(100, 108)
	LOCAL:1 = RAND(1, 4)
	PRINTFORML %CALLNAME:TARGET%の部屋にあった{LOCAL:1}個の%ITEMNAME:LOCAL%を回収しました
	ITEM:LOCAL += LOCAL:1
ENDIF

;鳳凰ならボーナス
IF CSTR:種族 == "鳳凰" && FLAG:気温 > 0
	LOCAL = FLAG:気温
	SELECTCASE FLAG:天気
		CASE 2
			TIMES LOCAL , 0.50
		CASE 3 TO 7
			TIMES LOCAL , 0.20
	ENDSELECT
	;気温と天気で計算したボーナスを一部の初期PALAMに付与する
	REPEAT VARSIZE("PALAM")
		SELECTCASE PALAMNAME:COUNT
			CASE "快Ａ", "快Ｂ", "潤滑", "恭順", "欲情", "屈服", "習得"
				PALAM:COUNT += LOCAL
			CASE "快Ｃ"
				SELECTCASE SEX(TARGET)
					CASE 1
						SIF PENIS(TARGET)
							PALAM:COUNT += LOCAL
					CASE 2
						PALAM:COUNT += LOCAL
				ENDSELECT
			CASE "快Ｖ"
				SIF VAGINA(TARGET)
					PALAM:COUNT += LOCAL
		ENDSELECT
	REND
ENDIF

;開始前の愛情経験を入れておく
TFLAG:愛情経験計算用 = EXP:愛情経験

;オナホパフェフラグリセット
オナホパフェ = 0

;実績:アイテムを何も持ってない状態で調教を始める
LOCAL = 0
REPEAT VARSIZE("ITEM")
	IF ITEM:COUNT
		LOCAL = 1
		BREAK
	ENDIF
REND
SIF LOCAL == 0
	CALL 実績解除, "そんな装備で大丈夫か？"

LOCAL = 0

;実績:ロッテとラッテを対面させる
IF ASSI > 0
	SIF (CHARANAMEF(TARGET) == "ロッテ" && CHARANAMEF(ASSI) == "ラッテ") || (CHARANAMEF(TARGET) == "ラッテ" && CHARANAMEF(ASSI) == "ロッテ")
		CALL 実績解除, "パラドックス"
ENDIF

;実績;パステルくん主人で奴隷と好相性の助手を使用する
SIF CHARANAMEF(MASTER) == "パステルくん" && ASSI > 0 && RELATION:(NO:ASSI) > 100
	CALL 実績解除, "「わ～いらくち～ん」"

CALL KOJO_START

;コマンド後のTFLAGリセット処理
@EVENTUSERCOM
REPEAT 100
	TFLAG:COUNT = 0
REND

;コマンド終了時に呼ばれる ここでは調教終了処理と媚薬処理
@EVENTUSERCOMEND
IF !TEQUIP:媚薬 && CFLAG:媚薬 >= 10
	SELECTCASE CSTR:MASTER:種族
		CASE "獣人", "獣"
			PRINTFORML %CALLNAME:MASTER%のフェロモンで%CALLNAME:TARGET%は媚薬を飲まされたかのように発情してしまった……
		CASEELSE
			PRINTFORML %CALLNAME:ASSI%のフェロモンで%CALLNAME:TARGET%は媚薬を飲まされたかのように発情してしまった……
	ENDSELECT
	TEQUIP:媚薬 = 1
ENDIF
IF !TEQUIP:MASTER:媚薬 && CFLAG:MASTER:媚薬 >= 10
	SELECTCASE CSTR:種族
		CASE "獣人", "獣"
			PRINTFORML %CALLNAME:TARGET%のフェロモンで%CALLNAME:MASTER%は媚薬を飲まされたかのように発情してしまった……
		CASEELSE
			PRINTFORML %CALLNAME:ASSI%のフェロモンで%CALLNAME:MASTER%は媚薬を飲まされたかのように発情してしまった……
	ENDSELECT
	TEQUIP:MASTER:媚薬 = 1
ENDIF
IF ASSI > 0
	IF !TEQUIP:ASSI:媚薬 && CFLAG:ASSI:媚薬 >= 10
		SELECTCASE CSTR:種族
			CASE "獣人", "獣"
				PRINTFORML %CALLNAME:TARGET%のフェロモンで%CALLNAME:ASSI%は媚薬を飲まされたかのように発情してしまった……
			CASEELSE
				PRINTFORML %CALLNAME:MASTER%のフェロモンで%CALLNAME:ASSI%は媚薬を飲まされたかのように発情してしまった……
		ENDSELECT
	ENDIF
	TEQUIP:ASSI:媚薬 = 1
ENDIF

IF 上級売春中
	IF BASE:MASTER:体力 <= 500
		PRINTFORML （%CALLNAME:MASTER%の体力が限界のため、売春を終了します）
		;お客様満足度一定以上
		IF 上級売春中 == 2
			上級売春中 = 4
		ELSE
			上級売春中 = 3
		ENDIF
	ENDIF
ELSE
	;体力切れ
	IF BASE:体力 <= 0
		;自爆した場合
		IF ハッキング命令 == "自爆"
			PRINTFORML ★★★%CALLNAME:TARGET%は自爆しました★★★
			自爆キャラ:(NO:TARGET) = 1
			BEGIN AFTERTRAIN
		ELSE
			PRINTFORML （激しい調教で%CALLNAME:TARGET%はひどく消耗したようです　調教を終了します）
			;従順ダウン 陥落済みなら発生しない
			IF ABL:従順 > 0 && !CFLAG:陥落
				ABL:従順--
				PRINTFORML %CALLNAME:TARGET%の従順が1下がった
			ENDIF
			;回復量低下フラグ
			CFLAG:回復量低下 += 2
			CFLAG:ストレス値 += 10
			BASE:体力 = 1
			;条件を満たしていれば吸血イベント発生するか選択できる
			;条件：主人が吸血鬼で、対象が種族[人間]である
			IF TALENT:MASTER:吸血鬼 && CSTR:種族 == "人間"
				PRINTFORMW 消耗して動けない%CALLNAME:TARGET%を見下ろしながら思う……
				PRINTFORMW このまま%CALLNAME:TARGET%を……
				PRINTL [0] 眷属に迎え入れようか
				PRINTL [1] 放っておこうか
				DO
					INPUT
					;吸血
					IF RESULT == 0
						CALL KOJO_MESSAGE_EVENT, "主人吸血"
						;吸血鬼化
						IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
							TALENT:吸血鬼 = 1
							CSTR:種族 = ヴァンパイア
							;上位陥落素質ありで妄信イベント
							IF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && !TALENT:妄信
								PRINTFORML %CALLNAME:TARGET%は[妄信]を得た。
								TALENT:妄信 = 1
							ENDIF
						;ゾンビ化
						ELSE
							TALENT:ゾンビ = 1
							CSTR:種族 = ゾンビ
						ENDIF
						IF CFLAG:被吸血経験 == 0
							EXP:異常経験++
							PRINTL 異常経験+1
						ENDIF
						CFLAG:被吸血経験++
						EXP:MASTER:吸血経験++
					ENDIF
				LOOP LOOPRES(0, 1)
			;助手が陥落して条件を満たしていれば、助手を使った吸血イベント発生するか選択できる
			;条件：助手が居て、助手が吸血鬼で、対象が種族[人間]である
			ELSEIF ASSI >= 0 && CFLAG:ASSI:陥落 && TALENT:ASSI:吸血鬼 && CSTR:種族 == "人間"
				PRINTFORML 今の無防備な%CALLNAME:TARGET%なら好き勝手できそうだ。
				PRINTFORML %CALLNAME:ASSI%に血を吸わせますか？
				PRINTL [0] - 吸わせる
				PRINTL [1] - 吸わせない
				DO
					INPUT
					IF RESULT == 0
						CALL KOJO_MESSAGE_EVENT, "助手吸血"
						;対象が処女、再生処女、童貞なら吸血鬼化
						IF TALENT:処女 || TALENT:再生処女 || TALENT:童貞
							TALENT:吸血鬼 = 1
							CSTR:種族 = ヴァンパイア
						;ゾンビ化
						ELSE
							TALENT:ゾンビ = 1
							CSTR:種族 = ゾンビ
						ENDIF
						;初めて血を吸われたら異常経験追加
						IF CFLAG:被吸血経験 == 0
							EXP:異常経験++ 
							PRINTL 異常経験+1
						ENDIF
						CFLAG:被吸血経験++
						EXP:ASSI:吸血経験++
					ENDIF
				LOOP LOOPRES(0, 1)
			ENDIF
			;実績:ガッキー博士で奴隷の体力を0にする
			SIF CHARANAMEF(PLAYER) == "ガッキー博士"
				CALL 実績解除, "「科学の発展に犠牲は付き物デース」"
			BEGIN AFTERTRAIN
		ENDIF
	ELSEIF BASE:体力 < 500
		PRINTL （体力が限界に来ています　調教を終了します）
		BEGIN AFTERTRAIN
	ENDIF
ENDIF

;流産時に体力が削りきれなかった場合は強制的に調教終了
IF TFLAG:流産
	PRINTFORML （%CALLNAME:TARGET%が流産したため、調教を終了します）
	CFLAG:使用不可 += 30
	CFLAG:出産トラウマ += 50
	SIF CFLAG:出産トラウマ >= 100
		CFLAG:出産トラウマ = 100
	BEGIN AFTERTRAIN
ENDIF
;自爆時も同様
IF ハッキング命令 == "自爆"
	PRINTFORML ★★★%CALLNAME:TARGET%は自爆しました★★★
	自爆キャラ:(NO:TARGET) = 1
	BEGIN AFTERTRAIN
ENDIF

;調教で【崩壊】を得た場合のイベント
;@COLLAPSE_MIND_TRAIN
;PRINTFORMW %CALLNAME:TARGET%の様子がおかしい
;PRINTFORMW 激しい調教の影響によって、%CALLNAME:TARGET%の精神に異常をきたしてしまったようだ
;PRINTFORMW %NAME:TARGET%は【崩壊】してしまった
;TALENT:崩壊 = 1
;IF CFLAG:陥落
;	PRINTFORMW 今まで重ねてきた時間も虚しく、%CALLNAME:TARGET%はすっかり心を閉ざしてしまった……
;	SIF TALENT:恋慕
;		PRINTFORMW %CALLNAME:TARGET%は[恋慕]を失った
;	SIF TALENT:淫乱
;		PRINTFORMW %CALLNAME:TARGET%は[淫乱]を失った
;	SIF TALENT:服従
;		PRINTFORMW %CALLNAME:TARGET%は[服従]を失った
;	SIF TALENT:親愛
;		PRINTFORMW %CALLNAME:TARGET%は[親愛]を失った
;	SIF TALENT:娼婦
;		PRINTFORMW %CALLNAME:TARGET%は[娼婦]を失った
;	SIF TALENT:隷属
;		PRINTFORMW %CALLNAME:TARGET%は[隷属]を失った
;	CFLAG:陥落 = 0
;ENDIF

;調教終了時のイベント
@EVENTEND
#LATER
#DIM DYNAMIC LCOUNT
CALL KOJO_TURNEND
PRINTW 調教を終了しました

;壊れたアイテムを修理する
FOR LCOUNT, 0, VARSIZE("ITEM")
	IF ITEM:LCOUNT == -1
		PRINTFORML 破損した%ITEMNAME:LCOUNT%を修理しました
		ITEM:LCOUNT = 1
	ENDIF
NEXT

;自爆したらDELCHARAして全部無視してTURNEND
IF ハッキング命令 == "自爆"
	DELCHARA TARGET
	TARGET = -1
	BEGIN TURNEND
ENDIF

;屋外プレイで雨、もしくはお風呂場プレイ中に終了した場合、人魚の体力気力を半減させる
IF TEQUIP:お風呂場
	IF CSTR:種族 == "人魚"
		MAXBASE:体力 /= 2
		MAXBASE:気力 /= 2
		BASE:体力 /= 2
		BASE:気力 /= 2
	ENDIF
ELSEIF TEQUIP:野外プレイ
	SELECTCASE FLAG:天気
		CASE 3, 4, 5
			IF CSTR:種族 == "人魚"
				MAXBASE:体力 /= 2
				MAXBASE:気力 /= 2
				BASE:体力 /= 2
				BASE:気力 /= 2
			ENDIF
	ENDSELECT
ENDIF

;快感に素直、快感の否定は絶頂回数に応じてストレス増減
IF TALENT:快感に素直
	CFLAG:ストレス値 -= EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
ELSEIF TALENT:快感の否定 && !CFLAG:陥落
	CFLAG:ストレス値 += EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂+EX:Ｂ絶頂+EX:射精/3
ENDIF

;恋慕系陥落で流産してなければ愛情経験に応じてストレス低下
LOCAL = EXP:愛情経験-TFLAG:愛情経験計算用
IF LOCAL > 0 && CFLAG:ストレス値 > 0
	IF TALENT:恋慕 && LOCAL >= 2
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の求愛によってストレスが緩和されました
		CFLAG:ストレス値 -= LOCAL/2
	ELSEIF TALENT:親愛
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%からの愛を確かめることでことでストレスが緩和されました
		CFLAG:ストレス値 -= LOCAL
	ENDIF
ENDIF

;淫乱系陥落で絶頂回数に応じてストレス低下
LOCAL = EX:Ｃ絶頂+EX:Ｖ絶頂+EX:Ａ絶頂*EX:Ｂ絶頂+EX:射精
IF LOCAL > 0
	IF TALENT:淫乱
		PRINTFORMW %CALLNAME:TARGET%は欲求を満たされることでストレスが緩和しました
		CFLAG:ストレス値 = LOCAL
	ELSEIF TALENT:娼婦
		PRINTFORMW %CALLNAME:TARGET%は欲求を満たされることでストレスが緩和しました
		CFLAG:ストレス値 -= LOCAL*2
	ENDIF
ENDIF

;マゾっ気があればPALAM:苦痛のPALAMLVに応じてストレス低下
IF ABL:マゾっ気 && GETPALAMLV(PALAM:苦痛, 10)
	PRINTFORMW %CALLNAME:TARGET%はマゾヒズムを満たされることでストレスが緩和しました
	CFLAG:ストレス値 -= MIN(ABL:マゾっ気, GETPALAMLV(PALAM:苦痛, 10))
ENDIF

;精液中毒 主人と助手の射精回数に応じてストレス低下
LOCAL = EX:MASTER:射精+EX:MASTER:触手射精
SIF ASSI > 0
	LOCAL += EX:ASSI:射精
IF LOCAL > 4 && ABL:EXP:精液中毒
	PRINTFORMW %CALLNAME:TARGET%は精液への渇望を満たされることでストレスが緩和しました
	CFLAG:ストレス値 -= MIN(ABL:精液中毒, LOCAL/5)
ENDIF

;調教後行為のチェック 気絶、流産時はスルー
SIF !TFLAG:気絶 && !TFLAG:流産 && !CFLAG:使用不可
	CALL SELF_CHECK

;実績「1回の調教でフローラに10回以上中出しする」
IF CHARANAMEF(TARGET) == "フローラ"
	LOCAL = 0
	LOCAL += PREG:0
	SIF ASSI > -1
		LOCAL += PREG:1
	IF LOCAL >= 10
		CALL 実績解除, "生命の環を紡いで"
	ENDIF
ELSEIF ASSI > -1 && CHARANAMEF(ASSI) == "フローラ"
	LOCAL = 0
	LOCAL += PREG:ASSI:0
	LOCAL += PREG:ASSI:2
	IF LOCAL >= 10
		CALL 実績解除, "生命の環を紡いで"
	ENDIF
ENDIF

;全体の妊娠判定の処理
CALL IN_VAGINA

;搾乳した母乳の売却
CALL SELL_MILK

;搾精した精液の売却
CALL SELL_SAMEN

;調教時に録画したビデオを売却
CALL SELL_VIDEO

;調教時に撮影した写真を売却
CALL SELL_PICTURE

;何の珠を得られたか
CALL JUEL_CHECK

;否定の珠の打ち消しチェック
CALL DENIAL_CHECK

;淫乱or娼婦奴隷の相性増加
IF TALENT:淫乱
	IF RAND:3 == 0
		SIF RELATION:(NO:MASTER) < 150
			RELATION:(NO:MASTER)++
		SIF ASSI > 0 && RELATION:(NO:ASSI) < 150
			RELATION:(NO:ASSI)++
	ENDIF
ELSEIF TALENT:娼婦
	IF RAND:2 == 0
		SIF RELATION:(NO:MASTER) < 200
			RELATION:(NO:MASTER)++
		SIF ASSI > 0 && RELATION:(NO:ASSI) < 200
			RELATION:(NO:ASSI)++
	ENDIF
ENDIF

BEGIN ABLUP

@JUEL_CHECK
REPEAT 15
	SELECTCASE GETPALAMLV(PALAM:COUNT, 10)
		CASE 0
			LOCAL = 0
		CASE 1
			IF PALAM:COUNT < PALAMLV:1*3
				LOCAL = 1
			ELSE
				LOCAL = 2
			ENDIF
		CASE 2
			IF PALAM:COUNT < PALAMLV:2*3
				LOCAL = 10
			ELSE
				LOCAL = 20
			ENDIF
		CASE 3
			IF PALAM:COUNT < PALAMLV:3*2
				LOCAL = 100
			ELSE
				LOCAL = 200
			ENDIF
		CASE 4
			LOCAL = 500
		CASE 5
			LOCAL = 1000
		CASE 6
			LOCAL = 2000
		CASE 7
			LOCAL = 3000
		CASE 8
			LOCAL = 5000
		CASE 9
			LOCAL = 8000
		CASEELSE
			LOCAL = PALAM:COUNT/20
	ENDSELECT

	SELECTCASE COUNT
		CASE 0
			IF PENIS(TARGET)
				GOTJUEL:COUNT = LOCAL+(EX:射精*2000)+(EX:潮噴き*3000)
			ELSE
				GOTJUEL:COUNT = LOCAL+EX:Ｃ絶頂*1000
			ENDIF
		CASE 1
			GOTJUEL:COUNT = LOCAL+EX:Ｖ絶頂*1000
		CASE 2
			GOTJUEL:COUNT = LOCAL+EX:Ａ絶頂*1000
		CASE 3
			GOTJUEL:COUNT = LOCAL+EX:Ｂ絶頂*1000
		CASE 4 TO 11
			GOTJUEL:COUNT = LOCAL
		CASEELSE
			GOTJUEL:否定 += LOCAL
	ENDSELECT
REND

DRAWLINE
PRINTL 調教の結果：
REPEAT 13
	IF COUNT == 12
		PRINTFORML 否定の珠×({JUEL:否定}+{GOTJUEL:否定})
		JUEL:否定 += GOTJUEL:否定
	ELSEIF PALAMNAME:COUNT != "潤滑"
		PRINTFORML %PALAMNAME:COUNT%の珠×({JUEL:COUNT}+{GOTJUEL:COUNT})
		JUEL:COUNT += GOTJUEL:COUNT
	ENDIF
REND

PRINTW 以上の珠を得ました

;上限は9,999,999
REPEAT 13
	IF COUNT == 12
		SIF JUEL:否定 > 9999999
			JUEL:否定 = 9999999
	ELSE
		SIF JUEL:COUNT > 9999999
			JUEL:COUNT = 9999999
	ENDIF
REND

@DENIAL_CHECK
;欲情、屈服、恭順の珠から順に打ち消される
SIF JUEL:否定 == 0
	RETURN 0

DRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています

IF JUEL:否定 > 0 && (JUEL:恭順+JUEL:欲情+JUEL:屈服) > 0
	LOCAL:1 = JUEL:否定
	SIF JUEL:欲情 < JUEL:否定
		LOCAL:1 = JUEL:欲情

	JUEL:欲情 -= LOCAL:1
	JUEL:否定 -= LOCAL:1

	SIF LOCAL:1 > 0
		PRINTFORML 欲情の珠×{LOCAL:1}減少

	LOCAL:1 = JUEL:否定
	SIF JUEL:屈服 < JUEL:否定
		LOCAL:1 = JUEL:屈服

	SIF LOCAL:1 > 0
		PRINTFORML 屈服の珠×{LOCAL:1}減少

	JUEL:屈服 -= LOCAL:1
	JUEL:否定 -= LOCAL:1

	LOCAL:1 = JUEL:否定
	SIF JUEL:恭順 < JUEL:否定
		LOCAL:1 = JUEL:恭順

	JUEL:恭順 -= LOCAL:1
	JUEL:否定 -= LOCAL:1

	SIF LOCAL:1 > 0
		PRINTFORML 恭順の珠×{LOCAL:1}減少
ENDIF

PRINTL その結果、
REPEAT 13
	IF COUNT == 12
		PRINTFORML 否定の珠×({JUEL:100})
	ELSEIF PALAMNAME:COUNT != "潤滑"
		PRINTFORML %PALAMNAME:COUNT%の珠×({JUEL:COUNT})
	ENDIF
REND

PRINTW 以上のように変化しました

@CVAR_RESET
;調教時のキャラ変数を初期化する
IF TARGET >= 0
	VARSET GOTJUEL, 0
	VARSET TEQUIP, 0
	VARSET EX, 0
	VARSET PALAM, 0
	VARSET SOURCE, 0
ENDIF
IF ASSI >= 0
	VARSET TEQUIP:ASSI:0, 0
	VARSET EX:ASSI:0, 0
ENDIF
VARSET TEQUIP:MASTER:0, 0
VARSET EX:MASTER:0, 0
PREVCOMS = 
SELECTCOMS = 
SELECTCOM = 0

