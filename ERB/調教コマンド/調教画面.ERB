;TRAIN内の構成に呼び出される
@SHOW_STATUS
DRAWLINE
IF YEAR > 0
	PRINTFORM {YEAR}
ELSE
	PRINTFORM BC{ABS(YEAR)+1}
ENDIF
PRINTFORM /{MONTH}/{DAYS}(%DOW(ZELLER(YEAR,MONTH,DAYS))%)
PRINTFORM  {DAY}日目(\@ TIME == 0 ? 昼 # 夜 \@) 
CALL PASELI, @"所持金:{MONEY}＄ "
PRINTFORM 気温:{FLAG:気温/10}.{ABS(FLAG:気温%10)}℃ 天気:
SELECTCASE FLAG:天気
	CASE 1
		PRINT 晴れ
	CASE 2
		PRINT 曇り
	CASE 3
		PRINT 雨
	CASE 4
		PRINT 大雨
	CASE 5
		PRINT 雷雨
	CASE 6
		PRINT 雪
	CASE 7
		PRINT 大雪
ENDSELECT
PRINTL 

IF 上級売春中
	PRINTFORML 客:%NAME:TARGET% 娼婦:\@ ASSIPLAY ? %NAME:ASSI%(助手) # %NAME:MASTER% \@
ELSEIF FLAG:ゲームモード == 7
	PRINTFORML 受け:%NAME:TARGET% 攻め:\@ ASSIPLAY ? %NAME:ASSI%(助手) # %NAME:MASTER% \@
ELSE
	PRINTFORML 調教中:%NAME:TARGET% 調教者:\@ ASSIPLAY ? %NAME:ASSI%(助手) # %NAME:MASTER% \@
ENDIF
SIF TALENT:PLAYER:孔雀の瞳
	PRINTFORML (%CALLNAME:TARGET%の依存度:{CFLAG:依存度} %CALLNAME:TARGET%のストレス値{CFLAG:ストレス値})

CALL SHOW_STATE

IF 上級売春中
	PRINTFORML □%CALLNAME:TARGET%のステータス
	IF PENIS(TARGET)
		PRINT 　射精
		SIF CFLAG:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", TARGET)%
	ENDIF

	CALL SHOW_INFO_PALAM

	PRINTFORML □%CALLNAME:MASTER%のステータス
	SIF TALENT:MASTER:魔術師
		PRINTFORML 　魔力%BARBASE("魔力", MASTER)%
	SIF ASSI > 0 && TALENT:ASSI:魔術師
		PRINTFORML 　魔力%BARBASE("魔力", ASSI)%
	CALL SHOW_INFO_HEALTH, MASTER
	IF PENIS(MASTER)
		PRINT 　射精(主人)
		SIF CFLAG:MASTER:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", MASTER)%
	ENDIF

	IF ASSI >= 0 && PENIS(ASSI)
		PRINT 　射精(助手)
		SIF CFLAG:ASSI:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", ASSI)%
	ENDIF
ELSE
	PRINTFORML □%CALLNAME:TARGET%のステータス
	CALL SHOW_INFO_HEALTH
	IF PENIS(TARGET)
		PRINT 　射精
		SIF CFLAG:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", TARGET)%
	ENDIF

	SIF TALENT:母乳体質
		PRINTFORML 　母乳%BARBASE("母乳", TARGET)%

	;服装
	CALL PLAYER_CLOTHES, TARGET
;	PRINTFORM 　%CALLNAME:TARGET%の服装:
;	SIF TEQUIP:特殊
;		PRINTFORM [%CSTR:特殊%]
;	SIF TEQUIP:上半身上着
;		PRINTFORM [%CSTR:上半身上着%]
;	SIF TEQUIP:下半身上着
;		PRINTFORM [%CSTR:下半身上着%]
;	SIF TEQUIP:全身上着
;		PRINTFORM [%CSTR:全身上着%]
;	SIF TEQUIP:上半身下着
;		PRINTFORM [%CSTR:上半身下着%]
;	SIF TEQUIP:下半身下着
;		PRINTFORM [%CSTR:下半身下着%]
;	SIF TEQUIP:足元
;		PRINTFORM [%CSTR:足元%]
;	SIF !TEQUIP:特殊 && !TEQUIP:上半身上着 && !TEQUIP:下半身上着 && !TEQUIP:全身上着 && !TEQUIP:上半身下着 && !TEQUIP:下半身下着 && !TEQUIP:足元
;		PRINTFORM [全裸]
;	PRINTL 


	CALL SHOW_INFO_PALAM

	IF FLAG:ゲームモード == 7
		PRINTFORML □%CALLNAME:MASTER%のステータス
	ELSE
		PRINTL □主人と助手のステータス
	ENDIF
	SIF TALENT:MASTER:魔術師
		PRINTFORML 　魔力%BARBASE("魔力", MASTER)%
	SIF ASSI > 0 && TALENT:ASSI:魔術師
		PRINTFORML 　魔力%BARBASE("魔力", ASSI)%
	IF PENIS(MASTER)
		PRINT 　射精(主人)
		SIF CFLAG:MASTER:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", MASTER)%
	ENDIF

	IF ASSI >= 0 && PENIS(ASSI)
		PRINT 　射精(助手)
		SIF CFLAG:ASSI:残数 == 0
			PRINT ※打ち止め※
		PRINTFORML %BARBASE("射精", ASSI)%
	ENDIF
ENDIF

SIF TEQUIP:触手
	PRINTFORML 　射精(触手)%BARBASE("触手射精", MASTER)%

;服装の表示
CALL PLAYER_CLOTHES, MASTER
SIF ASSI >= 0
	CALL PLAYER_CLOTHES, ASSI

IF TALENT:MASTER:ドラゴン使い
	PRINTFORML □%CSTR:MASTER:ドラゴン%のステータス
	PRINTFORML 　射精　　　%BARBASE("ドラゴン射精", MASTER)%
ENDIF
IF ASSI > 0 && TALENT:ASSI:ドラゴン使い
	PRINTFORML □%CSTR:ASSI:ドラゴン%のステータス
	PRINTFORML 　射精　　　%BARBASE("ドラゴン射精", ASSI)%
ENDIF

DRAWLINE

PRINT 使用中のアイテム：
CALL SHOW_EQUIP

@PLAYER_CLOTHES, ARG
;服装の表示
PRINTFORM 　%CALLNAME:ARG%の服装:
SIF TEQUIP:ARG:特殊
	PRINTFORM [%CSTR:ARG:特殊%]
SIF TEQUIP:ARG:上半身上着
	PRINTFORM [%CSTR:ARG:上半身上着%]
SIF TEQUIP:ARG:下半身上着
	PRINTFORM [%CSTR:ARG:下半身上着%]
SIF TEQUIP:ARG:全身上着
	PRINTFORM [%CSTR:ARG:全身上着%]
SIF TEQUIP:ARG:上半身下着
	PRINTFORM [%CSTR:ARG:上半身下着%]
SIF TEQUIP:ARG:下半身下着
	PRINTFORM [%CSTR:ARG:下半身下着%]
SIF TEQUIP:ARG:足元
	PRINTFORM [%CSTR:ARG:足元%]
SIF !TEQUIP:ARG:特殊 && !TEQUIP:ARG:上半身上着 && !TEQUIP:ARG:下半身上着 && !TEQUIP:ARG:全身上着 && !TEQUIP:ARG:上半身下着 && !TEQUIP:ARG:下半身下着 && !TEQUIP:ARG:足元
	PRINTFORM [全裸]
PRINTL 


@STAIN_INFO
;汚れ表示
CALL SHOW_STAIN, MASTER
CALL SHOW_STAIN, TARGET
SIF ASSI > 0
	CALL SHOW_STAIN, ASSI
WAIT

@SHOW_STAIN, ARG
PRINTFORML 【%CALLNAME:ARG%の汚れ】
REPEAT 9
	SELECTCASE COUNT
		CASE 2
			SIF SEX(ARG) == 2 && !TALENT:ARG:ふたなり
				CONTINUE
		CASE 3, 6
			SIF SEX(ARG) == 1 && !TALENT:ARG:ふたなり
				CONTINUE
		CASE 7
			SIF CSTR:ARG:下半身下着 == ""
				CONTINUE
	ENDSELECT

	SELECTCASE COUNT
		CASE 0
			PRINT 　口唇：
		CASE 1
			PRINT 　手指：
		CASE 2
			PRINT 　陰茎：
		CASE 3
			PRINT 　膣口：
		CASE 4
			PRINT 　肛門：
		CASE 5
			PRINT 　乳房：
		CASE 6
			PRINT 　膣内：
		CASE 7
			PRINT 　下着：
	ENDSELECT
	SIF GETBIT(STAIN:ARG:COUNT, 0)
		PRINT <愛液>
	SIF GETBIT(STAIN:ARG:COUNT, 1)
		PRINT <先走り>
	SIF GETBIT(STAIN:ARG:COUNT, 2)
		PRINT <精液>
	SIF GETBIT(STAIN:ARG:COUNT, 3)
		PRINT <アナル>
	SIF GETBIT(STAIN:ARG:COUNT, 4)
		PRINT <母乳>
	SIF GETBIT(STAIN:ARG:COUNT, 5)
		PRINT <粘液>
	SIF GETBIT(STAIN:ARG:COUNT, 6)
		PRINT <破瓜の血>
	SIF GETBIT(STAIN:ARG:COUNT, 7)
		PRINT <尿>
	PRINTL 
REND

@SHOW_STATE
;状況の表示
;豹変は現在No.1004 紅刃とNo.1019 冷音のみ
PRINT 　
SIF TRANCE:200
	PRINTFORM [%CALLNAME:TARGET%豹変中]
SIF ASSI >= 0 && TRANCE:ASSI:200
	PRINTFORM [%CALLNAME:ASSI%豹変中]
SIF TFLAG:実行判定無視
	PRINTFORM [%CALLNAME:TARGET%の意識が朦朧]

SIF TEQUIP:ビデオカメラ
	PRINTFORM [撮影中(残{10-TFLAG:ビデオ撮影回数}回　撮影者:\@ TFLAG:ビデオ撮影者 == 2 ? %CALLNAME:ASSI% # %CALLNAME:MASTER% \@)]

SIF TEQUIP:触手
	PRINT [触手召喚中] 
SIF TEQUIP:野外プレイ
	PRINT [野外プレイ中]
SIF TEQUIP:羞恥プレイ
	PRINT [羞恥プレイ中]
SIF TEQUIP:お風呂場
	PRINT [お風呂場プレイ中]
SIF TEQUIP:エプロン
	PRINT [新妻プレイ中]
PRINTL 

@SHOW_EQUIP
;使用中の道具・魔法を表示する
;一度使用したら解除するまで止まらない道具や調教を
;追加した場合はここに追加をしてください

LOCAL = 0

IF TEQUIP:MASTER:コンドーム
	PRINT [主人避妊具]
	LOCAL++
ENDIF
IF ASSI >= 0 && TEQUIP:ASSI:コンドーム
	PRINT [助手避妊具]
	LOCAL++
ENDIF
IF CFLAG:酔
	IF TALENT:下戸 && CFLAG:酔 >= 20
		PRINTFORM [%CALLNAME:TARGET%泥酔中]
	ELSEIF !TALENT:酒豪 && CFLAG:酔 >= 50
		PRINTFORM [%CALLNAME:TARGET%泥酔中]
	ELSE
		PRINTFORM [%CALLNAME:TARGET%酒気帯び]
	ENDIF
	LOCAL++
ENDIF
FOR COUNT, 11, 40
	SIF TEQUIP:COUNT == 0
		CONTINUE
	SELECTCASE TEQUIPNAME:COUNT
		;触手最優先
		CASE "触手"
			PRINT [触手召喚中]
		;薬系は一番最初
		CASE "媚薬"
			PRINT [媚薬効果発揮中]
			LOCAL++
		CASE "利尿剤"
			PRINT [利尿剤効果発揮中]
			LOCAL++
		CASE "強精神薬"
			IF TEQUIP:強精神薬 == 1
				PRINT [強精神薬反動中]
			ELSE
				PRINT [絶頂抑制中]
			ENDIF
			LOCAL++
		CASE "クリキャップ"
			IF TEQUIP:触手
				PRINT [触手クリ責め]
			ELSE
				PRINT [クリキャップ]
			ENDIF
			LOCAL++
		CASE "オナホール"
			IF TEQUIP:触手
				PRINT [触手オナホ]
			ELSE
				PRINT [オナホール]
			ENDIF
			LOCAL++
		CASE "バイブ"
			IF TEQUIP:触手
				PRINT [触手挿入]
			ELSE
				PRINT [バイブ]
			ENDIF
			LOCAL++
		CASE "アナルバイブ"
			IF TEQUIP:触手
				PRINT [触手アナル挿入]
			ELSE
				PRINT [アナルバイブ]
			ENDIF
			LOCAL++
		CASE "ニプルキャップ"
			IF TEQUIP:触手
				PRINT [触手乳首責め]
			ELSE
				PRINT [ニプルキャップ]
			ENDIF
			LOCAL++
		CASE "搾乳器"
			IF TEQUIP:触手
				PRINT [触手搾乳]
			ELSE
				PRINT [搾乳器]
			ENDIF
			LOCAL++
		CASE "アナルビーズ", "アイマスク", "縄", "ボールギャグ", "浣腸器", "拡張バルーン", "アナル電極", "乳房電極", "シャワー", "触手口辱"
			PRINTFORM [%TEQUIPNAME:COUNT%]
			LOCAL++
		CASE "搾精コンドーム"
			IF TEQUIP:触手
				PRINTL [触手搾精中]
			ELSE
				PRINTL [搾精コンドーム]
			ENDIF
			LOCAL++
		CASE "Ｖ異物"
			PRINT [膣異物
			SELECTCASE TEQUIP:Ｖ異物
				CASE 1
					PRINT (缶)]
				CASE 2
					PRINT (ボトル)]
				CASE 3
					PRINT (瓶)]
				CASE 4
					PRINT (消火器)]
			ENDSELECT
			LOCAL++
		CASE "Ａ異物"
			PRINT [アナル異物
			SELECTCASE TEQUIP:Ａ異物
				CASE 1
					PRINT (缶)]
				CASE 2
					PRINT (ボトル)]
				CASE 3
					PRINT (瓶)]
				CASE 4
					PRINT (消火器)]
			ENDSELECT
			LOCAL++
	ENDSELECT
NEXT
IF TEQUIP:MASTER:ペニスバンド
	PRINTFORM [%CALLNAME:MASTER%ペニスバンド装着中]
	LOCAL++
ELSEIF TEQUIP:ペニスバンド
	PRINTFORM [%CALLNAME:TARGET%ペニスバンド装着中]
	LOCAL++
ELSEIF ASSI > 0 && TEQUIP:ASSI:ペニスバンド
	PRINTFORM [%CALLNAME:ASSI%ペニスバンド装着中]
	LOCAL++
ENDIF

FOR COUNT, 190, 200
	IF TFLAGNAME:COUNT == "ラブキャンセーブユー"
		IF TFLAG:COUNT == 2
			PRINTFORM [ラブキャンセーブユー]
		ELSEIF TFLAG:COUNT == 1
			PRINTFORM [ラブキャンセーブユー反動中]
		ENDIF
	ELSEIF TFLAG:COUNT
		PRINTFORM [%TFLAGNAME:COUNT%]
		LOCAL++
	ENDIF
NEXT

SIF LOCAL
	PRINTL 
SIF LOCAL == 0
	PRINTL [無し]

