;以下はeraBEMANIVer2.23時点における仕様

@DUMMYFUNC
;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;    基本的な変数
;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;  CSVフォルダ内のファイルを参照
;  キャラ変数系はキャラ番号を省略するとTARGETとして解釈される
;-------------------------------------------------
;  素質を参照する
;-------------------------------------------------
;「TALENT:(キャラ番号):(素質名)」で対象の素質を参照する
;★構文例は下記
	SIF TALENT:処女
		PRINTFORML 【TARGETが処女の場合】
	SIF !TALENT:童貞 && PENIS(TARGET)
		PRINTFORML 【TARGETが童貞ではない場合、ただしペニスを持たない対象も判定されうるのでペニスありの条件もつけておく】
	SIF TALENT:MASTER:バストサイズ >= 4
		PRINTFORML 【主人のおっぱいが平均(3)よりでかい場合、素質は通常1か0だがバストサイズや体格などは2以上の数値が入っている事もある】
	SIF ASSI > 0 && TALENT:TARGET:ふたなり && TALENT:MASTER:ふたなり && TALENT:ASSI:ふたなり
		PRINTFORML 【三人全員ふたなりの場合、ASSIがいるかどうかは先に判定しておかないと-1を参照してエラー吐く】

;-------------------------------------------------
;  経験を参照する
;-------------------------------------------------
;「EXP:(キャラ番号):(素質名)」で対象の経験を参照する
;★構文例は下記
	SIF EXP:異常経験
		PRINTFORML 【TARGETに異常経験がある場合】
	SIF EXP:性交経験 >= 50
		PRINTFORML 【TARGETの性交経験が50以上の場合】

	SELECTCASE EXP:Ｖ経験
		CASE 0
			PRINTFORML 【TARGETにＶ経験がない場合】
		CASE 1 TO 49
			PRINTFORML 【TARGETのＶ経験が1～49の場合】
		CASE 50 TO 99
			PRINTFORML 【TARGETのＶ経験が50～99の場合】
		CASE IS >= 100
			PRINTFORML 【TARGETのＶ経験が100以上の場合】
	ENDSELECT

;-------------------------------------------------
;  能力を参照する
;-------------------------------------------------
;「ABL:(キャラ番号):(素質名)」で対象の能力を参照する
;★構文例は下記
	SIF !ABL:従順
		PRINTFORML 【TARGETの従順が0の場合】
	SIF ABL:技巧 > ABL:MASTER:技巧
		PRINTFORML 【TARGETの技巧が主人より高い場合】

;-------------------------------------------------
;  刻印を参照する
;-------------------------------------------------
;「MARK:(キャラ番号):(刻印名)」で対象の刻印を参照する
;★構文例は下記
	SELECTCASE MARK:反発刻印
		CASE 0
			PRINTFORML 【反発刻印なし】
			SIF MARK:反発刻印履歴 == 3
				PRINTFORML 【今は反発刻印は無いが以前Lv3を取得した経験がある場合】
		CASE 1
			PRINTFORML 【反発刻印Lv1】
		CASE 2
			PRINTFORML 【反発刻印Lv2】
		CASE 3
			PRINTFORML 【反発刻印Lv3】
	ENDSELECT

;-------------------------------------------------
;  パラメータを参照する
;-------------------------------------------------
;「PALAM:(刻印名)」で現在のパラメータを参照する PALAMLVとの併用推奨
;★構文例は下記
	SIF PALAM:勃起 >= PALAMLV:3
		PRINTFORML 【充分に勃起している場合】

	SELECTCASE GETPALAMLV(PALAM:潤滑, 4)
		CASE 0
			PRINTFORML 【全く濡れていない場合】
		CASE 1
			PRINTFORML 【ほとんど濡れていない場合】
		CASE 2
			PRINTFORML 【あまり濡れていない場合】
		CASE 3
			PRINTFORML 【適度に濡れている場合】
		CASE 4
			PRINTFORML 【充分に濡れている場合】
	ENDSELECT

;-------------------------------------------------
;  装着中・使用中の道具などを参照する
;-------------------------------------------------
;「TEQUIP:(キャラ番号):(道具名など)」で装着中道具などを参照する
;★構文例は下記
	SIF !TEQUIP:下半身下着
		PRINTFORML 【ぱんつはいてない場合】
	SIF TEQUIP:バイブ && TEQUIP:アナルバイブ
		PRINTFORML 【両穴にバイブが入っている場合】
	SIF TEQUIP:媚薬
		PRINTFORML 【媚薬の効果が発揮されている場合】

;-------------------------------------------------
;  キャラ固有のフラグを参照する
;-------------------------------------------------
;「CFLAG:(キャラ番号):(フラグ名)」でキャラフラグを参照する
;★構文例は下記
	SIF 陥落(TARGET)
		PRINTFORML 【TARGETが陥落している場合】

	SELECTCASE CFLAG:依存度
		CASE 0 TO 499
			PRINTFORML 【依存度0～499】
		CASE 500 TO 999
			PRINTFORML 【依存度500～999】
		CASE IS >= 1000
			PRINTFORML 【依存度1000以上】
	ENDSELECT

;-------------------------------------------------
;  キャラ固有の文字列情報を参照する
;-------------------------------------------------
;「CSTR:(キャラ番号):(フラグ名)」でキャラの文字列情報を参照する
;★構文例は下記
	SIF CSTR:処女 == "あなた"
		PRINTFORML 【あなたに処女を捧げた】
	SIF CSTR:童貞 == "まどか"
		PRINTFORML 【まどかに童貞を捧げた】
	SIF CSTR:上半身上着 == "セーラー服" && TEQUIP:上半身上着
		PRINTFORML 【セーラー服を着ている（調教中の場合着ているかどうかはTEQUIPを併用する必要がある）】
	SIF CSTR:種族 == CSTR:MASTER:種族
		PRINTFORML 【TARGETの種族が主人の種族と同じ】
	SIF GROUPMATCH("pop'n music", CSTR:登場作品, CSTR:登場作品２, CSTR:登場作品３)
		PRINTFORML 【TARGETがpop'n musicに登場するキャラクター】

;-------------------------------------------------
;  セーブデータのフラグを参照する
;-------------------------------------------------
;「FLAG:(フラグ名)」でフラグを参照する FLAG:曜日は式中関数化されているため便利な式中関数の項目に移設
	SELECTCASE FLAG:天気
		CASE 1
			PRINTFORML 【晴れ】
		CASE 2
			PRINTFORML 【曇り】
		CASE 3
			PRINTFORML 【雨】
		CASE 4
			PRINTFORML 【大雨】
		CASE 5
			PRINTFORML 【雷雨】
		CASE 6
			PRINTFORML 【雪】
		CASE 7
			PRINTFORML 【大雪】
	ENDSELECT

;-------------------------------------------------
;  ERHで定義されたシステム変数を参照する
;-------------------------------------------------
;システム部分の様々な変数がERHで定義されている
;★変数例は下記（一部）
	SIF YEAR == 1998 && MONTH == 9 && DAYS == 28
		PRINTFORML 【ゲーム中日付が1998年9月28日】
	SIF 購入人数 == 1
		PRINTFORML 【この周回中に購入した奴隷が1人だけ】
	SIF ハニーバニー
		PRINTFORML 【TARGETにハニーバニーが発動中】
	SIF GETCHARANAME("識") > 0 && ハニーバニー:GETCHARANAME("識"):0
		PRINTFORML 【所持奴隷の中に識がいて、識にハニーバニーが発動中】
	SIF パートナー == "レイシス"
		PRINTFORML 【経営モードのパートナーがレイシスの場合（パートナーは文字列で記録される）】
	SIF パートナー == CHARANAMEF(TARGET)
		PRINTFORML 【TAEGETがパートナーの場合】

	SELECTCASE SELECTCOMS
		CASE "愛撫"
			PRINTFORML 【現在選択中のコマンドが愛撫の場合】
		CASE "臍愛撫"
		CASE "臍挿入"
		CASE "臍針挿入"
	ENDSELECT

	SELECTCASE PREVCOMS
		CASE "フェラチオ", "イラマチオ"
			PRINTFORML 【前回選択したコマンドがフェラチオかイラマチオの場合】
		CASE "パイズリ", "パイズリフェラ", "縦パイズリ"
	ENDSELECT

	SELECTCASE ターン
		CASE "朝"
			PRINTFORML 【学園モードのみ】
		CASE "昼"
			PRINTFORML 【通常学園共通】
		CASE "夕"
			PRINTFORML 【学園モードのみ】
		CASE "夜"
			PRINTFORML 【通常学園共通】
	ENDSELECT

;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;    eraBEMANIの便利な式中関数
;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	SIF PENIS(TARGET)
		PRINTFORML 【TARGETにＰがある】
	SIF VAGINA(TARGET)
		PRINTFORML 【TARGETにＶがある】
	SIF SEX(TARGET) == 1
		PRINTFORML 【TARGETが男 女の場合は2】
	SIF Ｖ使用中(TARGET)
		PRINTFORML 【TARGETのＶに道具が入っている Ａ、Ｂ、Ｐ、Ｕの使用中もある】
	SIF ゲームモード() == "新婚モード"
		PRINTFORML 【このセーブデータのゲームモードは新婚モード】

	SELECTCASE FALLTYPE(TARGET)
		CASE 1
			PRINTFORML 【TARGETが恋慕系陥落】
		CASE 2
			PRINTFORML 【TARGETが淫乱系陥落】
		CASE 3
			PRINTFORML 【TARGETが服従系陥落】
		CASEELSE
			PRINTFORML 【TARGETが未陥落】
	ENDSELECT

	SIF RANDPER(50)
		PRINTFORML 【50％の確率で出る】
	SIF RANDPER(1)
		PRINTFORML 【1％の確率で出る】

	PRINTFORML 今日の天気は%天気()%です。
	PRINTFORMW あなたの名前は\@ 名前あり() ? %CALLNAME:PLAYER%さんですね # なんですか \@？
	CALL PRINTCOLOR, "この文字列を赤文字でPRINTする", "red"
	CALL PRINTCOLORL, "上記のは改行しないがこれは改行する", "red"

	PRINTFORML %CALLNAME:TARGET%は%体格(TARGET)%体型で、胸は%バストサイズ(TARGET)%だ。
	PRINTFORML と書くと%CALLNAME:TARGET%が小人体型だった場合に普通体型と表示されてしまうし、
	PRINTFORML バストサイズのない男だった場合にエラーになる可能性もあるので、
	PRINTFORMW %CALLNAME:TARGET%は\@ TALENT:小人体型 ? 小人 # %体格(TARGET)% \@体型で、胸は\@ !TALENT:バストサイズ ? 男なので無い # %バストサイズ(TARGET)%だ \@。と書くといいかもしれない……

	PRINTFORMW %CALLNAME:TARGET%から%CALLNAME:MASTER%への相性を20増やすよ
	CALL 相性加算, TARGET, MASTER, 20
	PRINTFORMW 減らす事もできる
	CALL 相性加算, TARGET, MASTER, -10
	PRINTFORMW こうして%CALLNAME:TARGET%から%CALLNAME:MASTER%への相性は{相性値(TARGET, MASTER)}になった

	PRINTFORML 今日は%DOW()%曜日！
	SELECTCASE DOW()
		CASE "土", "日"
			PRINTFORML 今日は休日だよ
		CASE "月", "水", "金"
			PRINTFORML 燃えるごみは月・水・金
	ENDSELECT

;-------------------------------------------------
;  条件式の短縮等
;-------------------------------------------------
;CHARAWORKING("（キャラ名）", "（施設）") 指定したキャラが指定した施設に配属されているか TARGET以外のキャラを参照する場合などに
;キャラがいなくてもエラーにならず0を返す 「存在するがどこにも配属されていない」の判定は「CHARAWORKING("（キャラ名）", "")」で行える
;「存在して別の施設に配属されている」はNOTCHARAWORK("（キャラ名）", "（施設）") で判定できる !だと配属の不一致とキャラの不在を区別不可能
;★構文例は下記
IF CSTR:配属 == "ゲームセンター"
	;【TARGETがゲームセンターに配属されている】
	IF CHARAWORKING("識", "ゲームセンター")
		;【さらに識も配属されていた場合】
	ENDIF
	IF NOTCHARAWORK("ジルチ", "ゲームセンター")
		;【ジルチが所持奴隷に存在するがゲーセンにはいない場合】
	ENDIF
ELSEIF CSTR:配属 != ""
	;【TARGETがゲームセンター以外のどこかに配属されている】
	IF CHARAWORKING("山形 まり花", CSTR:配属)
		;【さらにまり花もTARGETと同じ施設に配属されていた場合】
	ENDIF
ENDIF

;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;    学園モードでの構文など
;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;-------------------------------------------------
;  所属学校を参照する
;-------------------------------------------------
;「CSTR:(キャラ番号):在校」および「CSTR:(キャラ番号):在任」に学校名の文字列が入っている
;★構文例は下記
	IF CSTR:PLAYER:在校 == CSTR:在校
		PRINTFORML 【対象が同じ学校の生徒】
	ELSEIF CSTR:PLAYER:在校 == CSTR:在任
		PRINTFORML 【対象があなたの学校の教師】
	ELSEIF CSTR:在校 != "" || CSTR:在任 != ""
		PRINTFORML 【対象が違う学校の生徒か教師】
	ELSE
		PRINTFORML 【対象が無所属】
	ENDIF

;★生徒か教師かを区別しないなら以下のように記述可能
	IF GROUPMATCH(CSTR:PLAYER:在校, CSTR:在校, CSTR:在任)
		PRINTFORML 【対象の所属学校があなたと同じ】
	ELSEIF GROUPMATCH("", CSTR:在校, CSTR:在任) == 1
		PRINTFORML 【対象が違う学校の生徒か教師】
	ELSE
		PRINTFORML 【対象が無所属】
	ENDIF

;またGETCHARANAMEを併用する事で第三者の所属学校も参照可能
;★構文例は下記
	IF GROUPMATCH(CSTR:PLAYER:在校, CSTR:在校, CSTR:在任)
		IF CSTR:GETCHARANAME("ギジリ"):在任 == CSTR:PLAYER:在校
			PRINTFORML 【あなたと対象の学校の校長がギジリの時】
		ELSEIF CSTR:GETCHARANAME("梅桐 天土"):在任 == CSTR:PLAYER:在校
			PRINTFORML 【あなたと対象の学校の校長があーちゃんの時】
		ENDIF
	ELSE
		IF CSTR:GETCHARANAME("ギジリ"):在任 == CSTR:在校
			PRINTFORML 【あなたと対象が別の学校に所属していて、対象の学校の校長がギジリの時】
		ENDIF
	ENDIF

	SELECTCASE GROUPMATCH(CSTR:GETCHARANAME("ラーズ"):在校, CSTR:GETCHARANAME("ユーリ"):在任, CSTR:GETCHARANAME("アッシュ"):在任, CSTR:GETCHARANAME("スマイル"):在任)
		CASE 3
			PRINTFORML 【ラーズの所属する学校にdeuilの三人が揃っている場合】
		CASE 2
			PRINTFORML 【ラーズの所属する学校にdeuilのうち二人がいる場合】
		CASE 1
			PRINTFORML 【ラーズの所属する学校にdeuilの誰かがいる場合】
		CASE 0
			PRINTFORML 【ラーズの所属する学校にdeuilがいない場合】
	ENDSELECT

;-------------------------------------------------
;  現在地を参照する
;-------------------------------------------------
;あなたの現在地は「CSTR:PLAYER:現在地」に入っているためこれを参照する事でどこで会話が行われているか分岐可能
;★構文例は下記
	SELECTCASE CSTR:PLAYER:現在地
	    CASE CSTR:PLAYER:在校
			PRINTFORML 【現状学校には所属学校が同じキャラしか出現しないため対象の所属学校を判定する必要はない】
	    CASE "ゆうマート"
			PRINTFORML 【ゆうマートで遭遇した時（ロケーション名に/がある場合/の右側のみを書く）】
	    CASE "自宅"
			PRINTFORML 【家に呼んだ時】
	    CASEELSE
			PRINTFORML 【それ以外の場所で】
	ENDSELECT

;また学校に行っているキャラクターも「CSTR:(キャラ番号):現在地」に現在地が入っている
;★構文例は下記
	IF CSTR:PLAYER:現在地 != CSTR:PLAYER:在校
		;この分岐に入った場合あなたは今学校にいない
		IF CSTR:PLAYER:在校 == CSTR:GETCHARANAME("モード"):在校 && CSTR:GETCHARANAME("モード"):現在地 == CSTR:PLAYER:在校
			PRINTFORML 【あなたとモードは同じ学校に所属しており、あなたは学校をサボっているがモードは今学校にいる】
		ENDIF
	ELSE
		;この分岐に入った場合あなたは今学校にいる
		IF CSTR:PLAYER:在校 == CSTR:GETCHARANAME("リリーゼ"):在校 && CSTR:GETCHARANAME("リリーゼ"):現在地 != CSTR:PLAYER:在校
			PRINTFORML 【あなたとリリーゼは同じ学校に所属しており、あなたは学校にいるがリリーゼは学校をサボっている】
		ENDIF
	ENDIF

;-------------------------------------------------
;  好感度の上昇
;-------------------------------------------------
;★以下の記述で対象の好感度を増減させる事ができる 例は友情を5上げる場合
	CALL 好感度上昇, TARGET, "友情", 5

;""の中には友情、ときめき、尊敬、舎弟、セフレを指定する
;「母性」は対象が女性もしくはハニーの場合に表示だけ変更されている状態であり内部的には舎弟
;数値は5の倍数を推奨 マイナスも指定可能

;★またGETCHARANAMEを併用する事で第三者の好感度も増減可能 会話に対象以外の第三者を登場させた場合などに
	CALL 好感度上昇, GETCHARANAME("芽兎 めう"), "友情", 5
